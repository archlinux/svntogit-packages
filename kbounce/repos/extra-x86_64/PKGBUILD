# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kbounce
pkgver=15.04.2
pkgrel=1
pkgdesc="A single player arcade game with the elements of puzzle"
url="http://kde.org/applications/games/kbounce/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegames')
depends=('libkdegames' 'knotifyconfig')
makedepends=('extra-cmake-modules' 'python' 'kdoctools')
install=${pkgname}.install
conflicts=('kdegames-kbounce')
replaces=('kdegames-kbounce')
source=("http://download.kde.org/stable/applications/${pkgver}/src/kbounce-${pkgver}.tar.xz")
sha1sums=('4920e731ba1000689cd915c1dc8bc156f25bf9d4')

build() {
  mkdir build
  cd build
  cmake ../kbounce-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
