# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=netcdf-fortran
pkgver=4.4.3
pkgrel=1
pkgdesc="NetCDF fortran bindings"
arch=("i686" "x86_64")
url="http://www.unidata.ucar.edu/downloads/netcdf/netcdf-fortran/index.jsp"
depends=('gcc-libs' 'netcdf' 'curl' 'gcc-fortran')
makedepends=('doxygen')
options=('!makeflags' 'staticlibs')
license=('custom')
source=(http://www.unidata.ucar.edu/downloads/netcdf/ftp/netcdf-fortran-${pkgver}.tar.gz)
sha1sums=('90c4e2fcb6697d6e9014f4988ad2cf8ffdddb35f')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr --enable-shared
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}

  install -m755 -d ${pkgdir}/usr/lib
  install -m755 -d ${pkgdir}/usr/share/man
  make DESTDIR=${pkgdir} install

  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/COPYRIGHT \
        ${pkgdir}/usr/share/licenses/${pkgname}/COPYRIGHT

}
