# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux,org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=krfb
pkgver=17.08.0
pkgrel=1
pkgdesc='Desktop Sharing'
url='https://www.kde.org/applications/system/krfb'
arch=(i686 x86_64)
license=(LGPL)
depends=(kdnssd kxmlgui kwallet libvncserver libxdamage)
makedepends=(extra-cmake-modules kdoctools python)
conflicts=(kdenetwork-krfb)
replaces=(kdenetwork-krfb)
groups=(kde-applications kdenetwork)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig} krfb-gcc6.patch)
sha256sums=('7213ae652b1430e2e020150ab2b23d6c3d60dd60f5336a8b5496f49c3fa1de4f'
            'SKIP'
            '09543509640c404aaadb8881732f698aa39d66f039b4ea9216d392eb67b16656')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

# Fix build with GCC6 (Fedora)
  cd $pkgname-$pkgver
  patch -p1 -i ../krfb-gcc6.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
