# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux,org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=krfb
pkgver=15.11.80
pkgrel=1
pkgdesc='Desktop Sharing'
url='http://www.kde.org/applications/system/krfb'
arch=(i686 x86_64)
license=(LGPL)
depends=(kdnssd kxmlgui kwallet libvncserver libxdamage)
makedepends=(extra-cmake-modules kdoctools python)
conflicts=(kdenetwork-krfb)
replaces=(kdenetwork-krfb)
groups=(kde-applications kdenetwork)
source=("http://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
md5sums=('83237d2e7e2aa1fcb27e7f021cba4a13')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
