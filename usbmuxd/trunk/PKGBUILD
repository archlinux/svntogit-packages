# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Gabriel Martinez < reitaka at gmail dot com >

pkgname=usbmuxd
pkgver=1.1.0+48+g1cc8b34
pkgrel=2
pkgdesc="USB Multiplex Daemon"
url="http://marcansoft.com/blog/iphonelinux/usbmuxd/"
arch=('x86_64')
license=('GPL2' 'GPL3')
depends=('libimobiledevice')
makedepends=('git')
_commit=1cc8b343a4bc9320758c78e187b13bb3c9029f12  # master
source=("git+https://github.com/libimobiledevice/usbmuxd#commit=$_commit" usbmuxd.sysusers)
sha256sums=('SKIP'
            '044bfb2810b27f7d6c066f142210775176b7484e76a78ea91794f40fdcda6fd9')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

check() {
  cd $pkgname
  make check
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sbindir=/usr/bin
  make
}

package() {
  cd $pkgname
  make DESTDIR="${pkgdir}" install

  install -Dm644 "$srcdir"/usbmuxd.sysusers "$pkgdir"/usr/lib/sysusers.d/usbmuxd.conf
}
