# $Id: PKGBUILD 79583 2010-05-04 15:21:15Z ibiru $
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>

pkgbase=atkmm
pkgname=(atkmm atkmm-docs)
pkgver=2.22.2
pkgrel=1
arch=('i686' 'x86_64')
makedepends=('atk>=1.32.0' 'glibmm>=2.24.2' 'glibmm-docs>=2.24.2' 'libsigc++')
license=('LGPL')
options=('!libtool' '!emptydirs')
url="http://gtkmm.sourceforge.net/"
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgbase}/2.22/${pkgbase}-${pkgver}.tar.bz2)
sha256sums=('b94666ae0acebea53241fa0b2444468ff66e8a7790dfc7491489a9ad2eeefa34')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package_atkmm() {
  pkgdesc="C++ bindings for atk"
  depends=('atk>=1.32.0' 'glibmm>=2.24.2' 'libsigc++')

  cd "${srcdir}/${pkgbase}-${pkgver}"
  sed -i -e 's/^doc_subdirs/#doc_subdirs/' Makefile
  make DESTDIR="${pkgdir}" install
}

package_atkmm-docs() {
  pkgdesc="Developer documentation for atkmm"
  cd "${srcdir}/${pkgbase}-${pkgver}"
  make -C doc DESTDIR="${pkgdir}" install
}
