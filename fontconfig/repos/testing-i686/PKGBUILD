# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=fontconfig
pkgver=2.11.1
pkgrel=2
pkgdesc="A library for configuring and customizing font access"
arch=(i686 x86_64)
url="http://www.fontconfig.org/release/"
license=('custom')
depends=('expat' 'freetype2')
install=fontconfig.install
source=(http://www.fontconfig.org/release/$pkgname-$pkgver.tar.bz2
        0001-Revert-Bug-73291-poppler-does-not-show-fl-ligature.patch
        0002-Update-aliases-for-new-URW-fonts.patch)
sha256sums=('dc62447533bca844463a3c3fd4083b57c90f18a70506e7a9f4936b5a1e516a99'
            '657d180d9d78555df4f1b81871ba9c1c7c6056c83455514950d064fdfae4646b'
            '1194d1b92d228d8ed705f5502fdf4fcfad5a794f388bcb4928074df4c34a1bfb')

  # a nice page to test font matching:
  # http://zipcon.net/~swhite/docs/computers/browsers/fonttest.html

prepare() {
  cd $pkgname-$pkgver

  # Upstream patches implementing FS#45928
  patch -p1 -i ../0001-Revert-Bug-73291-poppler-does-not-show-fl-ligature.patch
  patch -p1 -i ../0002-Update-aliases-for-new-URW-fonts.patch
}

build() {
  cd $pkgname-$pkgver

  # make sure there's no rpath trouble and sane .so versioning - FC and Gentoo do this as well
  libtoolize -f
  autoreconf -fi

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --with-templatedir=/etc/fonts/conf.avail \
    --with-xmldir=/etc/fonts \
    --localstatedir=/var \
    --disable-static \
    --with-default-fonts=/usr/share/fonts \
    --with-add-fonts=/usr/share/fonts
  make
}

check() {
  cd $pkgname-$pkgver
  make -k check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
