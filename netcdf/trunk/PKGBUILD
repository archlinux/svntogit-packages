# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=netcdf
pkgver=4.3.3.1
pkgrel=2
pkgdesc="network Common Data Form interface for array-oriented data access and corresponding library"
arch=("i686" "x86_64")
url="http://www.unidata.ucar.edu/downloads/netcdf/index.jsp"
depends=('gcc-libs' 'hdf5' 'curl')
optdepends=('netcdf-fortran: fortran bindings' 'netcdf-cxx: c++ bindings')
options=('!makeflags')
license=('custom')
source=(http://www.unidata.ucar.edu/downloads/netcdf/ftp/netcdf-${pkgver}.tar.gz)
sha1sums=('074dd8ef6e8dde3f0c26c7bec0b5d3be6a38bbee')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr --enable-shared --enable-netcdf-4 --enable-dap-netcdf
  make
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}

  install -m755 -d ${pkgdir}/usr/lib
  install -m755 -d ${pkgdir}/usr/share/man
  make DESTDIR=${pkgdir} install

  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/COPYRIGHT \
    ${pkgdir}/usr/share/licenses/${pkgname}/COPYRIGHT
}
