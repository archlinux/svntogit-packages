# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=netcdf
pkgver=4.1.3
pkgrel=2
pkgdesc="network Common Data Form interface for array-oriented data access and corresponding library"
arch=("i686" "x86_64")
url="http://www.unidata.ucar.edu/downloads/netcdf/index.jsp"
depends=('gcc-libs' 'hdf5' 'gcc-fortran' 'curl')
options=('!libtool' '!makeflags')
license=('custom')
source=(http://www.unidata.ucar.edu/downloads/netcdf/ftp/netcdf-${pkgver}.tar.gz)
sha1sums=('e600322478276f51612e17cca3cda38489003f81')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr --enable-shared --enable-netcdf-4 --enable-dap-netcdf
  make
#  make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}

  install -m755 -d ${pkgdir}/usr/lib
  install -m755 -d ${pkgdir}/usr/share/man
  make DESTDIR=${pkgdir} install

  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/COPYRIGHT \
	${pkgdir}/usr/share/licenses/${pkgname}/COPYRIGHT
}
