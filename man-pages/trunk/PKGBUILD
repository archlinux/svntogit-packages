# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=man-pages
pkgver=6.04
_posixver=2017-a
pkgrel=1
pkgdesc="Linux man pages"
arch=('any')
license=('GPL' 'custom')
url="https://www.kernel.org/doc/man-pages/"
makedepends=('man2html' 'git')
# https://git.kernel.org/pub/scm/docs/man-pages/man-pages.git/
source=(https://www.kernel.org/pub/linux/docs/man-pages/$pkgname-$pkgver.tar.{xz,sign}
        https://www.kernel.org/pub/linux/docs/man-pages/man-pages-posix/$pkgname-posix-${_posixver}.tar.{xz,sign})
# https://www.kernel.org/pub/linux/docs/man-pages/sha256sums.asc
sha256sums=('c2c0b9329955df81af45ee80ebc84c47291f95df5157db1fab988199f9371af1'
            'SKIP'
            'ce67bb25b5048b20dad772e405a83f4bc70faf051afa289361c81f9660318bc3'
            'SKIP')
validpgpkeys=('E522595B52EDA4E6BFCCCB5E856199113A35CE5E') # Michael Kerrisk (Linux man-pages maintainer) <mtk.manpages@gmail.com>
# + for posix tarball
validpgpkeys+=('A9348594CE31283A826FBDD8D57633D441E25BB5') # Alejandro Colomar Andres <alx.manpages@gmail.com>

prepare() {
  cd "${srcdir}"/$pkgname-$pkgver

  # included in shadow
  rm man5/passwd.5
  rm man3/getspnam.3
  # included in tzdata
  rm man5/tzfile.5 man8/{tzselect,zdump,zic}.8
  # included in libxcrypt
  rm man3/crypt*.3
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  # install man-pages
  make DESTDIR="${pkgdir}" prefix=/usr install 

  # install posix pages
  pushd "${srcdir}"/$pkgname-posix-${_posixver%-*}
  make DESTDIR="${pkgdir}" install 
  popd
  
  # posix pages have a custom license
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 "${srcdir}"/$pkgname-posix-${_posixver%-*}/POSIX-COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgname}/POSIX-COPYRIGHT"
}
