# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>
pkgname=xulrunner
pkgver=1.9.2.12
_ffoxver=3.6.12
pkgrel=1
pkgdesc="Mozilla Runtime Environment"
arch=('i686' 'x86_64')
license=('MPL' 'GPL' 'LGPL')
depends=('gtk2>=2.22.0' 'gcc-libs' 'libidl2>=0.8.13' 'mozilla-common' 'nss>=3.12.8' 'libxt' 'hunspell>=1.2.8' 'startup-notification>=0.10' 'mime-types' 'dbus-glib>=0.86' 'alsa-lib>=1.0.23' 'libevent')
makedepends=('zip' 'pkg-config' 'diffutils' 'libgnomeui' 'python2' 'wireless_tools' 'autoconf2.13')
optdepends=('libgnomeui: GNOME integration and MIME handling'
            'wireless_tools: Location aware browsing')
url="http://wiki.mozilla.org/XUL:Xul_Runner"
source=(http://releases.mozilla.org/pub/mozilla.org/firefox/releases/${_ffoxver}/source/firefox-${_ffoxver}.source.tar.bz2
        mozconfig
        mozilla-pkgconfig.patch
        fix-mozilla-launcher.patch
        xulrunner-version.patch
        xulrunner-png14.patch
        enable-x86_64-tracemonkey.patch
        python2.7.patch)
md5sums=('80f4d83f23a7c45fc3f1904964ea2a5a'
         'e195cdcd51a8fb6fc77f0f653ff1e582'
         '2d2d7143a4e0bfe71149a51f8dbbab2f'
         '63eee2d1da3b43c9d604f2253f242f40'
         '371303c5bdc4fa0d955d14521b93b69d'
         '3bd0566180ad2daa32743b3ce58b2095'
         'cbd938cd1fb8210cd8a2c41833489af9'
         'ab3dc9aecae7f08b9492fb3c00a5fd28')

build() {
  cd "${srcdir}/mozilla-1.9.2"
  cp "${srcdir}/mozconfig" .mozconfig

  #fix libdir/sdkdir - fedora
  patch -Np1 -i "${srcdir}/mozilla-pkgconfig.patch"

  #Fix stub launcher - archlinux
  patch -Np0 -i "${srcdir}/fix-mozilla-launcher.patch"

  #Force installation to the same path for every version
  patch -Np1 -i "${srcdir}/xulrunner-version.patch"

  #Fix compile with libpng 1.4
  patch -Np0 -i "${srcdir}/xulrunner-png14.patch"

  #Tracemonkey for x86_64
  patch -Np0 -i "${srcdir}/enable-x86_64-tracemonkey.patch"

  #python2.7
  patch -Np0 -i "${srcdir}/python2.7.patch"

  unset CFLAGS
  unset CXXFLAGS

  make -j1 -f client.mk build MOZ_MAKE_FLAGS="$MAKEFLAGS"
  make -j1 DESTDIR="${pkgdir}" install

  #Remove included dictionaries, add symlink to system myspell path.
  #Note: this will cause file conflicts when users have installed dictionaries in the old location
  rm -rf "${pkgdir}/usr/lib/xulrunner-1.9.2/dictionaries"
  ln -sf /usr/share/myspell "${pkgdir}/usr/lib/xulrunner-1.9.2/dictionaries"

  # add xulrunner library path to ld.so.conf
  install -d ${pkgdir}/etc/ld.so.conf.d
  echo "/usr/lib/xulrunner-1.9.2" > ${pkgdir}/etc/ld.so.conf.d/xulrunner.conf
}
