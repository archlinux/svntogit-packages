# $Id$
# Maintainer: Alexander Baldeck <alexander@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
pkgname=xulrunner
pkgver=1.9.2
_ffoxver=3.6
pkgrel=2
pkgdesc="Mozilla Runtime Environment"
arch=(i686 x86_64)
license=('MPL' 'GPL' 'LGPL')
depends=('gtk2>=2.18.6' 'gcc-libs>=4.4.2' 'libidl2>=0.8.13' 'mozilla-common' 'nss>=3.12.4' 'libxt' 'hunspell>=1.2.8' 'startup-notification>=0.10' 'mime-types' 'dbus-glib>=0.82' 'alsa-lib>=1.0.21.a')
makedepends=('zip' 'pkgconfig' 'diffutils' 'libgnomeui' 'python' 'wireless_tools')
optdepends=('libgnomeui' 'python' 'wireless_tools')
provides=(gecko-sdk)
replaces=(gecko-sdk)
url="http://wiki.mozilla.org/XUL:Xul_Runner"
source=(http://releases.mozilla.org/pub/mozilla.org/firefox/releases/${_ffoxver}/source/firefox-${_ffoxver}.source.tar.bz2
        mozconfig
        mozilla-pkgconfig.patch
        fix-mozilla-launcher.patch
        mozilla-ps-pdf-simplify-operators.patch
        xulrunner-version.patch
        xulrunner-png14.patch)
md5sums=('458051557ff49e6a352c1d56eee5782a'
         'c99bced771a39690bd71e5e1c1770e73'
         'd92adb0f23e724c1667baecb4cdaea80'
         '63eee2d1da3b43c9d604f2253f242f40'
         '13dca58c04e62a8916691c63c5c492a0'
         '371303c5bdc4fa0d955d14521b93b69d'
         '3bd0566180ad2daa32743b3ce58b2095')

build() {
  cd "${srcdir}/mozilla-1.9.2"
  cp "${srcdir}/mozconfig" .mozconfig

  #Upstream patch. Still not applied to 1.9.2
  patch -Np1 -i "${srcdir}/mozilla-ps-pdf-simplify-operators.patch" || return 1

  #fix libdir/sdkdir - fedora
  patch -Np1 -i "${srcdir}/mozilla-pkgconfig.patch" || return 1

  #Fix stub launcher - archlinux
  patch -Np0 -i "${srcdir}/fix-mozilla-launcher.patch" || return 1

  #Force installation to the same path for every version
  patch -Np1 -i "${srcdir}/xulrunner-version.patch" || return 1

  patch -Np0 -i "${srcdir}/xulrunner-png14.patch" || return 1

  unset CFLAGS
  unset CXXFLAGS
  export LDFLAGS="${LDFLAGS} -Wl,-rpath,/usr/lib/xulrunner-1.9.2"

  make -j1 -f client.mk build MOZ_MAKE_FLAGS="$MAKEFLAGS" || return 1
  make -j1 DESTDIR="${pkgdir}" install || return 1
}
