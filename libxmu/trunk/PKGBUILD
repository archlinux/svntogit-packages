# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libxmu
pkgver=1.1.2
pkgrel=2
pkgdesc="X11 miscellaneous micro-utility library"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
depends=('libxext' 'libxt')
makedepends=('xorg-util-macros')
license=('custom')
source=("${url}/releases/individual/lib/libXmu-${pkgver}.tar.bz2")
sha512sums=('eba4e3d10f7d75ba8464881fb69f295a89774a4b37793197d75f3312e3a342b2df8b7e13e3f5c887962704329b5347ff2f3395e229af9dadf46a93b1e8613cfc')

build() {
  cd "${srcdir}/libXmu-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static
  make
}

package() {
  cd "${srcdir}/libXmu-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
