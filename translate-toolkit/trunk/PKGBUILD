# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Hugo Doria <hugodoria at gmail.com>

pkgname=translate-toolkit
pkgver=1.11.0
pkgrel=2
pkgdesc="A toolkit to convert between various different translation formats, help process and validate localisations"
arch=('any')
url="http://translate.sourceforge.net/wiki/toolkit/index"
license=('GPL')
depends=('bash' 'python2-lxml')
optdepends=('python2-iniparse: to use ini2po script')
source=("http://downloads.sourceforge.net/translate/${pkgname}-${pkgver}.tar.bz2")
md5sums=('69c5df09b83fdbf448c3446b6cf22098')

package() {
  cd ${pkgname}-${pkgver}
  sed -i -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
    -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
    -e "s|/usr/bin/python2.4|/usr/bin/python2|" \
    $(find . -name '*.py')

  python2 -s setup.py install --root="${pkgdir}" --optimize=1
  mv "${pkgdir}"/usr/lib/python2.7/site-packages/{docs,share} "${pkgdir}"/usr/lib/python2.7/site-packages/translate
  python2 -m compileall "${pkgdir}"/usr/lib/python2.7/site-packages/translate
}
