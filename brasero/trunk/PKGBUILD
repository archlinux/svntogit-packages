# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=brasero
pkgver=3.11.3
pkgrel=1
pkgdesc="A disc burning application for Gnome"
arch=('i686' 'x86_64')
url="http://www.gnome.org/projects/brasero"
license=('GPL')
options=('!emptydirs')
depends=('gtk3' 'gst-plugins-good' 'totem-plparser' 'cdrkit' 'cdrdao' 'desktop-file-utils' 'hicolor-icon-theme' 'shared-mime-info'  'libcanberra' 'dvd+rw-tools' 'dconf' 'libsm' 'libtracker-sparql' 'libnotify' 'gvfs')
makedepends=('gtk-doc' 'yelp-tools' 'gnome-common' 'intltool' 'libburn' 'libisofs' 'libnautilus-extension' 'gobject-introspection' 'gconf' 'itstool' 'docbook-xsl')
optdepends=('libburn: alternative back-end'
            'libisofs: libburn back-end'
            'dvdauthor: video project'
            'vcdimager: video project')
replaces=('nautilus-cd-burner')
install=brasero.install
groups=('gnome-extra')
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz
        tracker1.patch)
sha256sums=('0b6fa557d67845b7a4f9978b565a6769e1a023c397a73f2891a8f1c7c55e2d4c'
            '3af6715e9d913dcf9e0650e7af0af0fc2cb68dbf548168d95ba91971efd2b5d7')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../tracker1.patch
}
build() {
  cd "$pkgname-$pkgver"
  autoreconf -fi
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-caches \
      --disable-schemas-compile
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR=$pkgdir install
}

