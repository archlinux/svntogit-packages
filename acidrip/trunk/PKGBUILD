# $Id$
# Maintainer: Aaron Griffin <aaron@archlinux.org>

pkgname=acidrip
pkgver=0.14
pkgrel=7
pkgdesc="A Gtk2::Perl application for ripping and encoding DVD's"
arch=('any')
url="http://untrepid.com/acidrip/"
license=('GPL')
depends=('mplayer' 'lsdvd' 'lame' 'libdvdcss' 'gtk2-perl' 'perl>=5.10.0')
source=(ftp://ftp.archlinux.org/other/acidrip/$pkgname-$pkgver.tar.gz
        http://untrepid.com/acidrip/acidrip-icon4bit.png
        acidrip.desktop crop-fixes.patch)
md5sums=('c1c27bbf658e5c30f43e067a8ae283de' '7498602e2a3838cf35b94af85d9954d3'\
         'f6896612df224a325d66c5f8fa8553e7' '1a8470702e2e0eda8ecbc473141aa08f')
options=(!emptydirs)

build()
{
  cd $srcdir/$pkgname-$pkgver
  patch -up1 < $srcdir/crop-fixes.patch || return 1
  # install module in vendor directories.
  perl Makefile.PL INSTALLDIRS=vendor INSTALLVENDORSCRIPT=/usr/bin || return 1
  make  || return 1
  make install DESTDIR=${pkgdir} || return 1

  install -Dm644 ${srcdir}/acidrip-icon4bit.png \
      ${pkgdir}/usr/share/pixmaps/acidrip.png
  install -D -m644 ${srcdir}/acidrip.desktop \
      ${pkgdir}/usr/share/applications/acidrip.desktop
}
