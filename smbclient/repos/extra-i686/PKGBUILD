# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
pkgname=smbclient
pkgver=3.0.30
# We use the 'A' to fake out pacman's version comparators.  Samba chooses
# to append 'a','b',etc to their subsequent releases, which pamcan
# misconstrues as alpha, beta, etc.  Bad samba!
_realver=3.0.30
pkgrel=1
pkgdesc="Tools to access a server's filespace and printers via SMB"
arch=(i686 x86_64)
url="http://www.samba.org"
license=('GPL')
options=(!makeflags)
depends=('popt' 'libldap' 'libcap' 'heimdal>=1.0.1' 'db>=4.6')
source=(http://us1.samba.org/samba/ftp/samba-${_realver}.tar.gz)
md5sums=('d647ec1f34414fa8691f74536dcccfb5')

build() {
  cd ${startdir}/src/samba-${_realver}/source

  ./configure --prefix=/usr --with-smbmount --with-fhs --with-pam \
              --with-datadir=/usr/share --with-configdir=/etc/samba \
              --with-lockdir=/var/run/samba --with-ads --with-acl-support \
	      --localstatedir=/var
  make proto bin/smbclient libsmbclient \
       bin/rpcclient bin/smbspool bin/smbtree \
       bin/smbcacls bin/smbcquotas bin/smbmount bin/smbmnt bin/smbget \
       bin/smbumount bin/net bin/nmblookup || return 1
  mkdir -p ${startdir}/pkg/usr/bin ${startdir}/pkg/sbin
  install -m755 bin/* ${startdir}/pkg/usr/bin/
  install -m755 script/smbtar ${startdir}/pkg/usr/bin/
  ln -sf /usr/bin/smbmount ${startdir}/pkg/sbin/mount.smbfs
  ln -sf /usr/bin/smbumount ${startdir}/pkg/sbin/umount.smbfs
  mkdir -p ${startdir}/pkg/usr/lib/cups/backend
  ln -sf /usr/bin/smbspool ${startdir}/pkg/usr/lib/cups/backend/smb

  mkdir -p ${startdir}/pkg/usr/lib/samba
  mv ${startdir}/pkg/usr/bin/libsmbclient.* ${startdir}/pkg/usr/lib/samba/
  ln -sf samba/libsmbclient.so ${startdir}/pkg/usr/lib/libsmbclient.so
  ln -sf samba/libsmbclient.so ${startdir}/pkg/usr/lib/libsmbclient.so.0
  mkdir -p ${startdir}/pkg/usr/include
  install -m644 include/libsmbclient.h ${startdir}/pkg/usr/include/

  gcc -Wall -D_GNU_SOURCE -D_LARGEFILE64_SOURCE -o ${startdir}/pkg/sbin/mount.cifs client/mount.cifs.c
  gcc -Wall -D_GNU_SOURCE -D_LARGEFILE64_SOURCE -o ${startdir}/pkg/sbin/umount.cifs client/umount.cifs.c

  mkdir -p ${startdir}/pkg/usr/share/man/man{1,7,8}
  for man in smbmnt smbmount smbspool \
      smbumount umount.cifs mount.cifs net; do
    install -m644 ../docs/manpages/${man}.8 ${startdir}/pkg/usr/share/man/man8/
  done
  for man in rpcclient smbcacls smbclient smbcquotas smbget \
      smbtree smbtar nmblookup; do
    install -m644 ../docs/manpages/${man}.1 ${startdir}/pkg/usr/share/man/man1/
  done
  install -m644 ../docs/manpages/libsmbclient.7 ${startdir}/pkg/usr/share/man/man7/
}
