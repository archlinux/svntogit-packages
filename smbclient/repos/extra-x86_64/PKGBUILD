# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
pkgname=smbclient
pkgver=3.3.6
# We use the 'A' to fake out pacman's version comparators.  Samba chooses
# to append 'a','b',etc to their subsequent releases, which pacman
# misconstrues as alpha, beta, etc.  Bad samba!
_realver=3.3.6
pkgrel=2
pkgdesc="Tools to access a server's filespace and printers via SMB"
arch=(i686 x86_64)
url="http://www.samba.org"
license=('GPL3')
depends=('readline>=6.0.00' 'popt' 'libldap' 'libcap>=2.16' 'heimdal>=1.2-1' 'db>=4.7' 'tdb=3.3.6')
source=(http://us1.samba.org/samba/ftp/samba-${_realver}.tar.gz)
options=(!makeflags)

build() {
  cd ${srcdir}/samba-${_realver}/source

  ./configure --prefix=/usr --with-cifsmount --with-fhs --with-pam \
              --with-configdir=/etc/samba \
              --with-lockdir=/var/run/samba --with-ads --with-acl-support \
              --localstatedir=/var --with-syslog --with-pam_smbpass --disable-dnssd \
              --libdir=/usr/lib/samba

  make proto bin/smbclient libsmbclient \
       bin/rpcclient bin/smbspool bin/smbtree \
       bin/smbcacls bin/smbcquotas bin/smbget \
       bin/net bin/nmblookup bin/mount.cifs bin/umount.cifs || return 1
  mkdir -p ${pkgdir}/usr/bin ${pkgdir}/sbin ${pkgdir}/usr/lib/samba
  install -m755 bin/{smbclient,rpcclient,smbspool,smbtree,smbcacls,smbcquotas,smbget,net,nmblookup} ${pkgdir}/usr/bin/
  install -m755 bin/{mount.cifs,umount.cifs} ${pkgdir}/sbin/
  mv  bin/*.so* ${pkgdir}/usr/lib/samba
  install -m755 script/smbtar ${pkgdir}/usr/bin/
  mkdir -p ${pkgdir}/usr/lib/cups/backend
  ln -sf /usr/bin/smbspool ${pkgdir}/usr/lib/cups/backend/smb
  for i in ${pkgdir}/usr/lib/samba/*; do
   	ln -sf samba/$(basename $i) ${pkgdir}/usr/lib/$(basename $i)
  done
  rm -rf ${pkgdir}/usr/lib/libtdb*
  mkdir -p ${pkgdir}/usr/include
  install -m644 include/libsmbclient.h ${pkgdir}/usr/include/
  install -m644 lib/talloc/talloc.h ${pkgdir}/usr/include/
  install -m644 lib/netapi/netapi.h ${pkgdir}/usr/include/
  mkdir -p ${pkgdir}/usr/share/man/man{1,7,8}
  for man in smbspool \
      umount.cifs mount.cifs net; do
    install -m644 ../docs/manpages/${man}.8 ${pkgdir}/usr/share/man/man8/
  done
  for man in rpcclient smbcacls smbclient smbcquotas smbget \
      smbtree smbtar nmblookup; do
    install -m644 ../docs/manpages/${man}.1 ${pkgdir}/usr/share/man/man1/
  done
  install -m644 ../docs/manpages/libsmbclient.7 ${pkgdir}/usr/share/man/man7/
}

md5sums=('858cb6c640358be0e81297c5de615a3c')
