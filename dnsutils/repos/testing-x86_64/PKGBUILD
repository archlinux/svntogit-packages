# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: kevin <kevin@archlinux.org>
# Contributor: mario <mario_vazq@hotmail.com>

pkgname=dnsutils

# Use a period and not a hyphen before the patch level for proper versioning.
pkgver=9.8.0.P1
_pkgver=9.8.0-P1
pkgrel=1

pkgdesc='Various DNS utilities - dig host nslookup nsupdate'
arch=('i686' 'x86_64')
url='https://www.isc.org/software/bind'
license=('custom:ISC')
depends=('openssl' 'idnkit')
replaces=('bind-tools' 'host')
options=('!makeflags')
source=("http://ftp.isc.org/isc/bind9/${_pkgver}/bind-${_pkgver}.tar.gz"
        'tools-only.patch')
sha1sums=('aa8f308f218e437ac4bad616e0ae83a9b9c40c29'
          'ac88c89e25c26d73095a3dd85e1ca1070d184ded')

build() {
	cd "${srcdir}/bind-${_pkgver}"
	patch -p1 < ../tools-only.patch
	export STD_CDEFINES='-DDIG_SIGCHASE'
	./configure \
		--prefix=/usr --sysconfdir=/etc --localstatedir=/var \
		--with-openssl=yes --with-idn=yes --disable-linux-caps
	make
}

package() {
	cd "${srcdir}/bind-${_pkgver}/bin"
	make DESTDIR="${pkgdir}" install
	install -Dm644 ../COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
