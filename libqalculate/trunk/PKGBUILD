# Maintainer: Eric BÃ©langer <eric@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=libqalculate
_pkgver=3.9.0a
pkgver=3.9.0.a
pkgrel=1
pkgdesc="Multi-purpose desktop calculator"
arch=(x86_64)
url="https://qalculate.github.io/"
license=(GPL)
depends=(libxml2 curl mpfr)
makedepends=(intltool doxygen)
optdepends=('gnuplot: for plotting support')
source=(https://github.com/Qalculate/libqalculate/releases/download/v${_pkgver/a/}/$pkgname-$_pkgver.tar.gz)
sha256sums=('f46a5f99500337be96ee1cac4310a522ed37a76161a828d343f0229441e2cbf8')

build() {
  cd $pkgname-${_pkgver/a/}
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-${_pkgver/a/}
  make DESTDIR="$pkgdir" install
}
