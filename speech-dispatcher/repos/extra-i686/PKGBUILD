# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Steve Holmes <steve.holmes88@gmail.com>
# Contributor: Alexander Jenisch <nt@divzero.at>
# Contributor: Chris Brannon <cmbrannon@cox.net>
# Contributor: Andreas Messer <andi@bupfen.de>

pkgname=speech-dispatcher
pkgver=0.7.1
pkgrel=4
arch=('i686' 'x86_64')
pkgdesc="High-level device independent layer for speech synthesis interface"
url="http://www.freebsoft.org/speechd"
license=('GPL2' 'FDL')
depends=('glib2' 'python2' 'dotconf' 'texinfo' 'libsndfile')
makedepends=('pkgconfig' 'espeak')
optdepends=('festival: Speech output using Festival'
        'espeak: Speech output using ESpeak')
provides=("speechd=${pkgver}")
options=('!libtool') 
backup=(etc/speech-dispatcher/clients/gnome-speech.conf
	etc/speech-dispatcher/clients/emacs.conf
	etc/speech-dispatcher/modules/ibmtts.conf
	etc/speech-dispatcher/modules/espeak-generic.conf
	etc/speech-dispatcher/modules/espeak.conf
	etc/speech-dispatcher/modules/swift-generic.conf
	etc/speech-dispatcher/modules/festival.conf
	etc/speech-dispatcher/modules/cicero.conf
	etc/speech-dispatcher/modules/espeak-mbrola-generic.conf
	etc/speech-dispatcher/modules/dtk-generic.conf
	etc/speech-dispatcher/modules/llia_phon-generic.conf
	etc/speech-dispatcher/modules/ivona.conf
	etc/speech-dispatcher/modules/epos-generic.conf
	etc/speech-dispatcher/modules/flite.conf
	etc/speech-dispatcher/speechd.conf)
install="${pkgname}.install"
source=("http://www.freebsoft.org/pub/projects/speechd/$pkgname-$pkgver.tar.gz"
        'speechd.sh')
md5sums=('ccfc30ac006673d36b4223eb760ed696'
        '858293f464b83f03f3b6bb796291c71d')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --without-flite
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR=${pkgdir} install

  install -Dm755 ${srcdir}/speechd.sh ${pkgdir}/etc/rc.d/speechd
  install -d ${pkgdir}/var/log/speech-dispatcher

  # Fix spd-conf to use python 2.7
  sed -i -e 's|#!/usr/bin/python|#!/usr/bin/python2|' ${pkgdir}/usr/bin/spd-conf
  sed -i -e 's|#!/usr/bin/env python|#!/usr/bin/env python2|' ${pkgdir}/usr/lib/python2.7/site-packages/speechd/_test.py
  
  rm -f ${pkgdir}/usr/share/info/dir
  gzip -9nf ${pkgdir}/usr/share/info/*

  # Remove conflicts with erlang (FS#21674)
  rm -r ${pkgdir}/usr/bin/run_test
}
