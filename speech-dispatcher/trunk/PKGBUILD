# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
pkgbase=speech-dispatcher
pkgname=(speech-dispatcher libspeechd)
pkgver=0.10.1
pkgrel=1
arch=('x86_64')
pkgdesc="High-level device independent layer for speech synthesis interface"
url="http://www.freebsoft.org/speechd"
license=('GPL2' 'FDL')
makedepends=('intltool' 'espeak-ng' 'libltdl' 'python-xdg' 'dotconf' 'libpulse' 'libao')
source=("https://github.com/brailcom/speechd/releases/download/${pkgver}/speech-dispatcher-${pkgver}.tar.gz"
        'speech-dispatcherd.service')
sha512sums=('b70b88de08d90a024b147eb206f4a4dddc4c78a8afc4470f71cd47883b9845e787e8e7f8a70406937b9116c4980629dbeb007ab3810e464995a81eba3dae675e'
            '73eb563003b03f4836b17af66335664050d1dc3bd8434be368cd09ff8933aa2adc4bd17c4ce5c1bbbd406a0b0880191426a679fa05eac0a30f4005eaeeb47340')

prepare() {
  cd ${pkgname}-${pkgver}

  # Disabled due to bug: https://bugs.archlinux.org/task/35700
  sed -i "s/cicero //g" configure.ac
  sed -i "s/sd_cicero//g" src/modules/Makefile.am
}

build() {
  cd ${pkgname}-${pkgver}

  CFLAGS+=' -fcommon' # https://wiki.gentoo.org/wiki/Gcc_10_porting_notes/fno_common

  ./configure --prefix=/usr \
    --enable-shared \
    --sysconfdir=/etc \
    --without-flite
  make
}

package_speech-dispatcher() {
  depends=("libspeechd=$pkgver-$pkgrel" 'libltdl' 'python-xdg' 'dotconf' 'libpulse' 'libao')
  optdepends=('festival: Speech output using Festival'
              'espeak-ng: Speech output using ESpeak-ng'
              'pulseaudio: PulseAudio support')
  backup=(etc/speech-dispatcher/clients/emacs.conf
  	      etc/speech-dispatcher/modules/ibmtts.conf
  	      etc/speech-dispatcher/modules/espeak-generic.conf
  	      etc/speech-dispatcher/modules/espeak.conf
  	      etc/speech-dispatcher/modules/espeak-ng.conf
  	      etc/speech-dispatcher/modules/swift-generic.conf
  	      etc/speech-dispatcher/modules/festival.conf
  	      etc/speech-dispatcher/modules/cicero.conf
  	      etc/speech-dispatcher/modules/espeak-mbrola-generic.conf
  	      etc/speech-dispatcher/modules/espeak-ng-mbrola-generic.conf
  	      etc/speech-dispatcher/modules/dtk-generic.conf
  	      etc/speech-dispatcher/modules/llia_phon-generic.conf
  	      etc/speech-dispatcher/modules/ivona.conf
  	      etc/speech-dispatcher/modules/epos-generic.conf
          etc/speech-dispatcher/modules/flite.conf
          etc/speech-dispatcher/modules/pico-generic.conf
          etc/speech-dispatcher/speechd.conf)

  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -Dm644 "${srcdir}"/speech-dispatcherd.service "${pkgdir}/usr/lib/systemd/system/speech-dispatcherd.service"
  install -d "${pkgdir}/var/log/speech-dispatcher"

  sed -i 's|includedir=.*|includedir=${prefix}/include/speech-dispatcher|g' "${pkgdir}/usr/lib/pkgconfig/speech-dispatcher.pc"

  mkdir -p "${srcdir}"/libspeechd/usr/lib
  mv "${pkgdir}"/usr/include "${srcdir}"/libspeechd/usr
  mv "${pkgdir}"/usr/lib/libspeechd*so* "${srcdir}"/libspeechd/usr/lib
}

package_libspeechd() {
  mkdir -p "${pkgdir}"/usr/lib

  mv "${srcdir}"/libspeechd/usr/include "${pkgdir}"/usr
  mv "${srcdir}"/libspeechd/usr/lib/* "${pkgdir}"/usr/lib
}
