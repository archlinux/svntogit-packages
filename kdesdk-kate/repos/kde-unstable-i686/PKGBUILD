# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgbase=kdesdk-kate
pkgname=('kdebase-katepart'
         'kdebase-kwrite'
         'kdesdk-kate')
pkgver=4.9.80
pkgrel=1
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
makedepends=('kdelibs ''cmake' 'automoc4' 'kdebindings-python2')
source=("http://download.kde.org/unstable/${pkgver}/src/kate-${pkgver}.tar.xz"
        'pkgbuild-syntax-highlight.patch')
sha1sums=('8d4091c03312a75052b8a81a3c3676acef109a47'
          '5b45d0df8f340105633eba9ef7c58086e6c2e750')

build() {
  cd "${srcdir}"/kate-${pkgver}
  patch -p1 -i "${srcdir}"/pkgbuild-syntax-highlight.patch

  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../kate-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DPYTHON_EXECUTABLE=/usr/bin/python2
  make
}

package_kdebase-katepart() {
  pkgdesc="A fast and feature-rich text editor component"
  depends=('kdelibs')
  url="http://kate-editor.org/about-katepart/"
  install='kdebase-katepart.install'

  cd "${srcdir}"/build/part
  make DESTDIR="${pkgdir}" install
}

package_kdebase-kwrite() {
  pkgdesc="Text Editor"
  depends=('kdebase-runtime' 'kdebase-katepart')
  groups=('kde' 'kdebase')
  url="http://www.kde.org/applications/utilities/kwrite/"
  install='kdebase-kwrite.install'

  cd "${srcdir}"/build/kwrite
  make DESTDIR="${pkgdir}" install

  cd "${srcdir}"/build/doc/kwrite
  make DESTDIR="${pkgdir}" install
}

package_kdesdk-kate() {
  pkgdesc="Advanced Text Editor"
  depends=('kdebase-runtime' 'kdebase-katepart' 'qjson')
  groups=('kde' 'kdesdk')
  url="http://www.kde.org/applications/utilities/kate/"
  install='kdesdk-kate.install'
  optdepends=('kdebase-konsole: open a terminal in Kate'
              'kdebindings-python2: python bindings')

  cd "${srcdir}"/build/kate
  make DESTDIR="${pkgdir}" install

  cd "${srcdir}"/build/doc/kate
  make DESTDIR="${pkgdir}" install
}
