# $Id$
# Maintainer:
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jeff Mickey <jeff@archlinux.org>
# Contributor: Michel Brabants <michel.brabants@euphonynet.be>

pkgname=libzrtpcpp
pkgver=4.2.4
pkgrel=1
pkgdesc="A C++ implementation of Phil Zimmermann's ZRTP specification"
arch=('i686' 'x86_64')
url='http://www.gnutelephony.org/index.php/GNU_ZRTP'
license=('GPL3')
depends=('ccrtp')
makedepends=('cmake')
source=("$pkgname-$pkgver.tar.gz::https://github.com/wernerd/ZRTPCPP/archive/V$pkgver.tar.gz")
md5sums=('ef26bafd5eacd8c3623e89d6e17ddfe1')

prepare() {
  sed 's/lib64/lib/g' -i ZRTPCPP-${pkgver}/CMakeLists.txt
}

build() {
  mkdir -p build
  cd build
  cmake ../ZRTPCPP-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
