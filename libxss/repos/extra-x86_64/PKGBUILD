# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

pkgname=libxss
pkgver=1.2.2
pkgrel=3
pkgdesc="X11 Screen Saver extension library"
arch=('x86_64')
license=('custom')
url="https://xorg.freedesktop.org/"
depends=('libxext' 'scrnsaverproto')
makedepends=('xorg-util-macros')
source=("${url}/releases/individual/lib/libXScrnSaver-${pkgver}.tar.bz2")
sha512sums=('973aed3ad296c7160b66fccd4fff7375afc4332239acbe0ae1644fed9c453de69040f8174882ba264c731448edba9b499d398d013d556aea72434339399ed29f')

build() {
  cd libXScrnSaver-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd libXScrnSaver-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
