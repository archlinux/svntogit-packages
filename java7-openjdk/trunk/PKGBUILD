# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Maintainer: Guillaume ALAUX <guillaume@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=('jre7-openjdk-headless' 'jre7-openjdk' 'jdk7-openjdk' 'openjdk7-src' 'openjdk7-doc')
pkgbase=java7-openjdk
_java_ver=7
_icedtea_ver=2.4.7

# check "${srcdir}/icedtea-${_icedtea_ver}"/Makefile.am
_JDK_UPDATE_VERSION=55

_CORBA_CHANGESET=e6ad5b912691
_JAXP_CHANGESET=94b7e8e0d96f
_JAXWS_CHANGESET=bd9a50a78d04
_JDK_CHANGESET=9448fff93286
_LANGTOOLS_CHANGESET=8c26a3c39128
_OPENJDK_CHANGESET=13970e76b784

_HOTSPOT_CHANGESET=69b542696e5b  # see "${srcdir}/icedtea-${_icedtea_ver}"/hotspot.map

_bootstrap=0 # 0/1 for quick build or full bootstrap

pkgver=${_java_ver}.u${_JDK_UPDATE_VERSION}_${_icedtea_ver}
pkgrel=1
arch=('i686' 'x86_64')
url="http://icedtea.classpath.org"
license=('custom')
options=('!emptydirs')
makedepends=('jdk7-openjdk' 'libxp' 'libxslt'
             'alsa-lib' 'apache-ant>=1.8.1' 'giflib' 'libpng>=1.5.7' 'gtk2'
             'java-rhino' 'libpulse>=0.9.11' 'zip' 'unzip' 'cpio' 'fastjar' 'wget'
             'eclipse-ecj')

_url=http://icedtea.classpath.org/hg/release/icedtea7-forest-${_icedtea_ver:0:3}
source=(http://icedtea.classpath.org/download/source/icedtea-${_icedtea_ver}.tar.gz
        openjdk-${_OPENJDK_CHANGESET}.tar.gz::${_url}/archive/${_OPENJDK_CHANGESET}.tar.gz
        corba-${_CORBA_CHANGESET}.tar.gz::${_url}/corba/archive/${_CORBA_CHANGESET}.tar.gz
        jaxp-${_JAXP_CHANGESET}.tar.gz::${_url}/jaxp/archive/${_JAXP_CHANGESET}.tar.gz
        jaxws-${_JAXWS_CHANGESET}.tar.gz::${_url}/jaxws/archive/${_JAXWS_CHANGESET}.tar.gz
        jdk-${_JDK_CHANGESET}.tar.gz::${_url}/jdk/archive/${_JDK_CHANGESET}.tar.gz
        langtools-${_LANGTOOLS_CHANGESET}.tar.gz::${_url}/langtools/archive/${_LANGTOOLS_CHANGESET}.tar.gz
        hotspot-${_HOTSPOT_CHANGESET}.tar.gz::${_url}/hotspot/archive/${_HOTSPOT_CHANGESET}.tar.gz
        fontconfig-paths.diff
        openjdk7_nonreparenting-wm.diff
        jdk7-openjdk.profile
        jdk7-openjdk.profile.csh
        jre7-openjdk.profile
        jre7-openjdk.profile.csh)

sha256sums=('754350cbd704b22b7ba3d14c8283eb2d896d137824f95a9e6a2b34678658ade1'
            'bcd45546509defc439f42f230c0ca64e8aa6ec00542c3634aab3a4c10be3fe6b'
            'cc37272df260d08207c84763d4c39d7807728ba2d5908276b9bc63e925e70674'
            '3515cd105c29563bf78432576e658005386f45d7c3b2b7eac7af86cf196aaaea'
            '3e107628080d84a80a78ef0ef9dc3664989291dd17c8bacf031d59fba7bd7f4d'
            '9222e5317264f20d4a0b8170b4c4d02459cda98333c18e3a75064e7856ff58be'
            '5af29e32344e2f2fc0beb31f91b8312f2a0d6d02c53b4cb700ee2e27bcf1043b'
            'e3bbed298ed7c77169fdfddc47cdb85c62ef2e5e7ea04ca28aa8779861efca65'
            '9ad943ceb3dbcdf45d72974fc3667886a7ed65c69ab9abc17be5412827551a7f'
            '56b919ababb13bd6afdcdaceb112b529b6e82539255f2dae9a7e5eb91645164b'
            'bd55299e08e6f8e61ba5baad0990170891801a29a1e8137e502b5e8a10fc37ea'
            '3f28f8bfc6dd105a07f747d7135c77a77de433e2b8647dd7520a900135203fbd'
            'b7c045b08ad55a9f79390c104fa846d0e7dbb49fccffb2fab2a3824b6b19c9c8'
            '89d99d8ac269ca66e2e279aff652d5aac938a35faec93cd8cff8f048052bd3ce')

noextract=("openjdk-${_OPENJDK_CHANGESET}.tar.gz"
           "corba-${_CORBA_CHANGESET}.tar.gz"
           "jaxp-${_JAXP_CHANGESET}.tar.gz"
           "jaxws-${_JAXWS_CHANGESET}.tar.gz"
           "jdk-${_JDK_CHANGESET}.tar.gz"
           "langtools-${_LANGTOOLS_CHANGESET}.tar.gz"
           "hotspot-${_HOTSPOT_CHANGESET}.tar.gz")

[ "$CARCH" = "x86_64" ] && _JARCH=amd64
[ "$CARCH" = "i686" ]   && _JARCH=i386

_jvmdir=/usr/lib/jvm/java-7-openjdk
_imgdir="icedtea-${_icedtea_ver}/openjdk.build/j2sdk-image"
_nonheadless=(bin/policytool
              lib/${_JARCH}/libjsoundalsa.so
              lib/${_JARCH}/libpulse-java.so
              lib/${_JARCH}/libsplashscreen.so
              lib/${_JARCH}/xawt/libmawt.so)

build() {
  cd "${srcdir}/icedtea-${_icedtea_ver}"
  
  export ALT_PARALLEL_COMPILE_JOBS="${MAKEFLAGS/-j}"
  export HOTSPOT_BUILD_JOBS="${ALT_PARALLEL_COMPILE_JOBS}"

  . /etc/profile.d/apache-ant.sh

  cp "${srcdir}"/*.diff "${srcdir}"/icedtea-${_icedtea_ver}/patches
  export DISTRIBUTION_PATCHES="patches/fontconfig-paths.diff \
                               patches/openjdk7_nonreparenting-wm.diff"

  if [ "$_bootstrap" = "1" ]; then
     BOOTSTRAPOPT="--enable-bootstrap --with-ecj-jar=/usr/share/java/ecj.jar"
   else 
     BOOTSTRAPOPT="--disable-bootstrap"
  fi

  ./configure \
	$BOOTSTRAPOPT \
        --with-parallel-jobs="${MAKEFLAGS/-j}" \
        --disable-tests \
        --disable-downloading --disable-Werror \
        --with-pkgversion="ArchLinux build ${pkgver}-${pkgrel}-${CARCH}" \
        --with-jdk-home=${JAVA_HOME} \
        --with-openjdk-src-zip="${srcdir}/openjdk-${_OPENJDK_CHANGESET}.tar.gz" \
        --with-hotspot-src-zip="${srcdir}/hotspot-${_HOTSPOT_CHANGESET}.tar.gz" \
        --with-corba-src-zip="${srcdir}/corba-${_CORBA_CHANGESET}.tar.gz" \
        --with-jaxp-src-zip="${srcdir}/jaxp-${_JAXP_CHANGESET}.tar.gz" \
        --with-jaxws-src-zip="${srcdir}/jaxws-${_JAXWS_CHANGESET}.tar.gz" \
        --with-jdk-src-zip="${srcdir}/jdk-${_JDK_CHANGESET}.tar.gz" \
        --with-langtools-src-zip="${srcdir}/langtools-${_LANGTOOLS_CHANGESET}.tar.gz" \
        --enable-pulse-java \
        --enable-nss \
        --with-rhino \
        --with-abs-install-dir=${_jvmdir}
    make
}

check() {
  cd "${srcdir}/icedtea-${_icedtea_ver}"
  make -k check
}

package_jre7-openjdk-headless() {
  pkgdesc='OpenJDK Java 7 headless runtime environment'
  depends=('libjpeg-turbo' 'lcms2' 'nss' 'ca-certificates-java')
  optdepends=('libcups: needed for Java Mauve support - libmawt.so'
              'fontconfig: needed for Java Mauve support - libmawt.so'
              'java-rhino: for some JavaScript support')
  provides=('java-runtime-headless=7')
  conflicts=('openjdk6')
  # Upstream config files that should go to etc and get backup
  _backup_etc=(etc/java-7-openjdk/${_JARCH}/jvm.cfg
               etc/java-7-openjdk/calendars.properties
               etc/java-7-openjdk/content-types.properties
               etc/java-7-openjdk/flavormap.properties
               etc/java-7-openjdk/fontconfig.bfc
               etc/java-7-openjdk/fontconfig.properties
               etc/java-7-openjdk/images/cursors/cursors.properties
               etc/java-7-openjdk/logging.properties
               etc/java-7-openjdk/management/jmxremote.access
               etc/java-7-openjdk/management/jmxremote.password
               etc/java-7-openjdk/management/management.properties
               etc/java-7-openjdk/management/snmp.acl
               etc/java-7-openjdk/net.properties
               etc/java-7-openjdk/psfont.properties.ja
               etc/java-7-openjdk/psfontj2d.properties
               etc/java-7-openjdk/security/java.policy
               etc/java-7-openjdk/security/java.security
               etc/java-7-openjdk/security/nss.cfg
               etc/java-7-openjdk/sound.properties
               etc/java-7-openjdk/tz.properties)
  backup=(${_backup_etc[@]}
          etc/profile.d/jre.sh
          etc/profile.d/jre.csh)
  install=jre7-openjdk-headless.install

  cd "${srcdir}/${_imgdir}/jre"

  install -d -m755 "${pkgdir}${_jvmdir}/jre"
  cp -a bin lib "${pkgdir}${_jvmdir}/jre"

  # Set config files
  mv "${pkgdir}${_jvmdir}"/jre/lib/fontconfig.{Ubuntu.properties.src,properties}
  mv "${pkgdir}${_jvmdir}"/jre/lib/fontconfig.{Ubuntu.bfc,bfc}
  mv "${pkgdir}${_jvmdir}"/jre/lib/management/jmxremote.password{.template,}
  mv "${pkgdir}${_jvmdir}"/jre/lib/management/snmp.acl{.template,}
  rm -f "${pkgdir}${_jvmdir}"/jre/lib/fontconfig.*.bfc
  rm -f "${pkgdir}${_jvmdir}"/jre/lib/fontconfig.*.properties.src
  rm -f "${pkgdir}${_jvmdir}"/jre/lib/fontconfig.properties.src

  # Remove 'non-headless' files
  for f in ${_nonheadless[@]}; do
    rm "${pkgdir}${_jvmdir}/jre/${f}"
  done

  # Man pages
  pushd "${pkgdir}${_jvmdir}/jre/bin"
  install -d -m 755 "${pkgdir}"/usr/share/man/{,ja/}man1/
  for file in *; do
    install -m 644 "${srcdir}/${_imgdir}/man/man1/${file}.1" \
      "${pkgdir}/usr/share/man/man1/${file}.1"
    install -m 644 "${srcdir}/${_imgdir}/man/ja/man1/${file}.1" \
      "${pkgdir}/usr/share/man/ja/man1/${file}.1"
  done
  popd

  # Link binaries into /usr/bin
  pushd "${pkgdir}${_jvmdir}/jre/bin"
  install -d -m755 "${pkgdir}/usr/bin/"
  for file in *; do
    ln -sf ${_jvmdir}/jre/bin/${file} "${pkgdir}/usr/bin"
  done
  popd

  # Link JKS keystore from ca-certificates-java
  rm -f "${pkgdir}${_jvmdir}/jre/lib/security/cacerts"
  ln -sf /etc/ssl/certs/java/cacerts "${pkgdir}${_jvmdir}/jre/lib/security/cacerts"

  # Set some variables
  install -d -m755 "${pkgdir}/etc/profile.d/"
  install -m755 "${srcdir}/jre7-openjdk.profile" "${pkgdir}/etc/profile.d/jre.sh"
  install -m755 "${srcdir}/jre7-openjdk.profile.csh" "${pkgdir}/etc/profile.d/jre.csh"

  # Install license
  install -d -m755 "${pkgdir}/usr/share/licenses/${pkgbase}/"
  install -m644 ASSEMBLY_EXCEPTION LICENSE THIRD_PARTY_README \
                 "${pkgdir}/usr/share/licenses/${pkgbase}"
  ln -sf /usr/share/licenses/${pkgbase} "${pkgdir}/usr/share/licenses/${pkgname}"

  # Move config files that were set in _backup_etc from ./lib to /etc
  for file in ${_backup_etc[@]}; do
    _filepkgpath=${_jvmdir}/jre/lib/${file#etc/java-7-openjdk/}
    install -D -m 644 "${pkgdir}${_filepkgpath}" "${pkgdir}/${file}"
    ln -sf /${file} "${pkgdir}${_filepkgpath}"
  done
}

package_jre7-openjdk() {
  pkgdesc='OpenJDK Java 7 full runtime environment'
  depends=('jre7-openjdk-headless' 'xdg-utils' 'hicolor-icon-theme')
  optdepends=('icedtea-web-java7: web browser plugin + Java Web Start'
             'alsa-lib: for basic sound support'
             'giflib: for gif format support'
             'libpulse: for advanced sound support'
             'gtk2: for the Gtk+ look and feel - desktop usage'
             'libxtst: linked in xawt/libmawt.so - desktop usage')
  provides=('java-runtime=7')
  conflicts=('openjdk6')
  install=jre7-openjdk.install

  cd "${srcdir}/${_imgdir}/jre"

  for f in ${_nonheadless[@]}; do
    install -D ${f} "${pkgdir}${_jvmdir}/jre/${f}"
  done

  # Link binaries into /usr/bin
  pushd "${pkgdir}${_jvmdir}/jre/bin"
  install -d -m755 "${pkgdir}/usr/bin/"
  for file in *; do
    ln -sf ${_jvmdir}/jre/bin/${file} "${pkgdir}/usr/bin"
  done
  popd

  # Man pages
  pushd "${pkgdir}${_jvmdir}/jre/bin"
  install -d -m 755 "${pkgdir}"/usr/share/man/{,ja/}man1/
  for file in *; do
    install -m 644 "${srcdir}/${_imgdir}/man/man1/${file}.1" \
      "${pkgdir}/usr/share/man/man1/${file}.1"
    install -m 644 "${srcdir}/${_imgdir}/man/ja/man1/${file}.1" \
      "${pkgdir}/usr/share/man/ja/man1/${file}.1"
  done
  popd

  # Install icons and menu entries
  for s in 16 24 32 48 ; do
    install -d -m755 "${pkgdir}/usr/share/icons/hicolor/${s}x${s}/apps/"
    install -m644 ../../../openjdk/jdk/src/solaris/classes/sun/awt/X11/java-icon${s}.png \
                  "${pkgdir}/usr/share/icons/hicolor/${s}x${s}/apps/java.png"
  done
  
  # Install desktop files.
  install -d -m755 "${pkgdir}/usr/share/applications"
  install -m644 "${srcdir}/icedtea-${_icedtea_ver}/policytool.desktop" "${pkgdir}/usr/share/applications"

  # link license
  install -d -m755 "${pkgdir}/usr/share/licenses"
  ln -sf /usr/share/licenses/${pkgbase} "${pkgdir}/usr/share/licenses/${pkgname}"
}

package_jdk7-openjdk() {
  pkgdesc='OpenJDK Java 7 development kit'
  depends=('jre7-openjdk')
  provides=('java-environment=7')
  conflicts=('java-environment')
  replaces=('openjdk6')
  backup=(etc/profile.d/jdk.sh etc/profile.d/jdk.csh)

  cd "${srcdir}/${_imgdir}"

  # Main files
  install -d -m755 "${pkgdir}${_jvmdir}"

  cp -a include lib "${pkgdir}${_jvmdir}"

  # 'bin' files
  pushd bin
  install -d -m755 "${pkgdir}${_jvmdir}/bin/" \
                   "${pkgdir}"/usr/bin/ \
                   "${pkgdir}"/usr/share/man/{,ja/}man1/

  # 'java-rmi.cgi' will be handled separately as it should not be in the PATH and has no man page
  for b in $(ls | grep -v java-rmi.cgi); do
    if [ -e ../jre/bin/${b} ]; then
      # Provide a link of the jre binary in the jdk/bin/ directory
      ln -s ../jre/bin/${b} "${pkgdir}${_jvmdir}/bin/${b}"
    else
      # Copy binary to jdk/bin/
      install -m755 ${b} "${pkgdir}${_jvmdir}/bin/${b}"
      # Copy man page
      install -m644 ../man/man1/${b}.1 "${pkgdir}/usr/share/man/man1/${b}.1"
      install -m644 ../man/ja/man1/${b}.1 "${pkgdir}/usr/share/man/ja/man1/${b}.1"
      # Link from /bin/
      ln -s ${_jvmdir}/bin/${b} "${pkgdir}/usr/bin/${b}"
    fi
  done
  popd

  # Handling 'java-rmi.cgi' separately
  install -D -m755 bin/java-rmi.cgi "${pkgdir}${_jvmdir}/bin/java-rmi.cgi"

  # Install desktop files.
  install -d -m755 "${pkgdir}/usr/share/applications"
  install -m644 "${srcdir}/icedtea-${_icedtea_ver}/jconsole.desktop" "${pkgdir}/usr/share/applications"

  # Set some variables
  install -d -m755 "${pkgdir}/etc/profile.d/"
  install -m755 "${srcdir}/${pkgname}.profile" "${pkgdir}/etc/profile.d/jdk.sh"
  install -m755 "${srcdir}/${pkgname}.profile.csh" "${pkgdir}/etc/profile.d/jdk.csh"

  # Temporarily fixing FS#35141
  chmod go+r "${pkgdir}${_jvmdir}/lib/sa-jdi.jar"

  # link license
  install -d -m755 "${pkgdir}/usr/share/licenses"
  ln -sf /usr/share/licenses/${pkgbase} "${pkgdir}/usr/share/licenses/${pkgname}"
}

package_openjdk7-src() {
  pkgdesc='OpenJDK Java 7 sources'
  replaces=('openjdk6-src')

  install -D "${srcdir}/${_imgdir}/src.zip" "${pkgdir}${_jvmdir}/src.zip"
}

package_openjdk7-doc() {
  pkgdesc='OpenJDK Java 7 documentation'

  install -d -m755 "${pkgdir}/usr/share/doc/openjdk7-doc"
  for i in "${srcdir}"/icedtea-${_icedtea_ver}/openjdk.build/docs/*; do
    cp -a $i "${pkgdir}/usr/share/doc/openjdk7-doc/"
  done
}
