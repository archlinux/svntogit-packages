# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Guillaume ALAUX <guillaume@archlinux.org>

pkgname=('jre7-openjdk' 'jdk7-openjdk' 'openjdk7-src')
pkgbase=java7-openjdk
_java_ver=7
_openjdk_build=b147
_openjdk_date=27_jun_2011
_icedtea_ver=2.0
_date=20110922

# check "${srcdir}/icedtea7"/Makefile.am
_CORBA_CHANGESET=616c760dc288
_HOTSPOT_CHANGESET=1dd9b3d73b22
_JAXP_CHANGESET=c40983d6ae70
_JAXWS_CHANGESET=83db5e316798
_JDK_CHANGESET=0cb15650412a
_LANGTOOLS_CHANGESET=fb7fb3071b64
_OPENJDK_CHANGESET=3defd24c2671

pkgver=${_java_ver}.${_openjdk_build}_${_icedtea_ver}
pkgrel=0.20110922.1
arch=('i686' 'x86_64')
url="http://icedtea.classpath.org"
license=('custom')
makedepends=('eclipse-ecj' 'libcups' 'libxp' 'libxtst' 'libxi' 'libxt' 'libxslt' 'freetype2'
             'alsa-lib' 'xalan-java' 'glib2' 'gtk2' 'apache-ant>=1.6.5' 'giflib'
             'libjpeg>=6b' 'zlib' 'rhino' 'libpulse>=0.9.11' 'zip' 'unzip' 'cpio' 'lcms2')
source=(ftp://ftp.archlinux.org/other/$pkgname/icedtea7-${_date}-hg.tar.xz
	http://icedtea.classpath.org/hg/icedtea7-forest/archive/${_OPENJDK_CHANGESET}.tar.gz			# openjdk.tar.gz
        http://icedtea.classpath.org/hg/icedtea7-forest/corba/archive/${_CORBA_CHANGESET}.tar.gz		# corba.tar.gz
        http://icedtea.classpath.org/hg/icedtea7-forest/jaxp/archive/${_JAXP_CHANGESET}.tar.gz			# jaxp.tar.gz
        http://icedtea.classpath.org/hg/icedtea7-forest/jaxws/archive/${_JAXWS_CHANGESET}.tar.gz		# jaxws.tar.gz
        http://icedtea.classpath.org/hg/icedtea7-forest/jdk/archive/${_JDK_CHANGESET}.tar.gz		# jdk.tar.gz
        http://icedtea.classpath.org/hg/icedtea7-forest/langtools/archive/${_LANGTOOLS_CHANGESET}.tar.gz        # langtools.tar.gz
        http://icedtea.classpath.org/hg/icedtea7-forest/hotspot/archive/${_HOTSPOT_CHANGESET}.tar.gz	        # hotspot.tar.gz
        http://icedtea.classpath.org/download/drops/{jdk7-jaf-2010_08_19.zip,jdk7-jaxws2_2_4-b03-2011_05_27.zip,jaxp145_01.zip}
        fontconfig-paths.diff
        fix_corba_cmds_path.diff
        openjdk7_fix_jdk_cmds_path.diff
        openjdk7_nonreparenting-wm.diff
        jdk7-openjdk.profile
        jdk7-openjdk.profile.csh
        jre7-openjdk.profile
        jre7-openjdk.profile.csh
        jconsole.desktop
        policytool.desktop)
sha1sums=('e569c8a1265d30856caa646d101ce3e1e4a2a69f'
          '51dafe7367732d58a5ee531f5840fe1ab9c9d2bf'
          'c246e4a01d11bdbe4fb23f1bd3819456d1446f52'
          '8a286c9314f47f7bb6c59b8e8b4560db81019a83'
          'c0e7e9b99d4de220ef3346f3da942a4a39bb8b71'
          '6a115a71c94b4de1ad4e9c00fe3994bc31acd943'
          '29f8481fd98d709c72a81d5d742097455f38af7d'
          'a8a199fb4c908852a0afdd76937ad194191d20f7'
          '63ea970dcf129fbbba39a132d8f71add19fbf1f0'
          '539bcf80b9a86c97406a3c79e7d10684b213a4be'
          '106db88c8e53abb1a064e83d41cc7acd16b7713c'
          '00ae05d9c8679deb72c0a9008a670daafeb21565'
          'e551b40820393c4cab3a82154f51b55577644ba1'
          'fe04691f0a23f43a8d9017f7c5fccd4620bc9019'
          'ab22e135916786c181be66be39acd7954175bb9c'
          'b2dbb1a8a7264db229d6888467ed3f2799a007b0'
          '344f7e911ab52e38f1ff237ed770bec362d79f79'
          '683cbb342c8d3077b43b31be700864379c0aa54b'
          '2a12035cc3e586131952cbb78d9885f6dc10d1b2'
          '2e7827f47b9190dcc0697752b0f799caea872239'
          '0bc35e5436f1663f5251b8f8c159a04088a09484')

#http://www.java.net/download/openjdk/jdk${_java_ver}/promoted/${_openjdk_build}/openjdk-${_java_ver}-fcs-src-${_openjdk_build}-${_openjdk_date}.zip

noextract=("${_OPENJDK_CHANGESET}.tar.gz"
           "${_CORBA_CHANGESET}.tar.gz"
           "${_JAXP_CHANGESET}.tar.gz"
           "${_JAXWS_CHANGESET}.tar.gz"
           "${_JDK_CHANGESET}.tar.gz"
           "${_LANGTOOLS_CHANGESET}.tar.gz"
           "${_HOTSPOT_CHANGESET}.tar.gz"
           "jdk7-jaf-2010_08_19.zip"
           "jdk7-jaxws2_2_4-b03-2011_05_27.zip"
	   "jaxp145_01.zip")

# source PKGBUILD && mksource
#makedepends+=('mercurial')
mksource() {
  mkdir /tmp/icedtea7-${_date}
  pushd /tmp/icedtea7-${_date}
  hg -v clone http://icedtea.classpath.org/hg/icedtea7
  rm -rf icedtea7/.hg*
  tar -cvJf /tmp/icedtea7-${_date}/icedtea7-${_date}-hg.tar.xz *
  popd
}

build() {
  cd "${srcdir}/icedtea7"

  unset JAVA_HOME
  unset CLASSPATH
  unset MAKEFLAGS

  export ALT_PARALLEL_COMPILE_JOBS="${MAKEFLAGS/-j}"
  export HOTSPOT_BUILD_JOBS="${ALT_PARALLEL_COMPILE_JOBS}"

  . /etc/profile.d/apache-ant.sh

  cp ${srcdir}/*.diff ${srcdir}/icedtea7/patches
  export DISTRIBUTION_PATCHES="patches/fontconfig-paths.diff patches/fix_corba_cmds_path.diff patches/openjdk7_fix_jdk_cmds_path.diff patches/openjdk7_nonreparenting-wm.diff"

  # Bootstrap IcedTea with ecj and a GNU Classpath-based JDK:
  autoreconf --force --install

  ./configure \
	--disable-bootstrap \
        --disable-tests \
        --with-pkgversion=ArchLinux-${pkgver}-${pkgrel}-${CARCH} \
        --with-openjdk-src-zip=${srcdir}/${_OPENJDK_CHANGESET}.tar.gz \
        --with-hotspot-src-zip=${srcdir}/${_HOTSPOT_CHANGESET}.tar.gz \
        --with-corba-src-zip=${srcdir}/${_CORBA_CHANGESET}.tar.gz \
        --with-jaxp-src-zip=${srcdir}/${_JAXP_CHANGESET}.tar.gz \
        --with-jaxws-src-zip=${srcdir}/${_JAXWS_CHANGESET}.tar.gz \
        --with-jdk-src-zip=${srcdir}/${_JDK_CHANGESET}.tar.gz \
        --with-langtools-src-zip=${srcdir}/${_LANGTOOLS_CHANGESET}.tar.gz \
        --with-jaxp-drop-zip=${srcdir}/jaxp145_01.zip \
        --with-jaf-drop-zip=${srcdir}/jdk7-jaf-2010_08_19.zip \
        --with-jaxws-drop-zip=${srcdir}/jdk7-jaxws2_2_4-b03-2011_05_27.zip \
        --enable-pulse-java #--help

#        
	
#	  --enable-systemtap      Enable inclusion of SystemTap trace support
#	  --enable-nss            Enable inclusion of NSS security provider
#	  --with-abs-install-dir  The absolute path where the j2sdk-image dir will be installed

        
  #LD_PRELOAD="" 
  make
}

check() {
  cd "${srcdir}/icedtea7"
  make -k check
}

package_jre7-openjdk() {
  pkgdesc="Free Java environment based on OpenJDK 7.0 with IcedTea7 replacing binary plugs - runtime environment"
  depends=('gcc-libs' 'hicolor-icon-theme' 'ca-certificates-java' 'libxtst'
           'libxt' 'nss' 'libjpeg' 'freetype2' 'libxrender' 'libpng')
  optdepends=('icedtea-web-java7: web browser plugin + Java Web Start'
             'alsa-lib: for sound'
             'giflib: for gif format support')
  provides=('java-runtime=7')
  conflicts=('java-runtime')
#  replaces=('openjdk6') # once we remove openjdk6 pkg from the repos
  backup=(etc/profile.d/jre7-openjdk.sh)
  install=jre7-openjdk.install

  _jvmdir=/usr/lib/jvm/java-7-openjdk

  cd "${srcdir}/icedtea7/openjdk.build/j2sdk-image/jre"

  install -d -m755 ${pkgdir}/${_jvmdir}/jre/
  cp -a bin lib ${pkgdir}/${_jvmdir}/jre

  mv ${pkgdir}/${_jvmdir}/jre/lib/fontconfig.Ubuntu.properties.src \
     ${pkgdir}/${_jvmdir}/jre/lib/fontconfig.properties.src
  mv ${pkgdir}/${_jvmdir}/jre/lib/fontconfig.Ubuntu.bfc \
     ${pkgdir}/${_jvmdir}/jre/lib/fontconfig.bfc
  rm -f ${pkgdir}/${_jvmdir}/jre/lib/fontconfig.*.bfc
  rm -f ${pkgdir}/${_jvmdir}/jre/lib/fontconfig.*.properties.src

  # Install man pages
  pushd ../../j2re-image/man
  install -m755 -d ${pkgdir}/usr/share/man/{,ja/}man1/
  install -m644 man1/*.1 ${pkgdir}/usr/share/man/man1
  install -m644 ja_JP.UTF-8/man1/*.1 ${pkgdir}/usr/share/man/ja/man1
  popd

  # Install icons and menu entries
  for s in 16 24 32 48 ; do
    install -m755 -d ${pkgdir}/usr/share/icons/hicolor/${s}x${s}/apps/
    install -m644 ../../../openjdk/jdk/src/solaris/classes/sun/awt/X11/java-icon${s}.png \
                  ${pkgdir}/usr/share/icons/hicolor/${s}x${s}/apps/java.png
  done

  # Link binaries into /usr/bin
  pushd ${pkgdir}/${_jvmdir}/jre/bin
  install -m755 -d ${pkgdir}/usr/bin/
  for file in *; do
    ln -sf ${_jvmdir}/jre/bin/${file} ${pkgdir}/usr/bin
  done
  popd

  # Link JKS keystore from ca-certificates-java
  rm -f ${pkgdir}/${_jvmdir}/jre/lib/security/cacerts
  ln -sf /etc/ssl/certs/java/cacerts "${pkgdir}/${_jvmdir}/jre/lib/security/cacerts"

  # Set some variables
  install -m755 -d ${pkgdir}/etc/profile.d/
  install -m755 ${srcdir}/${pkgname}.profile ${pkgdir}/etc/profile.d/${pkgname}.sh
  install -m755 ${srcdir}/${pkgname}.profile.csh ${pkgdir}/etc/profile.d/${pkgname}.csh

  # Install license
  install -m755 -d ${pkgdir}/usr/share/licenses/${pkgbase}/
  install -m644 ASSEMBLY_EXCEPTION LICENSE THIRD_PARTY_README \
                 ${pkgdir}/usr/share/licenses/${pkgbase}
}

package_jdk7-openjdk() {
  pkgdesc="Free Java environment based on OpenJDK 7.0 with IcedTea7 replacing binary plugs - SDK"
  depends=('jre7-openjdk')
  provides=('java-environment=7')
  conflicts=('java-environment')
  #  replaces=('openjdk6')
  backup=(etc/profile.d/jdk7-openjdk.sh)

  _jvmdir=/usr/lib/jvm/java-7-openjdk

  cd "${srcdir}/icedtea7/openjdk.build/j2sdk-image"

  # Main files
  install -m755 -d ${pkgdir}/${_jvmdir}/

  cp -a demo include lib sample ${pkgdir}/${_jvmdir}

  # 'bin' files
  pushd bin
  install -m755 -d ${pkgdir}/${_jvmdir}/bin/ \
                   ${pkgdir}/usr/bin/ \
                   ${pkgdir}/usr/share/man/{,ja/}man1/

  # 'java-rmi.cgi' will be handled separately as it should not be in the PATH and has no man page
  for b in $(ls | grep -v java-rmi.cgi); do
    if [ -e ../jre/bin/${b} ]; then
      # Provide a link of the jre binary in the jdk/bin/ directory
      ln -s ../jre/bin/${b} ${pkgdir}/${_jvmdir}/bin/${b}
    else
      # Copy binary to jdk/bin/
      install -m755 ${b} ${pkgdir}/${_jvmdir}/bin/${b}
      # Copy man page
      install -m644 ../man/man1/${b}.1 ${pkgdir}/usr/share/man/man1/${b}.1
      install -m644 ../man/ja/man1/${b}.1 ${pkgdir}/usr/share/man/ja/man1/${b}.1
      # Link from /bin/
      ln -s ${_jvmdir}/bin/${b} ${pkgdir}/usr/bin/${b}
    fi
  done
  popd

  # Handling 'java-rmi.cgi' separately
  install -m755 -D bin/java-rmi.cgi ${pkgdir}/${_jvmdir}/bin/java-rmi.cgi

  # Desktop files
  install -m755 -d ${pkgdir}/usr/share/applications/
  install -m644 ${srcdir}/{jconsole,policytool}.desktop ${pkgdir}/usr/share/applications/

  # Set some variables
  install -m755 -d ${pkgdir}/etc/profile.d/
  install -m755 ${srcdir}/${pkgname}.profile ${pkgdir}/etc/profile.d/${pkgname}.sh
  install -m755 ${srcdir}/${pkgname}.profile.csh ${pkgdir}/etc/profile.d/${pkgname}.csh
}

package_openjdk7-src() {
  pkgdesc="Free Java environment based on OpenJDK 7.0 with IcedTea7 replacing binary plugs - sources"

  install -D ${srcdir}/icedtea7/openjdk.build/j2sdk-image/src.zip \
             ${pkgdir}/${_jvmdir}/src.zip
}
