# $Id$
# Maintainer: Vesa Kaihlavirta <vesa@archlinux.org>
# Special note for devs looking to upgrade this package:
#   ghc places a unique hash for each library when it is built.
#   Libraries depend on versions specified by those hashes.
#   This implies that all libraries need to be rebuilt when ghc is rebuilt.
#   Also, due to dependency loop problems, haskell-ghc-paths and haddock
#   do not carry version dependencies. You will have to remember to build them too.
pkgname=ghc
pkgver=7.0.3
pkgrel=2
pkgdesc="The Glasgow Haskell Compiler"
arch=(i686 x86_64)
url="http://www.haskell.org/ghc/"
license=("custom")
depends=('perl' 'gmp>=5.0' gcc)
makedepends=(ghc happy perl libxslt docbook-xsl)
provides=("haskell-array=0.3.0.2"
          "haskell-bytestring=0.9.1.10"
          "haskell-cabal=1.10.1.0"
          "haskell-containers=0.4.0.0"
          "haskell-directory=1.1.0.0"       
          "haskell-extensible-exceptions=0.1.1.2"
          "haskell-filepath=1.2.0.0"
          "haskell-haskell98=1.1.0.1"
          "haskell-hpc=0.5.0.6"
          "haskell-old-locale=1.0.0.2"
          "haskell-old-time=1.0.0.6"
          "haskell-pretty=1.0.1.2"
          "haskell-process=1.0.1.5"
          "haskell-random=1.0.0.3"
          "haskell-template-haskell=2.5.0.0"
          "haskell-time=1.2.0.3"
          "haskell-unix=2.4.2.0"
         )
replaces=("haskell-array<0.3.0.2" 
          "haskell-bytestring<0.9.1.10"
          "haskell-cabal<1.10.1.0"
          "haskell-containers<0.4.0.0"
          "haskell-directory<1.1.0.0"
          "haskell-extensible-exceptions<0.1.1.2"
          "haskell-filepath<1.2.0.0"
          "haskell-haskell98<1.1.0.1"
          "haskell-hpc<0.5.0.6"
          "haskell-old-locale<1.0.0.2"
          "haskell-old-time<1.0.0.6"
          "haskell-pretty<1.0.1.2"
          "haskell-process<1.0.1.5"
          "haskell-random<1.0.0.3"
          "haskell-template-haskell<2.5.0.0"
          "haskell-time<1.2.0.3"
          "haskell-unix<2.4.2.0"
         )

install=ghc.install
options=(!strip)
source=("http://www.haskell.org/ghc/dist/$pkgver/ghc-$pkgver-src.tar.bz2"
        build.mk)

build() {
  cd $srcdir/$pkgname-$pkgver

  cp $srcdir/build.mk mk/build.mk

  ./configure --prefix=/usr 
  make -j1 || return 1
}

package() {
  cd $srcdir/$pkgname-$pkgver
  
  make DESTDIR=$pkgdir install

  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/ghc/license
  install -d $pkgdir/usr/share/haskell
}

md5sums=('10bc5e9ae1b581404376efb85f260ff3'
         '5a3e0880c80b1dcecf779ff2e63a9c0d')
