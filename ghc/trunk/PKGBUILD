# $Id$
# Maintainer: Thomas Dziedzic <gostrc@gmail.com>
# Maintainer: Vesa Kaihlavirta <vesa@archlinux.org>
# Special note for devs looking to upgrade this package:
#   ghc places a unique hash for each library when it is built.
#   Libraries depend on versions specified by those hashes.
#   This implies that all libraries need to be rebuilt when ghc is rebuilt.

pkgname=ghc
pkgver=7.4.2
pkgrel=1
pkgdesc='The Glasgow Haskell Compiler'
arch=('i686' 'x86_64')
url='http://www.haskell.org/ghc/'
license=('custom')
# libffi might be needed in the future: http://hackage.haskell.org/trac/ghc/ticket/4496
depends=('perl' 'gmp>=5.0' 'gcc') # 'libffi')
makedepends=('ghc' 'perl' 'libxslt' 'docbook-xsl')
checkdepends=('python2')
install='ghc.install'
# keep this otherwise a lot of tests will show as unexpected failures... need to investigate why using -jn for n > 1 does this...
options=('!makeflags')
# missing rtl?
provides=('haskell-array=0.4.0.0'
          'haskell-base=4.5.1.0'
          'haskell-binary=0.5.1.0'
          'haskell-bin-package-db=0.0.0.0'
          'haskell-bytestring=0.9.2.1'
          'haskell-containers=0.4.2.1'
          'haskell-deepseq=1.3.0.0'
          'haskell-directory=1.1.0.2'
          'haskell-extensible-exceptions=0.1.1.4'
          'haskell-filepath=1.3.0.0'
          'haskell-ghc-prim=0.2.0.0'
          'haskell-haskell2010=1.1.0.1'
          'haskell-haskell98=2.0.0.1'
          'haskell-hoopl=3.8.7.3'
          'haskell-hpc=0.5.1.1'
          'haskell-integer-gmp=0.4.0.0'
          'haskell-old-locale=1.0.0.4'
          'haskell-old-time=1.1.0.0'
          'haskell-pretty=1.1.1.0'
          'haskell-process=1.1.0.1'
          'haskell-template-haskell=2.7.0.0'
          'haskell-time=1.4'
          'haskell-unix=2.5.1.1'
          'haskell-cabal=1.14.0'
         )
replaces=('haskell-array<0.4.0.0'
          'haskell-base<4.5.1.0'
          'haskell-binary<0.5.1.0'
          'haskell-bin-package-db<0.0.0.0'
          'haskell-bytestring<0.9.2.1'
          'haskell-containers<0.4.2.1'
          'haskell-deepseq<1.3.0.0'
          'haskell-directory<1.1.0.2'
          'haskell-extensible-exceptions<0.1.1.4'
          'haskell-filepath<1.3.0.0'
          'haskell-ghc-prim<0.2.0.0'
          'haskell-haskell2010<1.1.0.1'
          'haskell-haskell98<2.0.0.1'
          'haskell-hoopl<3.8.7.3'
          'haskell-hpc<0.5.1.1'
          'haskell-integer-gmp<0.4.0.0'
          'haskell-old-locale<1.0.0.4'
          'haskell-old-time<1.1.0.0'
          'haskell-pretty<1.1.1.0'
          'haskell-process<1.1.0.1'
          'haskell-template-haskell<2.7.0.0'
          'haskell-time<1.4'
          'haskell-unix<2.5.1.1'
          'haskell-cabal<1.14.0'
         )
source=("http://www.haskell.org/ghc/dist/${pkgver}/ghc-${pkgver}-src.tar.bz2"
        "http://www.haskell.org/ghc/dist/${pkgver}/ghc-${pkgver}-testsuite.tar.bz2"
        'build.mk'
        'silence-gen_contents_index.diff')
md5sums=('267462db5c5a7c245fb26361b77007c4'
         '528005749c761fe6c12a0079bd84fb90'
         '88ad4108c26f56fc38466d82aaf9a295'
         '2ac480d025b706fc47acc58c54b246bb')

build() {
  cd ghc-${pkgver}

  # Silence gen_contents_index for the common case.
  # http://hackage.haskell.org/trac/ghc/ticket/5992
  patch -Np1 -i ${srcdir}/silence-gen_contents_index.diff

  cp ${srcdir}/build.mk mk/build.mk

  ./configure \
    --prefix=/usr

  make
}

check() {
  cd ghc-${pkgver}

  # python2 rename
  sed -e 's/PYTHON = python/&2/' -i testsuite/mk/boilerplate.mk
  sed -e 's_#!/usr/bin/env python_&2_' -i testsuite/timeout/calibrate testsuite/timeout/timeout.py

  # upstream known failures on both i686 and x86_64:
  #    ghci.debugger/scripts     break011 [bad stderr] (ghci)
  #    perf/compiler             parsing001 [stat not good enough] (normal)
  #    simplCore/should_compile  spec-inline [stderr mismatch] (optasm)
  # upstream known failures on i686 only:
  #    numrun012 test fails on i686 works on x86_64 - http://hackage.haskell.org/trac/ghc/ticket/5856
  make test

  # enabling threads to speed up
  # causes more failures than make test, need to investigate
  #make THREADS=9 test

  # will most likely cause lots of failures
  # enable this when upstream has 0 test failures from a simple 'make test'
  # zero unexpected failures on all tier 1 platforms - http://hackage.haskell.org/trac/ghc/ticket/5757
  #make fulltest
}

package() {
  cd ghc-${pkgver}

  make DESTDIR=${pkgdir} install

  install -d ${pkgdir}/usr/share/licenses/ghc
  install -m644 LICENSE \
    ${pkgdir}/usr/share/licenses/ghc
}
