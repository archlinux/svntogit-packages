# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Andy Roberts (arooaroo) <dev@andy-roberts.net>

pkgname=lucene
pkgver=2.9.4 # don't update to 3.x tree - OOo is not yet ready to use it
pkgrel=1
arch=('any')
pkgdesc="A high-performance, full-featured text search engine library written entirely in Java"
url="http://lucene.apache.org/java/docs/index.html"
license=("APACHE")
depends=('java-runtime')
source=(http://www.mirrorservice.org/sites/ftp.apache.org/lucene/java/$pkgver/lucene-${pkgver}.tar.gz)
md5sums=('ffc60c3a163105b22610dec15602329a')

build() {
	/bin/true
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  install -m755 -d ${pkgdir}/usr/share/java/

  # install all *.jar files
  for i in `find ${srcdir} -name '*.jar'`; do
    install $i ${pkgdir}/usr/share/java/
  done

  # create symlinks
  cd ${pkgdir}/usr/share/java/
  for i in `ls -1 *${pkgver}*.jar`; do
    j=${i//-${pkgver}/}
    ln -sv $i $j
  done
  
  # and one with different version numbering
  ln -sv servlet-api-2.4.jar servlet-api.jar
}
