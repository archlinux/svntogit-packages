# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=nasm
pkgver=2.13.03
pkgrel=1
pkgdesc="An 80x86 assembler designed for portability and modularity"
arch=('x86_64')
url="http://www.nasm.us"
license=('BSD')
depends=('glibc')
makedepends=('perl-font-ttf' 'perl-sort-versions' 'fontconfig' 'adobe-source-sans-pro-fonts' 'ttf-liberation' 'ghostscript')
source=(http://www.nasm.us/pub/nasm/releasebuilds/${pkgver}/${pkgname}-${pkgver}.tar.xz)
sha1sums=('1338ae8832bdbabd00a2b7ee3e39b8fd2f923ea0')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
  make -C doc
}

package() {
  cd ${pkgname}-${pkgver}
  make INSTALLROOT="${pkgdir}" install install_rdf
  install -D -m644 LICENSE "${pkgdir}"/usr/share/licenses/nasm/LICENSE
}
