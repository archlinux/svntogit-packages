# $Id$
# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Aaron Griffin <aaron@archlinux.org>

pkgname=nasm
pkgver=2.10.05
pkgrel=1
pkgdesc="An 80x86 assembler designed for portability and modularity"
url="http://www.nasm.us"
license=('BSD')
arch=('i686' 'x86_64')
depends=('glibc')
source=(http://www.nasm.us/pub/nasm/releasebuilds/${pkgver}/${pkgname}-${pkgver}.tar.bz2)
sha1sums=('fb9cd57680ed8026c9b612f0db8ecf8df05a1e3c')
install=nasm.install

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  ./configure --prefix=/usr
  make
  make -C doc info
}

package() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  make INSTALLROOT="${pkgdir}" install install_rdf
  install -dm755 "${pkgdir}"/usr/share/info
  install -m644 doc/info/* "${pkgdir}"/usr/share/info/
  install -D -m644 LICENSE "${pkgdir}"/usr/share/licenses/nasm/LICENSE
}
