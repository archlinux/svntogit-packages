# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-kalzium
pkgver=4.13.1
pkgrel=1
pkgdesc="Periodic Table of Elements"
url="http://kde.org/applications/education/kalzium/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
depends=('kdebase-runtime' 'avogadro' 'ocaml' 'facile' 'chemical-mime-data')
makedepends=('cmake' 'automoc4' 'mesa')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/kalzium-${pkgver}.tar.xz")
sha1sums=('0a6aae34077bfcf410d8c1284375e40c9038a02c')

build() {
  mkdir build
  cd build
  cmake ../kalzium-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
