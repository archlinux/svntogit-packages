# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: tobias <tobias@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>

pkgname=dcraw
pkgver=9.28.0
pkgrel=1
pkgdesc='Decodes any raw image from any digital camera'
url='https://www.cybercom.net/~dcoffin/dcraw/'
arch=('x86_64')
license=('custom')
depends=('lcms2' 'jasper')
source=("https://www.cybercom.net/~dcoffin/dcraw/archive/${pkgname}-${pkgver}.tar.gz")
sha256sums=('2890c3da2642cd44c5f3bfed2c9b2c1db83da5cec09cc17e0fa72e17541fb4b9')

build() {
	cd "${srcdir}/${pkgname}"
	gcc ${CFLAGS} ${LDFLAGS} \
		-o dcraw dcraw.c \
		-lm -ljasper -ljpeg -llcms2 \
		-DLOCALEDIR=\"/usr/share/locale/\"
}

package() {
	cd "${srcdir}/${pkgname}"

	install -Dm755 dcraw "${pkgdir}"/usr/bin/dcraw
	install -Dm644 dcraw.1 "${pkgdir}"/usr/share/man/man1/dcraw.1
	
	for i in dcraw_*.1; do j=${i#dcraw_}; k=${j%.1}
		install -Dm644 $i "${pkgdir}"/usr/share/man/$k/man1/dcraw.1
	done

	for i in dcraw_*.po; do j=${i#dcraw_}; k=${j%.po}
		install -d "${pkgdir}"/usr/share/locale/$k/LC_MESSAGES
		msgfmt -o "${pkgdir}"/usr/share/locale/$k/LC_MESSAGES/dcraw.mo $i
	done

	install -d "${pkgdir}"/usr/share/licenses/${pkgname}
	head -25 dcraw.c > "${pkgdir}"/usr/share/licenses/${pkgname}/license.txt
}
