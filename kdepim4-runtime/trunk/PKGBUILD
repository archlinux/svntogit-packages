# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdepim4-runtime
pkgver=4.14.10
_kdeappver=15.04.3
pkgrel=3
pkgdesc='Extends the functionality of kdepim4'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kdepim-runtime'
license=('GPL' 'LGPL' 'FDL')
depends=('kdebase-runtime' 'libkgapi4' 'libkolab4' 'libaccounts-qt4' 'libsignon-qt4' 'akonadi-qt4')
makedepends=('cmake' 'automoc4' 'boost')
conflicts=('kdepim-runtime')
replaces=('kdepim-runtime<15')
install="${pkgname}.install"
source=("http://download.kde.org/stable/applications/${_kdeappver}/src/kdepim-runtime-${pkgver}.tar.xz")
sha1sums=('f5b1e44f1d10d29bad308b3fd90a48b34da7493c')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../kdepim-runtime-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
