# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=hunspell-ro
pkgver=3.3.10
pkgrel=5
pkgdesc="Romanian dictionary for Hunspell"
arch=('any')
url="https://sourceforge.net/projects/rospell/"
license=('LGPL' 'GPL' 'MPL')
depends=('hunspell')
makedepends=('qt5-webengine')
source=(https://downloads.sourceforge.net/rospell/ro_RO.$pkgver.zip)
sha512sums=('192d53fc087fb96306c46092a2b5cac9c28528f741c686d3b5e80c11c7bfdd16e1e406cfc1c32a99d72e2db01722e539b28c716379f38a3703e6954c8f9f2170')

package() {
  install -dm755 "$pkgdir"/usr/share/hunspell

  install -D -m644 *.aff *.dic -t "$pkgdir"/usr/share/hunspell/

 # the symlinks
  install -dm755 "$pkgdir"/usr/share/myspell/dicts
  pushd "$pkgdir"/usr/share/myspell/dicts
    for file in "$pkgdir"/usr/share/hunspell/*; do
      ln -sv /usr/share/hunspell/$(basename $file) .
    done
  popd

# Install webengine dictionaries
  install -d "$pkgdir"/usr/share/qt/qtwebengine_dictionaries/
  for _file in "$pkgdir"/usr/share/hunspell/*.dic; do
  _filename=$(basename $_file)
    qwebengine_convert_dict $_file "$pkgdir"/usr/share/qt/qtwebengine_dictionaries/${_filename/\.dic/\.bdic}
  done
}
