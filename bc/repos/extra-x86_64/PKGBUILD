# $Id$
# Maintainer: dorphell <dorphell@archlinux.org>

pkgname=bc
pkgver=1.06.95
pkgrel=1
pkgdesc="An arbitrary precision calculator language"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/bc/"
license=('GPL')
depends=('readline')
replaces=('bc-readline')
conflicts=('bc-readline')
install=bc.install
source=(ftp://alpha.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.bz2{,.sig}
        bc-1.06.95-void_uninitialized.patch)
md5sums=('5126a721b73f97d715bb72c13c889035'
         'SKIP'
         'fe85d42dd4151801e9b8c2d2c4d990bf')

prepare() {
  cd "${pkgname}-${pkgver}"

  # https://bugs.gentoo.org/show_bug.cgi?id=349339
  patch -Np0 -i ${srcdir}/bc-1.06.95-void_uninitialized.patch
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr --mandir=/usr/share/man \
    --infodir=/usr/share/info --with-readline
  make
}

check () {
  cd "${pkgname}-${pkgver}"
  echo "quit" | ./bc/bc -l Test/checklib.b # 10 failures
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
