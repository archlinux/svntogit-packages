# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdesdk-kioslaves
pkgver=14.12.2
pkgrel=1
pkgdesc='KIO-Slaves'
url='https://projects.kde.org/projects/kde/kdesdk/kdesdk-kioslaves'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdesdk')
depends=('kdebase-runtime' 'subversion')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/applications/${pkgver}/src/kdesdk-kioslaves-${pkgver}.tar.xz")
sha1sums=('208b4a68117bd691526cb3d1c283d203344c5196')

build() {
  mkdir build
  cd build
  cmake ../kdesdk-kioslaves-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
