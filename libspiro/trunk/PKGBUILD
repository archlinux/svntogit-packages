# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: RÃ©my Oudompheng <oudomphe@clipper.ens.fr>

pkgname=libspiro
epoch=1
pkgver=0.5.20150702
pkgrel=2
pkgdesc='Simplifies the drawing of beautiful curves'
url='https://github.com/fontforge/libspiro'
license=('GPL')
arch=('x86_64')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/fontforge/libspiro/archive/${pkgver}.tar.gz")
sha256sums=('14f761d83c7fa6be31c4e0317251ed1201b367dc5b2a8678e2da179d74938fc7')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	autoreconf -i
	automake --foreign -Wall
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	export LDFLAGS=
	./configure --prefix=/usr
	make
}

check() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make check
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	install -Dm644 libspiro.pc "${pkgdir}"/usr/lib/pkgconfig/libspiro.pc
}
