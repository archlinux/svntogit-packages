# Maintainer: Thomas Dziedzic <gostrc@gmail.com>

_hkgname=zlib
pkgname=haskell-zlib
pkgver=0.5.4.1
pkgrel=3
pkgdesc="Compression and decompression in the gzip and zlib formats"
url="http://hackage.haskell.org/package/${_hkgname}"
license=('custom:BSD3')
arch=('i686' 'x86_64')
options=('staticlibs')
depends=('ghc=7.8.2-2' 'sh' 'zlib')
source=("http://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
install=${pkgname}.install
md5sums=('d0d10786d2bbd1d401a8b28a83e88475')

build() {
  cd ${_hkgname}-${pkgver}

  runhaskell Setup configure -O -p \
    --enable-split-objs \
    --enable-shared \
    --prefix=/usr \
    --docdir=/usr/share/doc/${pkgname} \
    --libsubdir=\$compiler/site-local/\$pkgid

  runhaskell Setup build

  runhaskell Setup haddock

  runhaskell Setup register   --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd ${_hkgname}-${pkgver}

  install -D -m744 register.sh   ${pkgdir}/usr/share/haskell/${pkgname}/register.sh
  install    -m744 unregister.sh ${pkgdir}/usr/share/haskell/${pkgname}/unregister.sh

  install -d -m755 ${pkgdir}/usr/share/doc/ghc/html/libraries
  ln -s /usr/share/doc/${pkgname}/html ${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}

  runhaskell Setup copy --destdir=${pkgdir}

  install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
  rm -f ${pkgdir}/usr/share/doc/${pkgname}/LICENSE
}

