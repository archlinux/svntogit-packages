# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-kolf
pkgver=15.11.90
pkgrel=1
pkgdesc="A miniature golf game with 2d top-down view"
url="http://kde.org/applications/games/kolf/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegames')
depends=('kdebase-runtime' 'libkdegames4')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/unstable/applications/${pkgver}/src/kolf-${pkgver}.tar.xz")
sha1sums=('9fa37d663a10ed22f40c55154d0027ff10b803be')

build() {
  mkdir -p build
  cd build
  cmake ../kolf-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
