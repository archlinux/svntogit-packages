# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Stefano Zamprogno <stefano.zamprogno@gmail.com>

pkgname=k9copy
pkgver=2.3.7
pkgrel=1
pkgdesc="A small utility which allows the copy of DVD under Linux"
arch=('i686' 'x86_64')
url="http://k9copy.sourceforge.net"
license=('GPL')
depends=('kdebase-runtime' 'libmpeg2' 'xine-lib' 'dvd+rw-tools' 'dvdauthor')
makedepends=('automoc4' 'cmake')
install=k9copy.install
source=("http://downloads.sourceforge.net/project/${pkgname}/k9copy-kde4/${pkgver}/${pkgname}-${pkgver}-Source.tar.gz")
md5sums=('bbfda2937f5027d31495df67dc29efa4')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}-Source"

  sed -i -e "s:Name=k9copy:Name=K9copy:g" k9copy.desktop k9copy_assistant.desktop
  cmake -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}-Source"

  make DESTDIR="${pkgdir}" install
}
