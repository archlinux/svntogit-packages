# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kguiaddons
pkgver=5.99.0
pkgrel=2
pkgdesc='Addons to QtGui'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(qt5-x11extras qt5-wayland)
makedepends=(extra-cmake-modules clang doxygen qt5-tools qt5-doc plasma-wayland-protocols)
groups=(kf5)
source=(https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz{,.sig}
        https://invent.kde.org/frameworks/kguiaddons/-/commit/a30c1fd2.patch)
sha256sums=('ee87ea65b7a3cdcc0fe9798db8c02b028c194fd48a3b56f1d30b3494e0711b4f'
            'SKIP'
            'edc30e92d4b6412522f9b9425c6b455a3b354d8cc87eb00a4d48ec96be0ef65c')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>
options=(debug)

prepare() {
  patch -d $pkgname-$pkgver -p1 < a30c1fd2.patch # Fix high CPU usage
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
