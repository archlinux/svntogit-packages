# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=tinycdb
pkgver=0.78
pkgrel=2
pkgdesc='Very fast and simple package for creating and reading constant data bases'
url='http://www.corpit.ru/mjt/tinycdb.html'
license=('custom')
arch=('i686' 'x86_64')
source=("http://www.corpit.ru/mjt/tinycdb/${pkgname}-${pkgver}.tar.gz")
sha1sums=('ade42ee1e7c56f66a63cb933206c089b9983adba')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	sed -e 's/all: static/all: shared/' -e '/libcdb.a/d' -i Makefile
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make CFLAGS="${CFLAGS}" LDFLAGS="${LDFLAGS}"
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" prefix=/usr mandir=/usr/share/man \
		INSTALLPROG=cdb-shared install-{all,sharedlib}
	install -Dm644 debian/copyright "${pkgdir}"/usr/share/licenses/tinycdb/copyright
}
