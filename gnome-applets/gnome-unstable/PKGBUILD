# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-applets
pkgver=2.28.0
pkgrel=1
pkgdesc="GNOME Applets"
arch=(i686 x86_64)
license=('GPL')
depends=('gstreamer0.10-base-plugins>=0.10.24' 'gnome-panel>=2.27.92' 'libgtop>=2.28.0' 'gucharmap>=2.27.0' 'libnotify>=0.4.5' 'cpufrequtils' 'libgnomekbd>=2.27.91')
makedepends=('gnome-doc-utils>=0.17.5' 'pkgconfig' 'gnome-settings-daemon>=2.27.92' 'intltool' 'gnome-icon-theme>=2.27.90' 'gnome-python-desktop>=2.27.3' 'networkmanager')
optdepends=('gnome-python-desktop: invest-applet')
options=(!emptydirs)
url="http://www.gnome.org"
groups=(gnome)
install=gnome-applets.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.28/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('73aa1d5b9636ea3293d206986115473f30581dd4a937267873573880eb1bec7b')
	
build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/gnome-applets \
      --localstatedir=/var --disable-static \
      --disable-scrollkeeper --enable-mixer-applet \
      --disable-schemas-install || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1
  rm -f ${pkgdir}/usr/lib/*.la

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gnome-applets-2.0 ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
