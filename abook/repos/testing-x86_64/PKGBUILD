# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Thayer Williams <thayer@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=abook
pkgver=0.6.0pre2.20131110
pkgrel=2
pkgdesc='Text-based addressbook designed for use with Mutt'
url='http://abook.sourceforge.net/'
arch=('i686' 'x86_64')
license=('GPL2')
makedepends=('git')
depends=('readline')
source=("abook::git://abook.git.sourceforge.net/gitroot/abook/abook#commit=2e3d4f7341e154bf09907aabcdfe73345cc72e68"
        'gcc5.patch'
        'vcard.patch')
sha1sums=('SKIP'
          '5620fcf2442a4ecbf0cfef1515d936307378e307'
          '7f58e32814510ae491fadaa9f06d787ccd2ecfcd')

prepare() {
	cd "${srcdir}/${pkgname}"
	patch -p1 -i ../gcc5.patch
	patch -p1 -i ../vcard.patch
}

build() {
	cd "${srcdir}/${pkgname}"
	./configure --prefix=/usr --mandir=/usr/share/man
	make
	cd po && make update-po
}

package() {
	cd "${srcdir}/${pkgname}"
	make DESTDIR="${pkgdir}" install
}
