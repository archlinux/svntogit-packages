# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Thayer Williams <thayer@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=abook
pkgver=0.6.1
pkgrel=2
pkgdesc='Text-based addressbook designed for use with Mutt'
url='http://abook.sourceforge.net/'
arch=('i686' 'x86_64')
license=('GPL2')
makedepends=('git')
depends=('readline')
validpgpkeys=('3552E46F58F5FEC69A2CD85A5B4E2D0DD7F62B21')
source=("http://abook.sourceforge.net/devel/${pkgname}-${pkgver}.tar.gz"{,.asc}
        'gcc5.patch')
sha1sums=('f404b31bd3134969fdc8b40a34b50dd35299e9c7' 'SKIP'
          '5620fcf2442a4ecbf0cfef1515d936307378e307')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	aclocal && automake --add-missing && autoconf
	sed 's/0.18/0.19/g' -i po/Makefile.in.in
	patch -p1 -i ../gcc5.patch
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr --mandir=/usr/share/man
	make
	cd po && make update-po
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
