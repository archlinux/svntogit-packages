# $Id$
# Maintainer:

pkgname=gettext
pkgver=0.18.2.1
pkgrel=1
pkgdesc="GNU internationalization library"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/gettext/"
license=('GPL')
groups=('base' 'base-devel')
depends=('gcc-libs' 'acl' 'sh' 'glib2')
optdepends=('cvs: for autopoint tool')
options=(!libtool !docs)
install=gettext.install
source=(ftp://ftp.gnu.org/pub/gnu/gettext/${pkgname}-${pkgver}.tar.gz{,.sig})
md5sums=('034c8103b14654ebd300fadac44d6f14'
         'e4658c307333395a12f8d0b4d95fbb0f')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
