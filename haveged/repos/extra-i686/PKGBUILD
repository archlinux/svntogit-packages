# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <barthalion@gmail.com>
# Contributor: kfgz <kfgz at interia dot pl>
# Contributor: pootzko <pootzko at gmail dot com>

pkgname=haveged
pkgver=1.7.c
_pkgver=${pkgver/.c/c}
pkgrel=3
pkgdesc='Entropy harvesting daemon using CPU timings'
arch=('i686' 'x86_64')
url='http://www.issihosts.com/haveged'
license=('GPL')
depends=('glibc')
source=($url/$pkgname-$_pkgver.tar.gz
        service cleanexit.patch)
sha256sums=('40397eded96fc0d624a6a81c8e888ab023e46f3114f1ee699994e645531bf121'
            'badf8ceb5484d45888a089fabdfba5f44f6ec2930bca203c50200051a4d83e12'
            'be896659dd96cb85915ac5a9c2bd8d81d1ff348508362d1b71d64b207f69fa5c')

prepare() {
  cd $pkgname-$_pkgver
  patch -Np1 -i ../cleanexit.patch
}

build() {
  cd $pkgname-$_pkgver
  ./configure --prefix=/usr --bindir=/usr/bin --sbindir=/usr/bin
  make
}

package() {
  cd $pkgname-$_pkgver
  make DESTDIR="$pkgdir" install

  rm -rf "$pkgdir"/etc/init.d
  install -D -m644 "$srcdir"/service \
    "$pkgdir"/usr/lib/systemd/system/haveged.service
}
