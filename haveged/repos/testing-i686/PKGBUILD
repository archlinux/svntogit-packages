# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: kfgz <kfgz at interia dot pl>
# Contributor: pootzko <pootzko at gmail dot com>

pkgname=haveged
pkgver=1.9.1
pkgrel=3
pkgdesc='Entropy harvesting daemon using CPU timings'
arch=('i686' 'x86_64')
url='http://www.issihosts.com/haveged'
license=('GPL')
depends=('glibc')
validpgpkeys=('4CFBA9F06A360837CE23919DA24EDC651E265688') # Gary Wuertz <gary@issiweb.com>
source=("$url/$pkgname-$pkgver.tar.gz"
        "$pkgname-$pkgver.tar.gz.sig::$url/verify/$pkgname-$pkgver.sig"
        'service')
sha256sums=('9c2363ed9542a6784ff08e247182137e71f2ddb79e8e6c1ac4ad50d21ced3715'
            'SKIP'
            '5cd8bf4bf5ab18ee46dd5b461b82bad832ccf87cc3f1e0586574db9a55ae9925')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --bindir=/usr/bin --sbindir=/usr/bin
  make
}

package() {
  make -C $pkgname-$pkgver DESTDIR="$pkgdir" install
  install -Dm644 service "$pkgdir"/usr/lib/systemd/system/haveged.service
}
