# $Id$
# Maintainer: tobias <tobias@archlinux.org>

pkgname=autopano-sift
pkgver=2.4
pkgrel=4
pkgdesc="a C# based detector for picture matching"
arch=(i686 x86_64)
license=('GPL2')
url="http://user.cs.tu-berlin.de/~nowozin/autopano-sift/"
depends=('mono>=1.0' 'gtk-sharp')
options=(!makeflags)
source=(http://user.cs.tu-berlin.de/~nowozin/${pkgname}/${pkgname}-${pkgver}.tar.gz
        autopano-sift.desktop
        autopanog)
md5sums=('e497074d0cb5586cdc66506d782ccbd7'
         '7fb1da98031d7421a13ea713d914c89e'
         '3883892a0fa32f54d1aefc46e46657ed')

build() {
  export MONO_SHARED_DIR="${srcdir}/.wapi"
  mkdir -p "${MONO_SHARED_DIR}"

  cd "${srcdir}/${pkgname}-${pkgver}/src"
  make clean all || return 1

  install -m755 -d "${pkgdir}/usr/lib/autopano-sift"
  install -m755 -d "${pkgdir}/usr/bin"
  install -m755 -d "${pkgdir}/usr/share/man/man1"
  install -m755 -d "${pkgdir}/usr/share/man/man7"
  install -m755 -d "${pkgdir}/usr/share/pixmaps"
  install -m755 -d "${pkgdir}/usr/share/applications"

  install -m644 *.dll util/*.exe util/autopanog/autopanog.exe \
      "${pkgdir}/usr/lib/autopano-sift/" || return 1

  sed -i 's|^mono|/usr/bin/mono|' util/monoopt.sh
  install -m755 util/monoopt.sh "${pkgdir}/usr/bin/" || return 1
  sed -e 's|^MONO.*$|MONO=/usr/bin/monoopt.sh|' \
      -e 's|^AUTOPANO_PATH.*$|AUTOPANO_PATH=/usr/lib/autopano-sift|' \
      -i bin/*.sh || return 1
  install -m755 bin/*.sh "${pkgdir}/usr/bin/" || return 1

  install -m755 "${srcdir}/autopanog" "${pkgdir}/usr/bin/" || return 1

  install -m644 util/man/*.1 "${pkgdir}/usr/share/man/man1/" || return 1
  install -m644 util/man/*.7 "${pkgdir}/usr/share/man/man7/" || return 1

  install -m644 util/autopanog/image-bottom-right.png "${pkgdir}/usr/share/pixmaps/autopano-sift.png" || return 1

  install "${srcdir}/autopano-sift.desktop" "${pkgdir}/usr/share/applications/" || return 1

  rm -r "${MONO_SHARED_DIR}"
}
