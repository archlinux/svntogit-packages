# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdesdk-poxml
pkgver=14.12.3
pkgrel=1
pkgdesc='Translates DocBook XML files using gettext po files'
url='https://projects.kde.org/projects/kde/kdesdk/poxml'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdesdk')
depends=('kdelibs')
makedepends=('cmake' 'automoc4')
optdepends=('antlr2: required for po2xml and swappo tool')
source=("http://download.kde.org/stable/applications/${pkgver}/src/poxml-${pkgver}.tar.xz")
sha1sums=('d842a3b7b1e8717a8fd8506c8af58a71eaa9e5e4')

build() {
  mkdir build
  cd build
  cmake ../poxml-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
