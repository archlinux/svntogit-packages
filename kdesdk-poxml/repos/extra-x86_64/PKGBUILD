# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdesdk-poxml
pkgver=4.12.2
pkgrel=2
pkgdesc='Translates DocBook XML files using gettext po files'
url='https://projects.kde.org/projects/kde/kdesdk/poxml'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdesdk')
depends=('kdelibs')
makedepends=('cmake' 'automoc4' 'antlr2')
optdepends=('antlr2: required for po2xml and swappo tool')
source=("http://download.kde.org/stable/${pkgver}/src/poxml-${pkgver}.tar.xz")
sha1sums=('f0ec74385cf1716e9239aeeec6ecf7b17e21e77a')

build() {
  mkdir build
  cd build
  cmake ../poxml-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
