# $Id: PKGBUILD 295962 2017-05-13 12:58:56Z arojas $
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=xdg-desktop-portal-kde
pkgver=5.10.3
pkgrel=2
pkgdesc='A backend implementation for xdg-desktop-portal using Qt/KF5'
arch=(i686 x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
depends=(ki18n knotifications)
makedepends=(extra-cmake-modules python)
provides=(xdg-desktop-portal-impl)
groups=(plasma)
source=("https://download.kde.org/stable/plasma/${pkgver}/$pkgname-$pkgver.tar.xz"{,.sig} full-libexecdir.patch)
sha256sums=('ed7d2af02ca714251e0db605af57c80ab0b665b5224143ba57ca581782e0cbc2'
            'SKIP'
            'b3a41f1a171f820ba75e252e6b8025b62ec5b5fd4771360d9d6e430ffe2614c9')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell
              '348C8651206633FD983A8FC4DEACEA00075E1D76'  # KDE Neon
              'D07BD8662C56CB291B316EB2F5675605C74E02CF') # David Edmundson

prepare() {
  mkdir -p build

# Fix binary location in service file
  cd $pkgname-$pkgver
  patch -p1 -i ../full-libexecdir.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
