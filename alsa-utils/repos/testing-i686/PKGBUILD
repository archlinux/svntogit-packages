# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=alsa-utils
pkgver=1.1.0
pkgrel=1
pkgdesc="An alternative implementation of Linux sound support"
arch=('i686' 'x86_64')
url="http://www.alsa-project.org"
#can not use >=$pkgver due to use of letters in alsa-lib update versioning
depends=("alsa-lib>1.0.24" 'pciutils' 'ncurses' 'psmisc' 'libsamplerate' 'fftw')
makedepends=('xmlto' 'docbook-xsl')
license=('GPL')
source=(ftp://ftp.alsa-project.org/pub/utils/$pkgname-${pkgver}.tar.bz2
        90alsa)

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr --disable-alsaconf --sbindir=/usr/bin \
    --with-udev-rules-dir=/usr/lib/udev/rules.d \
    --with-systemdsystemunitdir=/usr/lib/systemd/system
  make
  # fix udev rules genaration
  cd alsactl
  make 90-alsa-restore.rules
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  install -D -m755 ../90alsa ${pkgdir}/etc/pm/sleep.d/90alsa

  # install alsa udev rule file
  install -D -m644 alsactl/90-alsa-restore.rules \
          ${pkgdir}/usr/lib/udev/rules.d/90-alsa-restore.rules

  # dir where to save ALSA state
  install -d ${pkgdir}/var/lib/alsa
}
md5sums=('b9d6102fbbd0b68040bb77023ed30c0c'
         '529216f6a46e61a546640e08ea7a0879')
