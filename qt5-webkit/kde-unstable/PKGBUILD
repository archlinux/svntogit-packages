# $Id: PKGBUILD 240297 2015-06-03 10:22:03Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=qt5-webkit
_qtver=5.9.0-beta3
pkgver=${_qtver/-/}
pkgrel=2
arch=('i686' 'x86_64')
url='http://qt-project.org/'
license=('GPL3' 'LGPL3' 'FDL' 'custom')
pkgdesc='Classes for a WebKit2 based implementation and a new QML API'
depends=('qt5-location' 'qt5-sensors' 'qt5-webchannel' 'libwebp' 'libxslt' 'libxcomposite' 'gst-plugins-base')
makedepends=('ruby' 'gperf' 'python' 'qt5-doc' 'qt5-tools')
optdepends=('gst-plugins-good: Webm codec support')
conflicts=('qt')
groups=('qt' 'qt5')
_pkgfqn="${pkgname/5-/}-opensource-src-${_qtver}"
source=("http://download.qt.io/community_releases/${pkgver%.*}/${_qtver}/qtwebkit-opensource-src-${_qtver}.tar.xz"
        qt5-webkit-icu59.patch)
sha256sums=('5f69a6a4f89a9da31b51875033870a2113d99a802d36f7eeb7e97ee161e00a4c'
            '3bc9c7202f5cb055311ffb8a14145cef89060c00002f4cd5b4f4bf210bd56295')

prepare() {
  mkdir -p build

# Fix build with ICU 59 (PLD linux)
  cd ${_pkgfqn}
  patch -p1 -i ../qt5-webkit-icu59.patch
}

build() {
  cd build  

  qmake ../${_pkgfqn}
  make
  make docs
}

package() {
  cd build
  make INSTALL_ROOT="$pkgdir" install install_docs

  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  find "$pkgdir/usr/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt5-base "$pkgdir"/usr/share/licenses/${pkgname}
}
