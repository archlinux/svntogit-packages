# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=kdevelop
pkgver=4.7.1
pkgrel=1
pkgdesc='A C/C++ development environment for KDE'
arch=('i686' 'x86_64')
url='http://www.kdevelop.org/'
license=('GPL')
depends=('kdebase-runtime' 'kdevplatform' 'kdebase-katepart')
makedepends=('cmake' 'automoc4' 'kdesdk-okteta' 'kdebase-workspace')
optdepends=('kdesdk-okteta: hex editor integration'
            'kdebase-konsole: embedded terminal'
            "kdebase-workspace: GDB plugin and 'Attach to process' functionality"
            'git: Git support'
            'subversion: SVN support'
            'cvs: Concurrent Versions System support'
            'gdb: GNU Debugger support'
            'qt5-doc: Qt documentation integration'
            'cmake: cmake integration')
install="${pkgname}.install"
source=("http://download.kde.org/stable/${pkgname}/${pkgver}/src/${pkgname}-${pkgver}.tar.xz")
sha1sums=('9b4cf77b753f6847f10709bb616c55c8e515c53c')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
