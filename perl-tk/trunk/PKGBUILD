# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: François Charette <francois ατ archlinux δοτ org>
# Contributor: Jason Chu <jchu@xentac.net>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

pkgname=perl-tk
pkgver=804.033
pkgrel=1
pkgdesc="A graphical user interface toolkit for Perl"
arch=('i686' 'x86_64')
url="http://search.cpan.org/dist/Tk"
license=('PerlArtistic' 'GPL' 'custom')
depends=('libpng' 'libjpeg' 'tk' 'perl')
checkdepends=('xorg-server-xvfb' 'perl-devel-leak' 'perl-test-pod')
options=('!emptydirs' '!strip')
source=(http://www.cpan.org/authors/id/S/SR/SREZIC/Tk-${pkgver}.tar.gz)
md5sums=('4c6d281ca9c6c2349e5cf84394e638fa')

build() {
  cd Tk-$pkgver

  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Tk-$pkgver
  xvfb-run make test
}

package() {
  cd Tk-$pkgver
  make install DESTDIR="$pkgdir"

  # license
  install -D -m644 pTk/license.terms \
                   "${pkgdir}/usr/share/licenses/${pkgname}/tk.license"
  install -D -m644 pTk/Tix.license \
                   "${pkgdir}/usr/share/licenses/${pkgname}/tix.license"
}
