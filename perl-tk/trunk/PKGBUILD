# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: François Charette <francois ατ archlinux δοτ org>
# Contributor: Jason Chu <jchu@xentac.net>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

pkgname=perl-tk
pkgver=804.032
pkgrel=3
pkgdesc="A graphical user interface toolkit for Perl"
arch=('i686' 'x86_64')
url="http://search.cpan.org/dist/Tk"
license=('PerlArtistic' 'GPL' 'custom')
depends=('libpng' 'libjpeg' 'tk' 'perl')
options=('!emptydirs' '!strip')
source=(http://www.cpan.org/authors/id/S/SR/SREZIC/Tk-${pkgver}.tar.gz
        https://github.com/eserte/perl-tk/commit/ba3a92a779f7adcf655b7e45b40ee5b0cb79bc8b.patch
        https://github.com/eserte/perl-tk/commit/1ca4589ef5a87999ec564081900bc8fdaed83c74.patch)
md5sums=('47a84c70d1fd254ccece208303e6e9d5'
         'e567bd0c209ad54004fa57cfac095f14'
         'f06be3193f64791c134741a6ed2144a9')

build() {
  cd Tk-$pkgver

  # FS#40692
  patch -p1 -i ../1ca4589ef5a87999ec564081900bc8fdaed83c74.patch
  patch -p1 -i ../ba3a92a779f7adcf655b7e45b40ee5b0cb79bc8b.patch

  perl Makefile.PL INSTALLDIRS=vendor
  make
}

#check() {
#  cd Tk-$pkgver
#  make test
#}

package() {
  cd Tk-$pkgver
  make install DESTDIR="$pkgdir"

  # license
  install -D -m644 pTk/license.terms \
                   "${pkgdir}/usr/share/licenses/${pkgname}/tk.license"
  install -D -m644 pTk/Tix.license \
                   "${pkgdir}/usr/share/licenses/${pkgname}/tix.license"
}
