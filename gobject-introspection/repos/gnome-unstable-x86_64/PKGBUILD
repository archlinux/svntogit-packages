# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gobject-introspection
pkgver=1.41.91
pkgrel=1
pkgdesc="Introspection system for GObject-based libraries"
url="https://live.gnome.org/GObjectIntrospection"
arch=('x86_64' 'i686')
license=('LGPL' 'GPL')
depends=('glib2')
makedepends=('cairo' 'python2' 'python2-mako')
optdepends=('python2: gir development tools'
            'python2-mako: g-ir-doc-tool')
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('a1ea708eeaa13dcc5a3078b1ae5aae178f22410023a56c699560f4e068722d9e')

prepare() {
  cd $pkgname-$pkgver
  sed -i '1s|#!/usr/bin/env python$|&2|' giscanner/*.py
}

build() {
  cd $pkgname-$pkgver
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --disable-static --enable-doctool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
