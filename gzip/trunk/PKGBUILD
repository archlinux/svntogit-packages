# $Id$
# Mainainer: SÃ©bastien "Seblu" Luttringer <seblu@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=gzip
pkgver=1.6
pkgrel=1
pkgdesc='GNU compression utility'
arch=('i686' 'x86_64')
url='http://www.gnu.org/software/gzip/'
license=('GPL3')
groups=('base' 'base-devel')
depends=('glibc' 'bash' 'less')
install=gzip.install
validpgpkeys=('155D3FC500C834486D1EEA677FD9FCCB000BEEEE') # Jim Meyering
source=("ftp://ftp.gnu.org/pub/gnu/gzip/gzip-$pkgver.tar.xz"{,.sig})
md5sums=('da981f86677d58a106496e68de6f8995'
         'SKIP')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make prefix="$pkgdir/usr" install
}

# vim:set ts=2 sw=2 et:
