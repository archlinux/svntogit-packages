# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Emanuele Rampichini <emanuele.rampichini@gmail.com>

pkgname=smb4k
pkgver=0.10.73
pkgrel=1
pkgdesc="A KDE program that browses samba shares"
arch=('i686' 'x86_64')
url="http://smb4k.berlios.de/"
license=('GPL')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4' 'docbook-xsl')
install=smb4k.install
source=("http://download.berlios.de/smb4k/${pkgname}-${pkgver}.tar.bz2")
md5sums=('8f161a1b1146dc44217826901deb35c8')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=RELEASE
  make VERBOSE=1
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
}
