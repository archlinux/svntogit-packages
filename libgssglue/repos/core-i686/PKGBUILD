# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgname=libgssglue
pkgver=0.3
pkgrel=1
pkgdesc="Exports a gssapi interface which calls other random gssapi libraries"
arch=('i686' 'x86_64')
url="http://www.citi.umich.edu/projects/nfsv4/linux/"
license=('BSD')
depends=('glibc')
makedepends=('pkgconfig' 'autoconf')
backup=(etc/gssapi_mech.conf)
options=('!libtool')
source=(http://www.citi.umich.edu/projects/nfsv4/linux/$pkgname/$pkgname-$pkgver.tar.gz
        gssapi_mech.conf)
md5sums=('48aaad11b7371ba3270faa322202c038'
         '080be866717e4e06fa6f7d6f43cb395a')

build() {
  cd "$srcdir"/$pkgname-$pkgver
  ./configure --prefix=/usr 
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  make DESTDIR="$pkgdir/" install

  # Configuration
  install -Dm644 "$srcdir"/gssapi_mech.conf "$pkgdir/"etc/gssapi_mech.conf
  # install license
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/libgssglue/COPYING
}
