# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=libgssglue
pkgver=0.5
pkgrel=1
pkgdesc="Library exporting a gssapi interface to call gssapi routines in other libraries"
arch=(x86_64)
url="https://gitlab.com/gsasl/libgssglue/"
license=(BSD)
depends=(glibc)
provides=(libgssglue.so)
backup=(etc/gssapi_mech.conf)
options=(debug)
source=(https://gitlab.com/gsasl/$pkgname/-/archive/$pkgname-$pkgver/$pkgname-$pkgname-$pkgver.tar.gz)
sha512sums=('3e03f2c49d7533bd083a387d35e57ba5ec08dd5206fb1b49b8bebde7d3d60857d0850061a38067a272abe191a2841dcc63700032035d6b17b0cb56a8a3d46bed')
b2sums=('b60a6316dc61b82b462838a8693757708ccaecec43355b1287cee61f742596b4f48bba9156923ea695be7d6098ae7932f54ad51a02df5f5f432a891fd83e8eb2')

prepare() {
  cd $pkgname-$pkgname-$pkgver
  touch ChangeLog
  autoreconf -fiv
}

build() {
  cd $pkgname-$pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  make DESTDIR="$pkgdir/" install -C $pkgname-$pkgname-$pkgver
  install -vDm 644 $pkgname-$pkgname-$pkgver/doc/gssapi_mech.conf -t "$pkgdir/etc/"
  install -vDm 644 $pkgname-$pkgname-$pkgver/COPYING -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 $pkgname-$pkgname-$pkgver/{AUTHORS,README,NEWS} -t "$pkgdir/usr/share/doc/$pkgname/"
}
