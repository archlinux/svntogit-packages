# $Id$
# Maintainer: Tom Gundersen <teg@jklm.no>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=libgssglue
pkgver=0.4
pkgrel=3
pkgdesc="Exports a gssapi interface which calls other random gssapi libraries"
arch=('x86_64')
url="http://www.citi.umich.edu/projects/nfsv4/linux/"
license=('BSD')
depends=('glibc')
makedepends=('pkgconfig' 'autoconf')
backup=(etc/gssapi_mech.conf)
source=(http://www.citi.umich.edu/projects/nfsv4/linux/$pkgname/$pkgname-$pkgver.tar.gz
        gssapi_mech.conf)

build() {
  cd "$srcdir"/$pkgname-$pkgver
  ./configure --prefix=/usr 
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  make DESTDIR="$pkgdir/" install

  # Configuration
  install -Dm644 "$srcdir"/gssapi_mech.conf "$pkgdir/"etc/gssapi_mech.conf
  # install license
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/libgssglue/COPYING
}
sha512sums=('25d514c08320e42851ff153d7691267a8454f205492faf942f566aa30c1ac1c83bd095732a1a0fcc010ba3a5d48b4c95a196ad05bc821598cc1fc3a2c4960d29'
            '28b8e77b61d6c86531b0b04e28bea6df6effd19f92b8f55f7c646b19cee75c1e9bb5132dc475a95dfc237ff0360ae4b117651a99474f15c5c1e710fdbe6f9465')
