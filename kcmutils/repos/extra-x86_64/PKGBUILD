# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kcmutils
pkgver=5.67.0
pkgrel=2
pkgdesc='Utilities for interacting with KCModules'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(kdeclarative)
makedepends=(extra-cmake-modules doxygen qt5-tools qt5-doc)
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
         kdebug-417396.patch::"https://cgit.kde.org/kcmutils.git/patch/?id=ea7120ed")
sha256sums=('ffa61de96a5b11d29b871cd9caf075995236ad3dd432738fb774c97197d9ee8b'
            'SKIP'
            '3de5d1ad17f837fab9a6a0a5c197c4fddc78bd1568d8387051b2147a2167a5f4')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../kdebug-417396.patch # Fix crash in kontact settings
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
