# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=zsh
pkgver=4.3.12
pkgrel=3
pkgdesc='A very advanced and programmable command interpreter (shell) for UNIX'
arch=('i686' 'x86_64')
url='http://www.zsh.org/'
license=('custom')
depends=('pcre' 'libcap' 'gdbm')
install=zsh.install
source=("ftp://ftp.zsh.org/pub/${pkgname}-${pkgver}.tar.bz2" 'modules.patch')
md5sums=('7a82c0bf0635e046ca4e9ec1bdef3811'
         'b367c1398b60242f7044a93a837227f9')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	# upstream patch to fix handling of compressed kernel modules
	patch -p1 -i ${srcdir}/modules.patch
	# FS#16360
	sed -i 's/init.d/rc.d/g' Doc/Zsh/compsys.yo \
				 Doc/zsh.texi \
				 Completion/Unix/Type/_services \
				 Completion/Unix/Command/_init_d
	./configure --prefix=/usr \
		--bindir=/bin \
		--enable-etcdir=/etc/zsh \
		--enable-zshenv=/etc/zsh/zshenv \
		--enable-zlogin=/etc/zsh/zlogin \
		--enable-zlogout=/etc/zsh/zlogout \
		--enable-zprofile=/etc/profile \
		--enable-zshrc=/etc/zsh/zshrc \
		--enable-maildir-support \
		--with-term-lib='ncursesw' \
		--enable-multibyte \
		--enable-function-subdirs \
		--enable-fndir=/usr/share/zsh/functions \
		--enable-scriptdir=/usr/share/zsh/scripts \
		--with-tcsetpgrp \
		--enable-pcre \
		--enable-cap \
		--enable-zsh-secure-free
	make
}

check() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	# This test wont work with the noatime mount option
	rm Test/C02cond.ztst
	HOME="${srcdir}" make check
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}/" install
	install -D -m644 LICENCE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
