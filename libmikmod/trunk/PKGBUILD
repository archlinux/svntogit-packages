# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=libmikmod
pkgver=3.3.4
pkgrel=1
pkgdesc="A portable sound library"
license=(GPL LGPL)
url="http://mikmod.sourceforge.net"
arch=(i686 x86_64)
depends=(openal sh)
makedepends=(alsa-lib)
install=$pkgname.install
source=(http://downloads.sourceforge.net/mikmod/$pkgname-$pkgver.tar.gz)
sha256sums=('2e8ca1d945ff7330741d085548a6d4e8b2f6a9d41edda773607905d8905683ae')

prepare() {
  mkdir build
}

build() {
  cd build
  ../$pkgname-$pkgver/configure \
    --prefix=/usr \
    --enable-openal \
    --disable-static
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}

