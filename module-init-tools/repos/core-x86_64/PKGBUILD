# $Id$
# Maintainer: Aaron Griffin <aaron@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=module-init-tools
pkgver=3.13
pkgrel=1
pkgdesc="utilities needed by Linux systems for managing loadable kernel modules"
arch=('i686' 'x86_64')
url="http://kerneltools.org"
license=('GPL')
depends=('glibc')
backup=('etc/modprobe.d/modprobe.conf')
source=(http://www.kernel.org/pub/linux/utils/kernel/module-init-tools/module-init-tools-$pkgver.tar.bz2
        modprobe.conf)
md5sums=('dc575e7df00d9f745bf23b32f927b7a6'
         '316f1bda4c21af02b30252eb014a0a55')

build() {
  cd $srcdir/$pkgname-$pkgver
  
  # do not regenerate man pages
  touch *.{5,8}
  
  ./configure --prefix=/usr --exec-prefix=/ --enable-zlib
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver

  make DESTDIR=$pkgdir install
 
  # Install our custom (read: empty) modprobe.conf
  install -Dm644 $srcdir/modprobe.conf $pkgdir/etc/modprobe.d/modprobe.conf
  
  # fix man page (FS#17559)
  sed -i "s#mod#man5/mod#" $pkgdir/usr/share/man/man5/modprobe.d.5
}
