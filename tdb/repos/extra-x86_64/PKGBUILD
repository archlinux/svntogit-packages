# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: eric <eric@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=tdb
pkgver=1.3.18
pkgrel=4
pkgdesc="A Trivial Database similar to GDBM but allows simultaneous commits"
arch=(x86_64)
license=('GPL3')
url="https://tdb.samba.org/"
makedepends=('python' 'libxslt' 'docbook-xsl')
optdepends=('python: for python bindings')
source=(https://samba.org/ftp/${pkgname}/${pkgname}-${pkgver}.tar.gz
        https://samba.org/ftp/${pkgname}/${pkgname}-${pkgver}.tar.asc
        samba-update-waf-for-py38.patch::https://gitlab.com/samba-team/samba/commit/aabdcc9151.patch
        wafsamba-use-native-waf-timer.patch)
validpgpkeys=('9147A339719518EE9011BCB54793916113084025') #Samba Library Distribution Key <samba-bugs@samba.org>

prepare() {
   cd ${srcdir}/${pkgname}-${pkgver}

   # https://bugzilla.samba.org/show_bug.cgi?id=13998
   patch -Np1 -i ../wafsamba-use-native-waf-timer.patch

   # https://bugzilla.samba.org/show_bug.cgi?id=13960
   patch -Np1 -i ../samba-update-waf-for-py38.patch
}

build() {
   cd ${srcdir}/${pkgname}-${pkgver}
   ./configure --prefix=/usr \
               --localstatedir=/var \
               --sysconfdir=/etc/samba
   make
}

package() {
   cd ${srcdir}/${pkgname}-${pkgver}
   make DESTDIR=$pkgdir/ install
}
md5sums=('fdb34ed48478a084e9e0c310cc178e87'
         'SKIP'
         '74e627915f28a848e046d37055d86f3b'
         '66185b90c2dcb6bc346ef8ba5200717f')
