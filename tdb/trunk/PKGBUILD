# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: eric <eric@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=tdb
pkgver=1.2.1
pkgrel=2
pkgdesc="A Trivia Database similar to GDBM but allows simultaneous commits"
arch=(i686 x86_64)
license=('GPL3')
url="http://tdb.samba.org/"
source=(http://samba.org/ftp/${pkgname}/${pkgname}-${pkgver}.tar.gz)
makedepends=('python2' 'libxslt' 'docbook-xsl')
optdepends=('python2: for python bindings')
md5sums=('73ea81282a82e5c959d9c082af2d0215')

build() {
   cd ${srcdir}/${pkgname}-${pkgver}
   sed -i 's#python2\.6\([^ ]*\)#python2.7\1 python2.6\1#' configure
   # Use system docbook.xsl
   _manstyle="/usr/share/xml/docbook/xsl-stylesheets-1.76.1/manpages/docbook.xsl"
   sed -i "s#http.*xsl#$_manstyle#" tdb.mk

   ./configure --prefix=/usr \
               --localstatedir=/var \
               --sysconfdir=/etc/samba
   make
}

package() {
   cd ${srcdir}/${pkgname}-${pkgver}
   make DESTDIR=$pkgdir/ install
}
