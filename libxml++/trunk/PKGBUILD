# $Id$
# Maintainer: damir <damir@archlinux.org>

pkgbase=libxml++
pkgname=('libxml++' 'libxml++-docs')
pkgver=2.30.1
pkgrel=1
arch=('i686' 'x86_64')
license=('LGPL')
url="http://libxmlplusplus.sourceforge.net/"
makedepends=('pkgconfig' 'glibmm-docs' 'libxml2>=2.7.7' 'glibmm>=2.24.2')
options=('!libtool' '!emptydirs')
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgbase}/2.30/${pkgbase}-${pkgver}.tar.bz2)
sha256sums=('433f0e36486573a81582d62c83dc741d50bf795ce1a2dca076a54b16c28ab90a')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  ./configure --prefix=/usr || return 1
  make || return 1
}

package_libxml++() {
  pkgdesc="C++ bindings to libxml2"
  depends=('libxml2>=2.7.7' 'glibmm>=2.24.2')
  replaces=('libxml++2')
  provides=("libxml++2=${pkgver}")
  conflicts=('libxml++2')

  cd "${srcdir}/${pkgbase}-${pkgver}"
  sed -i -e 's/install-data-am: install-data-local install-dist_referenceDATA/install-data-am: /' Makefile || return 1
  make DESTDIR="${pkgdir}" install || return 1
}

package_libxml++-docs() {
  pkgdesc="Developer documentation for libxml++"
  depends=('glibmm-docs')
  cd "${srcdir}/${pkgbase}-${pkgver}"
  make DESTDIR="${pkgdir}" install-data-local install-dist_referenceDATA || return 1
}
