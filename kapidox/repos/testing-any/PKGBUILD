# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kapidox
pkgver=5.99.0
pkgrel=1
pkgdesc='Frameworks API Documentation Tools'
arch=(any)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(python-jinja python-yaml)
makedepends=(python-build python-installer python-wheel python-setuptools qt5-base)
groups=(kf5)
source=(https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('0c2d698a883b507f33830e1221a6558e628fb286c3cd030aac8e77507ee92897'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
