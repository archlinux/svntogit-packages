# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kapidox
pkgver=5.4.0
pkgrel=1
pkgdesc='Frameworks API Documentation Tools'
arch=('any')
url='https://projects.kde.org/projects/frameworks/kapidox'
license=('LGPL')
depends=('python-jinja' 'python-yaml')
makedepends=('extra-cmake-modules' 'qt5-base')
groups=('kf5')
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
md5sums=('f56e72770ee8d39d119cf71f71153a9b')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
