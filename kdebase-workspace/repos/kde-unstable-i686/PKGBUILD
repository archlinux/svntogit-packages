# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdebase-workspace
_pkgname=kde-workspace
pkgver=4.8.97
pkgrel=1
pkgdesc="Provides the interface and basic tools for the KDE workspace"
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kde-workspace'
license=('GPL' 'LGPL' 'FDL')
groups=('kde')
# note on libxdamage:
# 	not detected by namcap because libgl depends on it
#	but nvidia providing libgl does not depend on libxdamage
depends=('kdepim-runtime' 'lm_sensors' 'libraw1394' 'libqalculate'
         'qimageblitz' 'polkit-kde' 'consolekit' 'xorg-xprop' 'libxdamage'
         'libxklavier' 'xorg-xsetroot' 'libxcomposite' 'libxinerama'
         'xorg-xrdb' 'libgles' 'libegl' 'libxres' 'xorg-xrandr'
         'xorg-xmessage' 'libusb-compat' 'kde-base-artwork')
makedepends=('cmake' 'automoc4' 'boost' 'kdebindings-python2' 'networkmanager')
optdepends=('kde-wallpapers: wallpapers for KDE Plasma Workspaces')
install="${pkgname}.install"
backup=('usr/share/config/kdm/kdmrc'
        'etc/pam.d/kde'
        'etc/pam.d/kde-np'
        'etc/pam.d/kscreensaver')
options=('emptydirs')
source=("http://download.kde.org/unstable/${pkgver}/src/${_pkgname}-${pkgver}.tar.xz"
        'kdm' 'kde.pam' 'kde-np.pam' 'kscreensaver.pam' 'kdm.service'
        'fixpath.patch' 'terminate-server.patch' 'kdm-xinitrd.patch')
sha1sums=('c02cab54df42b1452c183a1234405cfd6df18be7'
          '5db3a245201bd4a50e65aa2ef583cf5490e4f646'
          '712a90999bd429883dcef5dcaf288aace332ced8'
          'b321b5e613b60231330e606fdf1e124646148388'
          '106635aa1aae51d6f0668b1853f6c49a4fe9d3d8'
          'b6f8e8692737b11eec1f8022ce74b5b23e247b1b'
          'd7b5883f7e65c6839b1f65f94d58026673dd0226'
          'ac7bc292c865bc1ab8c02e6341aa7aeaf1a3eeee'
          'd509dac592bd8b310df27991b208c95b6d907514')

build() {
	cd "${srcdir}"/${_pkgname}-${pkgver}
	patch -p1 -i "${srcdir}"/kdm-xinitrd.patch
	patch -p0 -i "${srcdir}"/fixpath.patch
	patch -p0 -i "${srcdir}"/terminate-server.patch
	
	cd "${srcdir}"
	mkdir build
	cd build
	cmake ../${_pkgname}-${pkgver} \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_SKIP_RPATH=ON \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DWITH_Xmms=OFF \
        -DWITH_Googlegadgets=OFF \
        -DWITH_libgps=OFF \
        -DPYTHON_EXECUTABLE=/usr/bin/python2
	make
}

package() {
	cd "${srcdir}"/build
	make DESTDIR="${pkgdir}" install

	install -D -m644 "${srcdir}"/kde.pam "${pkgdir}"/etc/pam.d/kde
	install -D -m644 "${srcdir}"/kde-np.pam "${pkgdir}"/etc/pam.d/kde-np
	install -D -m644 "${srcdir}"/kscreensaver.pam "${pkgdir}"/etc/pam.d/kscreensaver

	install -d -m755 "${pkgdir}"/usr/share/xsessions/
	ln -sf /usr/share/apps/kdm/sessions/kde-plasma{,-safe}.desktop \
      "${pkgdir}"/usr/share/xsessions/
	install -d -m755 "${pkgdir}"/etc/kde/{env,shutdown}

	install -d -g 135 -o 135 "${pkgdir}"/var/lib/kdm
    install -D -m755 "${srcdir}"/kdm "${pkgdir}"/etc/rc.d/kdm
    install -D -m644 "${srcdir}"/kdm.service \
      "${pkgdir}"/usr/lib/systemd/system/kdm.service
}
