# $Id: $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdebase-workspace
pkgver=4.2.4
pkgrel=1
pkgdesc="KDE Base Workspace"
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('kde')
depends=('kdepimlibs' 'kdebase-runtime' 'libcaptury' 'libxxf86misc' 'libxcomposite' 'libxdamage'
	 'libxss' 'lm_sensors' 'libxklavier' 'qimageblitz' 'xdg-utils' 'consolekit')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'networkmanager' 'bluez' 'kdebindings')
replaces=('kdmtheme' 'kde-common' 'guidance-power-manager')
conflicts=('kde-common')
optdepends=('kdebindings: plasma scriptengine for Python')
options=('docs')
install='kdebase-workspace.install'
backup=('usr/share/config/kdm/kdmrc'
        'etc/pam.d/kde'
        'etc/pam.d/kde-np'
        'etc/pam.d/kscreensaver')
source=("http://download.kde.org/stable/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2"
        'kdm-zsh-profile.patch' 'kdm' 'kde.pam' 'kde-np.pam' 'kscreensaver.pam'
        'fixpath.patch'
        'fixsolidbluetooth.patch')
md5sums=('72f90e1a61d94a5f66c0b3ce39b7b279'
         '721e97031b62aee8914e8617e86f9235'
         '5d80164932e0d44d8b802d3929a004ab'
         '10a490653b002e6f9e7476ff9d37c011'
         '552337fd9a3982d809ea16c7f0033d42'
         '367a3538f54db71f108b34cfa31088ac'
         '47a1f12673f66e62e2463efd7037a26a'
         'd0331f95af08ae65552cb3d4117e8651')

build() {
	cd $srcdir/${pkgname}-${pkgver}
	patch -p0 -i ${srcdir}/kdm-zsh-profile.patch
	patch -p0 -i ${srcdir}/fixpath.patch
	patch -p1 -i ${srcdir}/fixsolidbluetooth.patch

	cd ../
	mkdir build
	cd build
	cmake ../${pkgname}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed' \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DWITH_Xmms=OFF \
		-DWITH_Googlegadgets=OFF \
		-DWITH_QEdje=OFF
	make || return 1
	make DESTDIR=$pkgdir install

	install -D -m755 ${srcdir}/kdm ${pkgdir}/etc/rc.d/kdm
	install -D -m644 ${srcdir}/kde.pam ${pkgdir}/etc/pam.d/kde
	install -D -m644 ${srcdir}/kde-np.pam ${pkgdir}/etc/pam.d/kde-np
	install -D -m644 ${srcdir}/kscreensaver.pam ${pkgdir}/etc/pam.d/kscreensaver
	install -d -m755 ${pkgdir}/etc/X11/sessions/
	ln -sf /usr/share/apps/kdm/sessions/kde{,-safe}.desktop ${pkgdir}/etc/X11/sessions/
	install -d -m755 ${pkgdir}/etc/kde/{env,shutdown}
}
