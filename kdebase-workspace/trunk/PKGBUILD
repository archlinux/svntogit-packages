# $Id: $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdebase-workspace
pkgver=4.1.3
pkgrel=1
pkgdesc="KDE Base Workspace"
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('kde')
depends=('kdelibs' 'kdebase-runtime' 'libcaptury' 'libxxf86misc' 'libxcomposite' 'libxres'
	 'libxss' 'lm_sensors' 'libxklavier' 'qimageblitz' 'xdg-utils' 'libxdamage')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'networkmanager' 'kdemultimedia' 'bluez-libs')
optdepends=('kdemultimedia')
replaces=('kdmtheme' 'kde-common')
conflicts=('kde-common')
options=('docs')
install='kdebase-workspace.install'
backup=('usr/share/config/kdm/kdmrc')
source=("http://download.kde.org/stable/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2"
        'kdm-zsh-profile.patch' 'kdm' 'kde.pam' 'kde-np.pam' 'kscreensaver.pam'
        'agent-startup.sh' 'agent-shutdown.sh')
md5sums=('42233b75ce73eaaacae61c0d5db8befa'
         '721e97031b62aee8914e8617e86f9235'
         '5d80164932e0d44d8b802d3929a004ab'
         '10a490653b002e6f9e7476ff9d37c011'
         '552337fd9a3982d809ea16c7f0033d42'
         '367a3538f54db71f108b34cfa31088ac'
         'c963ef6e37c9051119972a58c67f05fb'
         '18e8d01124b19f7df2937c06e177bf7f')

build() {
	cd $srcdir/${pkgname}-${pkgver}
	patch -p0 -i ${srcdir}/kdm-zsh-profile.patch || return 1
	cd ../
	mkdir build
	cd build
	cmake ../${pkgname}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DWITH_Xmms=OFF
	make
	make DESTDIR=$pkgdir install

	install -D -m755 ${srcdir}/kdm ${pkgdir}/etc/rc.d/kdm
	install -D -m644 ${srcdir}/kde.pam ${pkgdir}/etc/pam.d/kde
	install -D -m644 ${srcdir}/kde-np.pam ${pkgdir}/etc/pam.d/kde-np
	install -D -m644 ${srcdir}/kscreensaver.pam ${pkgdir}/etc/pam.d/kscreensaver
	install -d -m755 ${pkgdir}/etc/X11/sessions/
	ln -sf /usr/share/apps/kdm/sessions/kde{,-safe}.desktop ${pkgdir}/etc/X11/sessions/

	install -m755 -D ${srcdir}/agent-startup.sh ${pkgdir}/usr/env/agent-startup.sh
	install -m755 -D ${srcdir}/agent-shutdown.sh ${pkgdir}/usr/shutdown/agent-shutdown.sh
}
