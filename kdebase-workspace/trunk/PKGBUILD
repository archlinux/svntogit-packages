# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdebase-workspace
pkgver=4.6.3
pkgrel=1
pkgdesc="KDE Base Workspace"
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('kde')
# note on libxdamage:
# 	not detected by namcap because libgl depends on it
#	but nvidia providing libgl does not depend on libxdamage
depends=('kdepim-runtime' 'lm_sensors' 'libraw1394' 'qimageblitz' 'libqalculate'
         'polkit-kde' 'consolekit' 'xorg-xprop' 'libxdamage' 'libxklavier' 'libdmtx'
         'xorg-xsetroot' 'libxcomposite')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'boost' 'networkmanager' 'bluez'
             'python2' 'kdebindings-python')
replaces=('kdmtheme' 'kde-common' 'guidance-power-manager' 'policykit-kde' 'kdebase-kinfocenter')
conflicts=('kde-common' 'guidance-power-manager' 'policykit-kde' 'kdebase-kinfocenter')
install="${pkgname}.install"
backup=('usr/share/config/kdm/kdmrc'
        'etc/pam.d/kde'
        'etc/pam.d/kde-np'
        'etc/pam.d/kscreensaver')
source=("http://download.kde.org/stable/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2"
        'kdm-zsh-profile.patch' 'kdm' 'kde.pam' 'kde-np.pam' 'kscreensaver.pam'
        'fixpath.patch' 'terminate-server.patch' 'nm-09.patch')
sha1sums=('50103b7752defbab08965b665fa95361bb5b3ff6'
          '8c2bdefb23a03b753b78d16944d03fa3939d2d99'
          '5db3a245201bd4a50e65aa2ef583cf5490e4f646'
          'f7b38af38549242a240f1a90ab9964ca8a366129'
          '603cc79c4d2b4eae62bb5f244aeecb3a778b5516'
          '106635aa1aae51d6f0668b1853f6c49a4fe9d3d8'
          'd7b5883f7e65c6839b1f65f94d58026673dd0226'
          'ac7bc292c865bc1ab8c02e6341aa7aeaf1a3eeee'
          '91ed84f5bb99909cab1b5ca0779bbf5cf6c72d33')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}
	patch -p0 -i ${srcdir}/kdm-zsh-profile.patch
	patch -p0 -i ${srcdir}/fixpath.patch
	patch -p0 -i ${srcdir}/terminate-server.patch
    patch -p1 -i ${srcdir}/nm-09.patch

	cd ${srcdir}
	mkdir build
	cd build
	cmake ../${pkgname}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DWITH_Xmms=OFF \
		-DWITH_Googlegadgets=OFF
	make
}

package() {
	cd $srcdir/build
	make DESTDIR=$pkgdir install

	install -D -m755 ${srcdir}/kdm ${pkgdir}/etc/rc.d/kdm
	install -D -m644 ${srcdir}/kde.pam ${pkgdir}/etc/pam.d/kde
	install -D -m644 ${srcdir}/kde-np.pam ${pkgdir}/etc/pam.d/kde-np
	install -D -m644 ${srcdir}/kscreensaver.pam ${pkgdir}/etc/pam.d/kscreensaver
	install -d -m755 ${pkgdir}/usr/share/xsessions/
	ln -sf /usr/share/apps/kdm/sessions/kde-plasma{,-safe}.desktop ${pkgdir}/usr/share/xsessions/
	install -d -m755 ${pkgdir}/etc/kde/{env,shutdown}

	install -d -g 135 -o 135 ${pkgdir}/var/lib/kdm
}
