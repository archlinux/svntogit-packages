# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=figlet
pkgver=2.2.5
pkgrel=3
pkgdesc="A program for making large letters out of ordinary text"
arch=('x86_64')
url="http://www.figlet.org/"
license=('BSD')
depends=('glibc' 'sh')
source=(ftp://ftp.figlet.org/pub/figlet/program/unix/${pkgname}-${pkgver}.tar.gz)
sha512sums=('bb9610fd89a51dd3e65c485653fe1292f47dbca7cb9a05af4ce317f5d776bb346ae455aec07fc37c290f6658639920fd7c431c1135a0f4d456a21c0bd25f99fb')

build() {
  cd ${pkgname}-${pkgver}
  make DEFAULTFONTDIR=/usr/share/figlet/fonts all
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" BINDIR=/usr/bin MANDIR=/usr/share/man \
    DEFAULTFONTDIR=/usr/share/figlet/fonts install 
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
