# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=x11vnc
pkgver=0.9.13
pkgrel=1
pkgdesc='VNC server for real X displays'
arch=('i686' 'x86_64')
url='http://www.karlrunge.com/x11vnc/'
license=('GPL2')
depends=('openssl' 'libjpeg' 'zlib' 'libx11' 'libxtst' 'libxinerama' 'libxdamage' 'libxrandr' 'avahi')
optdepends=('tk: for GUI support')
backup=('etc/conf.d/x11vnc')
source=("http://downloads.sourceforge.net/project/libvncserver/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz"
	'conf.d'
	'rc.d')
sha1sums=('f011d81488ac94dc8dce2d88739c23bd85a976fa'
          '11011cce06511e9ba61672a1c80bb63a3a187539'
          '477892cf0f0020043444b7493c80684762ded506')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr --mandir=/usr/share/man
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	rm -fr "${pkgdir}"/usr/include/rfb # provided by libvncserver

	install -D -m 755 "${srcdir}/rc.d" "${pkgdir}/etc/rc.d/x11vnc"
	install -D -m 644 "${srcdir}/conf.d" "${pkgdir}/etc/conf.d/x11vnc"
}
