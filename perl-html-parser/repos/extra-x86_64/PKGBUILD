# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: kevin <kevin@archlinux.org>
# Contributor: Manolis Tzanidakis
# Contributor: Firmicus <francois.archlinux.org>

pkgname=perl-html-parser
pkgver=3.79
pkgrel=1
pkgdesc="Perl HTML parser class"
arch=('x86_64')
license=('PerlArtistic')
url="https://search.cpan.org/dist/HTML-Parser/"
depends=('perl-html-tagset' 'perl-http-message' 'perl-uri')
options=('!emptydirs')
source=(https://www.cpan.org/authors/id/O/OA/OALDERS/HTML-Parser-$pkgver.tar.gz)
sha512sums=('2c228cb7531902a31db09deb7fd8eb8c2f70a968e80c96664f94e7d9d9a55fe43641c2af8796e46e3082d601ee30bf701dbd63c91c9252bbf1803f8c490edfd8')

build() {
  cd HTML-Parser-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd HTML-Parser-$pkgver
  make test
}

package() {
  cd HTML-Parser-$pkgver
  make install DESTDIR="${pkgdir}"
}
