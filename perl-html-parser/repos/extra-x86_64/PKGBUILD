# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: kevin <kevin@archlinux.org>
# Contributor: Manolis Tzanidakis
# Contributor: Firmicus <francois.archlinux.org>

pkgname=perl-html-parser
pkgver=3.78
pkgrel=1
pkgdesc="Perl HTML parser class"
arch=('x86_64')
license=('PerlArtistic')
url="https://search.cpan.org/dist/HTML-Parser/"
depends=('perl-html-tagset' 'perl-http-message' 'perl-uri')
options=('!emptydirs')
source=(https://www.cpan.org/authors/id/O/OA/OALDERS/HTML-Parser-$pkgver.tar.gz)
sha512sums=('5bbed1324d79f7d677c31eb6e95c51c46742d596c30ec1efbee90c387eebf90b357b0b0cbb3da35326a3a2429bad837f957393bc0316ea1b0170c094467b06d3')

build() {
  cd HTML-Parser-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd HTML-Parser-$pkgver
  make test
}

package() {
  cd HTML-Parser-$pkgver
  make install DESTDIR="${pkgdir}"
}
