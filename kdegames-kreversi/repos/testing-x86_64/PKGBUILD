# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-kreversi
pkgver=4.14.0
pkgrel=1
pkgdesc="A simple one player strategy game played against the computer"
url="http://kde.org/applications/games/kreversi/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'libkdegames')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/kreversi-${pkgver}.tar.xz")
sha1sums=('4333ba353985b95a3351b8de314ca58d7c371108')

build() {
  mkdir build
  cd build
  cmake ../kreversi-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
