# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: dongiovanni <dongiovanni.archlinux.de>

pkgname=grub2
pkgver=1.98
pkgrel=4
pkgdesc="The GNU GRand Unified Bootloader"
url="http://www.gnu.org/software/grub/"
arch=('i686' 'x86_64')
license=('GPL3')
depends=('sh' 'lzo2' 'freetype2')
optdepends=('ruby:usr/share/grub/genmk.rb script' 'libusb: /sbin/grub-emu')
makedepends=('bdf-unifont' 'ruby')
backup=('boot/grub/grub.cfg' 'etc/default/grub' 'etc/grub.d/40_custom')
conflicts=('grub')
provides=('grub')
source=(ftp://alpha.gnu.org/gnu/grub/grub-${pkgver}.tar.gz  
	'archlinux-script-fixes.patch' 'gfxmenu-dependencies.patch'
   	'menucolors-in-mkconfig.patch' 'background-image-in-mkconfig.patch'
	'hidden-timeout-fix.patch' 'grub.default' 'grub.cfg' '20_memtest86+')
md5sums=('c0bcf60e524739bb64e3a2d4e3732a59'         '0910da120700a6928cd0290211acc79f'
         '897f8af2fc83fcdaf7329da39f06d3dc'         '00ae9ff8991b8f00ab90739859af2921'
         'fa14d3feabe17ac11dd3163501a66155'         '81bcdf26aa01b08b9925d72cbc4c3dab'
         'ff99d316b0c59e514e5bb641a392d330'         '743215998a581a54ac77630f0db222ce'
         '5a07e04c4ecb8ed145d54fec3043e0d5')

build() {
  cd $srcdir/grub-${pkgver}

  # some random patches to facilitate automatic creation of grub.cfg
  patch -Np1 -i ${srcdir}/archlinux-script-fixes.patch || return 1
  patch -Np1 -i ${srcdir}/gfxmenu-dependencies.patch || return 1
  patch -Np1 -i ${srcdir}/menucolors-in-mkconfig.patch || return 1
  patch -Np1 -i ${srcdir}/background-image-in-mkconfig.patch || return 1
  patch -Np1 -i ${srcdir}/hidden-timeout-fix.patch || return 1

  # run autogen.sh to create configure files
  ./autogen.sh

  # fix unifont.bdf location
  sed -i 's|/usr/src/unifont.bdf|/usr/share/fonts/misc/unifont.bdf|' configure || return 1

  CFLAGS= ./configure --prefix=/usr --enable-grub-mkfont --bindir=/bin \
		--sbindir=/sbin --mandir=/usr/share/man \
		--infodir=/usr/share/info --sysconfdir=/etc

  CFLAGS= make || return 1
}

package() {
  cd $srcdir/grub-${pkgver}
  make DESTDIR=${pkgdir} install || return 1

  # install /etc/default/grub
  install -Dm644 ${srcdir}/grub.default ${pkgdir}/etc/default/grub

  # install grub.cfg (needed so it doesn't get removed on upgrading because it was previously here)
  install -Dm644 ${srcdir}/grub.cfg ${pkgdir}/boot/grub/grub.cfg

  # install memtest config detection
  install -Dm755 ${srcdir}/20_memtest86+ ${pkgdir}/etc/grub.d/20_memtest86+
}
