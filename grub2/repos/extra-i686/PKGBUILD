# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Keshav P R <(the.ridikulus.rat) (aatt) (gemmaeiil) (ddoott) (ccoomm)>

_grub_bzr_rev="4232"
_pkgver="2.00~beta6"

_grub_lua_ver=24
_grub_gpxe_ver=12
_grub_ntldr_ver=21
_grub_915_ver=9

pkgname=('grub2-common' 'grub2-bios' 'grub2-efi-i386')
pkgbase=grub2
pkgver=2.00beta6
pkgrel=1
url="http://www.gnu.org/software/grub/"
arch=('i686' 'x86_64')
license=('GPL3')
epoch="1"
makedepends=('rsync' 'xz' 'bdf-unifont' 'python' 'autogen' 'texinfo'
	'help2man' 'gettext' 'device-mapper' 'fuse' 'dosfstools' 'efibootmgr')

# source=("ftp://ftp.archlinux.org/other/grub2/grub_r${_grub_bzr_rev}.tar.xz"
source=("http://alpha.gnu.org/gnu/grub/grub-${_pkgver}".tar.xz
        "ftp://ftp.archlinux.org/other/grub2/grub2_extras_lua_r${_grub_lua_ver}.tar.xz"
        "ftp://ftp.archlinux.org/other/grub2/grub2_extras_gpxe_r${_grub_gpxe_ver}.tar.xz"
        "ftp://ftp.archlinux.org/other/grub2/grub2_extras_ntldr-img_r${_grub_ntldr_ver}.tar.xz"
        "ftp://ftp.archlinux.org/other/grub2/grub2_extras_915resolution_r${_grub_915_ver}.tar.xz"
        'archlinux_grub_mkconfig_fixes.patch'
        'grub.default'
        'grub.cfg'
        '20_memtest86+')

noextract=("grub2_extras_lua_r${_grub_lua_ver}.tar.xz"
           "grub2_extras_gpxe_r${_grub_gpxe_ver}.tar.xz"
           "grub2_extras_ntldr-img_r${_grub_ntldr_ver}.tar.xz"
           "grub2_extras_915resolution_r${_grub_915_ver}.tar.xz")

sha1sums=('0ba5ccf6e6761911f9bf236e21f8f77a2d29ebdb'
          '89290031b974780c6df76893836d2477d4add895'
          'b04994c005910b4bf123f034b30109d5c8d6fd86'
          'eb4b35b4c36b64f9405cbcbc538cb205171c1c0a'
          'd5ae2efec25616028a9d89e98b6e454f1c4c415f'
          '26e4e946190bea1f03632658cf08ba90e11dec57'
          'a76b251b110c9c2d49d2340e4ae932db13ad75bd'
          'fd422806c87905f9cabe1f0dc13e544e5fc09e53'
          'ce35d7ae75cd1b5b677e894e528f96add40e77b9')

_build_grub2-common_and_bios() {

	## copy the source for building the common/bios package
	cp -r "${srcdir}/grub-${_pkgver}" "${srcdir}/grub_bios-${_pkgver}"
	cd "${srcdir}/grub_bios-${_pkgver}"

	## Apply Archlinux specific fixes to enable grub-mkconfig detect Arch kernels and initramfs
	patch -Np1 -i "${srcdir}/archlinux_grub_mkconfig_fixes.patch"
	echo

	## add the grub-extra sources
	export GRUB_CONTRIB="${srcdir}/grub_bios-${_pkgver}/grub-extras/"
	install -d "${srcdir}/grub_bios-${_pkgver}/grub-extras"

	bsdtar xf "${srcdir}/grub2_extras_lua_r${_grub_lua_ver}.tar.xz" \
		-C "${srcdir}/grub_bios-${_pkgver}/grub-extras"

	bsdtar xf "${srcdir}/grub2_extras_gpxe_r${_grub_gpxe_ver}.tar.xz" \
		-C "${srcdir}/grub_bios-${_pkgver}/grub-extras"

	bsdtar xf "${srcdir}/grub2_extras_ntldr-img_r${_grub_ntldr_ver}.tar.xz" \
		-C "${srcdir}/grub_bios-${_pkgver}/grub-extras"

	bsdtar xf "${srcdir}/grub2_extras_915resolution_r${_grub_915_ver}.tar.xz" \
		-C "${srcdir}/grub_bios-${_pkgver}/grub-extras"

	## Requires python2
	# sed 's|python |python2 |g' -i "${srcdir}/grub_bios-${_pkgver}/autogen.sh"

	## start the actual build process
	cd "${srcdir}/grub_bios-${_pkgver}"
	./autogen.sh

	## fix unifont.bdf location so grub-mkfont can create *.pf2 files
	sed 's|/usr/share/fonts/unifont|/usr/share/fonts/misc|g' -i "${srcdir}/grub_bios-${_pkgver}/configure"

	# install -d "${srcdir}/grub_bios-${_pkgver}/BUILD_BIOS"
	# cd "${srcdir}/grub_bios-${_pkgver}/BUILD_BIOS"

	CFLAGS="" ./configure \
		--with-platform="pc" \
		--target="i386" \
		--host="${CARCH}-unknown-linux-gnu" \
		"${_EFIEMU}" \
		--enable-mm-debug \
		--enable-nls \
		--enable-device-mapper \
		--enable-cache-stats \
		--enable-grub-mkfont \
		--enable-grub-mount \
		--prefix="/usr" \
		--bindir="/usr/bin" \
		--sbindir="/usr/sbin" \
		--mandir="/usr/share/man" \
		--infodir="/usr/share/info" \
		--datarootdir="/usr/share" \
		--sysconfdir="/etc" \
		--program-prefix="" \
		--with-bootdir="/boot" \
		--with-grubdir="grub" \
		--disable-werror

	CFLAGS="" make

}

_build_grub2-efi-i386() {

	## copy the source for building the efi package
	cp -r "${srcdir}/grub-${_pkgver}" "${srcdir}/grub_efi-${_pkgver}"
	cd "${srcdir}/grub_efi-${_pkgver}"

	export GRUB_CONTRIB="${srcdir}/grub_efi-${_pkgver}/grub-extras/"
	install -d "${srcdir}/grub_efi-${_pkgver}/grub-extras"

	bsdtar xf "${srcdir}/grub2_extras_lua_r${_grub_lua_ver}.tar.xz" \
		-C "${srcdir}/grub_efi-${_pkgver}/grub-extras"

	bsdtar xf "${srcdir}/grub2_extras_gpxe_r${_grub_gpxe_ver}.tar.xz" \
		-C "${srcdir}/grub_efi-${_pkgver}/grub-extras"

	## Requires python2
	# sed 's|python |python2 |g' -i "${srcdir}/grub_efi-${_pkgver}/autogen.sh"

	cd "${srcdir}/grub_efi-${_pkgver}"
	./autogen.sh

	CFLAGS="" ./configure \
		--with-platform="efi" \
		--target="i386" \
		--host="${CARCH}-unknown-linux-gnu" \
		--disable-efiemu \
		--enable-mm-debug \
		--enable-nls \
		--enable-device-mapper \
		--enable-cache-stats \
		--enable-grub-mkfont \
		--enable-grub-mount \
		--prefix="/usr" \
		--bindir="/usr/bin" \
		--sbindir="/usr/sbin" \
		--mandir="/usr/share/man" \
		--infodir="/usr/share/info" \
		--datarootdir="/usr/share" \
		--sysconfdir="/etc" \
		--program-prefix="" \
		--with-bootdir="/boot" \
		--with-grubdir="grub" \
		--disable-werror

	CFLAGS="" make
}

#_get_locale_files() {
#
#	cd "${srcdir}/grub-${_pkgver}"
#	rsync -Lrtvz translationproject.org::tp/latest/grub/ "${srcdir}/grub-${_pkgver}/po" || true
#	(cd "${srcdir}/grub-${_pkgver}/po" && ls *.po | cut -d. -f1 | xargs) > "${srcdir}/grub-${_pkgver}/po/LINGUAS"
#}

build() {

	## set architecture dependent variables
	if [[ "${CARCH}" == 'x86_64' ]]; then
		_EFIEMU="--enable-efiemu"
	else
		_EFIEMU="--disable-efiemu"
	fi

	_HOST="${CARCH}"

	cd "${srcdir}/grub-${_pkgver}"
	# _get_locale_files

	_build_grub2-common_and_bios
	_build_grub2-efi-i386

}

package_grub2-common() {

	pkgdesc="The GNU GRand Unified Bootloader version 2 - Files common for all platforms"
	depends=('xz' 'freetype2' 'gettext' 'device-mapper' 'fuse' 'sh')
	conflicts=('grub')
	backup=('boot/grub/grub.cfg' 'etc/default/grub' 'etc/grub.d/40_custom')
	optdepends=('libisoburn: provides xorriso for generating grub rescue iso using grub-mkrescue'
	            'os-prober: to detect other OSes when generating grub.cfg in BIOS systems'
	            'mtools: for grub-mkrescue FAT FS support')
	install="grub2.install"
	options=('strip' 'purge' 'docs' 'zipman' '!emptydirs')

	cd "${srcdir}/grub_bios-${_pkgver}"
	make bashcompletiondir=/usr/share/bash-completion/completions DESTDIR="${pkgdir}/" install

	## install extra /etc/grub.d/ files
	install -D -m0755 "${srcdir}/20_memtest86+" "${pkgdir}/etc/grub.d/20_memtest86+"

	## install /etc/default/grub (used by grub-mkconfig)
	install -D -m0644 "${srcdir}/grub.default" "${pkgdir}/etc/default/grub"

	## install grub.cfg (needed so it doesn't get removed on upgrading because it was previously here)
	install -D -m0644 "${srcdir}/grub.cfg" "${pkgdir}/boot/grub/grub.cfg"

	# remove platform specific files
	rm -rf "${pkgdir}/usr/lib/grub/i386-pc/"

}

package_grub2-bios() {

	pkgdesc="The GNU GRand Unified Bootloader version 2 - Built for i386 PC BIOS"
	depends=("grub2-common=${epoch}:${pkgver}")
	options=('!strip' '!emptydirs')

	cd "${srcdir}/grub_bios-${_pkgver}"
	make DESTDIR="${pkgdir}/" install

	## remove non platform-specific files
	rm -rf "${pkgdir}"/{boot,etc,usr/{share,bin,sbin}}

	## remove gdb debugging related files
	rm -f "${pkgdir}/usr/lib/grub/i386-pc"/*.module || true
	rm -f "${pkgdir}/usr/lib/grub/i386-pc"/*.image || true
	rm -f "${pkgdir}/usr/lib/grub/i386-pc"/{kernel.exec,gdb_grub,gmodule.pl} || true

}

package_grub2-efi-i386() {

	pkgdesc="The GNU GRand Unified Bootloader version 2 - i386 UEFI version"
	depends=("grub2-common=${epoch}:${pkgver}" 'dosfstools' 'efibootmgr')
	options=('!strip' '!emptydirs')

	cd "${srcdir}/grub_efi-${_pkgver}"
	make DESTDIR="${pkgdir}/" install

	## remove non platform-specific files
	rm -rf "${pkgdir}"/{boot,etc,usr/{share,bin,sbin}}

	## remove gdb debugging related files
	rm -f "${pkgdir}/usr/lib/grub/i386-efi"/*.module || true
	rm -f "${pkgdir}/usr/lib/grub/i386-efi"/*.image || true
	rm -f "${pkgdir}/usr/lib/grub/i386-efi"/{kernel.exec,gdb_grub,gmodule.pl} || true

}
