# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=poppler
pkgver=0.8.4
_dataver=0.2.0
pkgrel=1
pkgdesc="PDF rendering library based on xpdf 3.0"
arch=(i686 x86_64)
license=('GPL' 'custom')
depends=('libjpeg' 'gcc-libs>=4.3.1' 'cairo>=1.6.4' 'libxml2>=2.6.32' 'fontconfig>=2.6.0')
makedepends=('pkgconfig')
options=('!libtool')
url="http://poppler.freedesktop.org/"
source=(http://poppler.freedesktop.org/${pkgname}-${pkgver}.tar.gz
	http://poppler.freedesktop.org/poppler-data-${_dataver}.tar.gz
	poppler-0.6.1-cairoheaders.patch
	bug16579.patch)
md5sums=('29d341f295d01bff64c63a29a6abb274'
         'b7f98e84a4d2a2c794271d746ec7ee0b'
         'ce8f07aaca7abfafdca604582a9f8398'
	 '3d8f2457a2c70bcc81809541481d67f7')

build() {
  cd ${startdir}/src/${pkgname}-${pkgver}
  patch -Np1 -i ${startdir}/src/poppler-0.6.1-cairoheaders.patch || return 1
  patch -Np1 -i ${startdir}/src/bug16579.patch || return 1
  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var --disable-static \
	      --enable-cairo-output \
	      --enable-xpdf-headers \
	      --enable-libjpeg --enable-zlib \
	      --disable-poppler-qt4 \
	      --disable-poppler-glib \
	      --disable-poppler-qt \
	      --disable-gtk-test
  make || return 1
  make DESTDIR=${startdir}/pkg install

  cd ${startdir}/src/poppler-data-${_dataver}
  make prefix=/usr DESTDIR=${startdir}/pkg install

  mkdir -p ${startdir}/pkg/usr/share/licenses/${pkgname}
  install -m644 COPYING ${startdir}/pkg/usr/share/licenses/${pkgname}/
}
