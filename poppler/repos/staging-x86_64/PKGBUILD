# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=poppler
pkgname=('poppler' 'poppler-glib' 'poppler-qt4' 'poppler-qt5')
pkgver=0.26.0
pkgrel=1
arch=(i686 x86_64)
license=('GPL')
makedepends=('libjpeg' 'gcc-libs' 'cairo' 'fontconfig' 'openjpeg' 'gtk2' 'qt4' 'pkgconfig' 'lcms2' 'gobject-introspection' 'icu' 'qt5-base')
options=('!emptydirs')
url="http://poppler.freedesktop.org/"
_testtag=0d2bfd4af4c76a3bac27ccaff793d9129df7b57a
source=(http://poppler.freedesktop.org/${pkgbase}-${pkgver}.tar.xz
        http://cgit.freedesktop.org/poppler/test/snapshot/test-${_testtag}.tar.bz2
        poppler-0.24.2-mocversiongrep.patch)
md5sums=('76c3b2f18002a0f1e2bbbc7cccd1b957'
         '9dc64c254a31e570507bdd4ad4ba629a'
         'd5280e4eb6d02660971bdaa45065933f')

prepare() {
  ln -sf test-${_testtag} test
  cd ${pkgbase}-${pkgver}
  # fix build with recent qt versions, patch taken from FC
#  patch -Np1 -i ${srcdir}/poppler-0.24.2-mocversiongrep.patch
#  sed -i -e '/AC_PATH_XTRA/d' configure.ac
#  sed -i "s:AM_CONFIG_HEADER:AC_CONFIG_HEADERS:" configure.ac
#  autoreconf -fi
}

build() {
  cd ${pkgbase}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --enable-cairo-output \
      --enable-xpdf-headers \
      --enable-libjpeg --enable-zlib \
      --enable-poppler-qt4 \
      --enable-poppler-qt5 \
      --enable-poppler-glib
  make
}

check() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  LANG=en_US.UTF8 make check
}

package_poppler() {
  pkgdesc="PDF rendering library based on xpdf 3.0"
  depends=('libjpeg' 'gcc-libs' 'cairo' 'fontconfig' 'openjpeg' 'lcms2')
  optdepends=('poppler-data: encoding data to display PDF documents containing CJK characters')
  conflicts=("poppler-qt3<${pkgver}")

  cd "${srcdir}/${pkgbase}-${pkgver}"
  sed -e 's/^glib_subdir =.*/glib_subdir =/' \
      -e 's/^qt4_subdir =.*/qt4_subdir =/' \
      -e 's/^qt5_subdir =.*/qt5_subdir =/' -i Makefile
  make DESTDIR="${pkgdir}" install

  rm -f "${pkgdir}"/usr/lib/pkgconfig/poppler-{glib,qt4,qt5}.pc
}

package_poppler-glib() {
  pkgdesc="Poppler glib bindings"
  depends=("poppler=${pkgver}" 'glib2')

  cd "${pkgbase}-${pkgver}"
  make -C poppler DESTDIR="${pkgdir}" install-libLTLIBRARIES
  make -C glib DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/lib/pkgconfig"
  install -m644 poppler-glib.pc "${pkgdir}/usr/lib/pkgconfig/"
  rm -f "${pkgdir}"/usr/lib/libpoppler.*
  rm -f "${pkgdir}/usr/bin/poppler-glib-demo"
}

package_poppler-qt4() {
  pkgdesc="Poppler Qt4 bindings"
  provides=("poppler-qt=${pkgver}")
  replaces=('poppler-qt')
  conflicts=('poppler-qt')
  depends=("poppler=${pkgver}" 'qt4')

  cd "${pkgbase}-${pkgver}"
  make -C poppler DESTDIR="${pkgdir}" install-libLTLIBRARIES
  make -C qt4 DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/lib/pkgconfig"
  install -m644 poppler-qt4.pc "${pkgdir}/usr/lib/pkgconfig/"
  rm -f "${pkgdir}"/usr/lib/libpoppler.*
}

package_poppler-qt5() {
  pkgdesc="Poppler Qt5 bindings"
  depends=("poppler=${pkgver}" 'qt5-base')

  cd "${pkgbase}-${pkgver}"
  make -C poppler DESTDIR="${pkgdir}" install-libLTLIBRARIES
  make -C qt5 DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/lib/pkgconfig"
  install -m644 poppler-qt5.pc "${pkgdir}/usr/lib/pkgconfig/"
  rm -f "${pkgdir}"/usr/lib/libpoppler.*
}
