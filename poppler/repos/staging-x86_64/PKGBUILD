# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=poppler
pkgname=('poppler' 'poppler-glib' 'poppler-qt4' 'poppler-qt5')
pkgver=0.61.0
pkgrel=2
arch=(x86_64)
license=('GPL')
makedepends=('libjpeg' 'gcc-libs' 'cairo' 'fontconfig' 'openjpeg2' 'gtk3' 'qt4' 'pkgconfig' 'lcms2' 
             'gobject-introspection' 'icu' 'qt5-base' 'git' 'nss' 'gtk-doc' 'curl' 'poppler-data'
             'cmake' 'python2') # python2 just for make-glib-api-docs/gtkdoc.py
options=('!emptydirs')
url="https://poppler.freedesktop.org/"
source=(https://poppler.freedesktop.org/${pkgbase}-${pkgver}.tar.xz
        fix-incorrect-paths-in-pc-files.patch
        test::git+https://anongit.freedesktop.org/git/poppler/test/#commit=0d2bfd4)
sha256sums=('53cde17a2afa3b73eb8b209d24e4369b52bfac444065dbb0a8cbcc7356582b7f'
            'da402576a7eb6cf07596d76ebadbac9f6aadc69d7234630904a5577fc818c3c1'
            'SKIP')

prepare() {
  mkdir build
  cd ${pkgbase}-${pkgver}

  sed -i -e "s:python:python2:" make-glib-api-docs

  # https://bugs.freedesktop.org/show_bug.cgi?id=103578
  patch -Np1 -i ../fix-incorrect-paths-in-pc-files.patch
}

build() {
  cd build
  cmake ../${pkgbase}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX:PATH=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DENABLE_XPDF_HEADERS=ON \
    -DENABLE_GTK_DOC=ON
  make
}

check() {
  cd build
  LANG=en_US.UTF8 make test
}

package_poppler() {
  pkgdesc="PDF rendering library based on xpdf 3.0"
  depends=('libjpeg' 'gcc-libs' 'cairo' 'fontconfig' 'openjpeg2' 'lcms2' 'nss' 'curl')
  optdepends=('poppler-data: encoding data to display PDF documents containing CJK characters')
  conflicts=("poppler-qt3<${pkgver}")

  cd build
  make DESTDIR="${pkgdir}" install

  # cleanup for splitted build
  rm -vrf "${pkgdir}"/usr/include/poppler/{glib,qt4,qt5}
  rm -vf "${pkgdir}"//usr/lib/libpoppler-{glib,qt4,qt5}.*
  rm -vf "${pkgdir}"/usr/lib/pkgconfig/poppler-{glib,qt4,qt5}.pc
  rm -vrf "${pkgdir}"/usr/{lib,share}/gir*
  rm -vrf "${pkgdir}"/usr/share/gtk-doc
}

package_poppler-glib() {
  pkgdesc="Poppler glib bindings"
  depends=("poppler=${pkgver}" 'glib2')

  cd build

  make -C glib DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/lib/pkgconfig"
  install -m644 poppler-glib.pc "${pkgdir}/usr/lib/pkgconfig/"
  rm -vf "${pkgdir}"/usr/lib/libpoppler.*
  rm -vf "${pkgdir}/usr/bin/poppler-glib-demo"
}

package_poppler-qt4() {
  pkgdesc="Poppler Qt4 bindings"
  provides=("poppler-qt=${pkgver}")
  replaces=('poppler-qt')
  conflicts=('poppler-qt')
  depends=("poppler=${pkgver}" 'qt4')

  cd build
  make -C qt4 DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/lib/pkgconfig"
  install -m644 poppler-qt4.pc "${pkgdir}/usr/lib/pkgconfig/"
}

package_poppler-qt5() {
  pkgdesc="Poppler Qt5 bindings"
  depends=("poppler=${pkgver}" 'qt5-base')

  cd build
  make -C qt5 DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/lib/pkgconfig"
  install -m644 poppler-qt5.pc "${pkgdir}/usr/lib/pkgconfig/"
}

