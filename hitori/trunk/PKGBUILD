# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=hitori
pkgver=3.22.4
pkgrel=1
pkgdesc="GTK+ application to generate and let you play games of Hitori"
arch=(x86_64)
url="https://wiki.gnome.org/Apps/Hitori"
license=(GPL)
depends=(gtk3)
groups=(gnome-extra)
makedepends=(itstool yelp-tools autoconf-archive appstream-glib git)
_commit=4515af09f31b09e4d16b8b11b7b660d2f81bb351  # tags/3.22.4^0
source=("git+https://git.gnome.org/browse/hitori#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --enable-compile-warnings=minimum
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
