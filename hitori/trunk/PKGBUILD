# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=hitori
pkgver=3.38.4
pkgrel=1
pkgdesc="GTK+ application to generate and let you play games of Hitori"
arch=(x86_64)
url="https://wiki.gnome.org/Apps/Hitori"
license=(GPL)
depends=(gtk3)
makedepends=(yelp-tools appstream-glib git meson)
groups=(gnome-extra)
_commit=2498eff83d8cfdeb19eb455ae0c72b49ca6bd375  # tags/3.38.4^0
source=("git+https://gitlab.gnome.org/GNOME/hitori.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd hitori
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd hitori
}

build() {
  arch-meson hitori build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
