# $Id$
# Maintainer: Kevin Piche <kevin@archlinux.org>
# Contributor: Jason Chu <jchu@xentac.net>

pkgname=mahjong
pkgver=1.12.1
pkgrel=1
pkgdesc="The classical game of Mah Jong (not the solitare version) with network support"
arch=('i686' 'x86_64')
url="http://www.stevens-bradfield.com/MahJong/"
license=('GPL' 'custom')
depends=('gtk2')
source=(http://mahjong.julianbradfield.org/Source/mj-$pkgver-src.tar.gz)
md5sums=('a74acae39d6a49bad9c34b84088f72f3')

build() {
  cd "$srcdir/mj-$pkgver-src"
  sed -i 's/& Calling/Calling/' gui-dial.c
  make LDLIBS="-lm"
}

package() {
  cd "$srcdir/mj-$pkgver-src"
  make DESTDIR="$pkgdir/usr/" MANDIR=share/man/man1 install install.man
  install -D -m644 tiles-v1/README "$pkgdir/usr/share/licenses/$pkgname/tiles-license.txt"
}
