# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kmix
pkgver=20.08.0
pkgrel=2
pkgdesc="KDE volume control program"
url='https://kde.org/applications/multimedia/kmix/'
arch=(x86_64)
license=(GPL LGPL FDL)
depends=(knotifications kxmlgui solid hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools plasma-framework)
groups=(kde-applications kde-multimedia)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
         kdebug-425469.patch::"https://invent.kde.org/multimedia/kmix/-/commit/b3fe6379.patch")
sha256sums=('4553362d4d14fdaf019b1fc55b98aac16c2db56542996a3c2f87603c41bc2859'
            'SKIP'
            '917d6ff6d1cc94853600bacf65365c02a2523448f50e5642372ad3c912fffb02')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  patch -d $pkgname-$pkgver -p1 -i ../kdebug-425469.patch # Fix crash
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
