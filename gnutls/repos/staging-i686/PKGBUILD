# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnutls
pkgver=3.1.3
pkgrel=2
pkgdesc="A library which provides a secure layer over a reliable transport layer"
arch=('i686' 'x86_64')
license=('GPL3' 'LGPL')
url="http://www.gnu.org/software/gnutls/"
install=gnutls.install
options=('!libtool' '!zipman')
depends=('gcc-libs>=4.7.1-5' 'libtasn1>=3.0' 'readline' 'zlib' 'nettle>=2.4' 'p11-kit>=0.12')
makedepends=('valgrind' 'strace')
source=(http://ftp.gnu.org/gnu/gnutls/${pkgname}-${pkgver}.tar.xz{,.sig})
md5sums=('8a7b47456f65ffa8c01c58461fed1388'
         'ecd7bfe70612f88afd117124e18444df')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
	--with-zlib \
	--disable-static \
	--disable-guile \
	--disable-valgrind-tests
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make -k check # passes all  || /bin/true
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  
  # lots of .png files are put into infodir and are gzipped by makepkg! this may need to be fixed by using !zipman
  # gzip -9 all files in infodir and manpages manually
  find "$pkgdir/usr/share/info" -name '*.info*' -exec gzip -9 {} \;
  find "$pkgdir/usr/share/man" -exec gzip -9 {} \;
}
