# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnutls
pkgver=3.0.2
pkgrel=1
pkgdesc="A library which provides a secure layer over a reliable transport layer"
arch=('i686' 'x86_64')
license=('GPL3' 'LGPL')
url="http://www.gnu.org/software/gnutls/"
install=gnutls.install
options=('!libtool' '!zipman')
depends=('gcc-libs' 'libtasn1' 'readline' 'zlib' 'nettle>=2.2')
makedepends=('valgrind')
source=(ftp://ftp.gnu.org/gnu/gnutls/${pkgname}-${pkgver}.tar.xz)
md5sums=('1f8c3b74b2c6faa9b08f84a422f93863')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
	--with-zlib \
	--disable-static \
	--disable-guile \
	--without-p11-kit \
	--disable-valgrind-tests # breaks make check. errors have been reported upstream
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check # passes all  || /bin/true
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  # lots of .png files are put into infodir and are gzipped by makepkg! this may need to be fixed by using !zipman
  # gzip -9 all files in infodir and manpages manually
  find "$pkgdir/usr/share/info" -name '*.info*' -exec gzip -9 {} \;
  find "$pkgdir/usr/share/man" -exec gzip -9 {} \;
}
