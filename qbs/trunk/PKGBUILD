# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Donald Carr <sirspudd@gmail.com>
# Contributor: Jake Petroules <jake.petroules@qt.io>

pkgname=qbs
pkgver=1.10.1
pkgrel=1
pkgdesc='Cross platform build tool'
arch=(x86_64)
url='https://wiki.qt.io/Qbs'
license=(LGPL)
depends=(qt5-script)
source=($pkgname-$pkgver.tar.gz::"https://github.com/qbs/qbs/archive/v$pkgver.tar.gz")
sha256sums=('1bb3ab572eb3eac139375c5f416a04ef36a713419276f79f0cb8042e44c33cc7')

build() {
  cd $pkgname-$pkgver
  qmake QBS_INSTALL_PREFIX=/usr \
    QBS_LIBEXEC_INSTALL_DIR=/usr/lib/qbs \
    QBS_RELATIVE_LIBEXEC_PATH=../lib/qbs \
    CONFIG+=qbs_enable_project_file_updates \
    qbs.pro
  make
}

package() {
  cd $pkgname-$pkgver
  INSTALL_ROOT="$pkgdir" make install
}
