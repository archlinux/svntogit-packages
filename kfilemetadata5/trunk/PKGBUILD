# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kfilemetadata5
pkgver=5.1.2
pkgrel=1
pkgdesc="A library for extracting file metadata"
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kdelibs/kfilemetadata'
license=('LGPL')
depends=('ki18n' 'karchive' 'exiv2' 'poppler-qt5' 'taglib' 'ffmpeg' 'ebook-tools')
makedepends=('extra-cmake-modules' 'python2')
conflicts=('kfilemetadata-frameworks')
replaces=('kfilemetadata-frameworks')
source=("http://download.kde.org/stable/plasma/${pkgver}/kfilemetadata-$pkgver.tar.xz")
md5sums=('994b8a39da1877778f06ac054f5573a6')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kfilemetadata-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
