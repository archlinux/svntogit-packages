# $Id$
# Maintainer: Tom Gundersen <teg@jklm.no>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=libbluedevil
pkgver=2.0rc1
_pkgver=2.0-rc1
pkgrel=2
epoch=1
pkgdesc='A Qt4 wrapper for bluez used in the new KDE bluetooth stack'
arch=('i686' 'x86_64')
url="https://projects.kde.org/projects/playground/libs/libbluedevil"
license=('GPL')
depends=('qt4' 'bluez')
makedepends=('cmake')
source=("http://download.kde.org/unstable/${pkgname}/${_pkgver}/src/${pkgname}-${_pkgver}.tar.xz")
md5sums=('3883a20334ed210fb46077ebea163c0a')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../${pkgname}-${_pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DQT_QMAKE_EXECUTABLE=/usr/bin/qmake-qt4
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
