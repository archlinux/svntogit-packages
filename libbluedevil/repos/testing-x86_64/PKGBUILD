# $Id$
# Maintainer: Tom Gundersen <teg@jklm.no>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=libbluedevil
pkgver=1.9.3
pkgrel=2
pkgdesc='A Qt4 wrapper for bluez used in the new KDE bluetooth stack'
arch=('i686' 'x86_64')
url="https://projects.kde.org/projects/playground/libs/libbluedevil"
license=('GPL')
depends=('qt4' 'bluez4')
makedepends=('cmake')
source=("http://download.kde.org/stable/${pkgname}/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2")
md5sums=('fcb1480feba45dcd076b3ee54150bdf5')

build() {
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
