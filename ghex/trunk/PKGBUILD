# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>

pkgname=ghex
pkgver=3.41.1
pkgrel=1
pkgdesc="A simple binary editor for the Gnome desktop"
url="https://wiki.gnome.org/Apps/Ghex"
arch=(x86_64)
license=(GPL)
depends=(gtk3)
makedepends=(git meson yelp-tools)
groups=(gnome-extra)
_commit=2bd5cb649e40de5e3b197ce7666ee9bd14ae2c24  # tags/3.41.1^0
source=("git+https://gitlab.gnome.org/GNOME/ghex.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
