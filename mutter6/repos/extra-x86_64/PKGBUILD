# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Michael Kanis <mkanis_at_gmx_dot_de>

pkgname=mutter6
pkgver=3.36.8
pkgrel=2
pkgdesc='A window manager for GNOME'
url=https://gitlab.gnome.org/GNOME/mutter
arch=(x86_64)
license=(GPL2)
depends=(
  dconf
  gnome-desktop
  gnome-settings-daemon
  gobject-introspection-runtime
  gsettings-desktop-schemas
  libcanberra
  libgraphene-1.0.so
  libgudev
  libinput
  libsm
  libxkbcommon-x11
  libxkbfile
  pipewire
  startup-notification
  upower
  xorg-server-xwayland
  zenity
)
makedepends=(
  egl-wayland
  git
  gobject-introspection
  meson
  xorg-server
)
checkdepends=(xorg-server-xvfb)
provides=(libmutter-6.so)
_tag=a24048bebb41abf48c3615a945717978f5b7f669
source=(git+https://gitlab.gnome.org/GNOME/mutter.git#tag=${_tag})
sha256sums=('SKIP')

pkgver() {
  cd mutter

  git describe --tags
}

build() {
  CFLAGS="${CFLAGS/-O2/-O3} -fno-semantic-interposition"
  LDFLAGS+=" -Wl,-Bsymbolic-functions"
  arch-meson mutter build \
    --libexecdir /usr/lib/mutter-6 \
    -D egl_device=true \
    -D installed_tests=false \
    -D profiler=false \
    -D wayland_eglstream=true \
    -D xwayland_initfd=disabled
  meson compile -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
  rm -rf "${pkgdir}"/usr/{bin,share}
}

# vim: ts=2 sw=2 et:
