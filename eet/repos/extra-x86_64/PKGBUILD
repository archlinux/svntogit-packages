# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Ronald van Haren <ronald.archlinux.org>

pkgname=eet
pkgver=1.7.2
pkgrel=1
pkgdesc="A data storage and compression library"
arch=('i686' 'x86_64')
url="http://www.enlightenment.org"
license=('BSD')
depends=('libjpeg>=7' 'openssl' 'eina')
options=(!libtool)
source=(http://download.enlightenment.org/releases/$pkgname-$pkgver.tar.gz)
sha1sums=('8e768421107e48ae55839ba0e3d36440f74e0bf6')

build() {
   cd $srcdir/$pkgname-$pkgver

  export CFLAGS="-fvisibility=hidden -ffunction-sections -fdata-sections ${CFLAGS}"
  export CXXFLAGS="-fvisibility=hidden -ffunction-sections -fdata-sections ${CXXFLAGS}"
  export LDFLAGS="-fvisibility=hidden -ffunction-sections -fdata-sections -Wl,--gc-sections -Wl,--as-needed  ${LDFLAGS}"

  ./configure --prefix=/usr --enable-openssl --disable-gnutls \
	--disable-build-examples \
	--disable-doc --disable-install-examples \
	--disable-old-eet-file-format --disable-tests \
	--disable-static
  make
}

package(){
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install 

  # install license files
  install -Dm644 $srcdir/$pkgname-$pkgver/COPYING \
        $pkgdir/usr/share/licenses/$pkgname/COPYING
}

