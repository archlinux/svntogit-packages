# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ndctl
pkgver=66
pkgrel=1
pkgdesc='Utility library for managing the libnvdimm (non-volatile memory device) sub-system in the Linux kernel'
arch=('x86_64')
url="https://github.com/pmem/ndctl"
license=('LGPL')
depends=('json-c' 'kmod' 'systemd-libs' 'libutil-linux')
makedepends=('asciidoctor' 'xmlto' 'bash-completion')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pmem/ndctl/archive/v$pkgver.tar.gz")
sha512sums=('3647905b585a68975b28d61f26b0b486468a90a7eea31ef7fd874f9848671dd72db2ad6d85f5268b63988bfffab2c0be569d668dccf95bbdf01fad55300cc0c8')

prepare() {
  # fix location of modprobe.d conf
  sed -i 's|^modprobedir =.*|modprobedir = /usr/lib/modprobe.d/|' $pkgname-$pkgver/Makefile.am
}

build() {
  cd "$srcdir"/$pkgname-$pkgver
  ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
