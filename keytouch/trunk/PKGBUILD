# $Id$
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: mouse256 & AndyRTR
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=keytouch
pkgver=2.4.1
pkgrel=2
pkgdesc="A program which allows you to easily configure the extra function keys of your keyboard"
arch=('i686' 'x86_64')
url="http://keytouch.sourceforge.net/"
license=('GPL')
depends=('libxtst' 'gnome-menus' 'alsa-lib')
makedepends=('pkgconfig')
options=('!makeflags')
install=keytouch.install
source=(http://downloads.sourceforge.net/sourceforge/keytouch/${pkgname}-${pkgver}.tar.gz \
        keytouch.daemon keytouch.desktop Xsession)
md5sums=('c3a917ae9666c9649d43d9aa09ecc96a' 'a35776aee9361577220f718be81c2b4d'\
         'c5ffe28988dc74fae812ffe204f8883b' '795430001f4fdb6691d0e93b09a241a9')
sha1sums=('f1f26dc95c21afd404aebe2b1066e75c8313ca80' 'af23f4526cb1f3e5b9a3680af6a24d1566dabb7d'\
         '5bac2f62f1ac26caa32da02a8075dc090d981cfb' '960b2fa51cfdeb2bc3fa7e086dc9f3a3be483dff')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}
	./configure --prefix=/usr --sysconfdir=/etc || return 1
	make || return 1

	cd keytouch-config
	./configure --prefix=/usr --sysconfdir=/etc || return 1
	make || return 1

	cd ../keytouch-keyboard
	./configure --prefix=/usr --sysconfdir=/etc || return 1
	make || return 1
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}
	install -d ${pkgdir}/etc/{rc.d,X11/Xsession.d} || return 1
	install -d ${pkgdir}/usr/share/keytouch || return 1
	make DESTDIR=${pkgdir} install || return 1

	cd keytouch-config
	make DESTDIR=${pkgdir} install || return 1

	cd ../keytouch-keyboard
  	make DESTDIR=${pkgdir} install || return 1

	install -Dm755 ${srcdir}/keytouch.daemon \
	    ${pkgdir}/etc/rc.d/keytouch || return 1
	install -Dm644 ${srcdir}/keytouch.desktop \
	    ${pkgdir}/usr/share/applications/keytouch.desktop || return 1
	install -Dm755 ${srcdir}/Xsession \
	    ${pkgdir}/etc/X11/Xsession || return 1
	chmod 755  ${pkgdir}/etc/X11/Xsession.d/* || return 1
	rm ${pkgdir}/etc/rc.d/*.sh
}
