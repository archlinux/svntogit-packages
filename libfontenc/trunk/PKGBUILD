#Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libfontenc
pkgver=1.1.4
pkgrel=1
pkgdesc="X11 font encoding library"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('zlib')
makedepends=('pkgconfig' 'xorgproto')
source=(${url}/releases/individual/lib/${pkgname}-${pkgver}.tar.bz2{,.sig})
sha512sums=('76fa851d00113241f15fdd5b5bb7e927b8d8b9a82ce3fcb0678b8c7e32cb5e8d898c4bda1d60dafb7748145049d3240627ac34a2360c64e98a7a912ea7c30582'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') #Alan Coopersmith <alan.coopersmith@oracle.com>

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --with-encodingsdir=/usr/share/fonts/encodings      
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
