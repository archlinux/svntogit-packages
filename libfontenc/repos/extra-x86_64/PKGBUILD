# $Id$
#Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libfontenc
pkgver=1.1.3
pkgrel=1
pkgdesc="X11 font encoding library"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('zlib')
makedepends=('pkgconfig' 'xproto>=7.0.18')
source=(${url}/releases/individual/lib/${pkgname}-${pkgver}.tar.bz2{,.sig})
sha256sums=('70588930e6fc9542ff38e0884778fbc6e6febf21adbab92fd8f524fe60aefd21'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') #Alan Coopersmith <alan.coopersmith@oracle.com>

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --with-encodingsdir=/usr/share/fonts/encodings      
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
