#Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libxvmc
pkgver=1.0.10
pkgrel=2
pkgdesc="X11 Video Motion Compensation extension library"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libxv')
makedepends=('pkgconfig')
source=(${url}/releases/individual/lib/libXvMC-${pkgver}.tar.bz2{,.sig})
sha256sums=('e501a079b5dfaef0897c56152770c77e05e362065cec58910289aa567277ee2e'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') #  Alan Coopersmith <alan.coopersmith@oracle.com>
validpgpkeys+=('C41C985FDCF1E5364576638B687393EE37D128F8') # Matthieu Herrb <matthieu.herrb@laas.fr>

build() {
  cd ${srcdir}/libXvMC-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static
  make
}

package() {
  cd ${srcdir}/libXvMC-${pkgver}
  make DESTDIR=${pkgdir} install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
