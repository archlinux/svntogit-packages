# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gedit
pkgver=3.22.0+4+g2c70ccb86
pkgrel=1
pkgdesc="A text editor for GNOME"
arch=(i686 x86_64)
license=(GPL)
depends=(gtksourceview3 gsettings-desktop-schemas libpeas gspell python-gobject dconf)
makedepends=(yelp-tools intltool vala gobject-introspection gnome-common git gtk-doc)
optdepends=('gedit-plugins: Additional features')
groups=(gnome-extra)
options=('!emptydirs')
url="http://www.gnome.org"
_commit=2c70ccb86ff7d20de5eb87e988f10d891178442b  # master
source=("git://git.gnome.org/gedit#commit=$_commit"
        "git://git.gnome.org/libgd")
sha256sums=('SKIP'
            'SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr \
      --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/usr/lib --disable-updater --disable-schemas-compile \
      --enable-python --enable-gtk-doc

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package(){
  cd $pkgname
  make -j1 DESTDIR="$pkgdir" install
}
