# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gedit
pkgver=2.28.0
pkgrel=1
pkgdesc="A text editor for GNOME"
arch=(i686 x86_64)
license=('GPL')
depends=('gconf>=2.27.0' 'pygtksourceview2>=2.8.0' 'enchant>=1.4.2' 'desktop-file-utils' 'iso-codes>=3.10.1' 'gtksourceview2>=2.8.0' 'libsm')
makedepends=('pkgconfig' 'gnome-doc-utils>=0.17.5' 'intltool')
groups=('gnome-extra')
options=('!libtool' '!emptydirs')
url="http://www.gnome.org"
install=gedit.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.28/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('f12699367ec4f48ffce87f8b116b55561a03e055c4bf74cc96e5adb3b4e2fce6')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/usr/lib --disable-scrollkeeper || return 1
  make || return 1
  make -j1 GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1
 
  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gedit ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
