# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gedit
pkgver=3.12.0
pkgrel=1
pkgdesc="A text editor for GNOME"
arch=(i686 x86_64)
license=(GPL)
depends=(gtksourceview3 gsettings-desktop-schemas libpeas enchant iso-codes desktop-file-utils python-gobject dconf)
makedepends=(yelp-tools intltool zeitgeist vala)
optdepends=('zeitgeist: Zeitgeist plugin')
groups=(gnome-extra)
options=('!emptydirs')
url="http://www.gnome.org"
install=gedit.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('6028d535d50c85017739b84e7fe94ad5a1940064bb315375d5d1d6ed99eb38c2')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
      --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/usr/lib --disable-updater --disable-schemas-compile \
      --enable-python
  make
}

package(){
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
