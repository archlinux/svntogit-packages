# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=messagelib
pkgver=17.12.3
pkgrel=1
pkgdesc="KDE PIM messaging library"
arch=(x86_64)
url="https://community.kde.org/KDE_PIM"
license=(LGPL)
depends=(kdepim-apps-libs kmailtransport libgravatar kidentitymanagement kmbox qt5-webengine)
makedepends=(extra-cmake-modules python boost)
conflicts=('libkdepim<16')
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
        messagelib-qt-5.11.patch::"https://cgit.kde.org/messagelib.git/patch/?id=38d83230")
sha256sums=('8d2ab084b55342585e85358a4837c9c4613b40cef4ec4f5de6494d899affadde'
            'SKIP'
            'af50d2d7bfdaee085e814025cb6d3cba119a69a72f952ba4edd5efd9eeda3fc2')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

# Fix displaying messages with Qt 5.11
  cd $pkgname-$pkgver
  patch -p1 -i ../messagelib-qt-5.11.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
