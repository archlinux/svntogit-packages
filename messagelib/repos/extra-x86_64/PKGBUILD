# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=messagelib
pkgver=19.12.2
pkgrel=1
pkgdesc="KDE PIM messaging library"
arch=(x86_64)
url="https://kontact.kde.org"
license=(LGPL)
depends=(kdepim-apps-libs kmailtransport libgravatar kidentitymanagement kmbox qca)
makedepends=(extra-cmake-modules boost)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('0dddb4b7fd9759fa6984d57ab1ff10b5c6a253f42a92bb15bbbf5a48fa403f7c'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
