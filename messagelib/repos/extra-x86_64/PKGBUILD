# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=messagelib
pkgver=20.04.1
pkgrel=2
pkgdesc="KDE PIM messaging library"
arch=(x86_64)
url="https://kontact.kde.org"
license=(LGPL)
depends=(kdepim-apps-libs kmailtransport libgravatar kidentitymanagement kmbox qca)
makedepends=(extra-cmake-modules boost)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
         messagelib-qt5.15.patch::"https://invent.kde.org/pim/messagelib/-/commit/1f2548805d.patch")
sha256sums=('6142a47750b2aa8ef68b27855c9f78bbce45a5aaa2c4c49214fac2254c22780f'
            'SKIP'
            '182a1b840ae7d6792afc51f8c85711666c5c12544f229784efb4b924daa90b17')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  patch -d $pkgname-$pkgver -p1 -i ../messagelib-qt5.15.patch # Fix header display with Qt 5.15
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
