# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=messagelib
pkgver=16.08.0
pkgrel=1
pkgdesc="KDE PIM messaging library"
arch=(i686 x86_64)
url="https://community.kde.org/KDE_PIM"
license=(LGPL)
depends=(kdepim-apps-libs kmailtransport libgravatar akonadi-notes kidentitymanagement qt5-webkit)
makedepends=(extra-cmake-modules kdoctools python boost qt5-tools kdesignerplugin)
conflicts=('libkdepim<16')
source=("http://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
md5sums=('90871c20a7f31527d8699baf929b8012')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
