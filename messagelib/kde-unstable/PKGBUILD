# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=messagelib
pkgver=18.07.80
pkgrel=1
pkgdesc="KDE PIM messaging library"
arch=(x86_64)
url="https://community.kde.org/KDE_PIM"
license=(LGPL)
depends=(kdepim-apps-libs kmailtransport libgravatar kidentitymanagement kmbox qt5-webengine)
makedepends=(extra-cmake-modules boost)
conflicts=('libkdepim<16')
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('1006af389915c9104b13dd6f54f5a939a4c3f467ca0e50845343dbd0fdedd8e4'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
