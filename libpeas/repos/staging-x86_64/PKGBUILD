# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=libpeas
pkgver=1.24.0
pkgrel=2
pkgdesc="A GObject plugins library"
arch=(x86_64)
url="https://wiki.gnome.org/Projects/Libpeas"
license=(GPL2)
depends=(gtk3 gobject-introspection-runtime)
makedepends=(gtk-doc python-gobject glade gobject-introspection git meson vala)
checkdepends=(xorg-server-xvfb)
optdepends=('python-gobject: Python loader')
_commit=f9962a516ead8ce20531d1b23e1b656caecd9274  # tags/libpeas-1.24.0^0
source=("git+https://gitlab.gnome.org/GNOME/libpeas.git#commit=$_commit")
sha256sums=('SKIP')


pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^libpeas-//;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build -D vapi=true -D gtk_doc=true
  ninja -C build
}

check() {
  xvfb-run meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}

# vim:set ts=2 sw=2 et:
