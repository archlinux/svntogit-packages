# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kalendar
pkgver=22.12.0
pkgrel=2
pkgdesc='A calendar application using Akonadi to sync with external services'
arch=(x86_64)
url='https://apps.kde.org/kalendar/'
license=(GPL)
depends=(kdepim-runtime mailcommon eventviews qqc2-desktop-style kirigami-addons)
makedepends=(extra-cmake-modules)
groups=(kde-applications kde-pim)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('49a25f4a5ae2d04b9b1feb7d51affe9fcbf922d019fcc801dd54c400c9d59ed1'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
options=(debug)

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
