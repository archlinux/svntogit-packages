# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-chess
pkgver=42.1
pkgrel=1
pkgdesc="Play the classic two-player boardgame of chess"
url="https://wiki.gnome.org/Apps/Chess"
arch=(x86_64)
license=(GPL)
depends=(gtk4 libadwaita librsvg)
makedepends=(gobject-introspection yelp-tools vala git meson) 
optdepends=('gnuchess: Play against computer')
groups=(gnome-extra)
options=(debug)
_commit=5c0a47498eb4651adbee150ab144d7959658a75d  # tags/42.1^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-chess.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
