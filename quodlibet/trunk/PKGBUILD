# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=quodlibet
pkgver=2.4.1
pkgrel=1
pkgdesc="An audio player written in pygtk"
arch=('i686' 'x86_64')
license=('GPL2')
url="http://code.google.com/p/quodlibet/"
depends=('gstreamer0.10-python' 'gstreamer0.10-base-plugins' 'gstreamer0.10-good-plugins' 
         'gstreamer0.10-ugly-plugins' 'mutagen' 'pygtk')
makedepends=('intltool')
optdepends=('gstreamer0.10-ffmpeg: for ffmpeg (ASF/WMA) support ' 
            'gstreamer0.10-bad-plugins: for MPEG-4 (AAC) and Musepack support' 
            'python2-dbus: for dbus support' 
            'notification-daemon: for notification support' 
            'libgpod: for ipod support' 
            'python2-feedparser: for audio feeds (podcast) support' 
            'udisks: for media devices support' 
            'media-player-info: for media devices support')
options=('!makeflags')
source=(http://quodlibet.googlecode.com/files/${pkgname}-${pkgver}.tar.gz)
sha1sums=('4db812f0b447805a27ae8f25517623205712b75e')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i 's#env python#env python2#' *.py quodlibet/util/fmps.py
  sed -i 's#libudev.so.0#libudev.so.1#' quodlibet/devices/__init__.py
  ./setup.py build
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./setup.py install --prefix="${pkgdir}/usr"
  install -D -m644 quodlibet/images/hicolor/64x64/apps/exfalso.png "${pkgdir}/usr/share/pixmaps/exfalso.png"
  install -D -m644 quodlibet/images/hicolor/64x64/apps/quodlibet.png "${pkgdir}/usr/share/pixmaps/quodlibet.png"
}
