# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=quodlibet
pkgver=3.2.2
pkgrel=1
pkgdesc="An audio library tagger, manager and player"
arch=('any')
license=('GPL2')
url="http://code.google.com/p/quodlibet/"
depends=('gtk3' 'python2-gobject' 'python2-dbus' 'mutagen' 
         'gst-plugins-base' 'gst-plugins-good' 'gst-plugins-ugly' 
	 'gnome-icon-theme')
makedepends=('intltool')
optdepends=('gst-libav: for ffmpeg (ASF/WMA) support'
            'gst-plugins-bad: for Musepack support'
            'libgpod: for ipod support'
            'python2-feedparser: for audio feeds (podcast) support'
            'libkeybinder3: for the multimedia keys support'
            'media-player-info: for media devices support'
            'cddb-py: for "CDDB Lookup" plugin'
            'python2-musicbrainz2: for "MusicBrainz Lookup" plugin'
            'python2-pyinotify: for "Automatic library update" plugin'
	    'kakasi: for "Kana/Kanji Simple Inverter" plugin'
	    'zeitgeist: for "Event Logging" plugin'
            'gst-plugins-bad: for "Audio Pitch/Speed" plugin')
provides=('quodlibet-plugins')
conflicts=('quodlibet-plugins')
replaces=('quodlibet-plugins')
options=('!makeflags')
install=quodlibet.install
source=(https://bitbucket.org/lazka/quodlibet-files/raw/default/releases/${pkgname}-${pkgver}.tar.gz)
sha1sums=('bad265479b39aefdbb2f52376808502ba8d11eec')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -i 's/env python/env python2/' quodlibet/ext/songsmenu/replaygain.py
}

build() {
  cd ${pkgname}-${pkgver}
  python2 setup.py build
}

package() {
  cd ${pkgname}-${pkgver}
  python2 setup.py install --root="${pkgdir}"
}
