# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=libcloudproviders
pkgver=0.2.5
pkgrel=2
pkgdesc="DBus API that allows cloud storage sync clients to expose their services"
url="https://gitlab.gnome.org/External/libcloudproviders"
arch=(x86_64)
license=(LGPL3)
depends=(glib2)
makedepends=(git meson gtk-doc)
_commit=6fc78d1ac63400a098037d14984eaadba5ad6526  # tags/0.2.5^0
source=("git+https://gitlab.gnome.org/External/libcloudproviders.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build -Denable-gtk-doc=true
  ninja -C build
}

check() {
  cd build
  meson test
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}
