# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Fina Wilke <fina@felinira.net>

pkgname=libcloudproviders
pkgver=0.3.1+r8+g3a229ee
pkgrel=1
pkgdesc="DBus API that allows cloud storage sync clients to expose their services"
url="https://gitlab.gnome.org/World/libcloudproviders"
arch=(x86_64)
license=(LGPL3)
depends=(glib2)
makedepends=(git gobject-introspection meson vala gtk-doc)
_commit=3a229eee7dbdf7159fafab2f797e90fd89fdd05c  # master
source=("git+https://gitlab.gnome.org/World/libcloudproviders.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build -D enable-gtk-doc=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
