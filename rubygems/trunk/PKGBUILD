# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>

pkgname=rubygems
pkgdesc='Package management framework for Ruby'
pkgver=3.0.2
pkgrel=1
arch=(any)
url='https://rubygems.org/'
license=(MIT)
# To avoid circular dependency with ruby, move it to makedepends
makedepends=(ruby)
backup=(etc/gemrc)
checkdepends=(rake)
source=(rubygems-$pkgver.zip::https://github.com/rubygems/rubygems/archive/v${pkgver}.zip
        gemrc)
sha512sums=('8e233d28707927e4a9c729d51f1f85899aae0d8c4ff524eaeba5bdfec23ff1278df058220449a0acd159d5ba9a99f6df28ed7a852e1911a66e435557f4809454'
            '8cafd14d414ee3c16aa94f79072bc6c100262f925dc1300e785846c3fabbbbffc1356b8e2223af5684e3340c55032d41231179ffa948bb12e01dbae0f4131911')

check() {
  cd rubygems-${pkgver}

  # disable as it requires a bund of ruby dependencies
  #rake test
}

package() {
  depends+=(ruby)
  cd rubygems-${pkgver}

  install -D -m644 ${srcdir}/gemrc "${pkgdir}/etc/gemrc"

  local rubyver=$(ruby --version | sed -r 's|^ruby ([[:digit:]]+\.[[:digit:]]+\.).*$|\1|')0

  install -d "${pkgdir}/usr/lib/ruby/${rubyver}/"
  install -D -m755 bin/gem "${pkgdir}/usr/bin/gem"
  cp -r lib/* "${pkgdir}/usr/lib/ruby/${rubyver}/"
  install -D -m644 MIT.txt "${pkgdir}/usr/share/licenses/rubygems/MIT.txt"
}
