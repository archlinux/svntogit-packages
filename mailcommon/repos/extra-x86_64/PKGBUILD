# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=mailcommon
pkgver=21.12.3
pkgrel=2
pkgdesc='KDE PIM library providing support for mail applications'
arch=(x86_64)
url='https://kontact.kde.org'
license=(LGPL)
depends=(mailimporter messagelib phonon-qt5)
makedepends=(extra-cmake-modules boost qt5-tools doxygen)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('4e8dcb2ece18dfca8e219838d85d8e402cb353e6a2ca8fa9d79bbcb4e1fc28fe'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
options=(debug)

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
