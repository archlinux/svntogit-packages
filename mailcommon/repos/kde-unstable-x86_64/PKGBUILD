# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=mailcommon
pkgver=20.11.90
pkgrel=1
pkgdesc="KDE PIM library providing support for mail applications"
arch=(x86_64)
url="https://kontact.kde.org"
license=(LGPL)
depends=(mailimporter messagelib phonon-qt5)
makedepends=(extra-cmake-modules boost qt5-tools)
source=("https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('6782c48dc7bd636a6e786a6d804e543d67c469b86a80f77301f5c8cb7997db24'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
