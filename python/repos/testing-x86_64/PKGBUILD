# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributer: Jason Chu <jason@archlinux.org>

pkgname=python
pkgver=2.6
pkgrel=1
_pybasever=2.6
pkgdesc="A high-level scripting language"
arch=('i686' 'x86_64')
license=('custom')
url="http://www.python.org"
depends=('db' 'bzip2' 'gdbm' 'openssl>=0.9.8d' 'zlib')
makedepends=('tk>=8.5.0' 'sqlite3')
optdepends=('tk: for IDLE, pynche and modulator')
provides=('python-elementtree' 'python-ctypes')
replaces=('python-elementtree' 'python-ctypes')
source=(http://www.python.org/ftp/python/${pkgver}/Python-${pkgver}.tar.bz2
	modulator-launcher
	pynche-launcher)
md5sums=('1b3c31fc54be2250ed18eb089b4d2779'
         'faa0d4b947bfe67fc8684b5c464f46f2'
         '60a44af1d8af276274e20c564c0f3d8b')

build() {
  cd ${startdir}/src/Python-${pkgver}

  ./configure --prefix=/usr --enable-shared --with-threads --enable-unicode
  
  # temporary backwards compatibility stuff...
  sed -i 's#SITEPATH=#SITEPATH=:/usr/lib/python2.5/site-packages#' Makefile

  make || return 1
  make DESTDIR=${pkgdir} install

  rm ${pkgdir}/usr/bin/python
  ln -sf python${_pybasever} ${pkgdir}/usr/bin/python
  ln -sf python${_pybasever} ${pkgdir}/usr/bin/python2

  # Install the tools
  #modulator
  install -m755 ${srcdir}/modulator-launcher ${pkgdir}/usr/bin/modulator
  cp -r Tools/modulator ${pkgdir}/usr/lib/python${_pybasever}/site-packages/

  #pynche
  install -m755 ${srcdir}/pynche-launcher ${pkgdir}/usr/bin/pynche
  rm -f Tools/pynche/*.pyw
  cp -r Tools/pynche ${pkgdir}/usr/lib/python${_pybasever}/site-packages/

  #gettext
  install -m755 Tools/i18n/pygettext.py ${pkgdir}/usr/bin/
  install -m755 Tools/i18n/msgfmt.py ${pkgdir}/usr/bin/

  mkdir -p ${pkgdir}/usr/lib/python${_pybasever}/Tools/scripts
  install Tools/scripts/README ${pkgdir}/usr/lib/python${_pybasever}/Tools/scripts/
  install Tools/scripts/*py ${pkgdir}/usr/lib/python${_pybasever}/Tools/scripts/

  # license
  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
