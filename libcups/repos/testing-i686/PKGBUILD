# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=libcups
pkgver=1.3.11
pkgrel=1
pkgdesc="The CUPS Printing System"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.cups.org/"
depends=('gnutls>=2.8.1' 'libtiff>=3.8.2-5' 'libpng>=1.2.37' 'heimdal>=1.2.1-5') 
makedepends=('poppler>=0.10.7-2' 'autoconf')
source=(ftp://ftp.easysw.com/pub/cups/${pkgver}/cups-${pkgver}-source.tar.bz2
	gnutls-pkgconfig.dpatch)
md5sums=('17f3e2bcb3cae3dd9dceb65a2bfd295f'
         '7322c46c2f33863dce85eb75a1d9ccab')

build() {
  cd ${srcdir}/cups-${pkgver}
  # http://patch-tracking.debian.net/patch/series/view/cups/1.3.10-5/gnutls-pkgconfig.dpatch
  patch -Np1 -i ${srcdir}/gnutls-pkgconfig.dpatch || return 1
  aclocal
  autoconf
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
     --with-logdir=/var/log/cups -with-docdir=/usr/share/cups/doc \
     --with-cups-user=daemon --with-cups-group=lp --without-php \
     --enable-pam=yes --disable-ldap --libdir=/usr/lib --enable-raw-printing \
     --enable-dbus --with-dbusdir=/etc/dbus-1 --enable-ssl=yes --enable-gnutls \
     --enable-pdftops --with-pdftops=pdftops --with-optim="$CFLAGS"

  for dir in cups filter; do
    pushd ${dir}
    make
    make BUILDROOT=${pkgdir} install
    popd
  done

  mkdir -p ${startdir}/pkg/usr/bin
  install -m755 cups-config ${startdir}/pkg/usr/bin/cups-config
  #Remove filters
  rm -rf ${pkgdir}/usr/lib/cups
}
