# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=libcups
pkgver=1.3.9
pkgrel=1
pkgdesc="The CUPS Printing System"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.cups.org/"
depends=('gnutls>=2.4.1' 'libtiff' 'libpng' 'heimdal>=1.2')
source=(ftp://ftp.easysw.com/pub/cups/${pkgver}/cups-${pkgver}-source.tar.bz2)
md5sums=('cf63f451c356e6cabb08972d4d11c365')

build() {
  cd ${srcdir}/cups-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
     --with-logdir=/var/log/cups -with-docdir=/usr/share/cups/doc \
     --with-cups-user=daemon --with-cups-group=lp --enable-ssl=yes \
     --disable-ldap --disable-dbus --libdir=/usr/lib

  for dir in cups filter; do
    pushd ${dir}
    make
    make BUILDROOT=${pkgdir} install
    popd
  done

  mkdir -p ${startdir}/pkg/usr/bin
  install -m755 cups-config ${startdir}/pkg/usr/bin/cups-config
  #Remove filters
  rm -rf ${startdir}/pkg/usr/lib/cups
}
