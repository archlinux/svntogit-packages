# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeutils-printer-applet
pkgver=4.8.0
pkgrel=1
pkgdesc='System tray icon for managing print jobs'
url='http://kde.org/applications/system/printerapplet/'
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
depends=('kdebase-runtime' 'kdebindings-python' 'system-config-printer-common')
makedepends=('cmake' 'automoc4')
groups=('kde' 'kdeutils')
source=("http://download.kde.org/stable/${pkgver}/src/printer-applet-${pkgver}.tar.bz2")
sha1sums=('6d6f6afb89ac43c6f1c9393592d8645480530662')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../printer-applet-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DPYTHON_EXECUTABLE=/usr/bin/python2
  make
}

package() {
  cd $srcdir/build
  make DESTDIR=$pkgdir install
  cd $srcdir/build/doc
  make DESTDIR=$pkgdir install

  # Fix python 2 path
  sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|' \
    "${pkgdir}"/usr/share/apps/printer-applet/{authconn,debug,monitor,printer-applet,statereason}.py
}
