# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=bomber
pkgver=15.04.2
pkgrel=1
pkgdesc="A single player arcade game"
url="http://kde.org/applications/games/bomber/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegames')
depends=('libkdegames')
makedepends=('extra-cmake-modules' 'python' 'kdoctools')
install=${pkgname}.install
conflicts=('kdegames-bomber')
replaces=('kdegames-bomber')
source=("http://download.kde.org/stable/applications/${pkgver}/src/bomber-${pkgver}.tar.xz")
sha1sums=('0029137de8ef18732ce2234b91e4864fd5d94a2b')

build() {
  mkdir build
  cd build
  cmake ../bomber-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
