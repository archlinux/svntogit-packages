# $Id: PKGBUILD 102633 2010-12-09 08:40:59Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=polkit-qt
pkgname=('polkit-qt4' 'polkit-qt5')
pkgver=0.112.0
pkgrel=2
pkgdesc='A library that allows developers to access PolicyKit API with a nice Qt-style API'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kdesupport/polkit-qt-1'
license=('LGPL')
makedepends=('cmake' 'automoc4' 'polkit' 'qt4' 'qt5-base')
source=("http://download.kde.org/stable/apps/KDE4.x/admin/${pkgbase}-1-${pkgver}.tar.bz2"
        'systembus-usage.patch')
md5sums=('bee71b71c12797e6fc498540a06c829b'
         '067355984adf2cd87e2fac715bd025f3')

prepare() {
  mkdir build{,-qt5}

  cd ${pkgbase}-1-${pkgver}
  patch -p1 -i "${srcdir}"/systembus-usage.patch
}

build() {
  cd build
  cmake ../${pkgbase}-1-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_DESTINATION=/usr/lib \
    -DUSE_QT4=ON
  make

  cd ../build-qt5
  cmake ../${pkgbase}-1-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_DESTINATION=/usr/lib
  make
}

package_polkit-qt4() {
  depends=('polkit' 'qt4')
  replaces=('polkit-qt')
  provides=('polkit-qt')
  conflicts=('polkit-qt')

  cd build
  make DESTDIR="${pkgdir}" install
}

package_polkit-qt5() {
  depends=('polkit' 'qt5-base')

  cd build-qt5
  make DESTDIR="${pkgdir}" install
}
