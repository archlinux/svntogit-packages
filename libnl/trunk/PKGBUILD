# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=libnl
pkgver=3.2.7
pkgrel=1
pkgdesc="Library for applications dealing with netlink sockets"
arch=(i686 x86_64)
url="http://www.infradead.org/~tgr/libnl/"
license=(GPL)
depends=(glibc)
backup=(etc/libnl/classid etc/libnl/pktloc)
options=(!libtool)
source=("$url/files/$pkgname-$pkgver.tar.gz")
md5sums=('6a233a9dffa0ee3a7f6110c95c5410ab')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --disable-static
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
