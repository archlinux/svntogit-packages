# Maintainer:  Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Thomas Bächler <thomas@archlinux.org>

pkgname=libnl
pkgver=3.4.0
pkgrel=1
pkgdesc='Library for applications dealing with netlink sockets'
arch=(x86_64)
url='https://github.com/thom311/libnl/'
license=(GPL)
depends=(glibc)
backup=(etc/libnl/classid etc/libnl/pktloc)
source=(https://github.com/thom311/libnl/releases/download/libnl${pkgver//./_}/${pkgname}-${pkgver}.tar.gz{,.sig})
sha256sums=('b7287637ae71c6db6f89e1422c995f0407ff2fe50cecd61a312b6a9b0921f5bf'
            'SKIP')
validpgpkeys=('49EA7C670E0850E7419514F629C2366E4DFC5728') # Thomas Haller

build() {
  cd $pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --disable-static
  make
}

check() {
  make -C $pkgname-$pkgver check
}

package() {
  make -C $pkgname-$pkgver DESTDIR="$pkgdir" install
}
