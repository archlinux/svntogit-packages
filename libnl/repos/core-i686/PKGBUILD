# $Id$
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>

pkgname=libnl
pkgver=3.2.27
pkgrel=1
pkgdesc="Library for applications dealing with netlink sockets"
arch=(i686 x86_64)
url="http://www.infradead.org/~tgr/libnl/"
license=(GPL)
depends=(glibc)
backup=(etc/libnl/classid etc/libnl/pktloc)
source=(https://github.com/thom311/libnl/releases/download/libnl${pkgver//./_}/${pkgname}-${pkgver}.tar.gz{,.sig})
sha256sums=('4bbbf92b3c78a90f423cf96260bf419a28b75db8cced47051217a56795f58ec6'
            'SKIP')
validpgpkeys=('49EA7C670E0850E7419514F629C2366E4DFC5728') # Thomas Haller

build() {
  cd "$srcdir"/$pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --disable-static
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
