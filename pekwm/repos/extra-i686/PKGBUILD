# $Id$
# Maintainer: Kevin Piche <kevin@archlinux.org>
# Contributor: Eddie Lozon <almostlucky@attbi.com>

pkgname=pekwm
pkgver=0.1.11
pkgrel=2
pkgdesc="A small, fast, functional, and flexible window manager"
arch=(i686 x86_64)
license=('GPL')
url="http://pekwm.org/"
depends=('gcc-libs' 'libjpeg>=7' 'libpng' 'libxft' 'libxinerama' 'libxpm'
'libxrandr')
backup=(etc/pekwm/config
  etc/pekwm/vars
  etc/pekwm/autoproperties
  etc/pekwm/start
  etc/pekwm/keys
  etc/pekwm/mouse
  etc/pekwm/menu)
source=(http://pekwm.org/projects/3/files/${pkgname}-${pkgver}.tar.bz2
pekwm.desktop libpng-1.4.patch)
md5sums=('79df6d01c48e6eb1907dcd3a8246410c' '5a78fc6653fbb0b7282ecf7f1f81e2c5'\
         '075bf1b9d32bf94780329499e4fa40e3')
sha1sums=('28cfa30cfc046440f6e5924214959f8647bc0a9d' '1e23c54b3ba2f4f454a15a390cd869743f498167'\
         'f06f8f9976c391a09d53e9cc0e55a758fb9cd687')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -p1 < ../libpng-1.4.patch || return 1
  # Default settings:
  #    --enable-shape --enable-xinerama --enable-menus \
  #    --enable-harbour --disable-debug --disable-pedantic \
  #    --enable-xft --enable-image-xpm --enable-image-jpeg \
  #    --enable-image-png --enable-xrandr
  ./configure --prefix=/usr --sysconfdir=/etc

  make || return 1
  make DESTDIR=${pkgdir} install
  install -D -m644 ${srcdir}/${pkgname}.desktop \
                   ${pkgdir}/etc/X11/sessions/${pkgname}.desktop
}
# vim: ts=2 sw=2 et ft=sh
