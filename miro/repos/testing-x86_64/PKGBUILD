# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>

pkgname=miro
pkgver=2.5.4
pkgrel=4
pkgdesc="The free and open source internet TV platform"
arch=('i686' 'x86_64')
url="http://www.getmiro.com"
license=('GPL2')
depends=('xine-lib>=1.1.16.3' 'boost>=1.41.0' 'gnome-python-extras>=2.25.3' 'dbus-python>=0.83.0' 'pyrex' 
	'xulrunner>=1.9.0.10' 'desktop-file-utils' 'gstreamer0.10'
	'gstreamer0.10-python' 'gstreamer0.10-good' 'shared-mime-info'
	'hicolor-icon-theme')
makedepends=('pkgconfig' 'pm-utils' 'qt3')
install=miro.install
source=(http://ftp.osuosl.org/pub/pculture.org/miro/src/miro-${pkgver}.tar.gz
	'xine.pyx.patch')
md5sums=('d0ae2bf0463201c6b6dc7367f1fa8b3c'
         '3b30e1247b86e0f6bb8da9e2bdd2530e')

build() {
  cd ${srcdir}/miro-${pkgver}/platform/gtk-x11

  patch -Np3 -i ${srcdir}/xine.pyx.patch || return 1 #fix xine backend FS#17085

  python setup.py install --root=${pkgdir} || return 1
}
