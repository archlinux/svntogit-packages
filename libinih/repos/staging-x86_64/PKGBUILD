# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Alberto Oporto Ames <otreblain@gmail.com>

pkgname=libinih
pkgver=53
pkgrel=2
pkgdesc='A simple .INI file parser written in C'
arch=(x86_64)
url=https://github.com/benhoyt/inih
license=(BSD)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  git
  meson
)
provides=(
  libinih.so
  libINIReader.so
)
_tag=cb55f57d87ae840bd0f65dbe6bd22fa021a873a7
source=(git+https://github.com/benhoyt/inih.git#tag=${_tag})
b2sums=(SKIP)

prepare() {
  cd inih
  git cherry-pick -n 32519ea045670fbd9a057a9c48f77d7b473ce53b
}

pkgver() {
  cd inih
  git describe --tags | sed 's/^r//'
}

build() {
  arch-meson inih build \
    -Ddefault_library=shared \
    -Ddistro_install=true \
    -Dwith_INIReader=true
  meson compile -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
  install -Dm 644 inih/LICENSE.txt -t "${pkgdir}"/usr/share/licenses/libinih/
}

# vim: ts=2 sw=2 et:
