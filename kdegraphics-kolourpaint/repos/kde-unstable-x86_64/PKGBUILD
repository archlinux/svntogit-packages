# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegraphics-kolourpaint
pkgver=15.11.80
pkgrel=2
pkgdesc="Paint Program"
url="http://kde.org/applications/graphics/kolourpaint/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegraphics')
depends=('kdebase-runtime' 'qimageblitz' 'libksane4')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/unstable/applications/${pkgver}/src/kolourpaint-${pkgver}.tar.xz")
sha1sums=('1ca57732af1213d0f5f937815000d771f006a565')

build() {
  cd "${srcdir}"
  mkdir -p build
  cd build
  cmake ../kolourpaint-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
