# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegraphics-kolourpaint
pkgver=4.13.1
pkgrel=1
pkgdesc="Paint Program"
url="http://kde.org/applications/graphics/kolourpaint/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegraphics')
depends=('kdebase-runtime' 'qimageblitz')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/kolourpaint-${pkgver}.tar.xz")
sha1sums=('a11c5acf4aebfb1d6173e414fb2e7d1451a2000b')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../kolourpaint-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
