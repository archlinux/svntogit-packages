# $Id$
# Maintainer:
# Contributor: Andreas Radke <andyrtr at archlinux.org>
# Contributor: eric <eric@archlinux.org>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>

pkgname=raptor1
pkgver=1.4.21
pkgrel=4
pkgdesc="A C library that parses RDF/XML/N-Triples into RDF triples - old V1 api for compatibility"
arch=('i686' 'x86_64')
url="http://librdf.org/raptor"
depends=('curl' 'libxslt')
license=('LGPL')
options=('!libtool' '!docs')
source=("http://librdf.org/dist/source/raptor-$pkgver.tar.gz"
        'curl.patch')
md5sums=('992061488af7a9e2d933df6b694bb876'
         '8fe28761645d6a6607b25c94f9ada964')

build() {
  cd "${srcdir}"/raptor-${pkgver}
  patch -p1 -i "${srcdir}"/curl.patch

  ./configure --prefix=/usr \
    --disable-static
  make
}

package() {
  cd "${srcdir}/raptor-${pkgver}"
  make prefix="${pkgdir}"/usr install
  
  mv "${pkgdir}"/usr/bin/rapper "${pkgdir}"/usr/bin/rapperV1
  rm -f "${pkgdir}"/usr/share/man/man1/rapper.1
}
