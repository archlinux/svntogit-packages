# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgname=openslp
pkgver=2.0.0
pkgrel=1
pkgdesc="Open-source implementation of Service Location Protocol"
arch=('i686' 'x86_64')
url="http://www.openslp.org"
license=('BSD')
depends=('bash' 'openssl')
makedepends=('doxygen')
backup=('etc/slp.conf' 'etc/slp.reg' 'etc/slp.spi')
options=(!makeflags)
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.gz
        #https://github.com/lunar-linux/moonbase-other/commit/ae74b5873e639d50db0a563d2424114a530c01ae
        openslp.service)
md5sums=('18cf7940bcc444e32592cf34e84f833f'
         '90eea4426492287ebfc01b2056a3ef9d')

prepare() {
  sed -i 's|$(datadir)/doc|$(docdir)|' ${pkgname}-${pkgver}/doc/Makefile.in
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --docdir=/usr/share/doc/openslp-${pkgver} \
    --enable-slpv1 \
    --enable-slpv2-security # service would fail to start without this
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  make DESTDIR="${pkgdir}" INSTALL_DATA="cp -r" install-docs
  rm "${pkgdir}/usr/share/doc/openslp-${pkgver}/openslp-doxy-${pkgver}.tar.gz"
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -D -m644 "${srcdir}/openslp.service" "${pkgdir}/usr/lib/systemd/system/openslp.service"
}
