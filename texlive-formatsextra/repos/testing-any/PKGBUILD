# Contributor: Firmicus <firmicus āt gmx dōt net>
# Maintainer: Rémy Oudompheng <remy@archlinux.org>

pkgname=texlive-formatsextra
pkgver=2019.50602
_revnr=${pkgver#2019.}
pkgrel=1
pkgdesc="TeX Live - collection of extra TeX 'formats'"
license=('GPL')
arch=(any)
depends=('texlive-core')
groups=('texlive-most')
url='http://tug.org/texlive/'
source=("https://sources.archlinux.org/other/texlive/$pkgname-$pkgver-src.zip"
        "${pkgname}.fmts"
        "${pkgname}.maps")
options=('!emptydirs')
provides=('texlive-htmlxml')
conflicts=('texlive-htmlxml')
replaces=('texlive-htmlxml')
sha256sums=('f7a566ccb2a9b65fbf5b033595256c62678d7bfb270a64aa460297e7642b1e32'
            'edee1d8b46005847381dbf20afacad6d14f3d59406facdc609c92d9e20ed6df2'
            'ad67f39779bfe76327351fe4e8699369b031ac700947e04ff57a0c5ebe9b613b')

build() {
   cd "$srcdir"
   for p in *.tar.xz; do
       case $p in
           aleph.*|lambda.*|omega.*)
               ;;
           *)
               bsdtar -xf $p
               ;;
       esac
   done
   rm -rf {tlpkg,doc,source} || true
}

package() {
   cd "$srcdir"
   install -m755 -d $pkgdir/var/lib/texmf/arch/installedpkgs
   sed -i '/^#/d' CONTENTS
   install -m644 CONTENTS $pkgdir/var/lib/texmf/arch/installedpkgs/${pkgname}_${_revnr}.pkgs
   install -m644 "${pkgname}.fmts" $pkgdir/var/lib/texmf/arch/installedpkgs/
   install -m755 -d $pkgdir/usr/share
   wanteddirs=$(for d in *; do test -d $d && [[ $d != texmf* ]] && echo $d; done) || true
   for dir in $wanteddirs; do
     find $dir -type d -exec install -d -m755 $pkgdir/usr/share/texmf-dist/'{}' \;
     find $dir -type f -exec install -m644 '{}' $pkgdir/usr/share/texmf-dist/'{}' \;
   done
   if [[ -d texmf-dist ]]; then
     find texmf-dist -type d -exec install -d -m755 $pkgdir/usr/share/'{}' \;
     find texmf-dist -type f -exec install -m644 '{}' $pkgdir/usr/share/'{}' \;
   fi
   if [[ -d $pkgdir/usr/share/texmf-dist/scripts ]]; then
     find $pkgdir/usr/share/texmf-dist/scripts -type f -exec chmod a+x '{}' \;
   fi
}
