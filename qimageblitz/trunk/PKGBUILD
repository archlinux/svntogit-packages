# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=qimageblitz
pkgver=0.0.6
pkgrel=5
pkgdesc="A graphical effect and filter library for KDE"
arch=('i686' 'x86_64')
url="http://www.kde.org/"
license=('GPL2')
depends=('qt4')
makedepends=('cmake')
source=("http://download.kde.org/stable/${pkgname}/${pkgname}-${pkgver}.tar.bz2"
        'no-executable-stack.patch')
md5sums=('0ae2f7d4e0876764a97ca73799f61df4'
         '67a56f4418bb2bda5f8a7444931ca7ac')

prepare() {
  mkdir build

  cd ${pkgname}-${pkgver}
  patch -p3 -i "${srcdir}"/no-executable-stack.patch
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
