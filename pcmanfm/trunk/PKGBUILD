# $Id$
# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Maintainer: Juergen Hoetzel <juergen@archlinux.org>
pkgname=pcmanfm
pkgver=0.9.8
pkgrel=4
pkgdesc="File manager of the LXDE Desktop"
arch=('i686' 'x86_64')
url="http://pcmanfm.sourceforge.net/"
license=('GPL')
groups=('lxde')
depends=('gtk2' 'udisks' 'startup-notification' 'desktop-file-utils' 'libfm')
makedepends=('intltool' 'pkgconfig')
source=(http://downloads.sourceforge.net/pcmanfm/pcmanfm-$pkgver.tar.gz
        revert-new-IPC.patch)
md5sums=('4a7fdc0526ed14e3293d784c0ce27dea' '4bedb071a83ba5a7af1aa43b71a262a9')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -i $srcdir/revert-new-IPC.patch
  ./configure --sysconfdir=/etc --prefix=/usr 
  make 
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install 
}
