# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=perl-compress-bzip2
_realname=Compress-Bzip2
pkgver=2.24
pkgrel=2
pkgdesc="Interface to Bzip2 compression library"
arch=(i686 x86_64)
license=(GPL2)
depends=(perl bzip2)
url="http://search.cpan.org/dist/Compress-Bzip2"
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/R/RU/RURBAN/$_realname-$pkgver.tar.gz")
md5sums=('d5ef04d1e51fe45743b3044abad967d2')

build() {
  cd "$srcdir/$_realname-$pkgver"
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd "$srcdir/$_realname-$pkgver"
  make DESTDIR="$pkgdir" install

# template start; name=perl-binary-module-dependency; version=1;
if [[ $(find "$pkgdir/usr/lib/perl5/" -name "*.so") ]]; then
	_perlver_min=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]);')
	_perlver_max=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]+1);')
	depends+=("perl>=$_perlver_min" "perl<$_perlver_max")
fi
# template end;
}
