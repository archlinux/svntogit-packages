# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=perl-compress-bzip2
_realname=Compress-Bzip2
pkgver=2.26
pkgrel=1
pkgdesc="Interface to Bzip2 compression library"
arch=(i686 x86_64)
license=(GPL2)
depends=(perl bzip2)
url="https://metacpan.org/release/Compress-Bzip2"
options=('!emptydirs')
source=(https://cpan.metacpan.org/authors/id/R/RU/RURBAN/${_realname}-${pkgver}.tar.gz)
sha256sums=('5132f0c5f377a54d77ee36d332aa0ece585c22a40f2c31f2619e40262f5c4f0c')

build() {
  cd $_realname-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd $_realname-$pkgver
  make DESTDIR="$pkgdir" install

# template start; name=perl-binary-module-dependency; version=1;
if [[ $(find "$pkgdir/usr/lib/perl5/" -name "*.so") ]]; then
	_perlver_min=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]);')
	_perlver_max=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]+1);')
	depends+=("perl>=$_perlver_min" "perl<$_perlver_max")
fi
# template end;
}
