# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=pacrunner
pkgver=0.14
pkgrel=1
pkgdesc="Proxy configuration daemon"
arch=(x86_64)
url="https://git.kernel.org/pub/scm/network/connman/pacrunner.git"
license=(GPL2 LGPL2.1)
depends=(glib2 dbus curl)
makedepends=(git)
_commit=531dd59c60ddbecb46eab820aa5314b4be0b7d30  # tags/0.14^0
source=("git+https://git.kernel.org/pub/scm/network/connman/pacrunner.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  ./bootstrap
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib \
    --with-dbusconfdir=/usr/share \
    --enable-pie \
    --enable-duktape \
    --enable-curl
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install

  install -Dm644 /dev/stdin "$pkgdir/usr/lib/systemd/system/pacrunner.service" <<END
[Unit]
Description=Proxy configuration daemon
Before=network.target
Wants=network.target

[Service]
Type=dbus
BusName=org.pacrunner
ExecStart=/usr/bin/pacrunner

[Install]
WantedBy=multi-user.target
END

  install -Dm644 /dev/stdin "$pkgdir/usr/share/dbus-1/system-services/org.pacrunner.service" <<END
[D-BUS Service]
Name=org.pacrunner
Exec=/usr/bin/pacrunner
User=root
SystemdService=pacrunner.service
END
}

# vim:set sw=2 et:
