# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Matthias-Christian Ott <matthias.christian.at.tiscali.de>

pkgname=diffstat
pkgver=1.58
pkgrel=1
pkgdesc="Display a histogram of diff changes"
arch=('i686' 'x86_64')
url="http://invisible-island.net/diffstat"
depends=('glibc')
license=('MIT')
source=("ftp://invisible-island.net/${pkgname}/${pkgname}-${pkgver}.tgz")
md5sums=('6d6e13f7dcfe4db5da65c5175260ea47')

build () {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr \
              --mandir=/usr/share/man
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR=${pkgdir}/ install
  install -Dm644 package/debian/copyright "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
