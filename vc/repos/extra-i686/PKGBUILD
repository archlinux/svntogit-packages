# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: PedsXing <pedsxing at gmx dot net>

pkgname=vc
pkgver=0.6.1
pkgrel=1
pkgdesc="A library to ease explicit vectorization of C++ code"
url='http://code.compeng.uni-frankfurt.de/projects/vc/'
arch=('x86_64' 'i686')
license=('LGPL3')
makedepends=('cmake')
source=("http://code.compeng.uni-frankfurt.de/attachments/download/147/Vc-${pkgver}.tar.gz")
md5sums=('8b9885b58872a11131d2818ec54f838a')
 
build() {
  mkdir build 
  cd build
  cmake ../Vc-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

check() {
  cd build
  make test
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}

