# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>

pkgname=go-openoffice
_GOver=3.2.0.9 # = post OOo 3.2.0 final bugfix
pkgver=${_GOver}
pkgrel=2
pkgdesc="OpenOffice.org - go-oo.org enhanced version of SUN's office suite"
arch=('i686' 'x86_64')
_go_tree="OOO320"
_ootag=ooo320-m12 # m12 = OOo 3.2.0 RC5
license=('LGPL3')
url="http://go-oo.org/"
install=${pkgname}.install
depends=("curl>=7.19.6" "hunspell>=1.2.8" "python>=2.6.4" 'libwpd'
         'libxaw' "neon>=0.28.6" "icu>=4.2.1" 'hsqldb-java' 'libxslt' 'libxtst' 'lpsolve'
	  'beanshell' 'saxon' 'vigra' 'hyphen' 'libmspack' 'libldap' 'gtk2' 'lucene'
	 'hicolor-icon-theme' 'shared-mime-info' 'desktop-file-utils') #   'libmythes' 'libgraphite' 'redland>=1.0.10'
optdepends=('java-runtime:	adds java support'
            'libcups:		adds printing support'
            'gconf:		adds additional gnome support'
            'nss:		adds support for signed files/macros'
            'pstoedit:		translates PostScript and PDF graphics into other vector formats'
	    'poppler:		for the pdfimport extension'
	    'mesa:		for the OGLTrans extension'
	    'mono:		allows UNO automation with Mono'
	    'gstreamer0.10-base:	+ some gstr-plugins to support multimedia content, e.g. in impress'
            'kdelibs:		for kde integration')
makedepends=('automake' 'autoconf' 'wget' 'bison' 'findutils' 'flex' 'gawk' 'gcc-libs' 'libart-lgpl'
	'pam' 'sane' 'perl-archive-zip' 'pkgconfig' 'unzip' 'zip' "xulrunner>=1.9.2-4" 'apache-ant' 'cairo' 
	'gperf' 'libcups' 'pstoedit' 'gconf' "openjdk6>=1.5.2" 'unixodbc' 'mesa>=7.5' 'poppler>=0.10.7'
	'gstreamer0.10-base>=0.10.26'  'mono>=2.6.1' 'kdelibs>=4.4.0' 'libjpeg' 'boost' 'git' 'rsync')
backup=(usr/lib/go-openoffice/program/sofficerc)
provides=('openoffice-base')
conflicts=('openoffice-base')
_mirror="http://download.go-oo.org/"
source=(${_mirror}/${_go_tree}/ooo-build-${_GOver}.tar.gz
	ArchLinux.patch
	${pkgname}-${_ootag}.tar.xz
	http://download.go-oo.org//DEV300/ooo-cli-prebuilt-3.2.tar.bz2
	http://cairographics.org/releases//cairo-1.4.10.tar.gz
	http://download.go-oo.org//SRC680/mdbtools-0.6pre1.tar.gz
	http://download.go-oo.org//SRC680/extras-3.tar.bz2
	http://download.go-oo.org//SRC680/biblio.tar.bz2
	http://tools.openoffice.org/unowinreg_prebuild/680//unowinreg.dll
	http://download.go-oo.org//DEV300/scsolver.2008-10-30.tar.bz2
	http://download.go-oo.org//libwpd/libwpd-0.8.14.tar.gz
	http://download.go-oo.org//SRC680/libwps-0.1.2.tar.gz
	http://download.go-oo.org//SRC680/libwpg-0.1.3.tar.gz
	http://download.go-oo.org//DEV300/ooo_oxygen_images-2009-06-17.tar.gz
	http://download.go-oo.org/src//seamonkey-1.1.14.source.tar.gz
	http://archive.apache.org/dist/ant/binaries/apache-ant-1.7.0-bin.tar.gz
	buildfix_64bit_system_libjpeg.diff
	system-redland.patch
	localize-ooo.diff)
#options=('!distcc' '!ccache' '!makeflags')
options=('!makeflags')
noextract=(ooo-cli-prebuilt-3.2.tar.bz2 cairo-1.4.10.tar.gz mdbtools-0.6pre1.tar.gz extras-3.tar.bz2 biblio.tar.bz2 unowinreg.dll 
           scsolver.2008-10-30.tar.bz2 libwpd-0.8.14.tar.gz libwps-0.1.2.tar.gz libwpg-0.1.3.tar.gz ooo_oxygen_images-2009-06-17.tar.gz)

# source PKGBUILD && mksource
mksource() {
        mkdir /tmp/$pkgname-source
        pushd /tmp/$pkgname-source
	wget ${_mirror}/${_go_tree}/ooo-build-${_GOver}.tar.gz
	tar -xvf ooo-build-${_GOver}.tar.gz
	cd ooo-build-${_GOver}
	./configure --quiet --with-distro=ArchLinux
	./download --all
	pushd src; tar -cvJf ../../${pkgname}-${_ootag}.tar.xz clone; popd
        popd
}

build() {
	unset J2REDIR; unset J2SDKDIR; unset JAVA_HOME; unset CLASSPATH
	[ -z "${JAVA_HOME}" ] && . /etc/profile.d/openjdk6.sh
	[ -z "${MOZ_PLUGIN_PATH}" ] && . /etc/profile.d/mozilla-common.sh
	[ -z "${ANT_HOME}" ] && . /etc/profile.d/apache-ant.sh

	cd ${srcdir}/ooo-build-${_GOver}

	# our ArchLinux distribution patch until we go upstream
	patch -Np0 -i ${srcdir}/ArchLinux.patch || return 1

	# buildfix for broken language settings in build
	patch -Np0 -i ${srcdir}/localize-ooo.diff || return 1

	# fix bugs with recent system redland
#	patch -Np1 -i ${srcdir}/system-redland.patch || return 1

	# hotfixes not yet upstream
#	cp ${srcdir}/*.diff ${srcdir}/ooo-build-${_GOver}/patches/hotfixes/
	cp ${srcdir}/buildfix_64bit_system_libjpeg.diff ${srcdir}/ooo-build-${_GOver}/patches/hotfixes/

	# export C(XX)FLAGS
	# http://www.openoffice.org/issues/show_bug.cgi?id=103205
	unset CFLAGS
	unset CXXFLAGS
#	export ARCH_FLAGS="$CFLAGS"

	if [ "$CARCH" = "x86_64" ]; then
	      EXTRAOPTS="--without-stlport"
	 else EXTRAOPTS="--with-stlport"
	fi

#	autoreconf
	./configure --with-distro=ArchLinux \
		--with-build-version="${_GOver} ArchLinux build-${pkgrel} (${_ootag})"\
		--with-srcdir=${srcdir} \
		--with-max-jobs=${MAKEFLAGS/-j/} \
		--with-installed-ooo-dirname="${pkgname}" \
		--prefix=/usr --exec-prefix=/usr --sysconfdir=/etc \
		--with-docdir=/usr/share/doc/packages/"${pkgname}" \
		--mandir=/usr/share/man \
		--with-lang="" \
		--with-dict=ALL\
		--with-binsuffix=no \
		--disable-ldap \
		--enable-cairo\
		--disable-kde\
		--enable-kde4\
		--enable-lockdown\
		--with-system-boost\
		--with-system-cairo\
		--enable-crashdump\
		--without-gpc\
		--enable-opengl \
		--enable-minimizer \
		--enable-presenter-console \
		--enable-pdfimport \
		--enable-wiki-publisher \
		--enable-ogltrans \
		--with-ant-home="/usr/share/java/apache-ant"\
		--with-system-saxon\
		--with-saxon-jar=/usr/share/java/saxon/saxon9he.jar\
		--with-system-lucene\
		--with-lucene-core-jar=/usr/share/java/lucene-core.jar\
		--with-lucene-analyzers-jar=/usr/share/java/lucene-analyzers.jar\
		--with-system-beanshell\
		--with-system-vigra\
		--with-system-altlinuxhyph\
		--with-system-lpsolve\
		--without-system-redland \
		$EXTRAOPTS || return 1

#		--with-system-mythes\
#		--with-system-graphite\
#		--with-tag=${_ootag}
#		--enable-report-builder \
#		--with-additional-sections="OOXMLExport"

	unset MAKEFLAGS
#	./download
	LD_PRELOAD="" make  || return 1
}

package() {
	cd ${srcdir}/ooo-build-${_GOver}
	LD_PRELOAD="" make DESTDIR=${pkgdir} install || return 1

       # install all built dictionaries from source tree
       pushd ${srcdir}/ooo-build-${_GOver}/build/${_ootag}/dictionaries/unxlng?6.pro/bin/
       for i in `ls -1 dict-??.oxt`; do
         install -D -m644 $i ${pkgdir}/usr/lib/"${pkgname}"/share/extension/install/$i || return 1
       done
       popd

	# install all other built extensions
	pushd ${srcdir}/ooo-build-${_GOver}/build/${_ootag}/solver/320/unxlng?6.pro/bin/
	install -m644 pdfimport/pdfimport.oxt ${pkgdir}/usr/lib/"${pkgname}"/share/extension/install/pdfimport.oxt || return 1
	install -m644 swext/wiki-publisher.oxt ${pkgdir}/usr/lib/"${pkgname}"/share/extension/install/wiki-publisher.oxt || return 1
	install -m644 minimizer/sun-presentation-minimizer.oxt ${pkgdir}/usr/lib/"${pkgname}"/share/extension/install/sun-presentation-minimizer.oxt || return 1
	install -m644 presenter/presenter-screen.oxt ${pkgdir}/usr/lib/"${pkgname}"/share/extension/install/presenter-screen.oxt || return 1
	popd
	
	# fix unopkg call for mktemp, #15410
	sed -i "s:\/bin\/mktemp:\/usr\/bin\/mktemp:" ${pkgdir}/usr/lib/go-openoffice/program/unopkg || return 1
	
	#fix http://bugs.archlinux.org/task/17656
	find ${pkgdir} -perm 444 -exec ls -lh {} \; 
	find ${pkgdir} -perm 444 -exec chmod 644 {} \;
	find ${pkgdir} -perm 555 -exec ls -lh {} \;
	find ${pkgdir} -perm 555 -exec chmod 755 {} \;
}
