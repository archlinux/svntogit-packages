# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>

pkgname=go-openoffice
_GOver=3.2.1.3 # = final 3.2.1 != post OOo 3.2.1 final bugfix
pkgver=${_GOver}
pkgrel=1
pkgdesc="OpenOffice.org - go-oo.org enhanced version of SUN's office suite"
arch=('i686' 'x86_64')
_go_tree="OOO320"
_ootag=ooo320-m19 # m18/m19 = OOo 3.2.1 RC
license=('LGPL3')
url="http://go-oo.org/"
install=${pkgname}.install
makedepends=('bison' 'git' 'rsync' 'wget' 'automake' 'autoconf' 'findutils' 'flex' 'gawk' 'gcc-libs' 'libart-lgpl' 'libxtst' 'boost>=1.41.0'
             'pam' 'sane' 'zip' 'perl-archive-zip' 'pkgconfig' 'unzip' 'zip' "xulrunner>=1.9.2.2" 'unixodbc' 'nss' 'hsqldb-java' 
             'apache-ant>=1.8.0' 'cairo' 'gperf' 'libcups' 'poppler' 'kdelibs' 'pstoedit' 'gconf' "openjdk6>=6.b17_1.8"
	     'beanshell' 'vigra' 'libmspack' 'libldap' 'lucene'  'libmythes'
	     # for additional ooo-build features
	     'mesa>=7.5' 'gstreamer0.10-base>=0.10.26'  'mono>=2.6.1') 
	#'saxon'
depends=("curl>=7.20.0" "hunspell>=1.2.8" "python>=2.6.5" 'libwpd' 'libxaw' "neon>=0.28.6"
          'pango' 'nspr' 'libjpeg' 'libxrandr' 'libgl' 'dbus-glib' "icu>=4.2.1" 'libxslt'
	  'redland' 'libgraphite' 'hyphen' 'lpsolve'
	  'hicolor-icon-theme' 'desktop-file-utils' 'gtk2') # keep gtk2 for install script
optdepends=('java-runtime:     adds java support'
             'libcups:          adds printing support'
             'gconf:            adds additional gnome support'
             'nss:              adds support for signed files/macros'
             'pstoedit:         translates PostScript and PDF graphics into other vector formats'
             'poppler:          for shipped pdfimport extension'
             'kdelibs:          for kde integration'
             'libmythes:        for use in thesaurus'
             'hsqldb-java:      default database format for OpenOffice.org'
             'beanshell:        interactive java -- good for prototyping /macros'
             'vigra:            C++ computer vision library, usable in Basebmp'
             'libmspack:        library for Microsoft compression formats for use in FontOOo'
             'libldap:          to get profiles via ldap'
             'lucene:           full-text search engine library for Java needed in the help section'
             'sane:             for scanner access'
             'unixodbc:         adds ODBC database support'
             'mesa:             for the OGLTrans extension'
             'mono:             allows UNO automation with Mono'
             'gstreamer0.10-base: + some gstr-plugins to support multimedia content, e.g. in impress')
backup=(usr/lib/go-openoffice/program/sofficerc)
provides=('openoffice-base')
conflicts=('openoffice-base')
_mirror="http://download.go-oo.org/"
source=(${_mirror}/${_go_tree}/ooo-build-${_GOver}.tar.gz
	ArchLinux.patch
	${pkgname}-${_ootag}.tar.xz
	http://download.go-oo.org/DEV300/ooo-cli-prebuilt-3.2.1.tar.bz2
	http://cairographics.org/releases//cairo-1.4.10.tar.gz
	http://download.go-oo.org/SRC680/mdbtools-0.6pre1.tar.gz
	http://download.go-oo.org/SRC680/extras-3.tar.bz2
	http://download.go-oo.org/SRC680/biblio.tar.bz2
	http://tools.openoffice.org/unowinreg_prebuild/680//unowinreg.dll
	http://download.go-oo.org/DEV300/scsolver.2008-10-30.tar.bz2
	http://download.go-oo.org/libwpd/libwpd-0.8.14.tar.gz
	http://download.go-oo.org/SRC680/libwps-0.1.2.tar.gz
	http://download.go-oo.org/SRC680/libwpg-0.1.3.tar.gz
	http://download.go-oo.org/DEV300/ooo_oxygen_images-2009-06-17.tar.gz
	http://download.go-oo.org/src/seamonkey-1.1.14.source.tar.gz
	http://archive.apache.org/dist/ant/binaries/apache-ant-1.7.0-bin.tar.gz
	http://multidimalgorithm.googlecode.com/files//mdds_0.3.0.tar.bz2
	buildfix_64bit_system_libjpeg.diff)
options=('!makeflags')
noextract=(ooo-cli-prebuilt-3.2.tar.bz2 cairo-1.4.10.tar.gz mdbtools-0.6pre1.tar.gz extras-3.tar.bz2 biblio.tar.bz2 unowinreg.dll 
           scsolver.2008-10-30.tar.bz2 libwpd-0.8.14.tar.gz libwps-0.1.2.tar.gz libwpg-0.1.3.tar.gz ooo_oxygen_images-2009-06-17.tar.gz)

# source PKGBUILD && mksource
mksource() {
        mkdir /tmp/$pkgname-source
        pushd /tmp/$pkgname-source
	wget ${_mirror}/${_go_tree}/ooo-build-${_GOver}.tar.gz
	tar -xvf ooo-build-${_GOver}.tar.gz
	cd ooo-build-${_GOver}
	./configure --quiet --with-distro=ArchLinux
	./download --all
	pushd src; tar -cvJf ../../${pkgname}-${_ootag}.tar.xz clone; popd
        popd
}

build() {
	unset J2REDIR; unset J2SDKDIR; unset JAVA_HOME; unset CLASSPATH
	[ -z "${JAVA_HOME}" ] && . /etc/profile.d/openjdk6.sh
	[ -z "${MOZ_PLUGIN_PATH}" ] && . /etc/profile.d/mozilla-common.sh
	[ -z "${ANT_HOME}" ] && . /etc/profile.d/apache-ant.sh

	cd ${srcdir}/ooo-build-${_GOver}

	# our ArchLinux distribution patch until we go upstream
	patch -Np0 -i ${srcdir}/ArchLinux.patch || return 1

	# hotfixes not yet upstream
#	cp ${srcdir}/*.diff ${srcdir}/ooo-build-${_GOver}/patches/hotfixes/
	cp ${srcdir}/buildfix_64bit_system_libjpeg.diff ${srcdir}/ooo-build-${_GOver}/patches/hotfixes/

	# export C(XX)FLAGS
	# http://www.openoffice.org/issues/show_bug.cgi?id=103205
	unset CFLAGS
	unset CXXFLAGS

	if [ "$CARCH" = "x86_64" ]; then
	      EXTRAOPTS="--without-stlport"
	 else EXTRAOPTS="--with-stlport --with-additional-sections=SystemGraphiteWithSTLport"
	fi

#	autoreconf
	./configure --with-distro=ArchLinux \
		--with-build-version="${_GOver} ArchLinux build-${pkgrel} (${_ootag})"\
		--with-srcdir=${srcdir} \
		--with-max-jobs=${MAKEFLAGS/-j/} \
		--with-installed-ooo-dirname="${pkgname}" \
		--prefix=/usr --exec-prefix=/usr --sysconfdir=/etc \
		--with-docdir=/usr/share/doc/packages/"${pkgname}" \
		--mandir=/usr/share/man \
		--with-lang="" \
		--with-dict=ALL\
		--with-binsuffix=no \
		--enable-cairo\
		--enable-crashdump\
		--enable-evolution2\
		--enable-graphite\
		--disable-kde\
		--enable-kde4\
		--enable-ldap \
		--enable-lockdown\
		--enable-opengl \
		--enable-mediawiki\
		--enable-minimizer \
		--enable-odk\
		--enable-opengl\
		--enable-pdfimport \
		--enable-presenter-console \
		--enable-report-builder\
		--enable-wiki-publisher \
		--enable-ogltrans \
		--without-gpc\
		--without-fonts\
		--without-afms\
		--without-gpc\
		--without-nas\
		--without-ppds\
		--without-system-agg\
		--without-system-libwps\
		--without-system-libwpg\
		--without-system-mdds\
		--with-system-cppunit\
		--with-system-redland\
		--without-system-saxon\
		--with-openldap\
		--with-ant-home="/usr/share/java/apache-ant"\
		--with-system-boost\
		--with-system-cairo\
		--with-system-libs\
		--with-system-mythes\
		--with-system-unixodbc-headers\
		--with-system-xrender-headers\
		--with-system-headers\
		--with-alloc=system\
		--with-system-lucene\
		--with-lucene-core-jar=/usr/share/java/lucene-core.jar\
		--with-lucene-analyzers-jar=/usr/share/java/lucene-analyzers.jar\
		$EXTRAOPTS || return 1

# see http://qa.openoffice.org/issues/show_bug.cgi?id=110136
#		--with-system-saxon\
#		--with-saxon-jar=/usr/share/java/saxon/saxon9he.jar\
		
#		--with-tag=${_ootag}
#		--enable-report-builder \
#		--with-additional-sections="OOXMLExport"

	unset MAKEFLAGS
#	./download
	LD_PRELOAD="" make  || return 1
}

package() {
	cd ${srcdir}/ooo-build-${_GOver}
	LD_PRELOAD="" make DESTDIR=${pkgdir} install || return 1

       # install all built dictionaries from source tree
       pushd ${srcdir}/ooo-build-${_GOver}/build/${_ootag}/dictionaries/unxlng?6.pro/bin/
       for i in `ls -1 dict-??.oxt`; do
         install -D -m644 $i ${pkgdir}/usr/lib/"${pkgname}"/share/extension/install/$i || return 1
       done
       popd

	# install all other built extensions
	pushd ${srcdir}/ooo-build-${_GOver}/build/${_ootag}/solver/320/unxlng?6.pro/bin/
         install -D -m644 report-builder.oxt ${pkgdir}/usr/lib/openoffice/share/extension/install/report-builder.oxt || return 1
         install -D -m644 swext/wiki-publisher.oxt ${pkgdir}/usr/lib/"${pkgname}"/share/extension/install/wiki-publisher.oxt || return 1
         install -D -m644 minimizer/presentation-minimizer.oxt ${pkgdir}/usr/lib/"${pkgname}"/share/extension/install/presentation-minimizer.oxt || return 1
         install -D -m644 presenter/presenter-screen.oxt ${pkgdir}/usr/lib/"${pkgname}"/share/extension/install/presenter-screen.oxt || return 1
         install -D -m644 pdfimport/pdfimport.oxt ${pkgdir}/usr/lib/"${pkgname}"/share/extension/install/pdfimport.oxt || return 1
	popd
	
	# fix unopkg call for mktemp, #15410
	sed -i "s:\/bin\/mktemp:\/usr\/bin\/mktemp:" ${pkgdir}/usr/lib/go-openoffice/program/unopkg || return 1
	
	#fix http://bugs.archlinux.org/task/17656
	find ${pkgdir} -perm 444 -exec ls -lh {} \; 
	find ${pkgdir} -perm 444 -exec chmod 644 {} \;
	find ${pkgdir} -perm 555 -exec ls -lh {} \;
	find ${pkgdir} -perm 555 -exec chmod 755 {} \;
}
