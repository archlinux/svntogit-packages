# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Massimiliano Torromeo <massimiliano DOT torromeo AT google mail service>

pkgbase=virtuoso
pkgname=('virtuoso' 'virtuoso-base')
pkgver=6.1.7
pkgrel=1
arch=('i686' 'x86_64')
url='http://virtuoso.openlinksw.com/wiki/main/Main/'
license=('GPL')
makedepends=('libldap' 'bison' 'flex' 'gperf' 'net-tools' 'libxml2' 'optipng')
options=('!libtool')
source=("http://downloads.sourceforge.net/${pkgbase}/${pkgbase}-opensource-${pkgver}.tar.gz")
md5sums=('88885e84e8f7b61e59516f696b1d3be5')

prepare() {
  cd ${pkgbase}-opensource-${pkgver}

  optipng -quiet -force -fix appsrc/ODS-Wiki/http/images/opts_16.png
}

build() {
  cd ${pkgbase}-opensource-${pkgver}

  ./configure --with-layout=debian \
    --program-transform-name='s/isql$$/isql-vt/;s/isqlw/isqlw-vt/' \
    --with-readline
  make
}

package_virtuoso-base() {
  pkgdesc='A scalable cross-platform server of virtuoso (very minimal installation)'
  depends=('libldap')

  # install server
  cd ${pkgbase}-opensource-${pkgver}/binsrc/virtuoso
  make DESTDIR=${pkgdir} install

  # install driver
  cd ${srcdir}/${pkgbase}-opensource-${pkgver}/binsrc/driver
  make DESTDIR=${pkgdir} install

  # Install some useful tools; rename to avoid conflicts with unixodbc
  for bin in isql isqlw; do
    install -Dm755 ${srcdir}/${pkgbase}-opensource-${pkgver}/binsrc/tests/$bin \
      ${pkgdir}/usr/bin/$bin-vt
  done
}

package_virtuoso() {
  pkgdesc='A scalable cross-platform server that combines SQL/RDF/XML Data Management with Web Application Server and Web Services Platform functionality'
  depends=('virtuoso-base' 'libxml2')

  cd ${pkgbase}-opensource-${pkgver}
  make DESTDIR=${pkgdir} install

  # remove conflicts with virtuoso-base
  rm "${pkgdir}"/usr/bin/isql{,w-vt}
  rm "${pkgdir}"/usr/bin/virtuoso-t
  rm "${pkgdir}"/usr/lib/libvirtuoso-t.a
  rm "${pkgdir}"/usr/lib/virtodbc.{a,so}
  rm "${pkgdir}"/usr/lib/virtodbc_r.{a,so}
  rm "${pkgdir}"/usr/lib/virtodbcu.{a,so}
  rm "${pkgdir}"/usr/lib/virtodbcu_r.{a,so}
}
