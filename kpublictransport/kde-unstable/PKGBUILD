# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kpublictransport
pkgver=20.11.90
pkgrel=1
pkgdesc="Library to assist with accessing public transport timetables and other data"
arch=(x86_64)
url="https://www.kde.org"
license=(LGPL)
depends=(qt5-declarative)
makedepends=(extra-cmake-modules protobuf)
source=("https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('4d926f1166684993a08fca79ff0a58511d1af148f5492798566df8a418c34095'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
