# $Id$
# Maintainer: Ray Rashif <schiv@archlinux.org>

pkgname=lv2
pkgver=1.8.0
pkgrel=1
pkgdesc="Successor to the LADSPA audio plug-in standard"
url="http://lv2plug.in/"
license=('LGPL' 'custom')
arch=('i686' 'x86_64')
makedepends=('python2' 'libsndfile' 'gtk2')
optdepends=('libsndfile: Example sampler'
            'gtk2: Example sampler'
            'python2: Scripts')
provides=('lv2core')
conflicts=('lv2core')
replaces=('lv2core')
source=("http://lv2plug.in/spec/$pkgname-$pkgver.tar.bz2")
md5sums=('2bdcf01f24fa567448afbf6b8be17044')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  python2 waf configure --prefix=/usr
  python2 waf build $MAKEFLAGS
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  python2 waf install --destdir="$pkgdir"

  install -Dm644 COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
