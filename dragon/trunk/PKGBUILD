# Maintainer: Antonio Rojas <arojas@archlinux.org> 
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=dragon
pkgver=19.12.2
pkgrel=1
pkgdesc='A multimedia player where the focus is on simplicity, instead of features'
arch=(x86_64)
url='https://kde.org/applications/multimedia/dragonplayer'
license=(GPL)
depends=(hicolor-icon-theme kparts phonon-qt5)
makedepends=(extra-cmake-modules kdoctools)
groups=(kde-applications kdemultimedia)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('3fc3cb73acd54455198396dbce0ba8997b40acf7d914feb286b5bb931e21aa7b'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() { 
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}

