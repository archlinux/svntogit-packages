# Maintainer: Antonio Rojas <arojas@archlinux.org> 
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=dragon
pkgver=15.08.3
pkgrel=1
pkgdesc='A multimedia player where the focus is on simplicity, instead of features'
arch=(i686 x86_64)
url='http://kde.org/applications/multimedia/dragon'
license=(GPL)
depends=(hicolor-icon-theme kparts)
makedepends=(extra-cmake-modules kdoctools python)
conflicts=(kdemultimedia-dragonplayer)
replaces=(kdemultimedia-dragonplayer)
groups=(kde-applications kdemultimedia)
source=("http://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
install=$pkgname.install
md5sums=('c04e11f780fb52c5184b1fe3c7f55e75')

prepare() {
  mkdir -p build
}

build() { 
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}

