# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kshisen
pkgver=15.12.0
pkgrel=1
pkgdesc="A solitaire-like game played using the standard set of Mahjong tiles"
url="http://kde.org/applications/games/kshisen/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegames')
depends=('libkdegames' 'libkmahjongg' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'python' 'kdoctools')
install=${pkgname}.install
conflicts=('kdegames-kshisen')
replaces=('kdegames-kshisen')
source=("http://download.kde.org/stable/applications/${pkgver}/src/kshisen-${pkgver}.tar.xz")
sha1sums=('f7569ab3ee80a2cbcd70bc0c1f033076020ed346')

build() {
  mkdir -p build
  cd build
  cmake ../kshisen-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
