# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgbase=pyqt6-networkauth
pkgname=(python-pyqt6-networkauth)
pkgver=6.2.0
pkgrel=3
pkgdesc='Python bindings for QtNetworkAuth'
arch=(x86_64)
url='https://www.riverbankcomputing.com/software/pyqtnetworkauth/intro'
license=(GPL3)
groups=(pyqt6)
depends=(qt6-networkauth python-pyqt6)
makedepends=(sip pyqt-builder)
source=(https://pypi.python.org/packages/source/P/PyQt6-NetworkAuth/PyQt6_NetworkAuth-$pkgver.tar.gz)
sha256sums=('23e730cc0d6b828bec2f92d9fac3607871e6033a8af4620e5d4e3afc13bd6c3c')
options=(debug)

build() {
  cd PyQt6_NetworkAuth-$pkgver
  sip-build \
    --no-make \
    --qmake=/usr/bin/qmake6
  cd build
  make
}

package_python-pyqt6-networkauth(){
  cd PyQt6_NetworkAuth-$pkgver/build
  make INSTALL_ROOT="$pkgdir" install
}
