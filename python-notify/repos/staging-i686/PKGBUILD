# $Id$
# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Roman Kyrylych <Roman.Kyrylych@gmail.com>
# Contributor: Mario Danic <mario.danic@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
pkgname=python-notify
pkgver=0.1.1
pkgrel=11
pkgdesc="Python bindings for libnotify"
arch=('i686' 'x86_64')
url="http://www.galago-project.org/"
license=('GPL')
depends=('pygtk>=2.22.0' 'libnotify>=0.7.1')
makedepends=('pkgconfig' 'python2')
options=(!libtool)
source=(http://www.galago-project.org/files/releases/source/notify-python/notify-python-${pkgver}.tar.gz
        libnotify07.patch
        notify-python-0.1.1-fix-GTK-symbols.patch)
md5sums=('8f0ef0939cc8edd2efd896ce5ba80cf4'
         'b40c4542575d5aef559908fe60a21634'
         'c6922028da5951e69a6a0167bdb4461c')

build() {
    cd ${srcdir}/notify-python-${pkgver}

    patch -Np1 -i ${srcdir}/libnotify07.patch
    patch -Np1 -i ${srcdir}/notify-python-0.1.1-fix-GTK-symbols.patch

    ./configure --prefix=/usr

    # WARNING - we touch src/pynotify.override in build because upstream did not rebuild pynotify.c
    # from the input definitions, this forces pynotify.c to be regenerated, at some point this can be removed
    touch src/pynotify.override

    make clean
    make
}

package() {
    cd ${srcdir}/notify-python-${pkgver}
    make DESTDIR=${pkgdir} install
}
