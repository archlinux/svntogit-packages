# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

# after a .so bump first rebuild dirmngr
# with sudo testing-x86_64-build -- -I libgcrypt-1.6.0-1-x86_64.pkg.tar.xz
# then cp /usr/lib/libgcrypt.so.11 /var/lib/archbuild/staging-x86_64/root/usr/lib/ and do staging-x86_64-build

pkgname=libgcrypt
pkgver=1.6.0
pkgrel=2
pkgdesc="General purpose cryptographic library based on the code from GnuPG"
arch=(i686 x86_64)
url="http://www.gnupg.org"
license=('LGPL')
depends=('libgpg-error>=1.10-2')
options=('!emptydirs')
install=$pkgname.install
source=(ftp://ftp.gnupg.org/gcrypt/${pkgname}/${pkgname}-${pkgver}.tar.bz2
        fix_avx_detection.patch
        fix_slow_PBKDF2.patch)
sha1sums=('43283c0b41c41e3d3bc13c2d8f937dfe2aaa1a77'
          '5219ee3d6e08155149f0fa410b07736d4f502d17'
          'a27baab3f4bda7ff6fb493115f6bdd1c990184f1')

prepare() {
  cd ${pkgname}-${pkgver}
  
  # fix AVX/AVX2 detextion
  patch -Np1 -i ${srcdir}/fix_avx_detection.patch
  
  # fix slow PBKDF2
  patch -Np1 -i ${srcdir}/fix_slow_PBKDF2.patch
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr \
	--disable-static \
	--disable-padlock-support
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
