# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=libgcrypt
pkgver=1.5.0
pkgrel=2
pkgdesc="a general purpose crypto library based on the code used"
arch=(i686 x86_64)
url="http://www.gnupg.org"
license=('LGPL')
depends=('libgpg-error>=1.9')
options=('!libtool' '!emptydirs')
install=$pkgname.install
source=(ftp://ftp.gnupg.org/gcrypt/${pkgname}/${pkgname}-${pkgver}.tar.bz2
	#ftp://ftp.franken.de/pub/crypt/mirror/ftp.gnupg.org/gcrypt/libgcrypt/${pkgname}-${pkgver}.tar.bz2
)
sha1sums=('3e776d44375dc1a710560b98ae8437d5da6e32cf')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
#  [ "$CARCH" = "i686" ] && EXTRAFLAGS="--disable-aesni-support"
  ./configure --prefix=/usr \
	--disable-static \
	--disable-padlock-support #$EXTRAFLAGS
  make
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  # Move dynamic libraries to /lib
  install -d -m755 "${pkgdir}"/lib/
  mv "${pkgdir}"/usr/lib/libgcrypt.so* "${pkgdir}"/lib/
  ln -sf /lib/libgcrypt.so "${pkgdir}"/usr/lib/libgcrypt.so
}
