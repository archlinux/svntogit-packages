# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Thomas Baechler <thomas@archlinux.org>

pkgbase=thunderbird-i18n
pkgver=24.1.1
pkgrel=1
pkgdesc="Language pack for Thunderbird"
arch=('any')
url="http://www.mozilla.com/"
license=('MPL' 'GPL')
depends=("thunderbird>=$pkgver")

_languages=(
  'ar     "Arabic"'
  'ast    "Asturian"'
  'be     "Belarusian"'
  'bg     "Bulgarian"'
  'bn-BD  "Bengali (Bangladesh)"'
  'br     "Breton"'
  'ca     "Catalan"'
  'cs     "Czech"'
  'da     "Danish"'
  'de     "German"'
  'el     "Greek"'
  'en-GB  "English (United Kingdom)"'
  'en-US  "English (United States)"'
  'es-AR  "Spanish (Argentina)"'
  'es-ES  "Spanish (Spain)"'
  'et     "Estonian"'
  'eu     "Basque"'
  'fi     "Finnish"'
  'fr     "French"'
  'fy-NL  "Frisian (Netherlands)"'
  'ga-IE  "Irish"'
  'gd     "Gaelic"'
  'gl     "Galician"'
  'he     "Hebrew"'
  'hr     "Croatian"'
  'hu     "Hungarian"'
  'hy-AM  "Armenian"'
  'id     "Indonesian"'
  'is     "Icelandic"'
  'it     "Italian"'
  'ja     "Japanese"'
  'ko     "Korean"'
  'lt     "Lithuanian"'
  'nb-NO  "Norwegian (Bokmal)"'
  'nl     "Dutch"'
  'nn-NO  "Norwegian (Nynorsk)"'
  'pa-IN  "Punjabi"'
  'pl     "Polish"'
  'pt-BR  "Brazilian Portuguese"'
  'pt-PT  "Portuguese"'
  'rm     "Romansh"'
  'ro     "Romanian"'
  'ru     "Russian"'
  'si     "Sinhalese"'
  'sk     "Slovak"'
  'sl     "Slovenian"'
  'sq     "Albanian"'
  'sr     "Serbian"'
  'sv-SE  "Swedish"'
  'ta-LK  "Tamil (Sri Lanka)"'
  'tr     "Turkish"'
  'uk     "Ukrainian"'
  'vi     "Vietnamese"'
  'zh-CN  "Chinese (simplified)"'
  'zh-TW  "Chinese (traditional)"'
)

pkgname=()
source=()
_url=https://ftp.mozilla.org/pub/mozilla.org/thunderbird/releases/$pkgver/linux-i686/xpi

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}
  _pkgname=thunderbird-i18n-${_locale,,}

  pkgname+=($_pkgname)
  source+=("thunderbird-i18n-$pkgver-$_locale.xpi::$_url/$_locale.xpi")
  eval "package_$_pkgname() {
    _package $_lang
  }"
done

# Don't extract anything
noextract=(${source[@]%%::*})

_package() {
  pkgdesc="$2 language pack for Thunderbird"
  install -Dm644 thunderbird-i18n-$pkgver-$1.xpi \
    "$pkgdir/usr/lib/thunderbird/extensions/langpack-$1@thunderbird.mozilla.org.xpi"
}

md5sums=('71b52e9b2ea4b2813579151107b26d7c'
         '0001f92a0a732b2d8d4abe5b73739871'
         '0ca3fd860debb0f4bea75c295cd794bf'
         '3fa1b7e34efb29fcf12ff84c9b7cfb99'
         '0d412e040d7db1bff6ba7a5901c194a3'
         '196390a5683708ea9d1dff08d5da4969'
         '1b580efd13589da4a7c700ce675b4de8'
         '3f0b570ace30f4b8439328f63424914a'
         '5d7890dce639c703319185dcca099c19'
         '02352099e2bb79b9ed57b8b735d1b662'
         '1b463b18db5640f9fb1fbb82f9862d51'
         'f8053c0d509c95643be7c993f79469e8'
         'c7286e9fb549480c7565947162473eb3'
         'a180d36ebe0e04a452c452d8ca249936'
         'faa9054e681927eec112b989b9b9f916'
         'd5b4bbe5cd18b72b7d8c649b68a095bb'
         '5fd037b2d60ff663eccbf0788a52cce7'
         '824805de27a69e5323c6e547974c6c02'
         '6a3e55d20d4945ead133054127b1ba17'
         '81db405732d6b5e6fd057505be5b03c1'
         '25338963e6cbf51ea9c18bc7bd786a44'
         'bfc2a548805ba54bb81d8deaa5fcf406'
         'da5d4edacaf56bbb015987b334c49a46'
         '264aa5440cd50dd1d09b1f6c682dde95'
         '1a671a227b2713f0d7ede3b8c547e003'
         '4dfce580e8d6f889ef868cf1a85ddd89'
         'cded11a2ee85f1bfa704a8bcfd7063ac'
         'ca08979c98adc6b1c1086c1bf037d64a'
         '6d433d1b6395957c9c16eb9efe79c461'
         '412186cd09492af191ef5b8e5523997b'
         '9f5f15ba89ed4a7dd82b973616a84fb1'
         'ff817ed985260ba5b2284d70ede97cfc'
         'c6474a9d0a49b177b8d1daca5e3faf54'
         '81f1379b774a9762918b5bb6fd613a94'
         'dfb5c1393033518e12ae9ff91afeed97'
         '24383d610a424cc71c44b9fa923b9743'
         'c5918f13d25dd124ed8050b7177a6adf'
         '76ec5c28687220f39bac18fd2381ab29'
         '03263f5ebc6e38022c808689ff0f848c'
         'aaa66545fb46d23ea0b5537495b0c08e'
         '5e105d2fd7ad811f3afbe375ab8ef293'
         '955a91d8b73bd14f12afa4be13a45bca'
         'dbd1a1b2ea0777d1df361f973d53c27e'
         '03f0558bcbc1dafa4b29bcf1a69ca170'
         'd1a58f928165c099c01b8687e92889af'
         'a8b3385d0c7679b871cf9bcaf406ca26'
         '2fbeac1775ab8ce9096ee14d628c68e8'
         '450ac4b6e744b7ed8cd92e9852d0e80f'
         '31c56ec4e6de175c75b60e421f1381fc'
         '43d1335568bc7b6806ed5151e7b38243'
         'eaece7103da2f8b19967d5f4b5e7befc'
         '9b3abcfb1ccac0b863dcde99ab487d71'
         '343fcfdec8f55f2e227bb5e1a5804907'
         'ee9931bb007f935c0aaa16031c4319e7'
         'ee92083eea883b10946cee46ac529d21')
