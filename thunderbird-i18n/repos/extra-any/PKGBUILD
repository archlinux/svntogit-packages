# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Thomas Baechler <thomas@archlinux.org>

pkgbase=thunderbird-i18n
pkgver=24.6.0
pkgrel=1
pkgdesc="Language pack for Thunderbird"
arch=('any')
url="http://www.mozilla.com/"
license=('MPL' 'GPL')
depends=("thunderbird>=$pkgver")

_languages=(
  'ar     "Arabic"'
  'ast    "Asturian"'
  'be     "Belarusian"'
  'bg     "Bulgarian"'
  'bn-BD  "Bengali (Bangladesh)"'
  'br     "Breton"'
  'ca     "Catalan"'
  'cs     "Czech"'
  'da     "Danish"'
  'de     "German"'
  'el     "Greek"'
  'en-GB  "English (United Kingdom)"'
  'en-US  "English (United States)"'
  'es-AR  "Spanish (Argentina)"'
  'es-ES  "Spanish (Spain)"'
  'et     "Estonian"'
  'eu     "Basque"'
  'fi     "Finnish"'
  'fr     "French"'
  'fy-NL  "Frisian (Netherlands)"'
  'ga-IE  "Irish"'
  'gd     "Gaelic"'
  'gl     "Galician"'
  'he     "Hebrew"'
  'hr     "Croatian"'
  'hu     "Hungarian"'
  'hy-AM  "Armenian"'
  'id     "Indonesian"'
  'is     "Icelandic"'
  'it     "Italian"'
  'ja     "Japanese"'
  'ko     "Korean"'
  'lt     "Lithuanian"'
  'nb-NO  "Norwegian (Bokmal)"'
  'nl     "Dutch"'
  'nn-NO  "Norwegian (Nynorsk)"'
  'pa-IN  "Punjabi"'
  'pl     "Polish"'
  'pt-BR  "Brazilian Portuguese"'
  'pt-PT  "Portuguese"'
  'rm     "Romansh"'
  'ro     "Romanian"'
  'ru     "Russian"'
  'si     "Sinhalese"'
  'sk     "Slovak"'
  'sl     "Slovenian"'
  'sq     "Albanian"'
  'sr     "Serbian"'
  'sv-SE  "Swedish"'
  'ta-LK  "Tamil (Sri Lanka)"'
  'tr     "Turkish"'
  'uk     "Ukrainian"'
  'vi     "Vietnamese"'
  'zh-CN  "Chinese (simplified)"'
  'zh-TW  "Chinese (traditional)"'
)

pkgname=()
source=()
_url=https://ftp.mozilla.org/pub/mozilla.org/thunderbird/releases/$pkgver/linux-i686/xpi

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}
  _pkgname=thunderbird-i18n-${_locale,,}

  pkgname+=($_pkgname)
  source+=("thunderbird-i18n-$pkgver-$_locale.xpi::$_url/$_locale.xpi")
  eval "package_$_pkgname() {
    _package $_lang
  }"
done

# Don't extract anything
noextract=(${source[@]%%::*})

_package() {
  pkgdesc="$2 language pack for Thunderbird"
  install -Dm644 thunderbird-i18n-$pkgver-$1.xpi \
    "$pkgdir/usr/lib/thunderbird/extensions/langpack-$1@thunderbird.mozilla.org.xpi"
}

md5sums=('97252ee507ad8b999650ebd7792f82e5'
         '8a4676e4926282c18d62b9bc015557ed'
         '3414efc76dd0126ef26b14bd2b2b1c5c'
         'c03857128a68ddb34fafca0888a929c9'
         '5fa276aabc2a05b7a36aa801a1ac542a'
         '508a6e921305bccafe9540f4baf3948d'
         'ca74a112d8a4c2aa51f26c22fb2dff11'
         '323cd7bf902c87611ae57ab3ce58d207'
         'd54ce15b2120a20d2d48ba8cc1be1f6f'
         'd787a1f6bfaf1d96bdb4959a1a55b9a2'
         '2a84b4c0c2d4d93623c90077fcbc9acd'
         '37b81e21346d039c070b1db7a40bc15f'
         'f70af33367ff790c8c535be052e17127'
         'df6b41e8163914ecaeff54da489d310a'
         'd74ae28aa966db0e0167835ef7730aee'
         'bb787487cae4a214c284fff75ae4e0b3'
         '52dcc259dc65a0273abf62927a053339'
         '2d3640ae4323cd9746225f0751bc2897'
         '7a1e7ef22dafc2c0afcbfdbdbc7a7533'
         '4146cf5b8f0b0bd5cfa1eb689254375d'
         '9e451dbef1f699ff8ce4d7b95aeb01db'
         '63645712d4364f959b78d57bc7b80ac1'
         'ba1516aecf947bf13cdc8e33741475a4'
         'dccef7c6ffff74f771c640480ca9cc41'
         '66c50079a7bb16e4974d9d0a50144757'
         'f11c51e743e698c79dd5cf8bf3c9d50d'
         '6da3f951e9afa8d43880030a212d895b'
         '64d448dfdf20ddbfe0cd4dde74bf4326'
         '041d07884b6ae71ec2780045bcdbee76'
         '614a0fb97ffbe6d44a9e7a20b9f07ccb'
         '097d6ce905c9d518141465b6dd653986'
         '933c03c651637a714bdc0b7f8d70da26'
         '1b1c54e27d393c0d279c7d2d956d3ead'
         'b1fbc6babeddcf4ed26bd255ea7ce9f9'
         '86e68ce4534a90b9b83335b9166a4d43'
         '14edda05d8b442b4330bd620235d62a4'
         '5541342c17b1956d97c25aee2f4eeaf3'
         '0092179cc6adc5cc2d62adc1de4fe739'
         'b537937d1e99b51ae141f8cc0c88f5b6'
         'b8e03efdb3f658bc9f67214364658bd5'
         '65d561a3f83432abc1406ef117147d82'
         '35e00aa91be97eaceab4f6e45c4bc0f8'
         'c0e75520d41cb1d2060bdd88682e6b21'
         'af11e405b92dcbf7f0d826ae1b0d4633'
         'e7a73939108ea425a22986d180ebaf5c'
         'af08bcc42aaae872696417d173b27b3b'
         '255c2c457e30e93f2f0faecc2822d9d4'
         '0113d06aa86da3343c2132f151b929bb'
         'd4c9d98003333a80eb57f57d8ef36c4a'
         'a7849f78b5a19e7d08bb10c7c351c361'
         'a9e53caf3eee53f9750bf0704aedd72d'
         '6276b645d20be4ba978944844eee515c'
         '50bf6125e219c0a4ec7877314abf1bb7'
         '561ebc13c03048e4a627176575be8757'
         '6034cd5960e1bd56867bb671ebb9d21b')
