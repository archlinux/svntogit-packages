# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Thomas Baechler <thomas@archlinux.org>

pkgbase=thunderbird-i18n
pkgver=24.0.1
pkgrel=1
pkgdesc="Language pack for Thunderbird"
arch=('any')
url="http://www.mozilla.com/"
license=('MPL' 'GPL')
depends=("thunderbird>=$pkgver")

_languages=(
  'ar     "Arabic"'
  'ast    "Asturian"'
  'be     "Belarusian"'
  'bg     "Bulgarian"'
  'bn-BD  "Bengali (Bangladesh)"'
  'br     "Breton"'
  'ca     "Catalan"'
  'cs     "Czech"'
  'da     "Danish"'
  'de     "German"'
  'el     "Greek"'
  'en-GB  "English (United Kingdom)"'
  'en-US  "English (United States)"'
  'es-AR  "Spanish (Argentina)"'
  'es-ES  "Spanish (Spain)"'
  'et     "Estonian"'
  'eu     "Basque"'
  'fi     "Finnish"'
  'fr     "French"'
  'fy-NL  "Frisian (Netherlands)"'
  'ga-IE  "Irish"'
  'gd     "Gaelic"'
  'gl     "Galician"'
  'he     "Hebrew"'
  'hr     "Croatian"'
  'hu     "Hungarian"'
  'hy-AM  "Armenian"'
  'id     "Indonesian"'
  'is     "Icelandic"'
  'it     "Italian"'
  'ja     "Japanese"'
  'ko     "Korean"'
  'lt     "Lithuanian"'
  'nb-NO  "Norwegian (Bokmal)"'
  'nl     "Dutch"'
  'nn-NO  "Norwegian (Nynorsk)"'
  'pa-IN  "Punjabi"'
  'pl     "Polish"'
  'pt-BR  "Brazilian Portuguese"'
  'pt-PT  "Portuguese"'
  'rm     "Romansh"'
  'ro     "Romanian"'
  'ru     "Russian"'
  'si     "Sinhalese"'
  'sk     "Slovak"'
  'sl     "Slovenian"'
  'sq     "Albanian"'
  'sr     "Serbian"'
  'sv-SE  "Swedish"'
  'ta-LK  "Tamil (Sri Lanka)"'
  'tr     "Turkish"'
  'uk     "Ukrainian"'
  'vi     "Vietnamese"'
  'zh-CN  "Chinese (simplified)"'
  'zh-TW  "Chinese (traditional)"'
)

pkgname=()
source=()
_url=https://ftp.mozilla.org/pub/mozilla.org/thunderbird/releases/$pkgver/linux-i686/xpi

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}
  _pkgname=thunderbird-i18n-${_locale,,}

  pkgname+=($_pkgname)
  source+=("thunderbird-i18n-$pkgver-$_locale.xpi::$_url/$_locale.xpi")
  eval "package_$_pkgname() {
    _package $_lang
  }"
done

# Don't extract anything
noextract=(${source[@]%%::*})

_package() {
  pkgdesc="$2 language pack for Thunderbird"
  install -Dm644 thunderbird-i18n-$pkgver-$1.xpi \
    "$pkgdir/usr/lib/thunderbird/extensions/langpack-$1@thunderbird.mozilla.org.xpi"
}

md5sums=('e491d66d03fb853b4425cf88f39b9cd7'
         '328ca92dcc6ea0569427b6ab19f100d9'
         '21301ca33ff97b69f4a2bffef73bec37'
         'a7f59de9c7f248605b156820b0bb6974'
         '864f1f233b61274472c1430a0277e97e'
         'a8bd63364457bcefb99f343c7f585029'
         'aac6b57ff4698df7db0491c3ccd2ac58'
         'bc2a61b2594d500b06313adea2ebe66d'
         'bcfe2c729f93cc23da7768d11c76a84b'
         '54e2f129f10caebcf0590568e038c51a'
         'b60eebf56bbb3d3402f865ef746ce1ce'
         '1375578206739a9faa2cdd8f37fa5788'
         'd88a75548815bba75abed5f2d726b4d9'
         'e191337503ce93e8150143abf01105b9'
         '8cbf965eada8afe69e30053824c38428'
         '0c865889fa56a9718b0af6a9a05fec0c'
         '5429cae9da98f0fc34ac52e1dd42b83a'
         '2d109ea2d7a2a0169a146314c9236d42'
         '16ad858c98e3fa40243ec89e3013e929'
         'c439fc094aa6bee12045b33b468ec5ca'
         '2462886fd9514f0afcffbc989ce0dcc8'
         '7136930bcdacc89caad780d5cd4591d0'
         'aba303df908e2b950c5d8bc413084fb3'
         'ea205fb3a164e7bffde397adf2375423'
         'b45d679c483dcd2245ab737316d0fc1e'
         'c0ed961210957b3c1cdced6f8340d1ed'
         '1ddbda8ab5ec36f98478837387315611'
         '1fe1bfd47e9f3b9a3af54e060b898af7'
         'c54bb3111e8a50fab6013100b0e2635f'
         'b9bec5d174f096b6fd9962c1eb5b1c90'
         '4dfd0d7810e26169b1759996669305c8'
         '34a6e4fa48143975743286a108f2629b'
         '6c8af31093ab36ede20f7f384b9c56b4'
         '2bed578090504cb63b9b4686c92cd6cc'
         'ac3addf2043b60a7de3b6b67aa65ff2e'
         '54cd68900e93a11b07a8afb6aae1e1e4'
         '9ab73d3a1b2481c72adabaeded715291'
         '641602203d1a81e993bae85a895b30ee'
         '133165ef65d5ad7143d179e558ae30d1'
         'dff83b267bba7db40f9431f569020f0a'
         '4bbaf00d0658213bdbb50fd158cb6b15'
         '2dc4b70aae970a485ad2800fee46bef8'
         '57c212e133d94b2d92838b54a18e600f'
         'c3b7d91eb2bae2b6bb769db78c00d233'
         'e150a1e47e8d4a52e87d2128212cbe90'
         'e73577f5e872baba215c9876b31ef2ca'
         'a3d6f6b43135e13074451de41886b032'
         'd128b3faad606cf1f363efed4e3ebce1'
         '9bbbabc8a78868ca167c8b1f3dd38d9c'
         '70799519a3961de253a760db30b0aed6'
         '5bbbf240422baca65602fee7b7524e7c'
         'a0d5d7cc63c74c8fa469b3f1cbd4c82b'
         '4e80f24fdc262b53abf1803de49385e2'
         'cfaa6632f2d66ed194faf0108f71f65f'
         'b163237e5d63e6b9c927937f92a29a59')
