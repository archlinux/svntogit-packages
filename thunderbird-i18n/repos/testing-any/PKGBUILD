# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Thomas Baechler <thomas@archlinux.org>

pkgbase=thunderbird-i18n
pkgver=24.0
pkgrel=1
pkgdesc="Language pack for Thunderbird"
arch=('any')
url="http://www.mozilla.com/"
license=('MPL' 'GPL')
depends=("thunderbird>=$pkgver")

_languages=(
  'ar     "Arabic"'
  'ast    "Asturian"'
  'be     "Belarusian"'
  'bg     "Bulgarian"'
  'bn-BD  "Bengali (Bangladesh)"'
  'br     "Breton"'
  'ca     "Catalan"'
  'cs     "Czech"'
  'da     "Danish"'
  'de     "German"'
  'el     "Greek"'
  'en-GB  "English (United Kingdom)"'
  'en-US  "English (United States)"'
  'es-AR  "Spanish (Argentina)"'
  'es-ES  "Spanish (Spain)"'
  'et     "Estonian"'
  'eu     "Basque"'
  'fi     "Finnish"'
  'fr     "French"'
  'fy-NL  "Frisian (Netherlands)"'
  'ga-IE  "Irish"'
  'gd     "Gaelic"'
  'gl     "Galician"'
  'he     "Hebrew"'
  'hr     "Croatian"'
  'hu     "Hungarian"'
  'hy-AM  "Armenian"'
  'id     "Indonesian"'
  'is     "Icelandic"'
  'it     "Italian"'
  'ja     "Japanese"'
  'ko     "Korean"'
  'lt     "Lithuanian"'
  'nb-NO  "Norwegian (Bokmal)"'
  'nl     "Dutch"'
  'nn-NO  "Norwegian (Nynorsk)"'
  'pa-IN  "Punjabi"'
  'pl     "Polish"'
  'pt-BR  "Brazilian Portuguese"'
  'pt-PT  "Portuguese"'
  'rm     "Romansh"'
  'ro     "Romanian"'
  'ru     "Russian"'
  'si     "Sinhalese"'
  'sk     "Slovak"'
  'sl     "Slovenian"'
  'sq     "Albanian"'
  'sr     "Serbian"'
  'sv-SE  "Swedish"'
  'ta-LK  "Tamil (Sri Lanka)"'
  'tr     "Turkish"'
  'uk     "Ukrainian"'
  'vi     "Vietnamese"'
  'zh-CN  "Chinese (simplified)"'
  'zh-TW  "Chinese (traditional)"'
)

pkgname=()
source=()
_url=https://ftp.mozilla.org/pub/mozilla.org/thunderbird/releases/$pkgver/linux-i686/xpi

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}
  _pkgname=thunderbird-i18n-${_locale,,}

  pkgname+=($_pkgname)
  source+=("thunderbird-i18n-$pkgver-$_locale.xpi::$_url/$_locale.xpi")
  eval "package_$_pkgname() {
    _package $_lang
  }"
done

# Don't extract anything
noextract=(${source[@]%%::*})

_package() {
  pkgdesc="$2 language pack for Thunderbird"
  install -Dm644 thunderbird-i18n-$pkgver-$1.xpi \
    "$pkgdir/usr/lib/thunderbird/extensions/langpack-$1@thunderbird.mozilla.org.xpi"
}

md5sums=('00e5745138bd36a6dda6996ca82bb458'
         '2681d2dd9cba526734ac85a07bd72dff'
         'cd10d510639d2b4c524cd3a8fed30845'
         'b5d4c6176cb3d9181bc4958a59955371'
         'ec714abc51563df68c0e767499eb131b'
         'b3958d9eb8ff1074524c913eea9d4531'
         '6cd59396e969a9fe0c9c4494b05e0d70'
         'a9cb42920b2dc0a07df802f9720e2976'
         'ca88f9d29b53c5249e04007b27e6b6f1'
         '7800c97656d7c969d7d99efaf5449547'
         '52c26e8e74d054a9b0e8b785d05a1219'
         'da4d4b0a905905a6befdacfc93ce11ec'
         '7a1b3a80c2451cf388b1f394843d4d6e'
         'c9e1bb783a81d0a2cebe1ddec3a29c2b'
         'f586ca445a5e9cdb0cd8e5da5e301f75'
         'cda87ee3de3712dd7d98a31205cd0d78'
         '97dfd7b9bdd3ffc451874a7c918747a8'
         '3cf977ccd8b1f4b5215c7bec4e795063'
         'ce4b74e192d97b37b29bf725884bab77'
         '0e131941c0cb0e7c2ecac70092c931a5'
         '2140ad226acf709cbdf0a8323c704d97'
         'e539e40b18fb35398e88eadda0751256'
         'f7e0ff5bc085fa947d56677732ad40df'
         'e031bcb2d0df57a79d39aa8555e918ba'
         '87f1310bff7b978a8015362dc95dfeba'
         '2d3e0e115d0b9b1cd061beeb7cfb1039'
         'da16f65cb7095bf7ab50334d5310b46b'
         'b0ef1e6b1289376c25dfad11fd3b5825'
         'b33c0fc1a1b6d6f1b61e33e16a68c3aa'
         '91d710244168d0c1c769435a60b64f3c'
         '8debe2067c3ffdd910cfa0ad99ed62dd'
         '7b639bf8009edb028538722812d29c1f'
         '6ab044113bb056df256112716c4d7bc6'
         '1f1cfc93ae9c04d6062d824fdf89ef0a'
         'c13d3d3653cbb530aafd82ea3a455b31'
         '4f0acf27aa26483e161ad74c0bde0a0d'
         '0b19f435b174941580e423586330af30'
         '2c377b2b3dcbe5869c2db0dda2de06ed'
         'a7a04da21888fd15b755529b5bf2e36f'
         '9309c021fc49d3a0aee8df25d41f525a'
         '297f79f74a807063de05ed34159d7524'
         '35d94ffaa43bd3dc451723b392b2bd2a'
         '6028467f08ef13bd6b6a1de7348e06d3'
         'b2903194003960ed7189ce6834abfb07'
         '3841804189ed1a6bbf1a4845b5fdf53f'
         '58245e37b544aa0dd0ce79e383a6aa23'
         '5ee4beab35fd0adb3b250fb80a9b82a8'
         '3d702ac32f8184038d38b2dbc7e7c886'
         '27c5580bd0f7d45362e367c8b1675190'
         'a70ab64d7bc529083bfe5ae4be9a4c0c'
         'd7ca7f6c776958b22f97f59d7e28c647'
         '4beb1c99e43c4ea2fea8d9d877bcfe06'
         '67098d273f37d70f59f04b8be7a96595'
         '69dd513b2ea4472b5b651faf278ba836'
         'a62005875fba96de365a1432e6616842')
