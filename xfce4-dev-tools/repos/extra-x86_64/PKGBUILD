# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Tobias Kieslich <tobias funnychar archlinux.org>

pkgname=xfce4-dev-tools
pkgver=4.10.0
pkgrel=2
pkgdesc="Xfce developer tools"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('GPL2')
depends=('gtk-doc' 'automake' 'make' 'intltool' 'pkg-config')
source=(http://archive.xfce.org/src/xfce/xfce4-dev-tools/${pkgver%.*}/$pkgname-$pkgver.tar.bz2
        xfce4-dev-tools-4.10.0-remove-FORTIFY_SOURCE-2.patch)
sha256sums=('f79649bbfbbf16881494ee9f67035063765aec12e23ab9bac31de2c8a4f32bf8'
            '4ecfdf8cdb0940ce1044349bdb6646192dffd2b7f4e890f0ef5f00bfdfad7676')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -Np1 -i "$srcdir/xfce4-dev-tools-4.10.0-remove-FORTIFY_SOURCE-2.patch"
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
