# $Id: PKGBUILD 28363 2009-03-01 00:06:27Z pierre $
# Maintainer : Thomas Baechler <thomas@archlinux.org>

pkgname=nvidia-173xx
pkgver=173.14.30
_kernver='2.6.39-ARCH'
pkgrel=3
pkgdesc="NVIDIA drivers for kernel26, 173xx branch."
arch=('i686' 'x86_64')
[ "$CARCH" = "i686"   ] && ARCH=x86
[ "$CARCH" = "x86_64" ] && ARCH=x86_64
url="http://www.nvidia.com/"
depends=('kernel26>=2.6.39' 'kernel26<2.6.40' 'nvidia-173xx-utils')
makedepends=('kernel26-headers>=2.6.39' 'kernel26-headers<2.6.40')
conflicts=('nvidia-96xx' 'nvidia')
license=('custom')
install=nvidia.install
source=("http://download.nvidia.com/XFree86/Linux-$ARCH/${pkgver}/NVIDIA-Linux-$ARCH-${pkgver}-pkg0.run")
options=(!strip)
md5sums=('665f9f730c18ab24a997e0e14b863262')
[ "$CARCH" = "x86_64" ] && md5sums=('8c47463ae14a2f742d0fa00160456745')

build() {
	cd $srcdir
	sh NVIDIA-Linux-$ARCH-${pkgver}-pkg0.run --extract-only
	cd NVIDIA-Linux-$ARCH-${pkgver}-pkg0
	cd usr/src/nv/
	ln -s Makefile.kbuild Makefile
	make SYSSRC=/lib/modules/${_kernver}/build module
}

package() {
	cd $srcdir/NVIDIA-Linux-$ARCH-${pkgver}-pkg0/usr/src/nv/
	mkdir -p $pkgdir/lib/modules/${_kernver}/kernel/drivers/video/
	install -m644 nvidia.ko $pkgdir/lib/modules/${_kernver}/kernel/drivers/video/
        mkdir -p $pkgdir/etc/modprobe.d
        echo "blacklist nouveau" >> $pkgdir/etc/modprobe.d/nouveau_blacklist.conf
	sed -i -e "s/KERNEL_VERSION='.*'/KERNEL_VERSION='${_kernver}'/" $startdir/nvidia.install
        # gzip -9 module
        find "$pkgdir" -name '*.ko' -exec gzip -9 {} \;
}
