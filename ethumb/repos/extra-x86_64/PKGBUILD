# $Id$
# Maintainer: Ronald van Haren <ronald@archlinux.org>
# Contributor: Gustavo Sverzut Barbieri <barbieri@gmail.com>

pkgname=ethumb
pkgver=1.7.2
pkgrel=1
pkgdesc="Thumbnailing library and D-Bus service using EFL"
arch=('i686' 'x86_64')
url="http://www.enlightenment.org"
license=('BSD')
depends=('libexif' 'e_dbus'  'emotion')
options=('!libtool' '!emptydirs')
source=(http://download.enlightenment.org/releases/$pkgname-$pkgver.tar.gz)
sha1sums=('16d9a9908fb16df2b06f9084190a828683f72f8f')

build() {
  cd $srcdir/$pkgname-$pkgver

  export CFLAGS="-fvisibility=hidden -ffunction-sections -fdata-sections ${CFLAGS}"
  export CXXFLAGS="-fvisibility=hidden -ffunction-sections -fdata-sections ${CXXFLAGS}"
  export LDFLAGS="-fvisibility=hidden -ffunction-sections -fdata-sections -Wl,--gc-sections -Wl,--as-needed  ${LDFLAGS}"

  ./configure \
    --prefix=/usr \
    --libexecdir=/usr/lib/ethumb \
    --with-internal-maximum-log-level=3 \
    --disable-static \
    --enable-emotion \
    --disable-epdf \
    --enable-libexif \
    --disable-install-examples \
    --disable-doc
  make
}

package(){
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install

  # install license files
  install -Dm644 $srcdir/$pkgname-$pkgver/COPYING \
        $pkgdir/usr/share/licenses/$pkgname/COPYING
}
