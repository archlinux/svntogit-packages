# $Id$
# Maintainer:
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: Jeff 'codemac' Mickey <jeff@archlinux.org>
# Contributor: Dan McGee <dpmcgee@gmail.com>

pkgname=monotone
pkgver=0.46
pkgrel=2
pkgdesc="A modern distributed version control system, like hg, darcs, or git"
arch=('i686' 'x86_64')
url="http://www.monotone.ca"
license=('GPL')
depends=('botan' 'pcre' 'lua' 'sqlite3' 'libidn' 'zlib')
makedepends=('boost')
install=${pkgname}.install
source=(http://www.monotone.ca/downloads/$pkgver/$pkgname-$pkgver.tar.gz)
md5sums=('405286223efcf375cd022dabf2f1c845')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr \
    --sysconfdir=/etc || return 1
  make || return 1
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR="$pkgdir" install || return 1

  install -Dm644 contrib/monotone.bash_completion \
    "${pkgdir}/etc/bash_completion.d/monotone"
  install -Dm644 contrib/monotone.zsh_completion \
    "${pkgdir}/usr/share/zsh/site-functions/monotone"
}
