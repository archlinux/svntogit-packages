# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=knotes
pkgver=19.08.0
pkgrel=1
pkgdesc="Popup notes"
arch=(x86_64)
url="https://kontact.kde.org"
license=(GPL2)
depends=(kdepim-runtime kdnssd kontactinterface grantleetheme)
makedepends=(extra-cmake-modules kdoctools boost)
groups=(kde-applications kdepim)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('578afb9d879023db9fef4e58e326927d0e2e24edd1e1ac0d46eee968a0ab7030'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
