# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kgpg
pkgver=18.03.80
pkgrel=1
pkgdesc='A GnuPG frontend'
url='https://kde.org/applications/utilities/kgpg/'
arch=(x86_64)
license=(GPL LGPL FDL)
depends=(akonadi-contacts hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools python boost)
conflicts=(kdeutils-kgpg)
replaces=(kdeutils-kgpg)
groups=(kde-applications kdeutils)
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('0b34fceacf83e33dbd03e9b1f55848782eeebd894fc256533cca14943c0512a8'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
