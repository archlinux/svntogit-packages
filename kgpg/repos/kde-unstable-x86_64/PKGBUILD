# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kgpg
pkgver=20.11.90
pkgrel=1
pkgdesc='A GnuPG frontend'
url='https://kde.org/applications/utilities/kgpg/'
arch=(x86_64)
license=(GPL LGPL FDL)
depends=(akonadi-contacts)
makedepends=(extra-cmake-modules kdoctools boost)
groups=(kde-applications kde-utilities)
source=("https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('06d4fa1adb5b1178631e7d0691e23a87bb2b35b54792a6760af371803edb7cd9'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
