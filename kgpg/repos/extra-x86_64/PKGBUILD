# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kgpg
pkgver=20.08.3
pkgrel=1
pkgdesc='A GnuPG frontend'
url='https://kde.org/applications/utilities/kgpg/'
arch=(x86_64)
license=(GPL LGPL FDL)
depends=(akonadi-contacts)
makedepends=(extra-cmake-modules kdoctools boost)
groups=(kde-applications kde-utilities)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('a64d357872ad2d4a785047f1061aa5e9ebb1dca3c63b5d6279dd9f41950fe2c6'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
