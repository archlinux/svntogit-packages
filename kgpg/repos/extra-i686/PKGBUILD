# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kgpg
pkgver=17.04.1
pkgrel=1
pkgdesc='A GnuPG frontend'
url='https://kde.org/applications/utilities/kgpg/'
arch=(i686 x86_64)
license=(GPL LGPL FDL)
depends=(akonadi-contacts hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools python boost)
conflicts=(kdeutils-kgpg)
replaces=(kdeutils-kgpg)
groups=(kde-applications kdeutils)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('e934ad93b882603badc8e0fe7570062f62ecb13eb8b8cbaf2c79127166ceaebd'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
