# Maintainer: Dan McGee <dan@archlinux.org>

pkgname=pgbouncer
pkgver=1.5.4
pkgrel=6
pkgdesc="A lightweight connection pooler for PostgreSQL"
arch=('i686' 'x86_64')
url="http://pgfoundry.org/projects/pgbouncer"
license=('BSD')
depends=('libevent>=2.0' 'postgresql')
makedepends=('asciidoc' 'xmlto')
backup=('etc/pgbouncer/pgbouncer.ini' 'etc/logrotate.d/pgbouncer')
install=$pkgname.install
source=("http://ftp.postgresql.org/pub/projects/pgFoundry/$pkgname/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz"
        pgbouncer.ini
        pgbouncer.logrotate
        pgbouncer.service
        pgbouncer.tmpfiles.conf)
md5sums=('9ffaf2e6232e18e676651429813732df'
         '810813b9ef8891be382be9ffdd425edc'
         '6c65fe91821591bde0b5bcf70f23e959'
         'eede238f5ff3709dbfeef6dad3fbefc7'
         '472659f4c5de2b59a420f5ca42327ec1')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  install -D -m644 COPYRIGHT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -D -m644 ../pgbouncer.ini "$pkgdir/etc/pgbouncer/pgbouncer.ini"
  install -D -m644 ../pgbouncer.logrotate "$pkgdir/etc/logrotate.d/pgbouncer"
  install -D -m644 ../pgbouncer.service "$pkgdir/usr/lib/systemd/system/pgbouncer.service"
  install -D -m644 ../pgbouncer.tmpfiles.conf "$pkgdir/usr/lib/tmpfiles.d/pgbouncer.conf"
  mkdir -p "$pkgdir/var/log/pgbouncer"
}

# vim:set ts=2 sw=2 et:
