# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=mpfr
_pkgver=3.1.0
_patchlevel=p3
pkgver=${_pkgver}.${_patchlevel}
pkgrel=1
pkgdesc="Multiple-precision floating-point library"
arch=('i686' 'x86_64')
url="http://www.mpfr.org/"
license=('LGPL')
depends=('gmp>=5.0')
options=('!libtool')
install=mpfr.install
source=(http://www.mpfr.org/mpfr-current/mpfr-${_pkgver}.tar.xz{,.asc}
        mpfr-${_pkgver}.${_patchlevel}.patch)
md5sums=('6e495841bb026481567006cec0f821c3'
         '293374ee4b01527f8f7889fdfa9016f8'
         'd65858ccddbea968d6580124320fb6a0')

build() {
  cd "${srcdir}/${pkgname}-${_pkgver}"
  patch -Np1 -i $srcdir/mpfr-${_pkgver}.${_patchlevel}.patch
  ./configure --prefix=/usr --enable-thread-safe --enable-shared
  make
}

check() {
  cd "${srcdir}/${pkgname}-${_pkgver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${_pkgver}"
  make DESTDIR="${pkgdir}" install
}
