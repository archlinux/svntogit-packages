# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Sarah Hay <sarahhay@mb.sympatico.ca>

pkgname=xsane
pkgver=0.997
pkgrel=2
pkgdesc="A GTK-based X11 frontend for SANE and plugin for Gimp."
arch=(i686 x86_64)
url="http://www.xsane.org"
license=('GPL2')
makedepends=('gimp')
depends=('gtk2' 'lcms' 'sane' 'zlib' 'libjpeg')
install=$pkgname.install
source=(http://www.xsane.org/download/$pkgname-$pkgver.tar.gz)

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --mandir=/usr/share/man --disable-gimp
  /usr/bin/make || return 1
  /usr/bin/make DESTDIR="$pkgdir" install || return 1
  # build xsane with gimp
  make clean
  ./configure --prefix=/usr
  /usr/bin/make || return 1
  install -D -m755 "$srcdir/$pkgname-$pkgver/src/xsane" "$pkgdir/usr/bin/xsane-gimp" || return 1
  ## For making Gimp Plugin available 
  /bin/mkdir -p "$pkgdir/usr/lib/gimp/2.0/plug-ins"
  cd "$pkgdir/usr/lib/gimp/2.0/plug-ins/"
  /bin/ln -s ../../../../bin/xsane-gimp xsane
}
md5sums=('8377b3e3b792f3d2b7f13895467c7082')
