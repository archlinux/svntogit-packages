# $Id: PKGBUILD 87501 2013-04-02 00:06:04Z dreisner $
# Maintainer: Dave Reisner <d@falconindy.com>

pkgname=expac
pkgver=8
pkgrel=2
pkgdesc="pacman database extraction utility"
arch=('x86_64')
url="http://github.com/falconindy/expac"
license=('GPL')
depends=('pacman')
makedepends=('perl')
source=("https://pkgbuild.com/~dreisner/sources/$pkgname/$pkgname-$pkgver.tar.gz"{,.sig})
validpgpkeys=('487EACC08557AD082088DABA1EB2638FF56C0C53')  # Dave Reisner
md5sums=('ddecaf655df9c714e39ab786dde57640'
         'SKIP')

prepare() {
  cd "$pkgname-$pkgver"

  sed '/\*\//q' expac.c >LICENSE
}

build() {
  cd "$pkgname-$pkgver"

  make
}

package() {
  cd "$pkgname-$pkgver"

  make PREFIX=/usr DESTDIR="$pkgdir" install

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim: ft=sh syn=sh
