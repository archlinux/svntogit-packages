# Maintainer: Dave Reisner <d@falconindy.com>

pkgname=expac
pkgver=10
pkgrel=1
pkgdesc="pacman database extraction utility"
arch=('x86_64')
url="https://github.com/falconindy/expac"
license=('GPL')
depends=('pacman')
makedepends=('git' 'perl' 'meson')
source=("git+https://github.com/falconindy/expac?signed&tag=$pkgver")
validpgpkeys=('487EACC08557AD082088DABA1EB2638FF56C0C53')  # Dave Reisner
md5sums=('SKIP')

prepare() {
  cd "$pkgname"

  sed '/\*\//q' src/expac.c >LICENSE
}

build() {
  cd "$pkgname"

  meson build
  ninja -C build
}

package() {
  cd "$pkgname"

  DESTDIR=$pkgdir ninja -C build install

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim: ft=sh syn=sh
