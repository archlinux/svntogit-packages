# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=gnome-search-tool
pkgver=3.6.0
pkgrel=1
pkgdesc="Utility to search for files"
arch=(i686 x86_64)
url="http://gnome.org"
license=('GPL2')
depends=('nautilus' 'libsm')
makedepends=('intltool' 'yelp-tools')
install=$pkgname.install
options=('!emptydirs')
groups=('gnome-extra')
source=(http://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('a33000cd7d033be4ea50422f0f2cca611da5b79bd0f0875017f105a1bc177f42')

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
