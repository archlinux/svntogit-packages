# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgbase=glibmm
pkgname=('glibmm' 'glibmm-docs')
pkgver=2.30.1
pkgrel=1
arch=('i686' 'x86_64')
license=('LGPL')
makedepends=('glib2' 'libsigc++2.0' 'pkgconfig')
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
options=('!libtool')
url="http://www.gtkmm.org/"
sha256sums=('69582b584666cab1dc4253faffee548054730bc058e28466871e6a954d7ff897')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package_glibmm() {
  pkgdesc="Glib-- (glibmm) is a C++ interface for glib"
  depends=('glib2' 'libsigc++2.0')
  cd "$srcdir/$pkgbase-$pkgver"
  sed -i -e 's/^doc_subdirs/#doc_subdirs/' Makefile
  make DESTDIR="$pkgdir" install
}

package_glibmm-docs() {
  pkgdesc="Developer documentation for glibmm"
  cd "$srcdir/$pkgbase-$pkgver/docs"
  make DESTDIR="$pkgdir" install
}
