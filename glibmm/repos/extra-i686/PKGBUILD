# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgbase=glibmm
pkgname=('glibmm' 'glibmm-docs')
pkgver=2.32.1
pkgrel=1
arch=('i686' 'x86_64')
license=('LGPL')
makedepends=('glib2' 'libsigc++2.0' 'pkgconfig')
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
options=('!libtool')
url="http://www.gtkmm.org/"
sha256sums=('d6ed4d38e8739813941a0f0fec471c00ea93c5ed46179543340821c7ca798a2f')

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package_glibmm() {
  pkgdesc="Glib-- (glibmm) is a C++ interface for glib"
  depends=('glib2' 'libsigc++2.0')
  cd "$pkgbase-$pkgver"
  sed -i -e 's/^doc_subdirs/#doc_subdirs/' Makefile
  make DESTDIR="$pkgdir" install
}

package_glibmm-docs() {
  pkgdesc="Developer documentation for glibmm"
  cd "$pkgbase-$pkgver/docs"
  make DESTDIR="$pkgdir" install
}
