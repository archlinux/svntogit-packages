# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kmplot
pkgver=16.03.80
pkgrel=1
pkgdesc="Mathematical Function Plotter"
url="http://kde.org/applications/education/kmplot/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdeedu')
depends=('kdelibs4support' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'python' 'kdoctools')
install=${pkgname}.install
replaces=('kdeedu-kmplot')
conflicts=('kdeedu-kmplot')
source=("http://download.kde.org/unstable/applications/${pkgver}/src/kmplot-${pkgver}.tar.xz")
sha1sums=('145a6dfb160ddfb62481e33fef099d90e6ad5659')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
