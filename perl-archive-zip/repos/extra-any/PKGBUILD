# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Andrew Simmons <andrew.simmons@gmail.com>

pkgname=perl-archive-zip
pkgver=1.64
pkgrel=1
pkgdesc="Provide a perl interface to ZIP archive files"
arch=('any')
license=('GPL' 'PerlArtistic')
url="http://search.cpan.org/dist/Archive-Zip/"
depends=('perl')
checkdepends=('perl-test-mockmodule')
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/P/PH/PHRED/Archive-Zip-${pkgver}.tar.gz)
sha512sums=('7ae4c87ad50b30e764a79ada8f39ea2d698cfba957294d28dc15a2f3b2497923266a00574c5e43ddd59d6ec76d911f39007851920e0f09bfe3f466d46e17d6fc')

build() {
  cd Archive-Zip-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Archive-Zip-${pkgver}
  make test
}


package() {
  cd Archive-Zip-${pkgver}
  make DESTDIR="${pkgdir}" install
}
