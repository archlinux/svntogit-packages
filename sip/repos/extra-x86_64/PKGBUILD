# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=sip
pkgver=6.6.1
pkgrel=3
arch=(x86_64)
pkgdesc='A tool that makes it easy to create Python bindings for C and C++ libraries'
url='https://www.riverbankcomputing.com/software/sip/intro'
license=('custom:"sip"')
depends=(python-toml python-packaging python-setuptools python-ply)
source=(https://pypi.python.org/packages/source/s/sip/$pkgname-$pkgver.tar.gz
        https://www.riverbankcomputing.com/hg/sip/raw-rev/d43fc89f7ef2
        https://www.riverbankcomputing.com/hg/sip/raw-rev/0da96769835f
        https://www.riverbankcomputing.com/hg/sip/raw-rev/a069205270ea)
sha256sums=('696c575c72144122701171f2cc767fe6cc87050ea755a04909152a8508ae10c3'
            'a37813da5a4dd839dec5055b3f6e74c51f362dccb3e320841095eeb753c47562'
            '4d1ae102808431e5961f772ec7424f35c318a4310a430b7cab30fcf3fce29d7f'
            '130d0ae42a8c8f9e049ea0f90ace9aaf9743a2819f1ee869fbe58d23390cda2a')
options=(debug)

prepare() {
  cd $pkgname-$pkgver
# Fix broken parser
  patch -p1 < ../d43fc89f7ef2
  patch -p1 < ../0da96769835f
  patch -p1 < ../a069205270ea
}

build() {
  cd $pkgname-$pkgver
  python setup.py build
}

package() {
  cd $pkgname-$pkgver
  python setup.py install --skip-build --root="$pkgdir" --optimize=1
}
