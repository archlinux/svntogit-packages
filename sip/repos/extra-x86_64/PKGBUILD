# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=sip
pkgver=6.7.6
pkgrel=1
arch=(x86_64)
pkgdesc='A tool that makes it easy to create Python bindings for C and C++ libraries'
url='https://www.riverbankcomputing.com/software/sip/intro'
license=('custom:"sip"')
depends=(python-toml python-packaging python-setuptools python-ply)
makedepends=(python-build python-installer python-wheel)
source=(https://pypi.python.org/packages/source/s/sip/$pkgname-$pkgver.tar.gz)
sha256sums=('21d39b5b1956eefb912e93a4c10b9db252bc86302c36589742525345bfd2b2ea')

build() {
  cd $pkgname-$pkgver
  python -m build --wheel --no-isolation
}

package() {
  cd $pkgname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
