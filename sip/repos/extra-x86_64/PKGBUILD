# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=sip
pkgver=6.0.2
pkgrel=1
arch=(x86_64)
pkgdesc="A tool that makes it easy to create Python bindings for C and C++ libraries"
url='https://www.riverbankcomputing.com/software/sip/intro'
license=('custom:"sip"')
depends=(python-toml python-packaging)
makedepends=(python-setuptools)
conflicts=(sip5)
replaces=(sip5)
source=("https://pypi.python.org/packages/source/s/sip/sip-$pkgver.tar.gz")
sha256sums=('38633bfaa479b1dd35df8f414ffc5c5a342f174ca0c6a21d0e8eb359e33c35b9')

build() {
  cd sip-$pkgver
  python setup.py build
}

package() {
  cd sip-$pkgver
  python setup.py install --skip-build --root="$pkgdir" --optimize=1
}
