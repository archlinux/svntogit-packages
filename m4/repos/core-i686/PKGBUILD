# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>

pkgname=m4
pkgver=1.4.17
pkgrel=2
pkgdesc="The GNU macro processor"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/m4"
license=('GPL3')
groups=('base-devel')
depends=('glibc' 'bash')
source=(ftp://ftp.gnu.org/gnu/m4/$pkgname-$pkgver.tar.xz{,.sig}
        m4-1.4.17-perl-make-check.patch)
md5sums=('12a3c829301a4fd6586a57d3fcf196dc'
         'SKIP'
         '4e4df1abc05f425ec0dc3bc2b692b35f')
validpgpkeys=('CFE2BE707B538E8B26757D84151308092983D606')   # Gary Vaughan


prepare() {
  cd $pkgname-$pkgver

  # fix testsuite failures with newer perl
  patch -p1 -i $srcdir/m4-1.4.17-perl-make-check.patch
}

build() {
  cd $pkgname-$pkgver  
  ./configure --prefix=/usr
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make prefix=${pkgdir}/usr install
}
