# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=bug-buddy
pkgver=2.30.0
pkgrel=1
pkgdesc="A bug reporting tool for GNOME"
arch=(i686 x86_64)
install=bug-buddy.install
depends=('evolution-data-server>=2.29.92' 'libgtop>=2.28.0' 'libelf' 'hicolor-icon-theme' 'libbonobo>=2.24.2')
makedepends=('intltool' 'pkgconfig' 'gnome-doc-utils>=0.19.5')
groups=('gnome-extra')
license=(GPL)
options=('!libtool' '!emptydirs')
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.30/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('6e81a55bd252f552aa4aea180b8dbceb0898d2bf173e330c5c09f8fbdba66b05')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain bug-buddy ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
