# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Paul Mattal <paul@archlinux.org>
# Contributor: Andrew Wright <andreww@photism.org>
# Contributor: Andreas W. Hauser <andy-aur@splashground.de>
# Contributor: Marco Crosio <marco.crosio@gmail.com>

pkgbase=eclipse
pkgname=(eclipse eclipse-source)
pkgver=4.4.0
pkgrel=1
_release=luna-R
pkgdesc="An IDE for Java and other languages"
license=("EPL")
arch=('i686' 'x86_64')
url="http://eclipse.org"
depends=('java-environment' 'gtk2' 'unzip' 'webkitgtk2' 'libxtst')
source=("http://ftp-stud.fht-esslingen.de/pub/Mirrors/eclipse/technology/epp/downloads/release/${_release/-//}/$pkgname-standard-$_release-linux-gtk.tar.gz"
        "http://ftp-stud.fht-esslingen.de/pub/Mirrors/eclipse/technology/epp/downloads/release/${_release/-//}/$pkgname-standard-$_release-linux-gtk-x86_64.tar.gz"
        'eclipse.sh' 'eclipse.desktop')
md5sums=('cc700cc2eb164f5da3563e8c5157b373'
         '44a8a5ae5e74da7b3764da774a505632'
         'e07b2ccfc4689c288fd876a4beb8403d'
         'ba2cf02c48e6e35bfe3685401c26bb5b')

if (( ! GENINTEG )); then
  if [[ $CARCH == x86_64 ]]; then
    source=("${source[@]:1}")
    md5sums=("${md5sums[@]:1}")
  else
    source=("${source[0]}" "${source[@]:2}")
    md5sums=("${md5sums[0]}" "${md5sums[@]:2}")
  fi
fi

prepare() {
  mkdir -p eclipse-source/{features,plugins}
  mv eclipse/features/*.source_* eclipse-source/features
  mv eclipse/plugins/*.source_*  eclipse-source/plugins
}

package_eclipse() {
  install=eclipse.install

  install -d "$pkgdir/usr/share"
  cp -a eclipse "$pkgdir/usr/share/eclipse"

  install -D eclipse.sh "$pkgdir/usr/bin/eclipse"
  install -Dm644 eclipse.desktop "$pkgdir/usr/share/applications/eclipse.desktop"

  for _i in 16 32 48 256; do
    install -Dm644 eclipse/plugins/org.eclipse.platform_*/eclipse${_i}.png \
      "$pkgdir/usr/share/icons/hicolor/${_i}x${_i}/apps/eclipse.png"
  done
}

package_eclipse-source() {
  pkgdesc="Source components of the Eclipse IDE"
  depends=('eclipse')

  install -d "$pkgdir/usr/share"
  cp -a eclipse-source "$pkgdir/usr/share/eclipse"
}
