# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Paul Mattal <paul@archlinux.org>
# Contributor: Andrew Wright <andreww@photism.org>
# Contributor: Andreas W. Hauser <andy-aur@splashground.de>
# Contributor: Marco Crosio <marco.crosio@gmail.com>

pkgname=eclipse
pkgver=3.7.2
pkgrel=3
_date=201202080800
pkgdesc="An IDE for Java and other languages"
arch=('i686' 'x86_64')
url="http://eclipse.org"
depends=('java-environment' 'gtk2' 'unzip' 'libwebkit' 'libxtst')
install=${pkgname}.install
makedepends=('zip')
conflicts=('xulrunner')
license=("EPL/1.1")
source=("ftp://ftp.osuosl.org/pub/eclipse/eclipse/downloads/drops/R-$pkgver-${_date}/$pkgname-SDK-$pkgver-linux-gtk.tar.gz"
	 'eclipse.sh' 'eclipse.desktop' 'eclipse.svg')
md5sums=('79b90faa1ee6e7af1910c3a5077b594f'
         '7ea99a30fbaf06ec29261541b8eb1e23'
         'ba8a37e30a7ebd69774cec87c69e8c44'
         '77cff7543ccf7e177cb6667f5a20ce19')
[ "$CARCH" = "x86_64" ] && source[0]="ftp://ftp.osuosl.org/pub/eclipse/eclipse/downloads/drops/R-$pkgver-${_date}/$pkgname-SDK-$pkgver-linux-gtk-$CARCH.tar.gz"
[ "$CARCH" = "x86_64" ] && md5sums[0]='6a0fd32cb6a986032a67defab3753476'

package() {
  cd "$srcdir/eclipse"

  # install eclipse
  install -m755 -d "$pkgdir/usr/share"
  mv "$srcdir/eclipse" "$pkgdir/usr/share/"

  # install misc
  install -d $pkgdir/usr/bin $pkgdir/usr/share/applications \
    $pkgdir/usr/share/icons/hicolor/{16x16,32x32,48x48}/apps
  install -m755 "$srcdir/eclipse.sh" "$pkgdir/usr/bin/eclipse"
  install -m644 "$srcdir/eclipse.desktop" "$pkgdir/usr/share/applications/"
  ln -s /usr/share/eclipse/plugins/org.eclipse.sdk_$pkgver.v${_date}/eclipse.png \
    "$pkgdir/usr/share/icons/hicolor/16x16/apps/eclipse.png"
  ln -s /usr/share/eclipse/plugins/org.eclipse.sdk_$pkgver.v${_date}/eclipse32.png \
    "$pkgdir/usr/share/icons/hicolor/32x32/apps/eclipse.png"
  ln -s /usr/share/eclipse/plugins/org.eclipse.sdk_$pkgver.v${_date}/eclipse48.png \
    "$pkgdir/usr/share/icons/hicolor/48x48/apps/eclipse.png"

  # install icon
  install -Dm644 "$srcdir"/eclipse.svg \
    "$pkgdir"/usr/share/icons/hicolor/scalable/apps/eclipse.svg
  sed -i "s|#!/usr/bin/python|#!/usr/bin/python2|" "$pkgdir"/usr/share/eclipse/plugins/org.apache.ant_1.8.2.v20120109-1030/bin/runant.py
}
