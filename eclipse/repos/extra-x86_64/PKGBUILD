# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Paul Mattal <paul@archlinux.org>
# Contributor: Andrew Wright <andreww@photism.org>
# Contributor: Andreas W. Hauser <andy-aur@splashground.de>
# Contributor: Marco Crosio <marco.crosio@gmail.com>

pkgname=eclipse
pkgver=4.3.1
pkgrel=1
_date=201309111000
pkgdesc="An IDE for Java and other languages"
arch=('i686' 'x86_64')
url="http://eclipse.org"
depends=('java-environment' 'gtk2' 'unzip' 'webkitgtk2' 'libxtst')
install=${pkgname}.install
makedepends=('zip')
license=("EPL/1.1")
source=("ftp://ftp.osuosl.org/pub/eclipse/eclipse/downloads/drops4/R-$pkgver-${_date}/$pkgname-SDK-$pkgver-linux-gtk.tar.gz"
	 'eclipse.sh' 'eclipse.desktop' 'eclipse.svg')
md5sums=('04880311281ff9de71a7d1def630b29e'
         '7ea99a30fbaf06ec29261541b8eb1e23'
         'ba2cf02c48e6e35bfe3685401c26bb5b'
         '77cff7543ccf7e177cb6667f5a20ce19')
[ "$CARCH" = "x86_64" ] && source[0]="ftp://ftp.osuosl.org/pub/eclipse/eclipse/downloads/drops4/R-$pkgver-${_date}/$pkgname-SDK-$pkgver-linux-gtk-$CARCH.tar.gz"
[ "$CARCH" = "x86_64" ] && md5sums[0]='61e44d550751d3b176fbe56dd0f75060'

package() {
  # install eclipse
  install -m755 -d "$pkgdir/usr/share"
  mv eclipse "$pkgdir/usr/share/"

  # install misc
  install -d $pkgdir/usr/bin $pkgdir/usr/share/applications \
    $pkgdir/usr/share/icons/hicolor/apps
  install -m755 eclipse.sh "$pkgdir/usr/bin/eclipse"
  install -m644 eclipse.desktop "$pkgdir/usr/share/applications/"
  # install icon
  install -Dm644 "$srcdir"/eclipse.svg \
    "$pkgdir"/usr/share/icons/hicolor/scalable/apps/eclipse.svg
  sed -i "s|#!/usr/bin/python|#!/usr/bin/python2|" "$pkgdir"/usr/share/eclipse/plugins/org.apache.ant_1.8.4.v201303080030/bin/runant.py
}
