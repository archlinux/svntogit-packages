# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeutils-kwalletmanager
pkgver=14.12.2
pkgrel=1
pkgdesc='Wallet Management Tool'
url='http://kde.org/applications/system/kwalletmanager/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
groups=('kde' 'kdeutils')
conflicts=('kdeutils-kwallet')
replaces=('kdeutils-kwallet')
install=${pkgname}.install
source=("http://download.kde.org/stable/applications/${pkgver}/src/kwalletmanager-${pkgver}.tar.xz")
sha1sums=('a274707c64f464d017bac7b0f9fd478b656c2a71')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../kwalletmanager-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
