# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeutils-kwalletmanager
pkgver=4.13.95
pkgrel=1
pkgdesc='Wallet Management Tool'
url='http://kde.org/applications/system/kwalletmanager/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
groups=('kde' 'kdeutils')
conflicts=('kdeutils-kwallet')
replaces=('kdeutils-kwallet')
install=${pkgname}.install
source=("http://download.kde.org/unstable/${pkgver}/src/kwalletmanager-${pkgver}.tar.xz")
sha1sums=('bc7cf9ea896d505ccabaf6b5b997a0b0ecfa0396')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../kwalletmanager-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
