# Maintainer: Florian Pritz <bluewind@xinu.at>
# Contributor: tobias <tobias@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>
# Contributor: Firmicus <francois.archlinux.org>

pkgname=perl-image-exiftool
_srcname=Image-ExifTool
pkgver=12.40
pkgrel=1
pkgdesc="Reader and rewriter of EXIF informations that supports raw files"
arch=('any')
url="https://search.cpan.org/perldoc?exiftool"
license=('GPL')
depends=('perl')
optdepends=('perl-archive-zip: Support for ZIP-files, including EPUB')
provides=("perl-exiftool=$pkgver")
replaces=("perl-exiftool")
options=('!emptydirs')
source=(https://exiftool.org/${_srcname}-$pkgver.tar.gz)
md5sums=('72b40d69cf518edebbf5b661465950e7')

build() {
  cd "${srcdir}/${_srcname}-${pkgver}"
  # install module in vendor directories.
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "${srcdir}/${_srcname}-${pkgver}"
  make test
}

package() {
  cd "${srcdir}/${_srcname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -dm755 "$pkgdir/usr/share/$pkgname"
  cp -r fmt_files config_files arg_files "$pkgdir/usr/share/$pkgname"
}
