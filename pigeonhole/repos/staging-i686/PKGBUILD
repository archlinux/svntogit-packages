# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

# Contributorr: Peter Lewis <plewis@aur.archlinux.org>
# Contributor: Sebastian KÃ¶hler <sebkoehler@whoami.org.uk>

# This must be built against the version of dovecot being used,
# else mail delivery will fail.
# Specify the version of dovecot to be used here:
_dcpkgver=2.1.12
# Make sure to bump pkgrel if changing this.

pkgname=pigeonhole
pkgver=0.3.3
pkgrel=3
pkgdesc="Fully rewritten Sieve implementation for Dovecot v2.1"
arch=('i686' 'x86_64')
url="http://pigeonhole.dovecot.org/"
license=('LGPL')
depends=('dovecot='$_dcpkgver)
conflicts=('dovecot-sieve' 'pigeonhole-hg')
source=("http://www.rename-it.nl/dovecot/2.1/dovecot-2.1-$pkgname-$pkgver.tar.gz"{,.sig}
        "dovecot.conf")
options=('!libtool')
sha256sums=('1d581f6292c6e760b26a06193a0af266ba3b6f3a0431e6617fcf6e1c84f7dd0e'
            '8604f9c52cff28506873cf4d0026c57ef73def5f05877126ea561774f1b8ffec'
            'a457a1691cfa82495fc0503bfa4b61e54b149e63400fe0f568dff2c24a3f7858')

build() {
  cd "$srcdir/dovecot-2.1-$pkgname-$pkgver"

  ./configure --prefix=/usr \
    --with-dovecot=/usr/lib/dovecot \
    --disable-static
  make
}

package() {
  cd "$srcdir/dovecot-2.1-$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
  install -m 644 -D "$srcdir/dovecot.conf" "$pkgdir/etc/ld.so.conf.d/dovecot.conf"
}
