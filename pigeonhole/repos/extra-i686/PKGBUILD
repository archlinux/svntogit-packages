# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

# Contributorr: Peter Lewis <plewis@aur.archlinux.org>
# Contributor: Sebastian KÃ¶hler <sebkoehler@whoami.org.uk>

# This must be built against the version of dovecot being used,
# else mail delivery will fail.
# Specify the version of dovecot to be used here:
_dcpkgver=2.2.21
# Make sure to bump pkgrel if changing this.

pkgname=pigeonhole
pkgver=0.4.10
pkgrel=1
pkgdesc="Sieve implementation for Dovecot"
arch=('i686' 'x86_64')
url="http://pigeonhole.dovecot.org/"
license=('LGPL')
depends=('dovecot='$_dcpkgver)
conflicts=('dovecot-sieve' 'pigeonhole-hg')
source=("http://pigeonhole.dovecot.org/releases/2.2/dovecot-2.2-$pkgname-$pkgver.tar.gz"{,.sig}
        "dovecot.conf")
sha256sums=('e9dfe7160fd3b465d6d5068f219122626ef790cce507e290cd09852c7d90726f'
            'SKIP'
            'a457a1691cfa82495fc0503bfa4b61e54b149e63400fe0f568dff2c24a3f7858')
validpgpkeys=('42F3CD50D4F25A41833BEE3704D62B1E3DFBB4F4') # Stephan Bosch <stephan@rename-it.nl>

build() {
  cd dovecot-2.2-$pkgname-$pkgver

  ./configure --prefix=/usr \
    --with-dovecot=/usr/lib/dovecot \
    --with-moduledir=/usr/lib/dovecot/modules \
    --disable-static
  make
}

check() {
  cd dovecot-2.2-$pkgname-$pkgver
  make check
}

package() {
  cd dovecot-2.2-$pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
  install -m 644 -D "$srcdir/dovecot.conf" "$pkgdir/etc/ld.so.conf.d/dovecot.conf"
}
