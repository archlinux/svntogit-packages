# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

# Contributorr: Peter Lewis <plewis@aur.archlinux.org>
# Contributor: Sebastian KÃ¶hler <sebkoehler@whoami.org.uk>

# This must be built against the version of dovecot being used,
# else mail delivery will fail.
# Specify the version of dovecot to be used here:
_dcpkgver=2.2.13
# Make sure to bump pkgrel if changing this.

pkgname=pigeonhole
pkgver=0.4.3
pkgrel=1
pkgdesc="Sieve implementation for Dovecot"
arch=('i686' 'x86_64')
url="http://pigeonhole.dovecot.org/"
license=('LGPL')
depends=('dovecot='$_dcpkgver)
conflicts=('dovecot-sieve' 'pigeonhole-hg')
source=("http://pigeonhole.dovecot.org/releases/2.2/dovecot-2.2-$pkgname-$pkgver.tar.gz"{,.sig}
        "dovecot.conf")
sha256sums=('d692f4ab077bdf2c43c98829ed24b2e6f148db488ef8ba429b430394d8b4d757'
            'SKIP'
            'a457a1691cfa82495fc0503bfa4b61e54b149e63400fe0f568dff2c24a3f7858')

build() {
  cd "$srcdir/dovecot-2.2-$pkgname-$pkgver"

  ./configure --prefix=/usr \
    --with-dovecot=/usr/lib/dovecot \
    --with-moduledir=/usr/lib/dovecot/modules \
    --disable-static
  make
}

package() {
  cd "$srcdir/dovecot-2.2-$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
  install -m 644 -D "$srcdir/dovecot.conf" "$pkgdir/etc/ld.so.conf.d/dovecot.conf"
}
