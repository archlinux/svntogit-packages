# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=plasma-browser-integration
pkgver=5.19.90
pkgrel=1
pkgdesc='Components necessary to integrate browsers into the Plasma Desktop'
arch=(x86_64)
url='https://www.kde.org/plasma-desktop'
license=(GPL)
depends=(krunner purpose kfilemetadata)
makedepends=(extra-cmake-modules)
groups=(plasma)
source=("https://download.kde.org/unstable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('8159392459bbe093f926639ca1dc210bf0ec16f482ace1b49c5e35cff33e36c5'
            'SKIP')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

build() { 
  cmake -B build -S $pkgname-$pkgver \
    -DINSTALL_CHROME_MANIFEST=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
