# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Antonio Rojas

pkgname=plasma-workspace-wallpapers
pkgver=5.1.2
pkgrel=1
pkgdesc='Additional wallpapers for the Plasma Workspace'
arch=('any')
url='https://projects.kde.org/projects/kde/workspace/plasma-workspace'
license=('LGPL')
makedepends=('extra-cmake-modules')
groups=('plasma-next')
source=("http://download.kde.org/stable/plasma/${pkgver}/$pkgname-$pkgver.tar.xz")
md5sums=('ded5fa7e54ad45d4ab1e5968ef9ccc72')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
