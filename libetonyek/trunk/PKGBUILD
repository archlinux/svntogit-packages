# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Yue

pkgname=libetonyek
pkgver=0.1.9
pkgrel=1
pkgdesc='Library and a set of tools for reading and converting Apple Keynote presentations'
arch=('x86_64')
url="https://wiki.documentfoundation.org/DLP/Libraries/libetonyek"
license=('MPL')
depends=('libxml2' 'zlib' 'libwpd' 'boost-libs' 'librevenge' 'liblangtag')
makedepends=('boost' 'cppunit' 'gperf' 'doxygen' 'glm' 'mdds')
source=(https://dev-www.libreoffice.org/src/$pkgname/$pkgname-$pkgver.tar.xz)
sha256sums=('e61677e8799ce6e55b25afc11aa5339113f6a49cff031f336e32fa58635b1a4a')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --with-mdds=1.4
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
