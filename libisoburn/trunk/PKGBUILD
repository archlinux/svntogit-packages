# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Gour <Gour <gour@mail.inet.hr>

pkgname=libisoburn
pkgver=1.5.4 #.pl01
pkgrel=2
pkgdesc="frontend for libraries libburn and libisofs"
url="https://dev.lovelyhq.com/libburnia"
arch=('x86_64')
license=('GPL2')
depends=('libburn' 'libisofs' 'readline')
optdepends=('tk:      for xorriso-tcltk frontend'
            'sudo:    for use with xorriso-dd-target')
            #'bwidget: for xorriso-tcltk frontend') # AUR
provides=('xorriso' 'xorriso-tcltk')
source=(https://files.libburnia-project.org/releases/${pkgname}-${pkgver}.tar.gz{,.sig})
sha256sums=('2d89846d43880f17fa591c53b3bea42ffb803628e4e630c680fc2c9184f79132'
            'SKIP')
validpgpkeys=('44BC9FD0D688EB007C4DD029E9CBDFC0ABC0A854') # Thomas Schmitt <scdbackup@gmx.net>

build() {
  cd "${pkgname}"-${pkgver/.pl01/}
  ./configure --prefix=/usr
  make
}

package() {
  cd "${pkgname}"-${pkgver/.pl01/}
  make DESTDIR="${pkgdir}" install
}
