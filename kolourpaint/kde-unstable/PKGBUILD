# $Id: PKGBUILD 271532 2016-07-12 17:20:33Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kolourpaint
pkgver=16.07.90
pkgrel=1
pkgdesc="Paint Program"
url="http://kde.org/applications/graphics/kolourpaint/"
arch=(i686 x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kdegraphics)
depends=(kdelibs4support libksane hicolor-icon-theme)
makedepends=(extra-cmake-modules kdesignerplugin kdoctools)
conflicts=(kdegraphics-kolourpaint)
replaces=(kdegraphics-kolourpaint)
source=("http://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
sha1sums=('936413a195c8380721166fc90620e3b3f4ff9bfc')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
