# $Id$
# Contributor: Juergen Hoetzel <juergen@hoetzel.info>
# Contributor: basilburn (basilburn), Paul Bredbury (brebs) 

pkgname=privoxy
pkgver=3.0.16
pkgrel=1
pkgdesc="A web proxy with advanced filtering capabilities"
arch=('i686' 'x86_64')
url="http://www.privoxy.org"
license=('GPL')
depends=('pcre' 'zlib')
makedepends=('autoconf')
backup=(etc/privoxy/{config,trust,default.action,user.filter,default.filter} \
        etc/privoxy/{match-all,user}.action etc/logrotate.d/privoxy)
install=privoxy.install
source=(http://downloads.sourceforge.net/ijbswa/$pkgname-$pkgver-stable-src.tar.gz
	'privoxy'
        'privoxy.logrotated'
        'privoxy.confd')
md5sums=('64d3ffcdf8307e04a375773bb4eb255e'
         '3c0f0c3ce941d74f40ddf0f858f7ba8d'
         'a364c34c8dc6eb78c9a6b777237932de'
         'c27cf5aaf0cf89825fd0f7a5a4ebd7c0')

build() {
  GROUP_ID=42
  cd $srcdir/$pkgname-$pkgver-stable
  autoheader && autoconf
  ./configure --prefix=/usr --sysconfdir=/etc/privoxy
  make || return 1
  make prefix=$pkgdir/usr MAN_DEST=$pkgdir/usr/share/man/man1 \
    GROUP=$GROUP_ID CONF_BASE=$pkgdir/etc/privoxy VAR_DEST=$pkgdir/var \
    install

  rm $pkgdir/var/log/privoxy/* # don't overwrite existing logfiles!
  install -D -m755 ${srcdir}/privoxy $pkgdir/etc/rc.d/privoxy
  install -D -m644 ${srcdir}/privoxy.logrotated $pkgdir/etc/logrotate.d/privoxy
  install -D -m644 ${srcdir}/privoxy.confd $pkgdir/etc/conf.d/privoxy
  # fix the config paths
  sed -i \
    -e 's|^confdir.*$|confdir /etc/privoxy|' \
    -e 's|^logdir.*$|logdir /var/log/privoxy|' \
    -e '/^user-manual/s/.*/#user-manual \/usr\/share\/doc\/privoxy\/user-manual\//' \
    $pkgdir/etc/privoxy/config

  # fix permissions
  chgrp -R $GROUP_ID $pkgdir/etc/privoxy/
  find $pkgdir/etc/privoxy/ -type d | xargs chmod 770
  find $pkgdir/etc/privoxy/ -type f | xargs chmod 660
  chmod 770 $pkgdir/var/log/privoxy
  chgrp -R ${GROUP_ID} $pkgdir/var/log/privoxy/
}
