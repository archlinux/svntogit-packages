# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Michel Brabants <michel.linux@tiscali.be>

pkgname=seahorse
pkgver=3.20.0
pkgrel=2
pkgdesc="GNOME application for managing PGP keys."
arch=(i686 x86_64)
license=(GPL)
url="http://projects.gnome.org/seahorse/"
depends=(gtk3 gcr libsecret libsoup desktop-file-utils dconf gpgme gnome-keyring)
makedepends=(libldap intltool yelp-tools gobject-introspection openssh vala)
optdepends=('openssh: SSH support')
provides=(x11-ssh-askpass)
options=('!emptydirs')
groups=(gnome-extra)
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('e2b07461ed54a8333e5628e9b8e517ec2b731068377bf376570aad998274c6df')

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --enable-ldap --enable-hkp \
      --disable-schemas-compile
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
