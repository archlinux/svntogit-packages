# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Michel Brabants <michel.linux@tiscali.be>

pkgname=seahorse
pkgver=3.10.2
pkgrel=1
pkgdesc="GNOME application for managing PGP keys."
arch=(i686 x86_64)
license=(GPL)
url="http://projects.gnome.org/seahorse/"
depends=(gtk3 gcr libsecret libsoup gpgme desktop-file-utils hicolor-icon-theme dconf)
makedepends=(libldap intltool yelp-tools gobject-introspection openssh libsm)
optdepends=('openssh: SSH support')
provides=(x11-ssh-askpass)
options=('!emptydirs')
groups=(gnome-extra)
install=seahorse.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz
        seahorse-ssh-askpass.sh)
sha256sums=('89cabf19f77a55f220bc61a3b97e4db845a0980f0f1d9c66147cc9a4ced8cd16'
            '400ea9a6084acc93f1dbd4223b95622bbd48071b763063328efba00de378b68c')

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --enable-ldap --enable-hkp \
      --disable-schemas-compile
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm755 "$srcdir/seahorse-ssh-askpass.sh" \
    "$pkgdir/etc/profile.d/seahorse-ssh-askpass.sh"
}
