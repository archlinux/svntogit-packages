# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Michel Brabants <michel.linux@tiscali.be>

pkgname=seahorse
pkgver=42.0
pkgrel=1
epoch=1
pkgdesc="GNOME application for managing PGP keys."
url="https://wiki.gnome.org/Apps/Seahorse"
arch=(x86_64)
license=(GPL)
depends=(gtk3 gcr libsecret libsoup gpgme org.freedesktop.secrets libpwquality
         libhandy)
makedepends=(libldap yelp-tools gobject-introspection vala git meson)
provides=(x11-ssh-askpass)
options=(debug)
_commit=e16028469dc1ee3c627c2fd9d3a8df7955dc38ec  # tags/42.0^0
source=("git+https://gitlab.gnome.org/GNOME/seahorse.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd seahorse
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd seahorse
}

build() {
  arch-meson seahorse build -D manpage=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
