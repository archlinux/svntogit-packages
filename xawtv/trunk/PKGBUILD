# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Sarah Hay <sarahhay@mb.sympatico.ca>
# Contributor: Jason Chu <jason@archlinux.org>

pkgname=xawtv
pkgver=3.104+3+gc438af7
pkgrel=1
pkgdesc="A simple Xaw-based TV program which uses the bttv driver or video4linux"
arch=('i686' 'x86_64')
url="https://git.linuxtv.org/xawtv3.git"
license=('GPL')
depends=('aalib' 'v4l-utils' 'libxv' 'libxxf86dga' 'libxrandr' 'libgl' 'libdv' 'zvbi' 'libxinerama' 'lirc' 'libxft' 'libxaw' 'xorg-fonts-misc' 'libxxf86vm')
makedepends=('mesa' git)
_commit=c438af7955049fe4e77385985715605e48a4fadb  # master
source=("git+https://git.linuxtv.org/cgit.cgi/xawtv3.git#commit=$_commit")
md5sums=('SKIP')

pkgver() {
  cd xawtv3
  git describe --tags | sed 's/^xawtv-//;s/-/+/g'
}

prepare() {
  cd xawtv3
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd "${srcdir}/xawtv3"
  ./configure --prefix=/usr --disable-motif --disable-quicktime
  make
}

package() {
  cd "${srcdir}/xawtv3"
  make DESTDIR="${pkgdir}" install
}
