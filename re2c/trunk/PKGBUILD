# Maintainer:
# Contributor: nut543 <kfs1@online.no>
# Contributor: Dale Blount <dale@archlinux.org>

pkgname=re2c
pkgver=1.1.1
pkgrel=1
arch=(x86_64)
depends=(gcc-libs)
pkgdesc='A tool for generating C-based recognizers from regular expressions'
url='http://re2c.org'
license=(public domain)
source=(re2c-$pkgver.tar.gz::https://github.com/skvadrik/re2c/archive/$pkgver.tar.gz)
sha1sums=('ddc72454d2ccbbd66b038bbe6bc14783a349ac4f')

build() {
  cd $pkgname-$pkgver/re2c
  ./autogen.sh
  ./configure --prefix=/usr
  make
}

check() {
  cd $pkgname-$pkgver/re2c
  make tests
}

package() {
  cd $pkgname-$pkgver/re2c
  make DESTDIR="$pkgdir" install
}
