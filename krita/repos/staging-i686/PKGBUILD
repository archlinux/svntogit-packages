# $Id: PKGBUILD 262594 2016-03-26 20:49:29Z arojas $
# Maintainer: Antonio Rojas <arojas@archlinux,org>

pkgname=krita
pkgver=3.0.1.1
pkgrel=2
pkgdesc="Edit and paint images"
arch=(i686 x86_64)
url="http://krita.org"
license=(LGPL)
depends=(kio kitemmodels gsl libraw opencolorio exiv2 openexr fftw curl boost-libs hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools python boost eigen vc poppler-qt5)
optdepends=('poppler-qt5: PDF filter')
conflicts=(calligra-krita krita-l10n)
replaces=(calligra-krita krita-l10n)
source=("http://download.kde.org/stable/krita/$pkgver/krita-$pkgver.tar.gz")
md5sums=('ea0c41c44269b18dfce17130d9341654')
validpgpkeys=('05D00A8B73A686789E0A156858B9596C722EA3BD') # Boudewijn Rempt <foundation@krita.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DPACKAGERS_BUILD=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

# Fix conflicts with breeze
  rm "$pkgdir"/usr/share/color-schemes/Breeze*
}
