# $Id: PKGBUILD 262594 2016-03-26 20:49:29Z arojas $
# Maintainer: Antonio Rojas <arojas@archlinux,org>

pkgname=krita
pkgver=3.1.3
pkgrel=2
pkgdesc="Edit and paint images"
arch=(i686 x86_64)
url="http://krita.org"
license=(LGPL)
depends=(kio kitemmodels gsl libraw exiv2 openexr fftw curl boost-libs hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools python boost eigen vc poppler-qt5 opencolorio)
optdepends=('poppler-qt5: PDF filter' 'ffmpeg: to save animations' 'opencolorio: for the LUT docker')
conflicts=(calligra-krita krita-l10n)
replaces=(calligra-krita krita-l10n)
source=("http://download.kde.org/stable/krita/$pkgver/krita-$pkgver.tar.gz"{,.sig})
sha256sums=('816b9fa9717b78d8dc3a9c4efd49f7a5e3b7f2a0965f2434c48b708598d1b288'
            'SKIP')
validpgpkeys=('05D00A8B73A686789E0A156858B9596C722EA3BD') # Boudewijn Rempt <foundation@krita.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DPACKAGERS_BUILD=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
