# $Id$
# Maintainer: Alexander Baldeck <alexander@archlinux.org>
# Contributor: Rouslan Solomakhin <rouslan@localnet.com>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=oxine
pkgver=0.7.1
pkgrel=5
pkgdesc="A lightweight, purely osd based xine frontend for set-top boxes and home entertainment systems"
arch=('i686' 'x86_64')
url="http://oxine.sourceforge.net/"
license=('GPL')
depends=('xine-lib' 'libexif' 'eject' 'libcdio' 'curl' 'libxtst')
source=(http://downloads.sourceforge.net/sourceforge/oxine/${pkgname}-${pkgver}.tar.gz)
md5sums=('2a2d05ee21892624e35cf539e532c0dd')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i 's|#include <curl/types.h>||' configure src/download.c
  ./configure --prefix=/usr --disable-hal --without-jsw --disable-extractor \
    --without-imagemagick --disable-gdkpixbuf --disable-weather
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
