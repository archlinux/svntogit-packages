# $Id$
#Maintainer: Damir Perisa <damir.perisa@bluewin.ch>
#Contributor: Robert Emil Berge <robert@rebi.no>

pkgname=cmt
pkgver=1.16
pkgrel=4
pkgdesc="CMT for LADSPA"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.ladspa.org"
depends=('ladspa' 'gcc-libs')
groups=('ladspa-plugins')
source=("http://www.ladspa.org/download/${pkgname}_src_${pkgver}.tgz")
md5sums=('25648277bf55230ee593bbc1f3a9860c')

build() {
  cd "${srcdir}/${pkgname}/src"
  sed -e "/^CFLAGS/ s/-O3/${CFLAGS}/" \
      -e 's|-Werror||g' \
      -i makefile
  make
}

package() {
  cd "${srcdir}/${pkgname}/src"
  install -m755 -d "${pkgdir}/usr/lib/ladspa"
  install -m755 ../plugins/cmt.so "${pkgdir}/usr/lib/ladspa/"
}
