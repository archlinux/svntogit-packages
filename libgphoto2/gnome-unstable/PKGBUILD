# $Id$
# Maintainer: Eduardo Romero <eduardo@archlinux.org>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>

pkgname=libgphoto2
pkgver=2.4.6
pkgrel=3
pkgdesc="The core library of gphoto2, designed to allow access to digital camera by external programs."
arch=(i686 x86_64)
url="http://www.gphoto.org"
license=(LGPL)
depends=('libexif>=0.6.17' 'libjpeg>=7' 'libtool>=2.2.6a' 'libusb')
install=libgphoto2.install
options=('libtool')
source=(http://downloads.sourceforge.net/gphoto/${pkgname}-${pkgver}.tar.gz
        libgphoto2.udev-136.patch)
md5sums=('3ad331acbe7e6ff4601705d504bdff94' '24ed32c30477c95561fb9a146915791e')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/libgphoto2.udev-136.patch" || return 1
  udevscriptdir=/lib/udev ./configure --prefix=/usr --with-drivers=all --disable-static --disable-rpath || return 1
  LD_PRELOAD="" make || return 1
  make DESTDIR="${pkgdir}" install || return 1

  rm -f ${pkgdir}/usr/lib/libgphoto2/${pkgver}/*.a
  
  install -m755 -d "${pkgdir}/usr/share/hal/fdi/information/20thirdparty"
  install -m755 -d "${pkgdir}/lib/udev/rules.d"
  LD_LIBRARY_PATH="${pkgdir}/usr/lib${LD_LIBRARY_PATH:+:}$LD_LIBRARY_PATH" \
  CAMLIBS="${pkgdir}/usr/lib/libgphoto2/${pkgver}" \
      "${pkgdir}/usr/lib/libgphoto2/print-camera-list" hal-fdi > \
      "${pkgdir}/usr/share/hal/fdi/information/20thirdparty/10-camera-libgphoto2.fdi" || return 1

  LD_LIBRARY_PATH="${pkgdir}/usr/lib${LD_LIBRARY_PATH:+:}$LD_LIBRARY_PATH" \
  CAMLIBS="${pkgdir}/usr/lib/libgphoto2/${pkgver}" \
      "${pkgdir}/usr/lib/libgphoto2/print-camera-list" udev-rules version 136 group camera mode 0660 > \
      "${pkgdir}/lib/udev/rules.d/54-gphoto.rules" || return 1

  # Remove recursive symlink
  rm -f "${pkgdir}/usr/include/gphoto2/gphoto2" || return 1
}
