# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: Eduardo Romero <eduardo@archlinux.org>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>

pkgname=libgphoto2
pkgver=2.5.5.1
pkgrel=1
pkgdesc="The core library of gphoto2, designed to allow access to digital camera by external programs."
arch=(i686 x86_64)
url="http://www.gphoto.org"
license=(LGPL)
depends=('libexif' 'libjpeg' 'gd' 'libltdl' 'libusb')
install=libgphoto2.install
source=(http://downloads.sourceforge.net/gphoto/${pkgname}-${pkgver}.tar.gz{,.asc})
sha1sums=('d5952a74e355fb139dc97c405dcf1679f65cfe33'
         'SKIP')

build() {
  cd $pkgname-$pkgver
  ./configure \
	--prefix=/usr \
	--disable-rpath
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="${pkgdir}" install

  # Remove unused udev helper
  rm -rf "${pkgdir}/usr/lib/udev"

  install -m755 -d "${pkgdir}/usr/lib/udev/hwdb.d"
  LD_LIBRARY_PATH="${pkgdir}/usr/lib${LD_LIBRARY_PATH:+:}$LD_LIBRARY_PATH" \
  CAMLIBS="${pkgdir}/usr/lib/libgphoto2/${pkgver}" \
      "${pkgdir}/usr/lib/libgphoto2/print-camera-list" hwdb > \
      "${pkgdir}/usr/lib/udev/hwdb.d/20-gphoto.hwdb"

  # Remove recursive symlink
  rm -fv "${pkgdir}/usr/include/gphoto2/gphoto2"
}
