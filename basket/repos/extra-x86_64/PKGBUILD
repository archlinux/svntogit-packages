# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: James Rayner <james@archlinux.org>
# Contributor: lucke <lucke at o2 dot pl>

pkgname=basket
_pkgver=2.49
pkgver=${_pkgver}b
pkgrel=2
pkgdesc="All-purpose notes taker for KDE."
arch=('x86_64')
url="https://basket-notepads.github.io/"
depends=('kcmutils' 'kfilemetadata' 'kparts' 'phonon-qt5' 'libgit2')
makedepends=('extra-cmake-modules' 'kdoctools')
license=('GPL')
source=(https://launchpad.net/basket/kde5/$pkgver/+download/basket-$_pkgver-beta.tar.bz2)
sha256sums=('e760fc68deb1f56ef02cd4a1773c6cd348b79b88ef5ff5dfb05e66947244f186')

prepare() {
  mkdir -p build

  sed -e 's|LIBGIT2_SOVERSION|99|g' -i $pkgname-$_pkgver-beta/src/application.cpp # Hack to fix build with libgit2 0.99
}

build() {
  cd build
  cmake ../$pkgname-$_pkgver-beta \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
