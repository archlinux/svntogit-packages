# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: James Rayner <james@archlinux.org>
# Contributor: lucke <lucke at o2 dot pl>

pkgname=basket
_pkgver=2.49
pkgver=${_pkgver}a
pkgrel=1
pkgdesc="All-purpose notes taker for KDE."
arch=('x86_64')
url="https://basket-notepads.github.io/"
depends=('kcmutils' 'kfilemetadata' 'kparts')
makedepends=('extra-cmake-modules' 'kdoctools' 'frameworkintegration' 'kdelibs4support')
license=('GPL')
source=(https://launchpad.net/basket/kde5/$pkgver/+download/basket-$_pkgver-alpha.tar.bz2)
sha1sums=('2488581df9fa83a033be91c3c8bfb18735378c46')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$_pkgver-alpha \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
