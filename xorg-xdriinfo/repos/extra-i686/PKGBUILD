# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-xdriinfo
pkgver=1.0.5
pkgrel=1
pkgdesc="Query configuration information of DRI drivers"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libx11' 'libgl')
makedepends=('xorg-util-macros' 'glproto')
groups=('xorg-apps' 'xorg')
source=(http://xorg.freedesktop.org/archive/individual/app/xdriinfo-${pkgver}.tar.bz2{,.sig})
sha256sums=('4cba3766ef89557422062287248adeb933999071bad6f3ef9c0a478a3c680119'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # Alan Coopersmith <alan.coopersmith@oracle.com>

build() {
  cd xdriinfo-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd xdriinfo-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
