# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Thomas Arnhold <thomas at arnhold dot org>

pkgname=libe-book
pkgver=0.1.3
pkgrel=11
pkgdesc="Library for import of reflowable e-book formats."
arch=('x86_64')
url="https://sourceforge.net/projects/libebook/"
license=('LGPL2.1' 'MPL')
depends=('librevenge' 'libxml2' 'liblangtag')
makedepends=('gperf' 'boost' 'cppunit' 'doxygen')
source=(https://sourceforge.net/projects/${pkgname//-/}/files//${pkgname}-${pkgver}/${pkgname}-${pkgver}.tar.xz
        icu68.patch)
sha1sums=('18fde9588c236d51eea9437ed882d8b751daf01f'
          '132fd6bf8622f2d922a9e844c665a906d6b2bbdb')

prepare() {
  cd "${pkgname}"-${pkgver}
  patch -Np0 -i ../icu68.patch
}

build() {
  cd "${pkgname}"-${pkgver}
  ./configure --prefix=/usr
  make
}

check() {
  cd "${pkgname}"-${pkgver}
  make check
}

package() {
  cd "${pkgname}"-${pkgver}
  make DESTDIR="$pkgdir" install
}
