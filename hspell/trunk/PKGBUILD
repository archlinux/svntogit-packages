# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgbase=hspell
pkgname=('hspell' 'hunspell-he')
pkgver=1.1
pkgrel=1
arch=(i686 x86_64)
license=('GPL')
url="http://www.ivrix.org.il/projects/spell-checker/"
makedepends=('glibc' 'zlib' 'perl' 'hunspell' 'gawk')
options=('!libtool' '!makeflags')
source=(http://hspell.ivrix.org.il/${pkgname}-${pkgver}.tar.gz
	hspell-1.0.sharedlib.patch)
md5sums=('e79e75180ce529c008ea085954a52f2c'
         '0df6764bb37500ee936b9355fee0c7a5')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i ${srcdir}/hspell-1.0.sharedlib.patch
  autoreconf -v
  sed -i -e 's|/usr/bin/awk|/bin/awk|' wzip
  ./configure --prefix=/usr --mandir=/usr/share/man \
      --enable-linginfo --enable-fatverb
  make 
  make hunspell
}

package_hspell() {

  pkgdesc="Hebrew spell-checker"
  depends=('glibc' 'zlib' 'perl')

  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  rm -f ${pkgdir}/usr/lib/libhspell.a
}

package_hunspell-he() {

  pkgdesc="Hebrew hunspell dictionary"
  
  cd "${srcdir}/${pkgname}-${pkgver}"

  install -dm755 ${pkgdir}/usr/share/hunspell
  cp -p hunspell/new_he.dic ${pkgdir}/usr/share/hunspell/he_IL.dic
  cp -p hunspell/new_he.aff ${pkgdir}/usr/share/hunspell/he_IL.aff

  # the symlinks
  install -dm755 ${pkgdir}/usr/share/myspell/dicts
  pushd $pkgdir/usr/share/myspell/dicts
    for file in $pkgdir/usr/share/hunspell/*; do
      ln -sv /usr/share/hunspell/$(basename $file) .
    done
  popd
}
