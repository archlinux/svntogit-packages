# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Arjan Timmerman <arjan@soulfly.nl>

pkgname=genius
pkgver=1.0.9
pkgrel=3
pkgdesc="Advanced calculator including a mathematical programming language"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.jirka.org/genius.html"
depends=('gmp>=5.0.1' 'vte>=0.24.0' 'gtksourceview2>=2.10.0' 'mpfr>=2.4.2' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('intltool' 'pkgconfig' 'gnome-doc-utils>=0.20.0')
options=('!libtool' '!makeflags')
install=genius.install
source=(http://ftp.gnome.org/pub/GNOME/sources/genius/1.0/${pkgname}-${pkgver}.tar.bz2
        genius-gio.patch)
sha256sums=('5f3a7e9e8ab77e973cf83d64efcc173e5c5fa99fa96fe324e2ea82f4d3a26d68'
            '8b722ec3160d93af22a06a94279f15ee21abc87fd95d83922d94dfc40d21da44')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/genius-gio.patch" || return 1
  libtoolize --force || return 1
  aclocal || return 1
  autoconf || return 1
  automake || return 1
  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/genius \
      --localstatedir=/var --disable-static \
      --enable-gtksourceview --disable-scrollkeeper \
      --disable-update-mimedb || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
}
