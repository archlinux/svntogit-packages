# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Arjan Timmerman <arjan@soulfly.nl>

pkgname=genius
pkgver=1.0.14
pkgrel=1
pkgdesc="Advanced calculator including a mathematical programming language"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.jirka.org/genius.html"
depends=('vte>=0.26.2' 'gtksourceview2>=2.10.5' 'mpfr>=3.0.0' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('intltool' 'pkgconfig' 'gnome-doc-utils>=0.20.0')
options=('!libtool' '!makeflags')
install=genius.install
source=(http://ftp.gnome.org/pub/GNOME/sources/genius/1.0/${pkgname}-${pkgver}.tar.xz)
sha256sums=('90ed181e6bb0b4a8a5adcad1eb8273c55685345e7f322b3591b59be02e41bb5f')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/genius \
      --localstatedir=/var --disable-static \
      --enable-gtksourceview --disable-scrollkeeper \
      --disable-update-mimedb
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
