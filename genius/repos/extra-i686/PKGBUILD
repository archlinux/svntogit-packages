# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Arjan Timmerman <arjan@soulfly.nl>

pkgname=genius
pkgver=1.0.17
pkgrel=2
pkgdesc="Advanced calculator including a mathematical programming language"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.jirka.org/genius.html"
depends=('vte' 'gtksourceview2' 'mpfr' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('intltool' 'gnome-doc-utils')
options=('!makeflags')
install=genius.install
source=(http://ftp.gnome.org/pub/GNOME/sources/genius/1.0/${pkgname}-${pkgver}.tar.xz
        readline-fix.patch)
sha256sums=('2af0cf64a3bae45e8e6c38ce8c0c0724386ae29614a83d9eff179ee09ecef44f'
            '9f340c99b9f428bf476350f54d1f75556f630bc750c629da89ac6b8fe411b7d6')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../readline-fix.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/genius \
      --localstatedir=/var --disable-static \
      --enable-gtksourceview --disable-scrollkeeper \
      --disable-update-mimedb
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="${pkgdir}" install
}
