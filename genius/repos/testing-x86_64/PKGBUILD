# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Arjan Timmerman <arjan@soulfly.nl>

pkgname=genius
pkgver=1.0.23
pkgrel=2
pkgdesc="Advanced calculator including a mathematical programming language"
arch=('x86_64')
license=('GPL')
url="http://www.jirka.org/genius.html"
depends=('vte' 'gtksourceview2' 'mpfr' 'desktop-file-utils')
makedepends=('intltool' 'gnome-doc-utils')
options=('!makeflags')
source=(https://download.gnome.org/sources/genius/1.0/${pkgname}-${pkgver}.tar.xz)
sha256sums=('912e1c1beadee185614036c6808a974157b2009fe315b249efa2aa06a5ac6f4b')

build() {
  cd $pkgname-$pkgver
  autoreconf -fi
  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/genius \
      --localstatedir=/var --disable-static \
      --enable-gtksourceview --disable-scrollkeeper \
      --disable-update-mimedb
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="${pkgdir}" install
}
