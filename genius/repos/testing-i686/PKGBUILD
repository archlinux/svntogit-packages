# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Arjan Timmerman <arjan@soulfly.nl>

pkgname=genius
pkgver=1.0.20
pkgrel=2
pkgdesc="Advanced calculator including a mathematical programming language"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.jirka.org/genius.html"
depends=('vte' 'gtksourceview2' 'mpfr' 'desktop-file-utils')
makedepends=('intltool' 'gnome-doc-utils')
options=('!makeflags')
install=genius.install
source=(http://ftp.gnome.org/pub/GNOME/sources/genius/1.0/${pkgname}-${pkgver}.tar.xz)
sha256sums=('3994e414578fd494f02d89d0840ea86eff51a53e0a3fe1ae6d31d6c328b0d787')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/genius \
      --localstatedir=/var --disable-static \
      --enable-gtksourceview --disable-scrollkeeper \
      --disable-update-mimedb
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="${pkgdir}" install
}
