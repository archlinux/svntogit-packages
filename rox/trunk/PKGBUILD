# $Id$
# Maintainer: tobias <tobias@archlinux.org>
# Contributor: Jochem Kossen <j.kossen@home.nl>

pkgname=rox
pkgver=2.10
pkgrel=2
pkgdesc="A small and fast file manager which can optionally manage the desktop background and panels"
arch=('i686' 'x86_64')
license=('GPL2')
url="http://roscidus.com/desktop/"
depends=('sh' 'libsm' 'libglade')
makedepends=('librsvg' 'python2')
source=("http://downloads.sourceforge.net/${pkgname}/rox-filer-${pkgver}.tar.bz2"
        'rox.desktop' 'rox.svg' 'rox.sh' 'right-click.diff')
md5sums=('28e8494c9af783258ff86c830f8f0062'
         'de05c906395abd4402b0470c1bc2ae6e'
         '658c8648b51e215558e13e6afb2b5c76'
         '31578a90b241f0a8d09c9f8587608d00'
         '2b2105a33070ac09a6352c29a5a17e4e')

build() {
  cd "${srcdir}/rox-filer-${pkgver}/ROX-Filer"
  patch -p2 -i "${srcdir}/right-click.diff"
  ./AppRun --compile
# finally we render a png as fallback for svg unaware menu applications
# Attention: always make sure you check the dimensions of the source-svg,
# you can read the dimensions via inkscape's export function
  rsvg-convert -w 48 -h 38 -f png -o "${srcdir}/rox.png" "${srcdir}/rox.svg"
}

package() {
  cd "${srcdir}/rox-filer-${pkgver}"
  install -d "${pkgdir}/usr/share/Choices/MIME-types"
  install -m755 Choices/MIME-types/* "${pkgdir}/usr/share/Choices/MIME-types/"
  cp -rp ROX-Filer "${pkgdir}/usr/share/"
  rm -fr "${pkgdir}"/usr/share/ROX-Filer/{src,build}
 
  install -D -m755 "${srcdir}/rox.sh" "${pkgdir}/usr/bin/rox"
  install -D -m644 rox.1 "${pkgdir}/usr/share/man/man1/rox.1"
  ln -sf rox.1 "${pkgdir}/usr/share/man/man1/ROX-Filer.1"

  install -D -m644 "${srcdir}/rox.desktop" "${pkgdir}/usr/share/applications/rox.desktop"
  install -D -m644 "${srcdir}/rox.svg" "${pkgdir}/usr/share/pixmaps/rox.svg"
  install -D -m644 "${srcdir}/rox.png" "${pkgdir}/usr/share/pixmaps/rox.png"
}
