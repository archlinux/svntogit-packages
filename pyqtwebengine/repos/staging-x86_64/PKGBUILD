# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=pyqtwebengine
pkgname=(python-pyqtwebengine python2-pyqtwebengine)
pkgver=5.14.0
pkgrel=4
arch=(x86_64)
url="https://www.riverbankcomputing.com/software/pyqtwebengine/intro"
license=(GPL)
makedepends=(sip sip5 pyqt-builder qt5-webengine python-pyqt5 python2-pyqt5)
conflicts=(pyqtwebengine-common)
source=("https://pypi.python.org/packages/source/P/PyQtWebEngine/PyQtWebEngine-$pkgver.tar.gz")
sha256sums=('e11595051f8bfbfa49175d899b2c8c2eea3a3deac4141edf4db68c3555221c92')

prepare() {
  cp -a PyQtWebEngine-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/PyQtWebEngine-$pkgver
  sip-build \
    --no-make \
    --api-dir /usr/share/qt/qsci/api/python
  cd build
  make

  cd "$srcdir"/PyQtWebEngine-$pkgver-py2
  python2 configure.py \
    --sip='/usr/bin/sip' \
    --no-qsci-api
  make
}

package_python-pyqtwebengine() {
  pkgdesc="Python bindings for QtWebEngine"
  groups=(pyqt5)
  depends=(python-pyqt5 qt5-webengine)

  cd PyQtWebEngine-$pkgver/build
  make INSTALL_ROOT="$pkgdir" install -j1
}

package_python2-pyqtwebengine() {
  pkgdesc="Python 2 bindings for QtWebEngine"
  depends=(python2-pyqt5 qt5-webengine)

  cd PyQtWebEngine-$pkgver-py2
  make INSTALL_ROOT="$pkgdir" install -j1
}
