# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=wireguard-arch
pkgver=0.0.20200128
pkgrel=3
pkgdesc='Wireguard module for Arch Kernel'
url='https://www.wireguard.com/'
arch=('x86_64')
license=('GPL')
makedepends=('linux-headers' "wireguard-dkms>=$pkgver")
conflicts=('wireguard-dkms')
provides=('WIREGUARD-MODULE')

build() {
    _kernver=$(</usr/src/linux/version)

    dkms build --dkmstree "$srcdir" -m wireguard/$pkgver -k $_kernver
}

package() {
  depends=('linux')

  _kernver="$(</usr/src/linux/version)"

  install -Dt "$pkgdir/usr/lib/modules/$_kernver/extramodules" -m0644 \
    wireguard/${pkgver}/$_kernver/$CARCH/module/*

  # compress each module individually
  find "$pkgdir" -name '*.ko' -exec xz -T1 {} +
}

# vim:set sw=2 et:
