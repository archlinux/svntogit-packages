# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=sushi
pkgver=3.18.0
pkgrel=2
pkgdesc="A quick previewer for Nautilus"
arch=(i686 x86_64)
url="http://www.gnome.org"
license=('GPL2')
groups=(gnome)
depends=('clutter-gtk' 'clutter-gst' 'gjs' 'libmusicbrainz5' 'webkit2gtk' 'gtksourceview3' 'evince')
optdepends=('unoconv: OpenDocument formats')
install=sushi.install
makedepends=('intltool' 'gobject-introspection')
source=(http://download.gnome.org/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz
        clutter-gst3.patch)
sha256sums=('7f4df92be950bf23aff28da49ce108766dd21a88efc14a9d0807e10225608e9c'
            '7217c05f4cf4d797e8e042392c1cb92481bb616a1e7ce20393228591a6bc91e0')

prepare() {
  cd "$pkgname-$pkgver"
  patch -Np1 -i ../clutter-gst3.patch
}

build() {
  cd "$pkgname-$pkgver"
  autoreconf -fi
  ./configure --prefix=/usr --libexecdir=/usr/lib/sushi
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' -e 's/    if test "$export_dynamic" = yes && test -n "$export_dynamic_flag_spec"; then/      func_append compile_command " -Wl,-O1,--as-needed"\n      func_append finalize_command " -Wl,-O1,--as-needed"\n\0/' libtool
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
