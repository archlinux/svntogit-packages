# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve> 

_pypiname=pycurl
pkgbase=python-$_pypiname
pkgname=('python-pycurl' 'python2-pycurl')
pkgver=7.19.5.3
pkgrel=1
arch=('i686' 'x86_64')
url="http://pycurl.sourceforge.net/"
license=('LGPL' 'MIT')
makedepends=('python' 'python2' 'curl' 'git')
checkdepends=('python-bottle' 'python2-bottle' 'python-nose' 'python2-nose' 'vsftpd' 'python-flaky' 'python2-flaky' 'python-pyflakes' 'python2-pyflakes')
source=("git+https://github.com/pycurl/pycurl.git#tag=REL_${pkgver//./_}")
sha1sums=('SKIP')

prepare() {
  cp -a $_pypiname{,-py2}

  cd "$srcdir/$_pypiname"
  find . -name '*.py' -exec sed -i -e "s|#! /usr/bin/env python|#!/usr/bin/env python3|" {} +
    sed -i 's/"share", "doc", PACKAGE/"share", "doc", "python-pycurl"/' setup.py
  sed -e "s/nosetests/nosetests3/" -e "s/python/python3/" -i tests/run.sh

  cd "$srcdir/$_pypiname-py2"
  find . -name '*.py' -exec sed -i -e "s|#! /usr/bin/env python|#!/usr/bin/env python2|" {} +
    sed -i 's/"share", "doc", PACKAGE/"share", "doc", "python2-pycurl"/' setup.py
  sed -e "s/nosetests/nosetests2/" -e "s/python/python2/" -i tests/run.sh
  sed -e "s/pyflakes/pyflakes-python2/" -i Makefile
}

build() {
  cd "$srcdir/$_pypiname"
  make
  python3 setup.py build --with-ssl

  cd "$srcdir/$_pypiname-py2"
  make
  python2 setup.py build --with-ssl
}

check() {
  export PYCURL_VSFTPD_PATH=vsftpd

  cd "$srcdir/$_pypiname"
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-3.5:$PYTHONPATH" make test

  cd "$srcdir/$_pypiname-py2"
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-2.7:$PYTHONPATH" make PYTHON=python2 NOSETESTS=nosetests2 test
}

package_python-pycurl() {
  pkgdesc="A Python 3.x interface to libcurl"
  depends=('python' 'curl')

  cd $_pypiname
  python setup.py install -O1 --root="${pkgdir}"
  install -D -m644 COPYING-MIT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-pycurl() {
  pkgdesc="A Python 2.x interface to libcurl"
  depends=('python2' 'curl')

  cd $_pypiname-py2
  python2 setup.py install -O1 --root="${pkgdir}"
  install -D -m644 COPYING-MIT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
