# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=revelation
pkgver=0.4.11
pkgrel=7
pkgdesc="Password manager for the GNOME 2 desktop"
arch=(i686 x86_64)
license=('GPL')
depends=('gnome-python-desktop>=2.28.0' 'pycrypto>=2.0.1-3' 'gnome-panel>=2.28.0' 'cracklib' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('intltool')
install=revelation.install
url="http://oss.codepoet.no/revelation/"
source=(http://oss.codepoet.no/revelation/downloads/revelation-${pkgver}.tar.bz2
        fs12034.patch)
md5sums=('e2db4a2f00f59b18798d4453c778129f'
         'fd720d187ce5ae631c095872e9db9155')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/fs12034.patch"

  sed -i -e 's/gnome-python-extras/gnome-python-desktop/g' configure
  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/revelation \
      --localstatedir=/var \
      --disable-mime-update --disable-desktop-update \
      --with-python-include=/usr/include/python2.7
  make
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

  sed -i "s|#!/usr/bin/env python$|#!/usr/bin/env python2|" \
    $pkgdir/usr/bin/revelation \
    $pkgdir/usr/lib/python2.7/site-packages/revelation/bundle/PBKDFv2.py \
    $pkgdir/usr/lib/revelation/revelation-applet


  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain revelation ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
