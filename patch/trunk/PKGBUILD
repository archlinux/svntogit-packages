# $Id$
# Maintainer: SÃ©bastien Luttringer <seblu@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=patch
pkgver=2.7.1
pkgrel=3
pkgdesc='A utility to apply patch files to original sources'
arch=('i686' 'x86_64')
url='http://www.gnu.org/software/patch/patch.html'
license=('GPL')
groups=('base-devel')
depends=('glibc' 'attr')
makedepends=('ed')
optdepends=('ed: for patch -e functionality')
source=("ftp://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.xz"{,.sig}
        'patch-2.7.1-initialize-data-structures-early-enough.patch')
md5sums=('e9ae5393426d3ad783a300a338c09b72'
         'SKIP'
         'dc6367a7cd49933d4006c246789e98da')

prepare() {
  # Fix segfault on non-numeric strip-count
  # (also segfaults on nonexistent directory passed to -d)
  # http://savannah.gnu.org/bugs/?37500
  patch -Np1 -d $pkgname-$pkgver < patch-2.7.1-initialize-data-structures-early-enough.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
