# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-games
pkgver=3.4.1
pkgrel=1
pkgdesc="Some Games for GNOME"
arch=('i686' 'x86_64')
license=('GPL')
depends=('desktop-file-utils' 'libcanberra' 'clutter-gtk' 'hicolor-icon-theme' 'librsvg' 'python2-gobject' 'dconf')
makedepends=('yelp-tools' 'intltool' 'gobject-introspection' 'vala')
options=('!emptydirs' '!libtool')
install=gnome-games.install
url="http://www.gnome.org"
groups=('gnome-extra')
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('01726b0631205ccbd9f02938f7ccb1d7bd0c4c001cb4596f871fed7c3afc25d4')

build() {
  cd $pkgname-$pkgver
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static --disable-schemas-compile \
      --with-scores-user=root --with-scores-group=games \
      --enable-introspection=yes
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  # Remove all scores, we generate them from postinstall
  rm -rf "$pkgdir/var"

  sed -i 's_#! /usr/bin/env python_#!/usr/bin/env python2_' "$pkgdir/usr/bin/gnome-sudoku"
}
