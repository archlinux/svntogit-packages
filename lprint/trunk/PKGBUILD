# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: WorMzy Tykashi <wormzy.tykashi@gmail.com>

pkgname=lprint
pkgver=1.1.0
pkgrel=1
pkgdesc="Label Printer Application"
arch=('x86_64')
url="https://www.msweet.org/lprint/"
license=('Apache' 'custom')
depends=('pappl' 'libcups' 'libpng' 'libusb' 'pam')
source=("https://github.com/michaelrsweet/lprint/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz"{,.sig}
        fix-systemd-installdir.patch)
#validpgpkeys=('845464660B686AAB36540B6F999559A027815955')  # "Michael R Sweet <michael.r.sweet@gmail.com>"
validpgpkeys=('9086C3CDC66C3F563CF8F405BE67C75EC81F3244') # "Michael R Sweet <msweet@msweet.org>"
sha256sums=('d40e353ac9f6b0de96974717255fa1df0f9655d37ef3f9a6f35b6391992daf73'
            'SKIP'
            '5afaf8dad1e766f0c99a27095e56face16d2eae681d576c395db5d3c3761d182')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -Np1 -i ../fix-systemd-installdir.patch
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

check() {
   cd "$pkgname-$pkgver"
   #make -k check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  # add license + exception
  install -m644 -Dt "${pkgdir}/usr/share/licenses/${pkgname}" {LICENSE,NOTICE}
}
