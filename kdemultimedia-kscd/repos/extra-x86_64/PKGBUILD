# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdemultimedia-kscd
pkgver=4.14.1
pkgrel=1
pkgdesc="KDE CD player"
url='http://kde.org/applications/multimedia/kscd/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdemultimedia')
depends=('kdebase-runtime' 'libmusicbrainz3')
makedepends=('cmake' 'automoc4')
install="${pkgname}.install"
source=("http://download.kde.org/stable/${pkgver}/src/kscd-${pkgver}.tar.xz")
sha1sums=('c11c7dce7fb749eca6226ba05e9a9465767c38af')

build() {
  mkdir build
  cd build
  cmake ../kscd-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
