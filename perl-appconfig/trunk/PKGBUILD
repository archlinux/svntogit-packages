# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: eric <eric@archlinux.org>
# Contributor: Eric Johnson <eric@coding-zone.com>

pkgname=perl-appconfig
pkgver=1.71
pkgrel=10
pkgdesc="Perl/CPAN AppConfig module - Read configuration files and parse command line arguments"
arch=('any')
license=('PerlArtistic' 'GPL')
url="https://search.cpan.org/dist/AppConfig/"
depends=('perl')
options=('!emptydirs')
source=("https://www.cpan.org/authors/id/N/NE/NEILB/AppConfig-$pkgver.tar.gz")
sha256sums=('1177027025ecb09ee64d9f9f255615c04db5e14f7536c344af632032eb887b0f')

build() {
  cd AppConfig-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd AppConfig-$pkgver
  make test
}

package() {
  cd AppConfig-$pkgver
  make install DESTDIR="$pkgdir"
}
