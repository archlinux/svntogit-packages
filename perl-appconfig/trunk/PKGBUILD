# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: eric <eric@archlinux.org>
# Contributor: Eric Johnson <eric@coding-zone.com>

pkgname=perl-appconfig
_realname=AppConfig
pkgver=1.71
pkgrel=4
pkgdesc="Perl/CPAN AppConfig module - Read configuration files and parse command line arguments"
arch=('any')
license=('PerlArtistic' 'GPL')
url="http://search.cpan.org/dist/${_realname}/"
depends=('perl')
options=('!emptydirs')
source=("http://www.cpan.org/authors/id/N/NE/NEILB/${_realname}-${pkgver}.tar.gz")
md5sums=('7747d9241561ed5567d5e134b8648707')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make test
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make install DESTDIR="${pkgdir}"
}
