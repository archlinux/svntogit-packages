# $Id$
# Maintainer: Felix Yan <felixonamrs@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kstars
pkgver=15.03.90
pkgrel=1
pkgdesc="Desktop Planetarium"
url="http://kde.org/applications/education/kstars/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
depends=('wcslib' 'libindi' 'kplotting' 'knewstuff' 'qt5-multimedia' 'kinit' 'ktexteditor')
makedepends=('extra-cmake-modules' 'python' 'kdoctools' 'eigen')
install=${pkgname}.install
replaces=('kdeedu-kstars')
conflicts=('kdeedu-kstars')
source=("http://download.kde.org/unstable/applications/${pkgver}/src/kstars-${pkgver}.tar.xz")
sha1sums=('cbfd5cd9f0b1dafe3575cf5386760a5b893d9e6e')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../kstars-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_Xplanet=OFF \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install

  sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|g' \
    "${pkgdir}"/usr/share/kstars/scripts/supernova_updates_parser.py
}
