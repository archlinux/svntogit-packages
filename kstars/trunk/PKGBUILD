# $Id$
# Maintainer: Felix Yan <felixonamrs@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kstars
pkgver=16.04.1
pkgrel=1
pkgdesc="Desktop Planetarium"
url="http://kde.org/applications/education/kstars/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdeedu')
depends=('wcslib' 'libindi' 'kplotting' 'knewstuff' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'python' 'kdoctools' 'eigen' 'xplanet')
optdepends=('xplanet: XPlanet support')
replaces=('kdeedu-kstars')
conflicts=('kdeedu-kstars')
source=("http://download.kde.org/stable/applications/${pkgver}/src/kstars-${pkgver}.tar.xz")
sha1sums=('6740841e1cd7c002ffd351f277fca00ab67ba735')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

  sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|g' \
    "${pkgdir}"/usr/share/kstars/scripts/supernova_updates_parser.py
}
