# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>

pkgname=python-pylint
pkgver=2.5.2
pkgrel=1
pkgdesc="Analyzes Python code looking for bugs and signs of poor quality"
arch=('any')
url="https://pylint.org"
license=('GPL')
depends=('python-astroid' 'python-mccabe' 'python-isort' 'python-setuptools' 'python-toml')
makedepends=('python-pytest-runner' 'python-pytest-benchmark')
checkdepends=('mpdecimal')
optdepends=('tk: Pylint GUI'
            'graphviz: To have other output formats than dot or vcg')
source=("$pkgname-$pkgver.tar.gz::https://github.com/PyCQA/pylint/archive/pylint-$pkgver.tar.gz")
sha512sums=('c8281b1f3c23f75e2129e9b44630a43cc5c36ff562dcd474eda9e9a5db9a56538d73e52b3002fa16def681dd4e0d81103266bc01b28f2ca731d7aae069687901')

build() {
  cd "$srcdir"/pylint-pylint-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/pylint-pylint-$pkgver
  # https://github.com/PyCQA/pylint/issues/3198
  python setup.py pytest --addopts="-k 'not test_by_module_statement_value'"
}

package() {
  cd pylint-pylint-$pkgver
  python setup.py install --prefix=/usr --root="$pkgdir" --skip-build --optimize=1

  install -d "$pkgdir"/usr/share/man/man1
  install -m644 man/* "$pkgdir"/usr/share/man/man1
}
