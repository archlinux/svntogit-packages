# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>

pkgname=python-pylint
pkgver=2.7.2
pkgrel=1
pkgdesc="Analyzes Python code looking for bugs and signs of poor quality"
arch=('any')
url="https://pylint.org"
license=('GPL')
depends=('python-astroid' 'python-mccabe' 'python-isort' 'python-setuptools' 'python-toml')
makedepends=('python-pytest-runner' 'python-pytest-benchmark')
checkdepends=('mpdecimal')
optdepends=('tk: Pylint GUI'
            'graphviz: To have other output formats than dot or vcg')
source=("$pkgname-$pkgver.tar.gz::https://github.com/PyCQA/pylint/archive/pylint-$pkgver.tar.gz")
sha512sums=('575d6eff9d05625a05d517919d98c609e9f77dbdee1dce3a11699ff6e9a512345ec3136351a762da20cbc3e9ac732a35b848401cf84461b9b1d95b5d2b395071')

build() {
  cd pylint-pylint-$pkgver
  python setup.py build
}

check() {
  cd pylint-pylint-$pkgver
  # https://github.com/PyCQA/pylint/issues/3895
  python setup.py pytest || echo "Tests failed"
}

package() {
  cd pylint-pylint-$pkgver
  python setup.py install --prefix=/usr --root="$pkgdir" --skip-build --optimize=1

  install -d "$pkgdir"/usr/share/man/man1
  install -m644 man/* "$pkgdir"/usr/share/man/man1
}
