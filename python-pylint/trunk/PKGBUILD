# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>

pkgname=python-pylint
pkgver=2.6.0
pkgrel=2
pkgdesc="Analyzes Python code looking for bugs and signs of poor quality"
arch=('any')
url="https://pylint.org"
license=('GPL')
depends=('python-astroid' 'python-mccabe' 'python-isort' 'python-setuptools' 'python-toml')
makedepends=('python-pytest-runner' 'python-pytest-benchmark')
checkdepends=('mpdecimal')
optdepends=('tk: Pylint GUI'
            'graphviz: To have other output formats than dot or vcg')
source=("$pkgname-$pkgver.tar.gz::https://github.com/PyCQA/pylint/archive/pylint-$pkgver.tar.gz"
        python-3.9.patch)
sha512sums=('8e42ae71b407bd926d3dba51a377016c93139777d6dfdd06e70a293e94ef78d1c0d59daa63fb6bab29f20eb5adc99ec808fc3dd0ed15718722b7da29923ece74'
            'a4e5702ce706a9a19ba5a5575c0ee493c21c97ff4e0d03ef0e1ad5cee953c0fbc83174be08cfb7137ac6bcf5cece47089a5c77f31df5f3a850ecc1e58b6fae66')

prepare() {
  cd pylint-pylint-$pkgver
  patch -p1 -i ../python-3.9.patch
}

build() {
  cd pylint-pylint-$pkgver
  python setup.py build
}

check() {
  cd pylint-pylint-$pkgver
  # https://github.com/PyCQA/pylint/issues/3895
  python setup.py pytest || echo "Tests failed"
}

package() {
  cd pylint-pylint-$pkgver
  python setup.py install --prefix=/usr --root="$pkgdir" --skip-build --optimize=1

  install -d "$pkgdir"/usr/share/man/man1
  install -m644 man/* "$pkgdir"/usr/share/man/man1
}
