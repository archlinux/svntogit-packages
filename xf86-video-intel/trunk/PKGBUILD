# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xf86-video-intel
pkgver=2.18.0
pkgrel=3
pkgdesc="X.org Intel i810/i830/i915/945G/G965+ video drivers"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('intel-dri' 'libxvmc' 'libpciaccess' 'libdrm' 'xcb-util>=0.3.8' 'libxfixes' 'udev')
makedepends=('xorg-server-devel>=1.12.0' 'libx11' 'libdrm' 'xf86driproto' 'glproto' 'mesa' 'libxvmc' 'libxrender')
conflicts=('xorg-server<1.12.0' 'xf86-video-i810' 'xf86-video-intel-legacy')
options=('!libtool')
groups=('xorg-drivers' 'xorg')
source=(${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2
        0001-uxa-gen3-Remove-special-casing-of-solid-pictures.patch
        0002-uxa-Defer-the-call-to-EnterVT-till-after-outputs-are.patch
        0003-uxa-Remove-hook-for-CompositeRectangles.patch
        0004-uxa-Remove-broken-render-glyphs-to-dst.patch
        0005-uxa-Fix-leak-of-glyph-mask-for-unhandled-glyph-compo.patch)
sha1sums=('77fae98e73414140bf214dca5da32bcf079c4463'
          '8b46a5120c8c0b94fcd75801e5ce91d6baccd1ac'
          'd959c66ab40f521bfe4df66b6f43c98b13f59283'
          '5525715f1bbf80edfc34d55946b0528f6b8dbf75'
          '3e54a4b3911e004d29b6a8e07adb7c16eba5bc4f'
          '32e2e5a53926ce29290bf6454e7dcdbd18e91d5e')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/0001-uxa-gen3-Remove-special-casing-of-solid-pictures.patch"
  patch -Np1 -i "${srcdir}/0002-uxa-Defer-the-call-to-EnterVT-till-after-outputs-are.patch"
  patch -Np1 -i "${srcdir}/0003-uxa-Remove-hook-for-CompositeRectangles.patch"
  patch -Np1 -i "${srcdir}/0004-uxa-Remove-broken-render-glyphs-to-dst.patch"
  patch -Np1 -i "${srcdir}/0005-uxa-Fix-leak-of-glyph-mask-for-unhandled-glyph-compo.patch"
  ./configure --prefix=/usr --enable-dri
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
