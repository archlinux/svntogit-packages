# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=pahole
pkgver=1.21
pkgrel=1
pkgdesc="Pahole and other DWARF utils"
url="https://git.kernel.org/pub/scm/devel/pahole/pahole.git"
arch=(x86_64)
license=(GPL2)
depends=(libelf python)
makedepends=(git cmake ninja)
provides=(libdwarves{,_emit,_reorganize}.so)
_commit=25ad41e7b52e3ad677ad4c016698e71079403b3f  # tags/v1.21^0
source=("git+https://git.kernel.org/pub/scm/devel/pahole/pahole.git#commit=$_commit"
        "git+https://github.com/libbpf/libbpf"
        python.diff)
sha256sums=('SKIP'
            'SKIP'
            'defaea9ad325a73bb27bd029065604c084dd32f132b5eaa431718533e94dc073')

pkgver() {
  cd pahole
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd pahole

  # https://bugs.archlinux.org/task/70013
  git apply -3 ../python.diff

  git submodule init
  git submodule set-url lib/bpf "$srcdir/libbpf"
  git submodule update
}

build() {
  CFLAGS+=" $CPPFLAGS"
  cmake -S pahole -B build -G Ninja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -D__LIB=lib
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  export PYTHONHASHSEED=0
  python -m compileall -d / "$pkgdir"
  python -O -m compileall -d / "$pkgdir"
}

# vim:set sw=2 et:
