# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=pahole
pkgver=1.24
pkgrel=1
epoch=1
pkgdesc="Pahole and other DWARF utils"
url="https://git.kernel.org/pub/scm/devel/pahole/pahole.git"
arch=(x86_64)
license=(GPL2)
depends=(libelf python)
makedepends=(git cmake ninja)
provides=(libdwarves{,_emit,_reorganize}.so)
_commit=de2423445fa59a6ebbf6f1fba764f30818204348  # tags/v1.24^0
source=("git+https://git.kernel.org/pub/scm/devel/pahole/pahole.git#commit=$_commit"
        "git+https://github.com/libbpf/libbpf"
        python.diff)
sha256sums=('SKIP'
            'SKIP'
            '12f3d83bda3ec503aca910d0a4372ff8c72886a02aa512b2f37c3f1b21c690c7')

pkgver() {
  cd pahole
  git describe --tags | sed 's/^v//;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd pahole

  # https://bugs.archlinux.org/task/70013
  git apply -3 ../python.diff

  git submodule init
  git submodule set-url lib/bpf "$srcdir/libbpf"
  git submodule update
}

build() {
  cmake -S pahole -B build -G Ninja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -D__LIB=lib
  cmake --build build
}

check() {
  cd build
  ctest --output-on-failure --stop-on-failure -j$(nproc)
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  python -m compileall -d / "$pkgdir"
  python -O -m compileall -d / "$pkgdir"
}

# vim:set sw=2 et:
