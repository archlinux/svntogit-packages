# Maintainer: Firmicus <firmicus āt gmx dōt net>

pkgname=texlive-latexextra
pkgver=2008.13015
pkgrel=1
pkgdesc="TeX Live - Large collection of add-on packages for LaTeX"
license=('GPL')
arch=('i686' 'x86_64')
depends=('texlive-core')
optdepends=('texlive-pictures')
makedepends=('lzma-utils')
groups=('texlive-most')
url='http://tug.org/texlive/'
source=("ftp://ftp.archlinux.org/other/texlive/$pkgname-$pkgver-src.zip")
install=texlive.install
md5sums=('b06028ba8b3ff83e6d735393bd9f82e3')

build() {
   cd $srcdir
   install -m755 -d $pkgdir/usr/share/texmf-var/arch/installedpkgs || return 1
   sed -i '/^#/d' CONTENTS || return 1
   _revnr=`echo $pkgver | sed 's/2008\.//'`
   install -m644 CONTENTS $pkgdir/usr/share/texmf-var/arch/installedpkgs/${pkgname}_${_revnr}.pkgs || return 1
   for p in *.tar.lzma; do 
	   lzma --force -dc $p | tar xf - || return 1
   done
   install -m755 -d $pkgdir/usr/share || return 1
   find texmf* -type d -exec install -d -m755 $pkgdir/usr/share/'{}' \; || exit 1
   find texmf* -type f -exec install -m644 '{}' $pkgdir/usr/share/'{}' \; || exit 1
   find $pkgdir/usr/share/texmf-dist/scripts -type f -exec chmod a+x '{}' \; || return 1
   rm $pkgdir/usr/share/texmf-dist/scripts/glossaries/makeglossaries.bat
   rm $pkgdir/usr/share/texmf-dist/scripts/pst-pdf/ps4pdf.bat*
   rm $pkgdir/usr/share/texmf-dist/scripts/shipunov/biokey2html.bat

   
}
