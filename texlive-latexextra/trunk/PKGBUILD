# $Id$
# Maintainer: Firmicus <firmicus āt gmx dōt net>
# Maintainer: Rémy Oudompheng <remy@archlinux.org>

pkgname=texlive-latexextra
pkgver=2014.34865
_revnr=${pkgver#2014.}
pkgrel=1
pkgdesc="TeX Live - Large collection of add-on packages for LaTeX"
license=('GPL')
arch=(any)
depends=('texlive-core'
         'perl-file-which' # for pdfannotextractor
        )
optdepends=(
  'texlive-genericextra: to use the calctab package'
  'texlive-pictures: to use the package overpic'
  'java-environment: to use pdfannotextractor'
)
groups=('texlive-most')
url='http://tug.org/texlive/'
source=("https://sources.archlinux.org/other/texlive/$pkgname-$pkgver-src.zip" "$pkgname.maps")
options=('!emptydirs')
install=texlive.install
md5sums=('720366292fe9b8d9548dd1e369159580'
         '9e4825f47dac663dd62c4bfa67899e84')

build() {
   for p in *.tar.xz; do
	   bsdtar -xf $p
   done
   rm -rf {tlpkg,doc,source} || true
}
package() {
   install -m755 -d "$pkgdir"/var/lib/texmf/arch/installedpkgs
   sed -i '/^#/d' CONTENTS
   install -m644 CONTENTS "$pkgdir"/var/lib/texmf/arch/installedpkgs/${pkgname}_${_revnr}.pkgs
   install -m644 $pkgname.maps "$pkgdir"/var/lib/texmf/arch/installedpkgs/
   install -m755 -d "$pkgdir"/usr/share
   wanteddirs=$(for d in *; do test -d $d && [[ $d != texmf* ]] && echo $d; done) || true
   for dir in $wanteddirs; do
     find $dir -type d -exec install -d -m755 "$pkgdir"/usr/share/texmf-dist/'{}' \;
     find $dir -type f -exec install -m644 '{}' "$pkgdir"/usr/share/texmf-dist/'{}' \;
   done
   if [[ -d texmf-dist ]]; then
     find texmf-dist -type d -exec install -d -m755 "$pkgdir"/usr/share/'{}' \;
     find texmf-dist -type f -exec install -m644 '{}' "$pkgdir"/usr/share/'{}' \;
   fi
   if [[ -d "$pkgdir"/usr/share/texmf-dist/scripts ]]; then
     find "$pkgdir"/usr/share/texmf-dist/scripts -type f -exec chmod a+x '{}' \;
   fi
   #add symlinks that were in texlive-bin:
   install -m755 -d "$pkgdir"/usr/bin
   ln -s /usr/share/texmf-dist/scripts/authorindex/authorindex "$pkgdir"/usr/bin/authorindex
   ln -s /usr/share/texmf-dist/scripts/exceltex/exceltex "$pkgdir"/usr/bin/exceltex
   ln -s /usr/share/texmf-dist/scripts/glossaries/makeglossaries "$pkgdir"/usr/bin/makeglossaries
   ln -s /usr/share/texmf-dist/scripts/pax/pdfannotextractor.pl "$pkgdir"/usr/bin/pdfannotextractor
   ln -s /usr/share/texmf-dist/scripts/perltex/perltex.pl "$pkgdir"/usr/bin/perltex
   ln -s /usr/share/texmf-dist/scripts/pst-pdf/ps4pdf "$pkgdir"/usr/bin/ps4pdf
   ln -s /usr/share/texmf-dist/scripts/splitindex/splitindex.pl "$pkgdir"/usr/bin/splitindex
   ln -s /usr/share/texmf-dist/scripts/svn-multi/svn-multi.pl "$pkgdir"/usr/bin/svn-multi
   ln -s /usr/share/texmf-dist/scripts/vpe/vpe.pl "$pkgdir"/usr/bin/vpe
   rm "$pkgdir"/usr/share/texmf-dist/scripts/glossaries/makeglossaries.bat
   rm "$pkgdir"/usr/share/texmf-dist/scripts/pst-pdf/ps4pdf.bat*
   rm "$pkgdir"/usr/share/texmf-dist/scripts/shipunov/biokey2html.bat
}
