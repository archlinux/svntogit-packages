# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Anatol Pomozov
# Contributor: JSpaces <jspace@unseen.is>
# Contributor: Triode <triode1@btinternet.com>

pkgname=libsoxr
pkgver=0.1.3
pkgrel=1
pkgdesc='The SoX Resampler library that aims to give fast and high quality results for any constant resampling ratio'
arch=('x86_64')
url='http://sourceforge.net/p/soxr/wiki/Home/'
license=('GPL')
depends=('gcc-libs')
makedepends=('cmake')
source=("http://downloads.sourceforge.net/project/soxr/soxr-$pkgver-Source.tar.xz")
sha1sums=('32ea46b1a8c0c15f835422892d02fce8286aec3c')

prepare() {
  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd build

  cmake ../soxr-${pkgver}-Source \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DBUILD_EXAMPLES='OFF' \
    -DBUILD_SHARED_LIBS='ON' \
    -DWITH_AVFFT='ON' \
    -DWITH_LSR_BINDINGS='ON' \
    -DWITH_OPENMP='ON' \
    -DWITH_PFFFT='ON'
  make
}

check() {
  cd build

  make test
}

package() {
  cd build

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/share/doc
  install -Dm644 ../soxr-${pkgver}-Source/LICENCE -t "${pkgdir}"/usr/share/licenses/libsoxr/
}

# vim: ts=2 sw=2 et:
