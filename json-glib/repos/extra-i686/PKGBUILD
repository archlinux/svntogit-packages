# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=json-glib
pkgver=1.0.2
pkgrel=1
pkgdesc="JSON library built on GLib"
arch=('i686' 'x86_64')
url="http://live.gnome.org/JsonGlib"
license=('GPL')
depends=('glib2')
makedepends=('gobject-introspection')
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/${pkgver:0:3}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('887bd192da8f5edc53b490ec51bf3ffebd958a671f5963e4f3af32c22e35660a')

build(){
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

package(){
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}

check(){
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}
