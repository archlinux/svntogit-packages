# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=libaccounts-qt4
pkgdesc="Qt4-based client library for accessing the online accounts database"
pkgver=1.13
pkgrel=3
arch=(i686 x86_64)
url="https://gitlab.com/accounts-sso/libaccounts-qt"
license=(LGPL)
depends=(qt4 libaccounts-glib)
makedepends=(doxygen)
source=("https://gitlab.com/accounts-sso/libaccounts-qt/repository/archive.tar.bz2?ref=VERSION_$pkgver")
sha1sums=('4b4043fef3e9b42023d5f1e415690bcb4017f820')

prepare() {
  mv libaccounts-qt-VERSION_$pkgver-* accounts-qt-$pkgver
  cd accounts-qt-$pkgver
  sed -i 's|SUBDIRS  += Accounts tests|SUBDIRS += Accounts|' accounts-qt.pro

  # Qt4 fix
  sed -i 's|QHG_LOCATION           = qhelpgenerator|QHG_LOCATION = qhelpgenerator-qt4|' doc/doxy.conf
}

build() {
  cd accounts-qt-$pkgver
  qmake-qt4 PREFIX=/usr LIBDIR=/usr/lib
  make
}

package() {
  cd accounts-qt-$pkgver
  make INSTALL_ROOT="$pkgdir" install

# Remove docs to fix conflicts
  rm -r "${pkgdir}"/usr/share
}
