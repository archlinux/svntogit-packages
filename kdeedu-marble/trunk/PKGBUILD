# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-marble
pkgver=4.7.1
pkgrel=1
pkgdesc="Desktop Globe"
url="http://kde.org/applications/education/marble/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4' 'gpsd')
optdepends=('gpsd: gps support')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/marble-${pkgver}.tar.bz2"
        'gpsd3.patch')
sha1sums=('71c9ccd884495b34c8af4f7c432ca8942dfaa9dd'
          '3916dd6307109d70d0dd538de913c91eede81d78')

build() {
  cd "${srcdir}"/marble-${pkgver}
  patch -p1 -i "${srcdir}"/gpsd3.patch

  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../marble-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
