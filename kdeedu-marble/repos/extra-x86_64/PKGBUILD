# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-marble
pkgver=4.11.2
pkgrel=1
pkgdesc="Desktop Globe"
url="http://kde.org/applications/education/marble/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4' 'gpsd')
optdepends=('gpsd: gps support')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/marble-${pkgver}.tar.xz")
sha1sums=('d37528ba9c61f8938569ea6af39d0fd5181f52d4')

build() {
  mkdir build
  cd build
  cmake ../marble-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
