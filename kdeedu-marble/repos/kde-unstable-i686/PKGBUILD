# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-marble
pkgver=4.11.95
pkgrel=1
pkgdesc="Desktop Globe"
url="http://kde.org/applications/education/marble/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4' 'gpsd')
optdepends=('gpsd: GPS support'
            'quazip: reading and displaying KMZ files')
install=${pkgname}.install
source=("http://download.kde.org/unstable/${pkgver}/src/marble-${pkgver}.tar.xz")
sha1sums=('5585423d2e7ec7db442f07c142d122e5b3be9e04')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../marble-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
