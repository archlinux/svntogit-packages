# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=perl-xml-parser
pkgver=2.45
pkgrel=1
pkgdesc="Expat-based XML parser module for perl"
arch=('x86_64')
license=('GPL' 'PerlArtistic')
url="https://search.cpan.org/dist/XML-Parser/"
depends=('perl-libwww' 'expat')
options=('!emptydirs')
source=("https://search.cpan.org/CPAN/authors/id/T/TO/TODDR/XML-Parser-${pkgver}.tar.gz")
sha1sums=('f96a1daf60dc800afa9033dc0f28e3785f9006fe')

build() {
  cd XML-Parser-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd XML-Parser-${pkgver}
  make test
}

package() {
  cd XML-Parser-${pkgver}
  make DESTDIR="${pkgdir}" install
}
