# $Id$
# Contributor: Aaron Griffin <aaron@archlinux.org>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=vim-colorsamplerpack
pkgver=8.03
pkgrel=1
_scriptid=12179
pkgdesc="Different colorschemes for vim"
arch=('any')
url="http://www.vim.org/scripts/script.php?script_id=625"
license=('GPL2' 'GPL3' 'custom')
depends=('vim')
makedepends=('unzip')
groups=('vim-plugins')
source=(ColorSamplerPack.zip::"http://www.vim.org/scripts/download_script.php?src_id=$_scriptid"
	'LICENSE')
md5sums=('71d097ce83e4a0896843c8819f175aef'
	'f4639ddafc68f49395c25fb4caf0d6f7')

build() {
	cd ${srcdir}/colors
	# This already exist... we can't update it 8(
	rm torte.vim
}

package() {
	cd ${srcdir}
	installpath="${pkgdir}/usr/share/vim/vim72"

	install -d $installpath/colors/
	install -m644 colors/* $installpath/colors || return 1
	install -D -m644 plugin/color_sample_pack.vim \
	   $installpath/plugin/themes.vim || return 1
	install -D -m644 LICENSE \
	   ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE || return 1
}
