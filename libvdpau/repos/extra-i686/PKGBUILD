# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libvdpau
pkgver=1.1.1
pkgrel=1
pkgdesc="Nvidia VDPAU library"
arch=(i686 x86_64)
url='http://cgit.freedesktop.org/~aplattner/libvdpau'
depends=('gcc-libs' 'libxext')
license=('custom')
source=(http://people.freedesktop.org/~aplattner/vdpau/${pkgname}-${pkgver}.tar.bz2{,.sig})
makedepends=('dri2proto')
sha256sums=('857a01932609225b9a3a5bf222b85e39b55c08787d0ad427dbd9ec033d58d736'
            'SKIP')
validpgpkeys=('BD68A042C603DDAD9AA354B0F56ACC8F09BA9635') # Aaron Plattner

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
