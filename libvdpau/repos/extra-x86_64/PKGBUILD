# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libvdpau
pkgver=1.3
pkgrel=1
pkgdesc="Nvidia VDPAU library"
arch=(x86_64)
url='https://www.freedesktop.org/wiki/Software/VDPAU/'
depends=('libxext')
license=('custom')
_commit=f57a9904c43ef5d726320c77baa91d0c38361ed4  # 1.3
source=("git+https://anongit.freedesktop.org/git/vdpau/libvdpau#commit=$_commit")
makedepends=('dri2proto' 'git' 'meson')
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  ninja -C build test
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
  install -Dm 644 $pkgname/COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
