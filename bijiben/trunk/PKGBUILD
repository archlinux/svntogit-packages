# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=bijiben
pkgver=3.26.2
pkgrel=2
pkgdesc="Write out notes, every detail matters"
url="https://wiki.gnome.org/Apps/Bijiben"
arch=(x86_64)
license=(GPL)
depends=(tracker webkit2gtk gnome-online-accounts evolution-data-server)
makedepends=(intltool itstool evolution python appstream-glib yelp-tools autoconf-archive git)
_commit=bae6c22d229980d412d7edcb589e54732d1279bc  # tags/BIJIBEN_3_26_2^0
source=("git+https://git.gnome.org/browse/bijiben#commit=$_commit"
        "git+https://git.gnome.org/browse/libgd")
sha256sums=('SKIP'
            'SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^BIJIBEN_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd $pkgname

  git submodule init
  git config --local submodule.libgd.url "$srcdir/libgd"
  git submodule update

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/$pkgname \
    --disable-update-mimedb \
    --enable-compile-warnings
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
