# $Id$
# Maintainer: 
# Contributor: Judd <jvinet@zeroflux.org>

pkgname=libevent
pkgver=2.0.13
pkgrel=1
pkgdesc="An event notification library"
arch=('i686' 'x86_64')
url="http://www.monkey.org/~provos/libevent/"
license=('GPL2')
depends=('openssl')
optdepends=('python2: to use event_rpcgen.py')
options=('!libtool')
source=("http://www.monkey.org/~provos/libevent-$pkgver-stable.tar.gz")
md5sums=('af786b4b3f790c9d3279792edf7867fc')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}-stable"
  sed -i 's#python#python2#' event_rpcgen.py
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}-stable"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}-stable"
  make DESTDIR="${pkgdir}" install
}
