# Maintainer: Tom Gundersen <teg@jklm.no>
# Contributor: Judd <jvinet@zeroflux.org>

pkgname=libevent
pkgver=2.1.8
pkgrel=2
pkgdesc="An event notification library"
arch=('x86_64')
url="http://libevent.org/"
license=('BSD')
depends=('openssl')
optdepends=('python2: to use event_rpcgen.py')
source=(https://github.com/libevent/libevent/releases/download/release-$pkgver-stable/$pkgname-$pkgver-stable.tar.gz{,.asc})
sha256sums=('965cc5a8bb46ce4199a47e9b2c9e1cae3b137e8356ffdad6d94d3b9069b71dc2'
            'SKIP')
validpgpkeys=('B35BF85BF19489D04E28C33C21194EBB165733EA'
              '9E3AC83A27974B84D1B3401DB86086848EF8686D')

build() {
  cd ${pkgname}-${pkgver}-stable
  ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc --disable-libevent-regress
  make
}

check() {
  cd ${pkgname}-${pkgver}-stable
  make -j1 check
}

package() {
  cd ${pkgname}-${pkgver}-stable
  make DESTDIR="${pkgdir}" install
  install -Dm 644 LICENSE "${pkgdir}"/usr/share/licenses/libevent/LICENSE
}
