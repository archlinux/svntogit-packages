# $Id$
# Maintainer: Tom Gundersen <teg@jklm.no>
# Contributor: Judd <jvinet@zeroflux.org>

pkgname=libevent
pkgver=2.0.22
pkgrel=1
pkgdesc="An event notification library"
arch=('i686' 'x86_64')
url="http://libevent.org/"
license=('BSD')
depends=('openssl')
optdepends=('python2: to use event_rpcgen.py')
source=(http://sourceforge.net/projects/levent/files/libevent/libevent-${pkgver%.*}/${pkgname}-$pkgver-stable.tar.gz{,.asc})
md5sums=('c4c56f986aa985677ca1db89630a2e11'
         'SKIP')
validpgpkeys=('B35BF85BF19489D04E28C33C21194EBB165733EA')

build() {
  cd ${pkgname}-${pkgver}-stable
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

check() {
  cd ${pkgname}-${pkgver}-stable
  make check
}

package() {
  cd ${pkgname}-${pkgver}-stable
  make DESTDIR="${pkgdir}" install
  install -Dm 644 LICENSE "${pkgdir}"/usr/share/licenses/libevent/LICENSE
}
