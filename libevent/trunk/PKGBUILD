# $Id$
# Maintainer: Tom Gundersen <teg@jklm.no>
# Contributor: Judd <jvinet@zeroflux.org>

pkgname=libevent
pkgver=2.0.22
pkgrel=1
pkgdesc="An event notification library"
arch=('i686' 'x86_64')
url="http://libevent.org/"
license=('BSD')
depends=('openssl')
optdepends=('python2: to use event_rpcgen.py')
source=(https://github.com/libevent/libevent/archive/release-$pkgver-stable.zip)
sha1sums=('710c0d1e070b8bf0da37189d218b811871d517d4')
validpgpkeys=('B35BF85BF19489D04E28C33C21194EBB165733EA')

build() {
  cd ${pkgname}-release-${pkgver}-stable
  ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

check() {
  cd ${pkgname}-release-${pkgver}-stable
  make check
}

package() {
  cd ${pkgname}-release-${pkgver}-stable
  make DESTDIR="${pkgdir}" install
  install -Dm 644 LICENSE "${pkgdir}"/usr/share/licenses/libevent/LICENSE
}
