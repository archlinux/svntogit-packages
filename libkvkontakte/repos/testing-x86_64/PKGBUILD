# $Id$
# Maintainer:
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=libkvkontakte
pkgver=4.12.0
pkgrel=2
pkgdesc="C++ library for asynchronous interaction with VK social network via its web API"
arch=(i686 x86_64)
url='https://projects.kde.org/projects/extragear/libs/libkvkontakte'
license=(GPL)
depends=(kdelibs qjson)
makedepends=(cmake automoc4)
source=("http://download.kde.org/stable/$pkgname/$pkgver/src/$pkgname-$pkgver.tar.xz")
sha1sums=('51232c697dcc02231e210dc6b309aa7c7b4ceecc')

prepare() {
  mkdir -p build 

# Fix build with recent cmake
  cd $pkgname-$pkgver
  sed -e "/^cmake_minimum_required/ a cmake_policy(SET CMP0022 OLD)\ncmake_policy(SET CMP0059 OLD)" -i CMakeLists.txt
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
