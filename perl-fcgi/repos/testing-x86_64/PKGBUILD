# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: François Charette <firmicus ατ gmx δοτ net>

pkgname=perl-fcgi
pkgver=0.77
pkgrel=2
pkgdesc="Fast CGI"
arch=('i686' 'x86_64')
url="http://search.cpan.org/dist/FCGI"
license=('GPL' 'PerlArtistic')
depends=('perl')
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/E/ET/ETHER/FCGI-${pkgver}.tar.gz")
md5sums=('8d88f0fe90f05973522813263b0be39b')

build() {
  cd  "${srcdir}/FCGI-${pkgver}"
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd  "${srcdir}/FCGI-${pkgver}"
  make test
}

package() {
  cd  "${srcdir}/FCGI-${pkgver}"
  make install DESTDIR="${pkgdir}"

# template start; name=perl-binary-module-dependency; version=1;
if [[ $(find "$pkgdir/usr/lib/perl5/" -name "*.so") ]]; then
	_perlver_min=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]);')
	_perlver_max=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]+1);')
	depends+=("perl>=$_perlver_min" "perl<$_perlver_max")
fi
# template end;
}
