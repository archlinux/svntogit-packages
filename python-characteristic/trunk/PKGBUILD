# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>

pkgbase=python-characteristic
pkgname=('python-characteristic' 'python2-characteristic')
pkgver=14.0.0
pkgrel=1
pkgdesc="Service identity verification for pyOpenSSL"
arch=('any')
license=('MIT')
url="http://pypi.python.org/pypi/characteristic"
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest' 'python2-pytest')
source=("http://pypi.python.org/packages/source/c/characteristic/characteristic-${pkgver}.tar.gz")
md5sums=('baea1f28f71c47d88d7d24335f814ee8')

prepare() {
   cp -a characteristic-${pkgver}{,-python2}
}

build() {
   # Build python 3 module
   cd characteristic-${pkgver}
   python3 setup.py build
 
   # Build python 2 module
   cd ../characteristic-${pkgver}-python2
   python2 setup.py build
}

check() {
   # Check python3 module
   cd "${srcdir}"/characteristic-${pkgver}
   python -Walways setup.py test

   # Check python2 module
   cd "${srcdir}"/characteristic-${pkgver}-python2
   python2 -Walways setup.py test
}
 
package_python-characteristic() {
   depends=('python')
 
   cd characteristic-${pkgver}
   python3 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
   install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
 
package_python2-characteristic() {
   depends=('python2')
 
   cd characteristic-${pkgver}-python2
   python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
   install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
