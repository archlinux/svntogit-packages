# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-ksudoku
pkgver=4.9.90
pkgrel=1
pkgdesc="A logic-based symbol placement puzzle"
url="http://kde.org/applications/games/ksudoku/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'libkdegames')
makedepends=('cmake' 'automoc4' 'mesa')
install=${pkgname}.install
source=("http://download.kde.org/unstable/${pkgver}/src/ksudoku-${pkgver}.tar.xz")
sha1sums=('54cabfde7f7afaac42af1614e0e5b548b0533de0')

build() {
  mkdir build
  cd build
  cmake ../ksudoku-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
