# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-ksudoku
pkgver=4.12.97
pkgrel=1
pkgdesc="A logic-based symbol placement puzzle"
url="http://kde.org/applications/games/ksudoku/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'libkdegames' 'glu')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/unstable/${pkgver}/src/ksudoku-${pkgver}.tar.xz")
sha1sums=('1c44fa396d8bbb84a8bde03c37f2d9b67bb6f882')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../ksudoku-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
