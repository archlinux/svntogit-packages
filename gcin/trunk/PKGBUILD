# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=gcin
pkgver=2.8.3
pkgrel=2
pkgdesc='Input method server supporting various input methods'
url='http://hyperrate.com/dir.php?eid=67'
license=('LGPL')
arch=('i686' 'x86_64')
depends=('gtk2' 'libxtst')
makedepends=('qt5-base' 'qt4' 'gtk3' 'anthy' 'libchewing')
optdepends=('qt5-base: support for qt5 input method'
            'qt4: support for qt4 input method'
            'gtk3: support for gtk3 input method'
            'anthy: support for anthy input method'
            'libchewing: support for chewing input method')
source=("http://hyperrate.com/gcin-source/${pkgname}-${pkgver}.tar.xz")
sha1sums=('95b980f7cf9dc2ad91803113cdbb4ac831f056de')

install=install

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	sed 's/lib64/lib/g' -i configure
	sed '/include suffixes-rule/a \
		CFLAGS+='"${CFLAGS}"' \
		LDFLAGS+='"${LDFLAGS}"' \
		OPTFLAGS=' \
		-i Makefile
	sed \
		-e 's:qt5/QtGui/5.2.1:qt/QtGui/5.4.1:g' \
		-e 's:QT=qt5:QT=qt:' \
		-i qt5-im/Makefile
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr --use_i18n=Y
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
