# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=gcin
pkgver=1.5.5
pkgrel=1
pkgdesc='Traditional Chinese Input Method Server for X window system (XIM)'
arch=('i686' 'x86_64')
license=('LGPL')
url='http://www.csie.nctu.edu.tw/~cp76/gcin/'
depends=('gtk2>=2.16.1' 'libxtst')
makedepends=('anthy' 'qt')
optdepends=('qt:	support for Qt4 Input Method')
install=gcin.install
source=("http://cle.linux.org.tw/gcin/download/${pkgname}-${pkgver}.tar.bz2"
        'fix_x86_64_libdir.patch'
        'fix_qt4_paths.patch')
#options=('!makeflags')
sha1sums=('6d3313c1784ef74c09c2ba68be81720450892604'
          '60113b605bc509d660f03852be546ea757710f99'
          '1664ab0c760d2e769ee8a90c0879928b21fd7ae1')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np0 -i "${srcdir}/fix_x86_64_libdir.patch" || return 1
  patch -Np1 -i "${srcdir}/fix_qt4_paths.patch" || return 1
  ./configure --prefix=/usr || return 1
  make || return 1
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install || return 1
}
