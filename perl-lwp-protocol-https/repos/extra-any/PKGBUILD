# Maintainer: Felix Yan <felixonmars@gmail.com>

pkgname=perl-lwp-protocol-https
pkgver=6.10
pkgrel=2
pkgdesc="Provide https support for LWP::UserAgent"
arch=('any')
url="https://metacpan.org/release/LWP-Protocol-https"
license=('PerlArtistic' 'GPL')
depends=('ca-certificates' 'perl-io-socket-ssl' 'perl-net-http' 'perl-libwww')
checkdepends=('perl-test-requiresinternet')
options=('!emptydirs')
source=("https://search.cpan.org/CPAN/authors/id/O/OA/OALDERS/LWP-Protocol-https-$pkgver.tar.gz"
        certs.patch)
sha512sums=('4d3c89dab6fadcb574c176977ca6debba177ee3f817e87319d9cd81e0a0b09214e271c2e97edc456dad2f4a1ebf8c6266a4ae52c3a93144880a5b32db97e7a31'
            '96f401471199c368895eb9b0e819a7287ed9e25953f756585ef7e809010e5d19c8b0614f3cd99cad0c33ca7c9a609ceea4b90569d3f7dec546cc8eeecd810d26')

prepare() {
  cd LWP-Protocol-https-${pkgver}
  patch -p1 -i ../certs.patch
}

build() {
  cd LWP-Protocol-https-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd LWP-Protocol-https-${pkgver}
  make test
}

package() {
  cd LWP-Protocol-https-${pkgver}
  make DESTDIR="$pkgdir" install
}
