# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>

pkgbase=mtr
pkgname=(mtr mtr-gtk)
pkgver=0.85
pkgrel=3
arch=('i686' 'x86_64')
url="http://www.bitwizard.nl/mtr/"
license=('GPL')
makedepends=('ncurses' 'gtk2')
source=(ftp://ftp.bitwizard.nl/mtr/$pkgbase-$pkgver.tar.gz
        0001-Fix-bombout-with-ipv6-enabled-binary-on-ipv4-only-sy.patch
        0001-core-introduce-grace-period.patch
        0001-dns-iterate-over-all-configured-nameservers.patch)
md5sums=('5e665c617e5659b6ec3e201ee7488eb1'
         '9750d5aed1cd20e19b1c7c5e2f75705b'
         '1b3fe01c88411b1e09787724f0a95f31'
         'e0f75febc4a3a0421a953af84dd1a0e7')

prepare() {
    cd "$srcdir/$pkgbase-$pkgver"

    patch -Np1 -i ../0001-Fix-bombout-with-ipv6-enabled-binary-on-ipv4-only-sy.patch
    patch -Np1 -i ../0001-core-introduce-grace-period.patch
    patch -Np1 -i ../0001-dns-iterate-over-all-configured-nameservers.patch
}

build() {
    cp -r  $pkgbase-$pkgver $pkgbase-cli
    cd mtr-cli
    ./configure --prefix=/usr --without-gtk --sbindir=/usr/bin
    make

    cd "$srcdir/$pkgbase-$pkgver"
    ./configure --prefix=/usr --sbindir=/usr/bin
    make
}

package_mtr() {
    pkgdesc="Combines the functionality of traceroute and ping into one tool (CLI version)"
    depends=('ncurses' 'glib2')
    cd mtr-cli
    make DESTDIR="$pkgdir" install
}

package_mtr-gtk() {
    pkgdesc="Combines the functionality of traceroute and ping into one tool (GTK version)"
    depends=('ncurses' "gtk2")
    conflicts=("mtr")
    provides=("mtr=$pkgver")

    cd $pkgbase-$pkgver
    make DESTDIR="$pkgdir" install
}
