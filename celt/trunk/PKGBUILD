# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Lauri Niskanen <ape@ape3000.com>
# Contributor: Ray Rashif <schivmeister@gmail.com>

pkgname=celt
pkgver=0.11.1
pkgrel=4
pkgdesc='Low-latency audio communication codec'
arch=('i686' 'x86_64')
url='http://www.celt-codec.org'
license=('BSD')
depends=('libogg')
options=('!libtool')
source=("http://downloads.xiph.org/releases/${pkgname}/${pkgname}-${pkgver}.tar.gz")
md5sums=('94fe4acd241da8702c295bcd13a555e6')

build() {
  cd ${pkgname}-${pkgver}

  ./configure \
    --prefix=/usr \
    --enable-custom-modes

  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install

  install -D -m644 COPYING \
    ${pkgdir}/usr/share/licenses/${pkgname}/BSD
}
