# $Id$

# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: Brad Gordon <brad@rpgcyco.net>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=libmowgli
pkgver=2.0.0
pkgrel=1
pkgdesc='Performance and usability-oriented extensions to C'
url='http://www.atheme.org/project/mowgli'
license=('custom')
depends=('glibc')
arch=('i686' 'x86_64')
source=("http://atheme.org/downloads/${pkgname}-${pkgver}.tar.gz")
sha1sums=('5a697380f253f0456a5d8a38006a9d12847fb538')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
