# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-xmag
pkgver=1.0.6
pkgrel=2
pkgdesc="Magnify parts of the screen"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libxaw' 'libxmu' 'libxt' 'libx11')
makedepends=('xorg-util-macros')
source=(https://xorg.freedesktop.org/archive/individual/app/xmag-${pkgver}.tar.bz2{,.sig})
sha512sums=('98f3c8e0c58f31cd4f1736ba9c5cd68dba3b2e2de6191988de8ae3e91b1d310803461840189ad56be45ee57c9ca05a2c63544a49b02ce6f79f2ebc14c59613de'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # Alan Coopersmith <alan.coopersmith@oracle.com>

build() {
  cd xmag-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd xmag-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
