# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=lcms2
pkgver=2.12
pkgrel=1
pkgdesc='Small-footprint color management engine, version 2'
arch=('x86_64')
url='http://www.littlecms.com/'
license=('MIT')
depends=('libtiff')
provides=('liblcms2.so')
source=("https://github.com/mm2/Little-CMS/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('e501f1482fc424550ef3abbf86bf1c66090e1661249e89552d39ed5bf935df66')

build() {
  cd Little-CMS-${pkgver}

  ./configure \
    --prefix=/usr
  make
}

check() {
  cd Little-CMS-${pkgver}

  make check
}

package() {
  cd Little-CMS-${pkgver}

  make DESTDIR="${pkgdir}" install
  install -D -m0644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
