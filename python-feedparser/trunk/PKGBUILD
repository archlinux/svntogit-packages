# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgbase=python-feedparser
pkgname=('python2-feedparser' 'python-feedparser' 'python-sgmllib')
pkgver=5.1.3
pkgrel=2
pkgdesc="Parse RSS and Atom feeds in Python"
arch=('any')
url="http://code.google.com/p/feedparser/"
license=('custom')
makedepends=('python' 'python2' 'libxml2' 'python2-distribute' 'python-distribute')
source=(http://feedparser.googlecode.com/files/feedparser-${pkgver}.tar.bz2
        fix-chardet-support-in-python-3.patch)
sha1sums=('dfe654e4babd258b1402b89e889d78330206d78c'
          '18f6a1290b5d7a256b7bf3d1a41b9a7dd2706cf3')

build() {
  cd "${srcdir}"

  # http://code.google.com/p/feedparser/issues/detail?id=384
  # https://bugs.archlinux.org/task/33254
  patch -d "feedparser-$pkgver" -Np1 -i \
    "$srcdir/fix-chardet-support-in-python-3.patch"

  chmod 644  feedparser-${pkgver}/feedparser/feedparser.egg-info/*
  cp -r feedparser-${pkgver} feedparser-${pkgver}-python2
  cp -r feedparser-${pkgver} feedparser-${pkgver}-python
  cp -r feedparser-${pkgver} feedparser-${pkgver}-sgmllib

  cd feedparser-${pkgver}-python2
  sed -i 's#env python$#env python2#' feedparser/feedparser.py
  cd ..

  cd feedparser-${pkgver}-python
  2to3 -w feedparser/feedparser.py feedparser/feedparsertest.py
  cd ..

  cd feedparser-${pkgver}-sgmllib
  python /usr/lib/python3.3/compileall.py feedparser/sgmllib3.py
}

package_python2-feedparser() {
  depends=('python2' 'libxml2' )
  pkgdesc="Parse RSS and Atom feeds in Python2"

  cd "${srcdir}/feedparser-${pkgver}-python2"
  python2 setup.py install --root="${pkgdir}"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/license"
}

package_python-feedparser() {
  depends=('libxml2' 'python-sgmllib')
  
  cd "${srcdir}/feedparser-${pkgver}-python"
  python setup.py install --root="${pkgdir}"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/license"
}

package_python-sgmllib() {
  depends=('python')
  pkgdesc="Port of sgmllib to python-3"
  license=('PSF')
  
  cd "${srcdir}/feedparser-${pkgver}-sgmllib"
  install -Dm644 feedparser/sgmllib3.py \
    "${pkgdir}/usr/lib/python3.3/site-packages/sgmllib.py"
  install -Dm644 feedparser/__pycache__/sgmllib3.cpython-33.pyc \
    "${pkgdir}/usr/lib/python3.3/site-packages/__pycache__/sgmllib.cpython-33.pyc"
}
