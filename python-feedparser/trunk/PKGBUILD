# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgbase=python-feedparser
pkgname=('python2-feedparser' 'python-feedparser' 'python-sgmllib')
pkgver=5.2.1
pkgrel=1
pkgdesc="Parse RSS and Atom feeds in Python"
arch=('any')
url="https://github.com/kurtmckee/feedparser/"
license=('custom')
makedepends=('python' 'python2' 'libxml2' 'python2-setuptools' 'python-setuptools')
source=(feedparser-${pkgver}.tar.gz::https://github.com/kurtmckee/feedparser/archive/${pkgver}.tar.gz)
sha1sums=('13c9a17f821e46ba9a34d8777e0ae47a6eb86de4')

prepare() {
  cp -r feedparser-${pkgver} feedparser-${pkgver}-python2
  cp -r feedparser-${pkgver} feedparser-${pkgver}-python
  cp -r feedparser-${pkgver} feedparser-${pkgver}-sgmllib

  (cd feedparser-${pkgver}-python2; sed -i 's#env python$#env python2#' feedparser/feedparsertest.py)
  (cd feedparser-${pkgver}-python; 2to3 -w feedparser/feedparser.py feedparser/feedparsertest.py)
}

build() {
  cd feedparser-${pkgver}-sgmllib
  python /usr/lib/python3.5/compileall.py feedparser/sgmllib3.py
}

package_python2-feedparser() {
  depends=('python2' 'libxml2' )
  pkgdesc="Parse RSS and Atom feeds in Python2"

  cd "${srcdir}/feedparser-${pkgver}-python2"
  python2 setup.py install --root="${pkgdir}"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/license"
}

package_python-feedparser() {
  depends=('libxml2' 'python-sgmllib')
  
  cd "${srcdir}/feedparser-${pkgver}-python"
  python setup.py install --root="${pkgdir}"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/license"
}

package_python-sgmllib() {
  depends=('python')
  pkgdesc="Port of sgmllib to Python3"
  license=('PSF')
  
  cd "${srcdir}/feedparser-${pkgver}-sgmllib"
  install -Dm644 feedparser/sgmllib3.py \
    "${pkgdir}/usr/lib/python3.5/site-packages/sgmllib.py"
  install -Dm644 feedparser/__pycache__/sgmllib3.cpython-35.pyc \
    "${pkgdir}/usr/lib/python3.5/site-packages/__pycache__/sgmllib.cpython-35.pyc"
}
