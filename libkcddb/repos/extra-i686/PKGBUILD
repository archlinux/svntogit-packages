# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=libkcddb
pkgver=4.9.2
pkgrel=1
pkgdesc="KDE CDDB library"
url='https://projects.kde.org/projects/kde/kdemultimedia/libkcddb'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdelibs' 'libmusicbrainz5')
makedepends=('cmake' 'automoc4')
replaces=('kdemultimedia-kioslave')
conflicts=('kdemultimedia-kioslave')
source=("http://download.kde.org/stable/${pkgver}/src/${pkgname}-${pkgver}.tar.xz")
sha1sums=('11a83fcf8e699d787b0c037e50f2031874470d70')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
