# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdewebkit
pkgver=5.3.0
pkgrel=1
pkgdesc='KDE Webkit'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kdewebkit'
license=('LGPL')
depends=('qt5-webkit' 'kparts')
makedepends=('extra-cmake-modules')
groups=('kf5')
source=("http://download.kde.org/stable/frameworks/${pkgver}/${pkgname}-${pkgver}.tar.xz")
md5sums=('99a174cd395dd1b875f38f81783469c7')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
