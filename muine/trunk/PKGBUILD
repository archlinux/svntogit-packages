# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Ben Mazer <blm@groknil.org>

pkgname=muine
pkgver=0.8.11
pkgrel=1
pkgdesc="A music player written in C#"
arch=(i686 x86_64)
license=('GPL')
url="http://www.muine-player.org/"
depends=('gconf>=2.28.0' 'gtk2>=2.18.3' 'gnome-icon-theme>=2.28.0' 'gstreamer0.10>=0.10.25' 'mono>=2.4.2.3' 'gnome-sharp>=2.24.1' 'taglib-sharp>=2.0.3.2' 'ndesk-dbus>=0.6.0' 'ndesk-dbus-glib>=0.4.1' 'gstreamer0.10-base-plugins>=0.10.25' 'gstreamer0.10-good-plugins>=0.10.16')
makedepends=('intltool')
optdepends=('gstreamer0.10-ugly-plugins: Extra media codecs'
            'gstreamer0.10-bad-plugins: Extra media codecs'
            'gstreamer0.10-ffmpeg: Extra media codecs')
options=('!libtool' '!emptydirs' '!makeflags')
install=muine.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/0.8/${pkgname}-${pkgver}.tar.bz2)
md5sums=('f2a74519d9e9c6c4fca9c5217a13964a')

build() {

  export MONO_SHARED_DIR="${srcdir}/.wabi"
  mkdir -p "${MONO_SHARED_DIR}"
 
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --disable-static || return 1
  make || return 1
  make DESTDIR="${pkgdir}" GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 install || return 1

  install -m755 plugins/TrayIcon.{dll,xml} "${pkgdir}/usr/lib/muine/plugins/" || return 1
  install -m644 plugins/muine-tray-*.png "${pkgdir}/usr/lib/muine/plugins/" || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain muine ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
