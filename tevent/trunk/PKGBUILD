# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Christian Hesse <mail@eworm.de>
# Contributor: Marco A Rojas <marquicus at gmail dot com>
# Contributor: Ng Oon-Ee <ngoonee.talk@gmail.com>
# Contributor: Thomas Burdick <thomas.burdick@gmail.com>

pkgname=tevent
pkgver=0.12.1
pkgrel=1
epoch=1
pkgdesc='Event system based on the talloc memory management library'
url="https://tevent.samba.org/"
arch=('x86_64')
source=(https://samba.org/ftp/tevent/${pkgname}-${pkgver}.tar{.gz,.asc})
license=('GPL3')
depends=('talloc')
makedepends=('python' 'cmocka')
optdepends=('python: for python bindings')
provides=(libtevent.so)
validpgpkeys=('9147A339719518EE9011BCB54793916113084025') # Samba Library Distribution Key <samba-bugs@samba.org>
sha512sums=('ab70262702d70ce00e8b3aef41930c674b04c147fdbf0006e57cfa057d82f6049ba6da4b2c2b7b53aa191dd4f0f3d55e72b0666ae8f5b6948eeb9fde1988c938'
            'SKIP')

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --bundled-libraries=NONE \
    --builtin-libraries=replace
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
