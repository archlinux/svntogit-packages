# $Id$
# Maintainer: Aaron Griffin <aaron@archlinux.org>

pkgname=fetchmail
pkgver=6.3.20
pkgrel=1
pkgdesc="A remote-mail retrieval utility"
arch=('i686' 'x86_64')
url="http://fetchmail.berlios.de/"
license=('GPL')
depends=('glibc' 'openssl' 'python2')
optdepends=('tk: for using fetchmailconf')
backup=('etc/conf.d/fetchmail')
options=('!makeflags')
install=fetchmail.install
source=(http://download2.berlios.de/fetchmail/${pkgname}-${pkgver}.tar.bz2
        fetchmail.rc fetchmail.conf)
md5sums=('9176a9005a816946f766774bc4cbc642'
         '89fdde15bf81b3a5c065949322b5d138'
         '8fcd6a22dfbc247755fdd050c5c5c4d3')
sha1sums=('797b5b0050763ad111c244aba606b2fcb4dfdaad'
          '910ec21ad0a619db67e4665189dc46d2a0634ea5'
          '30401729386d6f774c6c36ab8530842166de54a8')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i 's|/usr/bin/env python|/usr/bin/env python2|' fetchmailconf.py
  ./configure --prefix=/usr --with-ssl=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm755 "${srcdir}/fetchmail.rc" "${pkgdir}/etc/rc.d/fetchmail"
  install -Dm644 "${srcdir}/fetchmail.conf" "${pkgdir}/etc/conf.d/fetchmail"
  install -d "${pkgdir}/var/lib/fetchmail"
}
