# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kdewebdev
pkgname=('kdewebdev-kfilereplace'
         'kdewebdev-kimagemapeditor'
         'kdewebdev-klinkstatus'
         'kdewebdev-kommander')
pkgver=16.04.2
pkgrel=1
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdewebdev')
makedepends=('cmake' 'automoc4' 'ruby' 'tidy' 'kdepimlibs4' 'boost')
source=("http://download.kde.org/stable/applications/${pkgver}/src/${pkgbase}-${pkgver}.tar.xz")
sha1sums=('1c777c6bfe360e37780db5f7ffefa101f166df8b')

build() {
	cd $srcdir
	mkdir -p build
	cd build
	cmake ../${pkgbase}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DKDE4_BUILD_TESTS=OFF \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package_kdewebdev-kfilereplace() {
	pkgdesc='Search & Replace Tool'
	depends=('kdebase-runtime')
	url="http://kde.org/applications/utilities/kfilereplace/"
	cd $srcdir/build/kfilereplace
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kfilereplace
	make DESTDIR=$pkgdir install
}

package_kdewebdev-kimagemapeditor() {
	pkgdesc='HTML Image Map Editor'
	depends=('kdebase-runtime')
    url="http://www.kde.org/applications/development/kimagemapeditor/"
	cd $srcdir/build/kimagemapeditor
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kimagemapeditor
	make DESTDIR=$pkgdir install
}

package_kdewebdev-klinkstatus() {
	pkgdesc='Link Checker'
	depends=('kdebase-runtime' 'tidy')
    url="http://www.kde.org/applications/development/klinkstatus/"
	cd $srcdir/build/klinkstatus
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/klinkstatus
	make DESTDIR=$pkgdir install
}

package_kdewebdev-kommander() {
	pkgdesc='Executor for Kommander dialogs'
	depends=('kdebase-runtime')
        url="http://www.kde.org/applications/development/kommander/"
	cd $srcdir/build/kommander
	make DESTDIR=$pkgdir install
}
