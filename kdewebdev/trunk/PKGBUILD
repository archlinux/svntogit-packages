# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kdewebdev
pkgname=('kdewebdev-kfilereplace'
         'kdewebdev-kimagemapeditor'
         'kdewebdev-klinkstatus'
         'kdewebdev-kommander')
pkgver=4.4.5
pkgrel=1
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdewebdev')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'ruby' 'kdepimlibs' 'kdebase-runtime' 'tidyhtml'
             'kdepim-runtime')
source=("http://download.kde.org/stable/${pkgver}/src/${pkgbase}-${pkgver}.tar.bz2")
sha1sums=('8e2693ba56b24e214a0a4ca0e1fa28ffac720ea4')

build() {
	cd $srcdir
	mkdir build
	cd build
	cmake ../${pkgbase}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package_kdewebdev-kfilereplace() {
	pkgdesc='Search & Replace Tool'
	depends=('kdebase-runtime' 'kdelibs')
	install='kdewebdev.install'
	cd $srcdir/build/kfilereplace
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kfilereplace
	make DESTDIR=$pkgdir install
}

package_kdewebdev-kimagemapeditor() {
	pkgdesc='HTML Image Map Editor'
	depends=('kdebase-runtime' 'kdelibs')
	install='kdewebdev.install'
	cd $srcdir/build/kimagemapeditor
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kimagemapeditor
	make DESTDIR=$pkgdir install
}

package_kdewebdev-klinkstatus() {
	pkgdesc='Link Checker'
	depends=('kdebase-runtime' 'kdepimlibs' 'tidyhtml' 'kdepim-runtime')
	install='kdewebdev.install'
	cd $srcdir/build/klinkstatus
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/klinkstatus
	make DESTDIR=$pkgdir install
}

package_kdewebdev-kommander() {
	pkgdesc='Executor for Kommander dialogs'
	depends=('kdebase-runtime' 'kdelibs')
	cd $srcdir/build/kommander
	make DESTDIR=$pkgdir install
}
