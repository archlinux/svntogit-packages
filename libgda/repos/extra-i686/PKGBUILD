# $Id$
# Maintainer: tobias <tobias@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>

pkgname=libgda
pkgver=5.2.2
pkgrel=1
pkgdesc="Data abstraction layer with mysql, pgsql, xml, sqlite providers"
arch=(i686 x86_64)
license=('GPL')
depends=('gtksourceview3' 'libxslt' 'json-glib' 'db' 'ncurses' 'libsoup'
         'libmariadbclient' 'postgresql-libs' 'python2' 'libsecret'
         'hicolor-icon-theme' 'desktop-file-utils' 'graphviz' 'gdk-pixbuf2'
         'iso-codes' 'openssl' 'libgcrypt' 'libldap')
makedepends=('intltool' 'gobject-introspection' 'gtk-doc' 'vala' 'itstool')
url="http://www.gnome-db.org"
install=libgda.install
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/${pkgver:0:3}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('c9b8b1c32f1011e47b73c5dcf36649aaef2f1edaa5f5d75be20d9caadc2bc3e4')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i '1s/python$/&2/' libgda-report/RML/trml*/trml*.py
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --with-bdb=/usr --with-bdb-libdir-name=lib \
      --disable-static --enable-json --enable-gda-gi
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
