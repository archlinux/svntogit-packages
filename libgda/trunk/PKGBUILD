# $Id$
# Maintainer: tobias <tobias@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>

pkgname=libgda
pkgver=4.2.3
pkgrel=1
pkgdesc="data abstraction layer; with mysql, pgsql, xml, sqlite providers"
arch=(i686 x86_64)
license=('GPL')
depends=('glib2>=2.26.0' 'libxslt' 'db>=5.1.19' 'ncurses>=5.7' 'libsoup>=2.32.0' 'libmysqlclient>=5.1.51' 'postgresql-libs>=9.0.1')
makedepends=('intltool')
options=('!libtool' '!makeflags')
url="http://www.gnome-db.org"
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/4.2/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('3da161f0dca5d080d0cb7bee7cce9bc918abd42e0e0d5683acff9dfe2e158cb5')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i -e 's/python/python2/' libgda-report/RML/trml*/trml*.py
  ./configure --prefix=/usr --sysconfdir=/etc \
      --with-bdb=/usr --with-bdb-libdir-name=lib \
      --disable-static
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
