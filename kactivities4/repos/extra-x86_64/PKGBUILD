# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgbase=kactivities4
pkgname=('kactivities4' 'libkactivities4')
pkgver=4.13.2
pkgrel=1
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kdelibs/kactivities'
license=('GPL' 'FDL')
makedepends=('kdelibs' 'cmake' 'automoc4' 'mesa')
source=("http://download.kde.org/stable/${pkgver}/src/kactivities-${pkgver}.tar.xz")
sha1sums=('e717ac5c1a9087cbd18131cead47e1ab3feb778c')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../kactivities-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_NepomukCore=OFF
  make
}

package_kactivities4() {
  pkgdesc='The KActivity Manager Daemon'
  depends=('libkactivities4')
  replaces=('kactivities')
  conflicts=('kactivities')
  provides=('kactivities')

  cd build
  install -Dm755 src/service/kactivitymanagerd \
    "${pkgdir}"/usr/bin/kactivitymanagerd
}

package_libkactivities4() {
  pkgdesc='API for using and interacting with the Activity Manager'
  depends=('kdelibs')

  cd build
  make DESTDIR="${pkgdir}" install

  # Fix conflicts
  rm "${pkgdir}"/usr/bin/kactivitymanagerd
  rmdir "${pkgdir}"/usr/bin
}
