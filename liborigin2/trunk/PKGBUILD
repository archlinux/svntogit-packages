# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgbase=liborigin2
pkgname=('liborigin2' 'liborigin2-docs')
pkgver=20110829
pkgrel=5
_tree_hh_ver=2.81
arch=('x86_64')
url="http://soft.proindependent.com/liborigin2/"
license=('GPL3')
makedepends=('doxygen' 'qt4' 'boost')
replaces=('liborigin')
provides=('liborigin')
source=(http://sourceforge.net/projects/qtiplot.berlios/files/$pkgbase-20110829.zip
	http://tree.phi-sci.com/tree-$_tree_hh_ver.tar.gz
	liborigin.pro.archlinux)
sha512sums=('d0e4013a686fbc61c58b1f39e98aace91978bcadffbb2ef9d32576a3b2f19cc2443d595da2d3ecc19298ec6e29c20717db94e1fda6a64d71f3763841228a01c0'
            '85e910fdb19ffd03ae79302a95ebffdb44725fd9f291a61d6912f743b54fe9b3d1c863e00c7e75d238f428946809e42c7ae6379b1051688fb06ee6c6d761d294'
            '3d6a2637f8dc296d85dbd1b4aa235bc19436fa2aa6249de889d8a703ec501efd33963e8e1bcc572ff8fa0b03f8d22a0b32a0e7b2bb82abfdeed29981f97ca125')

prepare() {
	cd "${srcdir}/${pkgbase}"

	cp ../liborigin.pro.archlinux liborigin2.pro
	cp ../tree-$_tree_hh_ver/src/tree.hh . # get the header back like it was in previous releases
}

build() {
	cd "${srcdir}/${pkgbase}"

	qmake-qt4 liborigin2.pro QMAKESPEC=linux-g++
  	make QTDIR=/usr/ QMAKESPEC=linux-g++

	 # create documentation
        cd doc
        doxygen
}

package_liborigin2() {
        pkgdesc="A library for reading OriginLab OPJ project files"
        depends=('boost-libs')

        cd "${srcdir}/${pkgbase}"

  	INSTALL_ROOT="${pkgdir}" make install

        # install missing includes
        install -Dm644 tree.hh "${pkgdir}/usr/include/liborigin2/tree.hh"
        for i in *.h; do
          install -Dm644 ${i} "${pkgdir}/usr/include/liborigin2/${i}"
        done
}
package_liborigin2-docs() {
        pkgdesc="Documentation for liborigin2: a library for reading OriginLab OPJ project files"

        cd "${srcdir}/${pkgbase}/doc"
	install -d "${pkgdir}/usr/share/doc/${pkgbase}"
	cp -rf html/* "${pkgdir}/usr/share/doc/${pkgbase}"
	chmod 755 "${pkgdir}/usr/share/doc/liborigin2/images"
	chmod 644 "${pkgdir}/usr/share/doc/liborigin2/images/origin_import.png"
}
