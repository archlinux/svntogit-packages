# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=liborigin2
pkgver=20100913
pkgrel=2
pkgdesc="A library for reading OriginLab OPJ project files"
url="http://soft.proindependent.com/liborigin2/"
source=(http://download.berlios.de/qtiplot/${pkgname}-13092010.zip
	liborigin.pro.archlinux
	http://tree.phi-sci.com/tree.hh)
license=('GPL3')
arch=('i686' 'x86_64')
depends=('gcc-libs' 'boost-libs')
makedepends=('doxygen' 'qt' 'boost')
replaces=('liborigin')
provides=('liborigin')
md5sums=('e2b41c4d8a1e2d357dd2b5b331d31375' 
         'd982e2e07836fe14a430c0062cb59cd2'
         'd24a1c3bdd811354ea7f5885d86ddacb')

build() {
	cd ${srcdir}/${pkgname}

	install -Dm644 ${srcdir}/liborigin.pro.archlinux ./liborigin.pro
        install -Dm644 ${srcdir}/tree.hh ./tree.hh # get the header back like it was in previous releases
	qmake liborigin.pro QMAKESPEC=linux-g++
  	make QTDIR=/usr/ QMAKESPEC=linux-g++
}

package() {
        cd ${srcdir}/${pkgname}

  	INSTALL_ROOT=${pkgdir} make install

        # install missing includes
        install -Dm644 tree.hh ${pkgdir}/usr/include/liborigin2/tree.hh
        for i in *.h; do
          install -Dm644 ${i} ${pkgdir}/usr/include/liborigin2/${i}
        done

	 # create documentation
        cd doc
        doxygen

	# install documentation manually
	install -d ${pkgdir}/usr/share/doc/${pkgname}
	cp -rf html/* ${pkgdir}/usr/share/doc/${pkgname}
	chmod 755 ${pkgdir}/usr/share/doc/liborigin2/images
	chmod 644 ${pkgdir}/usr/share/doc/liborigin2/images/origin_import.png
}
