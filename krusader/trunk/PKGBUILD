# $Id$
# Maintainer:
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>
# Contributor: Lucien Immink <l.immink@student.fnt.hvu.nl>

pkgname=krusader
pkgver=2.4.0beta1
_pkgver=2.4.0-beta1
pkgrel=4
pkgdesc="An alternative file manager for KDE resembling Midnight Commander"
arch=('i686' 'x86_64')
url="http://krusader.sourceforge.net/"
license=('GPL')
depends=('kdebase-runtime' 'kdebase-lib')
makedepends=('cmake' 'automoc4' 'docbook-xsl' 'kdeutils-ark')
optdepends=("kdebase-keditbookmarks: to edit bookmarks"
            "kdebase-kwrite: to edit/view files"
            "kdeutils-ark: extract archive with right-click")
install=${pkgname}.install
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${_pkgver}.tar.bz2")
md5sums=('d2340718d05d69e7f2a7f0846471ee12')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-${_pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}/build"
  make DESTDIR="${pkgdir}" install
}
