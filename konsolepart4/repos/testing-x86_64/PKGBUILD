# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=konsolepart4
pkgver=4.14.3
pkgrel=7
arch=('i686' 'x86_64')
url='http://kde.org/applications/system/konsole/'
pkgdesc="KDE terminal part"
license=('GPL' 'LGPL' 'FDL')
depends=('kdebase-runtime' 'kdebase-lib')
makedepends=('cmake' 'automoc4')
provides=('kdebase-konsole')
conflicts=("kdebase-konsole<$pkgver-$pkgrel")
source=("http://download.kde.org/stable/${pkgver}/src/konsole-${pkgver}.tar.xz")
sha1sums=('5ae78061f38f06f80ee10f4475ff33d1c0843b4d')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../konsole-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install

  # provided by konsole
  rm -r "$pkgdir"/usr/{bin,share/{applications,doc}}
  rm "${pkgdir}"/usr/lib/libkdeinit4_konsole.so
}
