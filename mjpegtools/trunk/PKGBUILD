# $Id$
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>
# Mantainer: Roberto Carvajal <roberto@archlinux.org>

pkgname=mjpegtools
pkgver=1.9.0
pkgrel=4
pkgdesc="Set of tools that can do recording of videos and playback, simple cut-and-paste editing and the MPEG compression of audio and video"
arch=(i686 x86_64)
license=('GPL')
url="http://mjpeg.sourceforge.net/"
depends=('libjpeg>=8' 'libpng>=1.4.0' 'sdl' 'gcc-libs' 'libdv')
makedepends=('gtk2>=2.18.6')
optdepends=('gtk2: glav GUI')
options=('!makeflags' '!libtool')
install=mjpegtools.install
source=(http://downloads.sourceforge.net/sourceforge/mjpeg/${pkgname}-${pkgver}.tar.gz
        mjpegtools-1.9.0-glibc-2.10.patch
        png2yuv-fix-memleak.patch
        mjpegtools-1.9.0-jpeg-7.patch)
md5sums=('309a6fcf0900a010d6a9c1e91afc2f5c' 'b8ae66237d83be533db8eea166fd3357'\
         '39e1def8fb0f7c58a217b22dc251a86a' 'ccc7562a3933877d3362da7cf4695ea0')
sha1sums=('1701233354c7ea86b5b7808c4dd5d03a71118e48' '3029f0e835e693b144298ed9f8143c9566be26f3'\
         '8af5c3747756353bef56d03bafbdd086ff15c02c' '2bdb1b3b8591cede11d4a133a758e8ead35db4dd')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i -e '/ARCHFLAGS=/s:=.*:=:' configure
  patch -Np1 -i "$srcdir/mjpegtools-1.9.0-glibc-2.10.patch"
  patch -Np1 -i "${srcdir}/png2yuv-fix-memleak.patch"
  patch -Np0 -i "${srcdir}/mjpegtools-1.9.0-jpeg-7.patch"
  ./configure --prefix=/usr --enable-largefile
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m644 mpeg2enc/mpeg2syntaxcodes.h "${pkgdir}/usr/include/mjpegtools/mpeg2enc/"
}
