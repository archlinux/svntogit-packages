# $Id$
# Maintainer: Daniel Isenmann <daniel@archlinux.org>

pkgname=babl
pkgver=0.1.44
pkgrel=2
pkgdesc="Dynamic, any to any, pixel format conversion library"
arch=(x86_64)
url="http://gegl.org/babl/"
license=(LGPL3)
depends=(glibc)
makedepends=(git meson)
_commit=9e4bfe9f1470a7836f7f885ea95607427ed7997f  # tags/BABL_0_1_44^0
source=("git+https://git.gnome.org/browse/babl#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^BABL_//;s/_/./g;s/-/+/g'
}

build() {
  arch-meson $pkgname build \
    -D enable-sse4_1=false \
    -D enable-f16c=false \
    -D with-docs=false
  ninja -C build
}

check() {
  cd build
  meson test
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}
