# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Hussam Al-Tayeb <ht990332@gmail.com>

pkgname=xmlsec
pkgver=1.2.25
pkgrel=1
pkgdesc="XML Security Library is a C library based on LibXML2"
license=('custom')
arch=('i686' 'x86_64')
url="https://www.aleksey.com/xmlsec/index.html"
depends=('libxslt' 'openssl' 'gnutls' 'nss' 'libltdl')
source=(https://www.aleksey.com/xmlsec/download/${pkgname}1-${pkgver}.tar.gz) # signature file not compatible
sha256sums=('967ca83edf25ccb5b48a3c4a09ad3405a63365576503bf34290a42de1b92fcd2')
#validpgpkeys=('') #

build() {
  cd ${pkgname}1-${pkgver}
  ./configure --prefix=/usr --disable-static
  make
}

check() {
  cd ${pkgname}1-$pkgver
  make -k check
}

package() {
  cd ${pkgname}1-${pkgver}
  make DESTDIR=${pkgdir} install
  install -m755 -d ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/
}
