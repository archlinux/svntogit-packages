# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Hussam Al-Tayeb <ht990332@gmail.com>

pkgname=xmlsec
pkgver=1.2.27
pkgrel=2
pkgdesc="XML Security Library is a C library based on LibXML2"
license=('custom')
arch=('x86_64')
url="https://www.aleksey.com/xmlsec/index.html"
depends=('libxslt' 'openssl' 'gnutls' 'nss' 'libltdl')
source=(https://www.aleksey.com/xmlsec/download/${pkgname}1-${pkgver}.tar.gz # signature file not compatible
        no_extern_c_scope.patch)
sha256sums=('97d756bad8e92588e6997d2227797eaa900d05e34a426829b149f65d87118eb6'
            '2c43c0c31da6b8c12944626598c9f067bb61ad87a58220897e0bf7f4dfffb95a')
#validpgpkeys=('') #

prepare() {
  cd ${pkgname}1-${pkgver}
  # https://github.com/lsh123/xmlsec/commit/0a14f52e9a5976c104d0fc2d1f739508042aa752 - needed to build with icu >=64.1
  patch -Np1 -i ../no_extern_c_scope.patch
}

build() {
  cd ${pkgname}1-${pkgver}
  ./configure --prefix=/usr --disable-static
  make
}

check() {
  cd ${pkgname}1-$pkgver
  make -k check
}

package() {
  cd ${pkgname}1-${pkgver}
  make DESTDIR=${pkgdir} install
  install -m755 -d ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/
}
