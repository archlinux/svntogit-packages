# Maintainer: Angel Velasquez <angvp@archlinux.org> 
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Alessio 'mOLOk' Bolognino <themolok@gmail.com>
# Contributor: Christophe Gueret <tolgam@homegnu.net>

pkgbase=python-gdata
pkgname=('python2-gdata')
pkgver=2.0.18
pkgrel=2
pkgdesc="Python client library for Google data APIs"
arch=('any')
url="https://github.com/google/gdata-python-client"
license=('APACHE')
depends=('python2')
conflicts=('python-gdata<=2.0.17-1')
replaces=('python-gdata<=2.0.17-1')
source=(https://files.pythonhosted.org/packages/source/g/gdata/gdata-${pkgver}.tar.gz)
sha1sums=('4cd6804f2af81697219307421996c6055c7c16e4')

prepare() {
  cd gdata-${pkgver}
  find . -type f -exec sed -i -e 's@^#!/usr/bin/env python@#!/usr/bin/env python2@' \
    -e 's@^#!/usr/.*bin/python.*@#!/usr/bin/python2@'  {} \;
}

package_python2-gdata() {
  cd gdata-${pkgver}
  python2 setup.py install --prefix=/usr --root "${pkgdir}" 
}
