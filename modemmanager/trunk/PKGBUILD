# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=modemmanager
_realname=ModemManager
pkgver=0.7.991
pkgrel=1
pkgdesc="Mobile broadband modem management service"
arch=(i686 x86_64)
url="http://cgit.freedesktop.org/ModemManager/ModemManager/"
license=(GPL2)
depends=(udev ppp libqmi libmbim polkit hicolor-icon-theme)
makedepends=(intltool git gtk-doc)
optdepends=('usb_modeswitch: install if your modem shows up as a storage drive')
options=('!libtool')
install=modemmanager.install
#source=(http://download.gnome.org/sources/$_realname/${pkgver::3}/$_realname-$pkgver.tar.xz)
source=("git://anongit.freedesktop.org/$_realname/$_realname#commit=a00f1d7")
sha256sums=('SKIP')

build() {
  cd $_realname
   ./autogen.sh --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --sbindir=/usr/bin \
        --with-udev-base-dir=/usr/lib/udev \
        --disable-static \
        --with-polkit
  make
}

package() {
  cd $_realname
  make DESTDIR="$pkgdir" install
}
