# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Tobias Kieslich <tobias funnychar archlinux.org>

pkgname=xfce4-mixer
pkgver=4.11.0
pkgrel=1
pkgdesc="The volume control plugin for the Xfce panel"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('GPL2')
groups=('xfce4')
depends=('xfce4-panel' 'gstreamer0.10-base-plugins' 'libunique'
         'libkeybinder2')
optdepends=('gstreamer0.10-good-plugins: for OSS and PulseAudio support')
makedepends=('intltool')
install=$pkgname.install
source=(http://archive.xfce.org/src/apps/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=('fb0c1df201ed1130f54f15b914cbe5a59286e994a137acda5609570c57112de2')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
