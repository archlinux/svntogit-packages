# $Id$
# Maintainer: Tobias Kieslich <tobias funnychar archlinux.org>

pkgname=xfce4-mixer
pkgver=4.8.0
pkgrel=2
pkgdesc="The volume control plugin for the Xfce panel"
arch=(i686 x86_64)
license=('GPL2')
url="http://www.xfce.org/"
groups=('xfce4')
depends=("xfce4-panel>=4.8.0" 'gstreamer0.10-base>=0.10.22' 'hicolor-icon-theme')
makedepends=('intltool')
optdepends=('gstreamer0.10-base-plugins:  to support basic audio hardware' \
            'gstreamer0.10-good-plugins:  well supported hardware'\
            'gstreamer0.10-bad-plugins:   not so well supported hardware'\
            'gstreamer0.10-ugly-plugins:  might contain questionable license hardware')
options=('!libtool')
install=${pkgname}.install
source=(http://archive.xfce.org/src/apps/${pkgname}/4.8/${pkgname}-${pkgver}.tar.bz2)
md5sums=('3fa3a9973e18c04da26709a654e242ff')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--libexecdir=/usr/lib \
	--localstatedir=/var \
	--disable-static \
	--with-sound=alsa \
	--disable-debug
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
