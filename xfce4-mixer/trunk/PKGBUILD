# $Id$
# Maintainer: Tobias Kieslich <tobias funnychar archlinux.org>

pkgname=xfce4-mixer
pkgver=4.10.0
pkgrel=1
pkgdesc="The volume control plugin for the Xfce panel"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('GPL2')
groups=('xfce4')
depends=('xfce4-panel' 'gstreamer0.10-base-plugins' 'libunique')
makedepends=('intltool')
options=('!libtool')
install=$pkgname.install
source=(http://archive.xfce.org/src/apps/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=('a8c589001b438171ea5a6b46ce9895c3ffbc5081ab67805ab9870e540068dade')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
