# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=xaw3d
pkgver=1.6.4
pkgrel=1
pkgdesc="Three-D Athena widgets"
arch=('x86_64')
url="https://directory.fsf.org/project/xaw3d/"
license=('MIT')
depends=('libxmu')
makedepends=('xorg-util-macros')
source=(https://xorg.freedesktop.org/archive/individual/lib/libXaw3d-${pkgver}.tar.xz{,.sig})
sha512sums=('388e905871a823d0f87fbc29e9ea2de95ac13ccaf50e031a01602d8d388d61bcf1823b4ded8c1a06bdce382e8ddcbc1b0182ccef00cde8064b09176163283dba'
            'SKIP')
#validpgpkeys=('3BB639E56F861FA2E86505690FDD682D974CA72A') # "Matt Turner <mattst88@gmail.com>"
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') #  "Alan Coopersmith <alan.coopersmith@oracle.com>"

build() {
  cd libXaw3d-${pkgver}
  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd libXaw3d-${pkgver}
  make DESTDIR="${pkgdir}" install
  
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
