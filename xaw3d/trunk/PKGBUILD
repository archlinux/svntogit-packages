# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=xaw3d
pkgver=1.6.3
pkgrel=1
pkgdesc="Three-D Athena widgets"
arch=('x86_64')
url="https://directory.fsf.org/project/xaw3d/"
license=('MIT')
depends=('libxmu')
makedepends=('xorg-util-macros')
source=(https://xorg.freedesktop.org/archive/individual/lib/libXaw3d-${pkgver}.tar.bz2{,.sig})
sha512sums=('f79f997ff8442563c74f38c7d76fab2c9253691743ca3143047ce698adb4c126d02dc968be7bdc79ab3052b785fd705375d2f01171e3a3266736beb531c91424'
            'SKIP')
validpgpkeys=('3BB639E56F861FA2E86505690FDD682D974CA72A') # "Matt Turner <mattst88@gmail.com>"

build() {
  cd libXaw3d-${pkgver}
  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd libXaw3d-${pkgver}
  make DESTDIR="${pkgdir}" install
  
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
