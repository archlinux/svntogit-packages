# $Id$
#Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libxt
pkgver=1.1.2
pkgrel=2
pkgdesc="X11 toolkit intrinsics library"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libsm' 'libx11')
makedepends=('xorg-util-macros')
options=('!libtool')
source=("${url}/releases/individual/lib/libXt-${pkgver}.tar.bz2"
        Revert_treat_unexpected_returned_events_from_poll_as_errors.diff)
sha1sums=('9e84b807419d78bda4acbd7aea05aed2ab0556cf'
          'bebadf4b90f6a49530b5600a12a9385253e0daa6')

build() {
  cd "${srcdir}/libXt-${pkgver}"
  # revert broken commit, see https://bugs.archlinux.org/task/28844 + https://bugs.archlinux.org/task/28875 + https://bugs.freedesktop.org/show_bug.cgi?id=47203
  patch -Np1 -R -i ${srcdir}/Revert_treat_unexpected_returned_events_from_poll_as_errors.diff
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static
  make
}

package() {
  cd "${srcdir}/libXt-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
