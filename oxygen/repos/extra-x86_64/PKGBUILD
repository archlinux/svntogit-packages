# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Antonio Rojas

pkgname=oxygen
pkgver=5.1.0.1
pkgrel=1
pkgdesc='KDE Oxygen style'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/workspace/oxygen'
license=('LGPL')
depends=('frameworkintegration' 'kwin')
makedepends=('extra-cmake-modules' 'kdoctools')
groups=('plasma-next')
source=("http://download.kde.org/stable/plasma/5.1.0/${pkgname}-${pkgver}.tar.xz")
md5sums=('24667744ad797a612d0fe57b2b1d30e0')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
