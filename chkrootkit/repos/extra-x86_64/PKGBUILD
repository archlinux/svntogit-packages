# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=chkrootkit
pkgver=0.49
pkgrel=3
pkgdesc="Locally checks for signs of a rootkit"
arch=('i686' 'x86_64')
url="http://www.chkrootkit.org"
depends=('sh' 'net-tools')
license=('BSD')
source=(http://www.reznor.com/tools/chkrootkit.tar.gz chkrootkit.cron \
        fix-tools-path.patch backslashes.patch kallsyms.patch)
md5sums=('304d840d52840689e0ab0af56d6d3a18'
         'f4b6494270f708bf016e087104681739'
         '3e5f2d5e2f4fa7a0d780baec9039c07f'
         '758f892dcf73e8a2a4694662fba366d4'
         'd087f3aad8a9e97fea496ef83e4f1d48')
sha1sums=('cec1a3c482b95b20d3a946b07fffb23290abc4a6'
          '6dda90abf779b6f5c3bacd638e1231f34635575d'
          '7fcad8117a064f0a6910134e8bb3a55de110650f'
          'e22546f445c145cf05dbc1a10f7b196fcd1c8202'
          'dc5b402ee69a7a5ae622ecfd733682516df54e88')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i 's|/var/adm|/var/log|' check_wtmpx.c chklastlog.c chkutmp.c chkwtmp.c
  patch -p0 -i "${srcdir}/fix-tools-path.patch"
  patch -p1 -i "${srcdir}/backslashes.patch"
  patch -p1 -i "${srcdir}/kallsyms.patch"
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  for i in check_wtmpx chkdirs chklastlog chkproc chkrootkit \
           chkutmp chkwtmp ifpromisc strings-static ; do
    install -D -m755 $i "${pkgdir}/usr/sbin/$i"
  done
  install -D -m744 "${srcdir}/chkrootkit.cron" "${pkgdir}/etc/cron.weekly/chkrootkit"
  install -D -m644 COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
