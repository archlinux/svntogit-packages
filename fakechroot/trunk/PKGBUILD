# $Id$
# Maintainer : Allan McRae <allan@archlinux.org>
# Contributor: Aaron Griffin <aaron@archlinux.org>

pkgname=fakechroot
pkgver=2.12
pkgrel=1
pkgdesc="Gives a fake chroot environment"
arch=('i686' 'x86_64')
url="https://github.com/fakechroot/fakechroot/wiki"
license=('LGPL')
options=('!libtool')
install=fakechroot.install
source=(https://github.com/downloads/fakechroot/fakechroot/fakechroot-${pkgver}.tar.gz)
md5sums=('2ce4d6ce9b6a2d727a959fd052d09328')

build()
{
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr --libdir=/usr/lib/libfakeroot
  make
  make check
}
  
package()
{
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  
  install -dm755 ${pkgdir}/etc/ld.so.conf.d/
  echo '/usr/lib/libfakeroot/fakechroot/' > ${pkgdir}/etc/ld.so.conf.d/fakechroot.conf
}
