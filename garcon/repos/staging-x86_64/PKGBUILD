# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Xavier Devlamynck <magicrhesus@ouranos.be>

pkgname=garcon
pkgver=4.18.0
pkgrel=1
pkgdesc="Freedesktop.org compliant menu library"
arch=('x86_64')
url="https://docs.xfce.org/xfce/garcon/start"
license=('LGPL')
groups=('xfce4')
depends=('libxfce4ui')
makedepends=('intltool' 'python' 'gobject-introspection')
source=(https://archive.xfce.org/src/xfce/garcon/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=('54633487566a8b8502b71c11a7f719efe27c069bd5773cc95f11ff4ea8f11a14')

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-debug
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
