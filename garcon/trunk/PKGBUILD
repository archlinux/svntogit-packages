# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Xavier Devlamynck <magicrhesus@ouranos.be>

pkgname=garcon
pkgver=0.2.0
pkgrel=1
pkgdesc="Implementation of the freedesktop.org menu specification"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('LGPL')
groups=('xfce4')
depends=('glib2' 'libxfce4util')
makedepends=('pkgconfig' 'intltool' 'xfce4-dev-tools')
replaces=('libxfce4menu')
options=('!libtool' '!makeflags')
source=(http://archive.xfce.org/src/xfce/garcon/0.2/garcon-$pkgver.tar.bz2)
sha256sums=('02dea3edb9c0039eca4748e964c61b0e1cc10f2d7f9ce0c837287ac5fa9ef76c')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
