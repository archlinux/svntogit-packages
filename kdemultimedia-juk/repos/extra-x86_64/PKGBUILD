# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdemultimedia-juk
pkgver=17.08.0
pkgrel=1
pkgdesc='A jukebox, tagger and music collection manager'
url='https://kde.org/applications/multimedia/juk/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdemultimedia')
depends=('kdebase-runtime' 'taglib')
makedepends=('cmake' 'automoc4')
source=("https://download.kde.org/stable/applications/${pkgver}/src/juk-${pkgver}.tar.xz"{,.sig})
sha256sums=('3c8e01c68fcbda87e4ca41b82118e8b48f466876aa6c44a3ab5a8c4cfab1f037'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cd "${srcdir}"
  mkdir -p build
  cd build
  cmake ../juk-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_TunePimp=OFF
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
