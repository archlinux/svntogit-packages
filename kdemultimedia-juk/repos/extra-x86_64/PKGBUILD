# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdemultimedia-juk
pkgver=4.9.0
pkgrel=1
pkgdesc='A jukebox, tagger and music collection manager'
url='http://kde.org/applications/multimedia/juk/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdebase-runtime' 'taglib')
makedepends=('cmake' 'automoc4')
install="${pkgname}.install"
source=("http://download.kde.org/stable/${pkgver}/src/juk-${pkgver}.tar.xz")
sha1sums=('fdd079faeb1572ed286bd2c1cfdd0de15d632c7a')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../juk-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_TunePimp=OFF
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
