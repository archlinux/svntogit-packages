# $Id$
# Maintainer: Dan McGee <dan@archlinux.org>

pkgname=libarchive
pkgver=3.3.1
pkgrel=4
pkgdesc="library that can create and read several streaming archive formats"
arch=('i686' 'x86_64')
url="http://libarchive.org/"
license=('BSD')
depends=('acl' 'attr' 'bzip2' 'expat' 'lz4' 'openssl' 'xz' 'zlib')
options=('strip' 'debug' '!libtool')
provides=('libarchive.so')
source=("$pkgname-$pkgver.tar.gz::https://github.com/$pkgname/$pkgname/archive/v$pkgver.tar.gz")
sha256sums=('ff138120fe7fca1bd02bed6f06d6869c7497658904a2f8916947f9a3f3257377')

build() {
  cd "$pkgname-$pkgver"

  autoreconf -fi
  ./configure \
      --prefix=/usr \
      --without-xml2 \
      --without-nettle \
      --disable-static

  make
}

check() {
  cd "$pkgname-$pkgver"

  # bsdcpio_test fails
  #make check
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  install -D -m644 COPYING "$pkgdir"/usr/share/licenses/libarchive/COPYING
}
