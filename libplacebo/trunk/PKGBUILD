# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=libplacebo
pkgver=0.4.0
pkgrel=1
pkgdesc='Reusable library for GPU-accelerated video/image rendering primitives'
url='https://github.com/haasn/libplacebo'
arch=('x86_64')
license=('LGPL2.1')
depends=('vulkan-icd-loader')
makedepends=('meson' 'ninja' 'vulkan-headers')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/haasn/libplacebo/archive/v${pkgver}.tar.gz)
sha512sums=('4dfcdfaa2e52d68b65219ed6ba8895388a6caadd996212cbe5389b7a2ed54296422ead3ad5b4f4495209e7bfb132cc15d6e6a0ac2edcf0ecae5562912c026710')

build() {
  cd ${pkgbase}-${pkgver}
  arch-meson build \
    -D tests=true \
    -D vulkan=true
  ninja -C build
}

check() {
  cd ${pkgbase}-${pkgver}
  ninja -C build test
}

package() {
  cd ${pkgbase}-${pkgver}
  DESTDIR="${pkgdir}" ninja -C build install
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
