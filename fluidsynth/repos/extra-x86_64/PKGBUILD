# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=fluidsynth
pkgver=2.1.0
pkgrel=1
pkgdesc="A real-time software synthesizer based on the SoundFont 2 specifications"
arch=('x86_64')
url="http://www.fluidsynth.org/"
license=('LGPL2.1')
groups=('pro-audio')
depends=('alsa-lib' 'glibc' 'glib2' 'libjack.so' 'libportaudio.so' 'libpulse'
'libreadline.so' 'libsndfile' 'libsystemd.so' 'sdl2')
makedepends=('cmake' 'ladspa' 'doxygen')
provides=('libfluidsynth.so' 'soundfont-synthesizer')
backup=("etc/conf.d/$pkgname")
source=("$pkgname-$pkgver.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/v$pkgver.tar.gz")
sha512sums=('d7af4047ebde49ef48098ae9dbab5d90422a4536acf28a2de32d8da67a50ebbbcf30e06833b630bd54e441040be0e432b377e3b63bf666e63106e69cd408ea0d')

build() {
  cd "${pkgname}-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DFLUID_DAEMON_ENV_FILE=/etc/conf.d/fluidsynth \
        -Denable-ladspa=ON \
        -Denable-portaudio=ON \
        -DLIB_SUFFIX="" \
        -B build \
        .
  make -C build VERBOSE=1
}

check() {
  cd "${pkgname}-${pkgver}"
  make -C build -k check
}

package() {
  cd "${pkgname}-${pkgver}"
  make -C build DESTDIR="$pkgdir" install
  # systemd user unit
  install -vDm 644 "build/${pkgname}.service" \
    -t "$pkgdir/usr/lib/systemd/user/"
  # system-wide configuration file
  install -vDm 644 "build/${pkgname}.conf" "${pkgdir}/etc/conf.d/${pkgname}"
  # docs
  install -vDm 644 {AUTHORS,ChangeLog,{CONTRIBUTING,README}.md,THANKS,TODO} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
