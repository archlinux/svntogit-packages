# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=fluidsynth
pkgver=2.2.7
pkgrel=1
pkgdesc="A real-time software synthesizer based on the SoundFont 2 specifications"
arch=(x86_64)
url="https://www.fluidsynth.org/"
license=(LGPL2.1)
groups=(pro-audio)
depends=(glibc sdl2)
makedepends=(alsa-lib cmake dbus doxygen glib2 jack ladspa libinstpatch
libpulse libsndfile portaudio readline systemd-libs)
provides=(libfluidsynth.so soundfont-synthesizer)
backup=(etc/conf.d/$pkgname)
options=(debug)
source=($pkgname-$pkgver.tar.gz::https://github.com/$pkgname/$pkgname/archive/v$pkgver.tar.gz)
sha512sums=('62709f96fa07bbafb9b907438da7376abdf1a4e679cd2b105c5bca257f1ff62c18bd4c646ef2ccba64ca5273604da996fb2caaf61eeee44686bf45fef1ed83a7')
b2sums=('fd53603947ddf5a76676cfc3d77b1d904146ec7c30ab6e54a2c32cae0e76ec4b66397506072cac277febdd1b799801d5b13c7b37cc0ea4a545fae26077a1d881')

build() {
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=None \
        -DFLUID_DAEMON_ENV_FILE=/etc/conf.d/fluidsynth \
        -Denable-ladspa=ON \
        -Denable-portaudio=ON \
        -DLIB_SUFFIX="" \
        -Wno-dev \
        -B build \
        -S $pkgname-$pkgver
  make -C build VERBOSE=1
}

check() {
  make -C build -k check
}

package() {
  depends+=(libasound.so libdbus-1.so libglib-2.0.so libgmodule-2.0.so
  libgobject-2.0.so libinstpatch-1.0.so libjack.so libportaudio.so
  libpulse-simple.so libreadline.so libsndfile.so libsystemd.so)

  make -C build DESTDIR="$pkgdir" install
  # systemd user unit
  install -vDm 644 build/$pkgname.service -t "$pkgdir/usr/lib/systemd/user/"
  # system-wide configuration file
  install -vDm 644 build/$pkgname.conf "$pkgdir/etc/conf.d/$pkgname"
  # docs
  install -vDm 644 $pkgname-$pkgver/{AUTHORS,ChangeLog,{CONTRIBUTING,README}.md,THANKS,TODO} -t "$pkgdir/usr/share/doc/$pkgname/"
}
