# $Id$
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=fluidsynth
pkgver=1.1.4
pkgrel=1
pkgdesc="A real-time software synthesizer based on the SoundFont 2 specifications"
arch=('i686' 'x86_64')
url="http://www.fluidsynth.org/"
depends=('glib2' 'jack' 'dbus-core' 'libpulse')
makedepends=('cmake' 'ladspa' 'doxygen')
optdepends=('pulseaudio: PulseAudio sound support'
            'bash: init script')
license=('LGPL')
backup=('etc/conf.d/fluidsynth')
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz"
        'fluidsynth.conf'
        'fluidsynthd')
md5sums=('71f297a90d00a253f0f12ceb5d3d661d'
         '16c5f4d4cbdddc6c5fcbd4fd4cc142f1'
         'b296dbfb524c2164f552f68bd9abe2ec')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  cmake . -DCMAKE_INSTALL_PREFIX=/usr \
          -DCMAKE_BUILD_TYPE=Release \
          -Denable-ladspa=ON \
          -DLIB_SUFFIX=""
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  install -Dm644 "$srcdir/fluidsynth.conf" \
    "$pkgdir/etc/conf.d/fluidsynth"

  install -Dm755 "$srcdir/fluidsynthd" \
    "$pkgdir/etc/rc.d/fluidsynth"
}

# vim:set ts=2 sw=2 et:
