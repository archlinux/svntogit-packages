# $Id$
# Contributor: damir <damir@archlinux.org>

pkgname=fluidsynth
pkgver=1.1.1
pkgrel=2
pkgdesc="A real-time software synthesizer based on the SoundFont 2 specifications"
arch=("i686" "x86_64")
url="http://www.fluidsynth.org/"
depends=('glib2' 'jack' 'ladspa')
options=('!libtool')
license=('LGPL')
backup=('etc/conf.d/fluidsynth')
source=("http://savannah.nongnu.org/download/fluid/$pkgname-$pkgver.tar.gz"
	'fluidsynth.conf' 'fluidsynthd')
md5sums=('0db3da78028d255026230809c6e21b44'
         '16c5f4d4cbdddc6c5fcbd4fd4cc142f1'
         'b296dbfb524c2164f552f68bd9abe2ec')

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure --prefix=/usr \
    --enable-ladspa || return 1
  make || return 1
}

package() {
  cd $srcdir/$pkgname-$pkgver

  make prefix=$pkgdir/usr install
  install -Dm644 ${srcdir}/fluidsynth.conf \
    ${pkgdir}/etc/conf.d/fluidsynth
  install -Dm755 ${srcdir}/fluidsynthd \
    ${pkgdir}/etc/rc.d/fluidsynth
}
