# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgbase=pygobject
pkgname=(python-gobject python2-gobject pygobject-devel)
pkgver=3.0.2
pkgrel=1
arch=('i686' 'x86_64')
url="https://live.gnome.org/PyGObject"
license=('LGPL')
makedepends=('python' 'python2' 'python-cairo' 'python2-cairo' 'gobject-introspection')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgbase}/${pkgver%.*}/${pkgbase}-${pkgver}.tar.xz)
options=('!libtool')
sha256sums=('e1eccc37afd2d4cbeb329b92ce3d71287d662dc2fec430c08a0471153e52a462')

build() {
  cd "${srcdir}"
  cp -a "${pkgbase}-${pkgver}" python2-build
  mkdir devel

  (
    cd python2-build
    export PYTHON=/usr/bin/python2
    ./configure --prefix=/usr
    make
  )

  (
    cd "${pkgbase}-${pkgver}"
    ./configure --prefix=/usr
    make
  )
}

package_python-gobject() {
  pkgdesc="Python 3 bindings for GObject"
  depends=('gobject-introspection' 'python-cairo' "pygobject-devel=${pkgver}")

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -r "${pkgdir}"/usr/{include,lib/pkgconfig}
}

package_python2-gobject() {
  pkgdesc="Python 2 bindings for GObject"
  depends=('gobject-introspection' 'python2-cairo' "pygobject-devel=${pkgver}")

  cd "${srcdir}/python2-build"
  make DESTDIR="${pkgdir}" install
  mv "${pkgdir}"/usr/{include,lib/pkgconfig} "${srcdir}/devel"
}

package_pygobject-devel() {
  pkgdesc="Development files for the pygobject bindings"
  cd "${srcdir}/devel"
  mkdir -p "${pkgdir}"/usr/{include,lib}
  mv include "${pkgdir}/usr/"
  mv pkgconfig "${pkgdir}/usr/lib/"
}
