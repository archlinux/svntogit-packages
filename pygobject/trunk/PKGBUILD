# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgbase=pygobject
pkgname=(python-gobject python2-gobject pygobject-devel)
pkgver=3.19.92
pkgrel=1
arch=(i686 x86_64)
url="https://live.gnome.org/PyGObject"
license=(LGPL)
makedepends=(python python2 python-cairo python2-cairo gobject-introspection)
source=(https://download.gnome.org/sources/$pkgbase/${pkgver:0:4}/$pkgbase-$pkgver.tar.xz)
sha256sums=('59ed22d59017727a4a31ae00afaec231350d8fb67a518f08f983ecf62b006191')

prepare() {
  mkdir build-py2 build-py3 devel
  cd ${pkgbase}-${pkgver}
}

build() {
  cd build-py2
  ../${pkgbase}-${pkgver}/configure --prefix=/usr --with-python=/usr/bin/python2
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make

  cd ../build-py3
  ../${pkgbase}-${pkgver}/configure --prefix=/usr --with-python=/usr/bin/python
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package_python-gobject() {
  pkgdesc="Python 3 bindings for GObject"
  depends=('gobject-introspection-runtime' "pygobject-devel=$pkgver" 'python' 'cairo')

  cd build-py3
  make DESTDIR="$pkgdir" install
  rm -r "$pkgdir"/usr/{include,lib/pkgconfig}
}

package_python2-gobject() {
  pkgdesc="Python 2 bindings for GObject"
  depends=('gobject-introspection-runtime' "pygobject-devel=$pkgver" 'python2' 'cairo')

  cd build-py2
  make DESTDIR="$pkgdir" install
  mv "$pkgdir"/usr/{include,lib/pkgconfig} "$srcdir/devel"
  python2 -m compileall "$pkgdir"//usr/lib/python2.7/site-packages/gi
}

package_pygobject-devel() {
  pkgdesc="Development files for the pygobject bindings"

  cd "devel"
  mkdir -p "$pkgdir"/usr/{include,lib}
  mv include "$pkgdir/usr/"
  mv pkgconfig "$pkgdir/usr/lib/"
}
