# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=librevenge
pkgver=0.0.4
pkgrel=5
pkgdesc="library for REVerses ENGineered formats filters"
arch=('x86_64')
url="https://sf.net/p/libwpd/librevenge/"
license=('MPL')
depends=('boost-libs')
makedepends=('boost' 'doxygen' 'cppunit')
options=('debug')
source=(https://sourceforge.net/projects/libwpd/files/librevenge/$pkgname-$pkgver/$pkgname-$pkgver.tar.xz)
sha1sums=('533dd83a668d3efc0095ad82718e34662395635d')
sha256sums=('933f0729f04267cc354b9a02bc3e9afefa5512a3bdd0b45f159ee14a3e3347b2')

build() {
	cd $pkgname-$pkgver
	./configure --prefix=/usr --disable-werror
	sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
	make
}

check() {
	cd $pkgname-$pkgver
	make check
}

package() {
	cd $pkgname-$pkgver
	make DESTDIR="$pkgdir/" install
}
