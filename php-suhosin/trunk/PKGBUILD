# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=php-suhosin
pkgver=0.9.31
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc='An advanced protection system for PHP installations'
url='http://www.hardened-php.net/suhosin/'
license='PHP'
source=("http://download.suhosin.org/suhosin-${pkgver}.tgz")
depends=('php')
conflicts=('php-suhosin-extension')
replaces=('php-suhosin-extension')
backup=('etc/php/conf.d/suhosin.ini')
md5sums=('70df3405806ee62a952510ded002dccd')

build() {
	cd ${srcdir}/suhosin-${pkgver}
	phpize || return 1
	./configure --prefix=/usr --enable-suhosin || return 1
	make || return 1
}

package() {
	cd ${srcdir}/suhosin-${pkgver}
	make INSTALL_ROOT=${pkgdir} install || return 1
	# disable by default
	sed -i 's|extension = suhosin.so|;extension=suhosin.so|g' suhosin.ini
	install -D -m644 suhosin.ini ${pkgdir}/etc/php/conf.d/suhosin.ini
}
