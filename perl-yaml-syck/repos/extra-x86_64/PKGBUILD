# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-yaml-syck
pkgver=1.31
pkgrel=1
pkgdesc="Fast, lightweight YAML loader and dumper"
arch=('x86_64')
url="http://search.cpan.org/dist/YAML-Syck/"
license=('BSD' 'custom')
depends=('perl')
options=('!emptydirs')
source=(http://www.cpan.org/authors/id/T/TO/TODDR/YAML-Syck-$pkgver.tar.gz)
sha512sums=('5177f415f1f3f0d46b9fc51e43478da627edc3d7e89d9baf095fdda2cd9f93eb46784429f1665fead2b3377a2749c6fb937a91136bbe713d79000c12206a83f5')

build() {
  cd YAML-Syck-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd YAML-Syck-$pkgver
  make test
}

package() {
  cd YAML-Syck-$pkgver
  make install DESTDIR="${pkgdir}"
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
