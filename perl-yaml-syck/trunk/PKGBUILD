# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-yaml-syck
pkgver=1.32
pkgrel=1
pkgdesc="Fast, lightweight YAML loader and dumper"
arch=('x86_64')
url="https://search.cpan.org/dist/YAML-Syck/"
license=('BSD' 'custom')
depends=('perl')
options=('!emptydirs')
source=(https://www.cpan.org/authors/id/T/TO/TODDR/YAML-Syck-$pkgver.tar.gz)
sha512sums=('284d62f1fabdcca9fbdc57b732f5b9b2c2739020c620dbb66e84a89ea83e0a80580ec8fd8516a1c080dd9de9a817dbe357ee66041daa5103a8bf49c436355c99')

build() {
  cd YAML-Syck-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd YAML-Syck-$pkgver
  make test
}

package() {
  cd YAML-Syck-$pkgver
  make install DESTDIR="${pkgdir}"
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
