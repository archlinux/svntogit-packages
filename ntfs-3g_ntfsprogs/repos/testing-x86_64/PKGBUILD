# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Thomas Bächler <thomas.archlinux.org>

pkgbase=ntfs-3g_ntfsprogs
pkgname=('ntfs-3g' 'ntfsprogs')
pkgver=2012.1.15
pkgrel=1
url="http://www.tuxera.com"
arch=('i686' 'x86_64')
license=('GPL2')
depends=('glibc' 'fuse')
makedepends=('pkgconfig')
options=('!libtool')
source=(http://tuxera.com/opensource/$pkgbase-$pkgver.tgz)
sha1sums=('8d55cf49afde172fefa369a0a85289e09c4d7bbb')

build() {
  cd "$srcdir/$pkgbase-$pkgver"
  ac_cv_path_LDCONFIG=/bin/true ./configure --prefix=/usr \
    --with-fuse=external --disable-static \
    --mandir=/usr/share/man
  make
}

package_ntfs-3g() {
  pkgdesc="Stable read and write NTFS driver"

  for DIR in src libntfs-3g include; do
    cd "$srcdir/$pkgbase-$pkgver/$DIR"
    make DESTDIR="$pkgdir" install
  done

  ln -s /bin/ntfs-3g "$pkgdir/sbin/mount.ntfs"
}

package_ntfsprogs() {
  pkgdesc="NTFS filesystem utilities"
  depends=('util-linux' 'ntfs-3g')

  cd "$srcdir/$pkgbase-$pkgver/ntfsprogs"
  make DESTDIR="$pkgdir" install
}
