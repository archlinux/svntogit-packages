# Maintainer: schuay <jakob.gruber@gmail.com>

pkgname=ksshaskpass
pkgver=0.5.3
pkgrel=4
pkgdesc='KDE4 implementation of ssh-askpass with Kwallet integration'
url='http://www.kde-apps.org/content/show.php/Ksshaskpass?content=50971'
license=('GPL')
arch=('i686' 'x86_64')
depends=('kdelibs')
makedepends=('cmake' 'automoc4')
provides=('x11-ssh-askpass')
conflicts=('x11-ssh-askpass')
source=("http://www.kde-apps.org/CONTENT/content-files/50971-$pkgname-$pkgver.tar.gz"
        "$pkgname.sh")
md5sums=('05dad7745e9d92b08bd86e7ab7a9540d'
         '70396ba019ae05cc91502456f548caff')

build() {
  cd "$srcdir"/$pkgname-$pkgver
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release
  make
}
package() {
  cd "$srcdir"/$pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  
  # Remove useless desktop file.
  rm -rf "$pkgdir"/usr/share/applications
  
  # Documentation requires that this application is installed as /usr/bin/ssh-askpass.
  # But this way is cleaner.
  # Add script to profile.d to set ssh password agent to ksshasskpass.
  install -Dm 755 "$srcdir"/$pkgname.sh "$pkgdir"/etc/profile.d/$pkgname.sh
}
