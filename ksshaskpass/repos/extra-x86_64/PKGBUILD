# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: schuay <jakob.gruber@gmail.com>

pkgname=ksshaskpass
pkgver=5.9.3
pkgrel=1
pkgdesc='ssh-add helper that uses kwallet and kpassworddialog'
url='https://www.kde.org/workspaces/plasmadesktop/'
license=('GPL')
arch=('i686' 'x86_64')
depends=('kwallet')
makedepends=('extra-cmake-modules' 'kdoctools' 'python')
provides=('x11-ssh-askpass' 'ksshaskpass-frameworks')
conflicts=('ksshaskpass-frameworks')
replaces=('ksshaskpass-frameworks')
groups=('plasma')
source=("https://download.kde.org/stable/plasma/${pkgver}/${pkgname}-${pkgver}.tar.xz"{,.sig})
install=$pkgname.install
sha256sums=('1c0ab2d4550ba860a492f8b2930283a59d3261ac1797480795122b615799664a'
            'SKIP')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell
              '348C8651206633FD983A8FC4DEACEA00075E1D76'  # KDE Neon
              'D07BD8662C56CB291B316EB2F5675605C74E02CF') # David Edmundson

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../ksshaskpass-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF
  make
}
package() {
  cd build
  make DESTDIR="$pkgdir" install
}
