# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: schuay <jakob.gruber@gmail.com>

pkgname=ksshaskpass
pkgver=5.11.95
pkgrel=1
pkgdesc='ssh-add helper that uses kwallet and kpassworddialog'
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(GPL)
arch=(x86_64)
depends=(kwallet)
makedepends=(extra-cmake-modules kdoctools python)
provides=(x11-ssh-askpass)
groups=(plasma)
source=("https://download.kde.org/unstable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig})
install=$pkgname.install
sha256sums=('3df9d4aa054f7541a5031acb026f278ea44b5a50cfe55b4966b486c9c3cfcbdb'
            'SKIP')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell
              '348C8651206633FD983A8FC4DEACEA00075E1D76'  # KDE Neon
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
