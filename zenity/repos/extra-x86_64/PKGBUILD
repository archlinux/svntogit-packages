# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=zenity
pkgver=3.26.0
pkgrel=1
pkgdesc="Display graphical dialog boxes from shell scripts"
url="https://git.gnome.org/browse/zenity"
arch=(i686 x86_64)
license=(LGPL)
depends=(webkit2gtk libnotify)
makedepends=(itstool yelp-tools gettext gnome-common git)
_commit=3dbfe2667888b0403303d5958d2b6a4ba0643c8b  # tags/ZENITY_3_26_0^0
source=("git+https://git.gnome.org/browse/zenity#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^ZENITY_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var
  make
}

package() {
  cd $pkgname
  make DESTDIR="${pkgdir}" install
}
