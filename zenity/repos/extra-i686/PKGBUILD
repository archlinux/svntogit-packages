# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=zenity
pkgver=3.12.1
pkgrel=2
pkgdesc="Display graphical dialog boxes from shell scripts"
arch=(i686 x86_64)
license=(LGPL)
depends=(gtk3 libnotify)
makedepends=(intltool gtk-doc itstool docbook-xsl yelp-tools)
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz
        0001-fix-dialog-size.patch)
sha256sums=('a59705cdd1ea5318fdae3075c1cedcbead479230e9bead204391566d973dae11'
            '8978914ecea3a3f643f4aec4c465e12e62ccbfe3384de4fe6a7d0e281ecd43a3')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../0001-fix-dialog-size.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="${pkgdir}" install
}
