# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Rustam Tsurik <rustam.tsurik@gmail.com>
# Contributor: Nico Suarez <nicodoggie@gmail.com>

pkgname=galera
pkgver=26.4.2
_mariadb_pkgver=10.4.6
pkgrel=1
pkgdesc='write set replication (WSREP) provider for MariaDB cluster'
arch=('x86_64')
license=('GPL2' 'custom:OpenSSL Linking Exception')
depends=('mariadb' 'openssl')
makedepends=('check' 'scons' 'boost')
optdepends=('rsync: transfer database state to a newly joined node with rsync')
url='http://www.codership.com/'
options=('!libtool')
validpgpkeys=('199369E5404BD5FC7D2FE43BCBCB082A1BB943DB') # MariaDB Package Signing Key <package-signing-key@mariadb.org>
source=("https://mirrors.n-ix.net/mariadb/mariadb-${_mariadb_pkgver}/${pkgname}-${pkgver}/src/${pkgname}-${pkgver}.tar.gz"{,.asc})
sha256sums=('d25159dba7cc597b745819d77ead4f7ca7f9a327d013863108c6040405fd035c'
            'SKIP')

build(){
  cd "${pkgname}-${pkgver}"

  scons system_asio=0
}

package(){
  cd "${pkgname}-${pkgver}"

  install -D -m0755 libgalera_smm.so "${pkgdir}"/usr/lib/galera/libgalera_smm.so
  ln -s galera/libgalera_smm.so "${pkgdir}"/usr/lib/libgalera_smm.so

  install -D -m0755 garb/garbd "${pkgdir}"/usr/bin/garbd

  install -D -m0644 scripts/packages/README "${pkgdir}"/usr/share/doc/galera/README
  install -D -m0644 scripts/packages/README-MySQL "${pkgdir}"/usr/share/doc/galera/README-MySQL
  install -D -m0644 scripts/mysql/README "${pkgdir}"/usr/share/doc/galera/mysql/README
  install -D -m0644 COPYING "${pkgdir}"/usr/share/doc/galera/COPYING
}

