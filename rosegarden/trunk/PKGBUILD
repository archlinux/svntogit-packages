# $Id$
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Robert Emil Berge <robert@rebi.no>

pkgname=rosegarden
pkgver=16.02
_pkgver=16.02
pkgrel=2
pkgdesc="MIDI/audio sequencer and notation editor"
arch=('i686' 'x86_64')
url="http://www.rosegardenmusic.com/"
license=('GPL')
depends=('liblrdf' 'dssi' 'fftw' 'lirc'
         'perl' 'qt5-tools' 'shared-mime-info' 'liblo>=0.28')
makedepends=('imake' 'cmake')
optdepends=('lilypond: notation display'
            'cups: printing support'
            'kdegraphics-okular: print preview, or any other PDF viewer'
            'timidity++: MIDI playback, or any other softsynth'
            'flac'
            'wavpack')
[ "$CARCH" = "i686" ] && optdepends+=('dssi-vst: win32 VST support')
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$_pkgver.tar.bz2" rosegarden-gcc6.patch)
md5sums=('d179f4d9b5d6b8fd1b71ba61ffc8faaa'
         'c79af470ecb11173a9bdfb26c6269b93')

prepare() {
  cd $pkgname-$pkgver
  # Fix build with GCC 6
  patch -p1 -i ../rosegarden-gcc6.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # qt4 compatibility (qt->qt5)
  #LRELEASE=/usr/lib/qt4/bin/lrelease
  #LUPDATE=/usr/lib/qt4/bin/lupdate

  #export lrdf_CFLAGS LRELEASE LUPDATE

  cmake -DBUILD_TESTING=OFF -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
