# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xf86-video-geode
pkgver=2.11.15
pkgrel=2
pkgdesc="X.org AMD/Geode LX & NX video driver"
# there is no 64bit geode
arch=(i686)
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('glibc')
makedepends=('xorg-server-devel' 'X-ABI-VIDEODRV_VERSION=15')
conflicts=('xorg-server<1.15.0' 'X-ABI-VIDEODRV_VERSION<15' 'X-ABI-VIDEODRV_VERSION>=16')
options=('!libtool')
conflicts=('xf86-video-amd')
replaces=('xf86-video-amd')
groups=('xorg-drivers' 'xorg')
source=(${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2
        geode-2.11.14-ftbfs.patch)
sha256sums=('00db3bd697b32286eb0e42f5dbd7fcc8d7255dd11c299b1ee16a06a244868ff0'
            'c444ab1615808fe8016668f41555bf2c1e3c9d326ea48056433ad67de5b1880a')

build() {
  cd ${pkgname}-${pkgver}
  # fix build with Xorg 1.15 - taken from FC
  patch -Np1 -i ${srcdir}/geode-2.11.14-ftbfs.patch
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
