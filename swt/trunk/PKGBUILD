# $Id$
# Maintainer: Guillaume ALAUX <guillaume@archlinux.org>
pkgname=swt
pkgver=3.7.1
pkgrel=1
pkgdesc="An open source widget toolkit for Java"
_date=201109091335
arch=(i686 x86_64)
url="http://www.eclipse.org/swt/"
license=('EPL')
depends=('java-runtime>=6' 'gtk2>=2.20.1' 'libxtst')
#optdepends=('libgnomeui' 'xulrunner' 'mesa')
#makedepends=('openjdk6' 'libxtst' 'mesa' 'libgnomeui' 'xulrunner>=1.9.1' 'unzip' 'pkgconfig' 'apache-ant')
optdepends=('libgnomeui' 'mesa')
makedepends=('openjdk6' 'libxtst' 'mesa' 'libgnomeui' 'unzip' 'pkgconfig' 'apache-ant')
if [ "${CARCH}" = "i686" ]; then
  source=(http://download.eclipse.org/eclipse/downloads/drops/R-${pkgver}-${_date}/swt-${pkgver}-gtk-linux-x86.zip
          build-swt.xml)
  noextract=(swt-${pkgver}-gtk-linux-x86.zip)
  md5sums=('b7d6d24e2feb7489b0baa0ce941ea4d2'
           'f5e548bc26a0f1f3c18131be76face40')
fi
if [ "${CARCH}" = "x86_64" ]; then
  source=(http://download.eclipse.org/eclipse/downloads/drops/R-${pkgver}-${_date}/swt-${pkgver}-gtk-linux-x86_64.zip
          build-swt.xml)
  md5sums=('8816631e7993a783a87e98ba39da7fa6'
           'f5e548bc26a0f1f3c18131be76face40')
  noextract=(swt-${pkgver}-gtk-linux-x86_64.zip)
fi

build() {
  cd "${srcdir}"
  . /etc/profile.d/openjdk6.sh
  . /etc/profile.d/apache-ant.sh
  unzip -jqo ${srcdir}/swt-${pkgver}-gtk-linux-*.zip "*src.zip"
  unzip -qo src.zip
  rm -rf about_files
  mkdir src
  mv org src/
  cp build-swt.xml build.xml

  [ "${CARCH}" = "x86_64" ] && export SWT_PTR_CFLAGS=-DJNI64

  make -f make_linux.mak make_awt
  make -f make_linux.mak make_swt
  make -f make_linux.mak make_atk
  make -f make_linux.mak make_gnome
# "XULRunner >= 2.0 is not currently supported"
#   https://bugs.eclipse.org/bugs/show_bug.cgi?id=327696
#
#  export MOZILLA_INCLUDES="`pkg-config --cflags libxul`"
#  export MOZILLA_LIBS="-Wl,-R`pkg-config libxul --variable=libdir` `pkg-config libxul --libs`"
#  export XULRUNNER_INCLUDES="${MOZILLA_INCLUDES}"
#  export XULRUNNER_LIBS="${MOZILLA_LIBS}"
#  make -f make_linux.mak make_mozilla
#  make -f make_linux.mak make_xulrunner

  make -f make_linux.mak make_cairo
  make -f make_linux.mak make_glx
 
  ant compile
}

package() {
  cd "${srcdir}"
  cp version.txt build/version.txt
  cp src/org/eclipse/swt/internal/SWTMessages.properties build/org/eclipse/swt/internal/

  ant jar

  install -m755 -d "${pkgdir}/usr/share/java"
  install -m755 -d "${pkgdir}/usr/lib"
  install -m644 swt.jar "${pkgdir}/usr/share/java/"
  install -m755 *.so "${pkgdir}/usr/lib/"
}
