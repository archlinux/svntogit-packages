# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: M Rawash <mrawash@gmail.com>

pkgname=xf86-input-wacom
pkgver=0.36.1
pkgrel=1
pkgdesc="X.Org Wacom tablet driver"
arch=(x86_64)
url="https://github.com/linuxwacom/xf86-input-wacom"
license=(GPL)
depends=(libxi libxinerama libxrandr libsystemd)
makedepends=('xorg-server-devel' 'X-ABI-XINPUT_VERSION=24.1' 'resourceproto' 'scrnsaverproto')
conflicts=('xorg-server<1.19' 'X-ABI-XINPUT_VERSION<24.1' 'X-ABI-XINPUT_VERSION>=25')
source=(https://github.com/linuxwacom/xf86-input-wacom/releases/download/$pkgname-$pkgver/$pkgname-$pkgver.tar.bz2{,.sig})
sha256sums=('3206b92a4ed1fece07438a85405c748b9ed62cf0f0c3df845a2ce695d5463e09'
            'SKIP')
validpgpkeys=('9A12ECCC5383CA2AF5B42CDCA6DC66911B2127D5') # Jason Gerecke <killertofu@gmail.com>
validpgpkeys+=('3C2C43D9447D5938EF4551EBE23B7E70B467F0BF') #  "Peter Hutterer (Who-T) <office@who-t.net>"

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
