# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: M Rawash <mrawash@gmail.com>

pkgname=xf86-input-wacom
pkgver=0.25.0
pkgrel=2
pkgdesc="X.Org Wacom tablet driver"
arch=(i686 x86_64)
url="http://linuxwacom.sourceforge.net/"
license=(GPL)
backup=('etc/X11/xorg.conf.d/50-wacom.conf')
depends=(libxi libxinerama libxrandr systemd)
makedepends=('xorg-server-devel' 'X-ABI-XINPUT_VERSION=21' 'resourceproto' 'scrnsaverproto')
conflicts=('xorg-server<1.16' 'X-ABI-XINPUT_VERSION<21' 'X-ABI-XINPUT_VERSION>=22')
source=(http://downloads.sourceforge.net/project/linuxwacom/$pkgname/$pkgname-$pkgver.tar.bz2)
md5sums=('2cf57400fbd9e35eb16b50ad9fe32de1')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --with-xorg-conf-dir=/etc/X11/xorg.conf.d
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
