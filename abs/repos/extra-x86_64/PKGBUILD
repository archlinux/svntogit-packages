# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>

pkgname=abs
pkgver=2.4.4
pkgrel=2
pkgdesc="Utilities to download and work with the Arch Build System (ABS)"
arch=('i686' 'x86_64')
url="http://projects.archlinux.org/abs.git/"
license=('GPL')
depends=('bash' 'rsync')
backup=(etc/abs.conf)
source=(https://sources.archlinux.org/other/abs/${pkgname}-${pkgver}.tar.gz{,.sig})
md5sums=('7258c739a93e02a4f31f114e9fc3b0ba'
         'a628f7d5b1cd1e838366b88709690c24')

build() {
  cd "$pkgname-$pkgver"

  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="${pkgdir}" install

  # Add readme file, and make base /var/abs path
  install -dm755 "${pkgdir}"/var/abs/local/
  install -Dm644 README "${pkgdir}"/var/abs/README

  # make adjustments to abs.conf
  if [[ $CARCH = "i686" ]]; then
    sed -i -e 's| multilib||' -e 's| !multilib-testing||' "${pkgdir}"/etc/abs.conf
  fi
  if [[ $CARCH = "x86_64" ]]; then
    sed -i '/ARCH=/s|i686|x86_64|' "${pkgdir}"/etc/abs.conf
  fi
}
