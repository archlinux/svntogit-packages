# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: OSAMC <https://github.com/osam-cologne/archlinux-proaudio>
# Contributor: Florian HÃ¼lsmann <fh@cbix.de>

pkgname=liblc3
pkgver=1.0.1
pkgrel=1
pkgdesc="Low Complexity Communication Codec library and tools"
url="https://github.com/google/liblc3"
arch=(x86_64)
license=(Apache)
depends=(glibc)
makedepends=(meson git)
provides=(liblc3.so)
_commit=cf1676d4ffde82ecc7f7e3c90c3295f7c906d39b  # tags/v1.0.1
source=("git+$url#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd liblc3
  git describe --tags | sed 's/^v//;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd liblc3
  git cherry-pick -n 214c343165006b664402c1667f4936e640c4376f  # add soversion
  git cherry-pick -n 5c2f8916e71490dddbef0e43793cc09206da0ef1  # fix build
}

build() {
  arch-meson liblc3 build -D tools=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -Dt "$pkgdir/usr/share/doc/$pkgname" -m644 liblc3/README.md
}

# vim:set sw=2 sts=-1 et:
