# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=slang
pkgver=2.3.3
pkgrel=1
pkgdesc="S-Lang is a powerful interpreted language"
arch=('x86_64')
url="https://www.jedsoft.org/slang/"
license=('GPL')
depends=('pcre')
backup=('etc/slsh.rc')
options=('!makeflags')
source=(https://www.jedsoft.org/releases/slang/${pkgname}-${pkgver}.tar.bz2{,.asc})
sha1sums=('38d9b8e80e4336f76dff3e1d85761d2d0f713595'
          'SKIP')
validpgpkeys=('64083373E9E1DE997EBBE7784B82D0B82930237D')  # John E. Davis

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

check() {
  cd ${pkgname}-${pkgver}

  [[ $CARCH = "i686" ]] || make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install-all
}
