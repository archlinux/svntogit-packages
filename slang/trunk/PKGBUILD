# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=slang
pkgver=2.2.4
pkgrel=3
pkgdesc="S-Lang is a powerful interpreted language"
arch=('i686' 'x86_64')
url="http://www.jedsoft.org/slang/"
license=('GPL')
depends=('pcre' 'zlib')
backup=('etc/slsh.rc')
options=('!makeflags')
source=(ftp://ftp.fu-berlin.de/pub/unix/misc/slang/v${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2{,.asc})
md5sums=('7fcfd447e378f07dd0c0bae671fe6487'
         'SKIP')
validpgpkeys=('AE962A02D29BFE4A4BB2805FDE401E0D5873000A')  # John E. Davis

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install-all
}
