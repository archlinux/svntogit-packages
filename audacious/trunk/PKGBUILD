# $Id$
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=audacious
pkgver=2.4.3
pkgrel=1
pkgdesc='Lightweight, GTK2-based advanced audio player focused on audio quality'
arch=('i686' 'x86_64')
url='http://audacious-media-player.org/'
license=('GPL3')
depends=('gtk2' 'libmcs' 'dbus-glib' 'libguess' 'audacious-plugins' 'desktop-file-utils' 'hicolor-icon-theme')
optdepends=('unzip: zipped skins support')
provides=('audacious-player')
replaces=('audacious-player')
install=audacious.install
source=("http://distfiles.atheme.org/$pkgname-$pkgver.tgz")
sha1sums=('81fd26c9488c543beb4ba2900a667ed386e63ef3')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  [[ $CARCH == x86_64 ]] && extraopts='' || extraopts='--disable-sse2'
  ./configure --prefix=/usr $extraopts
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
