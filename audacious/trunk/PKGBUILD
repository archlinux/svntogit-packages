# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=audacious
pkgver=3.6
_pkgver=${pkgver}-gtk3
pkgrel=1
pkgdesc='Lightweight, advanced audio player focused on audio quality'
url='http://audacious-media-player.org/'
license=('custom:BSD')
arch=('i686' 'x86_64')
depends=('gtk3' 'glib2' 'libguess' 'libsm' 'audacious-plugins'
         'gnome-icon-theme' 'hicolor-icon-theme' 'desktop-file-utils')
optdepends=('unzip: zipped skins support')
makedepends=('python2') # for gdbus-codegen
source=("http://distfiles.audacious-media-player.org/${pkgname}-${_pkgver}.tar.bz2")
sha1sums=('75b98f3e09c9a890e6ccb4c737ea864828f2b624')

provides=('audacious-player')
replaces=('audacious-player')
install=install

build() {
	cd "${srcdir}/${pkgname}-${_pkgver}"
	./configure --prefix=/usr --with-buildstamp='Arch Linux'
	make
}

package() {
	cd "${srcdir}/${pkgname}-${_pkgver}"
	make DESTDIR="${pkgdir}" install
	install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
