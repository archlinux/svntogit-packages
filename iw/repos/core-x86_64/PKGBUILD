# $Id$
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>

pkgname=iw
pkgver=4.1
pkgrel=1
pkgdesc="nl80211 based CLI configuration utility for wireless devices"
arch=("i686" "x86_64")
url="http://wireless.kernel.org/en/users/Documentation/iw"
license=("GPL")
depends=("libnl")
makedepends=("linux-api-headers")
source=(https://www.kernel.org/pub/software/network/$pkgname/$pkgname-$pkgver.tar.{xz,sign})
sha256sums=('5164aaacd8e82501d84a2187af194e8285a5a5b76e200447eb58d4dd78d1a34b'
            'SKIP')
validpgpkeys=('C0EBC440F6DA091C884D8532E0F373F37BF9099A') # Johannes Berg

build() {
  cd "$srcdir"/$pkgname-$pkgver
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  make DESTDIR="$pkgdir" SBINDIR="/usr/bin" install
}
