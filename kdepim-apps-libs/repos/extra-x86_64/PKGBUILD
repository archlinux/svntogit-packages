# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kdepim-apps-libs
pkgver=18.12.3
pkgrel=1
pkgdesc="KDE PIM mail related libraries"
arch=(x86_64)
url="https://kontact.kde.org"
license=(LGPL)
depends=(libkleo pimcommon grantleetheme)
makedepends=(extra-cmake-modules boost)
conflicts=('libkdepim<16')
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('40a6fb24fc262f5340fda4aed453c5d515976aea745765e83cf8053b44d60164'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
