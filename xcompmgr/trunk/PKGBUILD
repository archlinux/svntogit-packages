# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Leslie P. Polzer <leslie.polzer@gmx.net>
#
pkgname=xcompmgr
pkgver=1.1.9
pkgrel=1
pkgdesc="Composite Window-effects manager for X.org"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libxcomposite' 'libxdamage' 'libxrender' 'libxext')
source=(https://xorg.freedesktop.org/releases/individual/app/${pkgname}-${pkgver}.tar.xz{,.sig})
sha512sums=('699117492af7fd84b36d317dcbf422d5ec86a7f21e29370caa737cd940d8ff866a5c8704ba2e7bd17a64e085d77fd23b628263d619813dead85cd669ff5b860e'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # Alan Coopersmith <alan.coopersmith@oracle.com>

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
