# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Leslie P. Polzer <leslie.polzer@gmx.net>

pkgname=xcompmgr
pkgver=1.1.8
pkgrel=1
pkgdesc="Composite Window-effects manager for X.org"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libxcomposite' 'libxdamage' 'libxrender' 'libxext')
source=(https://xorg.freedesktop.org/releases/individual/app/${pkgname}-${pkgver}.tar.bz2{,.sig})
sha512sums=('ccf29426ffb0797a53653d1ce152de5eee0cec284811fed4b8e634ea0d948180531e4619d96f47e4f9594432c57322624fe0fdaee2c9200efac427fc0904953d'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # Alan Coopersmith <alan.coopersmith@oracle.com>

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
