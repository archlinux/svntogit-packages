# $Id$
# Maintainer: Alexander Baldeck <alexander@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
pkgname=xorg-fonts-100dpi
pkgver=1.0.1
pkgrel=3
pkgdesc="X.org 100dpi fonts"
arch=(any)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('xorg-fonts-encodings' 'xorg-fonts-alias' 'xorg-font-utils' 'fontconfig')
groups=('xorg')
install=xfonts.install
source=(${url}/releases/individual/font/font-adobe-100dpi-1.0.1.tar.bz2
        ${url}/releases/individual/font/font-adobe-utopia-100dpi-1.0.2.tar.bz2
        ${url}/releases/individual/font/font-bh-100dpi-1.0.1.tar.bz2
        ${url}/releases/individual/font/font-bh-lucidatypewriter-100dpi-1.0.1.tar.bz2
        ${url}/releases/individual/font/font-bitstream-100dpi-1.0.1.tar.bz2)
md5sums=('c754c3f4a5e08442bf6972b9466625ed'
         '1c3a2c26bd3f6e406fbadc7380efa369'
         '8af580b87e17ddacdf0ce3d775248387'
         'a2b3951dbc6ddb2e4c7e09519dd13333'
         '3b8748f8029c53595e4a4a1b23fa790a')

build() {
  cd "${srcdir}"
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"

  for dir in font-*-100dpi*; do
    if [ -d "${dir}" ]; then
      pushd ${dir}
      ./configure --prefix=/usr \
          --with-fontdir=/usr/share/fonts/100dpi
      make || return 1
      make DESTDIR="${pkgdir}" install || return 1
  
      install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.${dir%-100dpi-*}" || return 1
      popd
    fi
  done
  rm -f "${pkgdir}"/usr/share/fonts/100dpi/fonts.*
}
