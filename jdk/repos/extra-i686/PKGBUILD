# $Id$
# Maintainer: Jason Chu <jason@archlinux.org>

pkgname=jdk
pkgver=6u6
pkgrel=1
pkgdesc="Sun's Java Development Kit"
arch=(i686 x86_64)
  [ "${CARCH}" = "i686" ]   && ARCH=i586
  [ "${CARCH}" = "x86_64" ] && ARCH=amd64
url="http://java.sun.com"
depends=('glibc' 'jre')
install="jdk.install"
source=(http://download.java.net/dlj/binaries/jdk-${pkgver}-dlj-linux-${ARCH}.bin
        https://jdk-distros.dev.java.net/source/browse/*checkout*/jdk-distros/trunk/utils/construct.sh
	jdk.profile)
md5sums=('0efe7120ffb9a3379bc66d993056d80d'
         '94065b612df0046d9ae758943f9f6a75'
         '7b042ff5baaf44d23a9686a043540a2b')
replaces=('j2sdk')
conflicts=('java-environment' 'j2sdk')
provides=('java-environment' 'j2sdk')
license=('custom')

build() {
  cd ${startdir}/src

  mkdir unbundle-jdk
  cd unbundle-jdk

  sh ../jdk-${pkgver}-dlj-linux-${ARCH}.bin --accept-license

  cd ..

  sh construct.sh unbundle-jdk linux-jdk linux-jre

  rm -rf linux-jdk/jre

  mkdir -p ${startdir}/pkg/opt
  mv linux-jdk ${startdir}/pkg/opt/java

  install -D -m755 ${startdir}/src/${pkgname}.profile ${startdir}/pkg/etc/profile.d/${pkgname}.sh

  mkdir -p $startdir/pkg/usr/share/licenses/jdk
  cp $startdir/pkg/opt/java/COPYRIGHT $startdir/pkg/usr/share/licenses/jdk
  cp $startdir/pkg/opt/java/LICENSE $startdir/pkg/usr/share/licenses/jdk
  cp $startdir/pkg/opt/java/THIRDPARTYLICENSEREADME.txt $startdir/pkg/usr/share/licenses/jdk
}
