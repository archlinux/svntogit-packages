# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Tobias Kieslich <tobias (at) archlinux.org>

pkgname=orage
pkgver=4.8.4
pkgrel=2
pkgdesc="A simple calendar application with reminders for Xfce"
arch=('i686' 'x86_64')
license=('GPL2')
url="http://www.xfce.org/projects/orage/"
groups=('xfce4-goodies')
depends=('xfce4-panel' 'libical' 'popt' 'libnotify>=0.7.1' 'hicolor-icon-theme' 'desktop-file-utils')
makedepends=('intltool' 'xfce4-dev-tools' 'popt')
replaces=('xfcalendar')
install=${pkgname}.install
source=(http://archive.xfce.org/src/apps/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2)
sha1sums=('8b9facb0393385e4d639dd6a886c216729672958')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--libexecdir=/usr/lib \
	--localstatedir=/var \
	--enable-libical \
	--disable-static \
	--disable-debug 
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
