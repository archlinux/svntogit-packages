# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Tobias Kieslich <tobias (at) archlinux.org>

pkgname=orage
pkgver=4.10.0
pkgrel=1
pkgdesc="A simple calendar application with reminders for Xfce"
arch=('i686' 'x86_64')
license=('GPL2')
url="http://www.xfce.org/projects/"
groups=('xfce4-goodies')
depends=('xfce4-panel' 'libical' 'popt' 'libnotify')
makedepends=('intltool')
conflicts=('xfcalendar')
replaces=('xfcalendar')
install=$pkgname.install
source=(http://archive.xfce.org/src/apps/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=('6cb854f3437e31bab23abfce97bd10afa1636a9a1480d80e14e26c85d06be7c0')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --enable-libical \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
