# $Id: PKGBUILD 136836 2011-09-02 07:59:01Z andyrtr $
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgbase=icedtea-web-java7
pkgname=('icedtea-web-java7' 'icedtea-web-java7-doc')
pkgver=1.2.1
pkgrel=2
arch=('i686' 'x86_64')
url="http://icedtea.classpath.org/wiki/IcedTea-Web"
license=('GPL2')
makedepends=('jdk7-openjdk' 'zip' 'gtk2' 'libxtst' 'npapi-sdk' 'rhino' 'junit')
source=(http://icedtea.classpath.org/download/source/${pkgbase/-java7/}-$pkgver.tar.gz
        fix_firefox_crash.diff)
sha256sums=('134efcd429086a643ba03ec6e4da991527c3e5dfcd6ed6680a83824ad3f0cfd6'
            '9391182fe8631c9eaea93e0e5a1daf0e42658963596fe4fcbe1ba1afaf775af3')

_javaver=7
_jvmdir=/usr/lib/jvm/java-${_javaver}-openjdk

build() {
  cd "${srcdir}"/icedtea-web*

  # fix firefox crash - https://bugs.archlinux.org/task/30969
  patch -RNp1 -i ${srcdir}/fix_firefox_crash.diff

  . /etc/profile.d/jre.sh
  . /etc/profile.d/jdk.sh

  ./configure --prefix=${_jvmdir} \
      --datarootdir=/usr/share \
      --with-jdk-home=${_jvmdir}
  make
}

check() {
  cd "${srcdir}"/icedtea-web*
  # as more tests have been added some are expectged to fail
  # see http://mail.openjdk.java.net/pipermail/distro-pkg-dev/2012-March/017566.html
  make -k check || /bin/true
}


package_icedtea-web-java7() {

  pkgdesc="provides a Free Software web browser plugin running applets written in the Java programming language and an implementation of Java Web Start, originally based on the NetX project"
  depends=('jre7-openjdk' 'gtk2' 'desktop-file-utils')
  install=${pkgname}.install

  cd "${srcdir}"/icedtea-web* #${pkgname}-${_date}"
  # possible make target (see bottom of Makefile.am: install-exec-local install-data-local
  make DESTDIR="${pkgdir}" install-exec-local install-data-local

  # Install desktop files.
  install -m755 -d ${pkgdir}/usr/share/{applications,pixmaps}
  install -m644 javaws.png ${pkgdir}/usr/share/pixmaps
  install -m644 {javaws,itweb-settings}.desktop ${pkgdir}/usr/share/applications
  # remove splitted doc files
  rm -rf ${pkgdir}/usr/share/doc

  # link binaries into /usr/bin + jre/bin
  install -m755 -d ${pkgdir}/usr/bin
  install -m755 -d ${pkgdir}/${_jvmdir}/jre/bin
  pushd ${pkgdir}/${_jvmdir}/bin
  for file in *; do
    ln -sf ${_jvmdir}/bin/${file} ${pkgdir}/usr/bin
    ln -sf ${_jvmdir}/bin/${file} ${pkgdir}/${_jvmdir}/jre/bin
  done
  popd

  # link the mozilla-plugin - test it here http://www.java.com/en/download/help/testvm.xml
  install -m755 -d ${pkgdir}/usr/lib/mozilla/plugins/
  ln -sf ${_jvmdir}/lib/IcedTeaPlugin.so ${pkgdir}/usr/lib/mozilla/plugins/
}

package_icedtea-web-java7-doc() {

  pkgdesc="icedtea-web browser plugin + Java WebStart - documentation files"

  cd "${srcdir}"/icedtea-web*
  make DESTDIR="${pkgdir}" install-data-local
  # remove javaws about and man page
  rm -rf ${pkgdir}/usr/lib
  rm -rf ${pkgdir}/usr/share/man
  rm -rf ${pkgdir}/usr/share/icedtea-web # conflicting and unneeded file it seems
}
