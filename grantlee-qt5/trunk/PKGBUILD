# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=grantlee-qt5
pkgver=5.0.0
pkgrel=3
pkgdesc="A string template engine based on the Django template system and written in Qt5"
arch=('i686' 'x86_64')
url="https://www.gitorious.org/grantlee/pages/Home"
license=('LGPL3')
depends=('qt5-script')
makedepends=('cmake' 'doxygen')
source=("http://downloads.grantlee.org/grantlee-${pkgver}.tar.gz")
md5sums=('feb7f82d08e046fe078ea475dee710f6')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../grantlee-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTS=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
