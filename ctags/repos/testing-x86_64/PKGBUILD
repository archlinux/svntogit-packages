# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>

pkgname=ctags
_commit=45968eff8429f8f4942be5701f6c3b794afaf5f3  # master
pkgver=r20181015+g45968eff
pkgrel=1
epoch=1
pkgdesc='Generates an index file of language objects found in source files'
url='https://ctags.io/'
arch=('x86_64')
license=('GPL')
depends=('libxml2' 'jansson' 'libyaml' 'libseccomp')
makedepends=('git' 'python-docutils')
provides=('universal-ctags')
source=("git+https://github.com/universal-ctags/ctags#commit=$_commit")
sha512sums=('SKIP')

pkgver() {
  git -C ctags show --pretty='r%cd+g%h' --date='format:%Y%m%d' --no-patch
}

prepare() {
  cd ctags
  ./autogen.sh
}

build() {
  cd ctags
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib
  make
}

check() {
  make -C ctags check
}

package() {
  make -C ctags DESTDIR="${pkgdir}" install
}

# vim:set sw=2 et:
