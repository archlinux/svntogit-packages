# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kontactinterface
pkgver=18.11.90
pkgrel=1
pkgdesc="Kontact Plugin Interface Library"
arch=(x86_64)
url="https://kontact.kde.org"
license=(LGPL)
depends=(kparts)
makedepends=(extra-cmake-modules)
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('106c11a70c214dcc863e7ca9e2c59a3efb98e5ea162fbedea1cdc9cd3bc74315'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
