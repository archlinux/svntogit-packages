# $Id$
# Maintainer: Ray Rashif <schivmeister@gmail.com>
# Contributor: Leslie P. Polzer <polzer@stardawn.org>

pkgname=slv2
pkgver=0.6.6
pkgrel=2
pkgdesc="Library for LV2 hosts"
arch=('i686' 'x86_64')
url="http://drobilla.net/software/slv2"
license=('GPL')
depends=('redland' 'lv2core' 'jack')
makedepends=('python2')
install=$pkgname.install
changelog=$pkgname.changelog
source=(http://download.drobilla.net/$pkgname-$pkgver.tar.bz2)
md5sums=('b8e8e0e82dd04324fcd533e8acd1ce85')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # fix for python 2.7
  sed -i 's:^#!.*bin/env python:#!/usr/bin/env python2:' waf

  ./waf configure --prefix=/usr
  ./waf build $MAKEFLAGS
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  ./waf install --destdir="$pkgdir/"
}

# vim:set ts=2 sw=2 et:
