# $Id$
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: Leslie P. Polzer <polzer@stardawn.org>

pkgname=slv2
pkgver=0.6.6
pkgrel=4
pkgdesc="Library for LV2 hosts"
arch=('i686' 'x86_64')
url="http://drobilla.net/software/slv2"
license=('GPL')
depends=('redland>=1.0.12' 'lv2core' 'jack')
makedepends=('python2')
install=$pkgname.install
changelog=$pkgname.changelog
source=(http://download.drobilla.net/$pkgname-$pkgver.tar.bz2)
md5sums=('b8e8e0e82dd04324fcd533e8acd1ce85')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  python2 waf configure --prefix=/usr
  python2 waf build $MAKEFLAGS
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  python2 waf install --destdir="$pkgdir/"
}

# vim:set ts=2 sw=2 et:
