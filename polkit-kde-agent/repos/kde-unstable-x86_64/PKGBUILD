# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=polkit-kde-agent
pkgver=5.6.95
pkgrel=1
pkgdesc='Daemon providing a polkit authentication UI for KDE'
arch=('i686' 'x86_64')
url='https://www.kde.org/workspaces/plasmadesktop/'
license=('LGPL')
depends=('kiconthemes' 'kdbusaddons' 'kcrash')
makedepends=('extra-cmake-modules' 'kdoctools' 'python')
conflicts=('polkit-kde-frameworks')
replaces=('polkit-kde-frameworks')
source=("http://download.kde.org/unstable/plasma/$pkgver/$pkgname-1-$pkgver.tar.xz"{,.sig})
sha256sums=('12d5f0d597ba96110d3e3acf65d0bb560e2133ddd513b78991295ff60aa83b69'
            'SKIP')
validpgpkeys=('13C16D03EDE728514473AA73A506E6D4DD4D5088'  # Jonathan Riddell
              'D07BD8662C56CB291B316EB2F5675605C74E02CF') # David Edmundson

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-1-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_LIBEXECDIR=lib/polkit-kde \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
