# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Gerhard Brauer <gerbra@archlinux.de>
# Contributor: Richard Murri <admin@richardmurri.com>

pkgname=x2goclient
pkgver=4.0.3.2
pkgrel=1
pkgdesc="x2go qt4 client"
arch=('i686' 'x86_64')
license=('GPL2')
url="http://www.x2go.org"
depends=('qt4' 'libldap' 'libcups' 'nxproxy' 'libssh' 'libxpm' 'hicolor-icon-theme' 'xdg-utils')
#makedepends=('man2html')
install=x2goclient.install
source=(http://code.x2go.org/releases/source/${pkgname}/${pkgname}-${pkgver}.tar.gz{,.asc})
sha256sums=('f65a70463eae3d6d9fe6e4379f4ffacda50080a11f196d2792742f5b67fa0c6f'
          'SKIP')
validpgpkeys=('9BFBAEE86C0AA5FFBF2207829AF46B3025771B31'  # Mike Gabriel <mike.gabriel@das-netzwerkteam.de>
              '1AD23D1B8F087A35AB74BDE9F4A7678C9C6B0B2B') #  "X2go Git Administrator <git-admin@x2go.org>"

build() {
  cd ${pkgname}-${pkgver}
  sed -i "s:-o root -g root ::" Makefile
  make PREFIX=/usr
  make PREFIX=/usr build_man # target broken and making the build hang
#  make PREFIX=/usr build_pluginprovider
#  make PREFIX=/usr build_client 
#  make PREFIX=/usr build_plugin
}

package() {
  cd ${pkgname}-${pkgver}
  make PREFIX=/usr DESTDIR="$pkgdir" install
}
