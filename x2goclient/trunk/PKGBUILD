# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Gerhard Brauer <gerbra@archlinux.de>
# Contributor: Richard Murri <admin@richardmurri.com>

pkgbase=x2goclient
pkgname=('x2goclient' 'x2goplugin' 'x2goplugin-provider')
pkgver=4.0.5.0
pkgrel=2
arch=('i686' 'x86_64')
license=('GPL2')
url="http://www.x2go.org"
makedepends=('qt4' 'libldap' 'libcups' 'nxproxy' 'libssh' 'libxpm' 'hicolor-icon-theme' 'xdg-utils')
source=(http://code.x2go.org/releases/source/${pkgname}/${pkgname}-${pkgver}.tar.gz{,.asc})
sha256sums=('242e91720c1e5830f64a632c111ac3c4b6f6690d5005454c8e025b00bfbe42a1'
          'SKIP')
validpgpkeys=('9BFBAEE86C0AA5FFBF2207829AF46B3025771B31'  # Mike Gabriel <mike.gabriel@das-netzwerkteam.de>
              '1AD23D1B8F087A35AB74BDE9F4A7678C9C6B0B2B') #  "X2go Git Administrator <git-admin@x2go.org>"

prepare() {
  cd ${pkgname}-${pkgver}
  sed -i "s:-o root -g root ::" Makefile
}

build() {
  cd ${pkgname}-${pkgver}
# make -qp | awk -F':' '/^[a-zA-Z0-9][^$#\/\t=]*:([^=]|$)/ {split($1,A,/ /);for(i in A)print A[i]}' | grep ^install
# return 1

  make PREFIX=/usr
  make PREFIX=/usr build_man # target broken and making the build hang

#  make PREFIX=/usr build_pluginprovider
#  make PREFIX=/usr build_client 
#  make PREFIX=/usr build_plugin
}

package_x2goclient() {
  
  pkgdesc="a graphical client (Qt4) for the X2Go system"
  depends=('qt4' 'libldap' 'libcups' 'nxproxy' 'libssh' 'libxpm' 'hicolor-icon-theme' 'xdg-utils')
  install=x2goclient.install

  cd ${pkgbase}-${pkgver}
  make PREFIX=/usr DESTDIR="$pkgdir" install_client install_man
}

package_x2goplugin() {
  pkgdesc="provides X2Go Client as QtBrowser-based Mozilla plugin"
  depends=('qt4'  'libcups' 'nxproxy' 'libssh' 'libxpm')
  
  cd ${pkgbase}-${pkgver}
  make PREFIX=/usr DESTDIR="$pkgdir" install_plugin
}

package_x2goplugin-provider() {
  
  pkgdesc="an example configuration for providing the X2Go Plugin via an Apache webserver"
  depends=('apache')
  backup=(etc/x2go/plugin-provider/x2goplugin.html
          etc/x2go/x2goplugin-apache.conf
          etc/httpd/conf/x2goplugin-apache.conf)
  	
  cd ${pkgbase}-${pkgver}
  make PREFIX=/usr DESTDIR="$pkgdir" install_pluginprovider
  
  install -dm755 ${pkgdir}/etc/httpd/conf
  cd ${pkgdir}/etc/httpd/conf
  ln -svf /etc/x2go/x2goplugin-apache.conf ${pkgdir}/etc/httpd/conf/x2goplugin-apache.conf 
}
