# $Id$
# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: xduugu
# Contributor: Elis Hughes <elishughes@googlemail.com>

pkgname=rtmpdump
pkgver=20140918
pkgrel=2
pkgdesc='Tool to download rtmp streams'
arch=('i686' 'x86_64')
url='http://rtmpdump.mplayerhq.hu/'
license=('GPL2' 'LGPL2.1')
depends=('openssl')
makedepends=('git')
options=('!makeflags')
source=(git://git.ffmpeg.org/rtmpdump#commit=a1900c3)
md5sums=('SKIP')

build() {
  cd $pkgname
  make OPT="$CFLAGS" XLDFLAGS="$LDFLAGS"
}

package() {
  cd $pkgname
  make prefix=/usr sbindir=/usr/bin \
    mandir=/usr/share/man DESTDIR="$pkgdir" \
    install
}
