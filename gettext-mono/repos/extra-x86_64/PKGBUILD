# $Id$
# Maintainer: Daniel Isenmann <daniel@archlinux.org>

pkgname=gettext-mono
pkgver=0.18.3.1
pkgrel=1
pkgdesc="mono assemblies for GNU internationalization library"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/gettext"
license=('GPL')
depends=('mono>=3.2.3')
source=(ftp://ftp.gnu.org/pub/gnu/gettext/gettext-$pkgver.tar.gz)
md5sums=('3fc808f7d25487fc72b5759df7419e02')

build() {
  # get rid of that .wapi errors; thanks to brice
  mkdir -p $srcdir/weird
  export MONO_SHARED_DIR=$srcdir/weird
            
  cd $srcdir/gettext-$pkgver
  ./configure --prefix=/usr --enable-csharp=mono 
  make
}

package() {
  cd $srcdir/gettext-$pkgver
 # libraries are installed manually
  install -Dm644 gettext-runtime/intl-csharp/GNU.Gettext.dll \
    $pkgdir/usr/lib/GNU.Gettext.dll
  install -Dm644 gettext-tools/src/msgfmt.net.exe \
    $pkgdir/usr/lib/gettext/msgfmt.net.exe
  install -Dm644 gettext-tools/src/msgunfmt.net.exe \
    $pkgdir/usr/lib/gettext/msgunfmt.net.exe
}
# vim: ft=sh ts=2 et
