# $Id: PKGBUILD 278167 2016-10-10 12:21:17Z arojas $
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=syntax-highlighting
pkgver=5.34.0
pkgrel=1
pkgdesc='Syntax highlighting engine for structured text and code'
arch=(i686 x86_64)
url='https://community.kde.org/Frameworks'
license=(GPL2)
depends=(qt5-base)
makedepends=(extra-cmake-modules qt5-xmlpatterns qt5-tools)
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"{,.sig}
        pkgbuild-syntax-highlight.patch)
sha256sums=('884b266cfcec466d7f86053c7bcefccf27b256651ad123656939c9bfe9e2ce67'
            'SKIP'
            '0de3f92677e41f299278d3a9adaa012822576248fd1c4e86f5dd25e2790a4d81')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build

# Add PKGBUILD syntax highlighting
  cd $pkgname-$pkgver
  patch -p1 -i ../pkgbuild-syntax-highlight.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
