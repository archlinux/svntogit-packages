# $Id$
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Sebastien Piccand <sebcactus gmail com>

pkgbase=handbrake
pkgname=('handbrake' 'handbrake-cli')
pkgver=0.9.4
pkgrel=3
arch=('i686' 'x86_64')
url="http://handbrake.fr/"
license=('GPL')
depends=('bzip2' 'hal' 'libnotify' 'libwebkit')
makedepends=('intltool' 'python' 'yasm')
source=(http://saintdevelopment.com/mirror/${pkgname}/HandBrake-${pkgver}.tar.bz2
	gtk2.20_fixes.patch)
md5sums=('69f88b2e602550615da75947f914d7e6' 'e0296baaed129dceaf0d29c8284dc9fe')
sha1sums=('91329a83315b7eae0b2b3731a8bf0f2de2a6eb4d' '17b39144d9883b1f63d9b9ff2f6770b35bbdb16a')

build() {
  cd "${srcdir}/HandBrake-${pkgver}"

  #this patch is cherry picked from svn. remove it when updating to a new version
  patch -Np0 -i ${srcdir}/gtk2.20_fixes.patch

  ./configure --prefix=/usr --force
  cd build
  make
}

package_handbrake() {
  pkgdesc="Multiplatform, multithreaded DVD to MPEG-4/H264/Theora converter"
  install=${pkgname}.install

  cd "${srcdir}/HandBrake-${pkgver}/build"
  make DESTDIR="${pkgdir}" install
  rm "${pkgdir}/usr/bin/HandBrakeCLI"
}

package_handbrake-cli() {
  pkgdesc="Multiplatform, multithreaded DVD to MPEG-4/H264/Theora converter (CLI version)"
  depends=('bzip2' 'gcc-libs' 'zlib')
  conflict=('handbrake<=0.9.4-2')

  cd "${srcdir}/HandBrake-${pkgver}/build"
  install -D -m 755 HandBrakeCLI "${pkgdir}/usr/bin/HandBrakeCLI"
}
