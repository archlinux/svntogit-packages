# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kcron
pkgver=21.12.2
pkgrel=2
pkgdesc='Configure and schedule tasks'
url='https://apps.kde.org/kcron/'
arch=(x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kde-system)
depends=(kio cron)
makedepends=(extra-cmake-modules kdoctools)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        https://invent.kde.org/system/kcron/-/commit/2c04c9f6.patch
        https://invent.kde.org/system/kcron/-/commit/ef4266e3.patch)
sha256sums=('93d248a155416987b4abf226be677b9e3f858d2635bc6f9446b212be83a1af35'
            'SKIP'
            '6e4512beaa6d15c1f53ef06a2a5a0ad9fe49c1ebb263a372456a36239d9b30ce'
            'e13e5cc83727e3af1fa33ed1218b10101374fc9f3435d335d9d6271140ca9e1c')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
options=(debug)

prepare() {
  patch -d $pkgname-$pkgver -p1 < 2c04c9f6.patch # CVE-2022-24986
  patch -d $pkgname-$pkgver -p1 < ef4266e3.patch # CVE-2022-24986
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
