# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>
# Contributor: John Proctor <jproctor@prium.net>

pkgname=popt
pkgver=1.16
pkgrel=2
pkgdesc="A commandline option parser"
arch=('i686' 'x86_64') 
url="http://rpm5.org"
license=('custom')
depends=('glibc')
options=('!libtool')
source=(http://rpm5.org/files/$pkgname/$pkgname-$pkgver.tar.gz)
md5sums=('3743beefa3dd6247a73f8f7a32c14c33')

build() {
  cd $srcdir/$pkgname-$pkgver

  ./configure --prefix=/usr || return 1
  make || return 1
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install || return 1

  # move libs in /lib
  install -Dm755 ${pkgdir}/usr/lib/libpopt.so.0.0.0 \
    ${pkgdir}/lib/libpopt.so.0.0.0 || return 1
  
  rm ${pkgdir}/usr/lib/libpopt.so* || return 1
  ln -sf /lib/libpopt.so ${pkgdir}/usr/lib/libpopt.so
  ln -sf libpopt.so.0 ${pkgdir}/lib/libpopt.so
  ln -sf libpopt.so.0.0.0 ${pkgdir}/lib/libpopt.so.0

  # install license
  install -Dm644 $srcdir/$pkgname-$pkgver/COPYING \
    $pkgdir/usr/share/licenses/$pkgname/LICENSE || return 1
}
