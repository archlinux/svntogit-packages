# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgname=tunepimp
pkgver=0.5.3
pkgrel=7
pkgdesc="A development library geared towards developers who wish to create MusicBrainz enabled tagging applications (aka libtunepimp)"
url="http://www.musicbrainz.org/products/tunepimp/"
arch=('i686' 'x86_64')
license=('GPL')
options=('!libtool' '!makeflags')
makedepends=('python')
depends=('curl' 'musicbrainz' 'zlib' 'libmad' 'flac' 'libvorbis' 'readline' 'libmpcdec' 'libofa' 'taglib' 'libmp4v2')
replaces=('trm')
source=(ftp://ftp.musicbrainz.org/pub/musicbrainz/lib${pkgname}-${pkgver}.tar.gz
        tunepimp-gcc43.patch
	gcc4.4.patch
	mp4v2-1.9.patch)

 build() {
  cd ${srcdir}/lib${pkgname}-${pkgver}
  sed -i -e 's,-ltermcap,-lncurses,' configure
  sed -i -e 's:-lthr:-lpthread:g' lib/threads/posix/Makefile.in
  patch -Np0 -i ${srcdir}/tunepimp-gcc43.patch || return 1
  patch -Np1 -i ${srcdir}/gcc4.4.patch || return 1
  patch -Np1 -i ${srcdir}/mp4v2-1.9.patch || return 1
  ./configure --prefix=/usr
  make || return 1
  make DESTDIR=${pkgdir} install
  # install python bindings
  cd python
  python setup.py install --root=${pkgdir}

  # fix includes for kdemultimedia
  cd ${pkgdir}/usr/include
  ln -s tunepimp-0.5 tunepimp
}
md5sums=('09649f983acef679a548344ba7a9bb2f'
         '40f48ad658958b909909b204bf9b1367'
         'b325bf8ed98bc008611ece0c418642f8'
         '9f41d473a07e13808762545772fe3435')
