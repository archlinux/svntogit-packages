# Maintainer: AndyRTR <andyrtr@archlinux.org>

pkgname=mythes-en
pkgver=20060306
pkgrel=4
pkgdesc="English thesaurus"
arch=('any')
url="http://wiki.services.openoffice.org/wiki/Dictionaries#English_.28AU.2CCA.2CGB.2CNZ.2CUS.2CZA.29"
license=('LGPL')
optdepends=('libmythes: offers thesaurus library functions')
# Fedora uses http://www.danielnaber.de/wn2ooo/ but it doesn't look more fresh
source=("http://www.mirrorservice.org/sites/download.openoffice.org/contrib/dictionaries/thes_en_US_v2.zip")
md5sums=('ec611ad21ae4ee2b4415e27e252e4952')

package() {
  cd "$srcdir"
  install -dm755 ${pkgdir}/usr/share/mythes
  cp -p th_en_US_v2.* $pkgdir/usr/share/mythes

  pushd $pkgdir/usr/share/mythes/
  en_US_aliases="en_AG en_AU en_BS en_BW en_BZ en_CA en_DK en_GB en_GH en_IE en_IN en_JM en_NA en_NG en_NZ en_PH en_SG en_TT en_ZA en_ZW"
  for lang in $en_US_aliases; do
        ln -s th_en_US_v2.idx "th_"$lang"_v2.idx"
        ln -s th_en_US_v2.dat "th_"$lang"_v2.dat"
  done
  popd

  # the symlinks
  install -dm755 ${pkgdir}/usr/share/myspell/dicts
  pushd $pkgdir/usr/share/myspell/dicts
    for file in $pkgdir/usr/share/mythes/*; do
      ln -sv /usr/share/mythes/$(basename $file) .
    done
  popd
  
  # docs
  install -dm755 ${pkgdir}/usr/share/doc/$pkgname
  cp -p README_th_en_US_v2.txt $pkgdir/usr/share/doc/$pkgname
}
