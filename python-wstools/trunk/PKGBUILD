# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>

pkgbase=python-wstools
pkgname=python2-wstools
pkgver=0.4.3
pkgrel=1
pkgdesc="WSDL parsing services package for Web Services for Python"
arch=('any')
url="https://github.com/pycontribs/wstools"
license=('custom')
makedepends=('python2-setuptools')
depends=('python2')
source=("https://pypi.python.org/packages/source/w/wstools/wstools-${pkgver}.tar.gz")
md5sums=('82f800f83195519ab6f01304456dadd9')

prepare() {
  cd wstools-$pkgver
  sed -e 's|#! /usr/bin/env python$|#!/usr/bin/env python2|' \
      -e 's|#!/usr/bin/env python$|#!/usr/bin/env python2|' \
      -i src/wstools/{c14n.py,Namespaces.py,XMLSchema.py,Utility.py,logging.py,__init__.py,tests/*}
}

build() {
  cd wstools-${pkgver}
  python2 setup.py build
}

package_python2-wstools() {
  cd wstools-${pkgver}
  python2 setup.py install --root="${pkgdir}" --optimize=1

  install -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm644 docs/* "${pkgdir}/usr/share/licenses/${pkgname}"/
}
