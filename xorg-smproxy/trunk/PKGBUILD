# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-smproxy
pkgver=1.0.6
pkgrel=1
pkgdesc="Allows X applications that do not support X11R6 session management to participate in an X11R6 session"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libsm' 'libxt' 'libxmu')
makedepends=('xorg-util-macros')
groups=('xorg-apps' 'xorg')
source=(http://xorg.freedesktop.org/archive/individual/app/smproxy-${pkgver}.tar.bz2{,.sig})
sha256sums=('6cf19155a2752237f36dbf8bc4184465ea190d2652f887faccb4e2a6ebf77266'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # Alan Coopersmith <alan.coopersmith@oracle.com>

build() {
  cd smproxy-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd smproxy-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
