# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdebindings-python
pkgver=4.7.80
pkgrel=1
pkgdesc="KDE bindings for Python"
url="http://kde.org/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kdebindings')
depends=('kdepim-runtime' 'python2-pyqt' 'qscintilla' 'boost-libs')
makedepends=('cmake' 'automoc4' 'boost')
source=("http://download.kde.org/unstable/${pkgver}/src/pykde4-${pkgver}.tar.bz2")
sha1sums=('b67905a73566ad99b3bf1238b87f333c3ebb1fc5')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../pykde4-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DPYTHON_EXECUTABLE=/usr/bin/python2
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install

  # Use the python2 executable
  find "${pkgdir}" -name '*.py' | xargs sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|'
}
