# $Id$
# Maintainer:  Jan de Groot <jgc@archlinux.org>
# Maintainer:  Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=guile
pkgver=2.0.14
pkgrel=1
pkgdesc='Portable, embeddable Scheme implementation written in C'
url='http://www.gnu.org/software/guile/'
arch=('i686' 'x86_64')
license=('GPL')
depends=('gmp' 'libltdl' 'ncurses' 'texinfo' 'libunistring' 'gc' 'libffi')
source=(https://ftp.gnu.org/pub/gnu/$pkgname/$pkgname-$pkgver.tar.gz{,.sig})
validpgpkeys=('3CE464558A84FDC69DB40CFB090B11993D9AEBB5') # Ludovic Courtès <ludo@gnu.org>
md5sums=('333b6eec83e779935a45c818f712484e'
         'SKIP')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
    --disable-static  \
    --disable-error-on-warning
  make
}

package() {
  make -C $pkgname-$pkgver DESTDIR="$pkgdir" install
  rm "$pkgdir"/usr/lib/libguile-2.0.so.*-gdb.scm
}
