# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=guile
pkgver=3.0.8
pkgrel=1
pkgdesc='Portable, embeddable Scheme implementation written in C'
url='https://www.gnu.org/software/guile/'
arch=(x86_64)
license=(GPL)
depends=(gmp libltdl ncurses texinfo libunistring gc libffi)
source=(https://ftp.gnu.org/pub/gnu/$pkgname/$pkgname-$pkgver.tar.gz{,.sig}
        https://ftp.gnu.org/pub/gnu/$pkgname/$pkgname-2.2.7.tar.gz{,.sig})
validpgpkeys=('3CE464558A84FDC69DB40CFB090B11993D9AEBB5' # Ludovic Courtès <ludo@gnu.org>
              'FF478FB264DE32EC296725A3DDC0F5358812F8F2' # Andy Wingo
              '4FD4D288D445934E0A14F9A5A8803732E4436885') # Andy Wingo <wingo@pobox.com>"
sha256sums=('f25ae0c26e911af1b5005292d4f56621879f74d6958b30741cf67d8b6feb2016'
            'SKIP'
            '44b4c5fbbe257ccdebea18420212c9b3e90c3c86a54920d8554039fc6769a007'
            'SKIP')

build() {
  cd $pkgname-2.2.7
  ./configure --prefix=/usr \
    --disable-static  \
    --disable-error-on-warning
  make

  cd ../$pkgname-$pkgver
  ./configure --prefix=/usr \
    --disable-static  \
    --disable-error-on-warning
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  make -C $pkgname-2.2.7 DESTDIR="$pkgdir" install
  rm "$pkgdir"/usr/lib/libguile-2.?.so.*-gdb.scm
  rm "$pkgdir"/usr/lib/pkgconfig/guile-2.2.pc
  rm "$pkgdir"/usr/share/aclocal/guile.m4
  rm -r "$pkgdir"/usr/{bin,include,share/{info,man}}

  make -C $pkgname-$pkgver DESTDIR="$pkgdir" install
  rm "$pkgdir"/usr/lib/libguile-3.?.so.*-gdb.scm
}
