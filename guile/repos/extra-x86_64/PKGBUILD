# $Id$
# Maintainer:  Jan de Groot <jgc@archlinux.org>
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=guile
pkgver=2.0.11
pkgrel=3
pkgdesc='Portable, embeddable Scheme implementation written in C'
url='http://www.gnu.org/software/guile/'
arch=('i686' 'x86_64')
license=('GPL')
depends=('gmp' 'libltdl' 'ncurses' 'texinfo' 'libunistring' 'gc' 'libffi')
install=guile.install
source=(ftp://ftp.gnu.org/pub/gnu/$pkgname/$pkgname-$pkgver.tar.gz)
md5sums=('e532c68c6f17822561e3001136635ddd')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
    --disable-static  \
    --disable-error-on-warning
  make
}

package() {
  make -C $pkgname-$pkgver DESTDIR="$pkgdir" install
  rm "$pkgdir"/usr/lib/libguile-2.0.so.22.7.2-gdb.scm
}
