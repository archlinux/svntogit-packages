# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=spectacle
pkgver=15.12.0
pkgrel=1
pkgdesc="KDE screenshot capture utility"
arch=(i686 x86_64)
url='https://projects.kde.org/spectacle'
license=(GPL)
depends=(kio xcb-util-cursor libkscreen libkipi hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools)
conflicts=(kdegraphics-ksnapshot kscreengenie)
replaces=(kdegraphics-ksnapshot kscreengenie)
groups=(kde-applications kdegraphics)
source=("http://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
install=$pkgname.install
md5sums=('bedebdd924cf5a650c87b1306d4e6a94')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
   -DCMAKE_INSTALL_PREFIX=/usr \
   -DCMAKE_BUILD_TYPE=Release \
   -DCMAKE_INSTALL_LIBDIR=lib \
   -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}

