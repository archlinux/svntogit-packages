# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=spectacle
pkgver=17.04.1
pkgrel=1
pkgdesc="KDE screenshot capture utility"
arch=(i686 x86_64)
url='https://www.kde.org/applications/graphics/'
license=(GPL)
depends=(xcb-util-cursor libkipi purpose)
makedepends=(extra-cmake-modules kdoctools python)
optdepends=('kipi-plugins: export to various online services')
conflicts=(kdegraphics-ksnapshot kscreengenie)
replaces=(kdegraphics-ksnapshot kscreengenie)
groups=(kde-applications kdegraphics)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('5cea11dcb8c6b17e23944801b81634cd838627cb36a0e11442b75eda8761ec4b'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
   -DCMAKE_INSTALL_PREFIX=/usr \
   -DCMAKE_BUILD_TYPE=Release \
   -DKDE_INSTALL_LIBDIR=lib \
   -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}

