# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Yamashita Ren <lemaitre.lotus@gmail.com>

pkgname=dcadec
pkgver=0.1.0
pkgrel=2
pkgdesc='DTS Coherent Acoustics decoder with support for HD extensions'
arch=('i686' 'x86_64')
url='https://github.com/foo86/dcadec.git'
license=('LGPL2.1')
depends=('glibc')
provides=('libdcadec.so')
install='dcadec.install'
source=("dcadec-${pkgver}.tar.gz::https://github.com/foo86/dcadec/archive/v${pkgver}.tar.gz")
sha256sums=('52a68a63f79ae50e5e5fd9c631a715e9feeed5d2b4a8442f0b9208692012a43f')

build() {
  cd dcadec-${pkgver}

  export CONFIG_SHARED='TRUE'

  make
}

package() {
  cd dcadec-${pkgver}

  export CONFIG_SHARED='TRUE'
  export PREFIX='/usr'

  make DESTDIR="${pkgdir}" install
  mv "${pkgdir}"/usr/bin/dcadec{,-hd}
}

# vim: ts=2 sw=2 et:
