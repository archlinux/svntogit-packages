# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kmines
pkgver=15.12.0
pkgrel=1
pkgdesc="The classic Minesweeper game"
url="http://kde.org/applications/games/kmines/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegames')
depends=('libkdegames' 'knotifyconfig' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'python' 'kdoctools')
install=${pkgname}.install
conflicts=('kdegames-kmines')
replaces=('kdegames-kmines')
source=("http://download.kde.org/stable/applications/${pkgver}/src/kmines-${pkgver}.tar.xz")
sha1sums=('d1c9d86b37bd3f732f202670cd36c4140036b010')

build() {
  mkdir -p build
  cd build
  cmake ../kmines-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
