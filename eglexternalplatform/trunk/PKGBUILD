# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=eglexternalplatform
pkgver=1.0+3+g7c8f8e2
pkgrel=1
pkgdesc="EGL External Platform interface"
url=https://github.com/NVIDIA/eglexternalplatform
arch=(any)
license=(MIT)
depends=(mesa)
makedepends=(git)
_commit=7c8f8e2218e46b1a4aa9538520919747f1184d86  # master
source=("git+$url#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  git tag -f 1.0 53bf47ccd349b7525248687ba0058860168c8580  # Fixup missing tag
}

package() {
  cd $pkgname
  install -Dt "$pkgdir/usr/include/EGL" -m644 interface/*
  install -Dt "$pkgdir/usr/share/pkgconfig" -m644 *.pc
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 COPYING
}

# vim:set sw=2 et:
