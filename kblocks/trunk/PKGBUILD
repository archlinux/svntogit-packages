# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kblocks
pkgver=15.04.2
pkgrel=1
pkgdesc="The classic falling blocks game"
url="http://kde.org/applications/games/kblocks/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegames')
depends=('libkdegames' 'knotifyconfig')
makedepends=('extra-cmake-modules' 'python' 'kdoctools')
install=${pkgname}.install
conflicts=('kdegames-kblocks')
replaces=('kdegames-kblocks')
source=("http://download.kde.org/stable/applications/${pkgver}/src/kblocks-${pkgver}.tar.xz")
sha1sums=('add86253dcfa66f9f3a8ef40c31ebcf4e9419dd5')

build() {
  mkdir build
  cd build
  cmake ../kblocks-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
