# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Silvio Knizek (killermoehre)

pkgname=gnome-disk-utility
pkgver=42.0
pkgrel=1
pkgdesc="Disk Management Utility for GNOME"
url="https://gitlab.gnome.org/GNOME/gnome-disk-utility"
arch=(x86_64)
license=(GPL)
groups=(gnome)
depends=(udisks2 gtk3 libsecret libpwquality libcanberra libdvdread libnotify
         parted systemd libhandy)
makedepends=(git meson docbook-xsl)
options=(debug)
_commit=5f6488fc9660de73048d247da4fd0eecdd0a21a6  # tags/42.0^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-disk-utility.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
