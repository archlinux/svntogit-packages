# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
# Contributor: William DÃ­az <wdiaz@archlinux.us>

pkgname=farsight2
pkgver=0.0.29
pkgrel=1
pkgdesc="Audio/Video conference software for Instant Messengers"
arch=('i686' 'x86_64')
url="http://farsight.freedesktop.org"
license=('LGPL')
depends=('libnice' 'gstreamer0.10-base-plugins' 'gstreamer0.10-good' 'gstreamer0.10-bad' 'gstreamer0.10-python' 'pygobject')
optdepends=('gstreamer0.10-ugly-plugins: h264 codec')
options=('!libtool' '!emptydirs')
source=(http://farsight.freedesktop.org/releases/${pkgname}/${pkgname}-${pkgver}.tar.gz)
md5sums=('276076f65b70236d981c288413572168')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
      --with-package-name='Arch Linux farsight2 package' \
      --with-package-origin='http://archlinux.org' \
      --disable-static \
      --disable-valgrind \
      --disable-debug
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR=${pkgdir} install
}
