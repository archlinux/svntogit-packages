# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegraphics-ksnapshot
pkgver=15.08.1
pkgrel=1
pkgdesc="Screen Capture Program"
url="http://kde.org/applications/graphics/ksnapshot/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegraphics')
depends=('kdebase-runtime' 'libkipi')
makedepends=('cmake' 'automoc4')
optdepends=('kipi-plugins')
install=${pkgname}.install
source=("http://download.kde.org/stable/applications/${pkgver}/src/ksnapshot-${pkgver}.tar.xz")
sha1sums=('2738710cfaa4492800c647a153988a1359131cb2')

build() {
  cd "${srcdir}"
  mkdir -p build
  cd build
  cmake ../ksnapshot-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
