# $Id$
# Maintainer: kevin <kevin@archlinux.org>
# Contributor: Wael Nasreddine <gandalf@siemens-mobiles.org>
# Contributor: Francois Charette <francois.archlinux.org> 

pkgname=perl-term-readkey
_realname=TermReadKey
pkgver=2.30.01
pkgrel=3
pkgdesc="Provides simple control over terminal driver modes"
arch=('i686' 'x86_64')
license=('custom')
depends=('perl>=5.10.0' 'glibc')
url="http://search.cpan.org/~stsi/${_realname}/"
source=(http://search.cpan.org/CPAN/authors/id/S/ST/STSI/${_realname}-$pkgver.tar.gz)
options=('!emptydirs')
md5sums=('6c099eddb76ec9b92179f1ed929be71a')

build()
{
  cd ${srcdir}/TermReadKey-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
  make test
}

package() {
  cd ${srcdir}/TermReadKey-$pkgver
  make DESTDIR=${pkgdir} install
  install -d ${pkgdir}/usr/share/licenses/$pkgname/
  head -7 README > ${pkgdir}/usr/share/licenses/$pkgname/LICENSE

  # remove perllocal.pod and .packlist
  find ${pkgdir} -name perllocal.pod -delete
  find ${pkgdir} -name .packlist -delete
}
