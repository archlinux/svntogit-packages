# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: kevin <kevin@archlinux.org>
# Contributor: Wael Nasreddine <gandalf@siemens-mobiles.org>
# Contributor: Francois Charette <francois.archlinux.org>

pkgname=perl-term-readkey
_realname=TermReadKey
pkgver=2.30.02
pkgrel=4
pkgdesc="Provides simple control over terminal driver modes"
arch=('i686' 'x86_64')
license=('custom')
depends=('perl' 'glibc')
url="http://search.cpan.org/~stsi/${_realname}/"
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/S/ST/STSI/${_realname}-$pkgver.tar.gz)
md5sums=('69b92c8ddca8691c83823c683cd9838a')

build() {
  cd "${srcdir}/TermReadKey-$pkgver"
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "${srcdir}/TermReadKey-$pkgver"
  make test
}

package() {
  cd "${srcdir}/TermReadKey-$pkgver"
  make DESTDIR="${pkgdir}" install
  chmod 755 "${pkgdir}/usr/lib/perl5/vendor_perl/auto/Term/ReadKey/ReadKey.so"
  install -d "${pkgdir}/usr/share/licenses/$pkgname/"
  head -7 README > "${pkgdir}/usr/share/licenses/$pkgname/LICENSE"
}
