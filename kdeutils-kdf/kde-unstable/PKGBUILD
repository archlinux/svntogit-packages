# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeutils-kdf
pkgver=15.11.80
pkgrel=1
url="http://kde.org/applications/system/kdiskfree/"
arch=('i686' 'x86_64')
pkgdesc='View Disk Usage'
license=('GPL' 'LGPL' 'FDL')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
groups=('kde-applications' 'kdeutils')
install=${pkgname}.install
source=("http://download.kde.org/unstable/applications/${pkgver}/src/kdf-${pkgver}.tar.xz")
sha1sums=('be3b0b1bae03716e3c9f604f8b2bee5350be3c8b')

build() {
  cd "${srcdir}"
  mkdir -p build
  cd build
  cmake ../kdf-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $srcdir/build
  make DESTDIR=$pkgdir install
}
