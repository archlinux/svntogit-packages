# $Id$
# Maintainer: Guillaume ALAUX <guillaume@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>

pkgname=eclipse-ecj
pkgver=4.6.1
pkgrel=1
_date=201609071200
pkgdesc='Eclipse java bytecode compiler'
arch=('any')
license=('EPL')
url='http://www.eclipse.org/'
depends=('java-runtime')
makedepends=('apache-ant' 'java-environment')
source=(http://download.eclipse.org/eclipse/downloads/drops4/R-${pkgver}-${_date}/ecjsrc-${pkgver}.jar
        01-ecj-include-props.patch
        02-buildxml-fix-manifest.patch
        ecj)

sha256sums=('36a664f84b5d2ba2c31dc8b3cf72783a979c9cffe689fbed5c58130f2b269fbc'
            '8f6259c76dfe493549bbaec3c8a7ba29e82c70e127c918adca28737dcb570f6b'
            '5ca6bd94c2b1cb4c6f116d38c160edf1c4ca520647ac74b26486f958254767af'
            '63aff3d126243d303ddc4305cfa77827df72e87ccf85bd8a22a2f832357e396c')

build() {
  cd "${srcdir}"
  for p in 01-ecj-include-props 02-buildxml-fix-manifest; do
    patch -p0 < ${p}.patch
  done
  export LANG=en_US.UTF-8
  ant build
}

package() {
  install -Dm644 "${srcdir}/ecj.jar" "${pkgdir}/usr/share/java/eclipse-ecj-${pkgver}.jar"
  ln -s eclipse-ecj-${pkgver}.jar "${pkgdir}/usr/share/java/ecj.jar"
  ln -s eclipse-ecj-${pkgver}.jar "${pkgdir}/usr/share/java/eclipse-ecj.jar"
  install -Dm755 ecj "${pkgdir}/usr/bin/ecj"
  install -D -m 644 ecj.1 "${pkgdir}/usr/share/man/man1/ecj.1"
}
