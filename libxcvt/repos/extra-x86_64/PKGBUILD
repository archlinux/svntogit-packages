# Maintainer: AndyRTR <andyrtr@archlinux.org>

pkgname=libxcvt
pkgver=0.1.0
pkgrel=1
pkgdesc="library providing a standalone version of the X server implementation of the VESA CVT standard timing modelines generator"
arch=('x86_64')
url="https://gitlab.freedesktop.org/xorg/lib/libxcvt"
license=('custom')
depends=('glibc')
makedepends=('meson')
source=(https://xorg.freedesktop.org/releases/individual/lib/${pkgname}-${pkgver}.tar.xz{,.sig})
sha256sums=('266ded49b8aeae5b387768f5be232c6538e321737fa07fbbc9a6f6924d271a00'
            'SKIP')
validpgpkeys=('3BB639E56F861FA2E86505690FDD682D974CA72A') # "Matt Turner <mattst88@gmail.com>"

build() {
  arch-meson ${pkgname}-${pkgver} build
  meson compile -C build
}

check() {
  meson test -C build #--print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
  install -m644 -Dt "${pkgdir}/usr/share/licenses/${pkgname}" "${pkgname}-${pkgver}/COPYING"
}
