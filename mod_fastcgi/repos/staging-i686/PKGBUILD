# $Id$
# Maintainer:
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=mod_fastcgi
pkgver=2.4.7
pkgrel=2
_SNAP=SNAP-0910052141
pkgdesc="Apache module to support the FastCGI protocol"
arch=('i686' 'x86_64')
license=('custom')
url="http://www.fastcgi.com/"
depends=('apache')
# mod_fastcgi upstream development is dead. There is a third-party patch that
# adopts mod_fastcgi to apache 2.4 https://github.com/ByteInternet/libapache-mod-fastcgi
source=(http://www.fastcgi.com/dist/${pkgname}-${_SNAP}.tar.gz
        compile-against-apache24.diff)
sha256sums=('e6564ae206284806c781834e9e89aa4b0a4dd647b45c6da5cc389c65a984bdc7'
            'e405f365fac2d80c181a7ddefc9c6332cac7766cb9c67c464c272d595cde1800')

prepare() {
  cd $pkgname-$_SNAP
  patch -p1 < "$srcdir/compile-against-apache24.diff"
  cp Makefile.AP2 Makefile
}

build() {
  cd $pkgname-$_SNAP
  make top_dir=/usr/lib/httpd
}

package() {
  cd $pkgname-$_SNAP
  make top_dir=/usr/lib/httpd DESTDIR="${pkgdir}" install
  install -Dm644 docs/LICENSE.TERMS \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
