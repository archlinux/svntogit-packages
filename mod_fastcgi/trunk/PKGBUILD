# $Id$
# Maintainer:
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=mod_fastcgi
pkgver=2.4.7
pkgrel=1
_SNAP=SNAP-0910052141
pkgdesc="Apache module to support the FastCGI protocol"
arch=('i686' 'x86_64')
license=('custom')
url="http://www.fastcgi.com/"
depends=('apache')
source=(http://www.fastcgi.com/dist/${pkgname}-${_SNAP}.tar.gz)
md5sums=('c23ee51d4f03afd3487bcfeea953db5a')

prepare() {
  cd $pkgname-$_SNAP
  cp Makefile.AP2 Makefile
}

build() {
  cd $pkgname-$_SNAP
  make top_dir=/usr/lib/httpd
}

package() {
  cd $pkgname-$_SNAP
  make top_dir=/usr/lib/httpd DESTDIR="${pkgdir}" install
  install -Dm644 docs/LICENSE.TERMS \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
