# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeutils-print-manager
pkgver=14.12.2
pkgrel=1
pkgdesc="A tool for managing print jobs and printers"
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kdeutils/print-manager'
license=('GPL' 'LGPL' 'FDL')
depends=('kdebase-workspace' 'libcups')
makedepends=('cmake' 'automoc4')
optdepends=('system-config-printer: auto-detect the printer driver')
replaces=('print-manager' 'kdeutils-printer-applet' 'kdeadmin-system-config-printer-kde')
conflicts=('print-manager')
groups=('kde' 'kdeutils')
source=("http://download.kde.org/stable/applications/${pkgver}/src/print-manager-${pkgver}.tar.xz")
sha1sums=('b5f86cc52a31fb90396f6a80e9b5a168ebccf6d2')

build() {
  mkdir build
  cd build
  cmake ../print-manager-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
