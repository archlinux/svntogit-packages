# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeutils-print-manager
pkgver=4.10.90
pkgrel=1
pkgdesc="A tool for managing print jobs and printers"
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kdeutils/print-manager'
license=('GPL' 'LGPL' 'FDL')
depends=('kdebase-workspace' 'libcups')
makedepends=('cmake' 'automoc4')
optdepends=('system-config-printer: auto-detect the printer driver')
replaces=('print-manager' 'kdeutils-printer-applet' 'kdeadmin-system-config-printer-kde')
conflicts=('print-manager')
groups=('kde' 'kdeutils')
source=("http://download.kde.org/unstable/${pkgver}/src/print-manager-${pkgver}.tar.xz")
sha1sums=('178f500262184a75853cb8aa9ef86b91db033030')

build() {
  mkdir build
  cd build
  cmake ../print-manager-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
