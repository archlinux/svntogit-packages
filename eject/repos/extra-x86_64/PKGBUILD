# $Id$
# Maintainer: Tom Gundersen <teg@jlkm.no>
# Contributor: Natrio <natrio@list.ru>
# Contributor: dorphell <dorphell@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=eject
pkgver=2.1.5
pkgrel=6
pkgdesc="A program for ejecting removable media under software control. Patched for unlock tray. Fedora's patches added."
arch=('i686' 'x86_64')
url="http://eject.sourceforge.net/"
license=('GPL')
conflicts=('eject')
provides=('eject')
depends=('glibc')
source=("ftp://ftp.archlinux.org/other/eject/eject-${pkgver}.tar.bz2"
        'eject-2.1.1-verbose.patch'
        'eject-2.1.5-lock.patch'
        'eject-2.1.5-spaces.patch'
        'eject-2.1.5-umount.patch'
        'eject-timeout.patch'
        'eject-2.1.5-unlock.patch'
        'eject-2.1.5-openrw.patch')
md5sums=('5a6cab2184ae4ed21128054d931d558f'
         '4fb81806de181d5f353a459b7f122081'
         '7d18c14f7b1bb50149fd2b41215dc4f2'
         '64f5c54dac064e6dbc427025d919221b'
         '34221248a269703d3a47a8d2a6fdd2e2'
         'f6b1b702ef9174e2fd05a56ee2de850e'
         'f384f98fa689b087d99217a7403bf706'
         'e74d742540e4f399d348af495bf00e85')

build() {
  cd "${srcdir}/eject"
  patch -p1 -i "${srcdir}/eject-2.1.1-verbose.patch"
  patch -p1 -i "${srcdir}/eject-2.1.5-lock.patch"
  patch -p1 -i "${srcdir}/eject-2.1.5-spaces.patch"
  patch -p1 -i "${srcdir}/eject-2.1.5-umount.patch"
  patch -p1 -i "${srcdir}/eject-timeout.patch"
  patch -p1 -i "${srcdir}/eject-2.1.5-unlock.patch"
  patch -p1 -i "${srcdir}/eject-2.1.5-openrw.patch"
  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --enable-default-device=sr0
  make
}

package() {
  cd "${srcdir}/eject"
  make DESTDIR="${pkgdir}" install
}
