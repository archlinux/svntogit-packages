# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=xsnow
pkgver=1.42
pkgrel=6
pkgdesc="Display snow, Santa and his reindeers on the root window"
arch=('i686' 'x86_64')
url="http://dropmix.xs4all.nl/rick/Xsnow/"
license=('custom')
depends=('libxpm')
makedepends=('imake')
source=(http://dropmix.xs4all.nl/rick/Xsnow/${pkgname}-${pkgver}.tar.gz LICENSE)
sha1sums=('d63987560dac9c6341e50d270089e40d17031ce3'
          'c93e236bed35a2d5dc23202c1c615d4e146fba49')

build() {
  cd ${pkgname}-${pkgver}
  xmkmf
  make depend
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install install.man
  chmod 644 "${pkgdir}/usr/share/man/man1/xsnow.1x"
  install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
