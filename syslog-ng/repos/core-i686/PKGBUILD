# $Id$
# Maintainer: Aaron Griffin <aaron@archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=syslog-ng
pkgver=3.0.3
pkgrel=2
pkgdesc="Next-generation syslogd with advanced networking and filtering capabilities"
arch=('i686' 'x86_64')
license=('GPL2')
groups=('base')
url="http://www.balabit.com/network-security/syslog-ng/"
depends=('glib2' 'eventlog' 'tcp_wrappers' 'libcap>=2.16' 'openssl')
makedepends=('flex' 'pkgconfig') 
optdepends=('logrotate')
provides=('logger')
backup=('etc/syslog-ng.conf' 'etc/logrotate.d/syslog-ng')
source=(http://www.balabit.com/downloads/files/syslog-ng/sources/$pkgver/source/$pkgname\_$pkgver.tar.gz
        syslog-ng.conf
        syslog-ng.logrotate
        syslog-ng.rc)
install=syslog-ng.install
md5sums=('b8b3c9af28696bce48ceca0de6444426'
         'e02be6637d0c3b5484d4597ce049ae39'
         '9714c7479119922b55a7992677ebecb5'
         '9bd98250cfa49e13fa3fa731b6040549')
build() {
    cd $srcdir/$pkgname-$pkgver

    LDFLAGS="-Wl,--as-needed -Wl,--no-undefined" ./configure \
                --prefix=/usr \
                --sysconfdir=/etc \
                --localstatedir=/var/lib/syslog-ng \
                --enable-tcp-wrapper \
                --with-pidfile-dir=/var/run \
                --disable-spoof-source
    make || return 1
    make DESTDIR=$pkgdir install || return 1
    install -dm755 $pkgdir/var/lib/syslog-ng
    install -Dm644 $srcdir/syslog-ng.conf $pkgdir/etc/syslog-ng.conf
    install -Dm644 $srcdir/syslog-ng.logrotate $pkgdir/etc/logrotate.d/syslog-ng
    install -Dm755 $srcdir/syslog-ng.rc $pkgdir/etc/rc.d/syslog-ng
    # remove libexec
    rm -rf $pkgdir/usr/libexec
}
