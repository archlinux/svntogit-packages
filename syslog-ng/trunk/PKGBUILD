# $Id$
# Maintainer: Dave Reisner <dreisner@archlinux.org>
# Maintainer: Eric BÃ©langer <eric@archlinux.org>
# Maintainer: Aaron Griffin <aaron@archlinux.org>

pkgname=syslog-ng
pkgver=3.2.4
pkgrel=3
pkgdesc="Next-generation syslogd with advanced networking and filtering capabilities"
arch=('i686' 'x86_64')
license=('GPL2')
groups=('base')
url="http://www.balabit.com/network-security/syslog-ng/"
depends=('glib2' 'eventlog' 'openssl' 'libcap' 'awk')
makedepends=('flex' 'pkg-config')
optdepends=('logrotate: for rotating log files')
provides=('logger')
options=('!libtool')
backup=('etc/syslog-ng/modules.conf' 'etc/syslog-ng/scl.conf' \
        'etc/syslog-ng/syslog-ng.conf' 'etc/logrotate.d/syslog-ng')
source=(http://www.balabit.com/downloads/files/syslog-ng/sources/${pkgver}/source/${pkgname}_${pkgver}.tar.gz
        non-blocking-systemd-fds.patch
        syslog-ng.conf syslog-ng.logrotate syslog-ng.rc cap_syslog.patch)
md5sums=('5995f7dad0053a478b60a63f6f754203'
         '25e43afe51eb2223c25168e3c3e7aaf6'
         '344dddfff946300f5576b13a7e8ea19f'
         '735636090be4582885974542d2a75855'
         '8b4441343d859c2f01c2cf799a7e6f98'
         '46e5dcff71f820d497898331a3f608fe')
sha1sums=('ff732f7223bd2bd0424d4b9028b523cf62133af1'
          '73b83deae9a8b945dfb13adf331e6bf6f119b83e'
          'b9eb8c61f7cccda543fc5c97fe1d40a8d15e713f'
          'ac997b25d7d8e69e66782d3771a0e12aff55ae7f'
          '4481819762fd1b6f25903016a8878e63c0058fcb'
          'f093a09626a528d23f0f2aeece018cab15a735a9')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # fix #22555 for kernels >=2.6.38
  patch -p1 -i ../cap_syslog.patch

  # fix systemd blocking FD bug
  # https://bugzilla.balabit.com/show_bug.cgi?id=125
  patch -Np1 < "$srcdir/non-blocking-systemd-fds.patch"

  ./configure --prefix=/usr --sysconfdir=/etc/syslog-ng \
    --libexecdir=/usr/lib --localstatedir=/var/lib/syslog-ng \
    --enable-tcp-wrapper \
    --with-pidfile-dir=/var/run \
    --disable-spoof-source \
    --disable-tcp-wrapper \
    --enable-systemd

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -dm755 "${pkgdir}/var/lib/syslog-ng" "${pkgdir}/etc/syslog-ng/patterndb.d"
  install -Dm644 "${srcdir}/syslog-ng.conf" "${pkgdir}/etc/syslog-ng/syslog-ng.conf"
  install -Dm644 "${srcdir}/syslog-ng.logrotate" "${pkgdir}/etc/logrotate.d/syslog-ng"
  install -Dm755 "${srcdir}/syslog-ng.rc" "${pkgdir}/etc/rc.d/syslog-ng"
  install -Dm644 "doc/examples/syslog-ng.service" "$pkgdir/lib/systemd/system/syslog-ng.service"
}
