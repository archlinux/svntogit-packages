# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=eog
pkgver=3.16.0
pkgrel=1
pkgdesc="Eye of Gnome: An image viewing and cataloging program"
arch=(i686 x86_64)
license=(GPL)
depends=(gnome-desktop libexif lcms2 desktop-file-utils exempi libpeas librsvg dconf)
makedepends=(intltool itstool gobject-introspection)
install=eog.install
groups=(gnome)
options=('!emptydirs')
url="https://wiki.gnome.org/Apps/EyeOfGnome"
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('289f659ce593adb15868d73895634b5f4445c87fe84715eb598405d1d0e7a85e')

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
