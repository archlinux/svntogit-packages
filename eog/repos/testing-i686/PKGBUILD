# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=eog
pkgver=3.6.1
pkgrel=1
pkgdesc="Eye of Gnome: An image viewing and cataloging program"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gnome-desktop' 'libexif' 'lcms2' 'desktop-file-utils' 'exempi' 'libpeas' 'librsvg' 'gnome-icon-theme' 'dconf')
makedepends=('gtk-doc' 'intltool' 'itstool')
install=eog.install
groups=('gnome-extra')
options=('!emptydirs' '!libtool')
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('4442ec61b0c5092c686ccd13038359eb50e48ee49176a588b8ea04072c19bd57')

build() {
  cd "$pkgname-$pkgver"
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  # remove it from Xfce menu that ships its own thingy
  echo "NotShowIn=XFCE" >> $pkgdir/usr/share/applications/eog.desktop
}
