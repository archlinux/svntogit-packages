# $Id$
# Jan de Groot <jgc@archlinux.org>

pkgname=eog
pkgver=2.24.0
pkgrel=1
pkgdesc="Eye of Gnome: An image viewing and cataloging program"
arch=(i686 x86_64)
license=('GPL')
depends=('gnome-desktop>=2.24.0' 'libexif>=0.6.16' 'lcms>=1.17' 'desktop-file-utils' 'gnome-icon-theme>=2.24.0' 'exempi>=2.0.1')
makedepends=('perlxml' 'pkgconfig' 'gnome-doc-utils>=0.14.0')
install=eog.install
groups=('gnome-extra')
options=(!emptydirs)
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.24/${pkgname}-${pkgver}.tar.bz2)
md5sums=('9a8cb17555df81980ac0492715a195e3')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var --disable-scrollkeeper || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1
  
  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
