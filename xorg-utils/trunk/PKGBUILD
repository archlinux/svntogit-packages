# $Id$
# Maintainer: Alexander Baldeck <alexander@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
pkgname=xorg-utils
pkgver=7.4
pkgrel=4
pkgdesc="Collection of client utilities used to query the X server"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libxxf86dga' 'libxi' 'libxxf86misc' 'libxtst' 'libxinerama' 'libxv'
         'libdmx' 'libxft' 'libxaw>=1.0.5' 'libgl')
makedepends=('pkgconfig' 'mesa' 'glproto')
groups=('xorg')
source=(${url}/releases/individual/app/xdpyinfo-1.0.3.tar.bz2
        ${url}/releases/individual/app/xdriinfo-1.0.2.tar.bz2
        ${url}/releases/individual/app/xev-1.0.3.tar.bz2
        ${url}/releases/individual/app/xfd-1.0.1.tar.bz2
        ${url}/releases/individual/app/xfontsel-1.0.2.tar.bz2
        ${url}/releases/individual/app/xlsatoms-1.0.1.tar.bz2
        ${url}/releases/individual/app/xlsclients-1.0.1.tar.bz2
        ${url}/releases/individual/app/xlsfonts-1.0.2.tar.bz2
        ${url}/releases/individual/app/xprop-1.0.4.tar.bz2
        ${url}/releases/individual/app/xvinfo-1.0.2.tar.bz2
        ${url}/releases/individual/app/xwininfo-1.0.4.tar.bz2
        LICENSE.xdpyinfo LICENSE.xfd LICENSE.xlsclients 
        LICENSE.xprop LICENSE.xwininfo LICENSE.xev 
        LICENSE.xlsatoms LICENSE.xlsfonts LICENSE.xvinfo)
md5sums=('b7cbab6cbcd12bf7ad65dbc12d86e104' 'a5ec51ed9f0a55dc3462d90d52ff899c'\
         'a9532c3d1683c99bb5df1895cb3a60b1' 'c72abd90f50ef459bc14b39ec9fcc7f8'\
         '288fe4cf8a990e4e602aac16dd9109fb' '69adcda848479acc3b82b3928812d191'\
         '44473b880d26bfbe8b3d4d72b183cba7' '28958248590ff60ecd70e8f590d977b7'\
         '48aa8fd78802f477dcbf9ef0dfd9f783' 'e1e318436f49e2f0f3764593dadd9ad2'\
         'e2a9bf5ab7f2a0866700a3b49dd8c6bf' 'b43f731a9e2684728230ce8e33f63228'\
         '9e8a82b7e9898f36c378b80509f170a6' '9e8a82b7e9898f36c378b80509f170a6'\
         '801f9850b29dad506e995d011efe2914' '1c47d00801594a0e251694d70c7fb0b8'\
         'e84310edd65e120d85914350c795bfac' '9ee99bda91248e945186090905db25d8'\
         '345d6e65d71fdde292cc27f93c15e4a1' '67ea6bb3b11620c82ce4d24e78b4a0a9')

build() {
  cd "${srcdir}"
  for i in *; do
    if [ -d "${i}" ]; then
      pushd "${i}"
      ./configure --prefix=/usr --mandir=/usr/share/man || return 1
      make || return 1
      make DESTDIR="${pkgdir}" install || return 1
      install -D -m644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE.${i%-*} || return 1
      popd
    fi
  done
# Replacing stub license files
  install -D -m644 "${srcdir}"/LICENSE.* "${pkgdir}/usr/share/licenses/${pkgname}/"  || return 1
}
