# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-utils
pkgver=7.6
pkgrel=2
pkgdesc="Collection of client utilities used to query the X server"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libxxf86dga' 'libxi' 'libxxf86misc' 'libxtst' 'libxinerama' 'libxv'
         'libdmx' 'libxft' 'libxaw>=1.0.6' 'libgl' 'libxcomposite' 'xcb-util')
makedepends=('pkgconfig' 'mesa' 'glproto' 'xorg-util-macros')
groups=('xorg')
source=(${url}/releases/individual/app/xdpyinfo-1.1.0.tar.bz2
        ${url}/releases/individual/app/xdriinfo-1.0.3.tar.bz2
        ${url}/releases/individual/app/xev-1.0.4.tar.bz2
        ${url}/releases/individual/app/xfd-1.0.1.tar.bz2
        ${url}/releases/individual/app/xfontsel-1.0.2.tar.bz2
        ${url}/releases/individual/app/xlsatoms-1.1.0.tar.bz2
        ${url}/releases/individual/app/xlsclients-1.1.0.tar.bz2
        ${url}/releases/individual/app/xlsfonts-1.0.2.tar.bz2
        ${url}/releases/individual/app/xprop-1.1.0.tar.bz2
        ${url}/releases/individual/app/xvinfo-1.1.0.tar.bz2
        ${url}/releases/individual/app/xwininfo-1.0.5.tar.bz2
        LICENSE.xdpyinfo
        LICENSE.xfd
        LICENSE.xprop
        LICENSE.xlsfonts)
md5sums=('d1d516610316138105cd07064b257c5c'
         'cb304d1ce562ac48b68eedeba38c662f'
         '5f98c0a2725a33d60ef4956befe078fb'
         'c72abd90f50ef459bc14b39ec9fcc7f8'
         '288fe4cf8a990e4e602aac16dd9109fb'
         'c543ccb6489b629d427810d9a57d0724'
         '550a0fee047c5043f2cbf190b41f8a1b'
         '28958248590ff60ecd70e8f590d977b7'
         'f0bacbd30f0dd1c1e9ccafe97687b7a4'
         '8cc299b2cb636b5f7fe672610789d3d4'
         '908f8bc3255f639effa9780fb1c19ea4'
         'b43f731a9e2684728230ce8e33f63228'
         '9e8a82b7e9898f36c378b80509f170a6'
         '801f9850b29dad506e995d011efe2914'
         '345d6e65d71fdde292cc27f93c15e4a1')

build() {
  cd "${srcdir}"
  for i in *; do
    if [ -d "${i}" ]; then
      pushd "${i}"
      ./configure --prefix=/usr --mandir=/usr/share/man --without-xprint || return 1
      make || return 1
      make DESTDIR="${pkgdir}" install || return 1
      install -D -m644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE.${i%-*} || return 1
      popd
    fi
  done
# Replacing stub license files
  install -D -m644 "${srcdir}"/LICENSE.* "${pkgdir}/usr/share/licenses/${pkgname}/"  || return 1
}
