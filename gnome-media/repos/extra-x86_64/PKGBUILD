# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=gnome-media
pkgname=(gnome-media gnome-media-pulse)
pkgver=2.32.0
pkgrel=3
pkgdesc="GNOME Media Tools"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gstreamer0.10-good-plugins>=0.10.24' 'gstreamer0.10-base-plugins>=0.10.30' 'libcanberra>=0.25' 'hicolor-icon-theme')
makedepends=('intltool' 'pkgconfig' 'gnome-doc-utils>=0.20.1' 'libunique>=1.1.6' 'libpulse')
url="http://www.gnome.org"
groups=('gnome')
options=('!libtool' '!emptydirs')
install=gnome-media.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgbase}/2.32/${pkgbase}-${pkgver}.tar.bz2
        gnome_volume_control_fix_channel_bars.patch)
sha256sums=('2b0ad4decd75406398f46e82a170e53bcbc6e1bdc3363f03a07b00f59c2fe5e6'
            '51e59fb42114d70118f4b750a06a33e0e40b0a03ed6509dc1348c641cc274d74' )

build() {
  cd "${srcdir}"
  patch -Np0 -i "${srcdir}/gnome_volume_control_fix_channel_bars.patch"
  cp -a "${pkgbase}-${pkgver}"{,-pulse}

  cd "${pkgbase}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/gnome-media \
      --localstatedir=/var --disable-static \
      --disable-scrollkeeper --disable-pulseaudio
  make

  cd "${srcdir}/${pkgbase}-${pkgver}-pulse"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/gnome-media \
      --localstatedir=/var --disable-static \
      --disable-scrollkeeper --enable-pulseaudio
  make
}

_commonpackage() {
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgbase}.schemas" --domain gnome-media-2.0 ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas

  # remove it from Xfce menu that ships its own thingy
  echo "NotShowIn=XFCE" >> ${pkgdir}/usr/share/applications/gnome-volume-control.desktop
}

package_gnome-media() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  _commonpackage
}

package_gnome-media-pulse() {
  pkgdesc+=" with PulseAudio support"
  depends+=(libpulse)
  groups=(pulseaudio-gnome)
  conflicts=(gnome-media)
  provides=("gnome-media=${pkgver}-${pkgrel}")

  cd "${srcdir}/${pkgbase}-${pkgver}-pulse"
  _commonpackage

  # Install missing icon
  install -Dm644 \
    gst-mixer/icons/16x16/status/audio-input-microphone-muted.png \
    "${pkgdir}/usr/share/icons/hicolor/16x16/status/audio-input-microphone-muted.png"
}
