# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-media
pkgver=2.91.2
pkgrel=2
pkgdesc="GNOME Media Tools"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libgnome-media-profiles' 'libcanberra' 'hicolor-icon-theme')
makedepends=('intltool' 'pkgconfig' 'gnome-doc-utils')
url="http://www.gnome.org"
groups=('gnome')
replaces=('gnome-media-pulse')
options=('!libtool' '!emptydirs')
install=gnome-media.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.91/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('4aadc834d04c3d6f573fad667a69c6dc9668240e759cbd8cfb14d053d8e4da5f')

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}

  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/gnome-media \
      --localstatedir=/var --disable-static \
      --disable-scrollkeeper
  make
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgbase}.schemas" --domain gnome-media-2.0 ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas

  rm -rf "${pkgdir}/usr/share/sounds/"
}
