# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-media
pkgver=2.28.1
pkgrel=1
pkgdesc="GNOME Media Tools"
arch=(i686 x86_64)
license=('GPL')
depends=('libglade>=2.6.4' 'gstreamer0.10-good-plugins>=0.10.16' 'gstreamer0.10-base-plugins>=0.10.24' 'libcanberra>=0.14' 'hicolor-icon-theme')
makedepends=('intltool' 'pkgconfig' 'gnome-doc-utils>=0.18.0' 'libunique>=1.1.2')
url="http://www.gnome.org"
groups=('gnome')
options=('!libtool' '!emptydirs')
install=gnome-media.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.28/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('1a1efc4f8f0b127cde4839d64343a5041c00ffb87597d45aabd4e22c4422eed0')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/gnome-media \
      --localstatedir=/var --disable-static \
      --disable-scrollkeeper || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1
  
  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gnome-media-2.0 ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
