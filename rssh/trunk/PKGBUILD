# $Id$
# Contributor: Judd Vinet <jvinet@zeroflux.org>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=rssh
pkgver=2.3.3
pkgrel=4
pkgdesc='Restricted shell for use with OpenSSH, allowing only scp and/or sftp'
url='http://www.pizzashack.org/rssh/'
license=('custom:rssh')
arch=('i686' 'x86_64')
backup=('etc/rssh.conf')
depends=('openssh')
source=("http://downloads.sourceforge.net/sourceforge/rssh/rssh-${pkgver}.tar.gz"
        'env-breach.patch'
        'destdir.patch'
        'rsync.patch')
sha1sums=('0a6dd80b5e6059e0db12c9f1276121dd966b610a'
          '434712f82f24c60834a10142ca5c49b8a57555a7'
          '85bd1694decae5872cbeeafd578b147eb13313c6'
          '86564eab4493f4b4502a022e5938babb31450a00')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	patch -p1 -i ../env-breach.patch # FS#30950
	patch -p1 -i ../rsync.patch # FS#21783
	patch -p1 -i ../destdir.patch

	./configure \
		--prefix=/usr \
		--libexecdir=/usr/lib/rssh \
		--mandir=/usr/share/man \
		--sysconfdir=/etc \

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/rssh/LICENSE
}
