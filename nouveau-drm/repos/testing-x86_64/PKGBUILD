# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: buddabrod <buddabrod@gmail.com>

pkgname=nouveau-drm
_snapdate=20100313
pkgver=0.0.16_${_snapdate} # see master/drivers/gpu/drm/nouveau/nouveau_drv.h for version
_kernver='2.6.33-ARCH'
pkgrel=2
pkgdesc="nvidia opensource X driver"
arch=('i686' 'x86_64')
url="http://nouveau.freedesktop.org/"
depends=("kernel26>=2.6.33.3" "kernel26<2.6.34")
makedepends=("kernel26-headers>=2.6.33" "kernel26-headers<2.6.34")
# http://nouveau.freedesktop.org/wiki/InstallDRM#head-771a47a133fba6a5a70253656e76c61e9bff93bd
optdepends=('nouveau-firmware: needed for very new card (NV50 family and above) to work properly where the OSS replacement does not work well')
#makedepends=('git' 'autoconf' 'pkgconfig')
install=${pkgname}.install
license=('GPL')
#options=('force')
source=(ftp://ftp.archlinux.org/other/$pkgname/master-${_snapdate}.tar.gz 
	# http://people.freedesktop.org/~pq/nouveau-drm/master.tar.gz
	# get the Makefile from http://cgit.freedesktop.org/nouveau/linux-2.6/plain/nouveau/Makefile?h=master-compat
	Makefile)
md5sums=('ad7ad0fb73d56bcff3d2b54a70c3d511'
         'e7d5df80186d5c736e82b0a7d46216bc')

build() {
  cd ${srcdir}/master
  mkdir nouveau
  cp $srcdir/Makefile ${srcdir}/master/nouveau/
  cd nouveau
  make LINUXDIR=/lib/modules/${_kernver}/build DRM_MODULES="nouveau" || return 1

  install -D -m 0644 ${srcdir}/master/drivers/gpu/drm/drm.ko ${pkgdir}/lib/modules/${_kernver}/updates/drm.ko || return 1
  install -D -m 0644 ${srcdir}/master/drivers/gpu/drm/drm_kms_helper.ko ${pkgdir}/lib/modules/${_kernver}/updates/drm_kms_helper.ko || return 1
  install -D -m 0644 ${srcdir}/master/drivers/gpu/drm/nouveau/nouveau.ko ${pkgdir}/lib/modules/${_kernver}/kernel/drivers/video/nouveau.ko || return 1
  install -D -m 0644 ${srcdir}/master/drivers/gpu/drm/ttm/ttm.ko ${pkgdir}/lib/modules/${_kernver}/updates/ttm.ko || return 1
  install -D -m 0644 ${srcdir}/master/drivers/gpu/drm/i2c/ch7006.ko ${pkgdir}/lib/modules/${_kernver}/updates/ch7006.ko || return 1
  install -D -m 0644 ${srcdir}/master/include/drm/nouveau_drm.h ${pkgdir}/usr/include/nouveau_drm.h || return 1

  # install script
  sed -i -e "s/KERNEL_VERSION='.*'/KERNEL_VERSION='${_kernver}'/" ${startdir}/${pkgname}.install
}
