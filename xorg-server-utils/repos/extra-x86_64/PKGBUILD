# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-server-utils
pkgver=7.5
pkgrel=3
pkgdesc="X.Org utilities required by xorg-server"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libxfontcache>=1.0.5' 'libxi>=1.3' 'libxaw>=1.0.6' 'libxxf86misc>=1.0.2' 'libxrandr>=1.3.0' 'libxxf86vm>=1.1.0' 'mcpp>=2.7.2')
makedepends=('pkgconfig' 'xbitmaps' 'bigreqsproto' 'xtrans')
groups=('xorg')
source=(${url}/releases/individual/app/iceauth-1.0.3.tar.bz2
        ${url}/releases/individual/app/rgb-1.0.3.tar.bz2
        ${url}/releases/individual/app/sessreg-1.0.5.tar.bz2
        ${url}/releases/individual/app/xcmsdb-1.0.2.tar.bz2
        ${url}/releases/individual/app/xbacklight-1.1.1.tar.bz2
        ${url}/releases/individual/app/xgamma-1.0.3.tar.bz2
        ${url}/releases/individual/app/xhost-1.0.3.tar.bz2
        ${url}/releases/individual/app/xinput-1.5.0.tar.bz2
        ${url}/releases/individual/app/xmodmap-1.0.4.tar.bz2
        ${url}/releases/individual/app/xrandr-1.3.2.tar.bz2
        ${url}/releases/individual/app/xrdb-1.0.6.tar.bz2
        ${url}/releases/individual/app/xrefresh-1.0.3.tar.bz2
        ${url}/releases/individual/app/xset-1.1.0.tar.bz2
        ${url}/releases/individual/app/xsetmode-1.0.0.tar.bz2
        ${url}/releases/individual/app/xsetroot-1.0.3.tar.bz2
        ${url}/releases/individual/app/xvidtune-1.0.1.tar.bz2
        LICENSE.iceauth
        LICENSE.xcmsdb
        LICENSE.xvidtune)
md5sums=('975ade3f238c1eb10705da0a91e6e8e7'
         '44ea16cc3104de6401bc74035f642357'
         '00e834c3cbfd990e9d7d5ddc834242df'
         '7f2bed9f4dd3301d18d83eb296c3be0d'
         '3e39eec6d0fd5c587ca6d55aa7bb8fe1'
         'e8a88bf1a18f35b724619849dca97f4f'
         'c7f91b4a750d297f269c2a0a3206a1b2'
         '3e8a5f1faccc8ab00c6190e5a34e0a45'
         'bbe021f812e0014a8ee3692317788119'
         '2cb19bb1c19ccf77c40032b03dbe06f0'
         '8dd881cd5a7dcb6e456549817f0fb475'
         '8f4fafcfb77b3c6972cc44bb3f1c899c'
         'eeb0d02f69c76bd40470dede99b4bd49'
         'd074e79d380b031d2f60e4cd56538c93'
         'cbda9b9b8da5af4614110883d5a276b8'
         'e0744594f4e5969b20df28d897781318'
         '65695dc24c24958190724a23655ffda7'
         'b6235416dca2205f0ba83c0c4b3ddeb8'
         '4e6babde6334e3c5cb4a3136526aa564')

build() {
  cd "${srcdir}"
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  for i in *; do
    if [ -d "${i}" ]; then
      pushd "${i}"
      case "${i}" in
        xrdb*)
          ./configure --prefix=/usr --mandir=/usr/share/man --with-cpp=/usr/bin/mcpp || return 1
          ;;
        *)
          ./configure --prefix=/usr --mandir=/usr/share/man || return 1
          ;;
      esac
      make || return 1
      make DESTDIR="${pkgdir}" install || return 1
      install -m644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE.${i%-*} || return 1
      popd
    fi
  done
# Replacing stub license files
  install -m644 "${srcdir}"/LICENSE.* "${pkgdir}/usr/share/licenses/${pkgname}/"  || return 1
}
