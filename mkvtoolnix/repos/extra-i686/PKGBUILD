# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: xduugu <xduugu@gmx.com>

pkgbase=mkvtoolnix
pkgname=('mkvtoolnix-cli' 'mkvtoolnix-gui')
pkgver=8.5.2
pkgrel=1
arch=('i686' 'x86_64')
url='https://www.bunkus.org/videotools/mkvtoolnix/'
license=('GPL')
makedepends=('boost' 'boost-libs' 'file' 'flac' 'libebml' 'libmatroska'
             'libogg' 'libvorbis' 'qt5-base' 'ruby' 'zlib')
source=("http://www.bunkus.org/videotools/mkvtoolnix/sources/mkvtoolnix-${pkgver}.tar.xz")
md5sums=('2b4dec263148f9f6443cc060e0c608b6')

build() {
  cd mkvtoolnix-${pkgver}

  ./configure --prefix='/usr' \
              --without-curl \
              --disable-qt
  ./drake apps:mkvinfo $MAKEFLAGS
  mv src/mkvinfo{,-cli}

  ./configure --prefix='/usr' \
              --without-curl
  ./drake $MAKEFLAGS
}

package_mkvtoolnix-cli() {
  pkgdesc='Set of tools to create, edit and inspect Matroska files - CLI'
  depends=('boost-libs' 'file' 'flac' 'libebml' 'libmatroska' 'libogg'
           'libvorbis' 'zlib')

  cd mkvtoolnix-${pkgver}

  ./drake DESTDIR="${pkgdir}" install
  install -m 755 src/mkvinfo-cli "${pkgdir}"/usr/bin/mkvinfo
  rm -rf "${pkgdir}"/usr/share/{applications,icons,mime}
  find "${pkgdir}" -name mkvtoolnix-gui* -delete
}

package_mkvtoolnix-gui() {
  pkgdesc='Set of tools to create, edit and inspect Matroska files - GUI'
  depends=('desktop-file-utils' 'hicolor-icon-theme' 'mkvtoolnix-cli'
           'qt5-base' 'shared-mime-info' 'xdg-utils')
  replaces=('mkvtoolnix-gtk')
  install='mkvtoolnix-gui.install'

  cd mkvtoolnix-${pkgver}

  ./drake DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/share/locale
  for t in mkv{extract,info,merge,propedit}; do
    find "${pkgdir}" -name $t* -delete
  done
}

# vim: ts=2 sw=2 et:
