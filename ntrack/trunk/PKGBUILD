# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=ntrack
pkgver=13
pkgrel=2
epoch=1
pkgdesc="A network connectivity tracking library"
arch=('i686' 'x86_64')
url="http://launchpad.net/ntrack/"
license=('LGPL')
depends=('qt' 'libnl' 'gcc-libs')
makedepends=('python2')
source=("http://launchpad.net/${pkgname}/main/0${pkgver}/+download/${pkgname}-0${pkgver}.tar.gz"
        'dead-loop-fix.patch')
md5sums=('90596b924a108f8b4eea52239ba64369'
         '02bda6ae15ef04b6a7d9273c37752681')
options=('!libtool' '!emptydirs')

build() {
  cd "${srcdir}/${pkgname}-0${pkgver}"

  patch -p0 -i "${srcdir}"/dead-loop-fix.patch

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var
  make
}

package() {
  cd "${srcdir}/${pkgname}-0${pkgver}"
  make DESTDIR="${pkgdir}" install
}
