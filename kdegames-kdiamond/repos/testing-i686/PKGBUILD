# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-kdiamond
pkgver=4.13.1
pkgrel=1
pkgdesc="A single player puzzle game"
url="http://kde.org/applications/games/kdiamond/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'libkdegames')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/kdiamond-${pkgver}.tar.xz")
sha1sums=('2e1fb1544992d6d40bd0b4e2c68a33855c684191')

build() {
  mkdir build
  cd build
  cmake ../kdiamond-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
