# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=eog-plugins
pkgver=3.8.1
pkgrel=2
pkgdesc="Plugins for Eye of Gnome"
arch=('i686' 'x86_64')
url="http://www.gnome.org/"
license=('GPL2')
depends=('eog' 'libchamplain' 'libgdata')
makedepends=('intltool')
install=eog-plugins.install
options=('!libtool')
source=(http://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('5cf8dab5b6622b9e712da4094185112cc84baa96f2775cc76bf654b1c443cfb9')

build() {
  cd $pkgname-$pkgver
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr \
    --sysconfdir=/etc --localstatedir=/var

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
