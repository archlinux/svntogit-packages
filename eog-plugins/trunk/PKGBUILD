# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=eog-plugins
pkgver=3.12.1
pkgrel=1
pkgdesc="Plugins for Eye of Gnome"
arch=('i686' 'x86_64')
url="https://wiki.gnome.org/Apps/EyeOfGnome/Plugins"
license=('GPL2')
depends=('eog' 'libchamplain' 'libgdata')
makedepends=('intltool')
install=eog-plugins.install
source=(http://download.gnome.org/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('5b9a6854d5411e75aadb9d44694bbe02a35f5a79a23da5c1bb4feb6bacf56f1c')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
    --sysconfdir=/etc --localstatedir=/var

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
