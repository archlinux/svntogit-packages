# Maintainer: Guillaume ALAUX <guillaume@archlinux.org>
_libname=commons-net
pkgname=java-${_libname}1
pkgver=1.4.1
pkgrel=2
pkgdesc="Apache library that implements the client side of many basic Internet protocols"
arch=('any')
url="http://commons.apache.org/net/"
license=('APACHE')
depends=('java-runtime-headless')
makedepends=('apache-ant')
source=(http://archive.apache.org/dist/commons/net/source/${_libname}-${pkgver}-src.tar.gz
        http://archive.apache.org/dist/jakarta/oro/jakarta-oro-2.0.8.tar.gz)
md5sums=('ccbb3f67b55e8a7a676499db4386673c'
         '6f7690c6ba9750e3cbb8ebd10078a79a')


build() {
  cd "${srcdir}/${_libname}-${pkgver}"
  # "-Dbuild.sysclasspath=only" prevents ant from downloading oro
  ant \
    -lib ${srcdir}/jakarta-oro-2.0.8/jakarta-oro-2.0.8.jar \
    -Dbuild.sysclasspath=only \
    jar
}

package() {
  cd "${srcdir}/${_libname}-${pkgver}/target"
  install -dm755 ${pkgdir}/usr/share/java/${_libname}
  install -m644 ${_libname}-${pkgver}.jar ${pkgdir}/usr/share/java
  ln -s ${_libname}-${pkgver}.jar ${pkgdir}/usr/share/java/${_libname}.jar
}
