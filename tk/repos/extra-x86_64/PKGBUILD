# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=tk
pkgver=8.5.3
pkgrel=1
pkgdesc="A windowing toolkit for use with tcl"
arch=('i686' 'x86_64')
url="http://tcl.sourceforge.net/"
license=('custom')
depends=("tcl=${pkgver}" 'libxss' 'libxft')
source=(http://downloads.sourceforge.net/sourceforge/tcl/tk${pkgver}-src.tar.gz
        xproto-7-0-13.patch)
md5sums=('c7d2ef187d0f313850689cda20148ec3'
         '024f8db9663bd7035feac59aabd7e516')

build() {
  cd ${srcdir}/tk${pkgver}
  patch -Np1 -i ${srcdir}/xproto-7-0-13.patch

  cd ${srcdir}/tk${pkgver}/unix  
  if [ "$CARCH" = "x86_64" ]; then
    ./configure --prefix=/usr --enable-threads --enable-64bit
  else
    ./configure --prefix=/usr --enable-threads --disable-64bit
  fi
  
  make
  make INSTALL_ROOT=${pkgdir} install install-private-headers
  ln -sf wish8.5 ${pkgdir}/usr/bin/wish
  # install license
  install -Dm644 ../license.terms ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE

  # remove buildroot traces / fixes #3602
  sed -i \
  -e "s,^TK_BUILD_LIB_SPEC='-L.*/unix,TK_BUILD_LIB_SPEC='-L/usr/lib," \
  -e "s,^TK_SRC_DIR='.*',TK_SRC_DIR='/usr/include'," \
  -e "s,^TK_BUILD_STUB_LIB_SPEC='-L.*/unix,TK_BUILD_STUB_LIB_SPEC='-L/usr/lib," \
  -e "s,^TK_BUILD_STUB_LIB_PATH='.*/unix,TK_BUILD_STUB_LIB_PATH='/usr/lib," \
  ${pkgdir}/usr/lib/tkConfig.sh
}
