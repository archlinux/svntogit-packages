# Maintainer:
# Contributor: Zachary Vance <za3k at za3k . com>
 
pkgname=paper
pkgver=2.0
pkgrel=1
arch=(x86_64)
pkgdesc="Replacement for Debian's libpaper"
url="https://github.com/rrthomas/paper"
license=(GPL3)
depends=(perl)
replaces=(rrthomas-libpaper)
source=($pkgname-$pkgver.tar.gz::"https://github.com/rrthomas/paper/archive/v$pkgver.tar.gz"
        gnulib.tar.gz::"http://git.savannah.gnu.org/gitweb/?p=gnulib.git;a=snapshot;h=d279bc6d9f9323e19ad8c32b6d12ff96dfb0f5ba;sf=tgz")
sha256sums=('61a40576d0391e17dac1fbba0253967ace0ccec80bb18aa29d4ada41534fb16c'
            '171142863dd860b3a5babc0f6172048313cf9d98d00a289be9c154bf015ac68d')

build() {
  cd $pkgname-$pkgver
  
  ./bootstrap --gnulib-srcdir="$srcdir"/gnulib-d279bc6
  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
