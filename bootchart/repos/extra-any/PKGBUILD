# $Id$
# Maintainer : Aaron Griffin <aaron@archlinux.org>

pkgname=bootchart
pkgver=0.9
pkgrel=6
pkgdesc="Boot Process Performance Visualization"
url="http://www.bootchart.org/"
arch=('any')
license=('GPL2')
depends=('acct' 'sh')
makedepends=('apache-ant')
optdepends=('java-runtime: for bootchart-render')
install=bootchart.install
source=(http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.bz2
        bootchart-render
        #FS#8438 - fix the regex used to scan inittab for archlinux
        fix-inittab-regex.patch)
md5sums=('4be91177d19069e21beeb106f2f77dff'
         'f02888cda23a5b1c9b931140f6fdbb71'
         'c8a70f1f763a2632dc7759a6387f865f')

build()
{
  cd $srcdir/$pkgname-$pkgver
  patch -up1 < $srcdir/fix-inittab-regex.patch
  ant
  sed -i "s#ROOT=/#ROOT=$pkgdir#" install.sh

  install -dm755 $pkgdir/{sbin,etc}
  ./install.sh

  chmod 644 $pkgdir/etc/bootchartd.conf

  install -Dm644 bootchart.jar $pkgdir/usr/share/bootchart/bootchart.jar
  install -Dm755 $srcdir/bootchart-render $pkgdir/usr/bin/bootchart-render
}
