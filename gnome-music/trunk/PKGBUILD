# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-music
pkgver=3.20.2
pkgrel=1
pkgdesc="Music player and management application"
url="https://wiki.gnome.org/Apps/Music"
arch=(i686 x86_64)
license=(GPL)
depends=(grilo grilo-plugins tracker libdmapsharing python-dbus dbus-glib
         gnome-themes-standard gtk3 gvfs python-gobject python-cairo
         gst-plugins-base python-requests)
makedepends=(intltool itstool gobject-introspection)
optdepends=('gst-plugins-good: Extra media codecs'
            'gst-plugins-ugly: Extra media codecs'
            'gst-plugins-bad: Extra media codecs'
            'gst-libav: Extra media codecs')
groups=(gnome-extra)
source=(https://download.gnome.org/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('ca328bfd85ba8cb651e4e3c5d56499b111cb95b4f3e9b2e482cca7830213c7a0')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-schemas-compile
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="${pkgdir}" install
}
