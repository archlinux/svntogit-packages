# $Id$
# Maintainer:
# Contributor: Sergej Pupykin <sergej@aur.archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=python-m2crypto
pkgver=0.20.2
pkgrel=3
pkgdesc="A crypto and SSL toolkit for Python"
arch=('i686' 'x86_64')
url="http://wiki.osafoundation.org/bin/view/Projects/MeTooCrypto"
license=('BSD')
depends=('python2' 'openssl')
makedepends=('swig')
source=("http://pypi.python.org/packages/source/M/M2Crypto/M2Crypto-${pkgver}.tar.gz"
	'openssl1.patch')
md5sums=('6c24410410d6eb1920ea43f77a93613a'
         '80a707ad6610f2871efdf40658e30364')

build() {
  cd "${srcdir}/M2Crypto-${pkgver}"
  patch -Np0 -i ${srcdir}/openssl1.patch
  python2 setup.py build
}

package() {
  cd "${srcdir}/M2Crypto-${pkgver}"
  python2 setup.py install --root="${pkgdir}/" --optimize=1

  install -D -m644 LICENCE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
