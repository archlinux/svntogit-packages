# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-kgeography
pkgver=14.12.3
pkgrel=1
pkgdesc="Geography Trainer"
url="http://kde.org/applications/education/kgeography/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/applications/${pkgver}/src/kgeography-${pkgver}.tar.xz")
sha1sums=('200714cdfce8949e6e1642d8f4058d07a73948a5')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../kgeography-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
