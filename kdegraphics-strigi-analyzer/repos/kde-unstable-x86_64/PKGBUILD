# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegraphics-strigi-analyzer
pkgver=15.12.0
pkgrel=1
pkgdesc="Strigi analyzers for various graphics file formats"
url="https://projects.kde.org/projects/kde/kdegraphics/kdegraphics-strigi-analyzer"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('strigi' 'libtiff')
makedepends=('cmake' 'automoc4' 'kdelibs')
replaces=('kdegraphics-libs')
conflicts=('kdegraphics-libs')
source=("http://download.kde.org/stable/applications/${pkgver}/src/${pkgname}-${pkgver}.tar.xz")
sha1sums=('22e02fabd2d29ac601f616e51901f3e32e156827')

build() {
  cd "${srcdir}"
  mkdir -p build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
