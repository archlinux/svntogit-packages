# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=libgusb
pkgver=0.4.0
pkgrel=1
pkgdesc='GObject wrapper for libusb1'
url=https://github.com/hughsie/libgusb
arch=(x86_64)
license=(LGPL2.1)
depends=(
  glib2
  libjson-glib-1.0.so
  libusb-1.0.so
)
makedepends=(
  git
  gobject-introspection
  gtk-doc
  meson
  umockdev
  vala
)
_tag=6cd1e177a2aada5d26d351d58004040ce0baea45
source=(git+https://github.com/hughsie/libgusb.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd libgusb

  git describe --tags
}

build() {
  arch-meson libgusb build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
