# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kcontacts
epoch=1
pkgver=5.79.0
pkgrel=1
pkgdesc="Address book API for KDE"
arch=(x86_64)
url="https://community.kde.org/Frameworks"
license=(LGPL)
depends=(kcoreaddons kconfig ki18n kcodecs iso-codes)
makedepends=(extra-cmake-modules doxygen qt5-tools qt5-doc)
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('c9574d77235646c3c0c6b1a9fe1eceaf131a2051a1f9de48d9949ee6a7dbd449'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
