# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=gwenview
pkgver=14.12.1
pkgrel=2
pkgdesc="A fast and easy to use image viewer for KDE"
url="http://kde.org/applications/graphics/gwenview/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegraphics')
depends=('kded' 'kactivities-frameworks' 'kdelibs4support' 'exiv2' 'lcms2')
makedepends=('extra-cmake-modules' 'git' 'kdoctools' 'python')
replaces=('kdegraphics-gwenview')
conflicts=('kdegraphics-gwenview')
install=$pkgname.install
source=("http://download.kde.org/stable/applications/${pkgver}/src/gwenview-${pkgver}.tar.xz")
sha1sums=('eb4415328bea6309ac0a51fe1852b715ed263883')

build() {
  mkdir build
  cd build
  cmake ../gwenview-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
