# $Id$
# Maintainer:
# Contributor: Firmicus <francois.archlinux.org>
# Contributor: Tom K <tomk@runbox.com>

pkgname=mod_perl
pkgver=2.0.8
pkgrel=3
pkgdesc="Apache module that embeds the Perl interpreter within the server"
arch=('i686' 'x86_64')
url="http://perl.apache.org/"
license=('APACHE')
depends=('perl' 'apache' 'db' 'apr-util' 'perl-linux-pid')
makedepends=('subversion')
options=('!emptydirs')
# upstream from http://apache.org/dist/perl/$pkgname-$pkgver.tar.gz is not ready for apache 2.4 yet
# See the discussion http://mail-archives.apache.org/mod_mbox/perl-modperl/201402.mbox/thread
# Debian developers ported mod_perl to apache 2.4 that seems works well
source=(mod_perl::svn+http://svn.apache.org/repos/asf/perl/modperl/branches/httpd24/#revision=1572460
        mod_perl-2.0.6-nolfs.patch)
sha256sums=('SKIP'
            '5d1b6a051d847108b23b5dc06ea5885c37dd81d3a9156b69d179d95b5cf92f08')

prepare() {
  cd $pkgname

  # Patch from openSUSE to fix segfaults on i686 (FS#25008):
  #   avoid a conflict between apache's LARGEFILE64_SOURCE and perl's
  #   FILE_OFFSET_BITS=64 because the conflict isn't real and causes the perl
  #   structures to be invalid if only the apache flags are used
  # patch -Np1 -i "$srcdir/mod_perl-2.0.6-nolfs.patch"
}

build() {
  cd $pkgname

  # install module in vendor directories.
  perl Makefile.PL INSTALLDIRS=vendor MP_APXS=/usr/bin/apxs
  make
}

check() {
  cd $pkgname
  # tests are very flaky
  # make test
}

package() {
  cd $pkgname
  make install DESTDIR="$pkgdir"
}

# vim:set ts=2 sw=2 et:
