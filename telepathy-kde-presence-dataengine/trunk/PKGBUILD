# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Laurent Carlier <lordheavym@gmail.com>

pkgname=telepathy-kde-presence-dataengine
pkgver=0.1.0
pkgrel=2
pkgdesc="Plasma dataengine for managing Telepathy account presence"
arch=('i686' 'x86_64')
url="http://www.kde.org"
license=('GPL')
depends=('kdebase-workspace' 'telepathy-qt4')
makedepends=('cmake' 'automoc4')
groups=('telepathy-kde')
source=("http://download.kde.org/unstable/telepathy-kde/${pkgver}/src/${pkgname}_${pkgver}.tar.bz2")
sha1sums=('afaf16ce63eef9c9a4cdb06b1e88e62f887094bd')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}_${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
