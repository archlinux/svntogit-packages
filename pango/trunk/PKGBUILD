# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=pango
pkgver=1.44.7+11+g73b46b04
pkgrel=1
epoch=1
pkgdesc="A library for layout and rendering of text"
url="https://www.pango.org/"
arch=(x86_64)
license=(LGPL)
depends=(libthai cairo libxft harfbuzz fribidi)
makedepends=(gobject-introspection help2man gtk-doc git meson)
checkdepends=(ttf-dejavu cantarell-fonts)
provides=(libpango{,cairo,ft2,xft}-1.0.so)
_commit=73b46b04c724335ad73534cc69955baa2388b861  # pango-1-44
source=("git+https://gitlab.gnome.org/GNOME/pango.git#commit=$_commit"
        0001-Use-shape-flags-for-tab-width.patch
        test.diff)
sha256sums=('SKIP'
            'd87bf1bb8dcd7edba0d8f49ddc7fd6bd9965f21a38f186fbfb297828ad53f4d1'
            '401b51f3aee44510fc94878b5549ff4a4a6d79584b5d6df2dd7e0978b9d574ce')

pkgver() {
  cd pango
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd pango

  # https://bugs.archlinux.org/task/63914
  git apply -3 ../0001-Use-shape-flags-for-tab-width.patch

  # Harmless test case failure; could be from Cantarell update
  git apply -3 ../test.diff
}

build() {
  arch-meson pango build -D gtk_doc=true
  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
