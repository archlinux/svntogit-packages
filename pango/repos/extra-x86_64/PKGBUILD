# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgbase=pango
pkgname=(pango pango-docs)
pkgver=1.50.3
pkgrel=1
epoch=1
pkgdesc="A library for layout and rendering of text"
url="https://www.pango.org/"
arch=(x86_64)
license=(LGPL)
depends=(libthai cairo libxft harfbuzz fribidi)
makedepends=(gobject-introspection help2man git meson gi-docgen)
checkdepends=(ttf-dejavu cantarell-fonts)
_commit=26aadb2508f9022cbfc72e73b558c6791f5d46d9  # tags/1.50.3^0
source=("git+https://gitlab.gnome.org/GNOME/pango.git#commit=$_commit"
        fix-test.diff)
sha256sums=('SKIP'
            'c4ad17910d51832ca15d5805e5b43af4c3de4de51cbf1880e616e12dc664895f')

pkgver() {
  cd pango
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd pango

  # Fix tests... seems to be a rounding error
  git apply -3 ../fix-test.diff
}

build() {
  arch-meson pango build -D gtk_doc=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_pango() {
  provides=(libpango{,cairo,ft2,xft}-1.0.so)

  meson install -C build --destdir "$pkgdir"

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc
}

package_pango-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}

# vim:set sw=2 et:
