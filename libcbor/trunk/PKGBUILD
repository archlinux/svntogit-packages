# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Moritz Kiefer <moritz.kiefer@purelyfunctional.org>

pkgname=libcbor
pkgver=0.7.0
pkgrel=1
pkgdesc='C library for parsing and generating CBOR, the general-purpose schema-less binary data format'
url='https://github.com/PJK/libcbor'
arch=('x86_64')
license=('MIT')
depends=('glibc')
makedepends=('cmake' 'cmocka')
provides=('libcbor.so')
source=(https://github.com/PJK/libcbor/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('4e2ed55ef17b8b77f82d780b1ee998d0342b67e6beabc24795117e5e222549640fe7fd261c8e78524d6489d03f7f5aa46b987709790a81f93e4d7409c32fecda')
b2sums=('76f93d3199e703c4167aeed262da7ba78d1df82e081754609391572b89f216efba0c54511d4021049fc6c62ab01b08835f02a58bbf226d973174b37eff087761')

build() {
  mkdir -p ${pkgname}-${pkgver}/build
  cd ${pkgname}-${pkgver}/build
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_C_FLAGS="${CFLAGS} ${CPPFLAGS}" \
    -DWITH_TESTS=1
  make cbor cbor_shared VERBOSE=1
  make -C test VERBOSE=1
}

check() {
  cd ${pkgname}-${pkgver}/build
  make -C test test
}

package() {
  cd ${pkgname}-${pkgver}
  make -C build DESTDIR="${pkgdir}" install
  install -Dm 644 README.md CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
