# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>

pkgname=meld
pkgver=3.16.0
pkgrel=2
pkgdesc='Visual diff and merge tool'
url='http://meldmerge.org/'
license=(GPL)
arch=(any)
makedepends=(intltool itstool)
depends=(python2-gobject python2-cairo gtksourceview3 gsettings-desktop-schemas)
optdepends=('python2-dbus: open a new tab in an already running instance')
source=("https://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-${pkgver}.tar.xz"
	fix-dark-style.patch)
sha1sums=('12cd5074ad23650776334cf3d1f2d803d5f2db97'
          'b0c0d299524fb0ee57c47a723a570fc057766a51')

# Meld does not support Python 3. The build succeeds, but
# the main executable checks the version and errors out.

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../fix-dark-style.patch
}

build() {
  cd $pkgname-$pkgver
  python2 setup.py build
}

package() {
  cd $pkgname-$pkgver
  # using --skip-build breaks install
  python2 setup.py --no-update-icon-cache --no-compile-schemas \
    install --prefix=/usr --root="$pkgdir" --optimize=1
}
