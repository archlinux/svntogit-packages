# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>

pkgname=meld
pkgver=3.18.0
pkgrel=1
pkgdesc="Compare files, directories and working copies"
url="http://meldmerge.org/"
license=(GPL)
arch=(any)
depends=(python-gobject python-cairo gtksourceview3 gsettings-desktop-schemas dconf)
makedepends=(intltool itstool git)
_commit=012f03ce077557637e78f7b1390d789f169eeff0  # tags/3.18.0^0
source=("git+https://git.gnome.org/browse/meld#commit=$_commit")
sha1sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  cd $pkgname
  python setup.py build
}

package() {
  cd $pkgname
  # using --skip-build breaks install
  python setup.py --no-update-icon-cache --no-compile-schemas \
    install --prefix=/usr --root="$pkgdir" --optimize=1
}
