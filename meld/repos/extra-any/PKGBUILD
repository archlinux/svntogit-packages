# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>

pkgname=meld
pkgver=1.7.5
pkgrel=1
pkgdesc='Visual diff and merge tool'
url='http://meldmerge.org/'
license=('GPL')
arch=('any')
makedepends=('intltool' 'gnome-doc-utils')
depends=('pygtk' 'hicolor-icon-theme' 'desktop-file-utils')
optdepends=('pygtksourceview2: syntax highlighting'
            'python2-dbus: open a new tab in an already running instance'
            'python2-gconf: gnome integration')
source=("http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
sha1sums=('13be80b0f3487870315f66ce376ccca35c22b786')

install=install
options=('!emptydirs')

prepare() {
	cd "${pkgname}-${pkgver}"
	sed -i 's/update-/#update-/' Makefile
	sed -i 's/env python/env python2/' bin/meld
}

build() {
	cd "${pkgname}-${pkgver}"
	make PYTHON=/usr/bin/python2 prefix=/usr
}

package() {
	cd "${pkgname}-${pkgver}"
	make PYTHON=/usr/bin/python2 prefix=/usr DESTDIR="${pkgdir}" install
}
