# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=docutils
pkgver=0.8.1
pkgrel=1
pkgdesc="Set of tools for processing plaintext docs into formats such as HTML, XML, or LaTeX"
arch=('any')
url="http://docutils.sourceforge.net"
license=('custom')
depends=('python2')
source=(http://downloads.sourceforge.net/docutils/docutils-$pkgver.tar.gz)
md5sums=('2ecf8ba3ece1be1ed666150a80c838c8')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  python2 setup.py build
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  python2 setup.py install --root=${pkgdir} --optimize=1

  for f in ${pkgdir}/usr/bin/*.py; do
      ln -s $(basename $f) $pkgdir/usr/bin/$(basename $f .py)
  done

  sed -e 's|#!/usr/bin/env python|#!/usr/bin/env python2|' \
      -i ${pkgdir}/usr/lib/python2.7/site-packages/docutils/_string_template_compat.py
  sed -e 's|#!/usr/bin/env python|#!/usr/bin/env python2|' \
      -i ${pkgdir}/usr/lib/python2.7/site-packages/docutils/writers/xetex/__init__.py
  sed -e 's|#! /usr/bin/env python|#!/usr/bin/env python2|' \
      -i ${pkgdir}/usr/lib/python2.7/site-packages/docutils/math/math2html.py
  sed -e 's|#!/usr/bin/env python|#!/usr/bin/env python2|' \
      -i ${pkgdir}/usr/lib/python2.7/site-packages/docutils/math/latex2mathml.py
  sed -e 's|#!/usr/bin/env python|#!/usr/bin/env python2|' \
      -i ${pkgdir}/usr/lib/python2.7/site-packages/docutils/error_reporting.py

  install -D -m644 COPYING.txt ${pkgdir}/usr/share/licenses/${pkgname}/COPYING.txt
  install -D -m644 licenses/python* ${pkgdir}/usr/share/licenses/${pkgname}/
}
