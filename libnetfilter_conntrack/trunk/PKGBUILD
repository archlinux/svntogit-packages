# Maintainer: SÃ©bastien "Seblu" Luttringer <seblu@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Alessandro Sagratini <ale_sagra@hotmail.com>
# Contributor: Kevin Edmonds <edmondskevin@hotmail.com>

pkgname=libnetfilter_conntrack
pkgver=1.0.7
pkgrel=1
pkgdesc='Library providing an API to the in-kernel connection tracking state table'
arch=('x86_64')
depends=('libnfnetlink' 'libmnl')
url='https://www.netfilter.org/projects/libnetfilter_conntrack/'
license=('GPL')
validpgpkeys=('C09DB2063F1D7034BA6152ADAB4655A126D292E4') # Netfilter Core Team
source=("https://www.netfilter.org/projects/$pkgname/files/$pkgname-$pkgver.tar.bz2"{,.sig})
md5sums=('013d182c2df716fcb5eb2a1fb7febd1f'
         'SKIP')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}


# vim:set ts=2 sw=2 et:
