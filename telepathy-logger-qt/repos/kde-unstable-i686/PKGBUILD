# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: George Brooke <george+arch.aur@george-brooke.co.uk>

pkgname=telepathy-logger-qt
pkgver=0.9.80
pkgrel=1
pkgdesc="Qt bindings for telepathy logger"
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/extragear/network/telepathy/telepathy-logger-qt'
license=('GPL')
depends=('telepathy-qt5' 'telepathy-logger')
makedepends=('extra-cmake-modules' 'doxygen' 'python2')
source=("http://download.kde.org/unstable/kde-telepathy/${pkgver}/src/${pkgname}-${pkgver}.tar.xz")
sha1sums=('fea7a78c1ac84eeaf7344fb125a33ee84d31090c')

build() {
  mkdir build 
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DPYTHON_EXECUTABLE=/usr/bin/python2
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
