# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: George Brooke <george+arch.aur@george-brooke.co.uk>

pkgname=telepathy-logger-qt
pkgver=0.8.0
pkgrel=1
pkgdesc="Qt bindings for telepathy logger"
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/extragear/network/telepathy/telepathy-logger-qt'
license=('GPL')
depends=('telepathy-qt' 'telepathy-logger' 'qt-gstreamer')
makedepends=('automoc4' 'cmake' 'python2' 'doxygen' 'boost')
source=("http://download.kde.org/stable/${pkgname}/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2")
sha1sums=('a61650a06e5b8f6d452d6120ee402567439ef687')

build() {
  mkdir build 
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DPYTHON_EXECUTABLE=/usr/bin/python2
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
