# $Id$
# Maintainer: Angel Velasquez <angvp@archlinux.org> 
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=sonata
pkgver=1.6.2.1
pkgrel=9
pkgdesc="Elegant GTK+ music client for MPD"
arch=('i686' 'x86_64')
url="http://sonata.berlios.de/"
license=('GPL3')
depends=('pygtk' 'python2-mpd')
optdepends=('gnome-python-extras: Enhanced system tray support'
            'python2-tagpy: Metadata editing support'
            'python2-dbus: Various extra functionality (e.g. multimedia keys support)')
source=(http://download.berlios.de/${pkgname}/${pkgname}-$pkgver.tar.gz
        info.py.patch)
md5sums=('0b912325e7175abad3bf6c0edc071e05'
         '595fcf7615035829b264afecb5a1beeb')

build() {
  cd ${pkgname}-$pkgver
  patch -Np0 -i "$srcdir/info.py.patch"
  sed -i 's|/usr/bin/env python|/usr/bin/env python2|' sonata/breadcrumbs.py
}

package() {
  cd ${pkgname}-$pkgver
  python2 setup.py install --prefix=/usr --optimize 1 --root="$pkgdir"
}
