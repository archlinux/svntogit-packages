# $Id$
# Contributor: Eric Belanger <eric@archlinux.org>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=eric-plugins
pkgver=4.4.4a
pkgrel=3
pkgdesc="Plugins for the eric Python IDE"
arch=('any')
url="http://eric-ide.python-projects.org/index.html"
license=('GPL2')
depends=('eric')
makedepends=('unzip')
optdepends=('cx_freeze: for using CxFreeze plugin' 'pylint: for using PyLint plugin')
source=(http://downloads.sourceforge.net/eric-ide/Plugin_Checker_PyLint-4.2.1.tar.gz
	http://downloads.sourceforge.net/eric-ide/Plugin_Packager_CxFreeze-4.3.0.tar.gz
	http://downloads.sourceforge.net/eric-ide/Plugin_Refactoring_Rope-1.1.5.tar.gz
	http://downloads.sourceforge.net/eric-ide/Plugin_Tools_CharTables-4.1.4.tar.gz
	http://downloads.sourceforge.net/eric-ide/Plugin_Vcs_CVS-4.1.12.tar.gz
	http://downloads.sourceforge.net/eric-ide/Plugin_Project_Django-2.1.2.tar.gz)
md5sums=('91df79eb6f6d117f64be4d441dd54fa0'
         'f1404f63f300f72352545c99d95d0b06'
         '8c7bab8b84713c9a5cc20a43991554c3'
         'ee120a9950e86bb9e225a2d6d65a355f'
         '119cfb72a58c48272410cb20436e0e71'
         'ac12bd9c175aaa4e77cd71c214a66233')

package() {
	cd "${srcdir}"

	install -d  "${pkgdir}/usr/lib/python2.7/site-packages/eric4plugins"
	unzip -o Plugin_Checker_PyLint-4.2.1/*.zip \
	   -d "${pkgdir}/usr/lib/python2.7/site-packages/eric4plugins"
	unzip -o Plugin_Packager_CxFreeze-4.3.0/*.zip \
	   -d "${pkgdir}/usr/lib/python2.7/site-packages/eric4plugins"
	unzip -o Plugin_Refactoring_Rope-1.1.5/*.zip \
	   -d "${pkgdir}/usr/lib/python2.7/site-packages/eric4plugins"
	unzip -o Plugin_Tools_CharTables-4.1.4/*.zip \
	   -d "${pkgdir}/usr/lib/python2.7/site-packages/eric4plugins"
	unzip -o Plugin_Vcs_CVS-4.1.12/*.zip \
	   -d "${pkgdir}/usr/lib/python2.7/site-packages/eric4plugins"
	unzip -o Plugin_Project_Django-2.1.2/*.zip \
	   -d "${pkgdir}/usr/lib/python2.7/site-packages/eric4plugins"

	rm -r "${pkgdir}"/usr/lib/python2.7/site-packages/eric4plugins/{VERSION,*/Documentation}
	find "${pkgdir}" -type d -exec chmod 755 {} \;
	find "${pkgdir}" -type f -exec chmod 644 {} \;
}
