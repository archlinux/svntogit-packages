# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=akonadi
pkgver=18.12.3
pkgrel=3
pkgdesc="PIM layer, which provides an asynchronous API to access all kind of PIM data"
arch=(x86_64)
url='https://kontact.kde.org'
license=(LGPL)
depends=(kio kitemmodels mariadb hicolor-icon-theme)
makedepends=(extra-cmake-modules postgresql qt5-tools boost kdesignerplugin)
optdepends=('postgresql: PostgreSQL backend')
conflicts=(akonadi-client)
provides=(akonadi-client)
replaces=(akonadi-client)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
         akonadi-collectionattributetest.patch::https://cgit.kde.org/akonadi.git/patch/?id=3a062e6a
         akonadi-exceptions.patch::https://cgit.kde.org/akonadi.git/patch/?id=06f1a32b)
sha256sums=('f930deaade1cac1488b8af05cc28f4a78a441c34dbe875b673af3423f8a14658'
            'SKIP'
            '9ed125d59ea13a53df1077867f3c726882f67b5f462fffddd28d84158245cf28'
            'a273374a6f5b6c53f28b5e3429cd3f6906c72ce78a20592855e4119ccb70db6a')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -Rp1 -i ../akonadi-collectionattributetest.patch # Revert commit that causes hangs
  patch -p1 -i ../akonadi-exceptions.patch # Fix unhandled exceptions
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
