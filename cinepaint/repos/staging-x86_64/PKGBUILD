# $Id$
# Maintainer: tobias [tobias.archlinux.org]
# Contributor: tobias [tobias.justdreams.de]

pkgname=cinepaint
pkgver=1.3
pkgrel=5
pkgdesc="Sophisticated graphics manipulation programm supporting > 8bit pictures"
arch=('i686' 'x86_64')
license=('LGPL' 'GPL' 'MIT')
url="http://www.cinepaint.org"
depends=('gtk2' 'openexr' 'lcms' 'libxpm' 'fltk' 'ftgl' 'libxxf86vm')
makedepends=('python2' 'gutenprint>=5.2.9')
optdepends=('python2: for python plug-ins' 
            'gutenprint: for print plug-ins'
            'ghostscript: for pdf plug-ins')
options=('!libtool')
install=cinepaint.install
source=(http://downloads.sourceforge.net/project/cinepaint/CinePaint/CinePaint-${pkgver}/${pkgname}-${pkgver}.tgz
        LICENSE cinepaint-libpng15.patch cinepaint-missing-include.patch)
sha1sums=('1982e32b0f461f4fd0ff0fe17a2e81ccd8dbd52b'
          'ca8411db87f3fc51bbdfade22f4e1f555b2ee19e'
          '3dfc9f06f02791aee59b60d33ddf9d305b0f1de5'
          'a4f43aabdbb33d059f1a38442cd6625e4f00c5b9')

build() {
  cd "${srcdir}/${pkgname}"
  patch -p1 -i ../cinepaint-libpng15.patch
  patch -p1 -i ../cinepaint-missing-include.patch
  find plug-ins/pygimp -type f -exec sed -i 's#env python#env python2#' {} +
  sed -i 's|$(LDFLAGS) -o|$(LDFLAGS) $(GTK_LIBS) -o|' lib/Makefile.in

#  aclocal -I aclocal
#  autoconf
  sh autogen.sh
  
  LIBS+="-lstdc++ -lm -lX11" ./configure --prefix=/usr --libdir=/usr/lib --mandir=/usr/share/man \
    --enable-gtk2 --enable-pygimp --with-python=/usr/bin/python2
  sed -i 's/-Wl,,/-Wl,/' lib/fl_i18n/Makefile plug-ins/bracketing_to_hdr/Makefile \
    plug-ins/collect/Makefile plug-ins/icc_examin/icc_examin/Makefile plug-ins/pdf/Makefile
  make
}

package() {
  cd "${srcdir}/${pkgname}"
  make DESTDIR="${pkgdir}" install
  sed -i -e "s|-I$srcdir/cinepaint||" -e "s|-I$srcdir/cinepaint/lib||" -e "/libcinepaint.la/d" \
    -e "s|$srcdir/cinepaint/lib/.libs/\$dlname||" "${pkgdir}/usr/bin/cinepainttool"
  sed -i "s/cinepaint.png/cinepaint/" "${pkgdir}/usr/share/applications/cinepaint.desktop"
  install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
