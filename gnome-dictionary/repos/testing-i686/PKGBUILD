# $Id$
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=gnome-dictionary
pkgver=3.26.1
pkgrel=1
pkgdesc="Look up words in dictionary sources"
url="https://wiki.gnome.org/Apps/Dictionary"
arch=(i686 x86_64)
license=(GPL2)
depends=(dconf gtk3)
makedepends=(itstool docbook-xsl gtk-doc yelp-tools git gobject-introspection meson appstream-glib)
groups=(gnome)
_commit=2b351e832e28782b465176b055ba38fe007df40d  # tags/3.26.1^0
source=("git+https://git.gnome.org/browse/gnome-dictionary#commit=$_commit")
sha256sums=('SKIP')

check() {
  cd build
  meson test
}

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  mkdir build
  cd $pkgname
}

build() {
  cd build
  arch-meson ../$pkgname
  ninja
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}

# vim:set ts=2 sw=2 et:
