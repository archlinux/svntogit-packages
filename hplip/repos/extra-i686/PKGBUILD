# $Id$
# Maintainer: Tom Gundersen <teg@jklm.no>
# Contributor : RÃ©my Oudompheng <remy@archlinux.org>
# Contributor: Morgan LEFIEUX <comete@archlinuxfr.org>

pkgname=hplip
pkgver=3.12.10
pkgrel=1
pkgdesc="Drivers for HP DeskJet, OfficeJet, Photosmart, Business Inkjet and some LaserJet"
arch=('i686' 'x86_64')
url="http://hplipopensource.com"
license=('GPL')
depends=('python2' 'ghostscript>=8.64-6' 'foomatic-db' 'foomatic-db-engine'
	 'net-snmp>=5.7.1' 'wget')
makedepends=('python2-pyqt' 'python2-gobject2' 'sane' 'rpcbind' 'cups' 'libusbx')
optdepends=('cups: for printing support'
            'python2-dbus: for dbus support'
            'sane: for scanner support'
            'python-imaging: for commandline scanning support'
            'python-notify: for Desktop notification support'
            'rpcbind: for network support'
            'python2-pyqt: for running hp-toolbox'
            'python2-gobject2: for running hp-toolbox'
            'hplip-plugin: binary blobs for specific devices'
            'libusbx: for advanced usb support')
options=('!libtool')
install=hplip.install
source=(http://downloads.sourceforge.net/${pkgname}/$pkgname-$pkgver.tar.gz{,.asc}
        cups-1.6-buildfix.diff)
md5sums=('38136d7f005dd222cb35134ec586dc8b'
         'd4bfa67fbe34f71d859ad6fe10dd5861'
         'ef64578c6cb3ce252dbdcb0f1c2f4a47')

build() {
 cd "$srcdir/$pkgname-$pkgver"

 # fix build with cups 1.6 - FC hplip-ipp-accessors.patch
 patch -Np1 -i ${srcdir}/cups-1.6-buildfix.diff
 
 export PYTHON=python2
 
 find . -type f -exec sed -i 's~^#.*env python~#!/usr/bin/env python2~' {} +
 sed -i '1s|#!/usr/bin/python|#!/usr/bin/python2|' setup.py makeuri.py logcapture.py base/magic.py
 sed -i '1s|#!/usr/bin/python|#!/usr/bin/python2|' ui/upgradeform.py uninstall.py upgrade.py config_usb_printer.py
 sed -i 's|python ./print.py|python2 ./print.py|' scan.py
 sed -i 's|python ./testpage.py|python2 ./testpage.py|' setup.py
 sed -i 's|python ./setup.py|python2 ./setup.py|' ui4/devmgr5.py ui4/nodevicesdialog.py 
 sed -i 's|python %HOME%|python2 %HOME%|' base/utils.py
 sed -i 's|python ./plugin.py|python2 ./plugin.py|' base/pkit.py
 

 # https://bugs.archlinux.org/task/30085 - hack found in Gentoo
 # Use system foomatic-rip for hpijs driver instead of foomatic-rip-hplip
 # The hpcups driver does not use foomatic-rip
	local i
	for i in ppd/hpijs/*.ppd.gz ; do
		rm -f ${i}.temp
		gunzip -c ${i} | sed 's/foomatic-rip-hplip/foomatic-rip/g' | \
			gzip > ${i}.temp || return 1
		mv ${i}.temp ${i}
	done
   
 export AUTOMAKE='automake --foreign'
 autoreconf --force --install

 ./configure --prefix=/usr \
             --enable-qt4 \
             --disable-foomatic-rip-hplip-install \
             --enable-foomatic-ppd-install \
             --enable-hpcups-install \
             --enable-new-hpcups \
             --enable-cups-ppd-install \
             --enable-cups-drv-install \
             --enable-hpijs-install \
             --enable-foomatic-drv-install \
             --enable-pp-build \
             --enable-udev-acl-rules #--help

 make
}

package() {
 cd "$srcdir/$pkgname-$pkgver"
 make rulesdir=/lib/udev/rules.d DESTDIR="$pkgdir/" install
 # remove config provided by sane and autostart of hp-daemon
 rm -rf "$pkgdir"/etc/{sane.d,xdg}
 # remove HAL .fdi file because HAL is no longer used
 rm -rf "$pkgdir"/usr/share/hal
 # move to /usr
 mv "$pkgdir"/lib/udev "$pkgdir"/usr/lib/
 rm "$pkgdir"/lib -r
}
