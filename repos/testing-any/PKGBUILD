# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-service-identity
pkgver=21.1.0
pkgrel=6
pkgdesc="Service identity verification for pyOpenSSL"
arch=('any')
license=('MIT')
url="https://pypi.python.org/pypi/service_identity"
depends=('python-attrs' 'python-pyasn1-modules' 'python-pyasn1' 'python-cryptography' 'python-six')
makedepends=('python-setuptools' 'python-idna')
checkdepends=('python-pytest-runner')
optdepends=('python-idna: for Internationalized Domain Names support')
replaces=('python-service_identity')
source=("https://github.com/pyca/service-identity/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('6375bc5e8329118b2beb163ffffecc1479839e4e98173576e4737deebcc4c057ac357315976d3f53a280defd4e2c1f6b74e9604ec1aa94144240c2fb4bf07461')

build() {
  cd service-identity-$pkgver
  python setup.py build
}

check() {
  cd service-identity-$pkgver
  python setup.py pytest
}
 
package() {
  cd service-identity-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
