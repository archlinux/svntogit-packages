# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>

pkgname=autoconf
pkgver=2.66
pkgrel=2
pkgdesc="A GNU tool for automatically configuring source code"
arch=('any')
license=('GPL2' 'GPL3' 'custom')
url="http://www.gnu.org/software/autoconf"
groups=('base-devel')
depends=('awk' 'm4' 'diffutils' 'bash')
install=autoconf.install
source=(ftp://ftp.gnu.org/pub/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz
        autoconf-2.66-sizeof.patch
        autoconf-2.66-subdir.patch)
md5sums=('fb00d77cbc468994c55919298af1ff02'
         '0a4a03f350041cef5134faea2fa7f2e0'
         '423e0f2c83779dd383540b1efc873b35')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  
  patch -Np1 -i $srcdir/autoconf-2.66-sizeof.patch
  patch -Np1 -i $srcdir/autoconf-2.66-subdir.patch
  
  ./configure --prefix=/usr
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  # license exception
  install -Dm644 COPYING.EXCEPTION \
    $pkgdir/usr/share/licenses/autoconf/COPYING.EXCEPTION

  # conflict with bintuils
  rm -f ${pkgdir}/usr/share/info/standards.info
}
