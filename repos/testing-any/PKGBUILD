# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>

pkgname=autoconf
pkgver=2.69
pkgrel=2
pkgdesc="A GNU tool for automatically configuring source code"
arch=('any')
license=('GPL2' 'GPL3' 'custom')
url="http://www.gnu.org/software/autoconf"
groups=('base-devel')
depends=('awk' 'm4' 'diffutils' 'bash')
checkdepends=('gcc-fortran')
install=autoconf.install
source=(ftp://ftp.gnu.org/pub/gnu/${pkgname}/${pkgname}-${pkgver}.tar.xz{,.sig})
md5sums=('50f97f4159805e374639a73e2636f22e'
         '2941e2fe687e05b3431eac6b714053bc')
validpgpkeys=('71C2CC22B1C4602927D2F3AAA7A16B4A2527436A')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  # license exception
  install -Dm644 COPYING.EXCEPTION \
    $pkgdir/usr/share/licenses/autoconf/COPYING.EXCEPTION

  # remove unwanted file
  rm -f ${pkgdir}/usr/share/info/standards.info
}
