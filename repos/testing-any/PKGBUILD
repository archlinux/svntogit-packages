# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=alsa-topology-conf
pkgver=1.2.5
pkgrel=1
pkgdesc="ALSA topology configuration files"
url="https://alsa-project.org/"
arch=('any')
license=('BSD')
source=("https://www.alsa-project.org/files/pub/lib/${pkgname}-${pkgver}.tar.bz2"{,.sig})
sha512sums=('2eb4d8baf2dcbf0b631dd11dbf15bffc51694d9cc6931619e51787f3ba58d1a091d266e6721a3b737c040ec74a28270b93f39fb97f30a3227cf340dd646e5d51'
            'SKIP')
b2sums=('0870aa5db05d7b82e5312ac5bd3c2d887f0cafc25a317cbc416ce0ac6982ce296620833c4459ccb357b4b014d300329fc3d0028d3ec2024495cc21f9cc122e1f'
        'SKIP')
validpgpkeys=('F04DF50737AC1A884C4B3D718380596DA6E59C91') # ALSA Release Team (Package Signing Key v1) <release@alsa-project.org>

package() {
  cd "${pkgname}-"*/
  for dir in topology/*; do
    install -vDm 644 "${dir}"/*.conf -t "$pkgdir/usr/share/alsa/topology/$(basename $dir)"
  done
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
}
