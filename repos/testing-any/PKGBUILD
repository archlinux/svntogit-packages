# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=xcb-proto
pkgver=1.12
pkgrel=2
pkgdesc="XML-XCB protocol descriptions"
arch=(any)
url="http://xcb.freedesktop.org/"
license=('custom')
makedepends=('python' 'libxml2')
source=($url/dist/$pkgname-$pkgver.tar.bz2
        python-35x-fixes.patch)
sha256sums=('5922aba4c664ab7899a29d92ea91a87aa4c1fc7eb5ee550325c3216c480a4906'
            '1802aec973b6bf5836c1b2d65aaf5fb18051323f47826df6a95cf6621cb3282a')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../python-35x-fixes.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

check() {
  cd $pkgname-$pkgver 
  make -k check || /bin/true
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -m755 -d "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname"
}
