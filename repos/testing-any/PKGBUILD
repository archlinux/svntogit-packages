# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=orca
pkgver=3.2.2
pkgrel=1
pkgdesc="Screen reader for individuals who are blind or visually impaired"
arch=(any)
license=('LGPL')
url="http://www.gnome.org/projects/orca"
install=orca.install
options=('!libtool')
depends=('gtk3' 'python2-gobject' 'at-spi2-atk' 'dbus-python' 'pyatspi' 'pyxdg' 'speech-dispatcher' 'liblouis' 'brltty' 'xorg-xmodmap' 'hicolor-icon-theme')
makedepends=('intltool' 'gnome-doc-utils')
groups=('gnome-extra')
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('aa0099ad2b1ef49bca502dcbbfd5ed47bb66fa4a6ffcbdca29606ddcc824dfd5')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
