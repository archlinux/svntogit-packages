# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Marcin "avalan" Falkiewicz <avalatron@gmail.com>
# Contributor: C Anthony Risinger <anthony@xtfx.me>

pkgbase=python-ply
pkgname=(python-ply python2-ply)
pkgver=3.4
pkgrel=4
pkgdesc='Implementation of lex and yacc parsing tools'
arch=('any')
url='http://www.dabeaz.com/ply/'
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools')
source=("${url}ply-$pkgver.tar.gz")
sha256sums=('af435f11b7bdd69da5ffbc3fecb8d70a7073ec952e101764c88720cdefb2546b')

prepare() {
  cp -r ${pkgbase#python-}-$pkgver{,-py2}
}

package_python-ply() {
  depends=('python')

  cd "${pkgbase#python-}-$pkgver"

  python setup.py install --root="$pkgdir"
  
  install -d "$pkgdir/usr/share/licenses/$pkgname"
  head -n30 README > "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-ply() {
  depends=('python2')

  cd "${pkgbase#python-}-$pkgver-py2"

  python2 setup.py install --root="$pkgdir"
  
  install -d "$pkgdir/usr/share/licenses/$pkgname"
  head -n30 README > "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
