# $Id$
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>

#
# Create a new tarball from the linux-firmware git tree with the following command:
# git clone git://git.kernel.org/pub/scm/linux/kernel/git/firmware/linux-firmware.git
# git archive --format=tar --prefix linux-firmware-$(date +%Y%m%d)/ master | xz -9 > linux-firmware-$(date +%Y%m%d).tar.xz
#

pkgname=linux-firmware
pkgver=20130528
pkgrel=1
pkgdesc="Firmware files for Linux"
arch=('any')
url="http://git.kernel.org/?p=linux/kernel/git/firmware/linux-firmware.git;a=summary"
license=('GPL2' 'GPL3' 'custom')
provides=('kernel26-firmware=2.6.34.99')
conflicts=('linux-firmware-git'
           'kernel26-firmware'
           'ar9170-fw'
           'iwlwifi-1000-ucode'
           'iwlwifi-3945-ucode'
           'iwlwifi-4965-ucode'
           'iwlwifi-5000-ucode'
           'iwlwifi-5150-ucode'
           'iwlwifi-6000-ucode'
           'rt2870usb-fw'
           'rt2x00-rt61-fw'
           'rt2x00-rt71w-fw')
replaces=('kernel26-firmware'
           'ar9170-fw'
           'iwlwifi-1000-ucode'
           'iwlwifi-3945-ucode'
           'iwlwifi-4965-ucode'
           'iwlwifi-5000-ucode'
           'iwlwifi-5150-ucode'
           'iwlwifi-6000-ucode'
           'rt2870usb-fw'
           'rt2x00-rt61-fw'
           'rt2x00-rt71w-fw')
options=(!strip)
source=(ftp://ftp.archlinux.org/other/linux-firmware/$pkgname-$pkgver.tar.xz)

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # remove not needed Makefile #35236
  rm Makefile
  install -d -m755 "${pkgdir}/usr/lib/firmware"
  cp -a * "${pkgdir}/usr/lib/firmware/"
  install -d -m755 "${pkgdir}/usr/share/licenses/linux-firmware/"
  rm -f "${pkgdir}/usr/lib/firmware/"{configure,GPL*}
  mv "${pkgdir}/usr/lib/firmware"/{LICEN*,WHENCE} "${pkgdir}/usr/share/licenses/linux-firmware/"
}
# vim:set ts=2 sw=2 et:
md5sums=('8dc5aea5b03dff8595f096f9e368e888')
md5sums=('f1e8f2a874599752443427e052fe76ab')
