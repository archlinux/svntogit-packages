# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Lukas Sabota <punkrockguy318@comcast.net>
# Contributor: Brice Carpentier <brice@dlfp.org>

pkgname=scons
pkgver=3.1.1
pkgrel=5
pkgdesc="Extensible Python-based build utility"
arch=('any')
url="https://scons.org"
license=('MIT')
depends=('python')
makedepends=('docbook2x' 'epydoc' 'fop' 'texlive-bin' 'graphviz' 'ghostscript'
'lynx' 'python-lxml' 'python-setuptools' 'texlive-latexextra')
# potential additions include ipkg, rpm
checkdepends=('clang' 'gdc' 'dmd' 'ldc' 'lib32-glibc' 'nasm' 'python-pytest'
'python-virtualenv' 'swig' 'zip')
source=("$pkgname-$pkgver.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('3347a7ec5c6c7b596c0d51bb4a9654b971734736febe3834df0f40e7839e1f0578dafeae838d5ddd9f3319ac3d17b6790666fa1602c30480eba25122355179a5')

prepare() {
  cd "${pkgname}-${pkgver}"
  # emulate a release
  local _copyright='Copyright (c) 2001 - 2019 The SCons Foundation'
  local _date_of_release="$(grep "RELEASE ${pkgver}" src/CHANGES.txt | cut -d ',' -f2)"
  local _date="$(date -d "${_date_of_release}" +'%Y-%m-%d %H:%M:%S')"
  # change globals in all scripts
  for _script in $(find src/ -type f -iname "*.py"); do
    sed -e "s|__COPYRIGHT__|${_copyright}|g" \
        -e "s|__FILE__|/src/script/${_script}|g" \
        -e 's/__REVISION__/none/g' \
        -e "s|__DATE__|${_date}|g" \
        -e 's/__BUILDSYS__/none/g' \
        -e 's/__DEVELOPER__/none/g' \
        -e "s/__VERSION__/${pkgver}/g" \
        -i "${_script}"
  done
  # copy scripts to correct locations
  for _script in scons{,ign,-time,-configure-cache}; do
    cp -v "src/script/${_script}.py" "src/script/${_script}"
  done
  # disabling postscript creation, because it's broken
  sed -e '614,619d' \
      -e '/api_ps =/d' \
      -e 's/api_ps,//' \
      -i doc/SConscript
  # fixing refentrytitle, so man pages are rendered with correct file names
  sed -e 's/refentrytitle>SCONS-TIME/refentrytitle>scons-time/' \
      -e 's/refentrytitle>SCONSIGN/refentrytitle>sconsign/' \
      -e 's/refentrytitle>SCONS/refentrytitle>scons/' \
      -i doc/man/*.xml
}

build() {
  cd "${pkgname}-${pkgver}"
  # build documentation
  (
    python bootstrap.py SConstruct doc
    cd src
    for _xml in {scons,sconsign,scons-time}; do
      docbook2man "../build/doc/man/${_xml}_db.xml"
    done
  )
  (
    cd src
    python setup.py build
  )
}

check() {
  cd "${pkgname}-${pkgver}"
  python runtest.py -a -t || msg "Tests passing with 'NO RESULT' count as failed."
}

package() {
  local py_ver=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')
  cd "${pkgname}-${pkgver}/src"
  python setup.py install --prefix=/usr \
                          --skip-build \
                          --optimize=1 \
                          --standard-lib \
                          --install-data=/usr/share \
                          --root="$pkgdir"
  # strip $pkgdir from embedded paths:
  python -m compileall -d "/usr/lib/python${py_ver}" \
    "${pkgdir}/usr/lib/python${py_ver}"
  python -O -m compileall -d "/usr/lib/python${py_ver}" \
    "${pkgdir}/usr/lib/python${py_ver}"
  install -vDm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -vDm 644 {CHANGES,README,RELEASE}.txt \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
  # removing Windows only script
  rm -vf "${pkgdir}/usr/bin/scons"*.bat
}
