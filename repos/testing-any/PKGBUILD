# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-date-manip
pkgver=6.59
pkgrel=2
pkgdesc="Date::Manip - date manipulation routines"
arch=('any')
license=('PerlArtistic')
url="http://search.cpan.org/dist/Date-Manip/"
depends=('perl')
checkdepends=('perl-test-inter')
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/S/SB/SBECK/Date-Manip-${pkgver}.tar.gz")
sha512sums=('ec1217cdedf4901d4b1165f1d4e1213c66a49144f7d06fba697d1216cd9e59a900ef441b9fab98990b0ef9e54a88ac27e27c336dfafcc19a1e9343c9a1e7c801')

build() {
  cd Date-Manip-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Date-Manip-${pkgver}
  make test
}

package() {
  cd Date-Manip-${pkgver}
  make DESTDIR="${pkgdir}" install
}
