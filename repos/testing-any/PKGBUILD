# Contributor: Firmicus <firmicus āt gmx dōt net>
# Maintainer: Rémy Oudompheng <remy@archlinux.org>

pkgname=texlive-formatsextra
pkgver=2022.62529
_revnr=${pkgver#2022.}
pkgrel=1
pkgdesc="TeX Live - collection of extra TeX 'formats'"
license=('GPL')
arch=(any)
depends=('texlive-core')
groups=('texlive-most')
url='http://tug.org/texlive/'
source=("https://sources.archlinux.org/other/texlive/$pkgname-$pkgver-src.zip"
        "${pkgname}.maps"
        "${pkgname}.fmts")
options=('!emptydirs')
provides=('texlive-htmlxml')
conflicts=('texlive-htmlxml')
replaces=('texlive-htmlxml')
sha256sums=('fbd5206b4e2498414c1e90478cd7898c2ee4cccda20c269cd46e4e2f45ee35f6'
            'f969b60fed3a4cbd3fedc9e88e9385ef418f81bf69f958e8998a1898069fd50e'
            '9a3fbec7f166156ec1483de769a8acff5c9146c699d74eec3f6bba5a0c4ffda5')

build() {
   cd "$srcdir"
   for p in *.tar.xz; do
       case $p in
           aleph.*|lambda.*|omega.*)
               ;;
           *)
               bsdtar -xf $p
               ;;
       esac
   done
   rm -rf {tlpkg,doc,source} || true
}

package() {
   cd "$srcdir"
   install -m755 -d "$pkgdir"/var/lib/texmf/arch/installedpkgs
   sed -i '/^#/d' CONTENTS
   install -m644 CONTENTS "$pkgdir"/var/lib/texmf/arch/installedpkgs/${pkgname}_${_revnr}.pkgs
   install -m644 "${pkgname}.fmts" "$pkgdir"/var/lib/texmf/arch/installedpkgs/
   install -m755 -d "$pkgdir"/usr/share
   wanteddirs=$(for d in *; do test -d $d && [[ $d != texmf* ]] && echo $d; done) || true
   for dir in $wanteddirs; do
     find $dir -type d -exec install -d -m755 "$pkgdir"/usr/share/texmf-dist/'{}' \;
     find $dir -type f -exec install -m644 '{}' "$pkgdir"/usr/share/texmf-dist/'{}' \;
   done
   if [[ -d texmf-dist ]]; then
     find texmf-dist -type d -exec install -d -m755 "$pkgdir"/usr/share/'{}' \;
     find texmf-dist -type f -exec install -m644 '{}' "$pkgdir"/usr/share/'{}' \;
   fi
   if [[ -d "$pkgdir"/usr/share/texmf-dist/scripts ]]; then
     find "$pkgdir"/usr/share/texmf-dist/scripts -type f -exec chmod a+x '{}' \;
   fi
}
