# $Id$
# Maintainer: Alexander Baldeck <alexander@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=xcb-proto
pkgver=1.8
pkgrel=1
pkgdesc="XML-XCB protocol descriptions"
arch=(any)
url="http://xcb.freedesktop.org/"
license=('custom')
makedepends=('python2' 'libxml2')
source=($url/dist/$pkgname-$pkgver.tar.bz2)
sha1sums=('a8e71b9a09066870ff12049f45e8bccaaf83fc31')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -m755 -d "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname"
}
