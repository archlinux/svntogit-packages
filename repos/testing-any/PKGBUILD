# Maintainer: Firmicus <firmicus āt gmx dōt net>

pkgname=texlive-latexextra
pkgver=2010.19086
pkgrel=1
pkgdesc="TeX Live - Large collection of add-on packages for LaTeX"
license=('GPL')
arch=(any)
depends=('texlive-core')
optdepends=('texlive-pictures')
groups=('texlive-most')
url='http://tug.org/texlive/'
source=("ftp://ftp.archlinux.org/other/texlive/$pkgname-$pkgver-src.zip" "$pkgname.maps")
options=('!emptydirs')
options=(!strip)
install=texlive.install
md5sums=('e15eb1f93ccecf366e159c7a7f0297a7'
         '9e4825f47dac663dd62c4bfa67899e84')

build() {
   for p in *.tar.xz; do
	   bsdtar -xf $p || return 1
   done
   rm -rf {tlpkg,doc,source} || true
}
package() {
   install -m755 -d $pkgdir/var/lib/texmf/arch/installedpkgs || return 1
   sed -i '/^#/d' CONTENTS || return 1
   _revnr=`echo $pkgver | sed 's/2010\.//'`
   install -m644 CONTENTS $pkgdir/var/lib/texmf/arch/installedpkgs/${pkgname}_${_revnr}.pkgs || return 1
   install -m644 $pkgname.maps $pkgdir/var/lib/texmf/arch/installedpkgs/ || return 1
   install -m755 -d $pkgdir/usr/share || return 1
   wanteddirs=$(for d in *; do test -d $d && [[ $d != texmf* ]] && echo $d; done) || true
   for dir in $wanteddirs; do
     find $dir -type d -exec install -d -m755 $pkgdir/usr/share/texmf-dist/'{}' \; || return 1
     find $dir -type f -exec install -m644 '{}' $pkgdir/usr/share/texmf-dist/'{}' \; || return 1
   done
   if [[ -d texmf-dist ]]; then
     find texmf-dist -type d -exec install -d -m755 $pkgdir/usr/share/'{}' \; || return 1
     find texmf-dist -type f -exec install -m644 '{}' $pkgdir/usr/share/'{}' \; || return 1
   fi
   if [[ -d $pkgdir/usr/share/texmf-dist/scripts ]]; then
     find $pkgdir/usr/share/texmf-dist/scripts -type f -exec chmod a+x '{}' \;
   fi
   #add symlinks that were in texlive-bin:
   install -m755 -d $pkgdir/usr/bin || return 1
   ln -s /usr/share/texmf-dist/scripts/authorindex/authorindex $pkgdir/usr/bin/authorindex
   ln -s /usr/share/texmf-dist/scripts/glossaries/makeglossaries $pkgdir/usr/bin/makeglossaries
   ln -s /usr/share/texmf-dist/scripts/pax/pdfannotextractor.pl $pkgdir/usr/bin/pdfannotextractor
   ln -s /usr/share/texmf-dist/scripts/ppower4/pdfthumb.tlu $pkgdir/usr/bin/pdfthumb
   ln -s /usr/share/texmf-dist/scripts/perltex/perltex.pl $pkgdir/usr/bin/perltex
   #ln -s /usr/share/texmf-dist/scripts/ppower4/ppower4.tlu $pkgdir/usr/bin/ppower4
   ln -s /usr/share/texmf-dist/scripts/pst-pdf/ps4pdf $pkgdir/usr/bin/ps4pdf
   ln -s /usr/share/texmf-dist/scripts/splitindex/perl/splitindex.pl $pkgdir/usr/bin/splitindex
   ln -s /usr/share/texmf-dist/scripts/svn-multi/svn-multi.pl $pkgdir/usr/bin/svn-multi
   ln -s /usr/share/texmf-dist/scripts/vpe/vpe.pl $pkgdir/usr/bin/vpe
   # but ppower4 was a symlink to ppower4.tlu, which was broken, but this is far better:
   (
   cat <<"END"
#!/bin/bash
java -jar /usr/share/texmf-dist/scripts/ppower4/pp4p.jar $@
END
   ) > /tmp/ppower4 
   install -m755 /tmp/ppower4 $pkgdir/usr/bin/
   rm $pkgdir/usr/share/texmf-dist/scripts/glossaries/makeglossaries.bat
   rm $pkgdir/usr/share/texmf-dist/scripts/pst-pdf/ps4pdf.bat*
   rm $pkgdir/usr/share/texmf-dist/scripts/shipunov/biokey2html.bat
}
