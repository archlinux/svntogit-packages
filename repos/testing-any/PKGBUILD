# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Lukas Sabota <punkrockguy318@comcast.net>
# Contributor: Brice Carpentier <brice@dlfp.org>

pkgname=scons
pkgver=4.1.0
pkgrel=2
pkgdesc="Extensible Python-based build utility"
arch=('any')
url="https://scons.org"
license=('MIT')
depends=('python')
makedepends=('python-setuptools' 'python-sphinx' 'python-sphinx_rtd_theme'
'rst2pdf')
checkdepends=('python-pytest')
source=("$pkgname-$pkgver.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('f79b86bb09783767b3872cfb8efb665372714a604af2aaf3adc66eee63d3afe27bc6b2aab83813743c83f71c81c800d42842e916501787ba402ce2726dda9b44')
b2sums=('ede3d47a1ec652b9248681e26738073e9d01daab7ffeccc58f0fcadb79661e30edb9a35a511518e4a58459243acb027afd8fa5849b0f9e4bae451055f7b1a062')

prepare() {
  cd "${pkgname}-${pkgver}"
  # fix wrong build target
  sed -e 's/bdist_wheel/bdist/g' -i SConstruct
}

build() {
  cd "${pkgname}-${pkgver}"
  python scripts/scons.py --include-dir= -j1
}

check() {
  cd "${pkgname}-${pkgver}"
  python runtest.py --all --unit-only
}

package() {
  cd "${pkgname}-${pkgver}"
  export PYTHONHASHSEED=0
  python setup.py install --prefix=/usr \
                          --skip-build \
                          --optimize=1 \
                          --install-data=/usr/share \
                          --root="$pkgdir"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -vDm 644 {{CHANGES,RELEASE}.txt,README.rst} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"

  #move man pages to correct location from /usr/share/
  install -vdm 755 "${pkgdir}/usr/share/man/man1"
  mv -v "${pkgdir}/usr/share/"*.1 "${pkgdir}/usr/share/man/man1"
}
