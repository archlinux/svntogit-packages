# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnu-free-fonts
pkgver=20120503
pkgrel=5
pkgdesc="A free family of scalable outline fonts"
url="https://www.gnu.org/software/freefont/"
arch=(any)
license=(GPL3)
source=(https://ftp.gnu.org/gnu/freefont/freefont-otf-${pkgver}.tar.gz{,.sig}
        69-gnu-free-fonts.conf)
sha256sums=('3a6c51868c71b006c33c4bcde63d90927e6fcca8f51c965b8ad62d021614a860'
            'SKIP'
            '9bd436771b20e03d8ede3b2558dec63c5f48d2dfe797f9e7d7455188e850a415')
validpgpkeys=('A0156C139D2DAA3B352E42CD506361DBA36FDD52')

package() {
  install -d "$pkgdir/usr/share/fonts/${pkgname%-fonts}"
  install -t "$pkgdir/usr/share/fonts/${pkgname%-fonts}" -m644 freefont-$pkgver/*.otf
  install -Dm644 69-gnu-free-fonts.conf "$pkgdir/etc/fonts/conf.avail/69-gnu-free-fonts.conf"

  mkdir "$pkgdir/etc/fonts/conf.d"
  ln -srt "$pkgdir/etc/fonts/conf.d" "$pkgdir"/etc/fonts/conf.avail/*
}

# vim:set ts=2 sw=2 et:
