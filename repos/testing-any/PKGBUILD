# $Id$
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=mm-common
pkgver=0.9.10+2+g71a4333
pkgrel=4
pkgdesc="Common build files of the C++ bindings"
url="http://gtkmm.org"
arch=(any)
depends=(graphviz doxygen gtk-doc libsigc++-docs perl-xml-parser)
makedepends=(libsigc++ git)
license=(GPL2)
_commit=71a4333c8fbc85047a315dc126fe9e8e4619a6b6
source=("git://git.gnome.org/mm-common#commit=$_commit"
        http://gcc.gnu.org/onlinedocs/libstdc++/latest-doxygen/libstdc++.tag)
sha256sums=('SKIP'
            'fd3fed61b637e3e6e24794f8c40b22002f8ac154a8c5cc1f3a5b3ad7c8f4be4c')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  cp ../libstdc++.tag doctags
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --disable-network
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
