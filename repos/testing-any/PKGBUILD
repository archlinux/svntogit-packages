# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Kevin Piche <kevin@archlinux.org>
# Contributor: K. Piche <kpiche@rogers.com>

pkgname=perl-date-calc
_realname=Date-Calc
pkgver=6.4
pkgrel=8
pkgdesc="Perl module for Gregorian calendar date calculations"
arch=(any)
license=('PerlArtistic')
url="https://search.cpan.org/dist/Date-Calc/"
depends=('perl-bit-vector' 'perl')
options=(!emptydirs)
source=(https://search.cpan.org/CPAN/authors/id/S/ST/STBEY/Date-Calc-$pkgver.tar.gz)
md5sums=('69c635070d38c884682bfd4657031a8d')

build() {
  cd Date-Calc-$pkgver
  # install module in vendor directories.
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd Date-Calc-$pkgver
  make install DESTDIR="$pkgdir"
}
