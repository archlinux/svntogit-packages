# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=dconf
pkgver=0.11.7
pkgrel=1
pkgdesc="A low-level configuration system."
arch=(i686 x86_64)
url="http://live.gnome.org/dconf"
license=('LGPL2.1')
depends=('glib2' 'gtk-update-icon-cache')
makedepends=('vala' 'gtk3')
optdepends=('gtk3: for dconf-editor')
install=dconf.install
source=(http://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('ea0edb75157d8976c9256ee0d82faa37bf146c4d85c4c29ef0f8bcfbcdb35641')

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc \
    --libexecdir=/usr/lib/dconf
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
