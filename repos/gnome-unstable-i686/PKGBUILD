# $Id$
# Contributor: Roman Kyrylych <roman@archlinux.org>

pkgname=gnome-user-share
pkgver=3.18.3+2+g7b451ae
pkgrel=1
pkgdesc="Easy to use user-level file sharing for GNOME"
arch=(i686 x86_64)
url="https://git.gnome.org/browse/gnome-user-share"
license=(GPL)
depends=(mod_dnssd dconf gtk3 libnotify libcanberra)
makedepends=(intltool libnautilus-extension itstool docbook-xsl gnome-common git)
options=(!emptydirs)
groups=(gnome)
_commit=7b451ae27540ef05d116a6dd045cae8a956b1847  # master
source=("git://git.gnome.org/gnome-user-share#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^GNOME_USER_SHARE_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/$pkgname \
      --localstatedir=/var \
      --with-httpd=/usr/bin/httpd \
      --with-modules-path=/usr/lib/httpd/modules
  make
}

package() {
  cd $pkgname
  make DESTDIR="${pkgdir}" install
}
