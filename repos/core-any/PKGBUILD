# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Thomas Mudrunka <harvie@email.cz>
# Contributor: m4xm4n <max@maxfierke.com>

pkgname=dnssec-anchors
pkgver=20190629
pkgrel=1
pkgdesc='DNSSEC trust anchors for the root zone'
url='https://data.iana.org/root-anchors/'
license=('custom:none')
arch=('any')
makedepends=('unbound')
source=('LICENSE')
sha256sums=('dd37e92942d5a4024f1c77df49d61ca77fc6284691814903a741785df61f78cb')

prepare() {
	cd "${srcdir}"

	unbound-anchor -v -a root.key ||
	unbound-anchor -v -a root.key

	unbound-host -v -f root.key -t DNSKEY . |
	sed 's/ (secure)//;t;d' |
	sed 's/ has / IN /' |
	sed 's/ record / /' \
	> trusted-key.key
}

package() {
	cd "${srcdir}"
	install -Dm644 trusted-key.key "${pkgdir}"/etc/trusted-key.key
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
