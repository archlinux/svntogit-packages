# $Id$
# Maintainer: Firmicus <francois.archlinux.org>
# Contributor: bender02 at gmx dot com

pkgname=asymptote
pkgver=2.04
pkgrel=2
pkgdesc="A vector graphics language (like metapost)"
arch=('i686' 'x86_64')
url="http://asymptote.sourceforge.net/"
license=("GPL3")
depends=('texlive-core' 'gc' 'freeglut' 'gsl' 'fftw')
makedepends=('ghostscript' 'imagemagick')
optdepends=('python2:           for the xasy GUI'
            'python-imaging:    for the xasy GUI'
            'tix:               for the xasy GUI')
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.src.tgz)
install=texlive.install

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --enable-gc=/usr \
              --prefix=/usr \
              --with-latex=/usr/share/texmf/tex/latex \
              --with-context=/usr/share/texmf/tex/context || return 1
  make all || return 1
}
package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install-all || return 1
  sed -i -e 's@env python@env python2@' ${pkgdir}/usr/share/asymptote/GUI/*.py
  # this dir contains png files that are already embedded in the pdf documentation:
  rm -rf ${pkgdir}/usr/share/info/asymptote
}
md5sums=('6adb5d7714b662152b6f6f16c24f5751')
