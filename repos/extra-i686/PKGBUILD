# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=nvidia-lts
pkgver=290.10
_extramodules=extramodules-2.6.32-lts
_kernver="$(cat /lib/modules/${_extramodules}/version)"
pkgrel=1
pkgdesc="NVIDIA drivers for kernel26-lts"
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=('kernel26-lts>=2.6.32' 'kernel26-lts<2.6.33' "nvidia-utils=$pkgver")
makedepends=('kernel26-lts-headers>=2.6.32' 'kernel26-lts-headers<2.6.33')
provides=('nvidia')
license=('custom')
install=nvidia-lts.install
options=(!strip)
if [ "$CARCH" = "i686" ]; then
  _arch='x86'
  _pkg="NVIDIA-Linux-${_arch}-${pkgver}"
  source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
  md5sums=('50319a4b3818c12c9c7243525e0e6316')
elif [ "$CARCH" = "x86_64" ]; then
  _arch='x86_64'
  _pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
  source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
  md5sums=('cebfba9a7e91716a06c66bb5b38d9661')
fi

build() {
  cd "${srcdir}"
  sh ${_pkg}.run --extract-only
  cd "${_pkg}/kernel"
  make SYSSRC=/lib/modules/${_kernver}/build module
}

package() {
  install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" \
    "${pkgdir}/lib/modules/${_extramodules}/kernel/drivers/video/nvidia.ko"
  gzip "${pkgdir}/lib/modules/${_extramodules}/kernel/drivers/video/nvidia.ko"
  install -d -m755 "${pkgdir}/etc/modprobe.d"
  echo "blacklist nouveau" >> "${pkgdir}/etc/modprobe.d/nouveau_blacklist-lts.conf"
  install -D -m644 "${srcdir}/${_pkg}/LICENSE" "${pkgdir}/usr/share/licenses/nvidia-lts/LICENSE"
}
