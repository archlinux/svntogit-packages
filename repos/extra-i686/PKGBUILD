# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libglade
pkgver=2.6.4
pkgrel=5
pkgdesc="Allows you to load glade interface files in a program at runtime"
arch=(i686 x86_64)
license=('LGPL')
depends=('gtk2>=2.16.0' 'libxml2>=2.7.3')
makedepends=('python2' 'pkgconfig')
optdepends=('python2: libglade-convert script')
install=glade.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.6/${pkgname}-${pkgver}.tar.bz2
	libglade-2.0.1-nowarning.patch)
url="http://www.gnome.org"
md5sums=('d1776b40f4e166b5e9c107f1c8fe4139'
         '4f879c0ce550004905aa0fb24f25c353')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../libglade-2.0.1-nowarning.patch
}

build() {
  cd $pkgname-$pkgver
  PYTHON=/usr/bin/python2 ./configure --sysconfdir=/etc --prefix=/usr --localstatedir=/var
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="${pkgdir}" install
  install -m755 libglade-convert "${pkgdir}/usr/bin/"
}
