# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libdrm
pkgver=2.4.13
pkgrel=1
pkgdesc="Userspace interface to kernel DRM services"
arch=(i686 x86_64)
license=('custom')
depends=('glibc')
options=('!libtool')
url="http://dri.freedesktop.org/"
source=(http://dri.freedesktop.org/${pkgname}/${pkgname}-${pkgver}.tar.bz2
        no-pthread-stubs.patch
        intel-pciids.patch
        COPYING)
sha1sums=('0d3065d8521fca97239ce4aecbc81dc08d07de7d'
          'aa187b94c3ccc0f3b1139a719688b2e450343485'
          'acaaf6dba0cf4ac2b0c59142b11b34585414f680'
          'ba3dcd636997ee0d30df14b03dae05c24ae5d094')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/no-pthread-stubs.patch" || return 1
  patch -Np1 -i "${srcdir}/intel-pciids.patch" || return 1
  libtoolize --force || return 1
  aclocal || return 1
  autoconf || return 1
  automake || return 1
  ./configure --prefix=/usr \
      --enable-nouveau-experimental-api \
      --enable-radeon-experimental-api \
      --enable-udev || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 "${srcdir}/COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/" || return 1
}
