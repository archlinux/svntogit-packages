# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnutls
pkgver=2.12.4
pkgrel=1
pkgdesc="A library which provides a secure layer over a reliable transport layer"
arch=('i686' 'x86_64')
license=('GPL3' 'LGPL')
url="http://www.gnu.org/software/gnutls/"
install=gnutls.install
options=('!libtool' '!zipman')
depends=('gcc-libs>=4.4.3' 'libtasn1>=2.5' 'readline>=6.0' 'zlib' 'libgcrypt>=1.4.5')
source=(ftp://ftp.gnu.org/gnu/gnutls/${pkgname}-${pkgver}.tar.bz2)
md5sums=('3e17fc8cb1cf8272067f6b66baf02810')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # Nettle is the default crypto back end. Use --with-libgcrypt to use the libgcrypt back end.
  # we stay with libgcrypt for now. nettle would need to be moved from AUR to extra.
  ./configure --prefix=/usr \
	--with-zlib \
	--with-libgcrypt \
	--disable-static \
	--disable-guile
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check # passes all  || /bin/true
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  # lots of .png files are put into infodir and are gzipped by makepkg! this may need to be fixed by using !zipman
  # gzip -9 all files in infodir and manpages manually
  find "$pkgdir/usr/share/info" -name '*.info*' -exec gzip -9 {} \;
  find "$pkgdir/usr/share/man" -exec gzip -9 {} \;
}
