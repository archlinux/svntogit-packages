# $Id: PKGBUILD 165829 2012-08-31 06:08:20Z andyrtr $
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgbase=webkitgtk
pkgname=('webkitgtk2' 'webkitgtk3')
pkgver=1.10.1
pkgrel=2
arch=('i686' 'x86_64')
url="http://webkitgtk.org/"
license=('custom')
makedepends=('libxt' 'libxslt' 'sqlite' 'libsoup' 'enchant' 'libgl' 'geoclue' 'gtk2' 'gtk3' 'gst-plugins-base-libs' 'gstreamer0.10-base' 'gperf' 'gobject-introspection' 'python2' 'mesa' 'ruby' 'gtk-doc')
options=('!libtool' '!emptydirs')
source=(http://webkitgtk.org/releases/webkitgtk-$pkgver.tar.xz)
sha1sums=('3a2df2eb42ffdd7f3b39873d5461c347a6330493')

build() {
  cd "$srcdir/webkitgtk-$pkgver"
  mkdir build-gtk2 build-gtk3

  cd build-gtk2
  PYTHON=/usr/bin/python2 ../configure --prefix=/usr \
    --enable-introspection \
    --disable-silent-rules \
    --libexecdir=/usr/lib/webkitgtk2 \
    --with-gstreamer=0.10 \
    --with-gtk=2.0 \
    --disable-webkit2
  make all stamp-po
  
  cd ../build-gtk3
  PYTHON=/usr/bin/python2 ../configure --prefix=/usr \
    --enable-introspection \
    --disable-silent-rules \
    --libexecdir=/usr/lib/webkitgtk3 \
    --with-gstreamer=1.0
  make all stamp-po
}

package_webkitgtk3() {
  pkgdesc="GTK+ Web content engine library for GTK+ 3.0"
  depends=('libxt' 'libxslt' 'sqlite' 'libsoup' 'enchant' 'libgl' 'geoclue' 'gtk2' 'gtk3' 'gst-plugins-base-libs')
  conflicts=('libwebkit3')
  provides=("libwebkit3=${pkgver}")
  replaces=('libwebkit3')
  
  cd "$srcdir/webkitgtk-$pkgver/build-gtk3"
  make -j1 DESTDIR="$pkgdir" install
  install -Dm644 ../Source/WebKit/LICENSE "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}

package_webkitgtk2() {
  pkgdesc="GTK+ Web content engine library for GTK+ 2.0"
  depends=('libxt' 'libxslt' 'sqlite' 'libsoup' 'enchant' 'libgl' 'geoclue' 'gtk2' 'gstreamer0.10-base')
  conflicts=('libwebkit')
  provides=("libwebkit=${pkgver}")
  replaces=('libwebkit')
  
  cd "$srcdir/webkitgtk-$pkgver/build-gtk2"
  make -j1 DESTDIR="$pkgdir" install
  install -Dm644 ../Source/WebKit/LICENSE "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}
