# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Arjan Timmerman <arjan.archlinux.org>
# Contributor: Tom Newsom <Jeepster.gmx.co.uk>

pkgname=imlib2
pkgver=1.4.2
pkgrel=3
pkgdesc="Library that does image file loading and saving as well as rendering, manipulation, arbitrary polygon support"
url="http://sourceforge.net/projects/enlightenment/"
arch=('i686' 'x86_64')
license=('BSD')
depends=('libtiff>=3.8.2-5' 'giflib' 'bzip2' 'freetype2' 'libxext' 'libpng' 'libid3tag' 'libjpeg')
options=('!libtool')
source=("http://downloads.sourceforge.net/enlightenment/$pkgname-$pkgver.tar.bz2" \
        'bufferoverflow.patch')
md5sums=('9f15568e76ebda9092c571e0df261f54' '76def1fe0ac2ac1686ff24d29979606f')

build() {
  cd $srcdir/$pkgname-$pkgver

  # disable optimizations, they cause problems (e.g. FS#12268)
  [ $CARCH = "i686" ] && EXTRAOPTS="--disable-mmx"
  [ $CARCH = "x86_64" ] && EXTRAOPTS="--disable-amd64"


  # Security Fix (Buffer Overflow), upstream fix
  patch -Np0 -i $srcdir/bufferoverflow.patch || return 1

  # Configure and Build
  ./configure --prefix=/usr \
              --sysconfdir=/etc/imlib2 \
              --x-libraries=/usr/lib $EXTRAOPTS || return 1
  make || return 1
  make DESTDIR=$pkgdir install || return 1

  # Install License
  install -Dm644 COPYING $pkgdir/usr/share/licenses/$pkgname/COPYING
}
