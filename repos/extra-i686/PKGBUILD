# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=a2ps
pkgver=4.14
pkgrel=2
pkgdesc="An Any to PostScript filter"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/a2ps/"
license=('GPL3')
depends=('ghostscript' 'psutils' 'imagemagick' 'perl')
makedepends=('gperf')
backup=('etc/a2ps/a2ps.cfg' 'etc/a2ps/a2ps-site.cfg')
options=('!libtool')
install=a2ps.install
source=(ftp://ftp.gnu.org/gnu/a2ps/${pkgname}-${pkgver}.tar.gz \
        a2ps-4.13c-fnmatch-replacement.patch a2ps-4.13c-emacs.patch a2ps-4.13-manpage-chmod.patch \
        a2ps-4.14-check-mempcpy.patch a2ps-4.14-fix-stpcpy-proto.patch)
md5sums=('781ac3d9b213fa3e1ed0d79f986dc8c7'
         '93011b58e9f20a7fafa31287a51884a4'
         '2e493d0bc00d71eb3e8a9b6febc52b69'
         '09cc5ed4d3d8ff1b2103e519191e8286'
         '42aa39b74f6da8cf6e94185c4fc3e601'
         '066f9327fee6b6eca3a57d657d1097d1')
sha1sums=('365abbbe4b7128bf70dad16d06e23c5701874852'
          '8783952d3410d8d59ed953e1db45e2ef1a0b8f65'
          '94e35a6ad9268161bec22a56d4cfb696d635994a'
          '2bb3d0a2ef2f3ff9262723e35c742a80ab0235ce'
          '6aed29c1399e79f3914b408059610f9e7c0fc38e'
          '58fa90134f1027e3f05aeb08212cbcc10f420738')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 < ../a2ps-4.13c-fnmatch-replacement.patch
  patch -p1 < ../a2ps-4.13c-emacs.patch
  patch -p1 < ../a2ps-4.13-manpage-chmod.patch
  patch -p1 < ../a2ps-4.14-check-mempcpy.patch
  patch -p0 < ../a2ps-4.14-fix-stpcpy-proto.patch
  libtoolize --force --copy
  autoreconf -I m4
  ./configure --prefix=/usr --sysconfdir=/etc/a2ps \
    --includedir=/usr/include --enable-nls
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  sed -i "s:^countdictstack: \0:" "${pkgdir}/usr/bin/psset"
}
