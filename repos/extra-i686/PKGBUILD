# $Id$
# Maintainer: Dan McGee <dan@archlinux.org>

pkgbase=doxygen
pkgname=('doxygen' 'doxygen-docs')
pkgver=1.7.6.1
pkgrel=1
license=('GPL')
arch=(i686 x86_64)
url="http://www.doxygen.org/"
makedepends=('gcc-libs' 'flex' 'qt' 'texlive-core' 'ghostscript' 'texlive-latexextra')
source=(ftp://ftp.stack.nl/pub/users/dimitri/${pkgname}-${pkgver}.src.tar.gz)
md5sums=('084f1db244b77b3abff335550ef8eec5')
sha1sums=('6203d4423d12315f1094b56a4d7393347104bc4a')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  QTDIR=/usr ./configure --prefix /usr --with-doxywizard
  make
}

package_doxygen() {
  pkgdesc="A documentation system for C++, C, Java, IDL and PHP"
  depends=('gcc-libs')
  optdepends=('graphviz: for caller/callee graph generation'
              'qt: for doxywizard')

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make INSTALL="${pkgdir}"/usr MAN1DIR=share/man/man1 install
}

package_doxygen-docs() {
  pkgdesc="Developer documentation for doxygen"

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make INSTALL="${pkgdir}"/usr MAN1DIR=share/man/man1 install_docs
}
