# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=networkmanager-pptp
pkgver=0.9.10.0
pkgrel=1
pkgdesc="NetworkManager VPN plugin for pptp "
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnome.org/projects/NetworkManager/"
_pppver=2.4.6
depends=("networkmanager>=${pkgver}" 'pptpclient' 'gtk3' 'libgnome-keyring' "ppp=$_pppver")
makedepends=('intltool' 'network-manager-applet')
optdepends=('network-manager-applet: GNOME frontends to NetWorkmanager')
install=networkmanager-pptp.install
source=(http://ftp.gnome.org/pub/GNOME/sources/NetworkManager-pptp/0.9/NetworkManager-pptp-${pkgver}.tar.xz)
sha256sums=('564b16c9b2821a1d2ede06f56f4db5cb0d62ccb35f87c92ad6c636ed48e0af58')

build() {
  cd NetworkManager-pptp-${pkgver}
  ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --libexecdir=/usr/lib/networkmanager \
        --disable-static \
        --enable-more-warnings=yes \
        --with-pppd-plugin-dir=/usr/lib/pppd/$_pppver
  make
}

package() {
  cd NetworkManager-pptp-${pkgver}
  make DESTDIR="${pkgdir}" install
}
