# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kollision
pkgver=17.04.1
pkgrel=1
pkgdesc="A simple ball dodging game"
url="https://kde.org/applications/games/kollision/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegames')
depends=('libkdegames' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'kdoctools' 'python')
conflicts=('kdegames-kollision')
replaces=('kdegames-kollision')
source=("https://download.kde.org/stable/applications/${pkgver}/src/kollision-${pkgver}.tar.xz"{,.sig})
sha256sums=('9dca9bbcc5f69cb5de639e1820c430c8cc452ca77bc7f19545941ec8c86ba040'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

build() {
  mkdir -p build
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
