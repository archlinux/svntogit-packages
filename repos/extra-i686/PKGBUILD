# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Yue

pkgname=libetonyek
pkgver=0.1.4
pkgrel=1
pkgdesc='Library and a set of tools for reading and converting Apple Keynote presentations'
arch=('i686' 'x86_64')
url="https://wiki.documentfoundation.org/DLP/Libraries/libetonyek"
license=('MPL')
depends=('libxml2' 'zlib' 'libwpd' 'boost-libs' 'librevenge')
makedepends=('boost' 'cppunit' 'gperf' 'doxygen' 'glm' 'mdds')
source=(http://dev-www.libreoffice.org/src/$pkgname/$pkgname-$pkgver.tar.xz
        buildfix.diff)
md5sums=('d9f913f3d29fc4fc1c8fd7084ce48fff'
         'e2aa200ec4aac3d550cf5be363afa1b8')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../buildfix.diff
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
