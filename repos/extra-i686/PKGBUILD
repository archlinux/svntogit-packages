# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Andrew Simmons <andrew.simmons@gmail.com>

pkgname=thunar
pkgver=1.4.0
pkgrel=3
pkgdesc="Modern file manager for Xfce"
arch=('i686' 'x86_64')
url="http://thunar.xfce.org"
license=('GPL2' 'LGPL2.1')
groups=('xfce4')
depends=('desktop-file-utils' 'libexif' 'hicolor-icon-theme' 'libnotify' 'udev'
         'gtk2' 'exo' 'libxfce4util' 'libxfce4ui' 'libpng')
makedepends=('intltool' 'gtk-doc' 'xfce4-panel')
optdepends=('gvfs: for trash support, mounting with udisk and remote filesystems'
            'polkit-gnome: for mounting internal partitions (needs root password)'
            'xfce4-panel: for trash applet'
            'tumbler: for thumbnail previews'
            'thunar-volman: manages removable devices'
            'thunar-archive-plugin: create and deflate archives'
            'thunar-media-tags-plugin: view/edit id3/ogg tags')
options=('!libtool')
install=$pkgname.install
source=(http://archive.xfce.org/src/xfce/$pkgname/1.4/Thunar-$pkgver.tar.bz2
        thunar-1.4.0-dont-add-duplicates-of-devices.patch
        thunar-1.4.0-show-loop-devices-hack.patch)
sha256sums=('2c11a19e64cef708a264fb4d5d933389899c3d132fe7b1e313dd6e37bfe4c8ba'
            'c25f86a4c298c24327b0681702167aea9f9d712a738c8f3883f5d66da7f08f19'
            '185c44db9bdf578d22cb69b81dce7fcc3e2ee6031b1547de6453ff0752dbc97e')

build() {
  cd "$srcdir/Thunar-$pkgver"

  # https://bugzilla.xfce.org/show_bug.cgi?id=9440
  # Backport of http://git.xfce.org/xfce/thunar/commit/?id=70a4301
  patch -Np1 -i "$srcdir/thunar-1.4.0-dont-add-duplicates-of-devices.patch"

  # Fix: Loop devices not appearing in Thunar side pane
  # https://bugs.archlinux.org/task/29894
  # https://bugzilla.xfce.org/show_bug.cgi?id=8947
  patch -Np1 -i "$srcdir/thunar-1.4.0-show-loop-devices-hack.patch"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --enable-gio-unix \
    --enable-dbus \
    --enable-startup-notification \
    --enable-gudev \
    --enable-notifications \
    --enable-exif \
    --enable-pcre \
    --enable-gtk-doc \
    --disable-debug
  make
}

package() {
  cd "$srcdir/Thunar-$pkgver"

  make DESTDIR=${pkgdir} install
  sed -i 's:x-directory/gnome-default-handler;::' \
    "$pkgdir/usr/share/applications/Thunar-folder-handler.desktop"
}

# vim:set ts=2 sw=2 et:
