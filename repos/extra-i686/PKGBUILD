# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>

pkgname=geeqie
pkgver=1.1
pkgrel=1
pkgdesc="A lightweight image browser and viewer"
arch=('i686' 'x86_64')
url="http://geeqie.sourceforge.net/"
license=('GPL2')
depends=('exiv2' 'gtk2' 'lcms' 'desktop-file-utils')
makedepends=('intltool' 'doxygen' 'gnome-doc-utils')
optdepends=('librsvg: SVG rendering')
install=geeqie.install
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz)
sha1sums=('77167479e91e03d9512535a146c5d2d77941257f')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  CPPFLAGS="-D_FILE_OFFSET_BITS=64" ./configure --prefix=/usr
  make
}

package(){
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
