# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgbase=gtkmm3
_pkgbasename=gtkmm
pkgname=('gtkmm3' 'gtkmm3-docs')
pkgver=3.10.1
pkgrel=1
arch=('i686' 'x86_64')
makedepends=('gtk3' 'pangomm' 'atkmm' 'glibmm-docs' 'mm-common' 'doxygen' 'libxslt')
license=('LGPL')
options=('!emptydirs')
url="http://www.gtkmm.org/"
source=(http://ftp.gnome.org/pub/GNOME/sources/$_pkgbasename/${pkgver%.*}/$_pkgbasename-$pkgver.tar.xz)
sha256sums=('6fa5cdaa5cf386ea7c77fafed47e913afadd48fe45e28d8cb01075c3ee412538')

build() {
  cd "$_pkgbasename-$pkgver"
  ./configure --prefix=/usr
  make
}

package_gtkmm3() {
  pkgdesc="C++ bindings for gtk3"
  depends=('gtk3' 'pangomm' 'atkmm')
  cd "$_pkgbasename-$pkgver"

  sed -i -e 's/^doc_subdirs/#doc_subdirs/' Makefile

  make DESTDIR="$pkgdir" install
}

package_gtkmm3-docs() {
  pkgdesc="Developer documentation for gtkmm v3"

  cd "$_pkgbasename-$pkgver"
  make -C docs DESTDIR="$pkgdir" install
}
