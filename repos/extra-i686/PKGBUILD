# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Adria Arrufat <adria.arrufat@protonmail.ch>
pkgname=lasem
pkgver=0.4.3
pkgrel=2
pkgdesc="SVG and Mathml rendering library"
arch=('i686' 'x86_64')
url="https://wiki.gnome.org/Projects/Lasem"
license=('LGPL')
depends=('gtk3')
makedepends=('intltool')
options=('!libtool')
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver:0:3}/$pkgname-$pkgver.tar.xz)
sha256sums=('e7d950f9579defc7bb9d4159a0bfff165a3a621945ac9f202c930f2fea2dd58f')

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --disable-static
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  mv "$pkgdir/usr/doc" "$pkgdir/usr/share/doc"
}
