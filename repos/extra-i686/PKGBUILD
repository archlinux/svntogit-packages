# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=libzip
pkgver=0.11
pkgrel=1
pkgdesc="A C library for reading, creating, and modifying zip archives"
url="http://www.nih.at/libzip/index.html"
license=('BSD')
arch=('i686' 'x86_64')
depends=('zlib')
options=('!libtool')
source=("http://www.nih.at/${pkgname}/${pkgname}-${pkgver}.tar.xz"
        'fix-headers.patch')
md5sums=('9fb13212011721983fe7119393565533'
         '249395bd3a426c4c4e993e9d4753e1dd')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -p1 -i "${srcdir}/fix-headers.patch"
  autoreconf -i
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
