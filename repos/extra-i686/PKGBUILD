# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Andreas Radke <andyrtr at archlinux.org>
# Contributor: Ben Mazer <contrasutra@myrealbox.com>

pkgname=xfce4-notes-plugin
pkgver=1.7.7
pkgrel=6
pkgdesc="A notes plugin for the Xfce4 panel"
arch=('i686' 'x86_64')
license=('GPL2')
url="http://goodies.xfce.org/projects/panel-plugins/xfce4-notes-plugin"
groups=('xfce4-goodies')
depends=('xfce4-panel' 'libxfcegui4' 'libunique' 'hicolor-icon-theme')
makedepends=('intltool')
install=$pkgname.install
source=(http://archive.xfce.org/src/panel-plugins/$pkgname/1.7/$pkgname-$pkgver.tar.bz2)
sha256sums=('a7baa105b37ad05dea4d6b55d98fd3214c77ad5c7a0e91471d4906c81e5f5217')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
