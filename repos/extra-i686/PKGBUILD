# $Id$
# Maintainer:Antonio Rojas <arojas@archlinux.org>

pkgname=kgamma5
pkgver=5.6.5
pkgrel=1
pkgdesc="Adjust your monitor's gamma settings"
arch=(i686 x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
depends=(kdelibs4support)
makedepends=(extra-cmake-modules kdoctools python kdesignerplugin)
groups=(plasma)
source=("http://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('dc06a855dc6b2916728fa8f5b86068b7b7b23aaa238eca547f86fe36a90a2c62'
            'SKIP')
validpgpkeys=('13C16D03EDE728514473AA73A506E6D4DD4D5088') # Jonathan Riddell

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
