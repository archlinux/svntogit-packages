# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Justin "juster" Davis <jrcd83@gmail.com>
# Generator : pbjam 0.01

pkgname=perl-list-moreutils
pkgver=0.408
pkgrel=1
pkgdesc="Provide the stuff missing in List::Util"
arch=('i686' 'x86_64')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl-exporter-tiny')
url=http://search.cpan.org/dist/List-MoreUtils
source=("http://search.cpan.org/CPAN/authors/id/R/RE/REHSACK/List-MoreUtils-${pkgver}.tar.gz")
sha512sums=('786fc6e613ab3d19c02ef8b6a35405634124d55ccde6d52bf0169397599d8cab3e37a7ffa5eaf2fe83ba933958197aefad89e5cf7707b75b3e70f2dae23412bf')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor"     \
      PERL_MB_OPT="--installdirs vendor" \
      MODULEBUILDRC=/dev/null

    cd "${srcdir}/List-MoreUtils-${pkgver}"
    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    cd "${srcdir}/List-MoreUtils-${pkgver}"
    make test
  )
}

package() {
    cd "${srcdir}/List-MoreUtils-${pkgver}"
    make DESTDIR="$pkgdir" install
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
