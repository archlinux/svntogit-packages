# $Id$
# Maintainer: SÃ©bastien Luttringer
# Contributor: Valere Monseur <valere_monseur@hotmail.com>
# Contributor: Alessandro Sagratini <ale_sagra@hotmail.com>
# Contributor: Kevin Edmonds <edmondskevin@hotmail.com>

pkgname=conntrack-tools
pkgver=1.4.3
pkgrel=1
pkgdesc='Userspace tools to interact with the Netfilter connection tracking system'
arch=('i686' 'x86_64')
url='http://www.netfilter.org/projects/conntrack-tools'
license=('GPL2')
depends=('libnetfilter_conntrack'
         'libnetfilter_cttimeout'
         'libnetfilter_cthelper'
         'libnetfilter_queue')
backup=('etc/conntrackd.conf')
validpgpkeys=('57FF5E9C9AA67A860B557AF7A4111F89BB5F58CC') # Netfilter Core Team
source=("http://www.netfilter.org/projects/$pkgname/files/$pkgname-$pkgver.tar.bz2"{,.sig}
        'conntrackd.service')
install=$pkgname.install
sha1sums=('509db30f34b283f4a74a7e638ba0ca713d3fe98c'
          'SKIP'
          'ab888d59b98b673709d2d89554c668e2bea0cb86')

prepare() {
  sed -i -e 's,/etc/conntrackd/,/etc/,' -e 's,/var/lock/,/run/lock/,' \
    $pkgname-$pkgver/include/conntrackd.h
}


build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --sbindir=/usr/bin
  make
}

package() {
  # install binaries
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  # systemd
  cd "$pkgdir"
  install -D -m 644 "$srcdir/conntrackd.service" usr/lib/systemd/system/conntrackd.service
  # install empty configuration files
  install -D -m 644 /dev/null etc/conntrackd.conf
}

# vim:set ts=2 sw=2 et:
