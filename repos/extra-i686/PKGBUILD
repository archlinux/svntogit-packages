# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: buddabrod <buddabrod@gmail.com>

pkgname=xf86-video-nouveau
pkgver=1.0.11+31+g1ff13a9
pkgrel=1
pkgdesc="Open Source 2D acceleration driver for nVidia cards"
arch=('i686' 'x86_64')
url="http://nouveau.freedesktop.org/"
license=('GPL')
depends=('libdrm' 'systemd' 'mesa-dri')
makedepends=('xorg-server-devel' 'X-ABI-VIDEODRV_VERSION=20' 'git')
conflicts=('xorg-server<1.16' 'X-ABI-VIDEODRV_VERSION<20' 'X-ABI-VIDEODRV_VERSION>=21')
groups=('xorg-drivers' 'xorg')
install=$pkgname.install
#source=(http://xorg.freedesktop.org/archive/individual/driver/$pkgname-$pkgver.tar.bz2)
source=('git://anongit.freedesktop.org/nouveau/xf86-video-nouveau#commit=1ff13a9')
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --long | sed 's/xf86-video-nouveau-//' | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
#  cd $pkgname-$pkgver
  cd $pkgname
  ./configure --prefix=/usr
  make
}

package() {
#  cd $pkgname-$pkgver
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
