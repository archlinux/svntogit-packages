# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: eric <eric@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=tdb
pkgver=1.3.0
pkgrel=2
pkgdesc="A Trivial Database similar to GDBM but allows simultaneous commits"
arch=(i686 x86_64)
license=('GPL3')
url="http://tdb.samba.org/"
makedepends=('python2' 'libxslt' 'docbook-xsl')
optdepends=('python2: for python bindings')
source=(http://samba.org/ftp/${pkgname}/${pkgname}-${pkgver}.tar.gz
        0001-tdb-include-include-stdbool.h-in-tdb.h.patch)
md5sums=('f18cd6afc4f5670edfe58f72d2665f94'
         '2b0b6003b3d8ca03adccd4aa1a3b8313')

prepare() {
   cd ${srcdir}/${pkgname}-${pkgver}
   patch -Np1 -i ../0001-tdb-include-include-stdbool.h-in-tdb.h.patch
}

build() {
   cd ${srcdir}/${pkgname}-${pkgver}
   sed -i -e 's#python#python2#g' buildtools/bin/waf
   #sed -i 's#python2\.6\([^ ]*\)#python2.7\1 python2.6\1#' configure
   # Use system docbook.xsl
   #_manstyle="/usr/share/xml/docbook/xsl-stylesheets-1.76.1/manpages/docbook.xsl"
   #sed -i "s#http.*xsl#$_manstyle#" tdb.mk
   export PYTHON=/usr/bin/python2
   ./configure --prefix=/usr \
               --localstatedir=/var \
               --sysconfdir=/etc/samba
   make
}

package() {
   cd ${srcdir}/${pkgname}-${pkgver}
   export PYTHON=/usr/bin/python2
   make DESTDIR=$pkgdir/ install
}
