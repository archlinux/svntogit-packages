# $Id$
# Maintainer:
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: PedsXing <pedsxing at gmx dot net>

pkgname=vc
pkgver=1.2.0
pkgrel=1
pkgdesc="A library to ease explicit vectorization of C++ code"
url='https://github.com/VcDevel/Vc'
arch=(i686 x86_64)
license=(LGPL3)
makedepends=(cmake)
source=("https://github.com/VcDevel/Vc/releases/download/$pkgver/Vc-$pkgver.tar.gz"
        vc-gcc6.patch::"https://github.com/VcDevel/Vc/commit/7b27d13a.patch")
md5sums=('f2a213ae4bad0dcf4ec6469e4dad41c1'
         'f69fac8bacb3ab0470f997007d5bf1ef')

prepare() {
  mkdir -p build

# Fix build with GCC 6
  cd Vc-$pkgver
  patch -p1 -i ../vc-gcc6.patch
# Fix i686 build
  sed -e 's:(x86|AMD64):(i686|x86|AMD64):g' -i CMakeLists.txt
}

build() {
  cd build
  cmake ../Vc-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DTARGET_ARCHITECTURE=generic
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
