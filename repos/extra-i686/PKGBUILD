# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org> 

pkgname=discover
pkgver=5.9.4
pkgrel=1
pkgdesc='A collection of package management tools for KDE'
arch=(i686 x86_64)
url='https://projects.kde.org/projects/kde/workspace/discover'
license=(LGPL)
depends=(knewstuff kdeclarative kitemmodels qt5-graphicaleffects appstream-qt archlinux-appstream-data
         hicolor-icon-theme kirigami2)
makedepends=(extra-cmake-modules python plasma-framework packagekit-qt5)
optdepends=('packagekit-qt5: To install packages from Arch repositories')
conflicts=(muon)
replaces=(muon)
groups=(plasma)
source=("http://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig} discover-no-deb-rpm.patch)
md5sums=('bf72b471596b5a9115826a80e8a8c264'
         'SKIP'
         '0154ac2c0a875a9113adac332bea6788')
validpgpkeys=(2D1D5B0588357787DE9EE225EC94D18F7F05997E) # Jonathan Riddell <jr@jriddell.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
# Don't handle .rpm or .deb files
  patch -p1 -i ../discover-no-deb-rpm.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
