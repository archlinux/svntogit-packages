# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=skanlite
pkgver=0.4
_kdever=4.4.0
pkgrel=1
pkgdesc="Image Scanning Application for KDE"
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('LGPL')
depends=('kdegraphics-libs')
makedepends=('pkgconfig' 'cmake' 'automoc4')
options=('docs')
source=("http://download.kde.org/stable/${_kdever}/src/extragear/${pkgname}-${pkgver}-kde${_kdever}.tar.bz2")
md5sums=('b1e6e8a3e6b2ffab667afbf6426d344d')

build() {
  cd ${srcdir}
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver}-kde${_kdever} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make || return 1
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install
}
