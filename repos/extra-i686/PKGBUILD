# Maintainer: Florian Pritz <bluewind@xinu.at>

pkgname=perl-crypt-openssl-bignum
pkgver=0.08
pkgrel=1
pkgdesc="OpenSSL's multiprecision integer arithmetic"
arch=(i686 x86_64)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=(perl openssl)
url=https://metacpan.org/release/Crypt-OpenSSL-Bignum
source=("http://search.cpan.org/CPAN/authors/id/K/KM/KMX/Crypt-OpenSSL-Bignum-$pkgver.tar.gz")
md5sums=('99eb0165de08b328735d900689ec99bc')
sha512sums=('da36b377287037c46e905f25d491641a06113ea1157f00731c43fdc27762af6cbde0e55361d929712adf57fc3564197aadb54629498318c1eb9c1f3fd0c50e7d')
_ddir="Crypt-OpenSSL-Bignum-$pkgver"

build() {
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  /usr/bin/perl Makefile.PL
  make
}

check() {
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
}

package() {
  cd "$srcdir/$_ddir"
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
# template start; name=perl-binary-module-dependency; version=1;
if [[ $(find "$pkgdir/usr/lib/perl5/" -name "*.so") ]]; then
	_perlver_min=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]);')
	_perlver_max=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]+1);')
	depends+=("perl>=$_perlver_min" "perl<$_perlver_max")
fi
# template end;
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
