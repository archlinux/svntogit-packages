# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: MaÃ«l Lavault <moimael@neuf.fr>

pkgbase=abiword
pkgname=('abiword' 'abiword-plugins')
pkgver=2.8.2
pkgrel=2
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.abisource.com"
makedepends=('pkgconfig' 'fribidi' 'enchant' 'librsvg' 'wv' 'libgsf-gnome' 'aiksaurus' 'gtkmathview' 'libsoup' 'link-grammar' 'libwpd' 'psiconv' 'libxslt' 'asio' 'loudmouth')
options=('!makeflags' '!libtool')
source=(http://www.abisource.com/downloads/abiword/${pkgver}/source/${pkgbase}-${pkgver}.tar.gz)
md5sums=('b44ffbebd5bd0f5f98f34dc8c688b4ed')

build() {
  cd ${srcdir}/${pkgbase}-${pkgver}

  ./configure --prefix=/usr \
              --enable-plugins || return 1
  make || return 1
}

package_abiword() {
  pkgdesc="A fully-featured word processor"
  depends=('fribidi' 'enchant' 'librsvg' 'wv' 'libgsf-gnome')
  cd "${srcdir}/${pkgbase}-${pkgver}"
  make DESTDIR="${pkgdir}" install || return 1
  # Remove plugin directory provided by abiword-plugins
  rm -rf ${pkgdir}/usr/lib/abiword-2.8/plugins
}

package_abiword-plugins() {
  pkgdesc="Various plugins for Abiword"
  depends=("abiword>=${pkgver}" 'aiksaurus' 'gtkmathview' 'libsoup' 'link-grammar' 'libwpd' 'psiconv' 'libxslt' 'asio' 'loudmouth')
  cd "${srcdir}/${pkgbase}-${pkgver}/plugins"
  make DESTDIR="${pkgdir}" install || return 1
  # Remove files conflicting with abiword package
  rm -rf ${pkgdir}/usr/share
}
