# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: kevin <kevin@archlinux.org>

pkgname=ntp
pkgver=4.2.6.p4
_realver=4.2.6p4
pkgrel=2
pkgdesc='Network Time Protocol reference implementation'
url='http://www.ntp.org/'
license=('custom')
arch=('i686' 'x86_64')
depends=('openssl' 'readline' 'libcap')
makedepends=('perl-html-parser')
backup=('etc/ntp.conf' 'etc/conf.d/ntpd.conf')
source=("http://www.eecis.udel.edu/~ntp/ntp_spool/ntp4/ntp-${_realver}.tar.gz"
        'ntpd'
        'ntpdate'
        'ntp.conf'
        'ntpd.conf')
sha1sums=('b53c4565c5d1c37494584d4fb826f82ffea285cf'
          '97b41b8bc34f259f4e525ab2e58707c9724e72e2'
          '63572457586379aa6bf75b6e899e71d651e643f4'
          '6d2d234afb1db2dcb940747a62f70217246a3d13'
          '6cfcb7bbd34499b30e31dffca2f3e1a036010271')

install=install

build() {
	cd "${srcdir}/${pkgname}-${_realver}"

	./configure --prefix=/usr \
		--mandir=/usr/share/man \
		--enable-linux-caps \

	make
}

package() {
	cd "${srcdir}/${pkgname}-$_realver"

	make DESTDIR="${pkgdir}" install

	install -Dm755 ../ntpd "${pkgdir}"/etc/rc.d/ntpd
	install -Dm755 ../ntpdate "${pkgdir}"/etc/rc.d/ntpdate
	install -Dm644 ../ntp.conf "${pkgdir}"/etc/ntp.conf
	install -Dm644 ../ntpd.conf "${pkgdir}"/etc/conf.d/ntpd.conf

	cd html
	../scripts/html2man
	install -d "${pkgdir}"/usr/share/man
	mv man/man* "${pkgdir}"/usr/share/man
	mv "${pkgdir}/usr/share/man/man8/ntpd.8" "${pkgdir}/usr/share/man/man8/ntp-ntpd.8" # we should ditch openntpd
	cd ..

	rmdir "${pkgdir}"/usr/{lib,sbin}
	install -d "${pkgdir}/var/lib/ntp"
	install -Dm644 COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

	touch "${pkgdir}"/var/lib/ntp/.placeholder
}
