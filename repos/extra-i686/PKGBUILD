# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=libmbim
pkgver=1.12.4
pkgrel=1
pkgdesc="MBIM modem protocol helper library"
arch=(i686 x86_64)
url="http://www.freedesktop.org/wiki/Software/libmbim/"
license=(GPL2)
depends=(glib2 bash systemd libgudev)
makedepends=(gtk-doc python)
source=(http://www.freedesktop.org/software/$pkgname/$pkgname-$pkgver.tar.xz)
sha256sums=('7bd9af154e88ee1cccaf2fedd05e0f5cb82e3359ab6ad2a75cfec98bf57f973a')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib/$pkgname --disable-static
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
