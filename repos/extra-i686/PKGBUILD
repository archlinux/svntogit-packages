# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=upower
pkgver=0.99.0
pkgrel=3
pkgdesc="Abstraction for enumerating power devices, listening to device events and querying history and statistics"
arch=('i686' 'x86_64')
url="http://upower.freedesktop.org"
license=('GPL')
depends=('systemd-tools' 'systemd' 'libusb' 'dbus-glib' 'libimobiledevice')
makedepends=('git' 'intltool' 'gtk-doc' 'docbook-xsl' 'gobject-introspection' 'systemd')
backup=('etc/UPower/UPower.conf')
source=("git://anongit.freedesktop.org/upower#commit=e4ad754351b67c00438c736c14bb948905708009"
        revert-isdocked.patch
        revert-version.patch)
sha256sums=('SKIP'
            '81e250bc8402f91bf0841abc58ce8effde91a16ce2b889b7b7f15251d1ef8e92'
            'bf11b285dea24000c18fd75206b21c703d1fa5fbfa5e5a7f877383bf8efcbf2e')

prepare() {
  cd $pkgname
  patch -Np1 -R -i ../revert-version.patch
  patch -Np1 -R -i ../revert-isdocked.patch
}

build() {
  cd $pkgname

  ./autogen.sh --prefix=/usr --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/$pkgname \
    --disable-static
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
