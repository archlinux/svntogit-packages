# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-netaddr-ip
pkgver=4.075
pkgrel=2
pkgdesc="Perl module to manage IP addresses and subnets"
arch=('i686' 'x86_64')
url="http://search.cpan.org/dist/NetAddr-IP/"
license=('PerlArtistic' 'GPL')
depends=('perl')
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/M/MI/MIKER/NetAddr-IP-${pkgver}.tar.gz)
md5sums=('b72b91ebd228b096fd3b0acc87df877e')

build() {
  cd NetAddr-IP-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd NetAddr-IP-${pkgver}
  make test
}

package() {
  cd NetAddr-IP-${pkgver}
  make DESTDIR="${pkgdir}" install
# template start; name=perl-binary-module-dependency; version=1;
if [[ $(find "$pkgdir/usr/lib/perl5/" -name "*.so") ]]; then
	_perlver_min=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]);')
	_perlver_max=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]+1);')
	depends+=("perl>=$_perlver_min" "perl<$_perlver_max")
fi
# template end;
}
