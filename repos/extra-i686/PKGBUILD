# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: mgushee

pkgname=djvulibre
pkgver=3.5.26
pkgrel=1
pkgdesc="Suite to create, manipulate and view DjVu ('déjà vu') documents"
url='http://djvu.sourceforge.net/'
arch=('i686' 'x86_64')
license=('GPL2')
makedepends=('librsvg' 'git')
depends=('libtiff' 'hicolor-icon-theme')
#source=("http://downloads.sourceforge.net/project/djvu/DjVuLibre/${pkgver}/djvulibre-${pkgver}.tar.gz")
#sha1sums=('ad35056aabb1950f385360ff59520a82a6f779ec')
source=('git://git.code.sf.net/p/djvu/djvulibre-git#commit=3969bdfd2c904895ce091913feb7e91b2eca54ae')
sha1sums=('SKIP')

conflicts=('libdjvu')
provides=("libdjvu=${pkgver}")
replaces=('libdjvu')

install=install

build() {
	#cd "${srcdir}/${pkgname}-${pkgver}"
	cd "${srcdir}/djvulibre-git"
	./autogen.sh
	./configure --prefix=/usr --disable-desktopfiles
	make
}

package() {
	#cd "${srcdir}/${pkgname}-${pkgver}"
	cd "${srcdir}/djvulibre-git"
	make DESTDIR="${pkgdir}" install
	for sz in 22 32 48 64; do
		install -Dm644 desktopfiles/prebuilt-hi${sz}-djvu.png "${pkgdir}/usr/share/icons/hicolor/${sz}x${sz}/mimetypes/image-vnd.djvu.mime.png"
	done
}
