# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-xcmsdb
pkgver=1.0.5
pkgrel=1
pkgdesc="Device Color Characterization utility for X Color Management System"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libx11')
makedepends=('xorg-util-macros')
groups=('xorg-apps' 'xorg')
source=(http://xorg.freedesktop.org/archive/individual/app/xcmsdb-${pkgver}.tar.bz2{,.sig})
sha256sums=('e5585361bb8b6a05bb814a8d0e444ee93e0f00180881d3070aff4571e97f67c6'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # Alan Coopersmith <alan.coopersmith@oracle.com>

build() {
  cd xcmsdb-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd xcmsdb-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
