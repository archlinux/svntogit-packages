# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Silvio Knizek (killermoehre)

pkgname=gnome-disk-utility
pkgver=3.26.2
pkgrel=1
pkgdesc="Disk Management Utility for GNOME"
url="https://git.gnome.org/browse/gnome-disk-utility"
arch=(i686 x86_64)
license=(GPL)
groups=(gnome)
depends=(udisks2 gtk3 libsecret libpwquality libcanberra libdvdread libnotify parted)
makedepends=(docbook-xsl appstream-glib git meson)
_commit=d6624c0a1ac98249e44e0f1c19c9cb122360956b  # tags/3.26.2^0
source=("git+https://git.gnome.org/browse/gnome-disk-utility#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  mkdir build
  cd $pkgname
}

build() {
  cd build
  arch-meson ../$pkgname
  ninja
}

check() {
  cd build
  meson test
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}
