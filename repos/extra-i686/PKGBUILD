# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Suzy Williams <suzanne.williams3@verizon.net>

pkgname=xfce4-weather-plugin
pkgver=0.8.3
pkgrel=5
pkgdesc="A weather plugin for the Xfce4 panel"
arch=('i686' 'x86_64')
url="http://goodies.xfce.org/projects/panel-plugins/xfce4-weather-plugin"
license=('GPL2')
groups=('xfce4-goodies')
depends=('xfce4-panel' 'libxml2' 'libsoup' 'hicolor-icon-theme')
makedepends=('intltool')
install=$pkgname.install
source=(http://archive.xfce.org/src/panel-plugins/$pkgname/0.8/$pkgname-$pkgver.tar.bz2
        0001-Fix-color-parsing-when-reading-config-file.patch
        0002-Make-plugin-ready-for-met.no-locationforecast-1.2-AP.patch
        0003-Switch-to-met.no-locationforecastLTS-1.2-API-bug-109.patch)
sha256sums=('40a6a22be7653b15a47174a430da89040f178695b48e5e01e77990050f715ce4'
            'a3f00c0012e6f6aef678e2ecc2d683c2371e256d95681e31ec027207fbf7100c'
            '6c26a43817324496285306882b3cce38d70ed28e63e2e963ff873d97e4459732'
            'd953160b0c7aa5d64cffa2be9cd68f2cfcddd3ac3334b6bcf1566c0b95b20f01')


prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -Np1 -i ../0001-Fix-color-parsing-when-reading-config-file.patch
  patch -Np1 -i ../0002-Make-plugin-ready-for-met.no-locationforecast-1.2-AP.patch
  patch -Np1 -i ../0003-Switch-to-met.no-locationforecastLTS-1.2-API-bug-109.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --disable-debug \
    --disable-upower
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
