# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=librsvg
pkgver=2.40.16
pkgrel=2
epoch=1
pkgdesc="A SVG viewing library"
arch=(i686 x86_64)
license=('LGPL')
depends=('gdk-pixbuf2' 'pango' 'libcroco')
makedepends=('intltool' 'gobject-introspection' 'vala' 'python2')
options=('!emptydirs')
url="https://wiki.gnome.org/Projects/LibRsvg"
source=(https://download.gnome.org/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('d48bcf6b03fa98f07df10332fb49d8c010786ddca6ab34cbba217684f533ff2e')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-static --enable-vala

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' libtool

  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
