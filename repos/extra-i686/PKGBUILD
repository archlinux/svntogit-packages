# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdnssd
pkgver=5.15.0
pkgrel=1
pkgdesc='Abstraction to system DNSSD features'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kdnssd'
license=('LGPL')
depends=('qt5-base' 'avahi')
makedepends=('extra-cmake-modules' 'qt5-tools')
groups=('kf5')
replaces=('kdnssd-framework')
conflicts=('kdnssd-framework')
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
md5sums=('587a96871eabc9e56885fb7aa81923ec')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
