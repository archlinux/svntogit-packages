# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Antonio Rojas

pkgname=plasma-desktop
pkgver=5.3.1
pkgrel=3
pkgdesc='KDE Plasma Desktop'
arch=('i686' 'x86_64')
url='https://www.kde.org/workspaces/plasmadesktop/'
license=('LGPL')
depends=('powerdevil' 'polkit-kde-agent'
         'libcanberra' 'kwin' 'libusb-compat' 'libxkbfile' 'kmenuedit'
         'systemsettings' 'ksysguard' 'kpeople' 'baloo')
makedepends=('extra-cmake-modules' 'kdoctools' 'boost' 'xf86-input-evdev' 'xf86-input-synaptics' 'xorg-server-devel')
optdepends=('plasma-nm: Network manager applet')
conflicts=('kdebase-workspace' 'kcm-touchpad-frameworks')
replaces=('kcm-touchpad-frameworks')
install="${pkgname}.install"
groups=('plasma')
source=("http://download.kde.org/stable/plasma/${pkgver}/$pkgname-$pkgver.tar.xz" 'trash-applet.patch')
md5sums=('df4a3fabb26c7aa4f87405185835c20c'
         '1e13230b7b5858e6c2c9b327095917e7')

prepare() {
  mkdir build

# Fix trash applet loading with plasma-framework 5.11 https://bugs.kde.org/show_bug.cgi?id=349207
  cd $pkgname-$pkgver
  patch -p1 -i "$srcdir"/trash-applet.patch
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DLIBEXEC_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
