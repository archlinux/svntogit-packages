# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gom
pkgver=0.3.2
pkgrel=1
pkgdesc="A GObject to SQLite object mapper"
arch=(i686 x86_64)
url="https://wiki.gnome.org/Projects/Gom"
license=(LGPL2.1)
depends=(glib2 sqlite)
makedepends=(intltool gtk-doc gobject-introspection gdk-pixbuf2)
source=(http://download.gnome.org/sources/$pkgname/${pkgver:0:3}/$pkgname-$pkgver.tar.xz)
sha256sums=('bce8f0f94af6ff7847b853580ba6baebbab8ae531cedb0c78a5c473f39c758fd')
 
build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
    --disable-static --enable-gtk-doc
  make
}

check() {
  cd $pkgname-$pkgver
  make -k check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
