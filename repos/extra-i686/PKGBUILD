# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=bluedevil
pkgver=2.0rc1
_pkgver=2.0-rc1
pkgrel=1
epoch=1
pkgdesc='KDE bluetooth framework'
arch=('i686' 'x86_64')
url="https://projects.kde.org/projects/extragear/base/bluedevil"
license=('GPL')
depends=('kdebase-workspace' 'libbluedevil')
makedepends=('cmake' 'automoc4')
install="${pkgname}.install"
source=("http://download.kde.org/unstable/${pkgname}/${pkgname}-${_pkgver}.tar.xz")
md5sums=('7137a6dae1e860c91ea266546e6bfd39')

prepare() {
  mkdir build

  # Fix build
  sed -i 's/bluedevilaction//' ${pkgname}-${_pkgver}/src/wizard/CMakeLists.txt
}

build(){
  cd build
  cmake ../${pkgname}-${_pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
