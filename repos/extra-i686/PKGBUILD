# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=baloo-widgets
pkgver=15.08.3
pkgrel=1
pkgdesc="Widgets for Baloo"
arch=(i686 x86_64)
url='https://projects.kde.org/baloo-widgets'
license=(LGPL)
depends=(baloo kdelibs4support)
makedepends=(extra-cmake-modules kdoctools)
replaces=(baloo4-widgets)
source=("http://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
md5sums=('94b6e609427ca2aacec03d1958ba945d')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}

