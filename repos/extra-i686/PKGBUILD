# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-yaml-syck
pkgver=1.30
pkgrel=1
pkgdesc="Fast, lightweight YAML loader and dumper"
arch=('i686' 'x86_64')
url="http://search.cpan.org/dist/YAML-Syck/"
license=('BSD' 'custom')
depends=('perl')
options=('!emptydirs')
source=(http://www.cpan.org/authors/id/T/TO/TODDR/YAML-Syck-$pkgver.tar.gz)
sha512sums=('3c38c77589c12652b456ad95120b3223df9e96475a9478692511bf6207ce81bc61204feaa332dbd913d934c41cf5d6515420961b31c20c9fe245373e81ecdda4')

build() {
  cd YAML-Syck-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd YAML-Syck-$pkgver
  make test
}

package() {
  cd YAML-Syck-$pkgver
  make install DESTDIR="${pkgdir}"
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
# template start; name=perl-binary-module-dependency; version=1;
if [[ $(find "$pkgdir/usr/lib/perl5/" -name "*.so") ]]; then
	_perlver_min=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]);')
	_perlver_max=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]+1);')
	depends+=("perl>=$_perlver_min" "perl<$_perlver_max")
fi
# template end;
}
