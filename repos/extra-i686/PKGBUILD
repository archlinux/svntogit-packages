# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=fontforge
pkgver=20141014
pkgrel=1
pkgdesc='Outline and bitmap font editor'
url='http://www.fontforge.org/'
arch=('i686' 'x86_64')
license=('BSD')
makedepends=('git')
depends=('libltdl' 'libxkbui' 'libxi'
         'pango' 'giflib' 'libtiff' 'libspiro' 'libxml2' 'libspiro' 'libunicodenames' 'zeromq'
         'python2' 'desktop-file-utils' 'gtk-update-icon-cache' 'hicolor-icon-theme')
source=("https://github.com/fontforge/fontforge/releases/download/${pkgver}/fontforge-${pkgver}.tar.gz"
        'http://fontforge.org/cidmaps.tgz')
sha1sums=('b366293e423a94d213824368460fa80f9a1ad810'
          'efbc7c9d3e95159f5600dc71f9cccb370e46bb94')

options=('!makeflags')
install=install

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	export LIBZMQ_LIBS=-lzmq
	export PYTHON=python2
	./bootstrap
	./configure \
		--prefix=/usr \
		--mandir=/usr/share/man \
		--enable-python-extension \

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	install -d "${pkgdir}/usr/share/fontforge"
	install -m644 ../Adobe-* "${pkgdir}/usr/share/fontforge"

	install -d "${pkgdir}/usr/share/icons"
	cp -a desktop/icons "${pkgdir}/usr/share/icons/hicolor"
	rm -fr "${pkgdir}/usr/share/icons/hicolor/src"

	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 desktop/fontforge.desktop "${pkgdir}/usr/share/applications/fontforge.desktop"
}
