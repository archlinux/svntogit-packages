# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: M Rawash <mrawash@gmail.com>

pkgname=xf86-input-wacom
pkgver=0.23.0
pkgrel=2
pkgdesc="X.Org Wacom tablet driver"
arch=(i686 x86_64)
url="http://linuxwacom.sourceforge.net/"
license=(GPL)
backup=('etc/X11/xorg.conf.d/50-wacom.conf')
depends=(libxi libxinerama libxrandr)
makedepends=('xorg-server-devel' 'X-ABI-XINPUT_VERSION=20' 'resourceproto' 'scrnsaverproto')
conflicts=('xorg-server<1.15.0' 'X-ABI-XINPUT_VERSION<20' 'X-ABI-XINPUT_VERSION>=21')
optdepends=('inputattach: support for serial tablets')
source=(http://downloads.sourceforge.net/project/linuxwacom/$pkgname/$pkgname-$pkgver.tar.bz2
        70-wacom.rules)
md5sums=('353642b8a3f2dde089e913be32955aae'
         'cc46dcb399d5ee62c9feb610c60d1270')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --with-xorg-conf-dir=/etc/X11/xorg.conf.d
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  install -Dm644 ../70-wacom.rules "$pkgdir/usr/lib/udev/rules.d/70-wacom.rules"
}
