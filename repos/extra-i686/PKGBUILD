# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

pkgname=libxcb
pkgver=1.9.1
pkgrel=2
pkgdesc="X11 client-side library"
arch=('i686' 'x86_64')
url="http://xcb.freedesktop.org/"
depends=('xcb-proto>=1.8-2' 'libxdmcp' 'libxau')
makedepends=('pkgconfig' 'libxslt' 'python')
license=('custom')
source=($url/dist/$pkgname-$pkgver.tar.bz2
        libxcb-1.1-no-pthread-stubs.patch
        reorder-configure.patch)
sha256sums=('d44a5ff4eb0b9569e6f7183b51fdaf6f58da90e7d6bfc36b612d7263f83e362f'
            '1ec7a6e7dc8639baa6c3fbc61ca8556108fb824598bd6ab5ef1fa50d48d6d310'
            '3e1adc51246e1f48545c1ab9703219c987685d241f1eab5d912b0840ce8a122a')

prepare() {
  cd "$pkgname-$pkgver"
  patch -Np1 -i ../libxcb-1.1-no-pthread-stubs.patch
  # https://bugs.freedesktop.org/show_bug.cgi?id=66413
  patch -Np1 -i ../reorder-configure.patch
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr \
    --enable-xinput \
    --enable-xkb \
    --disable-static
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -m755 -d "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname"
}
