# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kio
pkgver=5.13.0
pkgrel=2
pkgdesc='Resource and network access abstraction'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kio'
license=('LGPL')
depends=('solid' 'karchive' 'kjobwidgets' 'kbookmarks' 'libxslt' 'kwallet'
        'desktop-file-utils')
makedepends=('extra-cmake-modules' 'kdoctools')
optdepends=('kio-extras: extra protocols support (sftp, fish and more)')
groups=('kf5')
install=kio.install
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz" 'custom-folder-icons.patch')
md5sums=('315c79dc4fb128343d18bfd9ef18a726'
         '9b534ca29ef53644344541c5c770ff51')

prepare() {
  mkdir -p build

# Fix custom folder icons https://bugs.kde.org/show_bug.cgi?id=350612
  cd $pkgname-$pkgver
  patch -p1 -i "$srcdir"/custom-folder-icons.patch
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DLIBEXEC_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
