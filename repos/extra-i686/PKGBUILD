# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=dmidecode
pkgver=3.1
pkgrel=1
pkgdesc="Desktop Management Interface table related utilities"
arch=('i686' 'x86_64')
url="http://www.nongnu.org/dmidecode"
license=('GPL')
depends=('glibc')
source=("http://download.savannah.nongnu.org/releases/${pkgname}/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha256sums=('d766ce9b25548c59b1e7e930505b4cad9a7bb0b904a1a391fbb604d529781ac0'
            'SKIP')
validpgpkeys=('90DFD6523C57373D81F63D19865688D038F02FC8') # Jean Delvare <khali@linux-fr.org>

build() {
  cd ${pkgname}-${pkgver}

  sed -i "s:sbin:bin:g" Makefile
  make prefix=/usr
}

package() {
  cd ${pkgname}-${pkgver}

  make prefix=/usr DESTDIR="${pkgdir}" install
}
