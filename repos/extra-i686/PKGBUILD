# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor : Ionut Biru <ibiru@archlinux.org>
# Contributor: Timm Preetz <timm@preetz.us>

pkgname=vala
pkgver=0.36.3
pkgrel=1
pkgdesc='Compiler for the GObject type system'
url='https://wiki.gnome.org/Projects/Vala'
arch=('i686' 'x86_64')
license=('LGPL')
depends=('glib2')
makedepends=('libxslt' 'git' 'vala')
checkdepends=('dbus' 'libx11' 'gobject-introspection')
_commit=b07797b3b60e426cb38fe274c7e1d4885a23d2b3  # tags/0.36.3^0
source=("git+https://git.gnome.org/browse/vala#commit=${_commit}")
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname}
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd ${pkgname}
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd ${pkgname}
  ./configure --prefix=/usr --enable-vapigen
  make
}

check() {
  cd ${pkgname}
  make check
}

package() {
  cd ${pkgname}
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
