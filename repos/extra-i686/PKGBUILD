# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-xinput
pkgver=1.6.2
pkgrel=1
pkgdesc="Small commandline tool to configure devices"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libx11' 'libxi>=1.5.99.2' 'xorg-xrandr' 'libxinerama')
makedepends=('xorg-util-macros' 'inputproto' 'libxfixes')
groups=('xorg-apps' 'xorg')
source=(http://xorg.freedesktop.org/archive/individual/app/xinput-${pkgver}.tar.bz2)
sha256sums=('3694d29b4180952fbf13c6d4e59541310cbb11eef5bf888ff3d8b7f4e3aee5c4')

build() {
  cd xinput-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd xinput-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
