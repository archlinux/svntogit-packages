# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
pkgname=libvpx
pkgver=0.9.7.p1
realver=0.9.7-p1
pkgrel=1
pkgdesc="The VP8 Codec SDK"
arch=('i686' 'x86_64')
url="http://www.webmproject.org/"
license=('BSD')
depends=('glibc')
makedepends=('yasm')
source=(http://webm.googlecode.com/files/${pkgname}-v${realver}.tar.bz2)
sha1sums=('dacfefaf3363f781de43858f09cdd0b0d469e6fc')

build() {
    cd "${srcdir}/${pkgname}-v${realver}"
    ./configure --prefix=/usr --enable-vp8 \
        --enable-runtime-cpu-detect \
        --enable-shared \
        --enable-postproc \
        --enable-pic \
        --disable-install-docs \
        --disable-install-srcs
    make
}

package() {
    cd "${srcdir}/${pkgname}-v${realver}"
    make DIST_DIR="$pkgdir/usr" install
    install -D -m 0644 LICENSE "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}
