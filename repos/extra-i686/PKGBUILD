# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=tcpdump
pkgver=4.7.3
pkgrel=2
pkgdesc="A tool for network monitoring and data acquisition"
arch=('i686' 'x86_64')
url="http://www.tcpdump.org"
license=('BSD')
depends=('libpcap' 'openssl')
source=("http://www.tcpdump.org/release/${pkgname}-${pkgver}.tar.gz"
        'tcpdump_duplicate_executable.patch'
        'CVE-2015-3138.patch')
md5sums=('334a4761594b29f80793c1303f0f4acc'
         '01379e4a12a208ce951c6c2abe80300f'
         '4154b0811d56fb05c3839695923bf46c')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # do not install tcpdump-4.1.1 executable (FS#23346)
  patch -Np1 -i "${srcdir}/tcpdump_duplicate_executable.patch"
  # CVE-2015-3138
  patch -Np1 -i "${srcdir}/CVE-2015-3138.patch"
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr \
              --sbindir=/usr/bin \
              --enable-ipv6
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  #install the license
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
