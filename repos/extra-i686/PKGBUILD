# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=pygobject
pkgver=2.26.0
pkgrel=2
pkgdesc="Python bindings for GObject"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('python2' 'glib2>=2.26.0' 'pycairo>=1.8.10' 'gobject-introspection>=0.9.6')
source=(http://ftp.gnome.org/pub/gnome/sources/pygobject/2.26/${pkgname}-${pkgver}.tar.bz2)
options=('!libtool')
url="http://www.pygtk.org/"
sha256sums=('5554acff9c27b647144143b0459359864e4a6f2ff62c7ba21cf310ad755cf7c7')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  PYTHON=python2 ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  find "$pkgdir"/usr/share/pygobject -name '*.py' | \
    xargs sed -i "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|"
}
