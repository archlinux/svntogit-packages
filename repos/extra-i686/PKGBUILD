# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=xfce4-terminal
pkgver=0.6.2
pkgrel=2
pkgdesc="A modern terminal emulator primarly for the Xfce desktop environment"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('GPL2')
groups=('xfce4')
depends=('libxfce4ui' 'vte' 'hicolor-icon-theme')
makedepends=('intltool')
conflicts=('terminal')
replaces=('terminal')
install=$pkgname.install
source=(http://archive.xfce.org/src/apps/$pkgname/0.6/$pkgname-$pkgver.tar.bz2
        xfce4-terminal-0.6.2-fix-up-the-encoding-menu-creation.patch)
sha256sums=('3d92422288d26311880af694bb4e02c9235997ca307a5e85001bf6bef65c3b35'
            '4708b6f223fe8ecc34f8f2c8f9b9d18c2ff8f7ff13300715f7a23e49cfb539fe')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # https://bugzilla.xfce.org/show_bug.cgi?id=10395
  patch -Np1 -i "$srcdir/xfce4-terminal-0.6.2-fix-up-the-encoding-menu-creation.patch"
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/xfce4 \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
