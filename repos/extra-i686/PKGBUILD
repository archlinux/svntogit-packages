# $Id$
# Contributor: Alexander Baldeck <alexander@archlinux.org>
# Contributor: Dale Blount <dale@archlinux.org>
# Contributor: Anders Bostrom <anders.bostrom@home.se>
pkgname=thunderbird
pkgver=6.0.1
pkgrel=1
pkgdesc="Standalone Mail/News reader"
arch=('i686' 'x86_64')
license=('MPL' 'GPL')
url="http://www.mozilla.org/thunderbird/"
depends=('gtk2' 'gcc-libs' 'mozilla-common' 'nss' 'libxt' 'shared-mime-info' 'alsa-lib' 'dbus-glib' 'hunspell' 'sqlite3>=3.7.4' 'desktop-file-utils' 'libnotify' 'libevent')
makedepends=('unzip' 'zip' 'pkg-config' 'python2' 'libidl2' 'wireless_tools' 'gconf' 'yasm' 'mesa')
optdepends=('libcanberra: for sound support')
options=(!buildflags)
install=thunderbird.install
source=(ftp://ftp.mozilla.org/pub/mozilla.org/thunderbird/releases/${pkgver}/source/thunderbird-${pkgver}.source.tar.bz2
        mozconfig
        thunderbird.desktop
        thunderbird-3.0-lang.patch)
md5sums=('603072c341a49c8f7268cf75b8f5a8dd'
         'b8fd97ab4bfb2e4538b1e1340d70f915'
         'af3e5b344d2edf1c7d61bb0a5a96de9a'
         '25b6fe16ac24cd5c852213e5c1adb272')

build() {
  cd "${srcdir}/comm-release"
  patch -Np1 -i "${srcdir}/thunderbird-3.0-lang.patch"

  cp "${srcdir}/mozconfig" .mozconfig

  export LDFLAGS="-Wl,-rpath,/usr/lib/thunderbird-${pkgver} -Wl,-O1,--sort-common,--hash-style=gnu,--as-needed"
  export PYTHON="/usr/bin/python2"

  make -j1 -f client.mk build MOZ_MAKE_FLAGS="$MAKEFLAGS"
}

package() {
  cd "${srcdir}/comm-release"
  make -j1 -f client.mk DESTDIR="${pkgdir}" install

  for i in 16x16 22x22 24x24 32x32 48x48 256x256; do
      install -Dm644 other-licenses/branding/thunderbird/mailicon${i/x*/}.png \
          "$pkgdir/usr/share/icons/hicolor/$i/apps/thunderbird.png"
  done

  install -Dm644 "${srcdir}/thunderbird.desktop" \
      "${pkgdir}/usr/share/applications/thunderbird.desktop"

  rm -rf "${pkgdir}/usr/lib/thunderbird-${pkgver}/dictionaries"
  ln -sf /usr/share/myspell/dicts "${pkgdir}/usr/lib/thunderbird-${pkgver}/dictionaries"

  # We don't want the development stuff
  rm -r "${pkgdir}"/usr/{include,lib/thunderbird-devel-${pkgver},share/idl}
}

