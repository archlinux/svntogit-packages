# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=cantor
pkgver=15.08.3
pkgrel=1
pkgdesc="KDE Frontend to Mathematical Software"
url="http://kde.org/applications/education/cantor/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdeedu')
depends=('analitza' 'libspectre' 'kdelibs4support' 'kpty' 'ktexteditor' 'knewstuff' 'libqalculate')
makedepends=('extra-cmake-modules' 'python' 'python2' 'kdoctools' 'luajit' 'r')
optdepends=('maxima: Maxima backend'
            'octave: Octave backend'
            'r: R backend'
            'luajit: LUA backend'
            'python: Python 3 backend'
	    'python2: Python 2 backend'
            'sagemath: SageMath backend')
conflicts=('kdeedu-cantor')
replaces=('kdeedu-cantor')
install=${pkgname}.install
source=("http://download.kde.org/stable/applications/${pkgver}/src/cantor-${pkgver}.tar.xz" python-3.5.patch)
sha1sums=('4e9f5632f16417e9ded39a46829420063bb2fc3e'
          'd6d04b323e4d301587ebb82e88672c0cee923bc1')

prepare() {
  mkdir -p build

# Fix build against python 3.5
  cd $pkgname-$pkgver
  patch -p1 -i ../python-3.5.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DPYTHON_INCLUDE_DIR=/usr/include/python2.7 \
    -DPYTHON_LIBRARY=/usr/lib/libpython2.7.so
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
