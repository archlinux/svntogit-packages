# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgbase="sqlite"
pkgname=('sqlite' 'sqlite-tcl' 'sqlite-doc') 
_amalgamationver=3080403
_docver=${_amalgamationver}
#_docver=3080001
pkgver=3.8.4.3
pkgrel=2
pkgdesc="A C library that implements an SQL database engine"
arch=('i686' 'x86_64')
license=('custom:Public Domain')
url="http://www.sqlite.org/"
makedepends=('tcl' 'readline')
source=( # tarball containing the amalgamation for SQLite >= 3.7.5 together with a configure script and makefile for building it; includes now also the Tcl Extension Architecture (TEA)
	http://www.sqlite.org/2014/sqlite-autoconf-$_amalgamationver.tar.gz
	http://www.sqlite.org/2014/sqlite-doc-${_docver}.zip
	license.txt
	sqlite-nautilus.patch)
options=('!emptydirs')
sha1sums=('70f3b100fa22e5bfebfe1b0a2102612e3c6c53fb'
          'ce8615799a9da7fc9d2cbcd2774d77da4ba72417'
          'f34f6daa4ab3073d74e774aad21d66878cf26853'
          '4e9a4f80c91bbaf80f0f45b9b97fb1cbe3464bc7')

prepare() {
  cd "$srcdir"/sqlite-autoconf-$_amalgamationver
  # https://bugs.archlinux.org/task/39861
  patch -Np1 -i ${srcdir}/sqlite-nautilus.patch
}

build() {
  export CFLAGS="$CFLAGS -DSQLITE_ENABLE_FTS3=1 -DSQLITE_ENABLE_COLUMN_METADATA=1 -DSQLITE_ENABLE_UNLOCK_NOTIFY -DSQLITE_SECURE_DELETE"

  # build sqlite
  cd "$srcdir"/sqlite-autoconf-$_amalgamationver

  ./configure --prefix=/usr \
	--disable-static
  make
  
  # build the tcl extension    
  cd "$srcdir"/sqlite-autoconf-$_amalgamationver/tea
  ./configure --prefix=/usr \
	--with-system-sqlite
  make

}

package_sqlite() {
 
 pkgdesc="A C library that implements an SQL database engine"
 depends=('readline')
 provides=("sqlite3=$pkgver")
 replaces=("sqlite3")
 
  cd ${srcdir}/sqlite-autoconf-$_amalgamationver
  make DESTDIR=${pkgdir} install
  
  # license - no linking required because pkgbase=pkgname
  install -D -m644 ${srcdir}/license.txt ${pkgdir}/usr/share/licenses/${pkgbase}/license.txt
}

package_sqlite-tcl() {
 
 pkgdesc="sqlite Tcl Extension Architecture (TEA)"
 depends=('sqlite')
 provides=("sqlite3-tcl=$pkgver")
 replaces=("sqlite3-tcl")
 
  cd ${srcdir}/sqlite-autoconf-$_amalgamationver/tea
  make DESTDIR=${pkgdir} install
  
  # link license
  install -m755 -d ${pkgdir}/usr/share/licenses
  ln -sf /usr/share/licenses/${pkgbase} "${pkgdir}/usr/share/licenses/${pkgname}"
}

package_sqlite-doc() {

 pkgdesc="most of the static HTML files that comprise this website, including all of the SQL Syntax and the C/C++ interface specs and other miscellaneous documentation"
 #arch=('any') - not yet supported
 provides=("sqlite3-doc=$pkgver")
 replaces=("sqlite3-doc")
 
  #cd ${srcdir}/sqlite-doc-${_amalgamationver}
  cd ${srcdir}/sqlite-doc-${_docver}
  mkdir -p ${pkgdir}/usr/share/doc/${pkgbase}
  cp -R *  ${pkgdir}/usr/share/doc/${pkgbase}/
  
  # fix permissions and remove obsolete files; https://bugs.archlinux.org/task/24605
  find ${pkgdir} -type f -perm 755 -exec ls -lha {} \;
  find ${pkgdir} -type f -perm 755 -exec chmod 644 {} \;

  find ${pkgdir} -type f -name '*~' -exec ls -lha {} \;
  find ${pkgdir} -type d -name '*~' -exec ls -lha {} \;
  find ${pkgdir} -name '*~' -exec rm -f {} \;

  find ${pkgdir} -type f -name '.~*' -exec ls -lha {} \; # /build/pkg/sqlite-doc/usr/share/doc/sqlite/images/fileformat/.~lock.indexpage.odg#
  find ${pkgdir} -type d -name '.~*' -exec ls -lha {} \;
  find ${pkgdir} -name '.~*' -exec rm -f {} \;
}
