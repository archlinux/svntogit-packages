# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-themes-standard
pkgver=3.20
pkgrel=1
pkgdesc="Default themes for the GNOME desktop"
arch=(i686 x86_64)
url="http://www.gnome.org"
license=(GPL2)
depends=(cantarell-fonts ttf-dejavu librsvg)
makedepends=(intltool gtk2 gtk3)
optdepends=('gtk-engines: HighContrast GTK2 theme')
groups=(gnome)
replaces=(gnome-themes)
conflicts=(gnome-themes)
options=('!emptydirs')
source=(http://download.gnome.org/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('1cde84b34da310e6f2d403bfdbe9abb0798e5f07a1d1b4fde82af8e97edd3bdc')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
