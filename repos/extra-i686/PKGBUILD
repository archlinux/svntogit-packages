# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=klines
pkgver=15.08.3
pkgrel=1
pkgdesc="A simple but highly addictive, one player game"
url="http://kde.org/applications/games/klines/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegames')
depends=('libkdegames')
makedepends=('extra-cmake-modules' 'python' 'kdoctools')
install=${pkgname}.install
conflicts=('kdegames-klines')
replaces=('kdegames-klines')
source=("http://download.kde.org/stable/applications/${pkgver}/src/klines-${pkgver}.tar.xz")
sha1sums=('1d2ea0df9d16fde1d3000a8b1bc5b857a41ca170')

build() {
  mkdir -p build
  cd build
  cmake ../klines-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
