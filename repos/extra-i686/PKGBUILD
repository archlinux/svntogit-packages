# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=mailcommon
pkgver=16.12.1
pkgrel=1
pkgdesc="KDE PIM library providing support for mail applications"
arch=(i686 x86_64)
url="https://community.kde.org/KDE_PIM"
license=(LGPL)
depends=(mailimporter messagelib)
makedepends=(extra-cmake-modules kdoctools python boost qt5-tools kdesignerplugin)
conflicts=('libkdepim<16')
source=("http://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
md5sums=('a121027cfc921bdfe77061c0aea8310f'
         'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
