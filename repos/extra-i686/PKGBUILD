# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>

pkgname=graphite
pkgver=1.0.3
pkgrel=1
epoch=1
arch=('i686' 'x86_64')
url="http://graphite.sil.org/"
pkgdesc='reimplementation of the SIL Graphite text processing engine'
license=('custom_SIL Dual license')
depends=('gcc-libs')
makedepends=('cmake' 'freetype2')
options=('!libtool' '!emptydirs')
source=("http://downloads.sourceforge.net/project/silgraphite/graphite2/graphite2-${pkgver}.tgz")
md5sums=('3bf481ca95109b14435125c0dd1f2217')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake -G "Unix Makefiles" ../graphite2-${pkgver} \
	-DCMAKE_INSTALL_PREFIX=/usr
  make
}

check() {
  cd "${srcdir}"/build
  ctest
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="$pkgdir/" install

  # licenses
  mkdir -p "${pkgdir}"/usr/share/licenses/${pkgname}
  install -m644 "${srcdir}"/graphite2-${pkgver}/COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/
}
