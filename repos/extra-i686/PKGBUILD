# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kio
pkgver=5.9.0
pkgrel=2
pkgdesc='Resource and network access abstraction'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kio'
license=('LGPL')
depends=('solid' 'karchive' 'kjobwidgets' 'kbookmarks' 'libxslt' 'kwallet'
        'desktop-file-utils')
makedepends=('extra-cmake-modules' 'kdoctools')
groups=('kf5')
install=kio.install
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz" 'dolphin-places.patch')
md5sums=('2fe89d374583bf38a091edc74b9bc1d3'
         '9700937823277d19f6f7d440f63f0954')

prepare() {
  mkdir -p build

# Fix disappearing bookmarks in Dolphin https://bugs.kde.org/show_bug.cgi?id=345174
  cd $pkgname-$pkgver
  patch -p1 -i "$srcdir"/dolphin-places.patch
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DLIBEXEC_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
