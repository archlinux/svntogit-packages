# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=networkmanager-openconnect
pkgver=0.9.10.0
pkgrel=2
pkgdesc="NetworkManager VPN integration for openconnect"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnome.org/projects/NetworkManager/"
depends=("networkmanager>=${pkgver::6}" 'openconnect' 'gtk3' 'libsecret')
makedepends=('intltool')
optdepends=('network-manager-applet: GNOME frontends to NetWorkmanager')
install=$pkgname.install
source=(http://ftp.gnome.org/pub/GNOME/sources/NetworkManager-openconnect/0.9/NetworkManager-openconnect-${pkgver}.tar.xz
        libopenconnect.so.4.patch
        libopenconnect.so.5.patch)
sha256sums=('8007d70a6943388bfa141f99fc4da9965fd122cf2741512ce0cf9c0f9c78331a'
            'e2a84ac7467b5ffb20891946fd132f9d34012f17a4847ab7c7cde34cca3a9762'
            '6b46edb11f96e0ad2b0fd39b11326b24520d9d2ff35a1d167a98c8bc5b7c6272')

prepare() {
  cd NetworkManager-openconnect-${pkgver}
  patch -p1 -i ../libopenconnect.so.4.patch
  patch -p1 -i ../libopenconnect.so.5.patch
}

build() {
  cd NetworkManager-openconnect-${pkgver}
  ./configure --prefix=/usr \
  	--sysconfdir=/etc \
	--libexecdir=/usr/lib/networkmanager \
	--enable-more-warnings=yes \
	--disable-static
  make
}

package() {
  cd NetworkManager-openconnect-${pkgver}
  make DESTDIR="${pkgdir}" install
}
