# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-xset
pkgver=1.2.2
pkgrel=2
pkgdesc="User preference utility for X"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libxmu')
makedepends=('xorg-util-macros')
groups=('xorg-apps' 'xorg')
source=(http://xorg.freedesktop.org/archive/individual/app/xset-${pkgver}.tar.bz2)
sha256sums=('61371c140030b8b05075a1378b34a4d7c438ed9159496a95f10782c6f4aec1e8')

build() {
  cd "${srcdir}/xset-${pkgver}"
  ./configure --prefix=/usr --without-fontcache --without-xf86misc
  make
 }
 
package() {
  cd "${srcdir}/xset-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
