# $Id$
# Maintainer: tobias <tobias at archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=xfce4-clipman-plugin
pkgver=1.2.2
pkgrel=1
pkgdesc="A clipboard plugin for the Xfce4 panel"
arch=('i686' 'x86_64')
license=('GPL')
url="http://goodies.xfce.org/projects/panel-plugins/xfce4-clipman-plugin"
groups=('xfce4-goodies')
depends=('xfce4-panel' 'libunique' 'libxtst')
makedepends=('intltool')
options=('!libtool')
install=${pkgname}.install
source=(http://archive.xfce.org/src/panel-plugins/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2)
md5sums=('60df7769a8271d7fcab177210f4291e8')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i 's|$(TARGET_DIR)/C/media|$(docdir)/html/C/media|' doc/Makefile.in
  ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--libexecdir=/usr/lib \
	--localstatedir=/var \
	--disable-static \
	--enable-unique \
	--disable-debug
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
