# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-lazy-object-proxy
pkgname=(python-lazy-object-proxy python2-lazy-object-proxy)
pkgver=1.2.1
pkgrel=1
pkgdesc='A fast and thorough lazy object proxy'
arch=('i686' 'x86_64')
url="https://github.com/ionelmc/python-lazy-object-proxy"
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools' 'git')
checkdepends=('python-pytest-capturelog' 'python2-pytest-capturelog'
              'python-pytest-benchmark' 'python2-pytest-benchmark')
source=("git+https://github.com/ionelmc/python-lazy-object-proxy#tag=v$pkgver")
sha256sums=('SKIP')

prepare() {
  cp -a python-lazy-object-proxy{,-py2}
}

build() {
  cd "${srcdir}/python-lazy-object-proxy"
  python setup.py build

  cd "${srcdir}/python-lazy-object-proxy-py2"
  python2 setup.py build
}

check() {
  cd "${srcdir}/python-lazy-object-proxy"
  LC_CTYPE=en_US.UTF-8 PYTHONPATH="$PWD/build/lib.linux-$CARCH-3.5:$PYTHONPATH" py.test --ignore=src

  cd "${srcdir}/python-lazy-object-proxy-py2"
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-2.7:$PYTHONPATH" py.test2 --ignore=src
}

package_python-lazy-object-proxy() {
  depends=("python")

  cd "${srcdir}/python-lazy-object-proxy"
  python setup.py install --root="${pkgdir}" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-lazy-object-proxy() {
  depends=("python2")

  cd "${srcdir}/python-lazy-object-proxy"
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
