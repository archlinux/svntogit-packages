# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=libkdcraw
pkgver=4.11.0
pkgrel=1
pkgdesc="A C++ interface used to decode RAW picture"
url="https://projects.kde.org/projects/kde/kdegraphics/libs/libkdcraw"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdelibs' 'lcms2')
makedepends=('cmake' 'automoc4')
replaces=('kdegraphics-libs')
conflicts=('kdegraphics-libs')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/${pkgname}-${pkgver}.tar.xz")
sha1sums=('35618bcdf322de457669e117df28b6843ae00853')

build() {
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_LCMS2=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
