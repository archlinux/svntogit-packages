# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=paprefs
pkgver=0.9.10
pkgrel=6
pkgdesc="A simple GTK-based configuration dialog for PulseAudio"
arch=(i686 x86_64)
url="http://0pointer.de/lennart/projects/paprefs"
license=(GPL)
depends=(gconfmm libglademm libsigc++ pulseaudio-gconf)
makedepends=(intltool lynx git)
_commit=6b50202f3f11bc5c016e41b8f0c529860a6baabb
source=("git://anongit.freedesktop.org/pulseaudio/paprefs#commit=$_commit")
sha256sums=('SKIP')

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./bootstrap.sh
}

build() {
  cd $pkgname
  CXXFLAGS+=' -std=c++11'
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
  install -D doc/README "$pkgdir/usr/share/doc/$pkgname/README"
}
