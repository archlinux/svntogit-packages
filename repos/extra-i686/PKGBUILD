# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdepim-runtime
pkgver=4.14.9
_kdeappver=15.04.2
pkgrel=1
pkgdesc='Extends the functionality of kdepim'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kdepim-runtime'
license=('GPL' 'LGPL' 'FDL')
depends=('kdebase-runtime' 'libkgapi' 'libkolab' 'libkfbapi' 'libaccounts-qt' 'libsignon-qt4')
makedepends=('cmake' 'automoc4' 'boost')
install="${pkgname}.install"
source=("http://download.kde.org/stable/applications/${_kdeappver}/src/${pkgname}-${pkgver}.tar.xz")
sha1sums=('5656c082f659ac241149fce9fbcca59fc83511ed')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
