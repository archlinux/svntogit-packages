# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=gvfs
pkgname=(gvfs gvfs-{smb,afc,gphoto2,goa,mtp,nfs,google})
pkgver=1.26.2
pkgrel=1
arch=(i686 x86_64)
license=(LGPL)
makedepends=(avahi dbus fuse intltool libarchive libcdio-paranoia libgphoto2 libimobiledevice
             libsoup smbclient udisks2 libsecret docbook-xsl gtk3 libmtp gnome-online-accounts
             libbluray libgudev libnfs libgdata)
url="http://www.gnome.org"
groups=(gnome)
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgbase/${pkgver:0:4}/$pkgbase-$pkgver.tar.xz)
sha256sums=('695b6e0f3de5ac2cb4d188917edef3f13299328150a2413f1a7131d9b2d48d18')

prepare() {
  cd "$pkgbase-$pkgver"
}

build() {
  cd "$pkgbase-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --libexecdir=/usr/lib/gvfs \
      --with-bash-completion-dir=/usr/share/bash-completion/completions
  make
}

package_gvfs() {
  pkgdesc="Userspace virtual filesystem implemented as a pluggable module for gio"
  depends=(avahi dconf fuse libarchive libcdio-paranoia libsoup udisks2 libsecret libbluray libgudev gcr)
  replaces=(gvfs-{obexftp,afp})
  optdepends=('gvfs-afc: AFC (mobile devices) support'
              'gvfs-smb: SMB/CIFS (Windows client) support'
              'gvfs-gphoto2: gphoto2 (PTP camera/MTP media player) support'
              'gvfs-mtp: MTP device support'
              'gvfs-goa: gnome-online-accounts (e.g. OwnCloud) support'
              'gvfs-nfs: NFS support'
              'gvfs-google: Google Drive support'
              'gtk3: Recent files support')
  install=gvfs.install

  cd "$pkgbase-$pkgver"
  sed -e 's/^am__append_4/#am__append_4/' \
      -e 's/^am__append_5/#am__append_5/' \
      -e 's/^am__append_6/#am__append_6/' \
      -e 's/^am__append_7/#am__append_7/' \
      -i monitor/Makefile
  make DESTDIR="$pkgdir" install

  cd "$pkgdir"
  rm usr/lib/gvfs/gvfsd-{smb,smb-browse,afc,gphoto2,mtp,nfs,google}
  rm usr/share/gvfs/mounts/{smb,smb-browse,afc,gphoto2,mtp,nfs,google}.mount
  rm usr/share/glib-2.0/schemas/org.gnome.system.smb.gschema.xml
  rm usr/share/GConf/gsettings/gvfs-smb.convert
}

package_gvfs-smb() {
  pkgdesc="SMB/CIFS (Windows client) backend for gvfs"
  depends=("gvfs=$pkgver" smbclient)
  install=gvfs-smb.install

  cd "$pkgbase-$pkgver/daemon"
  install -m755 -d "$pkgdir/usr/lib/gvfs"
  install -m755 -d "$pkgdir/usr/share/gvfs/mounts"

  install -m755 .libs/gvfsd-smb{,-browse} "$pkgdir/usr/lib/gvfs/"
  install -m644 smb{,-browse}.mount "$pkgdir/usr/share/gvfs/mounts/"

  install -Dm644 org.gnome.system.smb.gschema.xml \
    "$pkgdir/usr/share/glib-2.0/schemas/org.gnome.system.smb.gschema.xml"
  install -Dm644 gvfs-smb.convert \
    "$pkgdir/usr/share/GConf/gsettings/gvfs-smb.convert"
}

package_gvfs-afc() {
  pkgdesc="AFC (Apple mobile devices) backend for gvfs"
  depends=("gvfs=$pkgver" libimobiledevice usbmuxd)
  install=gvfs-module.install

  cd "$pkgbase-$pkgver/daemon"
  install -D .libs/gvfsd-afc "$pkgdir/usr/lib/gvfs/gvfsd-afc"
  install -Dm644 afc.mount "$pkgdir/usr/share/gvfs/mounts/afc.mount"

  cd "$srcdir/$pkgbase-$pkgver/monitor/afc"
  make DESTDIR="$pkgdir" install
}

package_gvfs-gphoto2() {
  pkgdesc="gphoto2 (PTP camera, MTP media player) backend for gvfs"
  depends=("gvfs=$pkgver" libgphoto2)
  install=gvfs-module.install

  cd "$pkgbase-$pkgver/daemon"
  install -D .libs/gvfsd-gphoto2 "$pkgdir/usr/lib/gvfs/gvfsd-gphoto2"
  install -Dm644 gphoto2.mount "$pkgdir/usr/share/gvfs/mounts/gphoto2.mount"

  cd "$srcdir/$pkgbase-$pkgver/monitor/gphoto2"
  make DESTDIR="$pkgdir" install
}

package_gvfs-mtp() {
  pkgdesc="MTP (Android, media player) backend for gvfs"
  depends=("gvfs=$pkgver" libmtp)
  install=gvfs-module.install

  cd "$pkgbase-$pkgver/daemon"
  install -D .libs/gvfsd-mtp "$pkgdir/usr/lib/gvfs/gvfsd-mtp"
  install -Dm644 mtp.mount "$pkgdir/usr/share/gvfs/mounts/mtp.mount"

  cd "$srcdir/$pkgbase-$pkgver/monitor/mtp"
  make DESTDIR="$pkgdir" install
}

package_gvfs-goa() {
  pkgdesc="Gnome Online Accounts (webservices) backend for gvfs"
  depends=("gvfs=$pkgver" gnome-online-accounts)
  install=gvfs-module.install

  cd "$srcdir/$pkgbase-$pkgver/monitor/goa"
  make DESTDIR="$pkgdir" install
}

package_gvfs-nfs() {
  pkgdesc="NFS backend for gvfs"
  depends=("gvfs=$pkgver" libnfs)
  install=gvfs-nfs.install

  cd "$pkgbase-$pkgver/daemon"
  install -D .libs/gvfsd-nfs "$pkgdir/usr/lib/gvfs/gvfsd-nfs"
  install -Dm644 nfs.mount "$pkgdir/usr/share/gvfs/mounts/nfs.mount"
}

package_gvfs-google() {
  pkgdesc="Google Drive backend for gvfs"
  depends=("gvfs-goa=$pkgver" libgdata)
  install=gvfs-module.install

  cd "$pkgbase-$pkgver/daemon"
  install -D .libs/gvfsd-google "$pkgdir/usr/lib/gvfs/gvfsd-google"
  install -Dm644 google.mount "$pkgdir/usr/share/gvfs/mounts/google.mount"
}
