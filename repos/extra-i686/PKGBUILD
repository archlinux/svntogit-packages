# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdenetwork-filesharing
pkgver=17.04.1
pkgrel=1
pkgdesc='Properties dialog plugin to share a directory with the local network'
url='https://www.kde.org/applications/internet/'
arch=(i686 x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kdenetwork)
depends=(kio)
makedepends=(extra-cmake-modules kdoctools python)
optdepends=('samba: folder sharing')
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('dd23c6e44cbdace25e306372a55b1086c38804977620f7105329b176ee7b3693'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
