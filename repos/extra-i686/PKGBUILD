# Packager: Justin Davis <jrcd83@gmail.com>
# $Id$

pkgname=perl-ev
pkgver=4.03
pkgrel=1
pkgdesc="perl interface to libev, a high performance full-featured event loop"
arch=(i686 x86_64)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=('perl-common-sense')
url=https://metacpan.org/release/EV
source=("http://search.cpan.org/CPAN/authors/id/M/ML/MLEHMANN/EV-${pkgver}.tar.gz")
md5sums=(33088705bc34bf66bccde50205c15e9f)
sha512sums=(fecf2014e89c37ca90d61e432d4020b4c59af3877e1c423693f4a86ed0c66325053737ad1c4e20c2c85b1ca65717c811f54fc596da32a853ad142bd7f60c8e03)
_distdir="${srcdir}/EV-${pkgver}"

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$_distdir"
    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    cd "$_distdir"
    make test
  )
}

package() {
    cd "$_distdir"
    make DESTDIR="$pkgdir" install
    find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
