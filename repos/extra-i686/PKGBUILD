# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-todo
pkgver=3.20.1
pkgrel=1
pkgdesc="Task manager for GNOME"
arch=(i686 x86_64)
url="http://gnome.org"
license=(GPL)
depends=(evolution-data-server libpeas)
makedepends=(intltool gobject-introspection)
groups=(gnome-extra)
source=(http://download.gnome.org/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz
        buildfix.patch)
sha256sums=('99748aa787e76027e118f9d002497b63ed45c74f40247db6e6daf1b3426cf46e'
            'ab88726d81ba076dd30067c0ccb08fe946b76b06c6e8664648d2ac1ff496f821')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../buildfix.patch
  autoreconf -fvi
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
