# $Id$
# Maintainer:Antonio Rojas <arojas@archlinux.org>

pkgname=purpose
pkgver=1.0rc1
pkgrel=1
pkgdesc="Framework for providing abstractions to get the developer's purposes fulfilled"
arch=(i686 x86_64)
url='https://projects.kde.org/purpose'
license=(LGPL)
depends=(kaccounts-integration hicolor-icon-theme)
makedepends=(extra-cmake-modules intltool)
source=("http://download.kde.org/unstable/$pkgname/$pkgname-$pkgver.tar.xz")
install=$pkgname.install
md5sums=('57186bef4f8758a0252bd13f10c90fa2')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
