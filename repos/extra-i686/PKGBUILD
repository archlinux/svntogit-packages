# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=solid
pkgver=5.15.0
pkgrel=1
pkgdesc='Hardware integration and detection'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/solid'
license=('LGPL')
depends=('qt5-declarative' 'media-player-info' 'upower' 'udisks2')
makedepends=('extra-cmake-modules' 'qt5-tools')
groups=('kf5')
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
md5sums=('5d18bd8ffd294a85ddf3f1765d8726d6')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
