# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=upower
pkgver=0.99.1
_commit=2199a9b
pkgrel=4
pkgdesc="Abstraction for enumerating power devices, listening to device events and querying history and statistics"
arch=('i686' 'x86_64')
url="http://upower.freedesktop.org"
license=('GPL')
depends=('systemd-tools' 'systemd' 'libusb' 'dbus-glib' 'libimobiledevice')
makedepends=('git' 'intltool' 'gtk-doc' 'docbook-xsl' 'gobject-introspection' 'systemd')
backup=('etc/UPower/UPower.conf')
source=("git://anongit.freedesktop.org/upower#commit=$_commit"
        revert-version.patch)
sha256sums=('SKIP'
            'fc69742f564784c9bfd0e669a3d8bfa2879eb1d288f13c390a139d5ff3715a04')

prepare() {
  cd $pkgname
  patch -Rp1 -i ../revert-version.patch
}

build() {
  cd $pkgname

  ./autogen.sh --prefix=/usr --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/$pkgname \
    --disable-static
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
