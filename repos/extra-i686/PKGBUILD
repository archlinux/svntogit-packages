# $Id$
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>
pkgname=v4l-utils
pkgver=0.8.5
pkgrel=1
pkgdesc="Userspace tools and conversion library for Video 4 Linux"
arch=('i686' 'x86_64')
url="http://freshmeat.net/projects/libv4l"
provides=("libv4l=$pkgver")
replaces=('libv4l')
conflicts=('libv4l')
license=('LGPL')
makedepends=('qt')
optdepends=('qt')
depends=('glibc' 'gcc-libs' 'sysfsutils' 'libjpeg-turbo')
source=(http://linuxtv.org/downloads/v4l-utils/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('c205848fbaf3489b97519dae04da31be4998bbc170a2ca9791357a8a549e594c')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i 's|CFLAGS :=|CFLAGS ?=|' Make.rules

  make PREFIX="/usr"
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make install PREFIX="/usr" DESTDIR="${pkgdir}/"
  rm -f "${pkgdir}/usr/bin/ivtv-ctl"
}
