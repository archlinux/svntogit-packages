# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Lucien Immink <l.immink@student.fnt.hvu.nl>

pkgname=curl
pkgver=7.19.5
pkgrel=2
pkgdesc="An URL retrival utility and library"
arch=('i686' 'x86_64')
url="http://curl.haxx.se"
license=('MIT')
depends=('zlib' 'openssl>=0.9.8b' 'bash' 'ca-certificates')
options=('!libtool')
source=(http://curl.haxx.se/download/${pkgname}-${pkgver}.tar.bz2)
md5sums=('426d161661dce70c8ea9ad8f553363a3')
sha1sums=('e9256f93f5a8c7f40a3c8696c2841a6c5c2b1bf7')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --without-libidn --disable-ldap \
    --with-ca-bundle=/etc/ssl/certs/ca-certificates.crt || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING" || return 1
  install -m755 -d "${pkgdir}/usr/share/aclocal"
  install -m644 docs/libcurl/libcurl.m4 "${pkgdir}/usr/share/aclocal/" || return 1
}
