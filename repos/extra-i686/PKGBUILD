# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Anton Leontiev <bunder@t-25.ru>

pkgname=graphicsmagick
pkgver=1.3.15
pkgrel=2
pkgdesc='Image processing system'
url='http://www.graphicsmagick.org/'
arch=('i686' 'x86_64')
license=('MIT')
makedepends=('perl')
depends=('bzip2' 'freetype2' 'ghostscript' 'jasper' 'lcms2' 'xz'
         'libtiff' 'libwmf' 'libsm' 'libxml2' 'libltdl')
options=('!libtool')
source=("http://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgver}/GraphicsMagick-${pkgver}.tar.xz")
sha1sums=('d5df4000d5531fad3ede9e008f9c8dc2d085d546')

build() {
	cd "${srcdir}/GraphicsMagick-${pkgver}"

	# fix typemap bug
	# https://rt.cpan.org/Public/Bug/Display.html?id=70150
	# http://trac.imagemagick.org/browser/ImageMagick/branches/ImageMagick-6.7.7/PerlMagick/typemap
	echo 'Graphics::Magick T_PTROBJ' > typemap

	./configure \
		--prefix=/usr \
		--with-perl \
		--enable-shared \
		--disable-static \
		--with-gs-font-dir=/usr/share/fonts/Type1 \

	make
}

package() {
	cd "${srcdir}/GraphicsMagick-${pkgver}"

	make DESTDIR="${pkgdir}" install

	# Install MIT license
	install -Dm644 "Copyright.txt" "${pkgdir}/usr/share/licenses/${pkgname}/Copyright.txt"

	# Install perl bindings
	# The patching was introduced in order to build perl module without installing package itself and
	# not to introduce unnecessary path into LD_RUN_PATH
	cd PerlMagick
	sed -i -e "s:'LDDLFLAGS'  => \"\(.*\)\":'LDDLFLAGS'  => \"-L${pkgdir}/usr/lib \1\":" Makefile.PL
	perl Makefile.PL INSTALLDIRS=vendor PREFIX=/usr DESTDIR="${pkgdir}"
	sed -i -e "s/LDLOADLIBS =/LDLOADLIBS = -lGraphicsMagick/" Makefile
	make
	make install

	# Remove perllocal.pod and .packlist
	rm -rf "${pkgdir}/usr/lib/perl5/core_perl"
	rm "${pkgdir}/usr/lib/perl5/vendor_perl/auto/Graphics/Magick/.packlist"
}
