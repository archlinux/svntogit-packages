# $Id$
# Maintainer: RÃ©my Oudompheng <remy@archlinux.org>
# Contributor: Firmicus <francois.archlinux.org>
# Contributor: bender02 at gmx dot com

pkgname=asymptote
pkgver=2.20
pkgrel=1
pkgdesc="A vector graphics language (like metapost)"
arch=('i686' 'x86_64')
url="http://asymptote.sourceforge.net/"
license=("GPL3")
depends=('texlive-core' 'gc' 'freeglut' 'glu' 'gsl' 'fftw' 'libsigsegv')
makedepends=('ghostscript' 'imagemagick')
optdepends=('python2:           for the xasy GUI'
            'python-imaging:    for the xasy GUI'
            'tix:               for the xasy GUI')
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.src.tgz)
install=texlive.install
sha1sums=('4c86ab71cff57a592944303b11d9bb6ec34f1cbd')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --enable-gc=/usr \
              --prefix=/usr \
              --with-latex=/usr/share/texmf/tex/latex \
              --with-context=/usr/share/texmf/tex/context
  make all
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check-all
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make -j1 DESTDIR="${pkgdir}" install-all
  sed -i -e 's@env python@env python2@' ${pkgdir}/usr/share/asymptote/GUI/*.py
  # this dir contains png files that are already embedded in the pdf documentation:
  rm -rf ${pkgdir}/usr/share/info/asymptote
}
