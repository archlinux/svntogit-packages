# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-xclipboard
pkgver=1.1.2
pkgrel=1
pkgdesc="X clipboard manager"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libxaw' 'libxmu' 'libxt' 'libx11')
makedepends=('xorg-util-macros')
source=(http://xorg.freedesktop.org/archive/individual/app/xclipboard-${pkgver}.tar.bz2)
sha1sums=('8dc77e1234c1c2853805124536676a4d5f7c7a3b')

build() {
  cd "${srcdir}/xclipboard-${pkgver}"
  ./configure --prefix=/usr
  make
}

package(){
  cd "${srcdir}/xclipboard-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
