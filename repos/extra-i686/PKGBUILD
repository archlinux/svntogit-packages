# $Id$
#Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libfontenc
pkgver=1.1.1
pkgrel=1
pkgdesc="X11 font encoding library"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('zlib')
makedepends=('pkgconfig' 'xproto>=7.0.18')
options=('!libtool')
source=(${url}/releases/individual/lib/${pkgname}-${pkgver}.tar.bz2)
sha1sums=('90bdd53d75585dd0f06ad7bc23faccd8a01bb733')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --with-encodingsdir=/usr/share/fonts/encodings      
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
