# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Link Dupont <link@subpop.net>

pkgname=devhelp
pkgver=3.8.2
pkgrel=1
pkgdesc="API documentation browser for GNOME"
arch=(i686 x86_64)
license=(GPL)
url="http://live.gnome.org/devhelp/"
depends=(webkitgtk3 hicolor-icon-theme)
makedepends=(pkgconfig intltool python)
options=('!libtool' '!emptydirs')
install=devhelp.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('a245b53824c6f2ff89245ff807bb2140bde74951ea6f1d759a0fd0c6959ca9f7')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --with-webkit2
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="${pkgdir}" install
}
