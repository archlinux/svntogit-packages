# $Id: $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Maxime de Roucy <maxime.deroucy@gmail.com>
# Contributor: Darwin Bautista <djclue917@gmail.com>

pkgname=gsm
pkgver=1.0.13
pkgrel=7
pkgdesc="Shared libraries for GSM 06.10 lossy speech compression"
arch=('i686' 'x86_64')
url="http://www.quut.com/gsm/"
license=('custom')
depends=('glibc')
source=("http://www.quut.com/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        'gsm.patch')
md5sums=('c1ba392ce61dc4aff1c29ea4e92f6df4'
         '30f46c92d0a5e5654258e707a9e8b5a6')

build() {
  cd "${srcdir}/${pkgname}-${pkgver%.*}-pl${pkgver##*.}/"

  patch -Np0 -i "${srcdir}/${pkgname}.patch"

  # flags for shared lib
  CFLAGS="${CFLAGS} -fPIC"
  make CCFLAGS="-c ${CFLAGS}"
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver%.*}-pl${pkgver##*.}/"

  # Prepare directories
  install -m755 -d "${pkgdir}"/usr/{bin,lib,include/gsm,share/{licenses/${pkgname},man/man{1,3}}}

  make -j1 INSTALL_ROOT="${pkgdir}/usr" \
    GSM_INSTALL_INC="${pkgdir}/usr/include/gsm" \
    GSM_INSTALL_MAN="${pkgdir}/usr/share/man/man3" \
    TOAST_INSTALL_MAN="${pkgdir}/usr/share/man/man1" \
    install

  # Install license
  install -m644 COPYRIGHT ${pkgdir}/usr/share/licenses/${pkgname}/license.txt
}
