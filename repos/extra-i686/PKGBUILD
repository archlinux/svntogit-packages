# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libpwquality
pkgver=1.2.3
pkgrel=1
pkgdesc="Library for password quality checking and generating random passwords"
arch=('i686' 'x86_64')
url="https://fedorahosted.org/libpwquality/"
license=('GPL')
depends=('cracklib')
optdepends=('python2: Python bindings')
makedepends=('python2')
source=(https://fedorahosted.org/releases/l/i/libpwquality/$pkgname-$pkgver.tar.bz2)
md5sums=('a8d4cf5d40a1c301f965432e6f3b821b')

prepare() {
    cd "$pkgname-$pkgver"
    sed -i -e 's/python setup.py/python2 setup.py/' python/Makefile.in
}

build() {
    cd "$pkgname-$pkgver"
    PYTHON=python2 ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --with-python-rev=2.7
    make
}

package() {
    cd "$pkgname-$pkgver"
    make DESTDIR="$pkgdir" install
}
