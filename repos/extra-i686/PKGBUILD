# $Id$
# Maintainer:  Ionut Biru <ibiru@archlinux.org>
# Maintainer:  Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: damir <damir@archlinux.org>
# Contributor: Paul Mattal <paul@archlinux.org>

pkgname=x264
pkgver=20131030
pkgrel=2
pkgdesc='Library for encoding H264/AVC video streams'
arch=('i686' 'x86_64')
url='http://www.videolan.org/developers/x264.html'
license=('GPL')
depends=('glibc')
makedepends=('yasm' 'git')
source=(git://git.videolan.org/x264.git#commit=c628e3bac)
md5sums=('SKIP')

build() {
  cd $pkgname

  ./configure --prefix=/usr \
    --enable-shared \
    --enable-pic

  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
