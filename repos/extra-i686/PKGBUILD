# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Jeff Mickey <jeff@archlinux.org>
# Contributor: Jani Talikka <jani.talikka@gmail.com>
# Contributor: Ralf Barth <archlinux.org@haggy.org>

pkgname=smartmontools
pkgver=5.42
pkgrel=1
pkgdesc="Control and monitor S.M.A.R.T. enabled ATA and SCSI Hard Drives."
url="http://smartmontools.sourceforge.net"
license=('GPL')
arch=('i686' 'x86_64')
depends=('gcc-libs')
backup=('etc/smartd.conf'
        'etc/conf.d/smartd')
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.gz \
        smartd.conf
        smartd.rc)
md5sums=('4460bf9a79a1252ff5c00ba52cf76b2a'
         '28c1b80c41dac52d433961dcfb4f70e0'
         'fb30e07f87f2c1d99532e018291ad3e8')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --enable-drivedb
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR=${pkgdir}/ install
  rm -rf ${pkgdir}/etc/rc.d
  install -Dm755 ${srcdir}/smartd.rc ${pkgdir}/etc/rc.d/smartd
  install -Dm644 ${srcdir}/smartd.conf ${pkgdir}/etc/conf.d/smartd
}
