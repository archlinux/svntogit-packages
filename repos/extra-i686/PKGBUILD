# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libxext
pkgver=1.3.2
pkgrel=1
pkgdesc="X11 miscellaneous extensions library"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libx11' 'xextproto')
makedepends=('xorg-util-macros')
options=('!libtool')
source=("${url}/releases/individual/lib/libXext-${pkgver}.tar.bz2")
sha256sums=('f829075bc646cdc085fa25d98d5885d83b1759ceb355933127c257e8e50432e0')

build() {
  cd "${srcdir}/libXext-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static
  make
}

package() {
  cd "${srcdir}/libXext-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
