# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>

pkgname=openbabel
pkgver=2.3.2
pkgrel=1
pkgdesc="A library designed to interconvert between many file formats used in molecular modeling and computational chemistry"
arch=('i686' 'x86_64')
url="http://openbabel.org/wiki/Main_Page"
license=('GPL')
depends=('gcc-libs' 'libxml2')
makedepends=('cmake' 'eigen2' 'wxgtk')
optdepends=('eigen2: to use bindings'
        'wxgtk: GUI interface')
options=('!libtool')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
md5sums=('9b0007560d9d838b40ab4ad06daf5610')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}/build"
  make DESTDIR="${pkgdir}" install
}
