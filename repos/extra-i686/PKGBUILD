# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gstreamer-vaapi
pkgver=1.8.2
pkgrel=1
pkgdesc="GStreamer Multimedia Framework VAAPI Plugins"
arch=(i686 x86_64)
license=(LGPL)
url="http://gstreamer.freedesktop.org/"
depends=(gst-plugins-base gst-plugins-bad libva libxrandr libvpx)
makedepends=(gtk-doc)
options=(!emptydirs)
source=($url/src/$pkgname/$pkgname-$pkgver.tar.xz)
sha256sums=('5479e86f593d80c00bbd4a43b16b2027cb935412a2bb22c9ba4ac085627a9229')

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --disable-builtin-libvpx --enable-gtk-doc \
    --with-package-name="GStreamer VAAPI Plugins (Arch Linux)" \
    --with-package-origin="http://www.archlinux.org/"

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
