# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=xfce4-netload-plugin
pkgver=1.2.4
pkgrel=2
pkgdesc="A netload plugin for the Xfce panel"
arch=('i686' 'x86_64')
url="http://goodies.xfce.org/projects/panel-plugins/xfce4-netload-plugin"
license=('GPL')
groups=('xfce4-goodies')
depends=('xfce4-panel')
makedepends=('intltool')
install=$pkgname.install
source=(http://archive.xfce.org/src/panel-plugins/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=('fface918822c012d806717116566e54947ad7613b9290b45a18e2d5ffb8730d3')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
