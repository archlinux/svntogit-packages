# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Antonio Rojas

pkgname=kwayland
pkgver=5.1.2
pkgrel=2
pkgdesc='Qt-style Client and Server library wrapper for the Wayland libraries'
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('LGPL')
depends=('qt5-base')
makedepends=('extra-cmake-modules')
source=("http://download.kde.org/stable/plasma/${pkgver}/kwayland-$pkgver.tar.xz"
        install-server.patch)
md5sums=('49a5563621a4d6bd6bfd670903ea34bb'
         'a2d79a0467a0ca8a208753f88f927511')

prepare() {
  mkdir -p build

  cd ${pkgname}-${pkgver}
  echo "" >> src/server/CMakeLists.txt
  patch -R -p1 -i ../install-server.patch
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
