# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=xaw3d
pkgver=1.6.2
pkgrel=2
pkgdesc="Three-D Athena widgets"
arch=('i686' 'x86_64')
url="https://directory.fsf.org/project/xaw3d/"
license=('MIT')
depends=('libxmu')
makedepends=('xorg-util-macros')
source=(https://xorg.freedesktop.org/archive/individual/lib/libXaw3d-${pkgver}.tar.bz2)
sha1sums=('0b1db72e9d5be0edae57cda213860c0289fac12f')

build() {
  cd libXaw3d-${pkgver}
  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd libXaw3d-${pkgver}
  make DESTDIR="${pkgdir}" install
  
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
