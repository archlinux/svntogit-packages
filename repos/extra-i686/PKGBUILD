# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=libzip
pkgver=0.11.1
pkgrel=1
pkgdesc="A C library for reading, creating, and modifying zip archives"
url="http://www.nih.at/libzip/index.html"
license=('BSD')
arch=('i686' 'x86_64')
depends=('zlib')
options=('!libtool')
source=("http://www.nih.at/${pkgname}/${pkgname}-${pkgver}.tar.xz"
        'fix-headers.patch')
md5sums=('87d5ec3629f6ad2a4b01ad961e7f0c19'
         '249395bd3a426c4c4e993e9d4753e1dd')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i "${srcdir}/fix-headers.patch"
}

build() {
  cd ${pkgname}-${pkgver}
  autoreconf -i
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
