# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Jan de Groot <jgc.archlinux.org>
# Contributor: Eric Johnson <eric.archlinux.org>

pkgname=libidn
pkgver=1.21
pkgrel=1
pkgdesc="Implementation of the Stringprep, Punycode and IDNA specifications"
url="http://www.gnu.org/software/libidn/"
arch=('i686' 'x86_64')
license=('GPL3' 'LGPL')
depends=('glibc' 'texinfo')
makedepends=('pkgconfig')
options=('!libtool')
source=(http://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz)
install=libidn.install
sha1sums=('991d5d4e24118d7672d53ed692a8a6ab87212f8f')
	
build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
