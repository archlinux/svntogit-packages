# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: PedsXing <pedsxing at gmx dot net>

pkgname=vc
pkgver=0.7.3
pkgrel=3
pkgdesc="A library to ease explicit vectorization of C++ code"
url='http://code.compeng.uni-frankfurt.de/projects/vc/'
arch=('x86_64' 'i686')
license=('LGPL3')
makedepends=('cmake')
options=('staticlibs')
source=("http://code.compeng.uni-frankfurt.de/attachments/download/174/Vc-${pkgver}.tar.gz")
md5sums=('a55e2871a33dbcbbe444c22ccef47dbd')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../Vc-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

check() {
  cd build
  make test
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
