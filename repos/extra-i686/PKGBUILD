# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=fontforge
pkgver=20150228
pkgrel=1
pkgdesc='Outline and bitmap font editor'
url='http://www.fontforge.org/'
arch=('i686' 'x86_64')
license=('BSD')
makedepends=('git')
depends=('libtool' 'libxkbui' 'libxi'
         'pango' 'giflib' 'libtiff' 'libspiro' 'libxml2' 'libspiro' 'libunicodenames' 'zeromq'
         'python2' 'desktop-file-utils' 'gtk-update-icon-cache' 'hicolor-icon-theme')
source=("https://github.com/fontforge/fontforge/archive/${pkgver}.tar.gz"
        'http://fontforge.org/cidmaps.tgz')
sha1sums=('a034746fb1c14cff31879e2abb51bd6972fabbcd'
          'efbc7c9d3e95159f5600dc71f9cccb370e46bb94')

install=install

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	export LIBZMQ_LIBS=-lzmq
	export PYTHON=python2
	./bootstrap --force
	./configure \
		--prefix=/usr \
		--mandir=/usr/share/man \
		--enable-python-extension \

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	install -d "${pkgdir}/usr/share/fontforge"
	install -m644 ../Adobe-* "${pkgdir}/usr/share/fontforge"

	install -d "${pkgdir}/usr/share/icons"
	cp -a desktop/icons "${pkgdir}/usr/share/icons/hicolor"
	rm -fr "${pkgdir}/usr/share/icons/hicolor/src"

	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 desktop/fontforge.desktop "${pkgdir}/usr/share/applications/fontforge.desktop"
}
