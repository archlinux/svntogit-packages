# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libsecret
pkgver=0.18.5
pkgrel=1
pkgdesc='Library for storing and retrieving passwords and other secrets.'
arch=('i686' 'x86_64')
license=('LGPL')
url="https://wiki.gnome.org/Projects/Libsecret"
depends=('glib2' 'libgcrypt')
checkdepends=('python2-dbus' 'python-dbus' 'dbus-glib' 'python2-gobject' 'python-gobject' 'gjs')
makedepends=('intltool' 'docbook-xsl' 'gobject-introspection' 'vala')
optdepends=('gnome-keyring: key storage service (or use any other service implementing org.freedesktop.secrets)')
source=(https://download.gnome.org/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('9ce7bd8dd5831f2786c935d82638ac428fa085057cc6780aba0e39375887ccb3')

prepare() {
  mkdir path
  ln -s /usr/bin/python2 path/python
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static
  make
}

check() {
  cd "$pkgname-$pkgver"
  PATH="$srcdir/path:$PATH" dbus-run-session make -k check 
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
