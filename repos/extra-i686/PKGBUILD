# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=dleyna-renderer
pkgver=0.5.0
pkgrel=1
pkgdesc="Library to discover and manipulate Digital Media Renderers"
arch=(i686 x86_64)
url="http://01.org/dleyna"
license=(LGPL2.1)
depends=(dleyna-connector-dbus gupnp-av gupnp-dlna libsoup)
source=(https://01.org/sites/default/files/downloads/dleyna/$pkgname-$pkgver.tar.gz
        crash.patch)
sha256sums=('a62e98991b268f3a23d1c3e2275cdffb114a440d0b364e45480c8028573a6bc3'
            '90f3ec4d37e9c5bb690e71081c67de9d495679725c70f10a40610de07741d5e5')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../crash.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --libexecdir=/usr/lib/$pkgname
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
