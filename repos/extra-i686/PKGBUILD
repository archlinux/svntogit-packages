# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: PedsXing <pedsxing at gmx dot net>

pkgname=vc
pkgver=0.7.4
pkgrel=1
pkgdesc="A library to ease explicit vectorization of C++ code"
url='http://code.compeng.uni-frankfurt.de/projects/vc/'
arch=('x86_64' 'i686')
license=('LGPL3')
makedepends=('cmake')
options=('staticlibs') # only builds a static library
source=("http://code.compeng.uni-frankfurt.de/attachments/download/183/Vc-${pkgver}.tar.gz")
md5sums=('b11f0c6925f64a4ce178d3b8f0c282f8')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../Vc-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

#check() {
#  cd build
#  make test
#}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
