# $Id$
# Maintainer:

pkgname=perl-netaddr-ip
pkgver=4.072
pkgrel=1
pkgdesc="Perl module to manage IP addresses and subnets"
arch=('i686' 'x86_64')
url="http://search.cpan.org/dist/NetAddr-IP/"
license=('PerlArtistic' 'GPL')
depends=('perl')
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/M/MI/MIKER/NetAddr-IP-${pkgver}.tar.gz)
md5sums=('7764bf54881cd014880c0b77161f16aa')

build() {
  cd NetAddr-IP-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd NetAddr-IP-${pkgver}
  make test
}

package() {
  cd NetAddr-IP-${pkgver}
  make DESTDIR="${pkgdir}" install
}
