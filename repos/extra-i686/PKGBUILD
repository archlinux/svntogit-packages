# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=upower
pkgver=0.9.10
pkgrel=1
pkgdesc="Abstraction for enumerating power devices, listening to device events and querying history and statistics"
arch=('i686' 'x86_64')
url="http://upower.freedesktop.org"
license=('GPL')
depends=('udev' 'libusb' 'polkit' 'pm-utils' 'dbus-glib')
makedepends=('pkg-config' 'intltool' 'docbook-xsl' 'gobject-introspection')
replaces=('devicekit-power')
options=('!libtool')
source=($url/releases/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('fa567509c4ce8d1975259665b907929cac692a59dbe0e217e7494b1cdc949288')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/usr/lib/upower --disable-static
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
