# $Id$
# Maintainer: Dave Reisner <dreisner@archlinux.org>

pkgname=pkgfile
pkgver=10
pkgrel=1
pkgdesc="a pacman .files metadata explorer"
arch=('i686' 'x86_64')
url="http://github.com/falconindy/pkgfile"
license=('MIT')
depends=('libarchive' 'curl' 'pcre' 'pacman')
conflicts=('pkgtools<24-2')
source=("http://code.falconindy.com/archive/$pkgname/$pkgname-$pkgver.tar.xz"{,.sig})
install=pkgfile.install
md5sums=('262d7359423dedbf1f7323cc14d4a1e3'
         'SKIP')

options=(!strip)

build() {
  cd "$pkgname-$pkgver"

  ./configure
  make

  # generate a license file
  sed '/\*\//q' src/pkgfile.c >LICENSE
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim: ft=sh syn=sh
