# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=grilo-plugins
pkgver=0.2.10
pkgrel=1
pkgdesc="Plugins for Grilo"
url="http://www.gnome.org"
arch=(i686 x86_64)
license=(LGPL)
depends=(grilo sqlite libgcrypt)
makedepends=(gupnp-av libgdata gmime libtracker-sparql libdmapsharing
             json-glib intltool avahi itstool)
optdepends=('gupnp-av: uPnP plugin'
            'libgdata: Youtube plugin'
            'gnome-online-accounts: Flickr plugin'
            'gmime: Podcasts plugin'
            'libtracker-sparql: Tracker plugin'
            'libdmapsharing: DMAP sharing plugin'
            'json-glib: TMDb plugin'
            'avahi: Freebox plugin')
options=('!emptydirs')
groups=('gnome')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('5a7dd406ed58145847553e6dbb5f766e9d58dc5d4cfeb7e290d5b38c88163871')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static \
    --enable-shoutcast --disable-pocket
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
