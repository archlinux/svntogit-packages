# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: James Rayner <james@archlinux.org>
# Contributor:  Partha Chowdhury <kira.laucas@gmail.com>

pkgname=conky
pkgver=1.7.2
pkgrel=5
pkgdesc="An advanced, highly configurable system monitor for X based on torsmo"
arch=('i686' 'x86_64')
url="http://conky.sourceforge.net/"
license=('custom')
replaces=('torsmo')
depends=('alsa-lib' 'libxml2' 'curl' 'wireless_tools' 'libxft' 'glib2' 'libxdamage' 'imlib2')
makedepends=('pkgconfig')
backup=(etc/conky/conky.conf etc/conky/conky_no_x11.conf)
source=(http://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.bz2
        fix_hwmon_for_2.6.31.patch)
md5sums=('cf6f5d45d42ad68c618c00271813b80d'
         '679475fc92700a90c0f38c393532ba1d')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  #  FS#16584
  patch -Np1 -i ${srcdir}/fix_hwmon_for_2.6.31.patch || return 1
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --enable-wlan \
	      --enable-rss \
	      --enable-ibm \
	      --enable-imlib2 \
	      --disable-lua
  make || return 1
  make DESTDIR=${pkgdir} install
  install -D -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
