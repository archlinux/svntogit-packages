# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=telepathy-qt
pkgver=0.9.3
pkgrel=2
pkgdesc="A library for Qt-based Telepathy clients"
arch=('i686' 'x86_64')
url="http://telepathy.freedesktop.org/wiki/"
license=('LGPL')
options=('!libtool')
depends=('qt' 'telepathy-glib')
#depends=('qt' 'telepathy-farstream')
makedepends=('libxslt' 'python2' 'cmake' 'doxygen')
conflicts=('telepathy-qt4')
replaces=('telepathy-qt4')
provides=('telepathy-qt4')
source=("http://telepathy.freedesktop.org/releases/${pkgname}/${pkgname}-${pkgver}.tar.gz")
md5sums=('94ac93aedf5f6fff49837bc8368e5a37')

build() {
    cd "$srcdir"
    mkdir build
    cd build
    cmake ../${pkgname}-${pkgver} \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DENABLE_TESTS=OFF
    make
}

package() {
    cd "$srcdir"/build
    make DESTDIR="$pkgdir" install
}
