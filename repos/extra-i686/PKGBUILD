# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-twm
pkgver=1.0.8
pkgrel=2
pkgdesc="Tab Window Manager for the X Window System"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
backup=(etc/X11/twm/system.twmrc)
license=('custom')
depends=('libxmu')
makedepends=('xorg-util-macros')
options=('!emptydirs')
source=(${url}/releases/individual/app/twm-${pkgver}.tar.bz2)
sha256sums=('7660352353d632127ff50390991706aa660b28a4ada816c2582ac02720722e44')

build() {
  cd twm-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd twm-${pkgver}
  make DESTDIR="${pkgdir}" install

  # fix config file location
  install -m755 -d "${pkgdir}/etc/X11/twm"
  install -m644 src/system.twmrc "${pkgdir}/etc/X11/twm/"
  rm $pkgdir/usr/share/X11/twm/system.twmrc
  
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
