# $Id$
# Maintainer: Angel Velasquez <angvp@archlinux.org>  
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=python-cairo
pkgver=1.10.0
pkgrel=2
pkgdesc="Python bindings for the cairo graphics library"
arch=('i686' 'x86_64')
license=('LGPL3')
depends=('python' 'cairo')
makedepends=('pkg-config')
options=('!libtool')
source=(http://cairographics.org/releases/pycairo-${pkgver}.tar.bz2)
url="http://www.cairographics.org/pycairo"
md5sums=('e6fd3f2f1e6a72e0db0868c4985669c5')

build() {
  cd "${srcdir}/pycairo-${pkgver}"

  # Ensure that ./waf has created the cached unpacked version
  # of the wafadmin source tree.
  # This will be created to a subdirectory like
  #    .waf3-1.5.18-a7b91e2a913ce55fa6ecdf310df95752
  python3 ./waf --version

  ./waf configure --prefix=/usr
  ./waf build
}

package() {
  cd "${srcdir}/pycairo-${pkgver}"
  ./waf install --destdir="${pkgdir}"
}
