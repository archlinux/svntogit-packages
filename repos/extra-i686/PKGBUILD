# $Id$
# Maintainer: dorphell <dorphell@archlinux.org>

pkgname=ntp
pkgver=4.2.6
pkgrel=3
pkgdesc="NTP (Network Time Protocol) tries to keep servers in sync"
arch=(i686 x86_64)
license=('custom')
url="http://www.ntp.org/"
depends=('openssl' 'readline' 'libcap')
makedepends=('perl-html-parser')
backup=('etc/ntp.conf' 'etc/conf.d/ntp-client.conf')
options=('!emptydirs')
source=(http://www.eecis.udel.edu/~ntp/ntp_spool/ntp4/ntp-${pkgver}.tar.gz
        ntp-4.2.4_p7-nano.patch
	fix-ipv6.patch
	ntp-4.2.4-html2man.patch
        ntp.conf
        ntp-client.conf
        ntpd
        ntpdate)
md5sums=('4d64a99592b818aa9419fc9dcb149746' '36c5711228cb0cf48966daa993e06f55'\
         '9dabdbc6b8cd020f2205515df7972c21' '1b04e888717bb31479a6087632981723'\
         '5bd3924f5720b97837969ec4ec4d5e09' 'c7f50632b69bd4f32cf052d0b1848463'\
         '46118a8ec2c4f5bbfafd730af21b7c03' '140855352baeb89da0c6c1c475c18b57')
sha1sums=('a1f21b23c8c80f35801d59e30ba30779f2025a72' '4de6cd3fad1946146107f86d1f161eb835fbb2d3'\
         '3632f8a00838f48e484bca24de06db443cc14fff' '7f8783e243e23c8853a9f9fb5b36f467aab8dc15'\
         '3625ed2aa71d9e0dd04634f32e65d83b76cfe780' 'c8e388f2577b4329da5e77f0455083db6dd97fa8'\
         'd9b09de9d718a864270e9330e20109e182dfbdd9' '6dfc3b6996f591123ee8938fd0fe7c27eb5c7552')

build() {
  cd "$srcdir/$pkgname-$pkgver" || return 1
  # fix glibc build issue
  patch -Np1 -i ../ntp-4.2.4_p7-nano.patch || return 1
  # fix ipv6 support
  patch -Np1 -i ../fix-ipv6.patch || return 1
  # fix man page generation
  patch -Np1 -i ../ntp-4.2.4-html2man.patch || return 1
  # configure
  export LDFLAGS="${LDFLAGS//-Wl,--as-needed}"
  libtoolize --copy  --force || return 1
  ac_cv_header_dns_sd_h=0 ./configure --prefix=/usr --mandir=/usr/share/man --enable-linux-caps || return 1

  # build and install
  make || return 1
  make DESTDIR="$pkgdir" install || return 1

  # install conf files
  mkdir -p "$pkgdir/usr/share/ntp" || return 1
  install -D -m644 conf/* "$pkgdir/usr/share/ntp/" || return 1

  # install launch scripts 
  mkdir -p "$pkgdir/etc/rc.d" || return 1
  install -D -m755 "$srcdir"/{ntpd,ntpdate} "$pkgdir/etc/rc.d/" || return 1

  # install man pages
  cd html
  ../scripts/html2man || return 1
  sed -i 's/^[\t\ ]*$//;/./,/^$/!d' man/man*/*.[58] || return 1
  install -d "$pkgdir"/usr/share/man/man{5,8}
  install -m644 man/man5/* "$pkgdir/usr/share/man/man5/" || return 1
  install -m644 man/man8/* "$pkgdir/usr/share/man/man8/" || return 1
  mv "$pkgdir/usr/share/man/man8/ntpd.8" "$pkgdir/usr/share/man/man8/ntp-ntpd.8" || return 1
  cd ..

  # install sample configs
  install -D -m644 "$srcdir/ntp.conf" "$pkgdir/etc/ntp.conf" || return 1
  install -D -m644 "$srcdir/ntp-client.conf" \
  	"$pkgdir/etc/conf.d/ntp-client.conf" || return 1

  # create /var/lib/ntp
  mkdir -p "$pkgdir/var/lib/ntp" || return 1
  touch "$pkgdir/var/lib/ntp/.placeholder" || return 1

  install -Dm644 "$srcdir/$pkgname-$pkgver/COPYRIGHT" "$pkgdir/usr/share/licenses/$pkgname/COPYRIGHT"
}
