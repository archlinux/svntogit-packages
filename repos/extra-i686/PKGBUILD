# $Id$
# Maintainer:
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=zip
pkgver=3.0
_pkgver=30
pkgrel=2
pkgdesc="Creates PKZIP-compatible .zip files"
arch=(i686 x86_64)
url="http://www.info-zip.org/pub/infozip/Zip.html"
license=('BSD')
depends=('bzip2')
options=('!makeflags')
source=("ftp://ftp.info-zip.org/pub/infozip/src/${pkgname}${_pkgver}.zip")
md5sums=('e88492c8abd68fa9cfba72bc08757dba')

build() {
   cd "${srcdir}/${pkgname}${_pkgver}"
   make -f unix/Makefile LOCAL_ZIP="$CFLAGS" prefix=/usr generic_gcc
}

package() {
   cd "${srcdir}/${pkgname}${_pkgver}"
   make -f unix/Makefile INSTALL=/bin/install prefix=${pkgdir}/usr \
     MANDIR=${pkgdir}/usr/share/man/man1 install
   install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/$pkgname/LICENSE
}
