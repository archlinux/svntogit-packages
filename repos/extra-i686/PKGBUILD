# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=baloo
pkgver=4.13.0
pkgrel=2
pkgdesc="A framework for searching and managing metadata"
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kdelibs/baloo'
license=('LGPL')
depends=('kdepimlibs' 'xapian-core' 'kfilemetadata')
makedepends=('cmake' 'automoc4' 'boost')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/${pkgname}-${pkgver}.tar.xz"
        'krunner-crash.patch')
sha1sums=('ace3a80d3c0e3bb444ebe7f30392db791eab7c61'
          'fdb37257dca64531559fdc2ddf4730582b9a009c')

prepare() {
  mkdir build

  cd ${pkgname}-${pkgver}
  patch -p1 -i "${srcdir}"/krunner-crash.patch
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
