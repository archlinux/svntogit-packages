# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kcoreaddons
pkgver=5.3.0
pkgrel=1
pkgdesc='KCoreAddons'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kcoreaddons'
license=('LGPL')
depends=('qt5-base' 'shared-mime-info' 'gamin')
makedepends=('extra-cmake-modules' 'qt5-tools')
groups=('kf5')
install=${pkgname}.install
source=("http://download.kde.org/stable/frameworks/${pkgver}/${pkgname}-${pkgver}.tar.xz")
md5sums=('6123b972897aa5369bd2368c94b5ec95')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
