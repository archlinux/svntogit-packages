# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Andrew Simmons <andrew.simmons@gmail.com>

pkgname=gparted
pkgver=0.7.1
pkgrel=1
pkgdesc="A Partition Magic clone, frontend to GNU Parted"
arch=('i686' 'x86_64')
url="http://gparted.sourceforge.net"
license=('GPL')
depends=('parted' 'gtkmm' 'libsigc++')
makedepends=('intltool' 'pkgconfig' 'gnome-doc-utils')
optdepends=('e2fsprogs: for ext2/ext3 partitions'
            'dosfstools: for fat16/32 partitions'
            'jfsutils: for jfs partitions'
            'ntfsprogs: for ntfs partitions'
            'reiserfsprogs: for reiser partitions'
            'xfsprogs: for xfs partitions'
            'gksu: to run gparted directly from menu')
install=gparted.install
source=(http://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgname}-${pkgver}/${pkgname}-${pkgver}.tar.bz2)
md5sums=('e9e79b527009e8a2fb6280d8deb4fc09')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
  sed -i -e "s:Exec= /usr/sbin:Exec=gksu /usr/sbin:g" ${pkgdir}/usr/share/applications/gparted.desktop || return 1
}
