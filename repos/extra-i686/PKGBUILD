# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Gerardo Exequiel Pozzi <vmlinuz386@yahoo.com.ar>

pkgname=libbsd
pkgver=0.6.0
pkgrel=2
pkgdesc="Provides useful functions commonly found on BSD systems like strlcpy()"
arch=('i686' 'x86_64')
url="http://libbsd.freedesktop.org"
license=('custom')
depends=('glibc')
options=('staticlibs')
source=(http://libbsd.freedesktop.org/releases/$pkgname-$pkgver.tar.xz{,.asc})
md5sums=('f6c75f0a9818e323a589bcbd560a0eb4'
         'SKIP')

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr
  make
}

check() {
  cd $pkgname-$pkgver

  make check
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install
  rm "${pkgdir}"/usr/lib/libbsd.a
  install -D -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
