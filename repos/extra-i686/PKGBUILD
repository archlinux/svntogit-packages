# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=totem
pkgver=2.22.2
pkgrel=1
url="http://www.hadess.net/totem.php3"
pkgdesc="A GNOME2 integrated movie player based on Gstreamer."
arch=(i686 x86_64)
license=('GPL')
depends=('gstreamer0.10-good-plugins' 'totem-plparser>=2.22.3' 'libxxf86vm' 'libxtst' 'desktop-file-utils' 'iso-codes>=2.0' 'libepc>=0.3.5' 'python>=2.5.2' 'startup-notification' 'libgnomeui>=2.22.1' 'python-gdata' 'gnome-vfs>=2.22.0-2')
makedepends=('perlxml' 'nautilus>=2.22.2' 'xulrunner>=1.9' 'pkgconfig' 'bluez-libs' 'lirc-utils' 'gnome-doc-utils>=0.12.2' 'gnome-control-center>=2.22.1' 'libtracker')
options=('!libtool' '!emptydirs')
conflicts=('totem-xine')
groups=('gnome-extra')
install=totem.install
source=(http://ftp.gnome.org/pub/gnome/sources/totem/2.22/${pkgname}-${pkgver}.tar.bz2
	30_totem_xulrunner19.patch)
md5sums=('6062080e81b4859f87fee8a592b92489' '42e3246acb3163bab35fc12816a19419')

build() {
  cd ${startdir}/src/${pkgname}-${pkgver}
  patch -Np1 -i ${startdir}/src/30_totem_xulrunner19.patch || return 1
  libtoolize --force --copy || return 1
  aclocal || return 1
  autoconf || return 1
  automake --add-missing || return 1
  ./configure --prefix=/usr --sysconfdir=/etc \
              --libexecdir=/usr/lib/totem \
              --localstatedir=/var --disable-static \
	      --enable-nautilus \
	      --enable-mozilla --with-dbus \
	      --disable-scrollkeeper || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 \
       DESTDIR=${startdir}/pkg install || return 1

  rm -f ${startdir}/pkg/usr/share/totem/vanity.*
  rm -f ${startdir}/pkg/usr/share/pixmaps/vanity.png

  rm -rf ${startdir}/pkg/usr/lib/mozilla

  mkdir -p ${startdir}/pkg/usr/share/gconf/schemas
  gconf-merge-schema ${startdir}/pkg/usr/share/gconf/schemas/${pkgname}.schemas ${startdir}/pkg/etc/gconf/schemas/*.schemas || return 1
  rm -f ${startdir}/pkg/etc/gconf/schemas/*.schemas
}
