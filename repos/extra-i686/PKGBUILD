# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=libsocialweb
pkgver=0.25.21
pkgrel=1
pkgdesc="A personal social data server"
arch=('i686' 'x86_64')
url="http://www.gtk.org/"
license=('GPL2')
depends=('glib2' 'gconf' 'libsoup-gnome' 'rest' 'dbus-glib' 'libgnome-keyring'
         'json-glib' 'networkmanager')
makedepends=('intltool' 'gtk-doc' 'vala' 'gobject-introspection')
options=('!libtool')
source=(http://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz
       libsocialweb-gir-fix.patch)
sha256sums=('f676567640cede66b6ff62f440c968943c2f3a164dd5f26155b6154ab93bf687'
            'fec8d2c67c62618b89dca238ebe502c6525d42987625b6d87dd704f88219a46a')

build() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../libsocialweb-gir-fix.patch
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/libsocialweb \
    --enable-all-services=yes \
    --with-online=networkmanager \
    --enable-vala-bindings
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
