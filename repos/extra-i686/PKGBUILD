# $Id$
# Maintainer: Dan McGee <dan@archlinux.org>

pkgbase=doxygen
pkgname=('doxygen' 'doxygen-docs')
pkgver=1.8.1.2
pkgrel=1
license=('GPL')
arch=(i686 x86_64)
url="http://www.doxygen.org/"
makedepends=('gcc-libs' 'flex' 'qt' 'texlive-core' 'ghostscript' 'texlive-latexextra')
source=(ftp://ftp.stack.nl/pub/users/dimitri/${pkgname}-${pkgver}.src.tar.gz)
md5sums=('9c62259c34d51d839dafd1f0d5d85f17')
sha1sums=('a11e65a597019510051290d2af0e6f379838b9c5')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  QTDIR=/usr ./configure --prefix /usr --with-doxywizard
  make
}

package_doxygen() {
  pkgdesc="A documentation system for C++, C, Java, IDL and PHP"
  depends=('gcc-libs')
  optdepends=('graphviz: for caller/callee graph generation'
              'qt: for doxywizard')

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make INSTALL="${pkgdir}"/usr MAN1DIR=share/man/man1 install
}

package_doxygen-docs() {
  pkgdesc="Developer documentation for doxygen"

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make INSTALL="${pkgdir}"/usr MAN1DIR=share/man/man1 install_docs
}
