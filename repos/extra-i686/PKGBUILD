# Maintainer:
# Contributor: Daniel Bermond < yahoo-com: danielbermond >

pkgname=libraqm
pkgver=0.3.0
pkgrel=2
pkgdesc='A library that encapsulates the logic for complex text layout'
arch=(i686 x86_64)
url='https://github.com/HOST-Oman/libraqm/'
license=(MIT)
depends=(freetype2 fribidi)
makedepends=(gtk-doc)
source=(https://github.com/HOST-Oman/libraqm/releases/download/v$pkgver/raqm-$pkgver.tar.gz)
sha256sums=('902592e098c79f8a332d8762dbeb4d277ac3c38ed7666b0f2615f188f2fe3bee')

build() {
    cd raqm-$pkgver
    
    ./configure \
        --prefix=/usr \
        --enable-static=no \
        --enable-shared=yes \
        --enable-fast-install=yes \
        --enable-gtk-doc=yes \
        --enable-gtk-doc-html=yes \
        --enable-gtk-doc-pdf=no
    make
}

package() {
    cd raqm-$pkgver
    
    make DESTDIR="$pkgdir" install
    
    install -D -m644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
