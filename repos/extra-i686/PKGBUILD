# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=libmng
pkgver=2.0.2
pkgrel=3
pkgdesc="A collection of routines used to create and manipulate MNG format graphics files"
arch=('i686' 'x86_64')
url="http://www.libmng.com/"
license=('custom')
depends=('lcms2')
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.xz{,.asc}
        libmng-stdio.patch)
sha1sums=('7dd35369ff5916e1823cbacef984ab1b87714b69'
          'SKIP'
          '2f8b472d53791ba3a44dab9b18fb215e8b19dd77')
validpgpkeys=('8048643BA2C840F4F92A195FF54984BFA16C640F')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p5 -i "${srcdir}/libmng-stdio.patch"
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
