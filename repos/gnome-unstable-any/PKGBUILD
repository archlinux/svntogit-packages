# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-devel-docs
pkgver=3.38.0
pkgrel=1
pkgdesc="Developer documentation for GNOME"
url="https://gitlab.gnome.org/GNOME/gnome-devel-docs"
arch=(any)
license=(FDL)
depends=(yelp)
makedepends=(yelp-tools git)
groups=(gnome-extra)
_commit=82382c77cc15132344648651b0742292bcbbd50a  # tags/3.38.0^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-devel-docs.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  autoreconf -fvi
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
