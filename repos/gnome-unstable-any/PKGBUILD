# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Steve Holmes <steve.holmes88@gmail.com>
# Contributor: Giulio Bai <giulio@hewle.com>

pkgname=accerciser
pkgver=3.12.0
pkgrel=1
pkgdesc="Interactive Python accessibility explorer for the GNOME desktop"
arch=('any')
url="http://live.gnome.org/Accerciser"
license=('BSD')
depends=('python' 'gtk3' 'python-atspi' 'ipython' 'hicolor-icon-theme')
makedepends=('itstool' 'docbook-xsl' 'intltool')
install=accerciser.install
groups=('gnome-extra')
source=(http://ftp.gnome.org/pub/GNOME/sources/accerciser/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('0d421a59cd294966441fb6591d34f28e4b60340815bdf91c9e8f39c2d826e96b')

build () {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
                 --disable-schemas-compile
  make
}

package () {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -m 755 -d "$pkgdir"/usr/share/licenses/$pkgname
  install -m 644 COPYING "$pkgdir"/usr/share/licenses/$pkgname
}
