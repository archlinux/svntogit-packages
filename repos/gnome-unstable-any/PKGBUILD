# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-getting-started-docs
pkgver=3.22.0
pkgrel=1
pkgdesc="Help a new user get started in GNOME"
arch=(any)
license=('CCPL:cc-by-sa-3.0')
# gst plugins required to decode help videos
depends=(yelp gst-plugins-base gst-plugins-good)
makedepends=(yelp-tools git gnome-common)
url="http://www.gnome.org"
groups=(gnome-extra)
_commit=f7f7ed4cd09b31636c7f027321e647c511bcc5e1
source=("git://git.gnome.org/gnome-getting-started-docs#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
