# Maintainer : Felix Yan <felixonmars@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=python-pyopenssl
pkgver=23.1.0
pkgrel=1
arch=('any')
pkgdesc="Python3 wrapper module around the OpenSSL library"
url='https://github.com/pyca/pyopenssl'
license=('Apache')
depends=('python-cryptography')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-pretend' 'python-flaky')
source=(https://github.com/pyca/pyopenssl/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('da26f3dfafb8e61c6fe41b81475c22602eddbb4fd95b38fb284133ad4c1932e57677928320997e4447d1dd21f008ce6020ad45b1585ebd9c9beeafbe5ab78455')

prepare() {
  export LC_CTYPE=en_US.UTF-8
}

build() {
  cd pyopenssl-$pkgver
  python setup.py build
}

check() {
  cd pyopenssl-$pkgver
  PYTHONPATH="$PWD"/build/lib pytest
}

package() {
  cd pyopenssl-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

# vim: ts=2 sw=2 et:
