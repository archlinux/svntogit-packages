# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgbase=pyatspi
pkgname=(python-atspi python2-atspi)
pkgver=2.6.0
pkgrel=2
arch=(any)
url="http://www.linuxfoundation.org/en/AT-SPI_on_D-Bus"
license=('GPL2')
makedepends=(python-gobject python2-gobject at-spi2-core)
source=(http://download.gnome.org/sources/${pkgbase}/${pkgver::3}/${pkgbase}-${pkgver}.tar.xz)
sha256sums=('6203fa4204267bb1938725ec66d5a1e618febc8a7d6ff5582a646b789f37f87e')

build() {
  mkdir python2 python3

  cd $pkgbase-$pkgver
  sed '/AC_PATH_XTRA/d' -i configure.ac
  autoreconf

  cd ../python2
  PYTHON=/usr/bin/python2 ../$pkgbase-$pkgver/configure --prefix=/usr
  make

  cd ../python3
  PYTHON=/usr/bin/python3 ../$pkgbase-$pkgver/configure --prefix=/usr
  make
}

package_python-atspi() {
  pkgdesc="Python 3 bindings for at-spi"
  depends=(python-gobject at-spi2-core)

  cd python3
  make DESTDIR="$pkgdir" install
}

package_python2-atspi() {
  pkgdesc="Python 2 bindings for at-spi"
  depends=(python2-gobject at-spi2-core)
  provides=("pyatspi=$pkgver")
  replaces=(pyatspi)
  conflicts=(pyatspi)

  cd python2
  make DESTDIR="$pkgdir" install
  rm -r "$pkgdir/usr/bin"
}

# vim:set ts=2 sw=2 et:
