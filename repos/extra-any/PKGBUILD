# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=archlinux-appstream-data
pkgver=20190726
pkgrel=1
pkgdesc="Arch Linux application database for AppStream-based software centers"
arch=(any)
url="https://www.archlinux.org"
license=(GPL)
depends=()
makedepends=()
source=()
noextract=()
for _repo in core extra community; do
 source+=($_repo-$pkgver.xml.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/Components-x86_64.xml.gz
          $_repo-icons-64x64-$pkgver.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-64x64.tar.gz
          $_repo-icons-128x128-$pkgver.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-128x128.tar.gz)
 noextract+=($_repo.xml.gz-$pkgver $_repo-icons-{64x64,128x128}-$pkgver.tar.gz)
done
sha256sums=('45a94fe15e07fcda1aa8faaabfc9142bd343caab7ad4187b4b8426d94b53cac8'
            '7989bb311baa38ef545250282aa065d23281c46dfb8faabe4c653487bdbded5c'
            '7989bb311baa38ef545250282aa065d23281c46dfb8faabe4c653487bdbded5c'
            '1c6c6613e305b62821ea9b00e1c945b6b5156e9791f45dde064c95053b2ff622'
            'f4a8061eb08615194fddb0ab3130eafc0a220d6c3561b94d6dadd2412698637b'
            '96aaff01057168d9a7b6d6d9744b6e400a7ac3cce364482433587ea3924bb9b3'
            'f90c7d0240aba719d28c8e386f17076ee9dbd8248ba9d226ba1b47db5a52bedd'
            '36416d69ce9c1968a8089fa566c7fe79b03d03de21b974412a12fcd9cc1fe9d8'
            '57dbc93f6dd754745c3e8a0e2d76b29d92ce943b37cf0465b1cc2fa0d31701ae')

package() {
  mkdir -p "$pkgdir"/usr/share/app-info/{icons/archlinux-arch-{core,extra,community}/{64x64,128x128},xmls}
  for _repo in core extra community; do
   tar -xzf $_repo-icons-64x64-$pkgver.tar.gz -C "$pkgdir"/usr/share/app-info/icons/archlinux-arch-$_repo/64x64
   tar -xzf $_repo-icons-128x128-$pkgver.tar.gz -C "$pkgdir"/usr/share/app-info/icons/archlinux-arch-$_repo/128x128
   install -m644 $_repo-$pkgver.xml.gz "$pkgdir"/usr/share/app-info/xmls/$_repo.xml.gz
  done
}
