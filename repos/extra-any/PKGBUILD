# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Adam Hani Schakaki (krzd) <krzd@krzd.net>

pkgname=gsettings-desktop-schemas
pkgver=3.4.1
pkgrel=1
pkgdesc="Shared GSettings schemas for the desktop"
arch=('any')
url="http://live.gnome.org/"
license=('GPL')
depends=('glib2')
makedepends=('intltool' 'gobject-introspection')
install=$pkgname.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('c7360fc77d82595e238e44345f83c2e1d7bc9a7c452c89df507ddd1cf2b3602b')

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-schemas-compile
  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}
