# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-devel-docs
pkgver=3.26.0
pkgrel=1
pkgdesc="Developer documentation for GNOME"
url="https://git.gnome.org/browse/gnome-devel-docs"
arch=(any)
license=(FDL)
depends=(yelp)
makedepends=(yelp-tools gnome-common git)
groups=(gnome-extra)
_commit=26aaefdd9198d332ef620e2b8fb8b1ef71d7695e  # master~3
source=("git://git.gnome.org/gnome-devel-docs#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  # Fixup missing tag for pkgver()
  git tag -f 3.26.0 26aaefdd9198d332ef620e2b8fb8b1ef71d7695e
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
