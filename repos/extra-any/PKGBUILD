# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=adobe-source-sans-pro-fonts
pkgver=2.010ro+1.065it
_relver=2.010R-ro/1.065R-it
_tarver=${_relver//\//-}
pkgrel=2
pkgdesc="Sans serif font family for user interface environments"
arch=(any)
url="http://adobe-fonts.github.io/source-sans-pro/"
license=(custom)
depends=(fontconfig)
install=fonts.install
source=("${_tarver}.tar.gz::https://github.com/adobe-fonts/source-sans-pro/archive/${_relver}.tar.gz")
sha256sums=('e29660947d1a77bc2682b6036cfa4d644d9cdf00a272baaf60dda56e547c79e8')

package() {
  cd source-sans-pro-$_tarver
  install -d "$pkgdir/usr/share/fonts/adobe-source-sans-pro"
  install -t "$pkgdir/usr/share/fonts/adobe-source-sans-pro" -m644 OTF/*.otf
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
