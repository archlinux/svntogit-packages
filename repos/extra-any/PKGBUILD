# $Id$
# Maintainer: Aaron Griffin <aaron@archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>
# Contributor: Evangelos Foutras <foutrelis@gmail.com>

pkgname=bash-completion
pkgver=1.2
pkgrel=1
pkgdesc="Programmable completion for the bash shell"
arch=('any')
url="http://bash-completion.alioth.debian.org/"
license=('GPL')
depends=('bash')
source=(http://bash-completion.alioth.debian.org/files/$pkgname-$pkgver.tar.bz2)
md5sums=('88c022a98a02a02293716f840eadd884')
sha1sums=('47e6fb919ae227e74172870379b0fe999a2ae70d')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # fixes for archlinux
  sed -i 's#/sbin" lsmod#/bin" lsmod#' bash_completion

  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
# vim:set ts=2 sw=2 et:
