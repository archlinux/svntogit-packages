# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>

pkgbase=python-defusedxml
pkgname=("python-defusedxml" "python2-defusedxml")
pkgver=0.4.1
pkgrel=1
pkgdesc="XML bomb protection for Python stdlib modules"
arch=('any')
url='https://bitbucket.org/tiran/defusedxml'
license=('Python')
makedepends=('python' 'python2')
source=("https://pypi.python.org/packages/source/d/defusedxml/defusedxml-0.4.1.tar.gz")
md5sums=('230a5eff64f878b392478e30376d673a')

prepare() {
  cp -a "defusedxml-$pkgver"{,-py2}
}

package_python-defusedxml() {
  depends=('python')

  cd defusedxml-$pkgver
  python3 setup.py install --root="${pkgdir}" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-defusedxml() {
  depends=('python2')

  cd defusedxml-$pkgver-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
