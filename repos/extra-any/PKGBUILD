# $Id$
# Maintainer : Felix Yan <felixonmars@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgbase=python-chardet
pkgname=('python-chardet' 'python2-chardet')
pkgver=2.3.0
pkgrel=1
arch=('any')
url="https://github.com/chardet/chardet"
license=('LGPL')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://pypi.python.org/packages/source/c/chardet/chardet-${pkgver}.tar.gz")
md5sums=('25274d664ccb5130adae08047416e1a8')

prepare() {
   cp -a chardet-${pkgver} python2-chardet-${pkgver}
}

package_python-chardet() {
   depends=('python')
   pkgdesc="Python3 module for character encoding auto-detection"

   cd chardet-${pkgver}
   python setup.py install --root="${pkgdir}" --optimize=1
}

package_python2-chardet() {
   depends=('python2')
   pkgdesc="Python2 module for character encoding auto-detection"

   cd python2-chardet-${pkgver}
   python2 setup.py install --root="${pkgdir}" --optimize=1

   # To avoid file conflict with the python3 version
   mv "$pkgdir/usr/bin/chardetect" "$pkgdir/usr/bin/chardetect-py2"
}
