# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>

pkgname=dejagnu
pkgver=1.5.1
pkgrel=2
pkgdesc="Framework for testing other programs"
arch=('any')
url="http://www.gnu.org/software/dejagnu/"
license=('GPL')
depends=('sh' 'expect')
install=dejagnu.install
source=(http://ftp.gnu.org/gnu/dejagnu/${pkgname}-${pkgver}.tar.gz{,.sig})
md5sums=('8386e04e362345f50ad169f052f4c4ab'
         '59335adf4900b889c64827c5a9426252')
validpgpkeys=('514F120ED959693B4227E054E66A4E29AFE7BCBD')

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

check() {
  cd $srcdir/$pkgname-$pkgver
  make check
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir/ install
}
