# Packager: Justin "juster" Davis <jrcd83@gmail.com>
# $Id$

pkgname=perl-net-http
pkgver=6.03
pkgrel=1
pkgdesc="Low-level HTTP connection (client)"
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=('perl>=5.6.2')
conflicts=('perl-libwww<6')
url=https://metacpan.org/release/Net-HTTP
source=("http://search.cpan.org/CPAN/authors/id/G/GA/GAAS/Net-HTTP-${pkgver}.tar.gz")
md5sums=(86957940d96649ca782b686686b82e7b)
sha512sums=(a9ffbe5a4c6e1633b130549bb292c9dce093436e0d80d3a9b2ac95c3d1f5d16d734388a1d52094cb34194a7b8973bfa24c549cced4fdc42f36ec08876f551a32)
_distdir="${srcdir}/Net-HTTP-${pkgver}"

build()
(
    export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$_distdir"
    /usr/bin/perl Makefile.PL
    make
)

check()
(
    export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    cd "$_distdir"
    make test
)

package() {
    cd "$_distdir"
    make DESTDIR="$pkgdir" install
    find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
