# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=iso-codes
pkgver=4.7.0
pkgrel=1
pkgdesc="Lists of the country, language, and currency names"
arch=('any')
url="https://salsa.debian.org/iso-codes-team/iso-codes"
license=('LGPL')
makedepends=(python git)
_commit=59767ed869f3952575f7d0f639a290a4c4b315e5
source=("git+https://salsa.debian.org/iso-codes-team/iso-codes/#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^iso-codes-//;s/-/+/g'
}

prepare() {
  cd $pkgname
  autoreconf -fi
}

build() {
  cd $pkgname
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" pkgconfigdir=/usr/lib/pkgconfig install
}

