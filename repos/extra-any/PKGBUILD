# $Id$
# Maintainer:
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: György Balló <ballogy@freestart.hu>

pkgname=zeitgeist
pkgver=0.8.2
pkgrel=2
pkgdesc="A service which logs the users's activities and events and makes relevant information available to other applications"
arch=('any')
url="http://zeitgeist-project.com/"
license=('GPL')
depends=('dbus-python' 'python2-gobject2' 'pyxdg')
makedepends=('intltool' 'raptor' 'python2-rdflib')
source=("http://launchpad.net/$pkgname/0.8/$pkgver/+download/$pkgname-$pkgver.tar.gz"
        'python2.patch')
md5sums=('589e7de784d21177491780bffd11097d'
         '2c8b0fa6a228088a556601128dfbbb3b')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Fix python2 path
  sed -i 's|/usr/bin/python|/usr/bin/python2|' extra/rdfxml2py
  patch -Np1 -i "${srcdir}/python2.patch"

  PYTHON=/usr/bin/python2 ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
