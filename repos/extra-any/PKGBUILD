# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=alsa-topology-conf
pkgver=1.2.5.1
pkgrel=3
pkgdesc="ALSA topology configuration files"
arch=(any)
url="https://alsa-project.org/"
license=(BSD)
source=(https://www.alsa-project.org/files/pub/lib/$pkgname-$pkgver.tar.bz2{,.sig})
sha512sums=('b34176b213e1afcb61fee19b569d6422a7692b62cf08b0ef651f2fbb1eefe49f25ca6386d0bfcec7152d4b5ed3daa84edb900181d40169e777751ae033125374'
            'SKIP')
b2sums=('1f8bb172510d86bd0530f2ba437dd1132401c0bd436085c07c61e5ef296a6da6a1c24fb8d585572186fc0073172c82d40948c5c39fd293a01762d49c9c393662'
        'SKIP')
validpgpkeys=('F04DF50737AC1A884C4B3D718380596DA6E59C91') # ALSA Release Team (Package Signing Key v1) <release@alsa-project.org>

package() {
  cd $pkgname-$pkgver
  for dir in topology/*; do
    install -vDm 644 "$dir"/*.conf -t "$pkgdir/usr/share/alsa/topology/$(basename $dir)"
  done
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
}
