# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com
# Contributor: Charles Mauch <cmauch@gmail.com>

pkgname=perl-file-which
_realname=File-Which
pkgver=1.18
pkgrel=1
pkgdesc="Portable implementation of which"
arch=(any)
url="http://search.cpan.org/dist/File-Which"
license=(GPL PerlArtistic)
depends=(perl)
options=('!emptydirs')
_author=PLICEASE
source=("http://www.cpan.org/authors/id/${_author:0:1}/${_author:0:2}/$_author/${_realname}-${pkgver}.tar.gz")
md5sums=('554bfb36568d828fbeee64f4b9a9faa8')

build() {
  cd "$srcdir/${_realname}-${pkgver}"
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd "$srcdir/${_realname}-${pkgver}"
  make install DESTDIR="$pkgdir"
}
