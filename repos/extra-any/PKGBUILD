# $Id$
# Maintainer: Dan McGee <dan@archlinux.org>
# Contributor: Sebastien Binet <binet@lblbox>

pkgname=python-pip
pkgver=0.8.3
pkgrel=1
pkgdesc="An easy_install replacement for installing pypi python packages"
url="http://pypi.python.org/pypi/pip"
arch=('any')
license=('MIT')
depends=('python2' 'setuptools')
makedepends=()
conflicts=('python-pyinstall')
replaces=('python-pyinstall')
backup=()
install=
source=(http://pypi.python.org/packages/source/p/pip/pip-${pkgver}.tar.gz
        LICENSE.txt)

build() {
  cd "$srcdir/pip-$pkgver"
  python2 setup.py build
}

package() {
  cd "$srcdir/pip-$pkgver"
  python2 setup.py install --prefix=/usr --root="$pkgdir"
  
  sed -i "s|#!/usr/bin/env python$|#!/usr/bin/env python2|" \
    ${pkgdir}/usr/lib/python2.7/site-packages/pip/__init__.py
  
  install -D -m644 "$srcdir/LICENSE.txt" \
	  "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

md5sums=('0603337a81f83df2b1d2f1151565efac'
         '2077e96dc6ee88d987bfb7ccb2e2bf44')
sha256sums=('1be2e18edd38aa75b5e4ef38a99ec33ba9247177cfcb4a6d2d2b3e73430e3001'
            'e5eaf3393dd7d6f34e1bc6de6c35654df33fa5a2d41044b5e7530c217c04421f')
