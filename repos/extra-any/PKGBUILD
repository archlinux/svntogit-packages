# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: chn <g897331845@gmail.com>

pkgname=libcuckoo
pkgver=0.3.1
pkgrel=1
pkgdesc="A high-performance, concurrent hash table"
arch=('any')
url='https://github.com/efficient/libcuckoo'
license=('Apache')
makedepends=('cmake')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/efficient/libcuckoo/archive/refs/tags/v${pkgver}.tar.gz)
sha256sums=('a87122b963ef6b7eadca3e41cb4b533f3b6ad9b588ab2f2140eb6ab0db9a7ba8')

build() {
    mkdir build
    cd build
    cmake ../${pkgname}-${pkgver} \
      -DCMAKE_BUILD_TYPE='None' \
      -DCMAKE_INSTALL_PREFIX='/usr' \
      -DBUILD_TESTS=1 \
      -DBUILD_STRESS_TESTS=1 \
      -DBUILD_UNIT_TESTS=1
    make
}

check() {
  cd build
  ctest
}

package() {
    cd build
    make DESTDIR="$pkgdir" install
}
