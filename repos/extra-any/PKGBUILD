# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Alan Young <harleypig@gmail.com>

pkgname=perl-module-build
pkgver=0.4232
pkgrel=1
pkgdesc="Build, test, and install Perl modules"
arch=('any')
url="https://search.cpan.org/dist/Module-Build"
license=('PerlArtistic' 'GPL')
depends=('perl' 'perl-cpan-meta' 'perl-inc-latest')
checkdepends=('perl-par-dist' 'perl-archive-zip')
options=('!emptydirs')
source=(https://search.cpan.org/CPAN/authors/id/L/LE/LEONT/Module-Build-$pkgver.tar.gz)
sha512sums=('f8ff0aa918e1627af59417eb3d6cf63df52c452195905596f5a88482fc094c31b1963b0215627826b1cd33fa74893cadce138e5fd705adc14d9968f586da333c')

build() {
  cd Module-Build-$pkgver
  perl Build.PL installdirs=vendor
  perl Build
}

check() {
  cd Module-Build-$pkgver
  perl Build test
}

package() {
  cd Module-Build-$pkgver
  perl Build install destdir="$pkgdir"
}
