# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Sarah Hay <sarah@archlinux.org>
# Contributor: Arjan Timmerman <arjan@soulfly.nl>

pkgname=perl-extutils-depends
pkgver=0.307
pkgrel=1
pkgdesc="The Perl depends module"
arch=('any')
url="http://search.cpan.org/dist/ExtUtils-Depends/"
license=('PerlArtistic')
depends=('perl')
options=('!emptydirs')
source=(http://www.cpan.org/authors/id/X/XA/XAOC/ExtUtils-Depends-${pkgver}.tar.gz)
md5sums=('b08b1943e30a0b25c32130c3f4fc84f8')

build() {
  cd ExtUtils-Depends-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd ExtUtils-Depends-${pkgver}
  make test
}

package() {
  cd ExtUtils-Depends-${pkgver}
  make DESTDIR="${pkgdir}" install
}
