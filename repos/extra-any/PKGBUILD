# $Id: PKGBUILD,v 1.20 2008/12/29 15:30:49 BaSh Exp $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgbase=system-config-printer
pkgname=('system-config-printer-common'
	'system-config-printer-gnome')
#	'system-config-printer-udev)
pkgver=1.1.15
pkgrel=1
pkgdesc="A CUPS printer configuration tool and status applet"
url="http://cyberelk.net/tim/software/system-config-printer/"
arch=('any')
license=('GPL')
makedepends=('intltool' 'python' 'xmlto' 'docbook-xsl' 'desktop-file-utils' 'libcups' 'libxml2') # libusb with system-config-printer-udev
source=(http://cyberelk.net/tim/data/${pkgbase}/1.1/${pkgbase}-${pkgver}.tar.bz2)
md5sums=('8644039d17a841ac65e0b8b5aa601714')

build() {
  cd ${srcdir}/${pkgbase}-${pkgver}

  ./configure --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --disable-nls
  make || return 1
}

package_system-config-printer-common() {
  pkgdesc='Pygtk CUPS Configuration'
  depends=('pycups' 'dbus-python' 'pysmbc')
  optdepends=('system-config-printer-gnome: for the GTK frontend'
	'kdeadmin-system-config-printer-kde: for the administration tool in KDE System Settings')
#	'system-config-printer-udev: for automatic disabling/enabling when printer is connected')
  replaces=('system-config-printer')
  provides=("system-config-printer=$pkgver")
  conflicts=('system-config-printer')

  cd ${srcdir}/${pkgbase}-${pkgver}
  install -d ${pkgdir}/usr/share/${pkgbase}
  install -m644 config.py \
    debug.py \
    installpackage.py \
    monitor.py \
    PhysicalDevice.py \
    ppdippstr.py \
    probe_printer.py \
    SearchCriterion.py \
    smburi.py \
    statereason.py \
    XmlHelper.py \
    ${pkgdir}/usr/share/${pkgbase}/ || return 1

  install -m755 pysmb.py \
    ${pkgdir}/usr/share/${pkgbase}/ || return 1

  install -d ${pkgdir}/etc/dbus-1/system.d/
  install -m644 newprinternotification.conf \
    ${pkgdir}/etc/dbus-1/system.d/ || return 1

  for file in build/lib/cupshelpers/*.py; do
    install -Dm644 $file \
      ${pkgdir}/usr/lib/python2.6/site-packages/cupshelpers/$(basename $file) || return 1
  done
}

package_system-config-printer-gnome() {
  pkgdesc='A CUPS printer configuration tool and status applet - GTK frontend'
  depends=('system-config-printer-common' 'gnome-python' 'gnome-icon-theme' 'pygobject' 'pygtk' 'libxml2' 'python-notify')

  cd ${srcdir}/${pkgbase}-${pkgver}
  make DESTDIR=${pkgdir} install || return 1
  cd ${pkgdir}/usr/share/${pkgbase}
  rm config.py \
    debug.py \
    installpackage.py \
    monitor.py \
    PhysicalDevice.py \
    ppdippstr.py \
    probe_printer.py \
    SearchCriterion.py \
    smburi.py \
    statereason.py \
    XmlHelper.py \
    pysmb.py || return 1
  rm ${pkgdir}/etc/dbus-1/system.d/newprinternotification.conf || return 1
  rm -r ${pkgdir}/usr/lib/python2.6/site-packages/cupshelpers/ || return 1
}

#package_system-config-printer-udev() {
#  pkgdesc='A CUPS printer configuration tool and status applet - udev rules for auto-setup'
#  depends=('udev' 'libusb' 'system-config-printer-common')
#
#  cd ${srcdir}/${pkgbase}-${pkgver}/udev
#  install -d ${pkgdir}/lib/udev/rules.d
#  gcc -o udev-configure-printer udev-configure-printer.c || return 1
#  install -m755 udev-{add,configure}-printer \
#    ${pkgdir}/lib/udev || return 1
#  install -m644 70-printers.rules \
#    ${pkgdir}/lib/udev/rules.d || return 1
#}
