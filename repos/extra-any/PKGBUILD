# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-cpan-meta
pkgver=2.150005
pkgrel=1
pkgdesc='the distribution metadata for a CPAN dist'
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl>=5.8' 'perl-cpan-meta-requirements>=2.121'
         'perl-cpan-meta-yaml>=0.008' 'perl-parse-cpan-meta>=1.4414')
url='https://metacpan.org/release/CPAN-Meta'
source=("http://search.cpan.org/CPAN/authors/id/D/DA/DAGOLDEN/CPAN-Meta-$pkgver.tar.gz")
sha512sums=('90c29c20b6adc3963bedbca87d41a2ff12de62d12801f282eacf2380dfec4cfbe1d211e34fcc2f7b2a10acddd5441040ec15c12b5266443cb1c2518cf8fe3528')

_ddir="CPAN-Meta-$pkgver"

build() (
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  /usr/bin/perl Makefile.PL
  make
)

check() (
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
)

package() (
  cd "$srcdir/$_ddir"
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
)

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et: 
