# $Id$
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>

pkgbase=gnome-shell-extensions
pkgver=3.0.1
_date=20110513
pkgrel=0.$_date
arch=(any)
url="http://live.gnome.org/GnomeShell/Extensions"
license=(GPL2)
makedepends=(gnome-shell intltool)
source=(ftp://ftp.archlinux.org/other/$pkgbase/$pkgbase-$pkgver-$_date.tar.xz)
sha256sums=('d4a7a3344bc0043abcf249bcbd7f7816e46d8462e5553344f3965305f063983d')

pkgname=(gnome-shell-extensions-common
         gnome-shell-extension-alternate-tab
         gnome-shell-extension-windows-navigator
         gnome-shell-extension-dock
         gnome-shell-extension-auto-move-windows
         gnome-shell-extension-user-theme
         gnome-shell-extension-drive-menu
         gnome-shell-extension-alternative-status-menu
         gnome-shell-extension-places-menu
         gnome-shell-extension-native-window-placement)
         # Doesn't seem to do work at the moment
         #gnome-shell-extension-system-monitor)

build() {
  _extensions=(alternate-tab windowsNavigator dock
               auto-move-windows user-theme drive-menu
               alternative-status-menu places-menu
               native-window-placement) # systemMonitor)

  cd "$srcdir/$pkgbase-$pkgver"
  ./configure --prefix=/usr \
    --enable-extensions="${_extensions[*]}" \
    --disable-schemas-compile
  make
}

package_gnome-shell-extensions-common() {
  pkgdesc="Extensions for GNOME Shell - common files"
  depends=(gnome-shell)

  cd "$srcdir/$pkgbase-$pkgver/po"
  make DESTDIR="$pkgdir" install
}

package_gnome-shell-extension-alternate-tab() {
  pkgdesc="Alternate Tab extension for GNOME Shell"
  depends=(gnome-shell-extensions-common)

  cd "$srcdir/$pkgbase-$pkgver/extensions/alternate-tab"
  make DESTDIR="$pkgdir" install
}

package_gnome-shell-extension-windows-navigator() {
  pkgdesc="Windows Navigator extension for GNOME Shell"
  depends=(gnome-shell-extensions-common)

  cd "$srcdir/$pkgbase-$pkgver/extensions/windowsNavigator"
  make DESTDIR="$pkgdir" install
}

package_gnome-shell-extension-dock() {
  pkgdesc="Dock extension for GNOME Shell"
  depends=(gnome-shell-extensions-common)

  cd "$srcdir/$pkgbase-$pkgver/extensions/dock"
  make DESTDIR="$pkgdir" install
}

package_gnome-shell-extension-auto-move-windows() {
  pkgdesc="Auto Move Windows extension for GNOME Shell"
  depends=(gnome-shell-extensions-common)
  install=gnome-shell-extensions.install

  cd "$srcdir/$pkgbase-$pkgver/extensions/auto-move-windows"
  make DESTDIR="$pkgdir" install
}

package_gnome-shell-extension-user-theme() {
  pkgdesc="User Theme extension for GNOME Shell"
  depends=(gnome-shell-extensions-common)
  install=gnome-shell-extensions.install

  cd "$srcdir/$pkgbase-$pkgver/extensions/user-theme"
  make DESTDIR="$pkgdir" install
}

package_gnome-shell-extension-drive-menu() {
  pkgdesc="Drive Menu extension for GNOME Shell"
  depends=(gnome-shell-extensions-common)

  cd "$srcdir/$pkgbase-$pkgver/extensions/drive-menu"
  make DESTDIR="$pkgdir" install
}

package_gnome-shell-extension-alternative-status-menu() {
  pkgdesc="Alternative Status Menu extension for GNOME Shell"
  depends=(gnome-shell-extensions-common)

  cd "$srcdir/$pkgbase-$pkgver/extensions/alternative-status-menu"
  make DESTDIR="$pkgdir" install
}

package_gnome-shell-extension-places-menu() {
  pkgdesc="Places Menu extension for GNOME Shell"
  depends=(gnome-shell-extensions-common)

  cd "$srcdir/$pkgbase-$pkgver/extensions/places-menu"
  make DESTDIR="$pkgdir" install
}

package_gnome-shell-extension-native-window-placement() {
  pkgdesc="Native Window Placement extension for GNOME Shell"
  depends=(gnome-shell-extensions-common)

  cd "$srcdir/$pkgbase-$pkgver/extensions/native-window-placement"
  make DESTDIR="$pkgdir" install
}

package_gnome-shell-extension-system-monitor() {
  pkgdesc="System Monitor extension for GNOME Shell"
  depends=(gnome-shell-extensions-common)

  cd "$srcdir/$pkgbase-$pkgver/extensions/systemMonitor"
  make DESTDIR="$pkgdir" install
}
