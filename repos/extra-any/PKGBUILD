# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-sudoku
pkgver=3.12.2
pkgrel=1
pkgdesc="Test your logic skills in this number grid puzzle"
arch=('any')
license=('GPL')
depends=('hicolor-icon-theme' 'python-gobject' 'dconf' 'gtk3')
makedepends=('intltool' 'itstool')
conflicts=('gnome-games')
replaces=('gnome-games')
options=('!emptydirs')
install=gnome-sudoku.install
url="https://live.gnome.org/GnomeSudoku"
groups=('gnome-extra')
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('31b57cdb60a0d4ecab529628822e04a700659ce7bb84aaa56b3c318500b1dd8b')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  sed -i 's_#! /usr/bin/python_#!/usr/bin/python2_' "$pkgdir/usr/bin/gnome-sudoku"
}
