# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=archlinux-appstream-data
pkgver=20171217
pkgrel=1
pkgdesc="Arch Linux application database for AppStream-based software centers"
arch=(any)
url="http://www.archlinux.org"
license=(GPL)
depends=()
makedepends=()
source=()
noextract=()
for _repo in core extra community; do
 source+=($_repo.xml.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/Components-x86_64.xml.gz
          $_repo-icons-64x64.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-64x64.tar.gz
          $_repo-icons-128x128.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-128x128.tar.gz)
 noextract+=($_repo.xml.gz $_repo-icons-{64x64,128x128}.tar.gz)
done
sha256sums=('da61313bcbd9afd012064b6cacf3fdadd29caca6a8bfb94b1b5e2c0f7a20232b'
            '7989bb311baa38ef545250282aa065d23281c46dfb8faabe4c653487bdbded5c'
            '7989bb311baa38ef545250282aa065d23281c46dfb8faabe4c653487bdbded5c'
            'a1e72abe2afb9107475e472c4c2cefec74f396de7d8236215abcbe81d4211dcc'
            '6dbf54fe7b6bd19292474d0142de77492e10c7405502f9a4945fcc0462018833'
            '2ee0a77d8f7b2577436c96fff4215e82ee92070dec9118787524af89a300d24e'
            '477810ec71f5b233438d2bcb8764ebe8969aaadc33e71c30cfec95f8e63ebc4b'
            '392170eb1cfc5d89ccc96b22486623779a8ef7c4b0d59780613bf5c24cba2925'
            'ea13f96b68afd9650628f6e1fe5d5cdbb9158ca2ad733a2ad6e8f53fd5e6349d')

package() {
  mkdir -p "$pkgdir"/usr/share/app-info/{icons/archlinux-arch-{core,extra,community}/{64x64,128x128},xmls}
  for _repo in core extra community; do
   tar -xzf $_repo-icons-64x64.tar.gz -C "$pkgdir"/usr/share/app-info/icons/archlinux-arch-$_repo/64x64
   tar -xzf $_repo-icons-128x128.tar.gz -C "$pkgdir"/usr/share/app-info/icons/archlinux-arch-$_repo/128x128
  done
  cp *.xml.gz "$pkgdir"/usr/share/app-info/xmls/
}
