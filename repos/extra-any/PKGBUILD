# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=cantarell-fonts
pkgver=0.200
pkgrel=1
epoch=1
pkgdesc="Humanist sans serif font"
url="https://gitlab.gnome.org/GNOME/cantarell-fonts"
arch=(any)
license=(custom:SIL)
makedepends=(meson appstream-glib git)
_commit=b4f71a432aec52c250c2b7af4692376659cb085c  # tags/v0.200^0
source=("git+https://gitlab.gnome.org/GNOME/cantarell-fonts.git#commit=$_commit"
        cattrs.diff)
sha256sums=('SKIP'
            'e6e8cce13fbce7e6923425b6a5de81dd50eadf7ea4b7d6d096cb91648535b214')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  python -m venv venv
  venv/bin/pip install -r $pkgname/requirements.txt

  # https://github.com/Tinche/cattrs/compare/v0.9.0..v1.0.0
  patch -d venv/lib/python3.8/site-packages -Np2 < cattrs.diff
}

build() {
  PATH="$srcdir/venv/bin:$PATH" arch-meson $pkgname build
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 $pkgname/COPYING
}

# vim:set ts=2 sw=2 et:
