# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Manolis Tzanidakis
# Contributor: Firmicus <francois.archlinux.org> 

pkgname=perl-uri
pkgver=5.18
pkgrel=1
pkgdesc="Uniform Resource Identifiers (absolute and relative)"
arch=('any')
url="https://search.cpan.org/dist/URI/"
license=('PerlArtistic')
depends=('perl')
checkdepends=('perl-test-fatal' 'perl-test-needs' 'perl-test-warnings')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/S/SI/SIMBABQUE/URI-$pkgver.tar.gz")
sha512sums=('86fd8fdf0a654c57a3599632976dee8bc59b34fbdf8c55c099fc0da24d332db6ddf3ef087b6e7d1521ed3a657629db77e770b6f379010b2441d9d61b21d3d377')

build() {
  cd URI-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd URI-$pkgver
  make test
}

package() {
  cd URI-$pkgver
  make install DESTDIR="$pkgdir"
}
# vim: ts=2 sw=2 et ft=sh
