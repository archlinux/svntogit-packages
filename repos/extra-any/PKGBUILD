# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=grml-zsh-config
pkgver=0.14.3
pkgrel=1
pkgdesc="grml's zsh setup"
arch=('any')
url='https://grml.org/zsh/'
license=('GPL2')
provides=('grmlzshrc')
depends=('zsh' 'coreutils' 'inetutils' 'grep' 'sed' 'procps')
makedepends=('txt2tags')
source=("https://deb.grml.org/pool/main/g/grml-etc-core/grml-etc-core_${pkgver}.tar.gz")
sha256sums=('c02e14ea2d6ec3db0ee1bf5411c35c3b34a581a2b79c573b905a7baf01fc9ed5')

build() {
	cd ${srcdir}/grml-etc-core-${pkgver}/doc
	make
}

package() {
	cd ${srcdir}/grml-etc-core-${pkgver}
	install -D -m644 etc/skel/.zshrc ${pkgdir}/etc/skel/.zshrc
	install -D -m644 etc/zsh/keephack ${pkgdir}/etc/zsh/keephack
	install -D -m644 etc/zsh/zshrc ${pkgdir}/etc/zsh/zshrc

	install -D -m644 doc/grmlzshrc.5 ${pkgdir}/usr/share/man/man5/grmlzshrc.5
	ln -sf grmlzshrc.5.gz ${pkgdir}/usr/share/man/man5/grml-zsh-config.5.gz
}
