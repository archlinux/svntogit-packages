# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=mkinitcpio-archiso
pkgver=62
pkgrel=1
pkgdesc="Initcpio scripts used by archiso"
arch=(any)
url="https://gitlab.archlinux.org/mkinitcpio/mkinitcpio-archiso"
license=(GPL3)
depends=(bash device-mapper mkinitcpio)
makedepends=(git)
checkdepends=(shellcheck shfmt)
optdepends=(
  'curl: for PXE over HTTP'
  'mkinitcpio-nfs-utils: for PXE over NFS'
  'nbd: for PXE over NBD'
  'pv: for status display during copy to RAM'
)
source=("git+https://gitlab.archlinux.org/mkinitcpio/${pkgname}.git#tag=v${pkgver}?signed")
sha256sums=('SKIP')
validpgpkeys=(
  'C7E7849466FE2358343588377258734B41C31549'  # David Runge <dvzrv@archlinux.org>
  'BB8E6F1B81CF0BB301D74D1CBF425A01E68B38EF'  # nl6720 <nl6720@archlinux.org>
)

check() {
  cd "$pkgname"
  make -k check
}

package() {
  cd "$pkgname"
  make DESTDIR="$pkgdir/" PREFIX=/usr install
}
