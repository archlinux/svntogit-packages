# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Marcin "avalan" Falkiewicz <avalatron@gmail.com>
# Contributor: C Anthony Risinger <anthony@xtfx.me>

pkgbase=python-ply
pkgname=(python-ply python2-ply)
pkgver=3.5
pkgrel=1
pkgdesc='Implementation of lex and yacc parsing tools'
arch=('any')
url='http://www.dabeaz.com/ply/'
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools')
source=("${url}ply-$pkgver.tar.gz")
sha256sums=('9c649e4791dec47e87ccd157866a4666243834403eb38af5b188e602b3d79291')

prepare() {
  cp -a ${pkgbase#python-}-$pkgver{,-py2}
}

package_python-ply() {
  depends=('python')

  cd "${pkgbase#python-}-$pkgver"
  python setup.py install --root="$pkgdir"
}

package_python2-ply() {
  depends=('python2')

  cd "${pkgbase#python-}-$pkgver-py2"
  python2 setup.py install --root="$pkgdir"
}

# vim:set ts=2 sw=2 et:
