# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=adobe-source-code-pro-fonts
pkgver=2.042u+1.062i+1.026vf
pkgrel=1
pkgdesc="Monospaced font family for user interface and coding environments"
url="https://adobe-fonts.github.io/source-code-pro/"
arch=(any)
license=(custom)
_relver=2.042R-u/1.062R-i/1.026R-vf
_tarname=source-code-pro-${_relver//\//-}
source=(
  "$_tarname.tar.gz::https://github.com/adobe-fonts/source-code-pro/archive/refs/tags/$_relver.tar.gz"
)
b2sums=('16d5097bb8adbdbf0c0c61f708ea460e7608ebde7921fed464a2b71ed159416d9920bcbd72ad63d47a89460426a5c5af0c5a19ebac00ee7a1db0d61ae77a9823')

pkgver() {
  <<<"$_relver" sed 's/R-//g;s|/|+|g'
}

package() {
  cd $_tarname
  install -Dt "$pkgdir/usr/share/fonts/${pkgname%-fonts}" -m644 OTF/*.otf VF/*.otf
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 LICENSE.md
}

# vim:set sw=2 sts=-1 et:
