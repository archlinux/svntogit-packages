# Maintainer: Felix Yan <felixonmars@gmail.com>
# Conributor: kevin <kevin@archlinux.org>
# Contributor: Manolis Tzanidakis

pkgname=perl-html-tagset
pkgver=3.20
pkgrel=13
pkgdesc="Data tables useful in parsing HTML"
arch=('any')
license=('PerlArtistic')
url="https://search.cpan.org/dist/HTML-Tagset/"
depends=('perl')
options=(!emptydirs)
source=(https://www.cpan.org/authors/id/P/PE/PETDANCE/HTML-Tagset-$pkgver.tar.gz)
sha256sums=('adb17dac9e36cd011f5243881c9739417fd102fce760f8de4e9be4c7131108e2')

build() {
  cd HTML-Tagset-$pkgver
  # install module in vendor directories.
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd HTML-Tagset-$pkgver
  make test
}

package() {
  cd HTML-Tagset-$pkgver
  make install DESTDIR="$pkgdir"
}

# vim: ts=2 sw=2 et ft=sh
