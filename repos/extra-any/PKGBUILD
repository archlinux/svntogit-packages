# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=mm-common
pkgver=1.0.0
pkgrel=1
pkgdesc="Common build files of the C++ bindings"
url="http://www.gtkmm.org/"
arch=(any)
depends=(graphviz doxygen gtk-doc libsigc++-docs perl-xml-parser)
makedepends=(libsigc++ git meson)
license=(GPL2)
_commit=ffba112d04908cd7badc7e7d39cb09bf6827ec7a  # tags/1.0.0^0
source=("git+https://gitlab.gnome.org/GNOME/mm-common.git#commit=$_commit"
        https://gcc.gnu.org/onlinedocs/libstdc++/latest-doxygen/libstdc++.tag)
sha256sums=('SKIP'
            'f6c8c74e8293aefaefb4d25fd154d5f537b3bc80e7ceecaa02c5a01836fc09e6')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  cp ../libstdc++.tag doctags
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}

# vim:set ts=2 sw=2 et:
