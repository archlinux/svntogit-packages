# $Id$
# Maintainer:

pkgname=perl-log-log4perl
pkgver=1.43
pkgrel=1
pkgdesc='Log4j implementation for Perl'
arch=('any')
url="https://metacpan.org/release/Log-Log4perl"
license=('PerlArtistic' 'GPL')
depends=('perl')
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/M/MS/MSCHILLI/Log-Log4perl-$pkgver.tar.gz")
md5sums=('c7b15008426c31f14726ed1257cd7b68')

build() {
  cd Log-Log4perl-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Log-Log4perl-$pkgver
  make test
}

package() {
  cd Log-Log4perl-$pkgver
  make DESTDIR="$pkgdir" install
}
