# Maintainer: David Runge <dvzrv@archlinux.org>

_name=notify2
pkgname=python-notify2
pkgver=0.3.1
pkgrel=8
pkgdesc="Python interface to DBus notifications"
arch=('any')
url="https://bitbucket.org/takluyver/pynotify2"
license=('BSD')
depends=('python-dbus')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('0b69a64e0a7e4955ee767c975db92c5c60d28c0e54f77498cfb98390771a8f536631fc34e42b5c46c138dfa1054b5516a0809b8e3b0cb9237a39825ddb827109')
b2sums=('649df1934d1fffb97dabbf2f483204c55427fbede851840d642da2e4e62a9bda9d49670e60f5b2cda36b62ee47b7d0cd861d757d338867547dce3c7d61ddf7b4')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}
# NOTE: tests require running dbus

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 examples/* -t "${pkgdir}/usr/share/doc/${pkgname}/examples"
}
