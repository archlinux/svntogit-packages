# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Gerardo Exequiel Pozzi <djgera@archlinux.org>

pkgname=archiso
pkgver=44
pkgrel=2
pkgdesc='Tools for creating Arch Linux live and install iso images'
arch=('any')
license=('GPL')
url="https://gitlab.archlinux.org/archlinux/archiso"
depends=('arch-install-scripts' 'curl' 'dosfstools' 'edk2-shell' 'libisoburn'
'lynx' 'squashfs-tools')
makedepends=('git')
source=("git+https://gitlab.archlinux.org/archlinux/archiso.git#tag=v${pkgver}?signed")
sha256sums=('SKIP')
validpgpkeys=('C7E7849466FE2358343588377258734B41C31549') # David Runge <dvzrv@archlinux.org>

package() {
  cd "${pkgname}"
  make DESTDIR="${pkgdir}" install
}
