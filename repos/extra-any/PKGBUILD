# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-file-desktopentry
pkgver=0.22
pkgrel=10
pkgdesc='Object to handle .desktop files'
arch=('any')
license=('GPL' 'PerlArtistic')
url="https://search.cpan.org/dist/File-DesktopEntry/"
depends=('perl-file-basedir' 'perl-uri')
checkdepends=('perl-test-pod' 'perl-test-pod-coverage')
options=('!emptydirs')
source=(https://search.cpan.org/CPAN/authors/id/M/MI/MICHIELB/File-DesktopEntry-$pkgver.tar.gz)
sha256sums=('169c01e3dae2f629767bec1a9f1cdbd6ec6d713d1501e0b2786e4dd1235635b8')

build() {
  cd File-DesktopEntry-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd File-DesktopEntry-$pkgver
  make test
}

package() {
  cd File-DesktopEntry-$pkgver
  make DESTDIR="${pkgdir}" install
}
