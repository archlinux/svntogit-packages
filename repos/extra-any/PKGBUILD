# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=orca
pkgver=43.1
pkgrel=1
pkgdesc="Screen reader for individuals who are blind or visually impaired"
url="https://wiki.gnome.org/Projects/Orca"
arch=(any)
license=(LGPL)
depends=(
  gtk3 at-spi2-core python-atspi python-dbus python-xdg speech-dispatcher
  python-setproctitle liblouis brltty xorg-xmodmap gsettings-desktop-schemas
  gst-plugins-base # playbin, audiotestsrc, basic decoders
  gst-plugins-good # pulsesink, more decoders
)
makedepends=(itstool git yelp-tools)
groups=(gnome)
_commit=a71ad7c67b1693af0fe578cbaa809957b12de912  # gnome-43~2
source=("git+https://gitlab.gnome.org/GNOME/orca.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd orca
  git describe --tags | sed 's/ORCA_//;s/_/\./g;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd orca

  # Fixup missing tag
  git tag -f ORCA_43_1 a71ad7c67b1693af0fe578cbaa809957b12de912

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd orca
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd orca
  make DESTDIR="$pkgdir" install
}

# vim:set sw=2 sts=-1 et:
