# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Sarah Hay <sarah@archlinux.org>
# Contributor: Arjan Timmerman <arjan@soulfly.nl>

pkgname=perl-extutils-pkgconfig
pkgver=1.15
pkgrel=1
pkgdesc="The Perl Pkgconfig module"
arch=('any')
url="http://gtk2-perl.sourceforge.net/"
license=('LGPL')
depends=('perl')
options=('!emptydirs')
source=(http://downloads.sourceforge.net/sourceforge/gtk2-perl/ExtUtils-PkgConfig-${pkgver}.tar.gz)
md5sums=('1872e659cf2bb0b2471d75fce735e622')

build() {
  cd ExtUtils-PkgConfig-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd ExtUtils-PkgConfig-${pkgver}
  make test
}

package() {
  cd ExtUtils-PkgConfig-${pkgver}
  make DESTDIR="${pkgdir}" install
}
