# $Id$
# Maintainer: Laurent Carlier <lordheavym@gmail.com>

pkgbase=vulkan-docs
pkgname=(vulkan-headers vulkan-html-docs)
pkgver=1.0.49
pkgrel=1
epoch=1
_pkgname=Vulkan-Docs
arch=(any)
url="https://www.khronos.org/vulkan/"
license=('custom')
makedepends=(asciidoctor python3 dblatex source-highlight cmake ghostscript git
             lyx pango gdk-pixbuf2) # for asciidoctor-mathematical
groups=(vulkan-devel)
source=("https://github.com/KhronosGroup/${_pkgname}/archive/v${pkgver}-core.tar.gz"
        git+https://github.com/leethomason/tinyxml2.git#commit=c8dad95
        git+https://github.com/KhronosGroup/Vulkan-Hpp.git#commit=6f37c62)
sha256sums=('1f6940a32f505c212df93e95908cf73cfa4516f550322bbb9958c38d803d48bf'
            'SKIP'
            'SKIP')

prepare() {
  cd Vulkan-Hpp
  git submodule init
  git config submodule.tinyxml2.url $srcdir/tinyxml2
  git submodule update

  # for asciidoctor
  # it was hard to find versions that builds properly ^^
  local _gemdir="$(ruby -e 'puts Gem.default_dir')"

  gem install -v 0.7.0 ruby-enum
  gem install -v 1.6.8 mathematical
  gem install -v 0.2.2 asciidoctor-mathematical  
  gem install -v 1.5.0.alpha.15 asciidoctor-pdf  
  gem install coderay  
}

build() {
  # we need this to build hpp headers
  cd Vulkan-Hpp
  cmake ./ && make

  ./VulkanHppGenerator "${srcdir}/${_pkgname}-${pkgver}-core/src/spec/vk.xml"

  cd "${srcdir}/${_pkgname}-${pkgver}-core/doc/specs/vulkan"

  # asciidoctor / mathematical workaround bug - fail to find liblasem.so
  cp ~/.gem/ruby/2.4.0/gems/mathematical-1.6.8/ext/mathematical/lasem/build/liblasem.so ${srcdir}
  LD_LIBRARY_PATH=${srcdir}:$LD_LIBRARY_PATH ./makeKHRAndKHX all

  rm -f ${srcdir}/liblasem.so
}

package_vulkan-headers() {
  pkgdesc="Vulkan header files"
  cd "${_pkgname}-${pkgver}-core"

  install -dm755 "${pkgdir}/usr/include/vulkan"
  install -dm755 "${pkgdir}/usr/share/vulkan"
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"

  install -m644 src/vulkan/vk_platform.h ${pkgdir}/usr/include/vulkan/
  install -m644 src/vulkan/vulkan.h ${pkgdir}/usr/include/vulkan
  install -m644 ${srcdir}/Vulkan-Hpp/vulkan/vulkan.hpp ${pkgdir}/usr/include/vulkan
  install -m644 src/spec/vk.xml ${pkgdir}/usr/share/vulkan

  install -m644 doc/specs/vulkan/copyright-ccby.txt ${pkgdir}/usr/share/licenses/${pkgname}/copyright-ccby.txt
  install -m644 doc/specs/vulkan/copyright-spec.txt ${pkgdir}/usr/share/licenses/${pkgname}/copyright-spec.txt
}

package_vulkan-html-docs() {
  pkgdesc="Vulkan html documentation"
  cd "${_pkgname}-${pkgver}-core"

  install -dm755 "${pkgdir}/usr/share/doc/vulkan"
  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"

  mv -v out/1.0/*.html "${pkgdir}/usr/share/doc/vulkan/"
  mv -v out/1.0/html/* "${pkgdir}/usr/share/doc/vulkan/"

  install -m644 doc/specs/vulkan/copyright-ccby.txt ${pkgdir}/usr/share/licenses/${pkgname}/copyright-ccby.txt
  install -m644 doc/specs/vulkan/copyright-spec.txt ${pkgdir}/usr/share/licenses/${pkgname}/copyright-spec.txt
}
