# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>

pkgname=deluge
pkgver=2.0.3+23+g5f1eada3e
pkgrel=1
pkgdesc="A BitTorrent client with multiple user interfaces in a client/server model"
arch=(any)
url="https://deluge-torrent.org/"
license=(GPL3)
depends=(
  # Follows DEPENDS.md
  'python-twisted>=17.1' python-service-identity python-idna
  'openssl>=1.0.1'
  python-pyopenssl
  'python-rencode>=1.0.2'
  python-xdg
  xdg-utils
  python-six
  'python-zope-interface>=4.4.2' 
  python-chardet 
  python-setproctitle 
  python-pillow
  python-dbus
  python-distro
  'libtorrent-rasterbar>=1.1.1'
  python-geoip
  python-mako
)
makedepends=(
  python-setuptools
  intltool
  gtk3
  python-gobject
  python-cairo
  librsvg
  libappindicator-gtk3
  #python-pygame
  libnotify
  git
)
optdepends=(
  'gtk3: gtk ui'
  'python-gobject: gtk ui'
  'python-cairo: gtk ui'
  'librsvg: gtk ui'
  'libappindicator-gtk3: appindicator notifications'
  #'python-pygame: audible notifications'
  'libnotify: desktop notifications'
)
_commit=5f1eada3eae215f0fd489000e97792c892fb7b17  # develop
source=("git://git.deluge-torrent.org/deluge.git#commit=$_commit"
        py3.8.diff)
sha256sums=('SKIP'
            'a0225692e5c312d7980f0047f8e840803e8e05d6c525464ae9f335f56e205297')

pkgver() {
  cd $pkgname
  git describe | sed 's/^deluge-//;s/-/+/g'
}

prepare() {
  cd $pkgname

  # Remove a broken logging.Logger.findCaller override
  # https://bugs.archlinux.org/task/64571
  patch -Np1 -i ../py3.8.diff
}

build() {
  cd $pkgname
  python setup.py build
}

package() {
  cd $pkgname
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dt "$pkgdir/usr/lib/systemd/system" \
    -m644 packaging/systemd/*.service
  install -Dt "$pkgdir/usr/lib/systemd/system/deluged.service.d" \
    -m644 packaging/systemd/user.conf
  install -Dt "$pkgdir/usr/lib/systemd/system/deluge-web.service.d" \
    -m644 packaging/systemd/user.conf

  echo 'u deluge - "Deluge BitTorrent daemon" /srv/deluge' |
    install -Dm644 /dev/stdin "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"
  echo 'd /srv/deluge 0770 deluge deluge' |
    install -Dm644 /dev/stdin "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"
}

# vim:set sw=2 et:
