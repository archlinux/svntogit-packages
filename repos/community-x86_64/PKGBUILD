# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=zimg
pkgver=3.0.3
pkgrel=1
pkgdesc='Scaling, colorspace conversion, and dithering library'
arch=(x86_64)
url=https://github.com/sekrit-twc/zimg
license=(custom:WTFPL)
depends=(
  gcc-libs
  glibc
)
makedepends=(git)
provides=(libzimg.so)
options=(!emptydirs)
_tag=1658ce429cf944e88868a88d8aef4683425d4e78
source=(git+https://github.com/sekrit-twc/zimg.git#tag=${_tag})
sha256sums=(SKIP)

prepare() {
  cd zimg
  ./autogen.sh
}

pkgver() {
  cd zimg
  git describe --tags | sed 's/^release-//'
}

build() {
  cd zimg
  ./configure \
    --prefix=/usr \
    --enable-x86simd \
    --disable-static
  make
}

package(){
  make DESTDIR="${pkgdir}" -C zimg install
  install -Dm 644 zimg/COPYING -t "${pkgdir}"/usr/share/licenses/zimg/
}

# vim: ts=2 sw=2 et:
