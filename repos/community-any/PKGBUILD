# Maintainer: Jelle van der Waa <jelle@archlinux.org>

_name=pytest-qt
pkgname=python-pytest-qt
pkgver=4.0.2
pkgrel=1
pkgdesc='pytest support for PyQt and PySide applications'
arch=(any)
license=('MIT')
url='https://github.com/pytest-dev/pytest-qt'
depends=('python-pytest')
makedepends=('python-setuptools-scm')
checkdepends=('python-pyqt5' 'pyside2' 'xorg-server-xvfb')
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha512sums=('7f59c499e1395460c2ff20a969cec16ca104f742c648faa0d7eb1528c04d8ee52c04c52798478bc01c5467bee33d3389b141424491ba5442f9df4e82f0dff173')

build() {
  cd ${_name}-$pkgver
  python setup.py build
}

check() {
  cd ${_name}-$pkgver/src
  PYTHONPATH=. PYTEST_QT_API=pyqt5 xvfb-run pytest --fixtures pytestqt ../tests
}

package() {
  cd ${_name}-$pkgver
  python setup.py install -O1 --root="${pkgdir}" --skip-build
  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
