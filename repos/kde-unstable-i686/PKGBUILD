# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kig
pkgver=15.03.90
pkgrel=1
pkgdesc="Interactive Geometry"
url="http://kde.org/applications/education/kig/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
depends=('ktexteditor' 'kdelibs4support' 'boost-libs')
makedepends=('extra-cmake-modules' 'kdoctools' 'python' 'boost')
replaces=('kdeedu-kig')
conflicts=('kdeedu-kig')
install=${pkgname}.install
source=("http://download.kde.org/unstable/applications/${pkgver}/src/kig-${pkgver}.tar.xz")
sha1sums=('2ab44a242d8660e00cb3ff0011c2b063c62e77e5')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kig-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install

  # Use the python2 executable
  sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|' "${pkgdir}"/usr/bin/pykig.py
}
