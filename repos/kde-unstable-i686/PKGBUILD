# $Id$
# Maintainer: Ronald van Haren <ronald@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgbase=digikam
pkgname=('digikam' 'kipi-plugins')
_pkgver=5.0.0-beta2
pkgver=${_pkgver//-/_}
pkgrel=1
pkgdesc="Digital photo management application for KDE"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.digikam.org/"
makedepends=('extra-cmake-modules' 'libkexiv2' 'libkdcraw' 'libkipi' 'libksane' 'liblqr' 'opencv' 'boost'
             'libgpod' 'hugin' 'opencv' 'qt5-webkit' 'doxygen' 'lensfun' 'imagemagick' 'eigen' 'libpgf'
             'libusb' 'libkface' 'kio' 'kdoctools' 'marble' 'kfilemetadata' 'akonadi-contact' 'qt5-multimedia'
             'threadweaver' 'kcalcore' 'knotifyconfig' 'kdelibs4support' 'clang') # 'libmediawiki' 'libkvkontakte'
source=("http://download.kde.org/unstable/${pkgname}/${pkgname}-${_pkgver}.tar.bz2")
sha1sums=('db2f6d7e84e67a338bee9373bdba2dc116f5e1f0')

build() {
  mkdir build
  cd build

  cmake ../${pkgname}-${_pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DBUILD_TESTING=OFF \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DENABLE_KFILEMETADATASUPPORT=ON \
    -DENABLE_MEDIAPLAYER=ON \
    -DENABLE_AKONADICONTACTSUPPORT=ON \
    -DCMAKE_C_COMPILER=clang # Fix error: C++ style comments are not allowed in ISO C90
  make
}

package_digikam() {
  pkgdesc="Digital photo management application for KDE"
  depends=('libgphoto2' 'liblqr' 'libkipi' 'lensfun' 'libpgf'
	'knotifyconfig' 'libksane' 'kfilemetadata' 'kitemmodels' 'qt5-multimedia' 'marble')
  optdepends=('kipi-plugins: more tools and plugins')
  install=digikam.install

  cd build/core
  make DESTDIR="${pkgdir}" install
  cd ../../build/doc/digikam/digikam
  make DESTDIR="${pkgdir}" install
  cd ../showfoto
  make DESTDIR="${pkgdir}" install
#  cd ../../../../build/po
#  make DESTDIR="${pkgdir}" install
}

package_kipi-plugins() {
  pkgdesc="A collection of plugins extending the KDE graphics and image applications as digiKam"
  depends=('kio' 'libkipi')
  optdepends=('libkgeomap4: Geolocalize tool'
      #       'libmediawiki: MediaWiki Export plugin'
      #       'libkvkontakte: VKontakte.ru Exporter plugin'
              'libgpod: iPodExport plugin'
              'opencv: Remove Red Eyes plugin'
              'imagemagick: Video SlideShow plugin'
              'qt5-gstreamer: Video SlideShow plugin'
              'threadweaver: panorama plugin'
	      'hugin: panorama plugin')
  install=kipi-plugins.install

  cd build/extra/kipi-plugins
  make DESTDIR="${pkgdir}" install

#  cd ../../../build/po
#  make DESTDIR="${pkgdir}" install

  cd ../../../build/doc/kipi-plugins
  make DESTDIR="${pkgdir}" install
}
