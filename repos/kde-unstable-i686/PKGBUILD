# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=spectacle
pkgver=15.11.80
pkgrel=1
pkgdesc="KDE screenshot capture utility"
arch=(i686 x86_64)
url='https://projects.kde.org/spectacle'
license=(GPL)
depends=(kio xcb-util-cursor libkscreen libkipi hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools)
conflicts=(kdegraphics-ksnapshot kscreengenie)
replaces=(kdegraphics-ksnapshot kscreengenie)
source=("http://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
install=$pkgname.install
md5sums=('a7f2d72ea739f54c7b2dae87eefe63f5')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
   -DCMAKE_INSTALL_PREFIX=/usr \
   -DCMAKE_BUILD_TYPE=Release \
   -DCMAKE_INSTALL_LIBDIR=lib \
   -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}

