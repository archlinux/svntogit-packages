# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux,org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=dolphin-plugins
pkgver=16.11.90
pkgrel=1
pkgdesc="Extra Dolphin plugins"
arch=(i686 x86_64)
url="https://www.kde.org/applications/system/dolphin/"
license=(LGPL)
depends=(dolphin)
makedepends=(extra-cmake-modules kdoctools ktexteditor kdesignerplugin)
optdepends=('ktexteditor: Mercurial plugin')
conflicts=(kdesdk-dolphin-plugins)
replaces=(kdesdk-dolphin-plugins)
groups=(kde-applications kdesdk)
source=("http://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
md5sums=('59a0f5509f1781da0716b929ff40dd0d'
         'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
