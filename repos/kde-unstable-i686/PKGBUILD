# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=polkit-qt
pkgver=0.99.0
pkgrel=1
pkgdesc='A library that allows developers to access PolicyKit API with a nice Qt-style API'
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('LGPL')
depends=('polkit' 'qt')
makedepends=('cmake' 'automoc4')
source=("ftp://ftp.kde.org/pub/kde/stable/apps/KDE4.x/admin/${pkgname}-1-${pkgver}.tar.bz2")
md5sums=('1c5b4113a2a167624b5f716b4f03a219')

build() {
	cd ${srcdir}
	mkdir build
	cd build
	cmake ../${pkgname}-1-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package() {
	cd ${srcdir}/build
	make DESTDIR=$pkgdir install
}
