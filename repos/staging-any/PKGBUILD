# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=orca
pkgver=44.0
pkgrel=1
pkgdesc="Screen reader for individuals who are blind or visually impaired"
url="https://wiki.gnome.org/Projects/Orca"
arch=(any)
license=(LGPL)
depends=(
  at-spi2-core
  brltty
  gsettings-desktop-schemas
  gst-plugins-base # playbin, audiotestsrc, basic decoders
  gst-plugins-good # pulsesink, more decoders
  gtk3
  liblouis
  python-atspi
  python-dbus
  python-setproctitle
  python-xdg
  speech-dispatcher
  xorg-xmodmap
)
makedepends=(
  git
  itstool
  yelp-tools
)
groups=(gnome)
_commit=ceb644cf38cf84a7950460534d8843f2078fd83a  # tags/ORCA_44_0^0
source=("git+https://gitlab.gnome.org/GNOME/orca.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd orca
  git describe --tags | sed 's/ORCA_//;s/_/\./g;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd orca
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd orca
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd orca
  make DESTDIR="$pkgdir" install
}

# vim:set sw=2 sts=-1 et:
