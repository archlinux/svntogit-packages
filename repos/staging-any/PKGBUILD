# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgbase=scour
pkgname=('scour' 'python2-scour')
pkgver=0.36
pkgrel=2
pkgdesc='An SVG scrubber'
arch=('any')
url='https://github.com/codedread/scour'
license=('Apache')
makedepends=('python-setuptools' 'python-six' 'python2-setuptools' 'python2-six')
source=("scour-${pkgver}.tar.gz::https://github.com/codedread/scour/archive/v${pkgver}.tar.gz")
sha256sums=('1b6820430c671c71406bf79afced676699d03bd3fcc65f01a651da5dcbcf3d3b')

prepare() {
  cp -r {,python-}scour-${pkgver}
  mv {,python2-}scour-${pkgver}
}

build() {
  for py in python{,2}; do
    pushd ${py}-scour-${pkgver}
    ${py} setup.py build
    popd
  done
}

package_scour() {
  depends=('python-setuptools' 'python-six')

  cd python-scour-${pkgver}

  python setup.py install --root="${pkgdir}" --optimize='1' --skip-build
}

package_python2-scour() {
  depends=('python2-six')

  cd python2-scour-${pkgver}

  python2 setup.py install --root="${pkgdir}" --optimize='1' --skip-build
  rm -rf "${pkgdir}"/usr/bin
}

# vim: ts=2 sw=2 et:
