# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arvedui <arvedui@posteo.de>

pkgname=python-retrying
pkgver=1.3.3
pkgrel=16
pkgdesc="A general-purpose retrying library"
arch=('any')
url="https://github.com/rholder/retrying"
license=('Apache')
depends=('python-six')
makedepends=('python-setuptools')
source=("https://github.com/rholder/retrying/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('e053e62cb71b31573b89b8fffa706c3410794b5cced829582c373a09af78158f7a16948e6ce52078589cf7d8f5bba726c889180588a2628be343f242bc40c035')

build() {
  cd retrying-$pkgver
  python setup.py build
}

check() {
  cd retrying-$pkgver
  python setup.py test
}

package() {
  cd retrying-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
