# Maintainer: Florian Pritz <bluewind@xinu.at>

pkgname=perl-net-dns-resolver-mock
pkgver=1.20200215
pkgrel=4
pkgdesc='Mock a DNS Resolver object for testing'
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=(perl-net-dns)
makedepends=('perl>=5.6')
checkdepends=(perl-test-exception)
url=https://metacpan.org/release/Net-DNS-Resolver-Mock
source=("https://cpan.metacpan.org/authors/id/M/MB/MBRADSHAW/Net-DNS-Resolver-Mock-$pkgver.tar.gz")
md5sums=(95319f092cdfe55fb5a68067d4b29604)
sha512sums=(92ac244dc2a7b939b1e8e8c71eb9ce65fdf1494dd08c62e6fc3720ffa82acc7c354bd7dcb8c77d9e52ee132773864b4b3ba869303af7363001ff5e9125c06802)
_ddir="Net-DNS-Resolver-Mock-$pkgver"

build()
(
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  /usr/bin/perl Makefile.PL
  make
)

check()
(
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
)

package()
(
  cd "$srcdir/$_ddir"
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
)

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
