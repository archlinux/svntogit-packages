# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=alsa-ucm-conf
pkgver=1.2.5
pkgrel=1
pkgdesc="ALSA Use Case Manager configuration (and topologies)"
url="https://alsa-project.org/"
arch=('any')
license=('BSD')
source=("https://www.alsa-project.org/files/pub/lib/${pkgname}-${pkgver}.tar.bz2"{,.sig})
sha512sums=('f16e8f04018c65de5e24f2a3eb0b3edff26cbce78cc1820c42c46907c903913c571797b59e803ae36b8ec3136487d0a5aacd034e2a8df3e15ed998b7a70152b7'
            'SKIP')
b2sums=('8ecd5adee6c9f429ab2791dfd57b0f9b1d58272b04d2c6cd3ad9aba36b53b380cfe626cae1e5c40f703eb7f891fb53b0bf70a78f9e5d2d8ae6b2dfb30fd6f659'
        'SKIP')
validpgpkeys=('F04DF50737AC1A884C4B3D718380596DA6E59C91') # ALSA Release Team (Package Signing Key v1) <release@alsa-project.org>

prepare() {
  # upstream creates tarball contents that don't match the version
  # https://github.com/alsa-project/alsa-ucm-conf/issues/95
  mv -v "${pkgname}-"*/ "${pkgname}-${pkgver}"
}

package() {
  cd "${pkgname}-${pkgver}"
  find ucm2 -type f -iname "*.conf" -exec install -vDm 644 {} "${pkgdir}/usr/share/alsa/"{} \;
  find ucm2 -type l -iname "*.conf" -exec cp -dv {} "${pkgdir}/usr/share/alsa/"{} \;
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -vDm 644 ucm2/README.md -t "$pkgdir/usr/share/doc/$pkgname/ucm2"
}
