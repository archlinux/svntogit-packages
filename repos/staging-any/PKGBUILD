# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-mako
pkgname=('python-mako' 'python2-mako')
pkgver=1.0.6
pkgrel=2
pkgdesc="Hyperfast and lightweight templating for the Python2 platform"
arch=('any')
url="http://www.makotemplates.org/"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'python-markupsafe' 'python2-markupsafe'
             'python-beaker' 'python2-beaker')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-mock' 'python2-mock'
              'python-babel' 'python2-babel' 'python-pygments' 'python2-pygments'
              'python-dogpile.cache' 'python2-dogpile.cache')
source=("https://pypi.io/packages/source/M/Mako/Mako-$pkgver.tar.gz"{,.asc})
sha1sums=('8cbc52319268525208c88dd3ef62c929069e4b24'
          'SKIP')
validpgpkeys=('83AF7ACE251C13E6BB7DEFBD330239C1C4DAFEE1')  # Michael Bayer

prepare() {
  cp -a Mako-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/Mako-$pkgver
  python setup.py build

  cd "$srcdir"/Mako-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/Mako-$pkgver
  python setup.py ptr

  cd "$srcdir"/Mako-$pkgver-py2
  python2 setup.py ptr
}

package_python-mako() {
  depends=('python-markupsafe' 'python-beaker' 'python-setuptools')

  cd Mako-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D LICENSE "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}

package_python2-mako() {
  depends=('python2-markupsafe' 'python2-beaker' 'python2-setuptools')

  cd Mako-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D LICENSE "$pkgdir"/usr/share/licenses/$pkgname/COPYING

  mv "$pkgdir"/usr/bin/mako-render{,2}
}
