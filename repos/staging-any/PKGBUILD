# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-resolvelib
pkgname=(python-resolvelib python2-resolvelib)
pkgver=0.5.1
pkgrel=3
pkgdesc="Resolve abstract dependencies into concrete ones"
url="https://github.com/sarugaku/resolvelib"
license=('ISC')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-commentjson' 'python-pytest-runner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/sarugaku/resolvelib/archive/$pkgver.tar.gz")
sha512sums=('5fee837933ab0e8279aa986ddaa78c1d7c84d89dabaabceef201cc12c7a30f0bdce73eeb9c3863f7057cda6b813e7405cdb3b18f136d35e9a624b71ad5092e62')

build() {
  cd resolvelib-$pkgver
  python setup.py build
  python2 setup.py build
}

check() {
  cd resolvelib-$pkgver
  python setup.py pytest
}

package_python-resolvelib() {
  depends=('python')

  cd resolvelib-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

package_python2-resolvelib() {
  depends=('python2')

  cd resolvelib-$pkgver
  python2 setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
