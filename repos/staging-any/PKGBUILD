# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-file-mimeinfo
pkgver=0.28
pkgrel=2
pkgdesc='Determine file type, includes mimeopen and mimetype'
arch=('any')
url="https://metacpan.org/release/File-MimeInfo"
license=('GPL' 'PerlArtistic')
depends=('perl-file-basedir' 'perl-file-desktopentry' 'shared-mime-info')
checkdepends=('perl-test-pod' 'perl-test-pod-coverage')
options=('!emptydirs')
source=(http://cpan.metacpan.org/authors/id/M/MI/MICHIELB/File-MimeInfo-${pkgver}.tar.gz)
sha1sums=('2c7f92541b4a7634ed7f719038c228cf49d4d9ec')

build() {
  cd File-MimeInfo-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd File-MimeInfo-${pkgver}
  make test
}

package() {
  cd File-MimeInfo-${pkgver}
  make DESTDIR="${pkgdir}" install
}
