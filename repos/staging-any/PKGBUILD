# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=oxygen-icons
pkgname=('oxygen-icons'
         'oxygen-icons-svg')
pkgver=4.7.3
pkgrel=1
pkgdesc="The Oxygen Icon Theme"
arch=('any')
url='http://www.oxygen-icons.org/'
license=('LGPL')
makedepends=('pkgconfig' 'cmake' 'automoc4')
source=("http://download.kde.org/stable/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2")
sha1sums=('eb26498ba287098127fc259816335f21cffe3784')

build() {
	cd $srcdir
	mkdir build
	cd build
	cmake ../${pkgname}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package_oxygen-icons() {
	cd $srcdir/build
	make DESTDIR=$pkgdir install
}

package_oxygen-icons-svg() {
	pkgdesc="The Oxygen Icon Theme (Scalable Vector Graphics)"
	cd $srcdir/${pkgbase}-${pkgver}
	find scalable -type f ! -name '*.sh' -exec \
		install -D -m644 "{}" "${pkgdir}/usr/share/icons/oxygen/{}" \;
}
