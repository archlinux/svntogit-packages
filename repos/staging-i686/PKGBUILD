# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgbase=phonon
pkgname=('phonon-qt4' 'phonon-qt5')
pkgver=4.8.3
pkgrel=3
arch=('i686' 'x86_64')
url='http://phonon.kde.org/'
license=('LGPL')
makedepends=('cmake' 'automoc4' 'libpulse' 'libqzeitgeist' 'qt5-base' 'qt5-tools' 'qt5-quick1')
options=('!emptydirs')
source=("http://download.kde.org/stable/${pkgbase}/${pkgver}/src/${pkgbase}-${pkgver}.tar.xz" 'qt-5.4.2.patch')
md5sums=('88bb9867261803eed61ff53a7c026338'
         '0235218127e47f646dd037661970aa97')

prepare() {
  mkdir build
  mkdir build-qt5

# Fix build with Qt 5.4.2
  cd $pkgbase-$pkgver
  patch -p1 -i "$srcdir"/qt-5.4.2.patch
}

build() {
  cd build
  cmake ../${pkgbase}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DPHONON_INSTALL_QT_EXTENSIONS_INTO_SYSTEM_QT=ON \
    -DQT_QMAKE_EXECUTABLE=/usr/bin/qmake-qt4 \
    -D__KDE_HAVE_GCC_VISIBILITY=NO \
    -DCMAKE_INSTALL_LIBDIR=lib
  make

  cd ../build-qt5
  cmake ../${pkgbase}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DPHONON_INSTALL_QT_EXTENSIONS_INTO_SYSTEM_QT=ON \
    -DPHONON_BUILD_PHONON4QT5=ON \
    -D__KDE_HAVE_GCC_VISIBILITY=NO \
    -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package_phonon-qt4(){
  pkgdesc="The multimedia framework for KDE4"
  depends=('phonon-qt4-backend' 'libpulse' 'libqzeitgeist')
  optdepends=('pulseaudio: PulseAudio support')
  replaces=('phonon')
  provides=('phonon')
  conflicts=('phonon')

  cd build
  make DESTDIR="${pkgdir}" install

  # Install headers into the Qt4 dir
  install -d "${pkgdir}"/usr/include/qt4
  mv "${pkgdir}"/usr/include/{phonon,KDE} "${pkgdir}"/usr/include/qt4/

  sed -i 's#includedir=/usr/include#includedir=/usr/include/qt4#' \
    "${pkgdir}/usr/lib/pkgconfig/phonon.pc"
}

package_phonon-qt5(){
  pkgdesc="The multimedia framework for KF5"
  depends=('phonon-qt5-backend' 'libpulse' 'qt5-base')
  optdepends=('pulseaudio: PulseAudio support')

  cd build-qt5
  make DESTDIR="${pkgdir}" install
}
