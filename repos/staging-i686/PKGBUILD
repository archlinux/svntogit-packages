# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=pango-perl
pkgver=1.225
pkgrel=2
pkgdesc="Perl bindings for Pango"
arch=(i686 x86_64)
license=('LGPL')
url="http://gtk2-perl.sourceforge.net/"
makedepends=('perl-extutils-pkgconfig' 'perl-extutils-depends')
depends=('pango' 'glib-perl' 'cairo-perl')
options=('!emptydirs')
source=(http://downloads.sourceforge.net/sourceforge/gtk2-perl/Pango-${pkgver}.tar.gz)
md5sums=('944ed0b4a89823c66362d5e2ecbe1054')

build() {
  cd Pango-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Pango-$pkgver
  make test
}

package() {
  cd Pango-$pkgver
  make install DESTDIR="${pkgdir}"
}
