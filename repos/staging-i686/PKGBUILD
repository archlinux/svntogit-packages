# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=texinfo
pkgver=5.2
pkgrel=4
pkgdesc='GNU documentation system for on-line information and printed output'
arch=('i686' 'x86_64')
url='http://www.gnu.org/software/texinfo/'
license=('GPL3')
groups=('base' 'base-devel')
depends=('ncurses' 'findutils' 'gzip' 'perl' 'sh')
install=texinfo.install
source=(ftp://ftp.gnu.org/pub/gnu/$pkgname/$pkgname-$pkgver.tar.xz{,.sig}
	texinfo-5.2-C-n-fix.patch)
md5sums=('cb489df8a7ee9d10a236197aefdb32c5'
         'SKIP'
         '89724bac1ecbd1a57b1ea81e87f96f44')
validpgpkeys=('17D3311B14BC0F248267BF020716748A30D155AD') # Karl Berry

prepare() {
  cd $pkgname-$pkgver
  patch -p0 -i ../texinfo-5.2-C-n-fix.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

check() {
  make -C $pkgname-$pkgver check
}

package() {
  make -C $pkgname-$pkgver DESTDIR="$pkgdir" install
}
