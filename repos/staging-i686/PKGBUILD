# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Eduardo Romero <eduardo@archlinux.org>
# Contributor: Jason Chu <jason@archlinux.org>

pkgname=gphoto2
pkgver=2.5.6
pkgrel=1
pkgdesc="A digital camera download and access program."
arch=(i686 x86_64)
url="http://www.gphoto.org"
license=('GPL' 'LGPL')
depends=('libgphoto2' 'popt' 'readline')
source=(http://downloads.sourceforge.net/gphoto/${pkgname}-${pkgver}.tar.bz2{,.asc})
sha1sums=('5094fd8b0f3b473a5fc3e869166c2f552132bbb2'
          'SKIP')
validpgpkeys=('D33BC5C3C0CC59B63989D77BEA7BF3970175623E')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="${pkgdir}" install
}
