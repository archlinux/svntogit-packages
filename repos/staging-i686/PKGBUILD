# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: kevin <kevin@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>

pkgname=graphviz
pkgver=2.36.0
pkgrel=3
pkgdesc='Graph visualization software'
url='http://www.graphviz.org/'
license=('CPL')
arch=('i686' 'x86_64')
depends=('libltdl' 'gd' 'librsvg' 'libxaw' 'ghostscript' 'pango' 'gts')
makedepends=('swig' 'mono' 'guile' 'lua51' 'ocaml' 'perl' 'php' 'python2' 'r' 'tk' 'qt4' 'gtk2')
optdepends=('mono: sharp bindings'
            'guile: guile bindings'
            'lua51: lua bindings'
            'ocaml: ocaml bindings'
            'perl: perl bindings'
            'php: php bindings'
            'python2: python bindings'
            'r: r bindings'
            'tcl: tcl bindings'
            'qt4: gvedit'
            'gtk2: gtk output plugin')
source=("${url}/pub/${pkgname}/stable/SOURCES/${pkgname}-${pkgver}.tar.gz")
sha1sums=('a41e9f1cbcc9a24651e14dd15a4cda3d912d7d19')

install=install

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	export LIBPOSTFIX=/
	export PYTHON=python2
	export LUA=lua5.1

	./configure --prefix=/usr
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	# Workaround symlink upgrade bug (fixed in pacman trunk)
	# Also deduplicates the tcl libraries (upstream bug?)
	# Might have been this commit: https://github.com/ellson/graphviz/commit/f11aead
	rm -r "${pkgdir}/usr/lib/tcl8.6/graphviz"
	ln -s ../graphviz/tcl "$pkgdir/usr/lib/tcl8.6/graphviz"
}
