# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Francois Charette <francois.archlinux.org> 

pkgbase=redland
epoch=1
pkgname=('redland' 'redland-storage-mysql' 'redland-storage-postgresql' 'redland-storage-virtuoso' 'redland-storage-sqlite')
pkgver=1.0.15
pkgrel=3
url="http://librdf.org/"
license=("GPL")
arch=('i686' 'x86_64')
makedepends=('rasqal>=0.9.28' 'raptor>=2.0.6' 'db>=5.3' 'postgresql-libs' 'libmysqlclient' 'unixodbc' 'sqlite3')
options=('!libtool')
source=(http://download.librdf.org/source/$pkgname-$pkgver.tar.gz{,.asc}
        rpath.diff)
sha1sums=('e93d6fafaeebdf9f15a1044be6f4a88270f007af'
          '3aeef38a7247ae33db6a0a19511ef4132240164b'
          'f5db05c925b6bcd6402d5abd75c648b137f069c7')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
	--enable-release \
	--disable-static \
	--with-raptor=system \
	--with-rasqal=system \
	--with-sqlite=3

  # nuke rpath
  patch -Np0 -i "${srcdir}/rpath.diff"

  make
}

package_redland() {
 pkgdesc="Library that provides a high-level interface to RDF data"
 depends=('rasqal>=0.9.26' 'raptor>=2.0.6' 'libltdl')
 
  cd "${srcdir}/${pkgbase}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}/usr/lib/redland"
}

package_redland-storage-mysql() {
 pkgdesc="MySQL storage support for Redland"
 depends=('redland' 'libmysqlclient')

  cd "${srcdir}/${pkgbase}-${pkgver}"
  install -dm755 "${pkgdir}/usr/lib/redland"
  install -m755 src/.libs/librdf_storage_mysql.so "${pkgdir}/usr/lib/redland/librdf_storage_mysql.so"
}

package_redland-storage-postgresql() {
 pkgdesc="PostgreSQL storage support for Redland"
 depends=('redland' 'postgresql-libs')

  cd "${srcdir}/${pkgbase}-${pkgver}"
  install -dm755 "${pkgdir}/usr/lib/redland"
  install -m755 src/.libs/librdf_storage_postgresql.so "${pkgdir}/usr/lib/redland/"
}

package_redland-storage-virtuoso() {
 pkgdesc="Virtuoso storage support for Redland"
 depends=('redland' 'unixodbc' 'db')

  cd "${srcdir}/${pkgbase}-${pkgver}"
  install -dm755 "${pkgdir}/usr/lib/redland"
  install -m755 src/.libs/librdf_storage_virtuoso.so "${pkgdir}/usr/lib/redland/"
}

package_redland-storage-sqlite() {
 pkgdesc="SQLite storage support for Redland"
 depends=('redland' 'sqlite3' 'db')
 
  cd "${srcdir}/${pkgbase}-${pkgver}"
  install -dm755 "${pkgdir}/usr/lib/redland"
  install -m755 src/.libs/librdf_storage_sqlite.so "${pkgdir}/usr/lib/redland/"
}
