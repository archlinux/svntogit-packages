# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gdb
pkgver=7.2
pkgrel=2
pkgdesc="The GNU Debugger"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/gdb/"
license=('GPL3')
depends=('ncurses' 'expat' 'python2')
makedepends=('texinfo')
backup=('etc/gdb/gdbinit')
options=('!libtool')
install=gdb.install
source=(http://ftp.gnu.org/gnu/gdb/${pkgname}-${pkgver}.tar.bz2)
md5sums=('64260e6c56979ee750a01055f16091a5')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  
  ./configure --prefix=/usr --disable-nls \
    --with-system-readline \
    --with-system-gdbinit=/etc/gdb/gdbinit
  make
  
  # requires dejagnu
  #make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  # install "custom" system gdbinit
  install -dm755 $pkgdir/etc/gdb
  touch $pkgdir/etc/gdb/gdbinit

  # resolve conflicts with binutils
  rm ${pkgdir}/usr/include/{ansidecl,bfd,bfdlink,dis-asm,symcat}.h
  rm ${pkgdir}/usr/lib/{libbfd,libiberty,libopcodes}.a
  rm ${pkgdir}/usr/share/info/{bfd,configure,standards}.info
}
