# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Suzy Williams <suzanne.williams3@verizon.net>

pkgname=xfce4-mailwatch-plugin
pkgver=1.1.0
pkgrel=4
pkgdesc="A mailbox watch/check plugin for the Xfce4 panel"
arch=(i686 x86_64)
license=('GPL2')
url="http://spurint.org/projects/xfce4-mailwatch-plugin/"
groups=('xfce4-goodies')
depends=('xfce4-panel' 'libxfcegui4' 'gnutls' 'libgcrypt' 'hicolor-icon-theme')
makedepends=('intltool' 'xfce4-dev-tools')
options=('!libtool')
install=${pkgname}.install
source=(http://spuriousinterrupt.org/files/mailwatch/${pkgname}-${pkgver}.tar.bz2
	xfce4-mailwatch-plugin-1.1.0-underlink.patch)
md5sums=('f84dce86be1d7f25f169f262aaacee4e'
         '47fe8588e3bb642e1b507a854927e6db')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  # fix for panel4.8 from Fedora
  patch -Np1 -i ${srcdir}/xfce4-mailwatch-plugin-1.1.0-underlink.patch
  xdt-autogen
  ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--libexecdir=/usr/lib \
	--localstatedir=/var \
	--disable-static \
	--disable-debug
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
