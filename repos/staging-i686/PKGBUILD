# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=grilo-plugins
pkgver=0.2.14
pkgrel=5
pkgdesc="Plugins for Grilo"
url="http://www.gnome.org"
arch=(i686 x86_64)
license=(LGPL)
depends=(grilo lua libmediaart libgdata libtracker-sparql gom libdmapsharing)
makedepends=(intltool itstool yelp-tools)
optdepends=('dleyna-server: DLNA plugin'
            'tracker: Tracker plugin')
options=('!emptydirs')
groups=('gnome')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver:0:3}/${pkgname}-${pkgver}.tar.xz
        local-metadata-crash.patch
        0002-local-metadata-Fixes-to-video_sanitise_string-non-al.patch)
sha256sums=('d0f18116675058c7e8c178b1fe26d6d73998a0452f85a73b1696df740c83d3ab'
            '221f770b064309635b52095d21d325707d45de03772da5cc579252b7795ced1b'
            'f95343c6433d438c6d67dfe0cf81804a9a19db1744606279159c008fce289aa7')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../local-metadata-crash.patch
  patch -Np1 -i ../0002-local-metadata-Fixes-to-video_sanitise_string-non-al.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static
  make
}

check() {
  cd $pkgname-$pkgver
  make -k check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
