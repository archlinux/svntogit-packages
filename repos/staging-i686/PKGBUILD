# $Id$
# Contributor: Eric Belanger <eric@archlinux.org>
# Maintainer:

pkgname=ispell
pkgver=3.3.02
pkgrel=7
pkgdesc="An interactive spell-checking program for Unix"
arch=('i686' 'x86_64')
url="http://ficus-www.cs.ucla.edu/geoff/ispell.html"
license=('BSD')
depends=('ncurses')
options=('!makeflags')
source=(http://fmg-www.cs.ucla.edu/geoff/tars/${pkgname}-${pkgver}.tar.gz 
	license.txt getline.patch)
md5sums=('12087d7555fc2b746425cd167af480fe' 'bf51b6181b9914dedc266ba970bb7319'
	 '1dca21c5d56405fe592175c3dde2fda2')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}

	patch -Np0 -i ${srcdir}/getline.patch

	sed -i -e 's/#undef USG/#define USG/' \
	       -e 's|/usr/local|/usr|' \
               -e 's|/lib|/lib/ispell|' local.h.linux
	cp local.h.linux local.h
	make TMPDIR=/tmp all
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}

        # creating directory structure
        install -dm755 ${pkgdir}/usr/{bin,lib/ispell,share/man/man{1,5}}

	# Installing binary tools
	for b in buildhash findaffix icombine ijoin ispell iwhich munchlist tryaffix; do
		install -m755 $b ${pkgdir}/usr/bin/
	done

	# Installing man pages
	install -m644 ispell.1 ${pkgdir}/usr/share/man/man1/ispell.1
	install -m644 ispell.5 ${pkgdir}/usr/share/man/man5/ispell.5

	# Installing dictionnaries
	install -m644 languages/american/americanmed.hash \
	    ${pkgdir}/usr/lib/ispell/americanmed.hash
	install -m644 languages/english/english.aff \
	    ${pkgdir}/usr/lib/ispell/english.aff
	ln -s americanmed.hash ${pkgdir}/usr/lib/ispell/american.hash
	ln -s americanmed.hash ${pkgdir}/usr/lib/ispell/english.hash

	# Installing license
	install -Dm644 ${srcdir}/license.txt \
	    ${pkgdir}/usr/share/licenses/${pkgname}/license.txt
}
