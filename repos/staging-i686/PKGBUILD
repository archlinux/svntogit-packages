# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=telepathy-kde-call-ui
_pkgname=ktp-call-ui
pkgver=0.9.0
pkgrel=2
pkgdesc="Voice/Video Call UI for Telepathy"
arch=('i686' 'x86_64')
url="http://community.kde.org/Real-Time_Communication_and_Collaboration"
license=('GPL')
depends=('telepathy-kde-common-internals' 'kdebase-runtime')
makedepends=('cmake' 'automoc4' 'boost')
groups=('kde-telepathy')
source=("http://download.kde.org/stable/kde-telepathy/${pkgver}/src/${_pkgname}-${pkgver}.tar.bz2")
sha1sums=('8f2abe7256c3bbbb308731ae0861665b82a96303')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../${_pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DQT_QMAKE_EXECUTABLE=qmake-qt4
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
