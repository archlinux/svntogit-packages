# $Id$
# Maintainer: Ronald van Haren <ronald@archlinux.org>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Jason Taylor <jftaylor21@gmail.com>
# Contributor: Darwin Bautista <djclue917@gmail.com>

pkgname=qhull
pkgver=2012.1
pkgrel=2
pkgdesc="A general dimension code for computing convex hulls and related structures"
arch=('i686' 'x86_64')
url="http://www.qhull.org/"
license=('custom')
depends=('gcc-libs')
makedepends=('cmake')
options=('!libtool')
source=("http://www.qhull.org/download/$pkgname-$pkgver-src.tgz")
md5sums=('d0f978c0d8dfb2e919caefa56ea2953c')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  if [ "${CARCH}" = "x86_64" ]; then
    EXTRAOPTS="-DCMAKE_CXX_FLAGS=-fPIC -DCMAKE_C_FLAGS=-fPIC"
  fi

  mkdir srcbuild
  cd srcbuild
  cmake -DCMAKE_INSTALL_PREFIX=/usr $EXTRAOPTS ..
  make
}

package() { 
  cd "$srcdir/$pkgname-$pkgver/srcbuild"

  make DESTDIR="$pkgdir" install

  # Install license
  install -Dm644 "$srcdir/$pkgname-$pkgver/COPYING.txt" "$pkgdir/usr/share/licenses/$pkgname/license.txt"
}
