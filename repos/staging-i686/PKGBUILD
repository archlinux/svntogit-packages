# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>

pkgname=libpagemaker
pkgver=0.0.2
pkgrel=2
pkgdesc="a library that parses the file format of Aldus/Adobe PageMaker documents"
arch=('i686' 'x86_64')
url="https://wiki.documentfoundation.org/DLP/Libraries/libpagemaker"
license=('MPL')
source=(http://dev-www.libreoffice.org/src/${pkgname}/${pkgname}-${pkgver}.tar.xz)
depends=('librevenge')
makedepends=('boost')
md5sums=('1e6fec3e30f4f47c4c2619ee232dec04')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

