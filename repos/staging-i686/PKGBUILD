# $Id$
# Maintainer: Tom Gundersen <teg@jklm.no>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Geoffroy Carrier <geoffroy@archlinux.org>

pkgname=bluez
pkgver=4.99
pkgrel=5
pkgdesc="Libraries and tools for the Bluetooth protocol stack"
url="http://www.bluez.org/"
arch=('i686' 'x86_64')
license=('GPL2')
depends=('dbus-core' 'python2' 'udev')
makedepends=('gstreamer0.10-base' 'libusb-compat' 'libsndfile')
optdepends=("gstreamer0.10-base: bluetooth GStreamer support"
            "alsa-lib: Audio bluetooth devices support"
            "dbus-python: to run bluez-simple-agent"
            "pygobject: to run bluez-simple-agent"
            "libusb-compat: USB adapters support"
            "cups: CUPS backend")
conflicts=('bluez-libs' 'bluez-utils')
provides=('bluez-libs' 'bluez-utils')
replaces=('bluez-libs' 'bluez-utils')
options=('!libtool')
backup=(etc/bluetooth/{main,rfcomm,audio,network,input,serial}.conf
        'etc/conf.d/bluetooth' 'etc/dbus-1/system.d/bluetooth.conf')
source=("http://www.kernel.org/pub/linux/bluetooth/${pkgname}-${pkgver}.tar.bz2"
	'0001-udev-remove-deprecated-function.patch'
        'bluetooth.conf.d'
	'rc.bluetooth')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/lib \
    --enable-gstreamer \
    --enable-alsa \
    --enable-usb \
    --enable-tools \
    --enable-bccmd \
    --enable-dfutool \
    --enable-hid2hci \
    --enable-hidd \
    --enable-pand \
    --enable-dund \
    --enable-cups \
    --enable-wiimote \
    --disable-test

  patch -p1 -i ../0001-udev-remove-deprecated-function.patch

  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  install -Dm755 ${srcdir}/rc.bluetooth ${pkgdir}/etc/rc.d/bluetooth
  
  install -d ${pkgdir}/etc/bluetooth
  install -m644 network/network.conf \
                input/input.conf \
                audio/audio.conf \
                serial/serial.conf \
    ${pkgdir}/etc/bluetooth/
  
  install -Dm644 ${srcdir}/bluetooth.conf.d \
    ${pkgdir}/etc/conf.d/bluetooth

  # FS#27630
  install -Dm755 test/simple-agent "${pkgdir}"/usr/bin/bluez-simple-agent
  install -Dm755 test/test-device "${pkgdir}"/usr/bin/bluez-test-device
  install -Dm755 test/test-input "${pkgdir}"/usr/bin/bluez-test-input
  sed -i 's#/usr/bin/python#/usr/bin/python2#' \
    "${pkgdir}"/usr/bin/bluez-simple-agent \
    "${pkgdir}"/usr/bin/bluez-test-device \
    "${pkgdir}"/usr/bin/bluez-test-input

  # fixed upstream in git
  rm "${pkgdir}"/usr/lib/udev/rules.d/97-bluetooth.rules
}
md5sums=('2387053eb5a7b02f37df4871df022a02'
         '3175155fbaf73f01377f6c35b5c5e468'
         '7412982b440f29fa7f76a41a87fef985'
         '8f9498707f809506928b2e480d3b6789')
