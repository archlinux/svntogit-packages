# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Hussam Al-Tayeb <ht990332@gmail.com>

pkgname=xmlsec
pkgver=1.2.18
pkgrel=2
pkgdesc="XML Security Library is a C library based on LibXML2"
license=('custom')
arch=(i686 x86_64)
url="http://www.aleksey.com/xmlsec/index.html"
depends=('libxslt>=1.1.26' 'openssl>=1.0.0' 'gnutls' 'nss>=3.12.10' 'libtool')
makedepends=('pkgconfig')
options=('!libtool')
source=(http://www.aleksey.com/xmlsec/download/${pkgname}1-${pkgver}.tar.gz)
md5sums=('8694b4609aab647186607f79e1da7f1a')

build() {
  cd ${srcdir}/${pkgname}1-${pkgver}
  ./configure --prefix=/usr --disable-static
  make
}

check() {
  cd "$srcdir/${pkgname}1-$pkgver"
  make -k check
}

package() {
  cd ${srcdir}/${pkgname}1-${pkgver}
  make DESTDIR=${pkgdir} install
  install -m755 -d ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/
}
