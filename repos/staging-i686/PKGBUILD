# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Kritoke <typeolinux@yahoo.com>

pkgname=gnumeric
pkgver=1.10.9
pkgrel=2
pkgdesc="A GNOME Spreadsheet Program"
arch=('i686' 'x86_64')
url="http://www.gnome.org/projects/gnumeric/"
license=('GPL')
depends=('goffice>=0.8.9' 'gconf>=2.28.1' 'desktop-file-utils' 'libglade>=2.6.4')
makedepends=('intltool' 'python2' 'gnome-doc-utils>=0.20.0' 'pygobject' 'psiconv')
optdepends=('pygobject: for python plugin support'
            'psiconv: for Psion 5 file support'
            'yelp: for viewing the help manual')
install=gnumeric.install
options=('libtool')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('64bc3ee96824e58acca0c4f0960e6090a15f9a8a5c5d9912f8ccaf380702e0d9')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-schemas-install --enable-ssindex
  make
  make DESTDIR="${pkgdir}" install

  install -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gnumeric "${pkgdir}"/etc/gconf/schemas/*.schemas
  rm -rf "${pkgdir}/etc/gconf"
}
