# $Id$
# Maintainer: Daniel Isenmann <daniel@archlinux.org>
# Contributor: orelien <aurelien.foret@wanadoo.fr>

pkgname=rp-pppoe
pkgver=3.11
pkgrel=4
pkgdesc="Roaring Penguin's Point-to-Point Protocol over Ethernet client"
arch=('i686' 'x86_64')
url="http://www.roaringpenguin.com/pppoe/"
license=('GPL')
depends=('ppp>=2.4.5' 'sh' 'net-tools')
backup=(etc/ppp/pppoe.conf etc/ppp/firewall-standalone etc/ppp/firewall-masq 
        etc/ppp/pppoe-server-options)
options=('!makeflags')
install=rp-pppoe.install
source=(http://www.roaringpenguin.com/files/download/rp-pppoe-$pkgver.tar.gz{,.sig} 
       adsl.service 
       kmode.patch)

prepare() {
  cd "$srcdir/$pkgname-$pkgver/src"
  patch -Np2 -i ../../kmode.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver/src"
  ./configure --prefix=/usr --sbindir=/usr/bin --enable-plugin
  make PLUGIN_DIR="/usr/lib/rp-pppoe" all rp-pppoe.so
}

package() {
  cd "$srcdir/$pkgname-$pkgver/src"
  make PLUGIN_DIR="/usr/lib/rp-pppoe" DESTDIR="$pkgdir" install

  #install -Dm755 "$srcdir/adsl" "$pkgdir/etc/rc.d/adsl"
  install -Dm644 "$srcdir/adsl.service" "$pkgdir/usr/lib/systemd/system/adsl.service"
}
md5sums=('13b5900c56bd602df6cc526e5e520722'
         'SKIP'
         '45dc8fc3d0341a3930182a480859ad76'
         'd38a903a247d689ff0323d7b6caa1c6b')
