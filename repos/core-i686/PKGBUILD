# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Jochem Kossen <j.kossen@home.nl>

pkgname=fakeroot
pkgver=1.14.4
pkgrel=2
pkgdesc="Gives a fake root environment, useful for building packages as a non-privileged user"
arch=('i686' 'x86_64')
license=('GPL')
url="http://packages.debian.org/fakeroot"
groups=('base-devel')
install=fakeroot.install
depends=('glibc' 'filesystem' 'grep' 'sed' 'sh')
options=('!libtool')
source=(http://ftp.debian.org/debian/pool/main/f/$pkgname/${pkgname}_${pkgver}.orig.tar.bz2)
md5sums=('bea628be77838aaa7323a2f7601c2d7e')

build() {
  cd ${srcdir}/$pkgname-$pkgver
  ./configure --prefix=/usr --libdir=/usr/lib/libfakeroot \
	 --disable-static --with-ipc=sysv
	# --with-ipc=tcp is currently broken and no option on smp systems
  make || return 1
}

package() {
  cd ${srcdir}/$pkgname-$pkgver
  make DESTDIR=${pkgdir} install || return 1
  
  install -dm755 ${pkgdir}/etc/ld.so.conf.d/
  echo '/usr/lib/libfakeroot' > ${pkgdir}/etc/ld.so.conf.d/fakeroot.conf
  
  # install README for sysv/tcp usage
  install -Dm644 $srcdir/$pkgname-$pkgver/README $pkgdir/usr/share/doc/$pkgname/README
}
