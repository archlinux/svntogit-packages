# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Fran√ßois Charette <firmicus@gmx.net>

pkgname=xz
pkgver=5.0.3
pkgrel=2
pkgdesc='Library and command line tools for XZ and LZMA compressed files'
arch=('i686' 'x86_64')
url='http://tukaani.org/xz/'
license=('GPL' 'LGPL' 'custom')
depends=('sh')
options=('!libtool')
source=("http://tukaani.org/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        "http://tukaani.org/${pkgname}/${pkgname}-${pkgver}.tar.gz.sig"
        'compile-fix.patch')
md5sums=('fefe52f9ecd521de2a8ce38c21a27574'
         'd0725667a6dae0853194dc691e900e26'
         'b74e4f7793848cc5765c848ea30f971f')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}
	patch -p1 -i ${srcdir}/compile-fix.patch

	./configure --prefix=/usr \
		--disable-rpath \
		--enable-werror
	make
}

check() {
	cd ${srcdir}/${pkgname}-${pkgver}
	make check
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}
	make DESTDIR=${pkgdir} install
	install -d -m755 ${pkgdir}/usr/share/licenses/xz/
	ln -sf /usr/share/doc/xz/COPYING ${pkgdir}/usr/share/licenses/xz/
	ln -sf /usr/share/licenses/common/GPL2/license.txt ${pkgdir}/usr/share/doc/xz/COPYING.GPLv2
}
