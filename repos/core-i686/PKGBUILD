# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=less
pkgver=444
pkgrel=2
pkgdesc="A terminal based program for viewing text files"
license=('GPL3')
arch=('i686' 'x86_64')
url="http://www.greenwoodsoftware.com/less"
groups=('base')
depends=('ncurses' 'pcre')
source=(http://www.greenwoodsoftware.com/$pkgname/$pkgname-$pkgver.tar.gz{,.sig})
md5sums=('56f9f76ffe13f70155f47f6b3c87d421'
         'd5fafbd94a9cfdeaad0c82143a033fe5')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr --sysconfdir=/etc --with-regex=pcre
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make prefix="${pkgdir}"/usr install
  install -dm755 "${pkgdir}"/bin
  mv "${pkgdir}"/usr/bin/${pkgname} "${pkgdir}"/bin
}
