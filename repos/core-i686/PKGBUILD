# $Id$
# Maintainer: Dan McGee <dan@archlinux.org>

pkgname=libarchive
pkgver=3.0.3
pkgrel=3
pkgdesc="library that can create and read several streaming archive formats"
arch=('i686' 'x86_64')
url="http://libarchive.googlecode.com/"
license=('BSD')
depends=('zlib' 'bzip2' 'xz>=5.0.0' 'acl' 'openssl>=1.0.0' 'expat')
source=("http://libarchive.googlecode.com/files/libarchive-${pkgver}.tar.gz"
        'interpret-non-posix-zips.patch'
        'test-with-zip-mtime.patch')
md5sums=('ca4090f0099432a9ac5a8b6618dc3892'
         'f02b88eb10877c7a7d527ed89c662e44'
         '8366def6d7d70d424fa28a986c78c015')
sha256sums=('c5fc7620f74a54b1717e4aed38aee85dc27a988ad1db7640f28eb63a82ea62d7'
            '9d8240a360d61464dfc5a98342f520ad41b0f922261f2ace7ec1fefb8c289bdc'
            'b7a8be5c1e3220960a9f67c9779b10d2663e25d72939546b4f01a49f1ee3a61f')

# keep an upgrade path for older installations
PKGEXT='.pkg.tar.gz'

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # http://code.google.com/p/libarchive/issues/detail?id=225
  patch -Np0 <"$srcdir/interpret-non-posix-zips.patch"

  # http://code.google.com/p/libarchive/issues/detail?id=231
  patch -Np0 <"$srcdir/test-with-zip-mtime.patch"

  ./configure --prefix=/usr --without-xml2
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"

  make check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  install -D -m644 COPYING "$pkgdir"/usr/share/licenses/libarchive/COPYING
}
