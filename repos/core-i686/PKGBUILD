# $Id$
# Maintainer: SÃ©bastien Luttringer

# Contacted by mail, author use svn commit as release version.
# He only push tested release code into svn and support using revision as version

pkgname=lz4
pkgver=130
pkgrel=1
pkgdesc='Very fast lossless compression algorithm'
arch=('i686' 'x86_64')
url='https://github.com/Cyan4973/lz4'
license=('GPL2')
makedepends=('git')
checkdepends=('diffutils')
depends=('glibc')
source=("git+https://github.com/Cyan4973/lz4.git#tag=r$pkgver")
md5sums=('SKIP')

build() {
  cd $pkgname
  make PREFIX=/usr
}

check() {
  $pkgname/programs/lz4 /etc/passwd passwd.lz4
  $pkgname/programs/lz4 -d passwd.lz4 passwd
  diff -q /etc/passwd passwd
  rm passwd
}

package() {
  cd $pkgname
  make install PREFIX=/usr DESTDIR="$pkgdir"
}

# vim:set ts=2 sw=2 et:
