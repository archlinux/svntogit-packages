# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=links
pkgver=2.12
pkgrel=1
pkgdesc="A text WWW browser, similar to Lynx"
arch=('i686' 'x86_64')
url="http://links.twibright.com/"
license=('GPL')
depends=('bzip2' 'xz' 'gpm' 'libevent')
makedepends=('libtiff' 'libpng' 'libxt' 'librsvg')
optdepends=('libx11: for using xlinks'
	    'libtiff: for using xlinks'
	    'libpng: for using xlinks'
	    'librsvg: for using xlinks')
source=(http://links.twibright.com/download/${pkgname}-${pkgver}.tar.bz2 links.desktop)
sha1sums=('952dfecded9496ff1a73184ce538d08dbbb5152b'
          'f600e27c2a71184444f7dd07a10230aa44463a02')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" configure
}

build() {
  cd ${pkgname}-${pkgver}
  (cd intl; ./gen-intl; ./synclang)
  ./configure --prefix=/usr --mandir=/usr/share/man --enable-javascript \
    --enable-graphics --with-x --with-fb
  make
  mv links xlinks

  ./configure --prefix=/usr --mandir=/usr/share/man --enable-javascript \
    --disable-graphics --without-x  --without-fb
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -m755 xlinks "${pkgdir}/usr/bin/xlinks"
  ln -s links.1.gz "${pkgdir}/usr/share/man/man1/xlinks.1.gz"

  install -D -m644 "${srcdir}/links.desktop" "${pkgdir}/usr/share/applications/links.desktop"
  install -d "${pkgdir}/usr/share/pixmaps"
  install -m644 links_16x16_1.xpm links_16x16_2.xpm links_32x32.xpm "${pkgdir}/usr/share/pixmaps/"

  install -d "${pkgdir}/usr/share/doc/links/calibration"
  install -m644 doc/links_cal/* "${pkgdir}/usr/share/doc/links/calibration/"
}
