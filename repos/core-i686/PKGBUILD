# $Id$
# Maintainer: Tom Gundersen <teg@jklm.no>
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Link Dupont <link@subpop.net>

pkgbase=dbus
pkgname=('dbus' 'libdbus')
pkgver=1.10.0
pkgrel=4
pkgdesc="Freedesktop.org message bus system"
url="http://www.freedesktop.org/Software/dbus"
arch=(i686 x86_64)
license=('GPL' 'custom')
makedepends=('libx11' 'systemd' 'xmlto' 'docbook-xsl')
source=(http://dbus.freedesktop.org/releases/dbus/dbus-$pkgver.tar.gz{,.asc}
        dbus.socket dbus-setenv.service)
md5sums=('5af6297348107a906c8449817a728b3b'
         'SKIP'
         '5fce2cd240ab1c90fe716076b7d33b8e'
         'cb82de93178a65dad38a1356210b2453')
validpgpkeys=('DA98F25C0871C49A59EAFF2C4DE8FF2A63C7CC90') # Simon McVittie <simon.mcvittie@collabora.co.uk>

build() {
  cd dbus-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/usr/lib/dbus-1.0 --with-dbus-user=dbus \
      --with-system-pid-file=/run/dbus/pid \
      --with-system-socket=/run/dbus/system_bus_socket \
      --with-console-auth-dir=/run/console/ \
      --enable-inotify \
      --disable-verbose-mode --disable-static \
      --disable-tests --disable-asserts \
      --with-systemdsystemunitdir=/usr/lib/systemd/system \
      --enable-systemd --enable-user-session
  make
}

package_dbus(){
  depends=('libdbus' 'expat')
  optdepends=('libx11: dbus-launch support')
  provides=('dbus-core')
  conflicts=('dbus-core')
  replaces=('dbus-core')

  cd dbus-$pkgver

  # Disable installation of libdbus
  sed -i -e 's/^SUBDIRS = dbus/SUBDIRS =/' Makefile

  make DESTDIR="$pkgdir" install

  rm -r "${pkgdir}/var/run"
  rm -r "${pkgdir}/usr/lib/pkgconfig"

  # Fix up manager environment
  install -m644 "$srcdir"/{dbus.socket,dbus-setenv.service} \
    "$pkgdir/usr/lib/systemd/user"

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/dbus/COPYING"
}

package_libdbus(){
  pkgdesc="DBus library"
  depends=('libsystemd')

  cd dbus-$pkgver
  make DESTDIR="$pkgdir" -C dbus install
  make DESTDIR="$pkgdir" install-data-am
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/libdbus/COPYING"
}
