# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=tzdata
pkgver=2009o
pkgrel=1
_tzcode=2009k
_tzdata=2009o
pkgdesc="Sources for time zone and daylight saving time data"
arch=('i686' 'x86_64')
url="http://www.twinsun.com/tz/tz-link.htm"
license=('GPL')
groups=('base')
depends=()
makedepends=()
optdepends=('sh: required by tzselect')
options=('!emptydirs')
source=(ftp://elsie.nci.nih.gov/pub/tzcode${_tzcode}.tar.gz \
        ftp://elsie.nci.nih.gov/pub/${pkgname}${_tzdata}.tar.gz \
        Makefile.patch
        argentina.patch)
md5sums=('d181116286661375966a350e3e358cfa'
         '75c74b9c8df7c83c8e3c3068fff08a82'
         'a64ed97d1fc03c66ee8612c0d9f40507'
         '774d012b4a882c3af2ca10ffaebb0630')

build() {
  cd ${srcdir}

  tar -xf tzcode${_tzcode}.tar.gz  || return 1
  tar -xf ${pkgname}${_tzdata}.tar.gz || return 1

  patch -Np1 -i "${srcdir}/Makefile.patch" || return 1
  patch -Np1 -i "${srcdir}/argentina.patch" || return 1

  make || return 1
  make DESTDIR="${pkgdir}" install || return 1

  rm "${pkgdir}/usr/share/zoneinfo/localtime"
}
