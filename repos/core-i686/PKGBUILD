# $Id$
# Maintainer: tobias [ tobias at archlinux org ]

pkgname=vi
pkgver=050325
pkgrel=3
epoch=1
pkgdesc='The original ex/vi text editor.'
arch=('i686' 'x86_64')
url='http://ex-vi.sourceforge.net/'
license=('custom:ex')
depends=('ncurses')
optdepends=('heirloom-mailx: used by the preserve command for notification')
groups=('base')
options=(strip !libtool emptydirs)
source=(http://downloads.sourceforge.net/sourceforge/ex-vi/ex-${pkgver}.tar.bz2
        navkeys.patch
        increase-tube.patch
        preserve_dir.patch)
md5sums=('e668595254233e4d96811083a3e4e2f3'
         'aac133930047eafa4f28736cfaf976b3'
         'e596e05a00a24187b576e389fa1de45d'
         '8c6d3503e7c41a4413145ae49a7f2b80')

build() {
  cd "${srcdir}/ex-${pkgver}"

  patch -Np1 -i  ../navkeys.patch
  patch -Np0 -i ../increase-tube.patch
  # FS#20653
  patch -Np1 -i ../preserve_dir.patch

  make PREFIX=/usr LIBEXECDIR=/usr/lib/ex PRESERVEDIR=/var/lib/ex \
      'FEATURES=-DCHDIR -DFASTTAG -DUCVISUAL -DMB -DBIT8' TERMLIB=ncurses
}

package() {
  cd "${srcdir}/ex-${pkgver}"

  make PREFIX=/usr LIBEXECDIR=/usr/lib/ex PRESERVEDIR=/var/lib/ex INSTALL=/usr/bin/install \
      DESTDIR="$pkgdir" install

  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
