# $Id$
# Maintainer: Aaron Griffin <aaron@archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=logrotate
pkgver=3.7.9
pkgrel=1
pkgdesc="Rotates system logs automatically"
url='https://fedorahosted.org/logrotate/'
arch=('i686' 'x86_64')
license=('GPL')
groups=('base')
depends=('popt' 'cron' 'gzip')
backup=('etc/logrotate.conf')
source=(https://fedorahosted.org/releases/l/o/logrotate/logrotate-${pkgver}.tar.gz
        logrotate.conf
        logrotate.cron.daily)
md5sums=('eeba9dbca62a9210236f4b83195e4ea5'
         '462a5f364717461537eb2ae6394ad23e'
         '8e23d5d4cc29b1e055b24df87e355cdc')

build() {
  cd $srcdir/${pkgname}-${pkgver}
  
  sed -i 's|#define DEFAULT_MAIL_COMMAND .*|#define DEFAULT_MAIL_COMMAND "/usr/bin/mail"|'\
    config.h
  sed -i "s|CFLAGS = -Wall|CFLAGS = -Wall $CFLAGS|" Makefile
  sed -i 's|$(BASEDIR)/man|$(BASEDIR)/share/man|' Makefile
  
  make
}

package() {
  cd $srcdir/${pkgname}-${pkgver}
  make PREFIX=$pkgdir install

  install -Dm644 $srcdir/logrotate.conf $pkgdir/etc/logrotate.conf
  install -Dm744 $srcdir/logrotate.cron.daily $pkgdir/etc/cron.daily/logrotate
}
