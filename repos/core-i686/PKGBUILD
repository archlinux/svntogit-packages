# $Id$
# Maintainer: Aaron Griffin <aaron@archlinux.org>
# Maintainer: Allan McRae <allan@archlinux.org>

pkgname=bash
_basever=4.2
_patchlevel=020 #prepare for some patches
pkgver=$_basever.$_patchlevel
pkgrel=1
pkgdesc="The GNU Bourne Again shell"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnu.org/software/bash/bash.html"
groups=('base')
backup=(etc/bash.bash{rc,_logout} etc/skel/.bash{rc,_profile,_logout})
depends=('readline>=6.1' 'glibc')
provides=('sh')
install=bash.install
source=(http://ftp.gnu.org/gnu/bash/bash-$_basever.tar.gz{,.sig}
        dot.bashrc
        dot.bash_profile
        dot.bash_logout
        system.bashrc
        system.bash_logout)
if [ $_patchlevel -gt 000 ]; then
    for (( p=1; p<=$((10#${_patchlevel})); p++ )); do
        source=(${source[@]} http://ftp.gnu.org/gnu/bash/bash-$_basever-patches/bash${_basever//./}-$(printf "%03d" $p){,.sig})
    done
fi

build() {
  cd ${srcdir}/${pkgname}-$_basever
  for (( p=1; p<=$((10#${_patchlevel})); p++ )); do
    msg "applying patch bash${_basever//./}-$(printf "%03d" $p)"
    patch -Np0 -i $srcdir/bash${_basever//./}-$(printf "%03d" $p)
  done

  _bashconfig=(-DDEFAULT_PATH_VALUE=\'\"/usr/local/bin:/usr/bin:/bin:/usr/local/sbin:/usr/sbin:/sbin\"\'
               -DSTANDARD_UTILS_PATH=\'\"/usr/bin:/bin:/usr/sbin:/sbin\"\'
               -DSYS_BASHRC=\'\"/etc/bash.bashrc\"\'
               -DSYS_BASH_LOGOUT=\'\"/etc/bash.bash_logout\"\')
  export CFLAGS="${CFLAGS} ${_bashconfig[@]}"

  ./configure --prefix=/usr --with-curses --enable-readline \
    --without-bash-malloc --with-installed-readline \
    --bindir=/bin --mandir=/usr/share/man --infodir=/usr/share/info
  make
}

check() {
  cd ${srcdir}/${pkgname}-$_basever
  make check
}

package() {
  cd ${srcdir}/${pkgname}-$_basever
  make DESTDIR=${pkgdir} install

  # for now, bash is our default /bin/sh
  cd ${pkgdir}/bin
  ln -s bash sh

  install -dm755 ${pkgdir}/etc/skel/
  
  # system-wide configuration files
  install -m644 ${srcdir}/system.bashrc ${pkgdir}/etc/bash.bashrc
  install -m644 ${srcdir}/system.bash_logout ${pkgdir}/etc/bash.bash_logout
  
  # user configuration file skeletons
  install -m644 ${srcdir}/dot.bashrc ${pkgdir}/etc/skel/.bashrc
  install -m644 ${srcdir}/dot.bash_profile ${pkgdir}/etc/skel/.bash_profile
  install -m644 ${srcdir}/dot.bash_logout ${pkgdir}/etc/skel/.bash_logout
}

md5sums=('3fb927c7c33022f1c327f14a81c0d4b0'
         '8d37a3f97a48c1e56e1a4ded877ed944'
         '027d6bd8f5f6a06b75bb7698cb478089'
         '2902e0fee7a9168f3a4fd2ccd60ff047'
         '42f4400ed2314bd7519c020d0187edc5'
         'fe5d3a367f7d5f754214dc05e3d958ab'
         '472f536d7c9e8250dc4568ec4cfaf294'
         '1100bc1dda2cdc06ac44d7e5d17864a3'
         'a7184b76eb4a079f10174a0a8f574819'
         '30e7948079921d3261efcc6a40722135'
         'c4d45307f7e69fe508ce347c4cec1955'
         '9ea06decec43a198f3d7cf29acc602f8'
         '74bddae6eeb9227a04a467d42597a34d'
         'fb48f6134d7b013135929476aa0c250c'
         '3e6a18226b16c773229246abd07a1f5e'
         'e70e45de33426b38153b390be0dbbcd4'
         'e667dc9348ebc3e0e14bfdd87f4b6ff2'
         'ce4e5c484993705b27daa151eca242c2'
         '41cbd8e57589bc081a546a014ddb12f8'
         '88d1f96db29461767602e2546803bda7'
         'b8b781520f4c7493a2a1ac3010a44a44'
         '24c574bf6d6a581e300823d9c1276af6'
         '354a0899a7c4b446454c52546562b55b'
         '4c5835f2fbab36c4292bb334977e5b6d'
         'ff4547ca7b508d52101729d61f5b77b6'
         '0a51602b535ef661ee707be6c8bdb373'
         'cec7c92a4d8052ea4b29216365d16566'
         '58deacf3d57cbd75575444ff6a3b0806'
         '9ed2a08e549c3c17745f19e8413cfb31'
         '72d5059820015231483bb7415d09e9db'
         '45b04b41a4230f469d3e47ab9952c2df'
         '608336ebe215984ef126e3c29d2d3409'
         '623f63025d155a84977b62531e260ee2'
         'ed19da878e3f630834c62b9d9dcc6fce'
         'eef2dd4a0e4505dc8f8e664b40c6cd66'
         '2d07daba0b8ca8f876d2aa052ff594b4'
         '0cb823a44e6dc1cde7ac97ae5c8163c9'
         '53d246537e1fffd1aaa02ba5c056211c'
         '60f983a1dded5f0b28040ff4b3f1e9b5'
         '304fd129a58fee2d8a34f8b4704db0aa'
         '9df4bf4f46aaadc436f430187741f815'
         '6921a0b4228fe89b6537a5c29f027c89'
         '4ef0e2a2cbfbdd53346f927000b7e112'
         '1195d85447f3d048d2c9bcd075fa765c'
         'b0f3a651ce29a1feee687d93177870f1'
         'b09000bba08da6ac753124593850cdf7'
         '204bb5054d1f8bafe2b446d4e5d0d99a')
