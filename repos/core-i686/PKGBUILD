# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Committer: Judd Vinet <jvinet@zeroflux.org>

pkgname=pkg-config
pkgver=0.25
pkgrel=3
pkgdesc="A system for managing library compile/link flags"
arch=('i686' 'x86_64')
url="http://pkgconfig.freedesktop.org/wiki/"
license=('GPL')
groups=('base-devel')
depends=('glibc' 'popt' 'glib2')
provides=("pkgconfig=${pkgver}")
conflicts=('pkgconfig')
replaces=('pkgconfig')
source=(http://pkgconfig.freedesktop.org/releases/${pkgname}-${pkgver}.tar.gz
        autoconf-2.66.patch)
md5sums=('a3270bab3f4b69b7dc6dbdacbcae9745'
         '0fbfc1a5088f792bbeec7b5090e449f1')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i $srcdir/autoconf-2.66.patch
  # Use system popt and glib
  ./configure --prefix=/usr --with-installed-glib --with-installed-popt 

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR=${pkgdir} install
}
