# $Id$
# Maintainer:
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: grimi@poczta.fm

pkgname=sdparm
pkgver=1.08
pkgrel=1
pkgdesc="An utility similar to hdparm but for SCSI devices"
arch=('i686' 'x86_64')
url="http://sg.danny.cz/sg/sdparm.html"
license=('BSD')
depends=('glibc' 'bash')
source=("http://sg.danny.cz/sg/p/${pkgname}-${pkgver}.tar.xz")
md5sums=('d290e6b45d690bfa2cc05700ec119073')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -Dm644 COPYING \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
