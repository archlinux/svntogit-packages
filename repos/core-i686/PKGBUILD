# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=ncurses
pkgver=5.6
pkgrel=8
pkgdesc="A System V Release 4.0 curses emulation library"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/ncurses/ncurses.html"
license=('MIT')
groups=('base')
depends=('glibc')
source=(ftp://ftp.gnu.org/pub/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz
        ftp://invisible-island.net/ncurses/${pkgver}/${pkgname}-${pkgver}-coverity.patch.gz)
md5sums=('b6593abe1089d6aab1551c105c9300e3'
         '27607b10fef869740cbcc408d86695b1')
options=(!makeflags)

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -Np1 -i ${srcdir}/${pkgname}-${pkgver}-coverity.patch

  ./configure --prefix=/usr --mandir=/usr/share/man \
     --with-shared --with-normal --without-debug --without-ada \
     --with-install-prefix=${pkgdir} --enable-widec 
  make || return 1
  make install

  # fix library permissions
  chmod 644 ${pkgdir}/usr/lib/libncurses++w.a

  # move libraries needed for boot to /lib (we call tput in initscripts)
  mkdir -p ${pkgdir}/lib
  mv ${pkgdir}/usr/lib/libncursesw.so.5* ${pkgdir}/lib
  ln -sf ../../lib/libncursesw.so.5 ${pkgdir}/usr/lib/libncursesw.so

  # Fool packages looking to link to non-wide-character ncurses libraries
  for lib in curses ncurses form panel menu ; do \
    rm -f ${pkgdir}/usr/lib/lib${lib}.so ; \
    echo "INPUT(-l${lib}w)" >${pkgdir}/usr/lib/lib${lib}.so ; \
    ln -sf lib${lib}w.a ${pkgdir}/usr/lib/lib${lib}.a ; \
  done
  ln -sf libncurses++w.a ${pkgdir}/usr/lib/libncurses++.a

  # install tput to /bin
  mkdir ${pkgdir}/bin/
  mv ${pkgdir}/usr/bin/tput ${pkgdir}/bin/tput

  # Some packages look for -lcurses during build
  rm -f ${pkgdir}/usr/lib/libcursesw.so
  echo "INPUT(-lncursesw)" >${pkgdir}/usr/lib/libcursesw.so
  ln -sf libncurses.so ${pkgdir}/usr/lib/libcurses.so
  ln -sf libncursesw.a ${pkgdir}/usr/lib/libcursesw.a
  ln -sf libncurses.a ${pkgdir}/usr/lib/libcurses.a

  # Install libncurses.so.5 for external binary support 
  ./configure --prefix=/usr \
    --with-shared --with-normal --without-debug --without-ada \
    --with-install-prefix=${pkgdir}
  make || return 1
  
  install -Dm755 lib/libncurses.so.5.6 ${pkgdir}/usr/lib/libncurses.so.5.6
  ln -sf libncurses.so.5.6 ${pkgdir}/usr/lib/libncurses.so.5

  # install license, rip it from the readme
  cd ${srcdir}/${pkgname}-${pkgver}
  mkdir -p ${pkgdir}/usr/share/licenses/$pkgname
  grep -B 100 '$Id' README > ${pkgdir}/usr/share/licenses/${pkgname}/license.txt
}
