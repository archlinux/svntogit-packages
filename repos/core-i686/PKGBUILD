# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>

pkgname=file
pkgver=5.04
pkgrel=3
pkgdesc="File type identification utility"
arch=('i686' 'x86_64')
license=('custom')
groups=('base')
url="http://www.darwinsys.com/file/"
depends=('glibc' 'zlib')
options=('!libtool')
source=(ftp://ftp.astron.com/pub/${pkgname}/${pkgname}-${pkgver}.tar.gz 
        file-5.04-python-2.7.patch
        file-5.04-zip64.patch)
md5sums=('accade81ff1cc774904b47c72c8aeea0' 
         '4f223a3ff4f157320f1e6faffc4afb88'
         'b547eb3430adb3deb756d3a43dd7ad46')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i 's#\$(datadir)/misc#\$(datadir)#' configure 

  # identify python 2.7 byte-compiled files
  patch -Np1 -i ../file-5.04-python-2.7.patch
  # identify zip64 files
  patch -Np1 -i ../file-5.04-zip64.patch

  ./configure --prefix=/usr --datadir=/usr/share/file
  make 
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR=${pkgdir} install
  install -dm755 ${pkgdir}/usr/share/misc
  ln -s ../file/magic.mgc ${pkgdir}/usr/share/misc
  
  install -D -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}
