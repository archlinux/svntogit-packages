# $Id$
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgname=syslinux
pkgver=4.03
pkgrel=4
arch=('i686' 'x86_64')
pkgdesc="Collection of boot loaders that boot from FAT, ext2/3/4 and btrfs filesystems, from CDs and via PXE"
url="http://syslinux.zytor.com/"
license=('GPL2')
depends=('perl' 'glibc')
optdepends=('perl-passwd-md5:  For md5pass'
            'perl-digest-sha1: For sha1pass'
            'mtools:           For mkdiskimage and syslinux'
           )
makedepends=('nasm')
backup=('boot/syslinux/syslinux.cfg')
install=syslinux.install
source=(http://www.kernel.org/pub/linux/utils/boot/syslinux/$pkgname-${pkgver}.tar.bz2
        syslinux-dont-build-dos-windows-targets.patch
        syslinux.cfg
        syslinux-install_update)
sha256sums=('c65567e324f9d1f7f794ae8f9578a0292bbd47d7b8d895a004d2f0152d0bda38'
            '7e9301e37113d8bcddadc3a7d2fd6f3f433ef20ef6fa6df9fe9e256fa8868753'
            'b4ba8f9daa67ec35da6e05adb610469bc8c4facf488468c1ec3e5860ba75313d'
            '49573a3b65362326c8b1e9bfa49e83225e0bf9d3710454de4070abc9481c614c')

build() {
  # Do not try to build syslinux with our default LDFLAGS, it will fail
  unset LDFLAGS
  cd "$srcdir"/$pkgname-${pkgver}
  # Do not try to build the Windows or DOS installers
  patch -p1 -i "$srcdir"/syslinux-dont-build-dos-windows-targets.patch
  # Fix FHS manpage path
  sed 's|/usr/man|/usr/share/man|g' -i MCONFIG
  make
}

package() {
  cd "$srcdir"/$pkgname-${pkgver}
  make INSTALLROOT="$pkgdir" AUXDIR=/usr/lib/syslinux install

  # Install the default configuration
  install -D -m644 "$srcdir"/syslinux.cfg "$pkgdir"/boot/syslinux/syslinux.cfg
  # Install the installation and update script
  # This script is maintained at git://gist.github.com/772138.git
  install -D -m755 "$srcdir"/syslinux-install_update "$pkgdir"/usr/sbin/syslinux-install_update
}
