# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=zlib
pkgver=1.2.5
pkgrel=1
pkgdesc='Compression library implementing the deflate compression method found in gzip and PKZIP'
arch=('i686' 'x86_64')
license=('custom')
url="http://www.zlib.net/"
depends=('glibc')
options=('!makeflags')
source=("http://zlib.net/zlib-${pkgver}.tar.gz")
md5sums=('c735eab2d659a96e5a594c9e8541ad63')

build() {
	cd ${srcdir}/zlib-$pkgver
	export CFLAGS="${CFLAGS/-O2/-O3} -DUNALIGNED_OK"
	./configure --prefix=/usr
	make || return 1

	grep -A 24 '^  Copyright' zlib.h > LICENSE
}

package() {
	cd ${srcdir}/zlib-$pkgver
	make install DESTDIR=${pkgdir} || return 1
	install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/zlib/LICENSE
}
