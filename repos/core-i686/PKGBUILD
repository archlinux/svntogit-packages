# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=libusb
pkgver=1.0.19
pkgrel=1
pkgdesc='Library that provides generic access to USB devices'
arch=('i686' 'x86_64')
url='http://libusb.info/'
license=('LGPL')
depends=('glibc' 'systemd')
replaces=('libusb1' 'libusbx')
provides=("libusbx=$pkgver")
conflicts=('libusbx')
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.bz2)
md5sums=('f9e2bb5879968467e5ca756cb4e1fa7e')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-static
  make
}

check() {
  make -C $pkgname-$pkgver check
}

package () {
  make -C $pkgname-$pkgver DESTDIR="$pkgdir" install
}
