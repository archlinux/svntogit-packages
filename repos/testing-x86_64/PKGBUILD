# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=swell-foop
pkgver=3.22.0+1+g6ac88b0
pkgrel=1
pkgdesc="Clear the screen by removing groups of colored and shaped tiles"
url="https://wiki.gnome.org/Apps/Swell%20Foop"
arch=(i686 x86_64)
license=(GPL)
depends=(clutter-gtk librsvg)
makedepends=(intltool itstool gobject-introspection yelp-tools appstream-glib vala git)
groups=(gnome-extra)
_commit=6ac88b0f0137dcd90910c8bfabe52d0d0bd6930d  # master
source=("git://git.gnome.org/swell-foop#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --disable-schemas-compile
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
