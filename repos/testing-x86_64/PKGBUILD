# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: Mark Rosenstand <mark@archlinux.org>

pkgbase=fuse3
pkgname=(fuse-common fuse3)
pkgver=3.14.1
pkgrel=1
pkgdesc='Interface for userspace programs to export a filesystem to the Linux kernel'
url='https://github.com/libfuse/libfuse'
arch=('x86_64')
license=('GPL2')
makedepends=('pkg-config' 'meson' 'udev')
options=('!emptydirs')
source=(https://github.com/libfuse/libfuse/releases/download/fuse-$pkgver/fuse-$pkgver.tar.xz{,.asc})
sha256sums=('126919d72b46b3e0eb58a9c6933a2a50c36f2ea69f61fe9e78bdba9f463ffa20'
            'SKIP')
validpgpkeys=(ED31791B2C5C1613AF388B8AD113FCAC3C4E599F) # Nikolaus Rath <Nikolaus@rath.org>

build() {
  cd fuse-$pkgver

  rm -rf build
  meson --prefix=/usr --sbindir=bin -D examples=false . build
  cd build
  ninja
}

package_fuse-common() {
  pkgdesc="Common files for fuse2/3 packages"
  backup=(etc/fuse.conf)

  install -Dm644 fuse-${pkgver}/util/fuse.conf "${pkgdir}"/etc/fuse.conf
}

package_fuse3() {
  depends=('fuse-common' 'glibc')

  cd fuse-$pkgver/build

  DESTDIR="${pkgdir}" ninja install

  rm -r "${pkgdir}"/etc/init.d
  rm -r "${pkgdir}"/etc/fuse.conf

  # static device nodes are handled by udev
  rm -r "${pkgdir}"/dev
}
