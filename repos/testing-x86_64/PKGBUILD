# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Christian Hesse <mail@eworm.de>
# Contributor: Marco A Rojas <marquicus at gmail dot com>
# Contributor: Ng Oon-Ee <ngoonee.talk@gmail.com>
# Contributor: Thomas Burdick <thomas.burdick@gmail.com>

pkgname=tevent
pkgver=0.9.39
pkgrel=4
epoch=1
pkgdesc="An event system based on the talloc memory management library"
arch=('x86_64')
url="https://tevent.samba.org/"
source=("https://samba.org/ftp/tevent/${pkgname}-${pkgver}.tar.gz"
        samba-update-waf-for-py38.patch::https://gitlab.com/samba-team/samba/commit/aabdcc9151.patch
        wafsamba-use-native-waf-timer.patch)
license=('GPL3')
depends=('talloc')
makedepends=('python')
optdepends=('python: for python bindings')

prepare() {
	cd ${srcdir}/${pkgname}-${pkgver}

	# https://bugzilla.samba.org/show_bug.cgi?id=13998
	patch -Np1 -i ../wafsamba-use-native-waf-timer.patch

	# https://bugzilla.samba.org/show_bug.cgi?id=13960
	patch -Np1 -i ../samba-update-waf-for-py38.patch
}

build() {
	cd ${srcdir}/${pkgname}-${pkgver}
	./configure --prefix=/usr \
                    --bundled-libraries=NONE \
                    --builtin-libraries=replace
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}
	
	make
	make DESTDIR=${pkgdir}/ install
}
md5sums=('b937d5e980fa9704f20b57df688845c0'
         '74e627915f28a848e046d37055d86f3b'
         '66185b90c2dcb6bc346ef8ba5200717f')
