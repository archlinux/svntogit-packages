# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=gnome-dictionary
pkgver=3.20.0
pkgrel=1
pkgdesc="Look up words in dictionary sources"
arch=(i686 x86_64)
url="http://gnome.org"
license=('GPL2')
depends=('dconf' 'gtk3')
makedepends=('intltool' 'itstool' 'docbook-xsl')
install=gnome-dictionary.install
groups=('gnome')
source=(http://download.gnome.org/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('efb36377d46eff9291d3b8fec37baab2355f9dc8bc7edb791b6a625574716121')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
