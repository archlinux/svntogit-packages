# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: eric <eric@archlinux.org>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>

_name=raptor2
pkgname=raptor
pkgver=2.0.16
pkgrel=1
pkgdesc="A C library that parses RDF/XML/N-Triples into RDF triples"
arch=(x86_64)
url="https://librdf.org/raptor"
license=(Apache GPL2 LGPL2.1)
depends=(glibc libxml2 libxslt xz zlib)
makedepends=(curl gtk-doc icu)
provides=(libraptor2.so)
source=(
  https://librdf.org/dist/source/$_name-$pkgver.tar.gz{,.asc}
  $pkgname-2.0.16-disable_broken_test_suites.patch
)
sha512sums=('9bd5cff36390e1e0ef15ac56e5413ecfceb4018cb531a4da8850d3623615f12a93690a78be61f9d9ae7a24e16f6446e356bc2b7f34051ddc077761d85a9b7c44'
            'SKIP'
            '74d7172e67b20be433d41229d24cbceab9a1871215d741d8dbd3e6d1b73ad12910e560bd18acb52153962ed16f5b9a7c8647f6b29a0e6657de9f0b788783d567')
b2sums=('1e5e5742ba4cdaacb98a9ba77a9352589df0da60869e7721ee140c81ed4886bf909b37b247bd925c82a4ac44b3c11a909c913f0851d49a1d9d91c9293189266d'
        'SKIP'
        '615decc1eefd3a17576c6fdc06e64e8083479c6c1409b685419a3e314bb4d9eaa54ba5717506d4c38410b7d77d936a899ec8d9b027babf7718a12048f48fc49e')
validpgpkeys=('F879F0DEDA780198DD08DC6443EC92504F71955A') # Dave Beckett <dave@dajobe.org>

prepare() {
  # remove broken test suites: https://github.com/dajobe/raptor/issues/49
  patch -d $_name-$pkgver -Np1 -i ../$pkgname-2.0.16-disable_broken_test_suites.patch

  cd $_name-$pkgver
  autoreconf -fiv
}

build() {
  local configure_options=(
    --prefix=/usr
    --disable-static
    --with-icu-config=/usr/bin/icu-config
  )

  cd $_name-$pkgver
  ./configure "${configure_options[@]}"
  # prevent excessive overlinking due to libtool
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  make check -C $_name-$pkgver
}

package() {
  depends+=(
    curl libcurl.so
    icu libicuuc.so
  )

  make DESTDIR="$pkgdir" install -C $_name-$pkgver
  install -vDm 644 $_name-$pkgver/{AUTHORS,ChangeLog*,README} -t "$pkgdir/usr/share/doc/$pkgname/"
}
