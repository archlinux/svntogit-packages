# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=phonon
pkgver=4.4.4
pkgrel=1
arch=('i686' 'x86_64')
url="http://phonon.kde.org"
license=('LGPL')
pkgdesc="The multimedia API for KDE4"
depends=('qt' 'phonon-backend' 'libpulse')
optdepends=('pulseaudio: PulseAudio support')
makedepends=('cmake' 'automoc4' 'pulseaudio')
source=("http://download.kde.org/stable/${pkgname}/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2")
md5sums=('1deb14ecb2185e1f2fe2741a0bd46852')

build() {
  cd ${srcdir}
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON
  make
}

package(){
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install
}
