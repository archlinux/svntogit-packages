# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Murtuza Akhtari <inxsible at gmail dot com>
# Contributor: Keshav Amburay <(the ddoott ridikulus ddoott rat) (aatt) (gemmaeiil) (ddoott) (ccoomm)>

_pkgname="efibootmgr"
pkgname="${_pkgname}"

_gitroot="https://github.com/vathpela/efibootmgr.git"
_gitname="${_pkgname}"
_gitbranch="master"

_GIT_COMMIT="f4e29e4200ed1e866c9cf98d2ebdcb8ac2e9826c"

pkgver=0.6.1.29.gf4e29e4
pkgrel=1
pkgdesc="Tool to modify UEFI Firmware Boot Manager Variables"
arch=('x86_64' 'i686')
url="https://github.com/vathpela/efibootmgr"
license=('GPL2')
makedepends=('git')
depends=('pciutils' 'efivar' 'zlib')
conflicts=("${_pkgname}-pjones")
provides=("${_pkgname}-pjones=${pkgver}")
options=('strip' 'zipman' '!emptydirs')

source=("${_gitname}::git+${_gitroot}#commit=${_GIT_COMMIT}")
sha1sums=('SKIP')

pkgver() {
	cd "${srcdir}/${_gitname}/"
	echo "$(git describe --tags)" | sed -e 's|-|\.|g'
}

build() {
	
	rm -rf "${srcdir}/${_gitname}_build/" || true
	cp -r "${srcdir}/${_gitname}" "${srcdir}/${_gitname}_build"
	
	cd "${srcdir}/${_gitname}_build/"
	
	make EXTRA_CFLAGS="-Os"
	echo
	
}

package() {
	
	cd "${srcdir}/${_gitname}_build/"
	
	install -d "${pkgdir}/usr/bin/"
	install -D -m0755 "${srcdir}/${_gitname}_build/src/efibootmgr/efibootmgr" "${pkgdir}/usr/bin/efibootmgr"
	
	install -d "${pkgdir}/usr/share/man/man8/"
	install -D -m0644 "${srcdir}/${_gitname}_build/src/man/man8/efibootmgr.8" "${pkgdir}/usr/share/man/man8/efibootmgr.8"
	
}
