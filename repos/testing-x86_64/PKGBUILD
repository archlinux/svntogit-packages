# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=networkmanager-pptp
pkgver=1.0.0
pkgrel=1
pkgdesc="NetworkManager VPN plugin for pptp "
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnome.org/projects/NetworkManager/"
_pppver=2.4.7
depends=("networkmanager>=${pkgver}" 'pptpclient' 'gtk3' 'libgnome-keyring' "ppp=$_pppver")
makedepends=('intltool' 'network-manager-applet')
optdepends=('network-manager-applet: GNOME frontends to NetWorkmanager')
install=networkmanager-pptp.install
source=(http://ftp.gnome.org/pub/GNOME/sources/NetworkManager-pptp/${pkgver::3}/NetworkManager-pptp-${pkgver}.tar.xz)
sha256sums=('200d5c65cdae248d3b4d0daa76a4f8bc1432376ed4da7afedd5085bb0da7ee76')

build() {
  cd NetworkManager-pptp-${pkgver}
  ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --libexecdir=/usr/lib/networkmanager \
        --disable-static \
        --enable-more-warnings=yes \
        --with-pppd-plugin-dir=/usr/lib/pppd/$_pppver
  make
}

package() {
  cd NetworkManager-pptp-${pkgver}
  make DESTDIR="${pkgdir}" install
}
