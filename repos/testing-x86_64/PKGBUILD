# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Stéphane Gaudreault <stephane@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=gdbm
pkgver=1.19
pkgrel=2
pkgdesc='GNU database library'
url="https://www.gnu.org/software/gdbm/gdbm.html"
license=('GPL3')
arch=('x86_64')
depends=('glibc' 'sh')
makedepends=('readline')
provides=('libgdbm_compat.so' 'libgdbm.so')
source=("https://ftp.gnu.org/gnu/gdbm/${pkgname}-${pkgver}.tar.gz"{,.sig})
sha512sums=('118c5b8cdf74898bfb7c2100302fedf80096be017bf08e80a44486563cad5d93b93567622f2e7c7aceb72f30460504bd0b4ddfccf34df994ed65166e12ecd495'
            'SKIP')
b2sums=('92a1a9c461c763b01d7abced58800aa8db200ae746240c8ef8079a615d920efe95f0d805a8cfa63d8aa6bcbda637bcb07d98d630f58de5678b7633c3fcf6bf9b'
        'SKIP')
validpgpkeys=('325F650C4C2B6AD58807327A3602B07F55D0C732') # Sergey Poznyakoff <gray@gnu.org>

prepare() {
  cd "$pkgname-$pkgver"
  autoreconf -fiv
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr \
              --enable-libgdbm-compat
  make
}

check() {
  cd "$pkgname-$pkgver"
  make check
}

package() {
  depends+=('libreadline.so')
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -vDm 644 {NOTE-WARNING,AUTHORS,NEWS,README,ChangeLog} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
