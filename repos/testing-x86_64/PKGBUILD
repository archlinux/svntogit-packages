# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=ptlib
pkgver=2.10.11
pkgrel=3
pkgdesc="Portable Windows Library"
arch=('i686' 'x86_64')
url="http://www.opalvoip.org"
license=('GPL')
depends=('sdl' 'libpulse' 'v4l-utils' 'libldap')
replaces=('pwlib')
conflicts=('pwlib')
source=(http://downloads.sourceforge.net/opalvoip/ptlib-$pkgver.tar.bz2
        ptlib-2.10.10-bison_fixes-1.patch
        ptlib-gcc5.patch)
sha256sums=('27a8ba1fd147195c0b310897f81d26f73996b77b18147e587638444cfacd681a'
            '11027b21f568b2ab84f9c25775e5a697a15304b39aaab6380922ed609aac63b9'
            '7cf1781f6f7136252770de712399be493b991d5c2e0b36e908eb1770b189cfd7')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../ptlib-2.10.10-bison_fixes-1.patch
  patch -Np1 -i ../ptlib-gcc5.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --libdir=/usr/lib \
      --sysconfdir=/etc --localstatedir=/var \
      --enable-opal --enable-plugins \
      --enable-oss --enable-v4l2 \
      --disable-avc --enable-sdl \
      --enable-ipv6 --enable-v4l \
      --disable-dc --disable-odbc \
      --enable-expat --enable-httpforms
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="${pkgdir}" install
}
