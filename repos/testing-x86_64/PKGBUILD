# Packager: Justin Davis (juster) <jrcd83@gmail.com>
# $Id$

pkgname=perl-async-interrupt
_ver=1.1
pkgver=1.10
pkgrel=3
pkgdesc='allow C/XS libraries to interrupt perl asynchronously'
arch=(i686 x86_64)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=(perl-common-sense)
url=https://metacpan.org/release/Async-Interrupt
source=("http://search.cpan.org/CPAN/authors/id/M/ML/MLEHMANN/Async-Interrupt-$_ver.tar.gz")
md5sums=(18f19b1537ecf5d6195a6a243110e64b)
sha512sums=(e8e62efc053c8103f509c08a19bfa0fefbfe760ca4e1e4670187de4ed4fbdec2e5a9a19bba1696cd2b721ad7fa6d4b723c24e342118b69dc8b7b1597f5e1385e)

build()
(
  cd "$srcdir/Async-Interrupt-$_ver"
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  /usr/bin/perl Makefile.PL
  make
)

check()
(
  cd "$srcdir/Async-Interrupt-$_ver"
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
)

package()
(
  cd "$srcdir/Async-Interrupt-$_ver"
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
)

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
