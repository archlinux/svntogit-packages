# Maintainer: damir <damir@archlinux.org>

pkgbase=libxml++
pkgname=('libxml++' 'libxml++-docs')
pkgver=3.2.0
pkgrel=1
arch=('x86_64')
license=('LGPL')
url="http://libxmlplusplus.sourceforge.net/"
makedepends=('glibmm-docs' 'libxml2' 'glibmm' 'git' 'mm-common')
options=('!emptydirs')
_commit=50c5efe0bcce3fa5273afbc832b1424b352469c9  # 3.2.0
source=("git+https://github.com/libxmlplusplus/libxmlplusplus#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd libxmlplusplus
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd libxmlplusplus
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd libxmlplusplus
  ./configure --prefix=/usr

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd libxmlplusplus
  make check
}

package_libxml++() {
  pkgdesc="C++ bindings to libxml2"
  depends=('libxml2' 'glibmm')

  cd libxmlplusplus
  make DESTDIR="${pkgdir}" install doc_subdirs=
}

package_libxml++-docs() {
  pkgdesc="Developer documentation for libxml++"
  depends=('glibmm-docs')
  cd libxmlplusplus
  make DESTDIR="${pkgdir}" -C docs install
}
