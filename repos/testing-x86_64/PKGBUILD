# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=inetutils
pkgver=1.9.4
pkgrel=6
pkgdesc="A collection of common network programs"
arch=('x86_64')
url="http://www.gnu.org/software/inetutils/"
license=('GPL3')
groups=('base')
depends=('pam' 'libcap')
makedepends=('help2man')
backup=('etc/xinetd.d/telnet' 'etc/xinetd.d/talk'
        'etc/xinetd.d/rlogin' 'etc/xinetd.d/rsh'
	'etc/pam.d/rlogin' 'etc/pam.d/rsh')
options=('!emptydirs')
install=inetutils.install
source=("https://ftp.gnu.org/gnu/inetutils/${pkgname}-${pkgver}.tar.xz"{,.sig}
        'ftpd.service' 'rlogin.pam' 'rlogin@.service' 'rlogin.socket' 'rlogin.xinetd'
        'rsh.pam' 'rsh@.service' 'rsh.socket' 'rsh.xinetd' 'talk.service' 'talk.socket'
        'talk.xinetd' 'telnet@.service' 'telnet.socket' 'telnet.xinetd')
sha1sums=('5e515cc9da142cb73bb1beda137b4c2dcf2b528c'
          'SKIP'
          '026181500b71f0c09ef5c262450be718651e5658'
          '387b371cbaa3611b95d30f806c0dd08b621a584a'
          '6f9a304391610a17b8ae3ad35b742c78c86aee16'
          '0455126fa18d2a9422469d79b9e73dd928b15652'
          'd9b8f8aab094ecb779d0f16f21b4683ab327516f'
          '556b5372cbab8794e931e110b896fa8a61628bb1'
          '828da7bce49ffc4e9645288759b275a98a2fbc40'
          '7071da32d42a133ead039197ed891b518bb0f773'
          '26763ae31b8a5be256666a0ba55746941d75d6ee'
          '4486c966d28736897bce1280d85d139bd10a5689'
          'd1813d42c5c9d2c60f03ebe33961de357a4b8b43'
          'b15ab4faa9d97652e182c98c1e3a3b3cbbf6383c'
          '0917dc6b5a80d914bf550065b1733b7da2c776f3'
          'a7ac7bbe917ff80fd8cd4357fbc62fab50595c34'
          '2b2dd71eb3233e5090f4c2df8abe9b4924d323f3')
validpgpkeys=('4FBD67621082C4C502448E3B180551BAD95A3C35')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --libexec=/usr/bin \
    --localstatedir=/var --sysconfdir=/etc \
    --without-wrap --with-pam \
    --enable-ftp --enable-ftpd \
    --enable-telnet --enable-telnetd \
    --enable-talk --enable-talkd \
    --enable-rlogin --enable-rlogind \
    --enable-rsh --enable-rshd \
    --enable-rcp --enable-hostname --enable-dnsdomainname \
    --disable-rexec --disable-rexecd \
    --disable-tftp --disable-tftpd \
    --disable-ping --disable-ping6 \
    --disable-logger --disable-syslogd \
    --disable-inetd --disable-whois \
    --disable-uucpd --disable-ifconfig --disable-traceroute
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  chmod -s "${pkgdir}"/usr/bin/{rcp,rlogin,rsh}

  install -D -m644 "${srcdir}/telnet.xinetd" "${pkgdir}/etc/xinetd.d/telnet"
  install -D -m644 "${srcdir}/talk.xinetd" "${pkgdir}/etc/xinetd.d/talk"
  install -D -m644 "${srcdir}/rlogin.xinetd" "${pkgdir}/etc/xinetd.d/rlogin"
  install -D -m644 "${srcdir}/rsh.xinetd" "${pkgdir}/etc/xinetd.d/rsh"

  install -D -m644 "${srcdir}/rlogin.pam" "${pkgdir}/etc/pam.d/rlogin"
  install -D -m644 "${srcdir}/rsh.pam" "${pkgdir}/etc/pam.d/rsh"

  install -D -m644 "${srcdir}/ftpd.service" "${pkgdir}/usr/lib/systemd/system/ftpd.service"
  install -D -m644 "${srcdir}/rlogin@.service" "${pkgdir}/usr/lib/systemd/system/rlogin@.service"
  install -D -m644 "${srcdir}/rlogin.socket" "${pkgdir}/usr/lib/systemd/system/rlogin.socket"
  install -D -m644 "${srcdir}/rsh@.service" "${pkgdir}/usr/lib/systemd/system/rsh@.service"
  install -D -m644 "${srcdir}/rsh.socket" "${pkgdir}/usr/lib/systemd/system/rsh.socket"
  install -D -m644 "${srcdir}/talk.service" "${pkgdir}/usr/lib/systemd/system/talk.service"
  install -D -m644 "${srcdir}/talk.socket" "${pkgdir}/usr/lib/systemd/system/talk.socket"
  install -D -m644 "${srcdir}/telnet@.service" "${pkgdir}/usr/lib/systemd/system/telnet@.service"
  install -D -m644 "${srcdir}/telnet.socket" "${pkgdir}/usr/lib/systemd/system/telnet.socket" 
}
