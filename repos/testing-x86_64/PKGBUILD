# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Rustam Tsurik <rustam.tsurik@gmail.com>
# Contributor: Nico Suarez <nicodoggie@gmail.com>

pkgname=galera
pkgver=26.4.11
_mariadb_ver=10.6.7
pkgrel=2
pkgdesc='write set replication (WSREP) provider for MariaDB cluster'
arch=('x86_64')
license=('GPL2' 'custom:OpenSSL Linking Exception')
depends=('mariadb' 'openssl')
makedepends=('boost' 'check' 'cmake')
optdepends=('rsync: State Snapshot Transfers with rsync'
            'socat: State Snapshot Transfers with mariabackup')
url='https://galeracluster.com/'
options=('debug' '!libtool')
validpgpkeys=('199369E5404BD5FC7D2FE43BCBCB082A1BB943DB') # MariaDB Package Signing Key <package-signing-key@mariadb.org>
source=("https://rsync.osuosl.org/pub/mariadb/mariadb-${_mariadb_ver}/galera-${pkgver}/src/galera-${pkgver}.tar.gz"{,.asc})
sha256sums=('08bbba89c33344474bdedbaa4db42ea33489085cf42249e9f3880a5a66dbf4b1'
            'SKIP')

prepare() {
  cd "${pkgname}-${pkgver}"

  sed -i '/^GALERA_VER/s/API + //' wsrep/tests/SConscript
}

build(){
  cd "${pkgname}-${pkgver}"

  cmake .
  make
}

package(){
  cd "${pkgname}-${pkgver}"

  install -D -m0755 libgalera_smm.so "${pkgdir}"/usr/lib/galera/libgalera_smm.so
  ln -s galera/libgalera_smm.so "${pkgdir}"/usr/lib/libgalera_smm.so

  install -D -m0755 garb/garbd "${pkgdir}"/usr/bin/garbd

  install -D -m0644 scripts/packages/README "${pkgdir}"/usr/share/doc/galera/README
  install -D -m0644 scripts/packages/README-MySQL "${pkgdir}"/usr/share/doc/galera/README-MySQL
  install -D -m0644 scripts/mysql/README "${pkgdir}"/usr/share/doc/galera/mysql/README
  install -D -m0644 COPYING "${pkgdir}"/usr/share/doc/galera/COPYING
}

