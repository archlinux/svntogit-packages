# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=cups-pk-helper
pkgver=0.2.7
pkgrel=1
pkgdesc="A helper that makes system-config-printer use PolicyKit"
arch=(x86_64)
url="https://www.freedesktop.org/software/cups-pk-helper/releases/"
license=('GPL')
depends=(polkit libcups)
makedepends=('meson')
source=(https://www.freedesktop.org/software/$pkgname/releases/$pkgname-$pkgver.tar.xz)
sha256sums=('66070ddb448fe9fcee76aa26be2ede5a80f85563e3a4afd59d2bfd79fbe2e831')

build() {
  arch-meson $pkgname-$pkgver build

  # Print config
  meson configure build
  meson compile -C build
}

check() {
  meson test -C build || /bin/true
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}
