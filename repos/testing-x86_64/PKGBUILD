# Maintainer: Dan McGee <dan@archlinux.org>
# Contributor: Florian Zeitz <florob at babelmonkeys dot de>

pkgname=numactl
pkgver=2.0.12
pkgrel=1
pkgdesc="Simple NUMA policy support"
arch=('x86_64')
url="http://oss.sgi.com/projects/libnuma/"
license=('LGPL2.1' 'GPL2')
depends=('perl')
source=(https://github.com/numactl/numactl/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('55bbda363f5b32abd057b6fbb4551dd71323f5dbb66335ba758ba93de2ada729')

build() {
  cd "$srcdir/$pkgname-${pkgver/_/-}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-${pkgver/_/-}"
  make DESTDIR="$pkgdir" install
  # move_pages(2) is provided by man-pages and is more up-to-date there anyway
  rm -rf "$pkgdir/usr/share/man/man2"
}
