# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andr√© Klitzing <aklitzing () gmail () com>

pkgname=clazy
pkgver=1.11
pkgrel=3
pkgdesc='Qt oriented code checker based on clang framework'
url='https://www.kdab.com/'
license=(GPL2)
depends=(clang)
makedepends=(cmake llvm)
arch=(x86_64)
source=(https://download.kde.org/stable/$pkgname/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        https://github.com/KDE/clazy/commit/20fca52d.patch)
sha256sums=('66165df33be8785218720c8947aa9099bae6d06c90b1501953d9f95fdfa0120a'
            'SKIP'
            '652854327c1bd48f1e7080ce834dba3fb37d8a8b1d21ebaa5c70be816d8164af')
validpgpkeys=(949014B23D24354DFD548E5457416A0ADCEF0EFE) # Sergio Martins <sergio.martins@kdab.com>

prepare() {
  patch -d $pkgname-$pkgver -p1 < 20fca52d.patch # Fix build with LLVM 15
}

build() {
  cmake -B build $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
