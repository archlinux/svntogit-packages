# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-documents
pkgver=3.28.0
pkgrel=1
pkgdesc="A document manager application for GNOME"
url="https://wiki.gnome.org/Apps/Documents"
arch=(x86_64)
license=(GPL)
depends=(evince gjs gtk3 gnome-desktop gnome-online-accounts libgdata tracker-miners libzapojit
         webkit2gtk gnome-online-miners librsvg libgepub)
makedepends=(gobject-introspection git meson yelp-tools inkscape)
optdepends=('libreoffice-fresh: Support for Libreoffice document types')
groups=(gnome)
_commit=37df033b1be9cdc1accd0aef62f5cb763df56de2  # tags/3.28.0^0
source=("git+https://git.gnome.org/browse/gnome-documents#commit=$_commit"
        "git+https://git.gnome.org/browse/libgd")
sha256sums=('SKIP'
            'SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname

  git submodule init
  git config --local submodule.subprojects/libgd.url "$srcdir/libgd"
  git submodule update
}
  

build() {
  arch-meson $pkgname build -D getting_started=true
  ninja -C build
}

check() {
  cd build
  meson test
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}

# vim:set ts=2 sw=2 et:
