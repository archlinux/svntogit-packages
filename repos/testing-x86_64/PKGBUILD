# $Id$
# Maintainer:
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

pkgname=neon
pkgver=0.29.6
pkgrel=4
pkgdesc="HTTP and WebDAV client library with a C interface"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL')
depends=('krb5' 'expat' 'ca-certificates')
url="http://www.webdav.org/neon/"
source=("http://www.webdav.org/${pkgname}/${pkgname}-${pkgver}.tar.gz")
md5sums=('591e0c82e6979e7e615211b386b8f6bc')
options=('libtool') # FS#16067

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  ./configure --prefix=/usr \
    --with-expat \
    --enable-shared \
    --disable-static \
    --with-ssl=openssl \
    --with-ca-bundle=/etc/ssl/certs/ca-certificates.crt
  make

  # fix invalid .so links in man pages (FS#24902)
  sed -i '/^\.so/s|\.so \([^.]\+\)\.\([[:digit:]]\)|.so man\2/\1.\2|' doc/man/*
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
