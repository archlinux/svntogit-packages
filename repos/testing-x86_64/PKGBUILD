# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

pkgname=xf86-video-ati
pkgver=7.5.0+99+g7186a87
epoch=1
pkgrel=1
pkgdesc="X.org ati video driver"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libdrm>=2.4.58' 'systemd' 'mesa-dri')
makedepends=('xorg-server-devel' 'X-ABI-VIDEODRV_VERSION=19' 'git')
conflicts=('xorg-server<1.16.0' 'X-ABI-VIDEODRV_VERSION<19' 'X-ABI-VIDEODRV_VERSION>=20')
groups=('xorg-drivers' 'xorg')
#source=(${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2)
source=('git://anongit.freedesktop.org/xorg/driver/xf86-video-ati#commit=7186a87')
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --long | sed 's/xf86-video-ati-//' | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd ${pkgname}

  ./configure --prefix=/usr \
    --enable-glamor
  make
}

package() {
  cd ${pkgname}

  make "DESTDIR=${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
