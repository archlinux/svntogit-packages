# $Id$
# Maintainer: Dan McGee <dan@archlinux.org>
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>

pkgname=nilfs-utils
pkgver=2.2.1
pkgrel=2
pkgdesc="A log-structured file system supporting continuous snapshotting (userspace utils)"
arch=('i686' 'x86_64')
url="http://nilfs.sourceforge.net/"
license=('GPL2' 'LGPL2.1')
backup=('etc/nilfs_cleanerd.conf')
depends=('util-linux')
source=(http://www.nilfs.org/download/$pkgname-$pkgver.tar.bz2
        0001-libmount-don-t-base-GC-startup-on-no-mtab-context.patch)
md5sums=('a4e2ee5e64d57bd38e1ffcc5630bd4ec'
         '48bed3723abefef0a6329610d60c4d44')

build() {
  cd $pkgname-$pkgver
  patch -Np1 < ../0001-libmount-don-t-base-GC-startup-on-no-mtab-context.patch
  sed -i -e 's#root_sbindir=/sbin#root_sbindir=/usr/bin#' configure
  ./configure --sbindir=/usr/bin --with-libmount
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" sbindir=/usr/bin install LDCONFIG=/bin/true
}
