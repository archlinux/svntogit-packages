# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=lynx
pkgver=2.8.8
_relver=${pkgver}rel.2
pkgrel=3
pkgdesc="A text browser for the World Wide Web"
url="http://lynx.isc.org/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('openssl' 'libidn')
backup=('etc/lynx.cfg')
source=("http://lynx.isc.org/current/${pkgname}${_relver}.tar.bz2"
        "http://lynx.isc.org/current/${pkgname}${_relver}.tar.bz2.asc")
md5sums=('b231c2aa34dfe7ca25681ef4e55ee7e8'
         'SKIP')
validpgpkeys=('0AFD1FFEEA2EA063B959ACDA5DDF8FB7688E31A6')

build() {
  cd ${srcdir}/${pkgname}${pkgver//./-}
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --with-ssl=/usr \
    --enable-nls \
    --enable-ipv6 \
    --mandir=/usr/share/man
  make
}

package() {
  cd ${srcdir}/${pkgname}${pkgver//./-}
  make DESTDIR=${pkgdir} install

  # FS#20404 - points to local help
  sed -i -e "s|^HELPFILE.*$|HELPFILE:file:///usr/share/doc/lynx/lynx_help/lynx_help_main.html|" ${pkgdir}/etc/lynx.cfg

  install -d ${pkgdir}/usr/share/doc/lynx
  cp -rf lynx_help ${pkgdir}/usr/share/doc/lynx
}
