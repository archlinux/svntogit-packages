# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
pkgname=modemmanager
_realname=ModemManager
pkgver=0.6.0.0
pkgrel=1
pkgdesc="Mobile broadband modem management service"
arch=('i686' 'x86_64')
url="http://cgit.freedesktop.org/ModemManager/ModemManager/"
license=('GPL2')
depends=('dbus-glib' 'udev' 'ppp')
makedepends=('intltool')
optdepends=('usb_modeswitch: install if your modem shows up as a storage drive')
options=('!libtool')
source=(http://download.gnome.org/sources/$_realname/0.6/$_realname-$pkgver.tar.xz
        modem-manager.service)
sha256sums=('d4468300cf4aa7baf21c8564fa515e578056f34de5a64f452b053331f89e8ae2'
            '118e36f2f956d719cf4036f3fd5f5bf0fde88ca55bdf7ed95051b5b09066fcbe')

build() {
  cd "$_realname-$pkgver"
   ./configure --prefix=/usr \
   	--sysconfdir=/etc \
	--localstatedir=/var \
	--with-udev-base-dir=/usr/lib/udev \
	--disable-static
  make
}

package() {
  cd "$_realname-$pkgver"
  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir/modem-manager.service" \
    "$pkgdir/usr/lib/systemd/system/modem-manager.service"
  echo "SystemdService=modem-manager.service" \
    >> "$pkgdir/usr/share/dbus-1/system-services/org.freedesktop.ModemManager.service"
}
