# $Id$
# Maintainer: Kevin <kevin@archlinux.org>
# Contributor: criminy <criminy@gmail.com>

pkgname=cairomm
pkgver=1.12.0
pkgrel=2
pkgdesc="C++ bindings to Cairo vector graphics library"
arch=('i686' 'x86_64')
url="http://www.cairographics.org"
license=('LGPL' 'MPL')
depends=('cairo' 'libsigc++')
makedepends=('pkgconfig')
source=(http://download.gnome.org/sources/${pkgname}/${pkgver:0:4}/${pkgname}-${pkgver}.tar.xz)
options=('!emptydirs')
sha256sums=('a54ada8394a86182525c0762e6f50db6b9212a2109280d13ec6a0b29bfd1afe6')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
