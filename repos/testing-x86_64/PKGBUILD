# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=tzdata
pkgver=2011g
pkgrel=1
_tzcode=2011g
_tzdata=2011g
pkgdesc="Sources for time zone and daylight saving time data"
arch=('i686' 'x86_64')
url="http://www.twinsun.com/tz/tz-link.htm"
license=('GPL')
depends=()
makedepends=()
optdepends=('bash: required by tzselect')
options=('!emptydirs')
source=(ftp://elsie.nci.nih.gov/pub/tzcode${_tzcode}.tar.gz \
        ftp://elsie.nci.nih.gov/pub/${pkgname}${_tzdata}.tar.gz \
        Makefile.patch)
md5sums=('ecb564279b28c5b184421c525d997d6c'
         'a068c27e7e426fdb12ab0c88506df20d'
         'a64ed97d1fc03c66ee8612c0d9f40507')

build() {
  cd ${srcdir}

  tar -xf tzcode${_tzcode}.tar.gz
  tar -xf ${pkgname}${_tzdata}.tar.gz

  patch -Np1 -i "${srcdir}/Makefile.patch"

  make
}

check() {
  cd ${srcdir}
  make check
}

package() {
  cd ${srcdir}
  make DESTDIR="${pkgdir}" install

  rm "${pkgdir}/usr/share/zoneinfo/localtime"
}
