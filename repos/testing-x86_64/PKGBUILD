# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Tobias Kieslich <tobias funnychar archlinux.org>

pkgname=xfce4-dev-tools
pkgver=4.18.0
pkgrel=1
pkgdesc="Xfce developer tools"
arch=('x86_64')
url="https://www.xfce.org/"
license=('GPL2')
depends=('glib2' 'intltool' 'gtk-doc')
optdepends=('docker: xfce-build helper script')
source=(https://archive.xfce.org/src/xfce/xfce4-dev-tools/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=('eedb4fc955f0e3459c46864ff98579295db2b900743e0ff69cad5970ba76be37')

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
