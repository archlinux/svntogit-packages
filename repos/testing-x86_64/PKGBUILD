# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Fran√ßois Charette <firmicus@gmx.net>

pkgname=xz
pkgver=4.999.9beta
_snapshot=142-gd8b4
pkgrel=4
pkgdesc='Library and command line tools for XZ and LZMA compressed files'
arch=('i686' 'x86_64')
url='http://tukaani.org/xz/'
license=('GPL' 'LGPL' 'custom')
depends=('sh')
provides=('lzma' 'lzma-utils' 'xz-utils')
replaces=('lzma' 'lzma-utils' 'xz-utils')
conflicts=('lzma' 'lzma-utils' 'xz-utils')
options=('!libtool')
source=("http://tukaani.org/${pkgname}/${pkgname}-${pkgver}-${_snapshot}.tar.gz")
md5sums=('d72381a74bfbf4f20f3511bc3d92d27d')

# keep an upgrade path for older installations
PKGEXT='.pkg.tar.gz'

build() {
	cd ${srcdir}/${pkgname}-$pkgver-${_snapshot}

	./configure --prefix=/usr \
		--enable-dynamic \
		--disable-rpath \
		--enable-werror
	sed -i 's|^hardcode_libdir_flag_spec=.*|hardcode_libdir_flag_spec=""|g' libtool
	sed -i 's|^runpath_var=LD_RUN_PATH|runpath_var=DIE_RPATH_DIE|g' libtool
	make
}

package() {
	cd ${srcdir}/${pkgname}-$pkgver-${_snapshot}
	make DESTDIR=${pkgdir} install
	install -d -m755 ${pkgdir}/usr/share/licenses/xz/
	ln -sf /usr/share/doc/xz/COPYING ${pkgdir}/usr/share/licenses/xz/
	ln -sf /usr/share/licenses/common/GPL2/license.txt ${pkgdir}/usr/share/doc/xz/COPYING.GPLv2
}
