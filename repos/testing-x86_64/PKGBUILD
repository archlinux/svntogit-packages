# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gstreamer-vaapi
pkgver=1.16.0
pkgrel=1
pkgdesc="GStreamer Multimedia Framework VAAPI Plugin"
url="https://gstreamer.freedesktop.org/"
arch=(x86_64)
license=(LGPL)
depends=(gst-plugins-bad libva libxrandr)
makedepends=(python gtk-doc git gtk3 meson)
_commit=2c3480796a6673c638f5b12c7bdf5c0f191120fd  # tags/1.16.0^0
source=("git+https://gitlab.freedesktop.org/gstreamer/gstreamer-vaapi.git#commit=$_commit"
        "gst-common::git+https://gitlab.freedesktop.org/gstreamer/common.git")
sha256sums=('SKIP'
            'SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname

  git submodule init
  git config --local submodule.common.url "$srcdir/gst-common"
  git submodule update
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
