# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=gzip
pkgver=1.5
pkgrel=3
pkgdesc="GNU compression utility"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/gzip/"
license=('GPL3')
groups=('base' 'base-devel')
depends=('glibc' 'bash')
install=gzip.install
source=(ftp://ftp.gnu.org/pub/gnu/gzip/gzip-$pkgver.tar.xz{,.sig}
        gzip-1.5-yesno-declaration.patch)
md5sums=('2a431e169b6f62f7332ef6d47cc53bae'
         '2de95937a3f65137acf9c55d4ad0447a'
         'd63249578de2eb6aadaded26918d8eed')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  patch -p1 -i $srcdir/gzip-1.5-yesno-declaration.patch
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make prefix=${pkgdir}/usr install
}
