# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=audiocd-kio
pkgver=22.04.0
pkgrel=1
pkgdesc='Kioslave for accessing audio CDs'
url='https://apps.kde.org/kio_audiocd/'
arch=(x86_64)
license=(GPL LGPL FDL)
depends=(kcmutils libkcddb libkcompactdisc lame)
makedepends=(extra-cmake-modules kdoctools)
optdepends=('opus-tools: encoding to opus format')
groups=(kde-applications kde-multimedia)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('d991b44fb9432303c01dc4a72c4a645419730a61df7f45b09172a0874c086c80'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
options=(debug)

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
