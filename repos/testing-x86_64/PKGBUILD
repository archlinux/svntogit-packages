# $Id$
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Paul Mattal <paul@archlinux.org>

pkgname=ddrescue
pkgver=1.20
pkgrel=2
pkgdesc="GNU data recovery tool"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/ddrescue/ddrescue.html"
license=('GPL3')
depends=('gcc-libs')
install=$pkgname.install
source=("http://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.lz"
        "http://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.lz.sig")
md5sums=('e1f1bd4bbb0615455f32f2be3b4bdabc'
         'SKIP')
validpgpkeys=('1D41C14B272A2219A739FA4F8FE99503132D7742') # Antonio Diaz Diaz

build() {
  cd "$srcdir"/$pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  make DESTDIR="$pkgdir" install install-man
  rm "$pkgdir"/usr/share/info/dir
}
