# $Id$
# Maintainer: Tom Gundersen <teg@jklm.no>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: James Rayner <iphitus@gmail.com>
# Contributor: Sergio Jovani Guzman <moret@paretux.org>

pkgname=konversation
pkgver=1.5
pkgrel=3
pkgdesc="A user friendly IRC client for KDE"
arch=('i686' 'x86_64')
url="http://konversation.kde.org"
depends=('kdebase-runtime' 'kdepimlibs')
makedepends=('automoc4' 'cmake' 'pkgconfig' 'docbook-xml' 'optipng')
optdepends=('python: python scripting support'
            'kdebase-konsole: open a terminal in konversation'
            'qca-ossl: Diffie-Hellman key exchange and Blowfish ECB/CBC support')
license=('GPL2' 'FDL')
install=${pkgname}.install
source=("http://download.kde.org/download.php?url=stable/${pkgname}/${pkgver}/src/${pkgname}-${pkgver}.tar.xz"
        'use-qdbus-qt4.patch')
sha1sums=('3955a43758750b63d84299a9d9a5933b73e5492b'
          'a21aed6079de7693e67abe13798807920de01f6a')

prepare() {
  mkdir build

  cd ${pkgname}-${pkgver}
  patch -p1 -i "${srcdir}"/use-qdbus-qt4.patch
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
  
  # Remove a bunch of symlinks pointing to non-existant files
  for _lang in pt_BR uk sv et de en nl it fr es ca; do
     rm "${pkgdir}"/usr/share/doc/kde/html/${_lang}/konversation/common
  done
}
