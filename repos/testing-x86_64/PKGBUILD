# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
pkgname=cups
pkgver=1.3.7
pkgrel=2
pkgdesc="The CUPS Printing System"
arch=(i686 x86_64)
license=('GPL')
url="http://www.cups.org/"
depends=('acl' 'openslp' 'pam' "libcups>=${pkgver}" 'xdg-utils' 'avahi' 'heimdal>=1.2')
makedepends=('xinetd')
backup=(etc/cups/{printers.conf,client.conf,cupsd.conf,mime.types,mime.convs} \
        etc/cups/classes.conf etc/logrotate.d/cups etc/pam.d/cups)
source=(ftp://ftp.easysw.com/pub/cups/${pkgver}/cups-${pkgver}-source.tar.bz2 \
        cups cups.logrotate cups.pam cupsd.conf cups-1.3.7-peercred.patch)
md5sums=('db4a45a17104f10f3ee599d88267c9e5' '5c85b7d8d2ddd02c2c64955cebbf55ea'\
         'f861b18f4446c43918c8643dcbbd7f6d' '96f82c38f3f540b53f3e5144900acf17'\
         '3a55ba2b942182ab0bd2584c2138cb2d' '99e54c2fc138360bb7622bea4f0977bc')
options=('!emptydirs' '!makeflags')

build() {
  cd ${startdir}/src/${pkgname}-${pkgver}
  patch -p1 < ../cups-1.3.7-peercred.patch || return 1
  # fix french search template
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
     --with-logdir=/var/log/cups -with-docdir=/usr/share/cups/doc \
     --with-cups-user=daemon --with-cups-group=lp --enable-ssl=yes \
     --enable-pam=yes --disable-ldap --disable-dbus
  make || return 1

  #Wreck out cups library installation
  sed -i -e 's/^DIRS\s=\scups/DIRS = /' Makefile

  make BUILDROOT=${startdir}/pkg install

  #Wreck out some stuff included in libcups
  
  if [ "$CARCH" = "x86_64" ]; then 
    if [ -f $startdir/pkg/usr/lib64 ]; then
	 rm -rf ${startdir}/pkg/usr/lib64/
    fi
  fi
  rm -f ${startdir}/pkg/usr/lib/libcupsimage.so* 
  rm -f ${startdir}/pkg/usr/bin/cups-config
  rm -rf ${startdir}/pkg/usr/include

  # kill the sysv stuff
  rm -rf ${startdir}/pkg/etc/rc*.d
  rm -rf ${startdir}/pkg/etc/init.d
  install -D -m755 ../cups ${startdir}/pkg/etc/rc.d/cups
  install -D -m644 ../cups.logrotate ${startdir}/pkg/etc/logrotate.d/cups
  install -D -m644 ../cups.pam ${startdir}/pkg/etc/pam.d/cups
  
  # fix perms on /var/spool and /etc
  chmod 755 ${startdir}/pkg/var/spool
  chmod 755 ${startdir}/pkg/etc

  # Enable raw printing by default [FS #2447]
  sed -i 's|^#application/octet-stream|application/octet-stream|' ${startdir}/pkg/etc/cups/{mime.types,mime.convs}
 
  # add own cupsd.conf with saner defaults as normal file
  rm ${startdir}/pkg/etc/cups/cupsd.conf
  install -D -m 644 ${startdir}/src/cupsd.conf ${startdir}/pkg/etc/cups/cupsd.conf
  # fix .desktop file
  sed -i 's|^Exec=htmlview http://localhost:631/|Exec=xdg-open http://localhost:631/|g' $startdir/pkg/usr/share/applications/cups.desktop
}
