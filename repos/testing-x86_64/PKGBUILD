# $Id$
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=fluidsynth
pkgver=1.1.2
pkgrel=3
pkgdesc="A real-time software synthesizer based on the SoundFont 2 specifications"
arch=(i686 x86_64)
url="http://www.fluidsynth.org/"
depends=('glib2' 'jack' 'bash')
#depends=('glib2' 'jack' 'dbus-core' 'bash')
#makedepends=('cmake' 'ladspa' 'doxygen')
license=('LGPL')
backup=('etc/conf.d/fluidsynth')
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz
        fluidsynth.conf
        fluidsynthd)
md5sums=('7c95f68d9c9b7f54fa64459eb7850680'
         '16c5f4d4cbdddc6c5fcbd4fd4cc142f1'
         'b296dbfb524c2164f552f68bd9abe2ec')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  #cmake . -DCMAKE_INSTALL_PREFIX=/usr \
  #        -DCMAKE_BUILD_TYPE=Release \
  #        -Denable-ladspa=ON \
  #        -Denable-floats=yes \
  #        -DLIB_SUFFIX=""
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  install -Dm644 "$srcdir/fluidsynth.conf" \
    "$pkgdir/etc/conf.d/fluidsynth"

  install -Dm755 "$srcdir/fluidsynthd" \
    "$pkgdir/etc/rc.d/fluidsynth"
}

# vim:set ts=2 sw=2 et:
