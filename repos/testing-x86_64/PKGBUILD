# Packager: Justin Davis (juster) <jrcd83@gmail.com>
# $Id$

pkgname=perl-ev
pkgver=4.11
pkgrel=2
pkgdesc='perl interface to libev, a high performance full-featured event loop'
arch=(i686 x86_64)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=(perl-common-sense)
url=https://metacpan.org/release/EV
source=("http://search.cpan.org/CPAN/authors/id/M/ML/MLEHMANN/EV-$pkgver.tar.gz")
md5sums=(405c6d74f9dff12918b12560c1a57877)
sha512sums=(6aca3eeb617a313de7947b760558c12ed85af4fa8efd5bb4fd8bc2f271b4556312989aebf77cda4e212fd3b1351c268624aeaf2b1cf700507ff46c0c9dcc9db8)
_dir="$srcdir/EV-$pkgver"

build()
(
  cd "$_dir"
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  /usr/bin/perl Makefile.PL
  make
)

check()
(
  cd "$_dir"
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
)

package()
(
  cd "$_dir"
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
)

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
