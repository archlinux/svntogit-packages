# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Aaron Griffin <aaron@archlinux.org>

pkgname=iputils
pkgver=20101006
_traceroutever=1.4a12
pkgrel=2
pkgdesc="IP Configuration Utilities (and Ping)"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.linuxfoundation.org/en/Net:Iputils"
groups=('base')
depends=('glibc' 'openssl' 'sysfsutils')
conflicts=('netkit-base' 'arping' 'netkit-tftpd' 'traceroute')
replaces=('netkit-base' 'traceroute')
source=(http://www.skbuff.net/${pkgname}/${pkgname}-s${pkgver}.tar.bz2
        ftp://ftp.ee.lbl.gov/traceroute-${_traceroutever}.tar.gz
        'permission-fix.patch'
        'iputils-s20101006-manpages.patch')
options=('!makeflags')
md5sums=('a36c25e9ec17e48be514dc0485e7376c'
         '964d599ef696efccdeebe7721cd4828d'
         '66109966cad06e425bb12a78bab85b70'
         'd02dffac6f16e841921a9fc73bd2d3bd')

build() {
  #build iptuils
  cd "${srcdir}/${pkgname}-s${pkgver}"
  
  # Doc fails to build (see FS#20251)
  # As a temporary workaround, we use these man pages from Fedora & LFS
  patch -Np1 -i ../iputils-s20101006-manpages.patch
  make

  # build traceroute
  # this is combined in this package because it's awkward as a separate package
  cd "${srcdir}/traceroute-${_traceroutever}"
  patch -Np1 -i ../permission-fix.patch
  ./configure --prefix=/usr \
              --sbindir=/bin \
              --mandir=/usr/share/man
  make CFLAGS="${CFLAGS} -Ilinux-include/"
}

package() {
  cd "${srcdir}/${pkgname}-s${pkgver}"
  for i in arping clockdiff rarpd rdisc tftpd tracepath tracepath6; do
    install -D -m755 $i "${pkgdir}/usr/sbin/$i"
  done
  for i in ping ping6 traceroute6; do
    install -D -m4755 $i "${pkgdir}/bin/$i"
  done
 
  install -dm755 "${pkgdir}/usr/share/man/man8"
  install -m644 doc/{arping,clockdiff,ping,rarpd,rdisc,tftpd,tracepath,traceroute6}.8 \
    "${pkgdir}/usr/share/man/man8/"

  cd "${pkgdir}/usr/share/man/man8"
  ln -sf ping.8.gz  ping6.8.gz
  ln -sf tracepath.8.gz tracepath6.8.gz

  cd "${srcdir}/traceroute-${_traceroutever}"
  make DESTDIR="${pkgdir}" install install-man
}
