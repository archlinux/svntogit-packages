# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=p11-kit
pkgver=0.23.17
pkgrel=2
pkgdesc="Provides a way to load and enumerate PKCS#11 modules"
arch=(x86_64)
url="https://p11-glue.freedesktop.org"
license=(BSD)
depends=(glibc libtasn1 libffi systemd)
makedepends=(gtk-doc git meson)
install=p11-kit.install
source=("git+https://github.com/p11-glue/p11-kit?signed#tag=$pkgver"
        0001-meson-Fix-dictionary-syntax.patch
        0002-meson-Install-p11-kit-client-module.patch
        0003-meson-PKCS-11-modules-should-only-export-C_GetFuncti.patch
        0004-meson-Don-t-prefix-p11_user_config.patch
        0005-meson-Build-and-install-man-pages.patch
        0006-meson-Install-p11-kit-server-units.patch
        0007-meson-Add-missing-prefix-to-system-path-defines.patch
        0008-Build-and-install-libnssckbi-p11-kit.so.patch)
sha256sums=('SKIP'
            'e255ae1650eaf236b89038175b9f4799e1ac7dd0820724f96a96be1a1c3d8c51'
            '423fdf1014eb8952cb3d0315c2cd0be164d38674b76a8480b2b8e804af62f4a5'
            '1498bfb9e1570750106a75219313b6de5e1d5377197e26e900864aadb0e5ebf0'
            '147e206d360df72812f989dd8ab630138e3b171e6c1b8c40291b48870c0086e3'
            '1b5ef2e0ad4bd80eee14aed1be9f789ab7df0a27341b79aa09ca39f2d020411a'
            '30b0f6d6ab50566d003bb96ab841203e94064b5d3940e6c7fc7302ca105e70f6'
            '531598f5c94bc83f2a3ebd83e6d2bc42c495f1d999504d695553e8e4b709f307'
            '2605f0de616a75f98459e984b8db2dca8757aac05f03514caa7838ef23c90078')
validpgpkeys=('C0F67099B808FB063E2C81117BFB1108D92765AF'  # Stef Walter
              '462225C3B46F34879FC8496CD605848ED7E69871') # Daiki Ueno

prepare() {
  cd p11-kit

  patch -Np1 -i ../0001-meson-Fix-dictionary-syntax.patch
  patch -Np1 -i ../0002-meson-Install-p11-kit-client-module.patch
  patch -Np1 -i ../0003-meson-PKCS-11-modules-should-only-export-C_GetFuncti.patch
  patch -Np1 -i ../0004-meson-Don-t-prefix-p11_user_config.patch
  patch -Np1 -i ../0005-meson-Build-and-install-man-pages.patch
  patch -Np1 -i ../0006-meson-Install-p11-kit-server-units.patch
  patch -Np1 -i ../0007-meson-Add-missing-prefix-to-system-path-defines.patch

  # Build and install an additional library (libnssckbi-p11-kit.so) which
  # is a copy of p11-kit-trust.so but uses the same label for root certs as
  # libnssckbi.so ("Builtin Object Token" instead of "Default Trust")
  # https://bugs.freedesktop.org/show_bug.cgi?id=66161
  patch -Np1 -i ../0008-Build-and-install-libnssckbi-p11-kit.so.patch
}

build() {
  arch-meson p11-kit build \
    -D gtk_doc=true \
    -D man=true \
    -D trust_paths=/etc/ca-certificates/trust-source:/usr/share/ca-certificates/trust-source
  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 p11-kit/COPYING
  ln -srf "$pkgdir/usr/bin/update-ca-trust" "$pkgdir/usr/lib/p11-kit/trust-extract-compat"
}

# vim:set ts=2 sw=2 et:
