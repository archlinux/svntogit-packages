# $Id$
# Maintainer: Tobias Kieslich <tobias@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>

pkgname=libvorbis
pkgver=1.3.4
pkgrel=1
pkgdesc="Vorbis codec library"
arch=('i686' 'x86_64')
url="http://www.xiph.org/vorbis/"
license=('BSD')
depends=('libogg')
source=(http://downloads.xiph.org/releases/vorbis/${pkgname}-${pkgver}.tar.gz)
md5sums=('8851c593a52d1ef9c526d95174873852')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  #-march=i686 optimizes too much, strip it out
  CFLAGS=${CFLAGS/-march=$CARCH} LIBS+="-lm" ./configure --prefix=/usr --disable-static
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make -j1 check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/$pkgname/LICENSE"
}
