# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=grep
pkgver=2.11
pkgrel=2
pkgdesc="A string search utility"
arch=('i686' 'x86_64')
license=('GPL3')
url="http://www.gnu.org/software/grep/grep.html"
groups=('base')
depends=('glibc' 'pcre' 'sh')
makedepends=('texinfo')
install=${pkgname}.install
source=(ftp://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.xz{,.sig}
        grep-2.11-exclude-dir-crash.patch)
md5sums=('ad9c6dbdeab93e50d2bc380f10ed3643'
         'e4fea5e355973c951f93f48bc86f92fa'
         'e882644cf8c1a4e37da7ba8c0867e70b')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -p1 -i $srcdir/grep-2.11-exclude-dir-crash.patch
  ./configure --prefix=/usr --without-included-regex
  make
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  
  install -dm755 ${pkgdir}/bin
  ln -s /usr/bin/grep ${pkgdir}/bin/grep
}
