# Packager: Justin Davis <jrcd83@gmail.com>
# $Id$

pkgname=perl-guard
pkgver=1.022
pkgrel=4
pkgdesc="safe cleanup blocks"
arch=('i686' 'x86_64')
url="https://metacpan.org/release/Guard"
license=('GPL' 'PerlArtistic')
depends=('perl')
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/M/ML/MLEHMANN/Guard-${pkgver}.tar.gz")
md5sums=('8d1d8b942fd5d0240f47906a6d6fac8c')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor"     \
      PERL_MB_OPT="--installdirs vendor" \
      MODULEBUILDRC=/dev/null

    cd "${srcdir}/Guard-${pkgver}"
    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    cd "${srcdir}/Guard-${pkgver}"
    make test
  )
}

package() {
    cd "${srcdir}/Guard-${pkgver}"
    make DESTDIR="$pkgdir" install
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
