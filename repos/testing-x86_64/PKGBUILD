# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s@arch@gmail.com>
# Contributor: Andreas Wagner <Andreas.Wagner@em.uni-frankfurt.de>

pkgname=s-nail
pkgver=14.8.5
pkgrel=1
pkgdesc='Mail processing system with a command syntax reminiscent of ed'
url='http://sdaoden.users.sourceforge.net/code.html#s-nail'
license=('custom:BSD')
arch=('i686' 'x86_64')
depends=('openssl' 'krb5')
optdepends=('smtp-forwarder: for sending mail')
validpgpkeys=('232C220BCB5690A37BD22FFDEB66022795F382CE')
source=("https://downloads.sourceforge.net/project/${pkgname}/${pkgname}-${pkgver//./_}.tar"{.xz,.asc})
sha1sums=('559244068b1c8086233e8891bb0fef273475afb5' 'SKIP')

groups=('base')
backup=('etc/mail.rc')
replaces=('mailx' 'mailx-heirloom' 'heirloom-mailx')
provides=('mailx' 'mailx-heirloom' 'heirloom-mailx')
conflicts=('mailx' 'mailx-heirloom' 'heirloom-mailx')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	CFLAGS+=" $CPPFLAGS"
	make \
		PREFIX=/usr \
		SYSCONFDIR=/etc \
		LIBEXECDIR=/usr/lib \
		MAILSPOOL=/var/spool/mail \
		SID= NAIL=mail \
		WANT_AUTOCC=0 \
		config

	make build
}

check() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make test
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" packager-install
	ln -sf mail "${pkgdir}"/usr/bin/mailx
	ln -sf mail.1.gz "${pkgdir}"/usr/share/man/man1/mailx.1.gz
	install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
