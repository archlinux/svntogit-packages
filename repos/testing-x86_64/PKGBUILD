# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>

pkgname=geeqie
pkgver=1.6
pkgrel=2
pkgdesc='Lightweight image viewer'
arch=('x86_64')
url="http://www.geeqie.org/"
license=('GPL2')
# https://github.com/BestImageViewer/geeqie#optional-libraries
depends=('exiv2' 'gtk3' 'ffmpegthumbnailer'
         'libdjvu' 'libheif' 'libchamplain')
makedepends=('intltool' 'python' 'librsvg' 'poppler-glib'
             'libwmf' 'libwebp' 'imagemagick'
             'fbida' 'gawk' 'perl-image-exiftool'
             # for the docs
             'doxygen' 'yelp-tools' 'graphviz')
optdepends=('librsvg: SVG rendering'
            'fbida: for jpeg rotation'
            'gawk: to use the geo-decode function'
            'perl-image-exiftool: for the jpeg extraction plugin'
            'poppler-glib: for displaying pdf files'
            'gphoto2: command-line tools for various (plugin) operations'
            'imagemagick: command-line tools for various (plugin) operations')
source=("http://www.geeqie.org/${pkgname}-${pkgver}.tar.xz"{,.asc}
        wayland_fix1.patch::https://github.com/BestImageViewer/geeqie/commit/fa91a270d420a7fcdb38ebb40332d14c4e2f4492.patch
        wayland_fix2.patch::https://github.com/BestImageViewer/geeqie/commit/8768ab3d7588019ea07a440a9cfd12679bc07d65.patch
        #https://github.com/BestImageViewer/geeqie/releases/download/v1.6/${pkgname}-${pkgver}.tar.xz"{,.asc}
)
sha256sums=('48f8a4474454d182353100e43878754b76227f3b8f30cfc258afc9d90a4e1920'
            'SKIP'
            'SKIP'
            'SKIP')
validpgpkeys=('85D4CA42952C949B175362B379D0B06F4E20AF1C') # "Klaus Ethgen <Klaus@Ethgen.de>"

# main repo: http://www.geeqie.org/cgi-bin/gitweb.cgi?p=geeqie.git
# bug tracker: https://github.com/BestImageViewer/geeqie/issues

prepare() {
  cd "${pkgname}-${pkgver}"

  # fix crash on wayland
  #https://github.com/BestImageViewer/geeqie/issues/539
  patch -Np1 -i ../wayland_fix1.patch
  patch -Np1 -i ../wayland_fix2.patch

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package(){
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
