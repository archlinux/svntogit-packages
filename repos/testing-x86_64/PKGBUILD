# Maintainer: Guillaume ALAUX <guillaume@archlinux.org>
# Contributor: Vitaliy Berdinskikh ur6lad[at]i.ua

pkgname=tomcat-native
pkgver=1.1.33
pkgrel=2
pkgdesc='Optional component for Tomcat to use certain native resources for performance, compatibility'
arch=('i686' 'x86_64')
url='http://tomcat.apache.org/native-doc/'
license=('APACHE')
depends=('apr')
makedepends=('java-environment')
options=('!emptydirs')
source=(https://archive.apache.org/dist/tomcat/tomcat-connectors/native/${pkgver}/source/${pkgname}-${pkgver}-src.tar.gz)
sha256sums=('523dde7393c57307eedf4972ebbe19a9e9af6f7699e3b1ef6dabd7a11677866e')

build() {
  cd "${srcdir}/tomcat-native-${pkgver}-src/jni/native"

  ./configure \
    --prefix=/usr \
    --with-java-home=/usr/lib/jvm/default \
    --with-apr=/usr \
    --with-ssl=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}-src/jni/native"

  make DESTDIR="${pkgdir}" install
}
