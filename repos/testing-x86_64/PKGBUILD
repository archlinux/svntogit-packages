# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Arjan Timmerman <arjan@soufly.nl>
# Contributor: Tor Krill

pkgname=autogen
pkgver=5.18.12
pkgrel=3
pkgdesc="A tool designed to simplify the creation and maintenance of programs that contain large amounts of repetitious text"
arch=('x86_64')
url="https://www.gnu.org/software/autogen/"
license=('GPL3')
depends=('guile2.0' 'libxml2')
source=(https://ftp.gnu.org/gnu/${pkgname}/rel${pkgver}/${pkgname}-${pkgver}.tar.xz{,.sig})
validpgpkeys=('44A088E295C3A722C450590EC9EF76DEB74EE762') # Bruce Korb <bkorb@gnu.org>
sha256sums=('be3ba62e883185b6ee8475edae97d7197d701d6b9ad9c3d2df53697110c1bfd8'
            'SKIP')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
} 
