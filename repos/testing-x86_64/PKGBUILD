# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdoctools
pkgver=5.16.0
pkgrel=1
pkgdesc='Documentation generation from docbook'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kdoctools'
license=('LGPL')
depends=('karchive' 'docbook-xsl')
makedepends=('extra-cmake-modules' 'ki18n' 'python' 'perl-uri')
groups=('kf5')
options=('staticlibs')
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
md5sums=('0184c309cca6e2cf8912f7f034534579')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
