# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Kritoke <kritoke@nospam.gmail.com>

pkgname=gnome-nettool
pkgver=3.8.1+144+gdd04271
pkgrel=1
pkgdesc="Graphical interface for various networking tools"
arch=(i686 x86_64)
license=(GPL)
depends=(gtk3 libgtop dnsutils iputils dconf whois)
makedepends=(yelp-tools intltool nmap net-tools git gnome-common)
optdepends=('nmap: Port scanning'
            'net-tools: Network statistics')
groups=(gnome-extra)
url="https://git.gnome.org/browse/gnome-nettool"
_commit=dd04271f17440ceb130995d72fed6478158f9d50  # master
source=("git+https://git.gnome.org/browse/gnome-nettool#commit=$_commit")
sha256sums=('SKIP')

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

check() {
  cd $pkgname
  make check
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-schemas-compile
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
