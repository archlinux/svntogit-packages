# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgname=pinentry
pkgver=0.9.6
pkgrel=2
pkgdesc="a collection of simple PIN or passphrase entry dialogs which utilize the Assuan protocol"
arch=('i686' 'x86_64')
license=('GPL')
url="http://gnupg.org/related_software/pinentry/"
depends=('ncurses' 'libcap>=2.16' 'libassuan')
makedepends=('gtk2' 'qt5-base' 'gcr')
optdepends=('gtk2: gtk2 backend'
            'qt5-base: qt backend'
            'gcr: gnome3 backend')
source=("ftp://ftp.gnupg.org/gcrypt/$pkgname/$pkgname-$pkgver.tar.bz2"{,.sig})
sha1sums=('296992d7b26ba2a517b093ccae26bc00b88b3bb5' 'SKIP')
validpgpkeys=('D8692123C4065DEA5E0F3AB5249B39D24F25E3B6') # Werner Koch

install=install

build() {
	cd $srcdir/$pkgname-$pkgver

	# https://bugs.gnupg.org/gnupg/issue1961
	CXXFLAGS+=' -std=c++11'

	./configure --prefix=/usr \
		--enable-pinentry-curses \
		--enable-fallback-curses \
		--enable-pinentry-tty \
		--enable-pinentry-emacs \
		--enable-pinentry-gtk2 \
		--enable-pinentry-gnome3 \
		--enable-pinentry-qt \

	make
}

package() {
	cd $srcdir/$pkgname-$pkgver
	make DESTDIR=${pkgdir} install
}
