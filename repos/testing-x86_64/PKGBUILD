# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=gnome-online-miners
pkgver=3.20.0
pkgrel=1
pkgdesc="Tracker miners for online accounts"
arch=(i686 x86_64)
url="http://www.gnome.org"
license=(GPL)
depends=(libgdata gnome-online-accounts grilo libzapojit tracker gfbgraph dleyna-server)
makedepends=('python')
source=(http://download.gnome.org/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('f46dac7743283385d2aeea588eeead216274d9f365e323b90f586de982336e36')
 
build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --libexecdir=/usr/lib/$pkgname --disable-static
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
