# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>

pkgname=libmpc
pkgver=1.0.2
_pkgver=1.0.1   # upstream stuff-up...
pkgrel=1
pkgdesc="Library for the arithmetic of complex numbers with arbitrarily high precision"
arch=('i686' 'x86_64')
url="http://www.multiprecision.org/"
license=('LGPL')
depends=('mpfr')
install=libmpc.install
source=(http://www.multiprecision.org/mpc/download/mpc-${pkgver/_/-}.tar.gz{,.sig})
md5sums=('b32a2e1a3daa392372fbd586d1ed3679'
         'SKIP')

build() {
  cd "${srcdir}/mpc-${_pkgver}"
  ./configure --prefix=/usr
  make
}

check() {
  cd "${srcdir}/mpc-${_pkgver}"
  make check
}

package() {
  cd "${srcdir}/mpc-${_pkgver}"
  make DESTDIR="${pkgdir}" install
  mv ${pkgdir}/usr/share/info/{mpc,libmpc}.info
}
