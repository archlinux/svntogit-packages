# $Id$
# Maintainer: Dan McGee <dan@archlinux.org>
# Maintainer: Allan McRae <allan@archlinux.org>

pkgname=valgrind
pkgver=3.9.0
pkgrel=7
pkgdesc='A tool to help find memory-management problems in programs'
arch=(i686 x86_64)
license=(GPL)
url='http://valgrind.org/'
depends=('glibc>=2.20' 'glibc<2.21' 'perl')
makedepends=('gdb' 'openmpi')
optdepends=('openmpi: MPI support')
options=('staticlibs' '!emptydirs')
source=(http://valgrind.org/downloads/$pkgname-$pkgver.tar.bz2
        valgrind-3.9.0-glibc-2.20.patch
        add_xabort_op.patch
        add_bnd_prefix_support.patch)
sha1sums=('9415e28933de9d6687f993c4bb797e6bd49583f1'
          '2080c3d44de20857f18b03a93404a8f3d3721f7e'
          '34e90bb5e6355fea796952de7cb270b49567874d'
          '074b60ab54ed9ff502660a9b544ff31303358a29')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i "$srcdir"/valgrind-3.9.0-glibc-2.20.patch
  patch -p0 -i "$srcdir"/add_xabort_op.patch  # FS#41696
  patch -p0 -i "$srcdir"/add_bnd_prefix_support.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --mandir=/usr/share/man --with-mpicc=mpicc
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
