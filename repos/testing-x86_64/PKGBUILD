# $Id$
# Maintainer: 

pkgname=gettext
pkgver=0.18.1.1
pkgrel=1
pkgdesc="GNU internationalization library"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/gettext"
license=('GPL')
groups=('base')
depends=('gcc-libs' 'acl' 'sh' 'glib2')
optdepends=('cvs: for autopoint tool')
options=(!libtool)
install=gettext.install
source=(ftp://ftp.gnu.org/pub/gnu/gettext/${pkgname}-${pkgver}.tar.gz)
md5sums=('3dd55b952826d2b32f51308f2f91aa89')

build() {
  cd $srcdir/${pkgname}-${pkgver}
  sed -i -e 's/libexpat.so.0/libexpat.so.1/' gettext-tools/src/x-glade.c
  ./configure --prefix=/usr --enable-csharp
  make || return 1
}

package() {
  cd $srcdir/${pkgname}-${pkgver}
  make DESTDIR=$pkgdir install
}
