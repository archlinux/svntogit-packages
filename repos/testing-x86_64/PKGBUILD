# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdesignerplugin
pkgver=5.85.0
pkgrel=1
pkgdesc='Integration of Frameworks widgets in Qt Designer/Creator'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(kconfig kcoreaddons)
makedepends=(extra-cmake-modules qt5-tools kdoctools)
groups=(kf5-aids)
source=(https://download.kde.org/stable/frameworks/${pkgver%.*}/portingAids/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('8ed96e2927d5b954ceb48c5f1d69e94d4439af987f5911489b55e4e3eb76905d'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
