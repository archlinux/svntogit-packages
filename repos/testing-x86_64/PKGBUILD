# $Id$
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>
pkgname=acl
pkgver=2.2.51
pkgrel=1
pkgdesc="Access control list utilities, libraries and headers"
arch=('i686' 'x86_64')
url="http://savannah.nongnu.org/projects/acl"
license=('LGPL')
depends=('attr>=2.4.46')
replaces=('xfsacl')
provides=('xfsacl')
conflicts=('xfsacl')
options=('!libtool')
source=(http://download.savannah.gnu.org/releases/${pkgname}/${pkgname}-${pkgver}.src.tar.gz)
sha256sums=('06854521cf5d396801af7e54b9636680edf8064355e51c07657ec7442a185225')

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}

  export INSTALL_USER=root INSTALL_GROUP=root 
  ./configure --prefix=/usr --libdir=/lib --libexecdir=/usr/lib
  make 
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DIST_ROOT="${pkgdir}" install install-lib install-dev

  # tidy up
  rm -f ${pkgdir}/lib/libacl.a
  chmod 0755 ${pkgdir}/lib/libacl.so.*.*.*
}
