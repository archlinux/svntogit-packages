# $Id$
# Maintainer: Daniel Isenmann <daniel [at] archlinux.org>
# Contributor: dorphell <dorphell@gmx.net>

pkgname=gc
pkgver=7.4.2
pkgrel=3
pkgdesc="A garbage collector for C and C++"
arch=('i686' 'x86_64')
url="http://www.hboehm.info/gc/"
license=('GPL')
depends=('gcc-libs' 'libatomic_ops')
source=("http://www.hboehm.info/gc/gc_source/${pkgname}-${pkgver}.tar.gz"
        noelision.patch
		gc-7.4.2-Export-GC-push-all-eager.patch)
md5sums=('12c05fd2811d989341d8c6d81f66af87'
         '0be354bf60cc18363dc4c1601697717b'
         '26984d81b4ab2b0281583a5442c26863')

prepare() {
  cd ${pkgname}-${pkgver}

  # https://bugs.archlinux.org/task/39631
  # https://github.com/ivmai/bdwgc/pull/51
  patch -p1 -i ../noelision.patch

  patch -p1 -i ../gc-7.4.2-Export-GC-push-all-eager.patch

  sed -i 's#pkgdata#doc#' doc/doc.am
}

build() {
  cd ${pkgname}-${pkgver}
  autoreconf -fi
  ./configure --prefix=/usr --enable-cplusplus --disable-static
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  sed -i -e 's/GC_MALLOC 1L/gc 3/' doc/gc.man
  install -m755 -d "${pkgdir}/usr/share/man/man3"
  install -m644 doc/gc.man "${pkgdir}/usr/share/man/man3/gc.3"
}
