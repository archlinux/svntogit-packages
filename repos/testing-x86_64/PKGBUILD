# $Id: PKGBUILD 24494 2009-01-17 20:42:01Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libproxy
pkgver=0.4.6
pkgrel=2
pkgdesc="A library that provides automatic proxy configuration management"
arch=(i686 x86_64)
license=('LGPL')
depends=('gcc-libs')
optdepends=('gconf: GNOME configuration module'
            'kdelibs: KDE configuration module'
            'networkmanager: NetworkManager configuration module'
            'libwebkit: WPAD/PAC parsing via WebKit JavaScript engine'
            'xulrunner: WPAD/PAC parsing via Mozilla JavaScript engine'
            'perl: Perl bindings'
            'python2: Python bindings')
makedepends=('xulrunner' 'gconf' 'cmake' 'networkmanager' 'libwebkit' 'automoc4' 'python2' 'kdelibs' 'perl')
url="http://libproxy.googlecode.com"
source=(http://libproxy.googlecode.com/files/${pkgname}-${pkgver}.tar.gz)
options=('!libtool')
md5sums=('199c6b120baf1f7258a55f38d5ec74f5')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  mkdir build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIBEXEC_INSTALL_DIR=/usr/lib/libproxy \
    -DCMAKE_SKIP_RPATH=ON \
    -DPERL_VENDORINSTALL=yes \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_CXX_FLAGS="${CXXFLAGS}" \
    -DCMAKE_C_FLAGS="${CFLAGS}" \
    ..
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/build"
  make DESTDIR="${pkgdir}" install
}
