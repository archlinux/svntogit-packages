# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Patryk Kowalczyk < patryk at kowalczyk dot ws>

pkgname=spice
pkgver=0.14.2
pkgrel=1
pkgdesc="SPICE server"
arch=('x86_64')
url="https://www.spice-space.org"
license=('LGPL2.1')
depends=(celt0.5.1 libjpeg-turbo libsasl pixman glib2 opus lz4)
makedepends=(python2-pyparsing python2-six qemu spice-protocol git libcacard)
source=(https://www.spice-space.org/download/releases/spice-server/spice-$pkgver.tar.bz2{,.sign})
sha256sums=('b203b3882e06f4c7249a3150d90c84e1a90490d41ead255a3d2cede46f4a29a7'
            'SKIP')
validpgpkeys=(206D3B352F566F3B0E6572E997D9123DE37A484F) # Victor Toso de Carvalho <me@victortoso.com>

build() {
  cd spice-$pkgver
  PYTHON=python2 ./configure --prefix=/usr --disable-static --enable-smartcard --enable-client --enable-celt051 --disable-werror
  make
}

check() {
  cd spice-$pkgver
  make check
}

package() {
  cd spice-$pkgver
  make DESTDIR="$pkgdir/" install
}
