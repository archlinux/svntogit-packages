# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=libkgapi
pkgver=17.04.0
pkgrel=3
pkgdesc="A KDE-based library for accessing various Google services via their public API"
url='https://www.kde.org/'
arch=(i686 x86_64)
license=(GPL LGPL FDL)
depends=(kcalcore kcontacts qt5-webengine)
makedepends=(extra-cmake-modules kdoctools qt5-tools kdesignerplugin python)
source=("https://download.kde.org/unstable/applications/${pkgver}/src/${pkgname}-${pkgver}.tar.xz"{,.sig}
        libkgapi-fix-auth.patch::"https://cgit.kde.org/libkgapi.git/patch/?id=68b89bce")
sha256sums=('8cc057ad5f88dee2a218b3a3aaf09720312a3fbb9a4a06b2aeb6706818557c54'
            'SKIP'
            '5903158fefff99bc11284de3653c23b32239b99e93d34b75d9fe6c61cb6a138e')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build

# Fix authentication
  cd $pkgname-$pkgver
  patch -p1 -i ../libkgapi-fix-auth.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
