# $Id$
# Maintainer: Dan McGee <dan@archlinux.org>
# Maintainer: Allan McRae <allan@archlinux.org>

pkgname=valgrind
pkgver=3.9.0
pkgrel=1
pkgdesc="A tool to help find memory-management problems in programs"
arch=('i686' 'x86_64')
license=('GPL')
url="http://valgrind.org/"
depends=('glibc>=2.18' 'glibc<2.19' 'perl')
makedepends=('gdb')
options=('staticlibs' '!emptydirs')
source=(http://valgrind.org/downloads/${pkgname}-${pkgver}.tar.bz2)
md5sums=('0947de8112f946b9ce64764af7be6df2')


build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  if [[ ${CARCH} = "x86_64" ]]; then
    ./configure --prefix=/usr --mandir=/usr/share/man --enable-only64bit
  else
    ./configure --prefix=/usr --mandir=/usr/share/man
  fi

  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
