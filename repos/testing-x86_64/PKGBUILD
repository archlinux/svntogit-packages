# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Thomas Arnhold <thomas at arnhold dot org>

pkgname=liborcus
pkgver=0.16.1
pkgrel=3
pkgdesc="File import filter library for spreadsheet documents."
arch=('x86_64')
url="https://gitlab.com/orcus/orcus/blob/master/README.md"
license=('MPL')
depends=('libixion')
makedepends=('boost' 'mdds')
source=(https://kohei.us/files/orcus/src/${pkgname}-${pkgver}.tar.xz)
# https://gitlab.com/orcus/orcus/-/releases
sha256sums=('a4b26b320d00bdfc769c3a03ed22bd5ad7e54a93dc6b1d00cd949f8f3519faae')
#sha256sums=('b02173c1787373fa728fdaad1b9658184dbcefefcc23c3afcf5400571282e512')

build() {
    cd "${pkgname}"-${pkgver}
    ./configure --prefix=/usr
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
    make
}

check() {
    cd "${pkgname}"-${pkgver}
    make check
}

package() {
    cd "${pkgname}"-${pkgver}
    make DESTDIR="$pkgdir" install
}
