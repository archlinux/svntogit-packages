# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=pappl
#https://github.com/michaelrsweet/pappl/commits/master
#_commit=0985256fb631ddd9b528795a0635af2b20828ddb # master 2020-11-30 = 1.0rc1
pkgver=1.0rc1
pkgrel=1
pkgdesc="a simple C-based framework/library for developing CUPS Printer Applications"
arch=('x86_64')
url="https://www.msweet.org/pappl/"
license=('Apache' 'custom')
depends=('libcups' 'libjpeg-turbo' 'pam')
makedepends=('git')
source=(#git+"https://github.com/michaelrsweet/pappl.git#commit=$_commit"
        https://github.com/michaelrsweet/pappl/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz{,.sig})
sha256sums=('b50bdd54f95cfad4faca2a3984a4d21f54272ff29c7854c673d1f3088a3a4228'
            'SKIP')
validpgpkeys=('845464660B686AAB36540B6F999559A027815955') # "Michael R Sweet <michael.r.sweet@gmail.com>"

#pkgver() {
#  cd $pkgname
#  git describe --tags | sed 's/-/+/g' | sed 's/v//'
#}

build() {
  cd "$pkgname-$pkgver"

  # The build system uses only DSOFLAGS but not LDFLAGS to build some libraries.
  export DSOFLAGS=${LDFLAGS}

  ./configure --prefix=/usr
  make
}

check() {
  cd "$pkgname-$pkgver"
  #make check

  # there's currently  no real test suite, 
  # the tool is only useful outside the fakeroot envireonment - when packaging it crashes:
  # testpappl: client.c:771: avahi_client_get_host_name_fqdn: Assertion `client' failed.
  # /startdir/PKGBUILD: line 43:  1878 Aborted                 (core dumped) ./testsuite/testpappl

  # run it later outside fakeroot!
  #./testsuite/testpappl 
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install

  # install the testing tool
  install -Dm755 testsuite/testpappl -t "$pkgdir"/usr/bin
  # install docs
  install -Dm644 doc/* -t "$pkgdir"/usr/share/doc/$pkgname
  # add license + exception
  install -m644 -Dt "${pkgdir}/usr/share/licenses/${pkgname}" {LICENSE,NOTICE}
}
