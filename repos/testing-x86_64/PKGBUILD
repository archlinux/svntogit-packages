# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=incidenceeditor
pkgver=22.04.0
pkgrel=1
pkgdesc='KDE PIM incidence editor'
arch=(x86_64)
url='https://kontact.kde.org'
license=(LGPL)
depends=(eventviews)
makedepends=(extra-cmake-modules qt5-tools doxygen)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('d11c61f2e27170254fb55273c3ebeed04f213ce5ec2fb6f5df3a3e6fae97e9a6'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
options=(debug)

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
