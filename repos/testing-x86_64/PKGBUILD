# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgname=rpcbind
pkgver=1.2.6
pkgrel=2
pkgdesc="portmap replacement which supports RPC over various protocols"
arch=(x86_64)
backup=('etc/conf.d/rpcbind')
makedepends=('systemd')
depends=('bash' 'glibc' 'libtirpc' 'systemd-libs')
url="http://rpcbind.sourceforge.net"
license=('custom')
replaces=('portmap')
# see also http://git.linux-nfs.org/?p=steved/rpcbind.git;a=summary
source=(https://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.bz2
        rpcbind-sunrpc.patch
        rpcbind.conf
        systemd_service.diff
        tmpfiles.d
        sysusers.d
        CVE-2017-8779.diff)
sha1sums=('a6dad1d9454f42f2f25a92caba8d3d7bddf527e3'
          '1b997ce76f9727efc8c72fc5f97189591c9a60e2'
          'ce58b8fccf0440aa81eeb21c99ef7dd654f03b7f'
          '5baeed59090b3bfa08c0852333e061194efcc4e7'
          'eb8b7375a4ca31b3f61fa726f387884d5e4ca7a3'
          '97fed152572790a44e972114cbb436ca517254f9'
          '38044ce8f3376a0b69da8e078e988fd1645bd435')

prepare() {
  cd $pkgname-$pkgver
  # patch for iana services file # FS#20273
  patch -Np1 -i ../rpcbind-sunrpc.patch
  # use distro config file; FS#52058
  patch -Np1 -i ../systemd_service.diff
  # http://seclists.org/oss-sec/2017/q2/209
  patch -Np1 -i ../CVE-2017-8779.diff
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sbindir=/usr/bin \
    --with-rpcuser=rpc \
    --enable-warmstarts \
    --with-statedir=/var/lib/rpcbind \
    --enable-rmtcalls
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  # install rpcbind config file
  install -D -m644 "$srcdir"/rpcbind.conf "$pkgdir"/etc/conf.d/rpcbind
  # install missing man page - https://bugs.archlinux.org/task/21271
  install -m644 man/rpcinfo.8 "$pkgdir"/usr/share/man/man8/
  # systemd sysusers/tempfile
  install -Dm644 ../tmpfiles.d "${pkgdir}"/usr/lib/tmpfiles.d/rpcbind.conf
  install -Dm644 ../sysusers.d "${pkgdir}"/usr/lib/sysusers.d/rpcbind.conf
  # install license
  install -D -m644 COPYING "$pkgdir"/usr/share/licenses/rpcbind/COPYING
}
