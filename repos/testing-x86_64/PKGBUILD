# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
pkgname=gdb
pkgver=6.8
pkgrel=3
pkgdesc="The GNU Debugger"
arch=(i686 x86_64)
url="http://sources.redhat.com/gdb/"
depends=('ncurses' 'expat')
options=('!libtool')
license=('GPL3')
install=gdb.install
source=(http://ftp.gnu.org/gnu/gdb/${pkgname}-${pkgver}.tar.bz2)
md5sums=('c9da266b884fb8fa54df786dfaadbc7a')

build() {
  cd ${startdir}/src/${pkgname}-${pkgver}
  ./configure --prefix=/usr --disable-nls --mandir=/usr/share/man \
      --infodir=/usr/share/info
  make || return 1
  make DESTDIR=${startdir}/pkg install || return 1
  rm -f ${pkgdir}/usr/share/info/dir
  # resolve conflicts with binutils
  rm -f ${startdir}/pkg/usr/include/{bfd,dis-asm,symcat,ansidecl,bfdlink}.h
  rm -f ${startdir}/pkg/usr/lib/{libiberty,libbfd,libopcodes}.a
  rm -f ${startdir}/pkg/usr/share/info/{bfd.info,configure.info,standards.info}
}
