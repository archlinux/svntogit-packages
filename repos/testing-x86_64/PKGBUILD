# $Id$
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=at-spi2-atk
pkgver=2.26.0
pkgrel=2
pkgdesc="A GTK+ module that bridges ATK to D-Bus at-spi"
url="https://wiki.gnome.org/Accessibility"
arch=(i686 x86_64)
license=(GPL2)
depends=(at-spi2-core libx11 libxml2 atk)
makedepends=(git meson)
_commit=6c1c9f343fda51780fc12ed1a1746832f070db85  # tags/AT_SPI2_ATK_2_26_0^0
source=("git+https://git.gnome.org/browse/at-spi2-atk#commit=$_commit"
        0001-meson-Ensure-paths-written-to-.pc-are-absolute.patch)
sha256sums=('SKIP'
            'a7aea6262a4159e5b50134171e52115688e4e2c90ff712f86031d9f9dab348be')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^AT_SPI2_ATK_//;s/_/./g;s/-/+/g'
}

prepare() {
  mkdir build
  cd $pkgname
  patch -Np1 -i ../0001-meson-Ensure-paths-written-to-.pc-are-absolute.patch
}

build() {
  cd build
  meson setup --prefix=/usr --buildtype=release ../$pkgname \
    --sysconfdir=/etc
  ninja
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}

# vim:set ts=2 sw=2 et:
