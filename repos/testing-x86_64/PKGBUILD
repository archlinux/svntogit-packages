# $Id$
# Maintainer: Tom Gundersen <teg@jklm.no>
# Contributor: Angel Velasquez <angvp@archlinux.org> 
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Aaron Griffin <aaron@archlinux.org>
# Contributor: Jochem Kossen <j.kossen@home.nl>

pkgname=lsof
pkgver=4.87
pkgrel=2
pkgdesc="Lists open files for running Unix processes"
arch=('i686' 'x86_64')
url="http://people.freebsd.org/~abe/"
license=('custom')
depends=('glibc')
source=(ftp://sunsite.ualberta.ca/pub/Mirror/lsof/${pkgname}_${pkgver}.tar.bz2 license.txt)

build() {
	cd ${srcdir}/${pkgname}_${pkgver}
	tar xf ${pkgname}_${pkgver}_src.tar
	cd ${pkgname}_${pkgver}_src
	sed -i 's|/\* #define\tHASSECURITY\t1 \*/|#define\tHASSECURITY\t1|' dialects/linux/machine.h
	./Configure -n linux
	make 
}

package() {
	cd ${srcdir}/${pkgname}_${pkgver}/${pkgname}_${pkgver}_src

    FILES=("00CREDITS" "00DCACHE" "00DIALECTS" "00DIST" "00FAQ" "00LSOF-L" "00MANIFEST" "00PORTING" "00QUICKSTART" "00README" "00.README.FIRST" "00TEST" "00XCONFIG") 
    for file in "${FILES[@]}"
    do
        install -D -m0644 -o root -g root $file \
        $pkgdir/usr/share/doc/$pkgname/$file
    done

	install -Dm0755 -o root -g root lsof ${pkgdir}/usr/bin/lsof 
	install -Dm0644 -o root -g root lsof.8 ${pkgdir}/usr/share/man/man8/lsof.8 
   
	install -D -m0644 -o root -g root ${srcdir}/license.txt \
	     ${pkgdir}/usr/share/licenses/lsof/LICENSE 
}
md5sums=('80e2a76d0e05826db910ec88e631296c'
         '1b63c76bd10437cabf890508c8e58d36')
