# $Id$
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>

pkgname=wpa_supplicant
pkgver=2.3
pkgrel=1
epoch=1
pkgdesc="A utility providing key negotiation for WPA wireless networks"
url="http://hostap.epitest.fi/wpa_supplicant"
arch=('i686' 'x86_64')
depends=('openssl' 'libdbus' 'readline' 'libnl')
optdepends=('wpa_supplicant_gui: wpa_gui program')
license=('GPL')
backup=('etc/wpa_supplicant/wpa_supplicant.conf')
source=("http://w1.fi/releases/${pkgname}-${pkgver}.tar.gz"
	0001-P2P-Validate-SSID-element-length-before-copying-it-C.patch
	config)
sha256sums=('eaaa5bf3055270e521b2dff64f2d203ec8040f71958b8588269a82c00c9d7b6a'
            'a026c2ed090fedb73187adce9a122345c3b0ac40cc67e6674b92fbe6a08cbbd1'
            '15be07f0d8003a2239dfba008f09602f1203bbf143004d8979ff4d863153c817')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}/${pkgname}/"
  cp "${srcdir}/config" ./.config

  # https://bugs.archlinux.org/task/44695
  patch -d .. -Np1 -i ../0001-P2P-Validate-SSID-element-length-before-copying-it-C.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}/${pkgname}"

  # The Makefile does not pick up our CPPFLAGS
  export CFLAGS="$CPPFLAGS $CFLAGS"
  make LIBDIR=/usr/lib BINDIR=/usr/bin
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/${pkgname}"
  make LIBDIR=/usr/lib BINDIR=/usr/bin DESTDIR="${pkgdir}" install

  install -d -m755 "${pkgdir}/etc/wpa_supplicant"
  install -m644 wpa_supplicant.conf "${pkgdir}/etc/wpa_supplicant/wpa_supplicant.conf"

  install -d -m755 "${pkgdir}/usr/share/man/man"{5,8}
  install -m644 doc/docbook/*.5 "${pkgdir}/usr/share/man/man5/"
  install -m644 doc/docbook/*.8 "${pkgdir}/usr/share/man/man8/"
  rm -f "${pkgdir}/usr/share/man/man8/wpa_"{priv,gui}.8

  install -d -m755 "${pkgdir}/usr/share/dbus-1/system-services"
  install -m644 dbus/{fi.epitest.hostap.WPASupplicant.service,fi.w1.wpa_supplicant1.service} "${pkgdir}/usr/share/dbus-1/system-services/"

  install -d -m755 "${pkgdir}/etc/dbus-1/system.d"
  install -m644 dbus/dbus-wpa_supplicant.conf "${pkgdir}/etc/dbus-1/system.d/wpa_supplicant.conf"

  install -d -m755 "${pkgdir}/usr/lib/systemd/system"
  install -m644 systemd/*.service "${pkgdir}/usr/lib/systemd/system/"
}
