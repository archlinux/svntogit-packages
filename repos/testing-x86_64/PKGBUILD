# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgbase=gutenprint
pkgname=('gutenprint' 'foomatic-db-gutenprint-ppds')
pkgver=5.3.1
pkgrel=1
pkgdesc="Top quality printer drivers for POSIX systems"
arch=('x86_64')
license=('GPL')
makedepends=('gimp' 'gtk2' 'cups' 'ghostscript'
            # for the docs
            'dialog' 'doxygen' 'docbook-utils' 'texi2html' 'texlive-bin')
source=(http://downloads.sourceforge.net/gimp-print/$pkgname-$pkgver.tar.xz)
url="http://gimp-print.sourceforge.net/"
options=('!emptydirs')
sha1sums=('f7e03d5c7070035ef0acba62973f57bbc505ef68')

prepare(){
  cd ${pkgbase}-${pkgver}
  # sbindir is not properly taken over there
  sed -i "s:cups_sbindir=\"\${cups_exec_prefix}\/sbin\":cups_sbindir=\"\${cups_exec_prefix}\/bin\":" m4/stp_cups.m4
  sed -i "s:m4local:m4extra:" Makefile.am
  autoreconf -vfi
}

build() {
  cd ${pkgbase}-${pkgver}
  ./configure --prefix=/usr \
    --sbindir=/usr/bin \
    --disable-rpath \
    --enable-samples \
    --disable-static \
    --disable-static-genppd \
    --enable-cups-ppds \
    --enable-simplified-cups-ppds=only \
    --enable-translated-cups-ppds \
    --enable-globalized-cups-ppds #--help

  # globalized ppds -> put all translations into one ppd file
  # enable translated cups ppds grows size 9,5MB -> 156MB
  # simplified cups ppds (yes, no, only) - only offer basic paper sizes, not all options 
  # 4,3MB (only), 17MB (both) -> translated 41MB simpl., 165MB, 231MB both
	
  make
}

package_gutenprint() {
  pkgdesc="Top quality printer drivers for POSIX systems"
  install=gutenprint.install
  depends=('cups' 'cups-filters')
  optdepends=('gimp:	adds gutenprint plugin to gimp'
            'libusb:	required for drivers that depend on gutenprint52usb backend'
            'foomatic-db-gutenprint-ppds: prebuilt ppd files')
  replaces=('gimp-print')
  options=('!emptydirs')
  
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  
  # split out files
  mkdir $srcdir/tmp_foomatic-db-gutenprint-ppds
  
  mv ${pkgdir}/usr/share/cups/model $srcdir/tmp_foomatic-db-gutenprint-ppds/

  # cleanup
  rm -rf ${pkgdir}/usr/share/foomatic 
  rm -rf ${pkgdir}/etc
}

package_foomatic-db-gutenprint-ppds() {
  pkgdesc="simplified prebuilt ppd files"
  optdepends=('cups:	to use cups printer spooler(recommended)')
  
  cd ${pkgbase}-${pkgver}
  mkdir -p ${pkgdir}/usr/share/cups
  mv $srcdir/tmp_foomatic-db-gutenprint-ppds/* ${pkgdir}//usr/share/cups/
}
