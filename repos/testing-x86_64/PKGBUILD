# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=modemmanager
_realname=ModemManager
pkgver=1.0.0
pkgrel=2
pkgdesc="Mobile broadband modem management service"
arch=(i686 x86_64)
url="http://www.freedesktop.org/wiki/Software/ModemManager/"
license=(GPL2)
depends=(udev ppp libqmi libmbim polkit hicolor-icon-theme)
makedepends=(intltool gtk-doc)
optdepends=('usb_modeswitch: install if your modem shows up as a storage drive')
install=modemmanager.install
source=(http://www.freedesktop.org/software/$_realname/$_realname-$pkgver.tar.xz)
sha256sums=('e0f3bbd9a7353f7d87be2391b8718a81d11f9ff8b1c067d640928322f8808563')

build() {
  cd $_realname-$pkgver
  ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --sbindir=/usr/bin \
        --with-udev-base-dir=/usr/lib/udev \
        --disable-static \
        --with-polkit=permissive
  make
}

package() {
  cd $_realname-$pkgver
  make DESTDIR="$pkgdir" install
}
