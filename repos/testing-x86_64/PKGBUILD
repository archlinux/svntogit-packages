# Maintainer:  Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Thomas Bächler <thomas@archlinux.org>

pkgname=libnl
pkgver=3.6.0
pkgrel=1
pkgdesc='Library for applications dealing with netlink sockets'
arch=('x86_64')
url='https://github.com/thom311/libnl/'
license=('GPL')
depends=('glibc')
backup=('etc/libnl/classid'
        ' etc/libnl/pktloc')
validpgpkeys=('49EA7C670E0850E7419514F629C2366E4DFC5728') # Thomas Haller
source=("https://github.com/thom311/libnl/releases/download/libnl${pkgver//./_}/${pkgname}-${pkgver}.tar.gz"{,.sig})
sha256sums=('532155fd011e5a805bd67121b87a01c757e2bb24112ac17e69cb86013b970009'
            'SKIP')

build() {
  cd $pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --disable-static
  make
}

check() {
  make -C $pkgname-$pkgver check
}

package() {
  make -C $pkgname-$pkgver DESTDIR="$pkgdir" install
}
