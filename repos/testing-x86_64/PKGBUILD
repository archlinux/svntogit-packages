# $Id$
# Maintainer: Dan McGee <dan@archlinux.org>

pkgname=libarchive
pkgver=3.0.4
pkgrel=1
pkgdesc="library that can create and read several streaming archive formats"
arch=('i686' 'x86_64')
url="http://libarchive.googlecode.com/"
license=('BSD')
depends=('zlib' 'bzip2' 'xz>=5.0.0' 'acl' 'openssl>=1.0.0' 'expat')
source=("https://github.com/downloads/libarchive/libarchive/libarchive-${pkgver}.tar.gz")
md5sums=('af443ca9a10ddbcbf00f7ae34ca7fc16')
sha256sums=('76e8d7c7b100ec4071e48c1b7d3f3ea1d22b39db3e45b7189f75b5ff4df90fac')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --without-xml2
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"

  make check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  install -D -m644 COPYING "$pkgdir"/usr/share/licenses/libarchive/COPYING
}
