# Maintainer:
# Contributor: Stanislaw Datskevich <me a nek0 net>
# Contributor: Gregor Robinson <gregor@fiatflux.co.uk>
# Contributor: Martin Perner <martin dot perner at gmail dot com>

pkgname=pkcs11-helper
pkgver=1.26.0
pkgrel=2
pkgdesc="A library that simplifies the interaction with PKCS11 providers for end-user applications using a simple API and optional OpenSSL engine"
arch=('x86_64')
url='https://github.com/OpenSC/pkcs11-helper'
license=(GPL BSD)
depends=('gnutls' 'nss' 'openssl')
provides=('libpkcs11-helper.so')
source=("https://github.com/OpenSC/${pkgname}/releases/download/${pkgname}-${pkgver%.0}/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('e886ec3ad17667a3694b11a71317c584839562f74b29c609d54c002973b387be')

build() {
  cd pkcs11-helper-$pkgver
  libtoolize
  aclocal
  autoheader
  automake --add-missing
  autoreconf -v
  ./configure --prefix=/usr
  make
}

package() {
  cd pkcs11-helper-$pkgver
  make DESTDIR="$pkgdir" install
  mkdir -p "$pkgdir"/usr/share/licenses/$pkgname/
  install -D -m0644 COPYING* "$pkgdir"/usr/share/licenses/$pkgname/
}

