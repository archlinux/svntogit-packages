# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=popt
pkgver=1.18
pkgrel=3
pkgdesc="A commandline option parser"
arch=('x86_64')
url="https://github.com/rpm-software-management/popt"
license=('custom')
depends=('glibc')
options=('staticlibs' 'debug')
source=(https://ftp.osuosl.org/pub/rpm/popt/releases/popt-1.x/popt-${pkgver}.tar.gz)
sha256sums=('5159bc03a20b28ce363aa96765f37df99ea4d8850b1ece17d1e6ad5c24fdc5d1')

build() {
  cd "${pkgname}-${pkgver}"
  # Avoid stripping issue
  CFLAGS+=" -ffat-lto-objects" ./configure --prefix=/usr
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
