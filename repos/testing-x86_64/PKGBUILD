# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=mpdecimal
pkgver=2.4.2
pkgrel=2
pkgdesc="Package for correctly-rounded arbitrary precision decimal floating point arithmetic"
arch=('x86_64')
url="http://www.bytereef.org/mpdecimal/index.html"
license=('custom')
depends=('glibc')
source=("http://www.bytereef.org/software/$pkgname/releases/$pkgname-$pkgver.tar.gz")
sha512sums=('eb18ad53b81b93c469db4d915bbb3fae21b36ad82a88e01fef3a6946ac5f50b54376e259a5ecbe23836f1efd59d226b942ecdee87eaba7f9e75cdcaaa9499ef7')

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
