# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Camille Moncelier <pix@devlife.org>

pkgname=m17n-lib
pkgver=1.8.0
pkgrel=6
pkgdesc='Multilingual text processing library (runtimes)'
url='https://www.nongnu.org/m17n/'
arch=('x86_64')
license=('GPL')
depends=('libxft' 'm17n-db' 'fribidi' 'libxml2' 'gd' 'libotf')
optdepends=('anthy: to use the anthy input method')
options=('!makeflags' '!emptydirs')
validpgpkeys=('D68584F9B40A6502FD7C53015F592A4029890630')
source=("https://download.savannah.gnu.org/releases/m17n/${pkgname}-${pkgver}.tar.gz")
sha256sums=('78bacae7451822dfff62482ce4f9433c5ae82846e4c64b590541e29e800fe64a')

prepare() {
	cd ${pkgname}-${pkgver}
	sed -i 's/freetype-config/pkg-config freetype2/' configure
}

build() {
	cd ${pkgname}-${pkgver}
	./configure --prefix=/usr
	make
}

package() {
	cd ${pkgname}-${pkgver}
	make DESTDIR="${pkgdir}" install
}
