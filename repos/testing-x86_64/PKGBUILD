# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=zsh
pkgver=4.3.15
pkgrel=3
pkgdesc='A very advanced and programmable command interpreter (shell) for UNIX'
arch=('i686' 'x86_64')
url='http://www.zsh.org/'
license=('custom')
depends=('pcre' 'libcap' 'gdbm')
install=zsh.install
source=("ftp://ftp.zsh.org/pub/${pkgname}-${pkgver}.tar.bz2"
        'zprofile' 'svn-1.7.patch')
backup=('etc/zsh/zprofile')
md5sums=('11766b0dd674a513810903cd33ffcac4'
         '24a9335edf77252a7b5f52e079f7aef7'
         '4fcbc119fdb624a8c483a278f3a82efd')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	patch -p1 -i "${srcdir}/svn-1.7.patch"
	# FS#16360
	sed -i 's/init.d/rc.d/g' Doc/Zsh/compsys.yo \
				 Doc/zsh.texi \
				 Completion/Unix/Type/_services \
				 Completion/Unix/Command/_init_d
	./configure --prefix=/usr \
		--bindir=/bin \
		--enable-etcdir=/etc/zsh \
		--enable-zshenv=/etc/zsh/zshenv \
		--enable-zlogin=/etc/zsh/zlogin \
		--enable-zlogout=/etc/zsh/zlogout \
		--enable-zprofile=/etc/zsh/zprofile \
		--enable-zshrc=/etc/zsh/zshrc \
		--enable-maildir-support \
		--with-term-lib='ncursesw' \
		--enable-multibyte \
		--enable-function-subdirs \
		--enable-fndir=/usr/share/zsh/functions \
		--enable-scriptdir=/usr/share/zsh/scripts \
		--with-tcsetpgrp \
		--enable-pcre \
		--enable-cap \
		--enable-zsh-secure-free
	make
}

check() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	# This test wont work with the noatime mount option
	rm Test/C02cond.ztst
	HOME="${srcdir}" make check
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}/" install
	install -D -m644 "${srcdir}/zprofile" "${pkgdir}/etc/zsh/zprofile"
	install -D -m644 LICENCE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
