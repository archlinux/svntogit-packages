# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Thomas S Hatch <thatch45@gmail.com>
# Contributor: Geoffroy Carrier <geoffroy@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=('protobuf' 'python2-protobuf')
pkgver=2.6.1
pkgrel=2
pkgdesc="Protocol Buffers - Google's data interchange format"
arch=('i686' 'x86_64')
url='https://developers.google.com/protocol-buffers/'
license=('BSD')
depends=('gcc-libs' 'zlib')
makedepends=('python2-setuptools')
source=(https://github.com/google/protobuf/releases/download/v$pkgver/protobuf-$pkgver.tar.bz2)
md5sums=('11aaac2d704eef8efd1867a807865d85')

build() {
  cd $pkgbase-$pkgver
  ./configure --prefix=/usr
  make
}

check() {
  make -C $pkgbase-$pkgver check
}

package_protobuf() {
  cd $pkgbase-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  install -Dm644 editors/protobuf-mode.el \
    "$pkgdir"/usr/share/emacs/site-lisp/protobuf-mode.el
}

package_python2-protobuf() {
  pkgdesc='Python 2 bindings for Google Protocol Buffers'
  depends=("python2" "protobuf=${pkgver}")
  provides=("protobuf-python")
  conflicts=("protobuf-python")
  replaces=("protobuf-python")

  cd $pkgbase-$pkgver/python
  python2 setup.py install --prefix=/usr --root="$pkgdir"

  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s /usr/share/licenses/$pkgbase/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/
}
