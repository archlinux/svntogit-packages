# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=sed
pkgver=4.2.2
pkgrel=1
pkgdesc="GNU stream editor"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/sed"
license=('GPL3')
groups=('base')
depends=('acl' 'sh')
makedepends=('gettext')
install=sed.install
source=(ftp://ftp.gnu.org/pub/gnu/sed/${pkgname}-${pkgver}.tar.gz{,.sig})
md5sums=('4111de4faa3b9848a0686b2f260c5056'
         '86a5ab72f414d4cb38126e8e27cf0101')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  
  mkdir $pkgdir/bin
  ln -s ../usr/bin/sed $pkgdir/bin
  
}
