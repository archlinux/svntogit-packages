# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Mirco Tischler <mt-ml at gmx dot de>

pkgname=ostree
_commit=15740d042c9c5258a1c082b5e228cf6f115edbb0  # tags/v2022.5^0
pkgver=2022.5
pkgrel=4
pkgdesc="Operating system and container binary deployment and upgrades"
url="https://ostreedev.github.io/ostree/"
arch=(x86_64)
license=(GPL)
depends=(fuse3 glibc libgpg-error libsodium util-linux openssl xz zlib)
makedepends=(avahi e2fsprogs git glib2 gobject-introspection gpgme gtk-doc
             libarchive libsoup libxslt mkinitcpio python systemd)
checkdepends=(cpio elfutils gjs parallel python-yaml syslinux)
options=(debug)
source=(
  "git+https://github.com/ostreedev/ostree#commit=$_commit"
  git+https://github.com/mendsley/bsdiff
  git+https://gitlab.gnome.org/GNOME/libglnx.git
  fusermount3.diff
)
b2sums=('SKIP'
        'SKIP'
        'SKIP'
        '58993607327ef5408472b410f4bc05dfe9e65ddc3afc875fa28ab16cdc42f999ff63012b794905b0f90ba5a0680d5a8115923484465ddcaa7d5287666e4f71e8')
# upstream ticket about chain of trust:
# https://github.com/ostreedev/ostree/issues/2349
validpgpkeys=('1CEC7A9DF7DA85ABEF843DC0A866D7CCAE087291') # Colin Walters <walters@redhat.com>

prepare() {
  cd ostree

  # https://github.com/ostreedev/ostree/issues/2685
  git cherry-pick -n edba4b33be10c052 0a908a180fcce98c

  # Use fusermount3
  git apply -3 ../fusermount3.diff

  git submodule init
  git submodule set-url bsdiff "$srcdir/bsdiff"
  git submodule set-url libglnx "$srcdir/libglnx"
  git submodule update

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd ostree

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib \
    --with-builtin-grub2-mkconfig \
    --with-ed25519-libsodium \
    --with-dracut \
    --with-mkinitcpio \
    --with-openssl \
    --enable-experimental-api \
    --disable-static \
    --enable-gtk-doc

  # circumvent overlinking
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

check() {
  cd ostree
  make check -k || echo "Known to fail: https://github.com/ostreedev/ostree/issues/2313"
}

package() {
  depends+=(libarchive.so libavahi-client.so libavahi-common.so
            libavahi-glib.so libgio-2.0.so libglib-2.0.so libgobject-2.0.so
            libgpgme.so libsoup-2.4.so libsystemd.so)
  provides+=(libostree-1.so)

  cd ostree
  make DESTDIR="$pkgdir" install
}
