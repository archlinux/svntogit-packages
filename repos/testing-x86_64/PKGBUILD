# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-chess
pkgver=3.22.0
pkgrel=1
pkgdesc="Play the classic two-player boardgame of chess"
url="https://wiki.gnome.org/Apps/Chess"
arch=(i686 x86_64)
license=(GPL)
depends=(gtk3 librsvg)
makedepends=(intltool itstool gobject-introspection yelp-tools appstream-glib vala git)
optdepends=('gnuchess: Play against computer')
groups=(gnome-extra)
_commit=30a0ada6391dec629a608265f02975d4b65eb53d
source=("git://git.gnome.org/gnome-chess#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --disable-schemas-compile
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
