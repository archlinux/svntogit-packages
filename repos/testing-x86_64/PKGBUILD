# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s@arch@gmail.com>
# Contributor: Andreas Wagner <Andreas.Wagner@em.uni-frankfurt.de>

pkgname=s-nail
pkgver=14.9.6
pkgrel=1
pkgdesc='Mail processing system with a command syntax reminiscent of ed'
url='http://www.sdaoden.eu/code.html#s-nail'
license=('custom:BSD')
arch=('x86_64')
depends=('openssl' 'krb5' 'libidn')
optdepends=('smtp-forwarder: for sending mail')
validpgpkeys=('EE19E1C1F2F7054F8D3954D8308964B51883A0DD')
source=("https://www.sdaoden.eu/downloads/${pkgname}-${pkgver}.tar.xz"{,.asc})
sha256sums=('0d603f0ee0111756db610202133fe0f9f701a689b04ec7bddcce4a9fd8222fbc'
            'SKIP')

groups=('base')
backup=('etc/mail.rc')
replaces=('mailx' 'mailx-heirloom' 'heirloom-mailx')
provides=('mailx' 'mailx-heirloom' 'heirloom-mailx')
conflicts=('mailx' 'mailx-heirloom' 'heirloom-mailx')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	CFLAGS+=" $CPPFLAGS"
	make \
		VAL_PREFIX=/usr \
		VAL_SYSCONFDIR=/etc \
		VAL_LIBEXECDIR=/usr/lib \
		VAL_MAIL=/var/spool/mail \
		VAL_SID= VAL_MAILX=mail \
		OPT_AUTOCC=0 \
		config

	make MAKEJOBS=--jobs build
}

check() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make test < /dev/null
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	ln -sf mail "${pkgdir}"/usr/bin/mailx
	ln -sf mail.1.gz "${pkgdir}"/usr/share/man/man1/mailx.1.gz
	install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
