# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=make
pkgver=4.0
pkgrel=2
pkgdesc="GNU make utility to maintain groups of programs"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/make"
license=('GPL3')
groups=('base-devel')
depends=('glibc' 'sh' 'guile')
install=$pkgname.install
source=(ftp://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.bz2{,.sig}
        make-4.0-char-cast.patch)
md5sums=('571d470a7647b455e3af3f92d79f1c18'
         'SKIP'
         '1840aae18e1c0d1c52563020ef3ff5f3')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # http://git.savannah.gnu.org/cgit/make.git/commit/?id=9d58570c
  patch -p1 -i ${srcdir}/make-4.0-char-cast.patch
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
