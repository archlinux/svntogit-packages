# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>

pkgname=file
pkgver=5.07
pkgrel=4
pkgdesc="File type identification utility"
arch=('i686' 'x86_64')
license=('custom')
groups=('base')
url="http://www.darwinsys.com/file/"
depends=('glibc' 'zlib')
options=('!libtool')
source=(ftp://ftp.astron.com/pub/${pkgname}/${pkgname}-${pkgver}.tar.gz 
        file-5.07-zip-detect.patch)
md5sums=('b8d1f9a8a644067bd0a703cebf3f4858'
         'ac155cf89af6665dfee76738c27366d8')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i 's#\$(datadir)/misc#\$(datadir)#' configure 

  # combined upstream patches to fix zip file detection
  patch -Np1 -i ${srcdir}/file-5.07-zip-detect.patch
  
  ./configure --prefix=/usr --datadir=/usr/share/file
  make 
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR=${pkgdir} install
  install -dm755 ${pkgdir}/usr/share/misc
  ln -s ../file/magic.mgc ${pkgdir}/usr/share/misc
  
  install -D -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}
