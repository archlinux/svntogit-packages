# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=exo
pkgver=4.18.0
pkgrel=1
pkgdesc="Application library for the Xfce desktop environment"
arch=('x86_64')
url="https://docs.xfce.org/xfce/exo/start"
license=('GPL2' 'LGPL')
groups=('xfce4')
depends=('libxfce4ui')
makedepends=('intltool')
source=(https://archive.xfce.org/src/xfce/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=('4f2c61d045a888cdb64297fd0ae20cc23da9b97ffb82562ed12806ed21da7d55')

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --disable-debug
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
