# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>

pkgname=db
pkgver=4.8.26
_basever=4.8.26
pkgrel=2
pkgdesc="The Berkeley DB embedded database system"
arch=('i686' 'x86_64')
url="http://www.oracle.com/technology/software/products/berkeley-db/index.html"
license=('custom')
depends=('gcc-libs' 'sh')
conflicts=('db4')
options=('!libtool' 'force')
install=db.install
source=(http://download-uk.oracle.com/berkeley-db/db-${_basever}.tar.gz)
md5sums=('3476bac9ec0f3c40729c8a404151d5e3')

build() {
  cd ${srcdir}/$pkgname-${_basever}/build_unix
  ../dist/configure --prefix=/usr --enable-compat185 \
    --enable-shared --enable-static --enable-cxx
  make LIBSO_LIBS=-lpthread || return 1
}

package() {
  cd ${srcdir}/$pkgname-${_basever}/build_unix
  make DESTDIR=${pkgdir} install
  rm -rf ${pkgdir}/usr/docs
  install -Dm644 ${srcdir}/${pkgname}-${_basever}/LICENSE \
    ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
