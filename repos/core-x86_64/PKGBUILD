# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=libtasn1
pkgver=4.14
pkgrel=2
pkgdesc="The ASN.1 library used in GNUTLS"
arch=('x86_64')
license=('GPL3' 'LGPL')
url="https://www.gnu.org/software/libtasn1/"
depends=('glibc')
source=(https://www.gnupg.org/ftp/gcrypt/gnutls/libtasn1//${pkgname}-${pkgver}.tar.gz{,.sig})
sha256sums=('9e604ba5c5c8ea403487695c2e407405820d98540d9de884d6e844f9a9c5ba08'
            'SKIP')
validpgpkeys=('1F42418905D8206AA754CCDC29EE58B996865171') #Nikos Mavrogiannopoulos <nmav@gnutls.org>

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  CODE_COVERAGE_LDFLAGS=${LDFLAGS} make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  make -C doc/reference DESTDIR="${pkgdir}" install-data-local
}
