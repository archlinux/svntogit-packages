# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Stéphane Gaudreault <stephane@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=gdbm
pkgver=1.20
pkgrel=1
pkgdesc='GNU database library'
url="https://www.gnu.org/software/gdbm/gdbm.html"
license=('GPL3')
arch=('x86_64')
depends=('glibc' 'sh')
makedepends=('readline')
provides=('libgdbm_compat.so' 'libgdbm.so')
source=("https://ftp.gnu.org/gnu/gdbm/${pkgname}-${pkgver}.tar.gz"{,.sig})
sha512sums=('5f2fbd4318259e5b879ea2bff09e22defbb4e566ebbe11c44ac5b5369fd00745d7ed9f72ec1206153027eba245c4ee4a515a8ec6d67f84ec477483e72753590b'
            'SKIP')
b2sums=('b0e01e63714b0485f9ac28f4a2260bf9309d2b3a87f6ccf6c9901619d383b6527d068b8614220eb6a6dec909fbf3fab5162a6c67bb928cfedaef36f1df26d0df'
        'SKIP')
validpgpkeys=('325F650C4C2B6AD58807327A3602B07F55D0C732') # Sergey Poznyakoff <gray@gnu.org>

prepare() {
  cd "$pkgname-$pkgver"
  autoreconf -fiv
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr \
              --enable-libgdbm-compat
  make
}

check() {
  cd "$pkgname-$pkgver"
  make check
}

package() {
  depends+=('libreadline.so')
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -vDm 644 {NOTE-WARNING,AUTHORS,NEWS,README,ChangeLog} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
