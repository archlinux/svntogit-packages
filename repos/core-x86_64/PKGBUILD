# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Committer: Judd Vinet <jvinet@zeroflux.org>

pkgname=pkg-config
pkgver=0.26
pkgrel=2
pkgdesc="A system for managing library compile/link flags"
arch=('i686' 'x86_64')
url="http://pkgconfig.freedesktop.org/wiki/"
license=('GPL')
groups=('base-devel')
depends=('glibc' 'popt' 'glib2')
provides=("pkgconfig=${pkgver}")
conflicts=('pkgconfig')
replaces=('pkgconfig')
source=(http://pkgconfig.freedesktop.org/releases/${pkgname}-${pkgver}.tar.gz)
md5sums=('47525c26a9ba7ba14bf85e01509a7234')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # Use system popt
  ./configure --prefix=/usr --with-installed-popt 

  make
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
