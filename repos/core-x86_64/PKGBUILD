# $Id$
# Maintainer: Paul Mattal <paul@archlinux.org>

pkgname=hdparm
pkgver=9.37
pkgrel=2
pkgdesc="A shell utility for manipulating Linux IDE drive/driver parameters"
arch=(i686 x86_64)
depends=('glibc')
optdepends=('sh: required by idectl and ultrabayd')
source=(http://downloads.sourceforge.net/sourceforge/hdparm/${pkgname}-${pkgver}.tar.gz
        wiper.sh.2_6.max-ranges.patch)
license=('BSD')
url="http://sourceforge.net/projects/hdparm/"
optdepends=('bash: for wiper.sh script')
options=('emptydirs')
md5sums=('0bb94ddd1bedd5c02b1ca62f1caaf6de'
         '74e368f384166a7710b447573cda120a')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  # Fix Range input/output error when wiping Intel G2 and OCZ drives
  patch -Np1 -i $srcdir/wiper.sh.2_6.max-ranges.patch
  
  # build
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  # install
  mkdir -p ${pkgdir}/{usr,sbin}
  make DESTDIR=${pkgdir} install
  install -m755 contrib/idectl ${pkgdir}/sbin
  install -m755 contrib/ultrabayd ${pkgdir}/sbin

  install -D -m 0644 $srcdir/$pkgname-$pkgver/wiper/README.txt $pkgdir/usr/share/doc/wiper/README.txt
  install -D -m 0755 $srcdir/$pkgname-$pkgver/wiper/wiper.sh $pkgdir/usr/sbin/wiper.sh

  #install license file
  install -D -m 644 LICENSE.TXT $pkgdir/usr/share/licenses/hdparm/LICENSE.TXT
}
