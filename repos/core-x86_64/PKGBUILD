# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>

pkgname=libmpc
pkgver=0.8.2
pkgrel=2
pkgdesc="Library for the arithmetic of complex numbers with arbitrarily high precision"
arch=('i686' 'x86_64')
url="http://www.multiprecision.org"
license=('LGPL')
depends=('mpfr>=3.0.0')
options=('!libtool')
install=libmpc.install
source=(http://www.multiprecision.org/mpc/download/mpc-${pkgver/_/-}.tar.gz)
md5sums=('e98267ebd5648a39f881d66797122fb6')

build() {
  cd $srcdir/mpc-$pkgver
  ./configure --prefix=/usr
  make
  make check
}

package() {
  cd $srcdir/mpc-$pkgver
  make DESTDIR=$pkgdir/ install
  mv -f $pkgdir/usr/share/info/{mpc,libmpc}.info
}
