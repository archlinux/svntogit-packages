# $Id$
# Maintainer: Aaron Griffin <aaron@archlinux.org>

pkgname=bash
_patchlevel=024 #prepare for some patches
pkgver=4.0.$_patchlevel
pkgrel=1
pkgdesc="The GNU Bourne Again shell"
arch=(i686 x86_64)
license=('GPL')
url="http://www.gnu.org/software/bash/bash.html"
groups=('base')
backup=(etc/profile.bash etc/skel/.bashrc etc/skel/.bash_profile)
depends=('readline>=6.0' 'glibc')
makedepends=(gzip)
provides=('sh')
install=bash.install
source=(http://ftp.gnu.org/gnu/bash/bash-4.0.tar.gz
        profile.bash bashrc)
if [ $_patchlevel -gt 000 ]; then
    for p in $(seq -w 001 $_patchlevel); do
        source=(${source[@]} http://ftp.gnu.org/gnu/bash/bash-4.0-patches/bash40-$p)
    done
fi

build() {
  cd ${startdir}/src/${pkgname}-4.0
  for p in ../bash40-*; do
    [ -e "$p" ] || continue
    msg "applying patch ${p}"
    patch -Np0 -i ${p} || return 1
  done

  ./configure --prefix=/usr --with-curses --enable-readline \
    --without-bash-malloc --with-installed-readline \
    --bindir=/bin --mandir=/usr/share/man --infodir=/usr/share/info
  make || return 1
  make DESTDIR=${startdir}/pkg install

  install -D -m644 ${startdir}/src/profile.bash ${startdir}/pkg/etc/profile.bash

  # for now, bash is our default /bin/sh
  cd ${startdir}/pkg/bin
  ln -s bash sh

  mkdir -p $startdir/pkg/etc/skel/
  install -D -m644 $startdir/src/bashrc $startdir/pkg/etc/skel/.bashrc
  echo ". \$HOME/.bashrc" >$startdir/pkg/etc/skel/.bash_profile
  
  rm -f ${pkgdir}/usr/share/info/dir
  gzip -9 ${pkgdir}/usr/share/info/bash.info
}
# wow, that's a lot of md5sums!
md5sums=('a90a1b5a6db4838483f05438e05e8eb9'
         'c2fae355facefcac8a732cce877da194'
         '3d2837e84645eac9bde399b58014bafa'
         'bc7f4762443939bd7dccb42370f0d932'
         'c2a4a4786a83ed4ec366c6a8924369a2'
         '22e8a824eddba21a8fce10d7984c2aba'
         'ed7cbced8c7c964323265522369a37a2'
         '8ed86b7d31423d71ecf3148251d63512'
         '5f447338cb98ff156cabf1fd9879d5f3'
         '96e946cb66a4ca186cba1da44f1ee163'
         'd3eb7b6f00d525e032478c33f51d46a8'
         '340601c997ce569532417a7ae92248b8'
         '0bd5ab96d514ffb1afbb8c7984b15146'
         '32cb20f339a20e1e9fb37a5d18f18fca'
         '33fd9e93d30a17988c19554ef26d56e0'
         'a266b42df5e9ed7e8818a8b00d50e00b'
         '86cac78f191a32cd1404f11264eb9b2a'
         'bb41963d030bc61a20e8185367b337c5'
         'f75455048a086528971252fd979b8755'
         '34b2cd57271a452f4a26b39d77ff908f'
         '99318eed8dcc05e10a14ae27043f175d'
         'af3b9aaeadc71a5007bec2b98c751cde'
         'eb7c7ddeb7e8451eb59228dca3329696'
         '585d701b978ceb63967dc020db3a2234'
         '7a70f2608f90f6ac0c2051e5f1c6c414'
         '7a23aa41630dd4fe8d30108a200e2d96'
         '82ba5fc9eb780eb57d8b7628a17b7d74')
