# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=zlib
pkgver=1.2.3.9
pkgrel=1
pkgdesc='Compression library implementing the deflate compression method found in gzip and PKZIP'
arch=('i686' 'x86_64')
license=('custom')
url="http://www.zlib.net/"
groups=('base')
depends=('glibc')
# source from zlib-devel
source=("ftp://ftp.archlinux.org/other/zlib/zlib-${pkgver}.tar.gz")
md5sums=('e63390cc6cbd0722f70729307d4dd01d')

build() {
	cd ${srcdir}/zlib-$pkgver
	export CFLAGS="${CFLAGS/-O2/-O3} -DUNALIGNED_OK"
	./configure --prefix=/usr --shared
	make || return 1

	grep -A 24 '^  Copyright' zlib.h > LICENSE
}

package() {
	cd ${srcdir}/zlib-$pkgver
	make install DESTDIR=${pkgdir} || return 1
	install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/zlib/LICENSE
}
