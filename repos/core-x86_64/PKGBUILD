#$Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>

pkgname=libcap
pkgver=2.23
pkgrel=2
pkgdesc="POSIX 1003.1e capabilities"
arch=('i686' 'x86_64')
url="http://sites.google.com/site/fullycapable/"
license=('GPL2')
depends=('glibc' 'attr')
options=('!staticlibs')
source=(https://www.kernel.org/pub/linux/libs/security/linux-privs/libcap2/libcap-2.23.tar.xz
        libcap-2.23-header.patch)
md5sums=('09a185e4b0aa8a81a51c1e4d0eba7db0'
         '945984c4bf5e601c24a7c80f001fb2c6')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # install into /usr/bin
  sed -i "/SBINDIR/s#sbin#bin#" Make.Rules

  # fix header path issues
  patch -p1 -i $srcdir/libcap-2.23-header.patch
  # and fix the build with that patch
  sed -i "s#uapi/##" libcap/Makefile
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make prefix=/usr lib=/lib DESTDIR=${pkgdir} RAISE_SETFCAP=no install
  
  install -Dm644 pam_cap/capability.conf \
    $pkgdir/usr/share/doc/$pkgname/capability.conf.example
}
