# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Maintainer: Dan McGee <dan@archlinux.org>

pkgname=libarchive
pkgver=3.4.0
pkgrel=3
pkgdesc='Multi-format archive and compression library'
arch=(x86_64)
url='https://libarchive.org/'
license=(BSD)
depends=(acl attr bzip2 expat lz4 openssl xz zlib zstd)
provides=(libarchive.so)
validpgpkeys=('CB55788360B992FA0885C878F040F7196BA99AF4') # Martin Matuska <martin@matuska.org>
source=("https://github.com/${pkgname}/${pkgname}/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz"{,.asc}
        '0001-test_write_filter_zstd-set-compression-level-to-7.patch')
sha256sums=('8643d50ed40c759f5412a3af4e353cffbce4fdf3b5cf321cb72cacf06b2d825e'
            'SKIP'
            '95b2998a1061d015af2f0e21a0d3433bf21389a4ab9c8836121f18e01980451c')

prepare() {
  cd $pkgname-$pkgver 

  patch -Np1 < ../0001-test_write_filter_zstd-set-compression-level-to-7.patch

  autoreconf -fi
}

build() {
  cd $pkgname-$pkgver 

  ./configure \
      --prefix=/usr \
      --without-xml2 \
      --without-nettle \
      --disable-static

  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/libarchive/COPYING"
}
