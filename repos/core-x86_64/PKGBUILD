#$Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>

pkgname=libcap
pkgver=2.22
pkgrel=5
pkgdesc="POSIX 1003.1e capabilities"
arch=('i686' 'x86_64')
url="http://sites.google.com/site/fullycapable/"
license=('GPL2')
depends=('glibc' 'attr')
options=('!staticlibs')
source=(ftp://ftp.archlinux.org/other/${pkgname}/${pkgname}-${pkgver}.tar.gz{,.asc})
md5sums=('b4896816b626bea445f0b3849bdd4077'
         '9d0983e25e5a251d098507f9561d2b27')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # install into /usr/bin
  sed -i "/SBINDIR/s#sbin#bin#" Make.Rules
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make prefix=/usr lib=/lib DESTDIR=${pkgdir} RAISE_SETFCAP=no install
  
  install -Dm644 pam_cap/capability.conf \
    $pkgdir/usr/share/doc/$pkgname/capability.conf.example
}
