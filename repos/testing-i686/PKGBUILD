# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=gzip
pkgver=1.4
pkgrel=4
pkgdesc="GNU compression utility"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/gzip/"
license=('GPL3')
groups=('base')
depends=('glibc' 'bash')
makedepends=('patch')
install=gzip.install
source=(ftp://ftp.gnu.org/pub/gnu/gzip/gzip-$pkgver.tar.gz{,.sig})
md5sums=('e381b8506210c794278f5527cba0e765'
         '3b11d485d1638f2d16f7494a0486a6e8')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

   # By default Gzip uses assembly code. While this may preform better, 
   # it is not position independent. The DEFS environment variable is 
   # set to use only C code.
   export DEFS="NO_ASM"
  ./configure --prefix=/usr
  make 
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make prefix=${pkgdir}/usr install

  install -dm755 ${pkgdir}/bin
  mv ${pkgdir}/usr/bin/{gunzip,gzip,uncompress,zcat} ${pkgdir}/bin/
}
