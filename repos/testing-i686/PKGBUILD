# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>

pkgname=perl-net-dns
pkgver=0.76_1
pkgrel=1
pkgdesc="Perl Module: Interface to the DNS resolver"
arch=('i686' 'x86_64')
license=('PerlArtistic')
url="http://search.cpan.org/dist/Net-DNS/"
depends=('perl-digest-hmac' 'perl-net-ip' 'perl')
checkdepends=('perl-test-pod')
options=('!emptydirs')
source=(http://www.cpan.org/authors/id/N/NL/NLNETLABS/Net-DNS-$pkgver.tar.gz)
sha1sums=('f719cf72fdc19ed9a78fcd5d381385c620a93548')

build() {
  cd Net-DNS-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Net-DNS-${pkgver}
  make test
}

package() {
  cd Net-DNS-${pkgver}
  make DESTDIR="${pkgdir}" install
}
