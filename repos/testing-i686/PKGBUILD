# $Id$
# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Aaron Griffin <aaron@archlinux.org>

pkgname=iputils
# Commit date + git rev-parse --short origin/master
_rev=1c59920
pkgver=20150815.${_rev}
pkgrel=3
pkgdesc="Network monitoring tools, including ping"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.skbuff.net/iputils/"
groups=('base')
depends=('openssl' 'sysfsutils' 'libcap')
optdepends=('xinetd: for tftpd')
makedepends=('docbook2x' 'opensp' 'git')
conflicts=('netkit-base' 'arping' 'netkit-tftpd')
replaces=('netkit-base')
backup=(etc/xinetd.d/tftp)
install=${pkgname}.install
source=("git+https://github.com/iputils/iputils.git#commit=${_rev}"
        tftp.xinetd)
sha1sums=('SKIP'
          'fc2ae26f5609725e3f4aeaf4ab82dfa6d2e378fd')

build() {
  cd "${srcdir}/${pkgname}"

  make USE_GNUTLS=no CCOPTOPT="$CFLAGS"

  cd doc
  for file in *.sgml; do
    xf=${file/.sgml/.xml}
    osx -xlower -xno-nl-in-tag $file > $xf || true
    sed -i "s|<refname>\(.*\), \(.*\)</refname>|<refname>\1</refname>, <refname>\2</refname>|g" $xf
    docbook2man $xf
  done
}

package() {
  cd "${srcdir}/${pkgname}"

  install -dm755 "${pkgdir}"/usr/bin

  install -m755 arping clockdiff ping rarpd rdisc tftpd tracepath tracepath6 "${pkgdir}"/usr/bin/

  install -dm755 "${pkgdir}"/usr/share/man/man8
  install -m644 doc/{arping,clockdiff,ping,rarpd,rdisc,tftpd,tracepath}.8 \
     "${pkgdir}"/usr/share/man/man8/

  cd "${pkgdir}"/usr/share/man/man8
  ln -sf tracepath.8.gz tracepath6.8.gz

  # FS#24768
  install -dm755 "${pkgdir}"/etc/xinetd.d/
  install -m644 "${srcdir}"/tftp.xinetd "${pkgdir}"/etc/xinetd.d/tftp
}
