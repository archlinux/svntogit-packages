# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-sessreg
pkgver=1.1.0
pkgrel=1
pkgdesc="Register X sessions in system utmp/utmpx databases"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('glibc')
makedepends=('xorg-util-macros' 'xproto')
groups=('xorg-apps' 'xorg')
source=(http://xorg.freedesktop.org/archive/individual/app/sessreg-${pkgver}.tar.bz2{,.sig})
sha256sums=('551177657835e0902b5eee7b19713035beaa1581bbd3c6506baa553e751e017c'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # Alan Coopersmith <alan.coopersmith@oracle.com>

build() {
  cd "${srcdir}/sessreg-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/sessreg-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
