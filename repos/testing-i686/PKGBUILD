# $Id$
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: Jonathan Liu <net147@gmail.com>

pkgname=ossp
pkgver=1.3.2
pkgrel=11
pkgdesc="Emulate OSS device using CUSE"
arch=(i686 x86_64)
url="http://sourceforge.net/projects/osspd"
license=(GPL2)
depends=('fuse>=2.8.0')
makedepends=(libpulse alsa-lib)
optdepends=("libpulse: PulseAudio backend"
            "alsa-lib: ALSA backend")
backup=(etc/conf.d/osspd)
source=("http://downloads.sourceforge.net/project/osspd/${pkgname}-${pkgver}.tar.gz"
        osspd.rc.d osspd.conf.d)
md5sums=('ee2d8c894de2fa4eb3291804fbc7df45'
         '0c02208ce1dfe74b6e9840f3600678cc'
         '5a5a3db1bb3c98d48d4794613cf12ef0')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" prefix=/usr UDEVDIR=/lib/udev/rules.d install

  # Make compatible with udev ACLs
  sed -e 's/GROUP="audio"/SUBSYSTEM=="cuse", MODE="0666"/' \
      -i "$pkgdir/lib/udev/rules.d/98-osscuse.rules"

  install -D -m644 README "$pkgdir/usr/share/doc/$pkgname/README"

  install -D -m755 "$srcdir/osspd.rc.d" "$pkgdir/etc/rc.d/osspd"
  install -D -m644 "$srcdir/osspd.conf.d" "$pkgdir/etc/conf.d/osspd"
}
