# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Michel Brabants <michel.linux@tiscali.be>

pkgname=seahorse
pkgver=3.2.2
pkgrel=1
pkgdesc="GNOME application for managing PGP keys."
arch=(i686 x86_64)
license=('GPL')
url="http://projects.gnome.org/seahorse/"
depends=('gtk3' 'libgnome-keyring' 'gnome-keyring' 'libsoup' 'gpgme' 'desktop-file-utils' 'hicolor-icon-theme' 'dconf')
makedepends=('gettext' 'libldap' 'intltool' 'pkgconfig' 'gnome-doc-utils' 'gobject-introspection' 'openssh' 'libsm')
optdepends=('openssh: SSH support')
provides=('x11-ssh-askpass')
options=('!libtool' '!emptydirs')
groups=('gnome-extra')
install=seahorse.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz
        seahorse-ssh-askpass.sh)
sha256sums=('25146aa4c4bb4c375ec1afec524e1907852989bd1b0c9e12490326616ac6a7e9'
            '1792ab2c0bbad446b8fad0a0c9008a75f60a9666c895d37d2adcd827e49fafbb')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --enable-ldap --enable-hkp \
      --disable-scrollkeeper \
      --disable-update-mime-database \
      --disable-schemas-compile
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm755 "$srcdir/seahorse-ssh-askpass.sh" \
    "$pkgdir/etc/profile.d/seahorse-ssh-askpass.sh"
}
