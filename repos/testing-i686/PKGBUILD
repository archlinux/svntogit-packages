# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Daniel Balieiro <daniel@balieiro.com>
# Contributor: Rodrigo L. M. Flores <mail@rodrigoflores.org>
pkgname=telepathy-gabble
pkgver=0.16.0
pkgrel=1
pkgdesc="A Jabber/XMPP connection manager for Telepathy"
arch=('i686' 'x86_64')
url="http://telepathy.freedesktop.org"
groups=('telepathy')
license=('LGPL2.1')
depends=('telepathy-glib' 'libsoup' 'libnice' 'sqlite3')
makedepends=('libxslt' 'python2')
options=('!libtool' '!emptydirs')
source=(http://telepathy.freedesktop.org/releases/$pkgname/$pkgname-$pkgver.tar.gz)
install=telepathy-gabble.install
md5sums=('15b15fb58a3c54102c3e3489b4bd5ee0')

build() {
  cd "$pkgname-$pkgver"
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr \
  	--libexecdir=/usr/lib/telepathy --with-tls=openssl
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  sed -i "s|#!/usr/bin/env python|#!/usr/bin/env python2|" "$pkgdir/usr/bin/telepathy-gabble-xmpp-console"
}
