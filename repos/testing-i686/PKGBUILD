# $Id$
# Maintainer:  Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Aurélien Wailly <aurelien.wailly@gmail.com>

pkgname=protobuf-c
pkgver=1.0.2
pkgrel=1
pkgdesc='Protocol Buffers implementation in C'
arch=('i686' 'x86_64')
url='https://github.com/protobuf-c/protobuf-c'
license=('BSD')
depends=('protobuf')
source=($url/releases/download/v$pkgver/protobuf-c-$pkgver.tar.gz)
md5sums=('42f03767ddbd1dbe26cce10bc6e08c84')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-static
  make
}

check() {
  make -C $pkgname-$pkgver check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
