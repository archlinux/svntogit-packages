# $Id$
#
pkgname=xfsprogs
pkgver=3.2.4
pkgrel=1
pkgdesc="XFS filesystem utilities"
arch=('i686' 'x86_64')
license=('LGPL')
url="http://oss.sgi.com/projects/xfs/"
groups=('base')
depends=('libutil-linux')
makedepends=('git')
options=('!makeflags')
source=("git://oss.sgi.com/xfs/cmds/xfsprogs#tag=v${pkgver}")
md5sums=('SKIP')

build() {
  cd "${srcdir}/${pkgname}"
  make configure
  export OPTIMIZER="-march=${CARCH/_/-} -O1"
  export DEBUG=-DNDEBUG
  ./configure --prefix=/usr --sbindir=/usr/bin
  make
}

package() {
  cd "${srcdir}/${pkgname}"
  make DIST_ROOT="${pkgdir}" PKG_ROOT_SBIN_DIR="/usr/bin" install install-dev
  chown -R root $pkgdir
  chgrp -R root $pkgdir
  # add hack as we cannot set rootlibdir
  mv "${pkgdir}"/lib/libhandle.so* "${pkgdir}/usr/lib/"
  rm -rf "${pkgdir}/lib"
}
