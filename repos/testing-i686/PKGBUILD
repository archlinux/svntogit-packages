# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=polari
pkgver=3.20.0
pkgrel=1
pkgdesc="An IRC Client for GNOME"
arch=(i686 x86_64)
license=(GPL)
url="https://wiki.gnome.org/Apps/Polari"
depends=(gjs gtk3 telepathy-glib telepathy-idle telepathy-mission-control telepathy-logger)
makedepends=(intltool gobject-introspection desktop-file-utils appdata-tools)
groups=(gnome-extra)
install=polari.install
source=(https://download.gnome.org/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('519402e5a7e7aa684d364523bf16357be748c4d0a08b1297e2dee379b8da440a')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
