# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Gabriel Martinez < reitaka at gmail dot com >

pkgname=usbmuxd
pkgver=1.0.8
pkgrel=2
pkgdesc="USB Multiplex Daemon"
url="http://marcansoft.com/blog/iphonelinux/usbmuxd/"
arch=('i686' 'x86_64')
license=('GPL2' 'GPL3' 'LGPL2.1')
depends=('libusb' 'libplist')
makedepends=('cmake')
install=usbmuxd.install
source=("http://cgit.sukimashita.com/${pkgname}.git/snapshot/${pkgname}-${pkgver}.tar.bz2"
	'usbmuxd.service'
	'85-usbmuxd.rules')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  echo "${pkgver}" > version.tag
  mkdir build
  cd build
  cmake ../ -DCMAKE_INSTALL_PREFIX:PATH=/usr -DLIB_SUFFIX=""
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/build"
  make DESTDIR="${pkgdir}" install

  # fix udev dir being hardcoded
  mv "${pkgdir}/lib/udev" "${pkgdir}/usr/lib/"
  rmdir "${pkgdir}/lib"
  # usrmove
  mv "${pkgdir}/usr/sbin/"* "${pkgdir}/usr/bin/"
  rmdir "${pkgdir}/usr/sbin"
  # install systemd service file
  install -D -m644 ${srcdir}/usbmuxd.service ${pkgdir}/usr/lib/systemd/system/usbmuxd.service
  # use udev rule that works with systemd
  install -m644 ${srcdir}/85-usbmuxd.rules ${pkgdir}/usr/lib/udev/rules.d/ 
}
md5sums=('87fd27773a84e97ac6e2dc28b08d682e'
         'dae0660f972510a6483992b5b81e1b5a'
         'dc4b2893651149eccbf7b2c8467cd288')
