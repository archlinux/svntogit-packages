# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=swell-foop
pkgver=3.20.0
pkgrel=1
pkgdesc="Clear the screen by removing groups of colored and shaped tiles"
arch=('i686' 'x86_64')
license=('GPL')
depends=('hicolor-icon-theme' 'clutter-gtk' 'librsvg')
makedepends=('intltool' 'itstool' 'gobject-introspection')
conflicts=('gnome-games')
replaces=('gnome-games')
options=('!emptydirs')
install=swell-foop.install
url="https://wiki.gnome.org/Apps/Swell%20Foop"
groups=('gnome-extra')
source=(https://download.gnome.org/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('40532a734dcae6d42a1e4692fd94e395442ff07c134acacf5fce3396c1e352a4')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
