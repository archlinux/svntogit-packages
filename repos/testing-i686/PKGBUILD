# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=networkmanager-vpnc
pkgver=1.2.4
pkgrel=2
pkgdesc="NetworkManager VPN plugin for VPNC"
url="https://wiki.gnome.org/Projects/NetworkManager"
arch=(i686 x86_64)
license=(GPL)
depends=(libnm libsecret vpnc)
makedepends=(libnm-gtk intltool python git)
_commit=f6969f0038438d4ef9320e275eb22df98592fe53  # tags/1.2.4^0
source=("git+https://git.gnome.org/browse/network-manager-vpnc#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd network-manager-vpnc
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd network-manager-vpnc
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd network-manager-vpnc
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib/NetworkManager --disable-static
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd network-manager-vpnc
  make DESTDIR="$pkgdir" install
}
