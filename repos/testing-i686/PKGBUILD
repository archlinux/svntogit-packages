# $Id$
# Maintainer: judd <jvinet@zeroflux.org>
pkgname=cpio
pkgver=2.11
pkgrel=6
pkgdesc="A tool to copy files into or out of a cpio or tar archive"
arch=(i686 x86_64)
license=('GPL')
url="http://www.gnu.org/software/cpio"
depends=('glibc')
source=(ftp://ftp.gnu.org/gnu/cpio/cpio-${pkgver}.tar.gz
        cpio-2.11-stdio.in.patch
        cpio-2.11-CVE-2014-9112.patch
        cpio-2.11-testsuite-CVE-2014-9112.patch
        cpio-2.11-check_for_symlinks-CVE-2015-1197.patch)
install=cpio.install
md5sums=('1112bb6c45863468b5496ba128792f6c'
         'd7e58f2a1ec286febd09ea75042cf96e'
         '2541e37b85cb7baffc3a3f687453737c'
         '348870bebae57146eafeb189adbd43a4'
         '31b5e1d853b85d58e55cee1d17fa42bf')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -Np1 -i ${srcdir}/cpio-2.11-stdio.in.patch
  # from fedora git
  patch -Np1 -i ${srcdir}/cpio-2.11-CVE-2014-9112.patch
  patch -Np1 -i ${srcdir}/cpio-2.11-testsuite-CVE-2014-9112.patch
  # fix CVE-2015-1197 #44173
  patch -Np1 -i ${srcdir}/cpio-2.11-check_for_symlinks-CVE-2015-1197.patch
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr --mandir=/usr/share/man
  make
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  rm -f ${pkgdir}/usr/bin/mt ${pkgdir}/pkg/usr/share/man/man1/mt.1
  rm -rf ${pkgdir}/usr/libexec
  # remove mt manpage it conflicts with mt-st from extra
  rm $pkgdir/usr/share/man/man1/mt.1 || return 1
  # remove infodir
  rm $pkgdir/usr/share/info/dir
}
