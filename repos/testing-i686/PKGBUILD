# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=baloo
pkgver=5.14.0
pkgrel=2
pkgdesc="A framework for searching and managing metadata"
arch=('i686' 'x86_64')
url='https://community.kde.org/Baloo'
license=('LGPL')
depends=('kfilemetadata' 'kidletime' 'kio' 'lmdb' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'kdoctools' 'python')
conflicts=('baloo-frameworks')
replaces=('baloo-frameworks')
provides=('baloo-frameworks')
groups=('kf5')
install="${pkgname}.install"
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"
  baloo-bug352454.patch::"https://quickgit.kde.org/?p=baloo.git&a=commitdiff&h=3312d6f&o=plain")
md5sums=('0e708135232432d371596d22ed864e3e'
         '3b61489b8e4a2910bced3fb7b8872f69')

prepare() {
  mkdir build

# Fix crash when Baloo is disabled https://bugs.kde.org/show_bug.cgi?id=352454
  cd $pkgname-$pkgver
  patch -p1 -i ../baloo-bug352454.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
