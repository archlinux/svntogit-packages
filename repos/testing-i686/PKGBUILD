# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=akonadi-contacts
pkgver=16.08.0
pkgrel=1
pkgdesc="Libraries and daemons to implement Contact Management in Akonadi"
arch=(i686 x86_64)
url="https://community.kde.org/KDE_PIM"
license=(LGPL)
depends=(akonadi grantlee-qt5 kcontacts kmime qt5-webengine) # prison
makedepends=(extra-cmake-modules kdesignerplugin kdoctools python boost akonadi-mime kcalcore)
conflicts=(akonadi-contact)
replaces=(akonadi-contact)
source=("http://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
md5sums=('01b20285974d54e96f3e9a0937a7235a')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
