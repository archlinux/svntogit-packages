# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=apr-util
pkgver=1.5.3
pkgrel=2
pkgdesc="The Apache Portable Runtime"
arch=('i686' 'x86_64')
url="http://apr.apache.org/"
depends=('apr' 'gdbm' 'expat' 'libldap' 'unixodbc')
license=('APACHE')
source=(http://www.apache.org/dist/apr/apr-util-${pkgver}.tar.bz2{,.asc})
md5sums=('6f3417691c7a27090f36e7cf4d94b36e'
         'SKIP')

build() {
  cd "${srcdir}/apr-util-${pkgver}"
  ./configure --prefix=/usr --with-apr=/usr \
    --with-ldap \
    --with-gdbm=/usr
  make
}

check() {
  cd "${srcdir}/apr-util-${pkgver}"
  make -j1 check
}

package() {
  cd "${srcdir}/apr-util-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
