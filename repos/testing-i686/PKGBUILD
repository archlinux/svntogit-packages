# Maintainer: Geoffroy Carrier <geoffroy@archlinux.org>

pkgname=bluez
pkgver=4.25
pkgrel=1
pkgdesc="Libraries and tools for the Bluetooth protocol stack"
url="http://www.bluez.org/"
arch=('i686' 'x86_64')
license=('GPL2')
depends=('dbus-core' 'glib')
makedepends=('gstreamer0.10-base' 'alsa-lib' 'libusb')
optdepends=('gstreamer0.10-base' 'alsa-lib' 'libusb')
conflicts=('bluez-libs' 'bluez-utils')
provides=('bluez-libs' 'bluez-utils')
replaces=('bluez-libs' 'bluez-utils')
backup=(etc/bluetooth/{main,rfcomm,audio,network,input}.conf
        etc/conf.d/bluetooth)
source=("http://www.kernel.org/pub/linux/bluetooth/$pkgname-$pkgver.tar.bz2"
        'bluetooth.conf.d'
        'rc.bluetooth')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --libexecdir=/lib \
        --mandir=/usr/share/man \
        --enable-gstreamer \
        --enable-alsa \
        --enable-usb \
        --enable-netlink \
        --enable-tools \
        --enable-bccmd \
        --enable-hid2hci \
        --enable-dfutool \
        --enable-hidd \
        --enable-pand \
        --enable-dund \
        --enable-cups \
        --enable-manpages \
        --enable-configfiles \
        --enable-pcmciarules || return 1
  make || return 1
  make DESTDIR="$pkgdir" install || return 1

  install -D -m644 "$srcdir/$pkgname-$pkgver/network/network.conf" "$pkgdir/etc/bluetooth/" || return 1
  install -D -m644 "$srcdir/$pkgname-$pkgver/input/input.conf"     "$pkgdir/etc/bluetooth/" || return 1
  install -D -m644 "$srcdir/$pkgname-$pkgver/audio/audio.conf"     "$pkgdir/etc/bluetooth/" || return 1
  install -D -m755 "$srcdir/rc.bluetooth"                          "$pkgdir/etc/rc.d/bluetooth" || return 1
  install -D -m644 "$srcdir/bluetooth.conf.d"                      "$pkgdir/etc/conf.d/bluetooth" || return 1
}

md5sums=('3d7b621b5b8a63f78f59e008b87c0738'
         '8593ea9cfa9814e2a22b19845b3c276c'
         '03d4f2463b1b580e8d2ada3767140685')
