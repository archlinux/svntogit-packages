# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=enchant
pkgver=1.6.0
pkgrel=8
pkgdesc="A wrapper library for generic spell checking"
arch=('i686' 'x86_64')
url="http://www.abisource.com/enchant/"
license=('LGPL')
depends=('aspell' 'dbus-glib' 'hunspell' 'hspell' 'libvoikko')
source=("http://www.abisource.com/downloads/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz"
       hunspell14_buildfix.diff)
md5sums=('de11011aff801dc61042828041fb59c7'
         '1305a6c285b5970bb1ef0f033f49f329')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # http://bugzilla.abisource.com/show_bug.cgi?id=13772
  patch -Np1 -i ${srcdir}/hunspell14_buildfix.diff
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
    --disable-static \
    --disable-ispell \
    --with-myspell-dir=/usr/share/myspell
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
