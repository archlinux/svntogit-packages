# $Id$
# Maintainer: Alexander Baldeck <alexander@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
pkgname=xorg-xdm
pkgver=1.1.11
pkgrel=3
pkgdesc="X Display Manager"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('pam' 'libxaw' 'libxinerama' 'xorg-xrdb' 'xorg-sessreg' 'libxft' 'consolekit')
makedepends=('pkgconfig' 'xorg-util-macros' 'systemd' 'xtrans')
backup=(etc/X11/xdm/Xaccess etc/X11/xdm/Xresources etc/X11/xdm/Xservers etc/X11/xdm/xdm-config etc/pam.d/xdm etc/X11/xdm/Xsetup_0 etc/X11/xdm/Xsession)
options=('!libtool')
source=(${url}/releases/individual/app/xdm-${pkgver}.tar.bz2
        Xsession-loginshell.patch
        Xsession-xsm.patch
        xdm-consolekit.patch
        xdm-1.0.5-sessreg-utmp-fix-bug177890.patch
        xdm.pam)
md5sums=('64a1af1f7eb69feae12e75d4cc3aaf19'
         '234ae568e7b22cbc09433bb396ff3d80'
         '2c30078c50bc653d72eb5114506eb313'
         '25c6d8adeed0796d432b2bbf1d83a7e9'
         '13032add23a0a8d48a638418094bfc00'
         '6de9362b1bf0835026270b20d8029909')
sha256sums=('d4da426ddea0124279a3f2e00a26db61944690628ee818a64df9d27352081c47'
            'fd3e7c20837b42a8ab111369fd6dc9612f9edb91c1f6904cca1d6a1fa3cfa0ff'
            '77a1ce9bdf363591b72798db08b4df3589bd4e64737fd32cf9028f9245450edb'
            '10ce850e6a93c6ba68b5d71104b1fd6680ad9760274068512f43a694b1edda76'
            '5f380a2d6f77feb910d77f7f6843fce9b00ff7610c159fc029ee44cc6c23a48a'
            '7d6818a1c1a44e9bd38774c035e03b0b831f6646681bc2bf556761aec7baf418')

build() {
  cd "${srcdir}/xdm-${pkgver}"
  patch -Np0 -i "${srcdir}/Xsession-loginshell.patch"
  patch -Np1 -i "${srcdir}/Xsession-xsm.patch"
  patch -Np1 -i "${srcdir}/xdm-consolekit.patch"
  patch -Np0 -i "${srcdir}/xdm-1.0.5-sessreg-utmp-fix-bug177890.patch"

  autoreconf -fi
  ./configure --prefix=/usr \
      --disable-xdm-auth \
      --disable-static \
      --with-xdmconfigdir=/etc/X11/xdm \
      --with-xdmscriptdir=/etc/X11/xdm \
      --with-pixmapdir=/usr/share/xdm/pixmaps
  make
}

package() {
  cd "${srcdir}/xdm-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/var/lib/xdm"
  install -m755 -d "${pkgdir}/etc/pam.d"
  install -m644 "${srcdir}/xdm.pam" "${pkgdir}/etc/pam.d/xdm"
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"

  sed -i -e 's/\/X11R6//g' "${pkgdir}"/etc/X11/xdm/*

  sed -i 's|^Alias=.*|Alias=display-manager.service|' \
    "$pkgdir/usr/lib/systemd/system/xdm.service"
}
