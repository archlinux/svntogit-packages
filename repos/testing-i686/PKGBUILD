# $Id$

pkgname=gd
pkgver=2.1.1
pkgrel=3
pkgdesc="Library for the dynamic creation of images by programmers"
arch=('i686' 'x86_64')
url="http://www.libgd.org/"
license=('custom')
depends=('fontconfig' 'libvpx' 'libxpm' 'libtiff')
makedepends=('git')
optdepends=('perl: bdftogd script')
source=("${pkgname}::git+https://github.com/libgd/libgd.git#tag=${pkgname}-${pkgver}"
        gd-2.1.1-libvpx-1.4.0.patch)
md5sums=('SKIP'
         '9114dd8259aaa88b0a09188fe7b19afc')

prepare() {
  cd ${pkgname}
  ./bootstrap.sh
  patch -p1 -i "${srcdir}/gd-2.1.1-libvpx-1.4.0.patch"
}

build() {
  cd ${pkgname}
  ./configure \
    --prefix=/usr \
    --disable-rpath \
    --with-vpx=/usr \
    --with-tiff=/usr
  make
}

check() {
  cd ${pkgname}
  make check
}

package() {
  cd ${pkgname}
  make DESTDIR="${pkgdir}" install
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
