# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=apr
pkgver=1.4.5
pkgrel=2
pkgdesc="The Apache Portable Runtime"
arch=('i686' 'x86_64')
url="http://apr.apache.org/"
depends=('util-linux-ng>=2.16')
options=('!libtool')
license=('APACHE')
source=(http://www.apache.org/dist/apr/apr-${pkgver}.tar.bz2)
md5sums=('8b53f5a5669d0597f2da889a2f576eb6')

build() {
  cd "${srcdir}/apr-${pkgver}"
  export apr_cv_accept4=no

  ./configure --prefix=/usr --includedir=/usr/include/apr-1 \
    --with-installbuilddir=/usr/share/apr-1/build \
    --enable-nonportable-atomics \
    --with-devrandom=/dev/urandom
  make
}

check() {
  cd "${srcdir}/apr-${pkgver}"
  make -j1 check
}

package() {
  cd "${srcdir}/apr-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
