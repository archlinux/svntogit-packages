# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>

pkgbase=lvm2
pkgname=('lvm2' 'device-mapper')
pkgver=2.02.54
_pkgverdm=1.02.39
_pkgverlvm=${pkgver}
pkgrel=1
arch=('i686' 'x86_64')
url="http://sourceware.org/lvm2/"
license=('GPL')
groups=('base')
source=(ftp://sources.redhat.com/pub/lvm2/LVM2.${_pkgverlvm}.tgz \
        lvm2_install lvm2_hook device-mapper.rules)
md5sums=('6a33416469652b01aa48cefbe9cac8c1' '40dccdb1044f00fc1e29ca549933d4bd'\
         'd6f3ca901747c386aa0d842e9ea64413' '065f32a36947e6183722ed9aef13dc0c')
sha1sums=('794fd5078f7f96eeb1ff1b9ddc47e37a13e8a75d' '86358b3360373dcd1d7e1bf009fe6e6d0c70cdc3'\
         '0c2a367f2217e6363e5e80257cbc7730d3841e42' '003f5cf654503dcc9c1ba7c11c7d6e77bf27bb8d')

build() {
  cd "${srcdir}/LVM2.${_pkgverlvm}"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --enable-readline \
    --enable-pkgconfig --enable-static_link --with-staticdir=/sbin \
    --enable-dmeventd --enable-cmdlib || return 1
  make || return 1
}

package_device-mapper() {
  pkgdesc="Device mapper userspace library and tools"
  url="http://sourceware.org/dm/"
  depends=('glibc' 'readline')

  cd "${srcdir}/LVM2.${_pkgverlvm}"
  make DESTDIR="${pkgdir}" sbindir="${pkgdir}/sbin" \
    libdir="${pkgdir}/lib" install_device-mapper || return 1
  # install the static libraries in /usr/lib
  rm "${pkgdir}"/lib/libdevmapper.a*
  install -D -m644 libdm/ioctl/libdevmapper.a "${pkgdir}/usr/lib/libdevmapper.a" || return 1
  rm "${pkgdir}"/lib/libdevmapper-event.a*
  install -D -m644 daemons/dmeventd/libdevmapper-event.a "${pkgdir}/usr/lib/libdevmapper-event.a" || return 1
  # install udev rule
  install -D -m644 "${srcdir}/device-mapper.rules" "${pkgdir}/lib/udev/rules.d/device-mapper.rules" || return 1
  # fix device-mapper link bug
  ln -sf /lib/libdevmapper.so "${pkgdir}/usr/lib/libdevmapper.so" || return 1
}

package_lvm2() {
  pkgdesc="Logical Volume Manager 2 utilities"
  depends=('bash' "device-mapper>=${_pkgverdm}")
  conflicts=('lvm')
  backup=('etc/lvm/lvm.conf')
  options=('!makeflags')

  cd "${srcdir}/LVM2.${_pkgverlvm}"
  make DESTDIR="${pkgdir}" sbindir="${pkgdir}/sbin" install_lvm2 || return 1
  rm "${pkgdir}"/usr/lib/liblvm2cmd.a*
  install -D -m644 tools/liblvm2cmd.a "${pkgdir}/usr/lib/liblvm2cmd.a" || return 1
  install -d "${pkgdir}"/etc/lvm/{archive,backup} || return 1
  # add hook
  install -D -m644 "${srcdir}/lvm2_hook" "${pkgdir}/lib/initcpio/hooks/lvm2" || return 1
  install -D -m644 "${srcdir}/lvm2_install" "${pkgdir}/lib/initcpio/install/lvm2" || return 1
}
