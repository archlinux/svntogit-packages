# $Id$
# Maintainer: Tom Gundersen <teg@jklm.no>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=libtirpc
pkgver=0.2.3
pkgrel=1
pkgdesc="Transport Independent RPC library (SunRPC replacement)"
arch=('i686' 'x86_64')
url="http://libtirpc.sourceforge.net/"
license=('BSD')
depends=('libgssglue')
backup=('etc/netconfig')
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/libtirpc/${pkgname}-${pkgver}.tar.bz2
       libtirpc-build.patch)

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -p1 <"$srcdir/libtirpc-build.patch"

  ./configure --prefix=/usr --sysconf=/etc --enable-gss

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -D -m644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
md5sums=('b70e6c12a369a91e69fcc3b9feb23d61'
         'd4c0eef2f0891abf06cd4146eb51ed36')
