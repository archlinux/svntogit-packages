# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=grilo-plugins
pkgver=0.1.18
pkgrel=3
pkgdesc="Plugins for Grilo"
url="http://www.gnome.org"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('grilo')
makedepends=('gupnp-av' 'libgdata' 'libquvi' 'sqlite3' 'gmime' 'libgcrypt'
             'rest' 'libtracker-sparql' 'gnome-common')
optdepends=('gupnp-av: uPnP plugin'
            'libgdata: Youtube plugin'
            'libquvi: Youtube plugin'
            'sqlite3: Podcasts plugin'
            'gmime: Podcasts plugin'
            'sqlite3: Bookmarks plugin'
            'sqlite3: Metadata store plugin'
            'libgcrypt: Vimeo plugin'
            'rest: Blip.tv plugin'
            'libtracker-sparql: Tracker plugin')
options=('!libtool' '!emptydirs')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz
        tracker-0.14.patch)
sha256sums=('7e382f402119f4f270380627a2f49b30a6c43a47ecd645bf5ffe4e0cd99a1c79'
            'c48201ed1633e2b0eb5132ef823d7b060e9c2cddb808c6981fb546133cd4ea63')

build() {
  cd $pkgname-$pkgver
  
  patch -Np1 -i ../tracker-0.14.patch

  gnome-autogen.sh --prefix=/usr --sysconfdir=/etc --disable-static \
    --enable-shoutcast
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
