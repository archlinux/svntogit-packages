# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=plasma-sdk
pkgver=5.6.0
pkgrel=1
pkgdesc="Applications useful for Plasma development"
arch=(i686 x86_64)
url='https://projects.kde.org/projects/extragear/sdk/plasma-sdk'
license=(LGPL)
depends=(plasma-framework ktexteditor)
makedepends=(extra-cmake-modules qt5-webkit kitemmodels knewstuff)
groups=(plasma)
source=("http://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz")
md5sums=('7405dabf94dbafaae354b188bc6dfcb7')

prepare() {
  mkdir -p build
}    

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
