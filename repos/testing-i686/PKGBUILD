# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=freetype2
pkgname=('freetype2' 'freetype2-demos')
pkgver=2.8
pkgrel=1
arch=(i686 x86_64)
license=('GPL')
url="https://www.freetype.org/"
# adding harfbuzz for improved OpenType features auto-hinting
# introduces a cycle dep to harfbuzz depending on freetype wanted by upstream
makedepends=('libx11' 'zlib' 'bzip2' 'sh' 'libpng' 'harfbuzz')
source=(https://download-mirror.savannah.gnu.org/releases/freetype/freetype-${pkgver}.tar.bz2{,.sig}
        https://download-mirror.savannah.gnu.org/releases/freetype/freetype-doc-${pkgver}.tar.bz2{,.sig}
        https://download-mirror.savannah.gnu.org/releases/freetype/ft2demos-${pkgver}.tar.bz2{,.sig}
        0001-Enable-table-validation-modules.patch
        0002-Enable-subpixel-rendering.patch
        0003-Enable-infinality-subpixel-hinting.patch
        0005-freetype-2.5.2-more-demos.patch
        freetype2.sh)
sha1sums=('42c6b1f733fe13a3eba135f5025b22cb68450f91'
          'SKIP'
          '5b221ee14fe674cd5f6db0193d55360bc0bd3655'
          'SKIP'
          '7849f1ac4a352971fab61a5fc2e5c6a597201201'
          'SKIP'
          'b31882ef5e8447e761acee1c4a44c0630cd4d465'
          'b1494810ed3aca25cdd8e8cedf634e5adfe6c09e'
          '41d27140fd590945e22e012c9dce62de3d6f11e6'
          '72cfecbe738085eec475e012617661ad0cc9b76f'
          'bc6df1661c4c33e20f5ce30c2da8ad3c2083665f')
validpgpkeys=('58E0C111E39F5408C5D3EC76C1A60EACE707FDA5')

prepare() {
  # Rename source dir to allow building the demos
  mv freetype-${pkgver} freetype2

  cd freetype2
  patch -Np1 -i ../0001-Enable-table-validation-modules.patch
  patch -Np1 -i ../0002-Enable-subpixel-rendering.patch
  patch -Np1 -i ../0003-Enable-infinality-subpixel-hinting.patch

  cd ../ft2demos-${pkgver}
  # enable more demos
  patch -Np1 -i ../0005-freetype-2.5.2-more-demos.patch

  # Suppress RPATH
  sed -i '/X11_LIB:%=-R%/d' graph/x11/rules.mk
}

build() {
  cd freetype2
  ./configure --prefix=/usr --disable-static
  make

  # Build demos
  cd ../ft2demos-${pkgver}
  make
}

check() {
  cd freetype2
  make -k check
}

package_freetype2() {
  pkgdesc="TrueType font rendering library"
  depends=('zlib' 'bzip2' 'sh' 'libpng' 'harfbuzz')
  provides=('libfreetype.so')
  install=freetype2.install
  backup=('etc/profile.d/freetype2.sh')

  cd freetype2
  make DESTDIR="${pkgdir}" install
  install -Dm644 ../freetype2.sh "${pkgdir}/etc/profile.d/freetype2.sh"

  # Package docs
  install -dm755 "${pkgdir}/usr/share/doc"
  cp -a docs "${pkgdir}/usr/share/doc/${pkgname}"

  cd ../ft2demos-${pkgver}
  mkdir -p $srcdir/ft-demos
  for _i in bin/{f,t}t*; do
    libtool --mode=install install $_i "$srcdir/ft-demos"
  done
}

package_freetype2-demos() {
  pkgdesc="Freetype tools and demos"
  depends=('freetype2' 'libx11')

  install -dm755 "${pkgdir}/usr/bin"
  cp -a $srcdir/ft-demos/* "${pkgdir}/usr/bin"
}

# vim:set ts=2 sw=2 et:
