# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=make
pkgver=3.82
pkgrel=3
pkgdesc="GNU make utility to maintain groups of programs"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/make"
license=('GPL3')
groups=('base-devel')
depends=('glibc' 'sh')
install=$pkgname.install
source=(ftp://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.bz2
        bug30612.patch
        bug30723.patch
        make-3.82-sort-blank.patch)
md5sums=('1a11100f3c63fcf5753818e59d63088f'
         'c8f496b22191f9fb9420ab14c1a19a47'
         '662e6450e19a5acdaa5c9fcb8ad78dea'
         '7d01a99f389d8f08dec93ed479071ee4')


build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  
  # http://savannah.gnu.org/bugs/?30612
  patch -Np1 -i $srcdir/bug30612.patch
  
  # http://savannah.gnu.org/bugs/?30723
  patch -Np1 -i $srcdir/bug30723.patch
  
  # https://bugs.archlinux.org/task/22733 (fix from Fedora)
  patch -Np1 -i $srcdir/make-3.82-sort-blank.patch
  
  ./configure --prefix=/usr
  make
  make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
