# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kross
pkgver=5.14.0
pkgrel=1
pkgdesc='Multi-language application scripting'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kross'
license=('LGPL')
depends=('kparts')
makedepends=('extra-cmake-modules' 'kdoctools' 'qt5-tools')
groups=('kf5-aids')
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/portingAids/${pkgname}-${pkgver}.tar.xz")
md5sums=('057ed7d554d16bb5439496e0537c2ed3')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
