# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: laloch <laloch@atlas.cz>

pkgbase=qt-gstreamer
pkgname=(qt4-gstreamer qt5-gstreamer)
pkgver=1.2.0
pkgrel=2
arch=(i686 x86_64)
url='http://gstreamer.freedesktop.org/modules/qt-gstreamer.html'
license=(LGPL2.1)
makedepends=(cmake boost mesa doxygen gst-plugins-good qt4 qt5-declarative)
source=("http://gstreamer.freedesktop.org/src/$pkgbase/$pkgbase-$pkgver.tar.xz"
	qt-gstreamer-1.6.patch::"http://cgit.freedesktop.org/gstreamer/qt-gstreamer/patch/?id=e2ca8094")
md5sums=('fd794045a828c184acc1794b08a463fd'
         'e39d0c9ba100b18b230d16ff22d8db5d')

prepare() {
  mkdir -p build{4,5}

# Fix build with gstreamer 1.6
  cd $pkgbase-$pkgver
  patch -p1 -i ../qt-gstreamer-1.6.patch
}

build() {
  cd build4
  cmake ../$pkgbase-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DQTGSTREAMER_EXAMPLES=OFF \
    -DQT_VERSION=4
  make

  cd ../build5
  cmake ../$pkgbase-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DQTGSTREAMER_EXAMPLES=OFF \
    -DQT_VERSION=5
  make
}

package_qt4-gstreamer() {
  pkgdesc="Qt4 bindings for GStreamer"
  depends=(qt4 gst-plugins-good)
  conflicts=(qt-gstreamer)
  provides=(qt-gstreamer)
  replaces=(qt-gstreamer)

  cd build4
  make DESTDIR="$pkgdir" install
}

package_qt5-gstreamer() {
  pkgdesc="Qt5 bindings for GStreamer"
  depends=(qt5-declarative gst-plugins-good)

  cd build5
  make DESTDIR="$pkgdir" install
}
