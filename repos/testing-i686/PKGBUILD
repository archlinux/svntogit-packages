# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=networkmanager-qt
pkgver=5.16.0
pkgrel=1
pkgdesc='Qt wrapper for NetworkManager API'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/networkmanager-qt'
license=('LGPL')
depends=('networkmanager' 'qt5-base')
makedepends=('extra-cmake-modules')
groups=('kf5')
replaces=('libnm-qt5')
conflicts=('libnm-qt5')
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
md5sums=('f83788edbad653e6e825898e7c4d431d')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
