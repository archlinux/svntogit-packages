# $Id$
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>
pkgname=cryptsetup
pkgver=1.6.5
pkgrel=2
pkgdesc="Userspace setup tool for transparent encryption of block devices using dm-crypt"
arch=(i686 x86_64)
license=('GPL')
url="http://code.google.com/p/cryptsetup/"
groups=('base')
depends=('device-mapper' 'libgcrypt' 'popt' 'libutil-linux')
makedepends=('util-linux')
options=('!emptydirs')
source=(https://www.kernel.org/pub/linux/utils/cryptsetup/v1.6/${pkgname}-${pkgver}.tar.xz
        #https://www.kernel.org/pub/linux/utils/cryptsetup/v1.6/${pkgname}-${pkgver}.tar.sign
        encrypt_hook
        encrypt_install
        sd-encrypt
        0001-Move-safe-table-params-wipe-into-function-which-allo.patch
        0002-Re-check-flags-after-DM-device-creations.patch
        0003-Properly-allow-activation-of-discard-even-if-dm_cryp.patch)
sha256sums=('267973f20be43f9d685f7193aa23954b60768c74a1d330243114d4b8bc17ca9a'
            '4406f8dc83f4f1b408e49d557515f721d91b358355c71fbe51f74ab27e5c84ff'
            'cfe465bdad3d958bb2332a05e04f2e1e884422a5714dfd1a0a3b9b74bf7dc6ae'
            'd442304e6a78b3513ebc53be3fe2f1276a7df470c8da701b3ece971d59979bdd'
            '54895ebba0cf1df27da7a53b6a256b7826f9b2d4cff577f840ab457dc32b5c49'
            'd022e43fa96ee60e295cf6041a8b9e15225eeb03b713f32675061c4063c17ea5'
            '16d55306a5dde637db16cb219834095432bc8579528789ee52aa975df12284d4')

prepare() {
  cd "${srcdir}"/$pkgname-${pkgver}
  patch -p1 -i "${srcdir}"/0001-Move-safe-table-params-wipe-into-function-which-allo.patch
  patch -p1 -i "${srcdir}"/0002-Re-check-flags-after-DM-device-creations.patch
  patch -p1 -i "${srcdir}"/0003-Properly-allow-activation-of-discard-even-if-dm_cryp.patch
}

build() {
  cd "${srcdir}"/$pkgname-${pkgver}
  ./configure --prefix=/usr --sbindir=/usr/bin --disable-static --enable-cryptsetup-reencrypt
  make
}

package() {
  cd "${srcdir}"/$pkgname-${pkgver}
  make DESTDIR="${pkgdir}" install
  # install hook
  install -D -m644 "${srcdir}"/encrypt_hook "${pkgdir}"/usr/lib/initcpio/hooks/encrypt
  install -D -m644 "${srcdir}"/encrypt_install "${pkgdir}"/usr/lib/initcpio/install/encrypt
  install -D -m644 "${srcdir}"/sd-encrypt "${pkgdir}"/usr/lib/initcpio/install/sd-encrypt
}
