# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: bzklrm <bzklrm@gmail.com>
# Additional contributors Senjin, Xavier, dundee

pkgname=glpk
pkgver=4.57
pkgrel=2
pkgdesc="GNU Linear Programming Kit : solve LP, MIP and other problems."
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/glpk/glpk.html"
license=('GPL')
depends=('gmp')
source=(http://ftp.gnu.org/gnu/glpk/${pkgname}-${pkgver}.tar.gz{,.sig})
sha1sums=('d2df2de59798c35024df8d140a53453e29a095be'
          'SKIP')
validpgpkeys=('A2B56CCB42220C06481335BAD17BF2305981E818') # Andrew Makhorin

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr --with-gmp

  if [ "$CARCH" == "x86_64" ]; then
    make CFLAGS="$CFLAGS -fPIC"
  else
    make
  fi
}

package(){
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 doc/glpk.pdf "${pkgdir}/usr/share/doc/glpk/glpk.pdf"
  install -Dm644 doc/gmpl.pdf "${pkgdir}/usr/share/doc/glpk/gmpl.pdf"
}
