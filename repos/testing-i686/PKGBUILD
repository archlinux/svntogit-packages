# Maintainer : Tobias Powalowski <tpowa@archlinux.org>
# Maintainer : Thomas BÃ¤chler <thomas@archlinux.org>
# Contributor: Keshav Amburay <(the ddoott ridikulus ddoott rat) (aatt) (gemmaeiil) (ddoott) (ccoomm)>

## "1" to enable IA32-EFI build in Arch x86_64, "0" to disable
_IA32_EFI_IN_ARCH_X64="1"

_VER="6.03-pre11"
_GIT_TAG="syslinux-${_VER}"

pkgname="syslinux"
pkgver="${_VER/-/}"
pkgrel="2"
arch=('x86_64' 'i686')
pkgdesc="Collection of boot loaders that boot from FAT, ext2/3/4 and btrfs filesystems, from CDs and via PXE"
url="http://syslinux.zytor.com/"
license=('GPL2')
options=('!makeflags' '!emptydirs')
backup=('boot/syslinux/syslinux.cfg')
makedepends=('git' 'python2' 'nasm' 'ncurses')
depends=('perl' 'glibc')
optdepends=('perl-passwd-md5:  For md5pass'
            'perl-digest-sha1: For sha1pass'
            'mtools:           For mkdiskimage and syslinux support'
            'gptfdisk:         For GPT support'
            'util-linux:       For isohybrid'
            'efibootmgr:       For EFI support'
            'dosfstools:       For EFI support')
install="${pkgname}.install"
source=("${pkgname}::git+http://git.zytor.com/syslinux/syslinux.git#tag=${_GIT_TAG}"
        "gnu-efi::git+http://git.code.sf.net/p/gnu-efi/code#commit=3c62e78556aea01e9798380cd46794c6ca09d4bd"
        'syslinux.cfg'
        'syslinux-install_update')

md5sums=('SKIP'
         'SKIP'
         '46ca150f53322ff8f1597d9a342f7e40'
         '9376f18fa3e42fc36cffa4cff0a84c09')

_pkgver() {
    cd "${srcdir}/${pkgname}/"
    echo "$(git describe --tags)" | sed -e 's|syslinux-||g' -e 's|-pre|pre|g' -e 's|-|.|g'
}

prepare() {
    
    mv "${srcdir}/${pkgname}" "${srcdir}/${pkgname}-${pkgver}/"
    cd "${srcdir}/${pkgname}-${pkgver}/"
    
    msg "Run git clean"
    git clean -x -d -f
    
    msg "Do not try to build the Windows or DOS installers and DIAG files"
    sed 's|diag libinstaller dos win32 win64 dosutil txt|libinstaller txt|g' -i "${srcdir}/${pkgname}-${pkgver}/Makefile" || true
    sed 's|win32/syslinux.exe win64/syslinux64.exe||g' -i "${srcdir}/${pkgname}-${pkgver}/Makefile" || true
    sed 's|dosutil/*.com dosutil/*.sys||g' -i "${srcdir}/${pkgname}-${pkgver}/Makefile" || true
    sed 's|dos/syslinux.com||g' -i "${srcdir}/${pkgname}-${pkgver}/Makefile" || true
    sed 's|INSTALLSUBDIRS = com32 utils dosutil|INSTALLSUBDIRS = com32 utils|g' -i "${srcdir}/${pkgname}-${pkgver}/Makefile" || true
    sed 's|install -m 644 -c $(INSTALL_DIAG) $(INSTALLROOT)$(DIAGDIR)|# install -m 644 -c $(INSTALL_DIAG) $(INSTALLROOT)$(DIAGDIR)|g' -i "${srcdir}/${pkgname}-${pkgver}/Makefile" || true
    
    msg "Fix FHS manpage path"
    sed 's|/usr/man|/usr/share/man|g' -i "${srcdir}/${pkgname}-${pkgver}/mk/syslinux.mk" || true
    
    cd "${srcdir}/gnu-efi/"
    
    msg "Run git clean for gnu-efi"
    git clean -x -d -f
    
    msg "Revert gnu-efi Makefile 'make install' problamatic commit"
    git revert --no-commit 06744d69273de4945cf0ffcaa4a6abf7cec707b6
    
    msg "Prepare gnu-efi source"
    cp -r "${srcdir}/gnu-efi/gnu-efi-3.0" "${srcdir}/${pkgname}-${pkgver}/gnu-efi/gnu-efi-3.0"
    
    cd "${srcdir}/${pkgname}-${pkgver}/"
    
}

_build_syslinux_bios() {
    
    rm -rf "${srcdir}/${pkgname}-${pkgver}-bios/" || true
    cp -r "${srcdir}/${pkgname}-${pkgver}" "${srcdir}/${pkgname}-${pkgver}-bios"
    
    mkdir -p "${srcdir}/${pkgname}-${pkgver}-bios/OBJDIR"
    cd "${srcdir}/${pkgname}-${pkgver}-bios/"
    
    msg "Do not try to compile bios build with our default LDFLAGS, it will fail"
    unset LDFLAGS
    
    msg "Run make bios"
    make PYTHON="python2" OBJDIR="${srcdir}/${pkgname}-${pkgver}-bios/OBJDIR" bios
    
    msg "Run make bios installer"
    make PYTHON="python2" OBJDIR="${srcdir}/${pkgname}-${pkgver}-bios/OBJDIR" bios installer
    
}

_build_syslinux_efi64() {
    
    rm -rf "${srcdir}/${pkgname}-${pkgver}-efi64/" || true
    cp -r "${srcdir}/${pkgname}-${pkgver}" "${srcdir}/${pkgname}-${pkgver}-efi64"
    
    mkdir -p "${srcdir}/${pkgname}-${pkgver}-efi64/OBJDIR/efi64/"
    cd "${srcdir}/${pkgname}-${pkgver}-efi64/gnu-efi/gnu-efi-3.0/"
    
    msg "Unset all compiler FLAGS for gnu-efi efi64 build"
    unset CFLAGS
    unset CPPFLAGS
    unset CXXFLAGS
    unset LDFLAGS
    unset MAKEFLAGS
    
    msg "Run make gnu-efi for syslinux efi64"
    make ARCH="x86_64" -j1
    
    msg "Run make install gnu-efi for syslinux efi64"
    make ARCH="x86_64" PREFIX="${srcdir}/${pkgname}-${pkgver}-efi64/OBJDIR/efi64/" -j1 install
    
    cd "${srcdir}/${pkgname}-${pkgver}-efi64/"
    
    msg "Unset all compiler FLAGS for efi64 build"
    unset CFLAGS
    unset CPPFLAGS
    unset CXXFLAGS
    unset LDFLAGS
    unset MAKEFLAGS
    
    msg "Run make efi64"
    make PYTHON="python2" OBJDIR="${srcdir}/${pkgname}-${pkgver}-efi64/OBJDIR" efi64
    
    msg "Run make efi64 installer"
    make PYTHON="python2" OBJDIR="${srcdir}/${pkgname}-${pkgver}-efi64/OBJDIR" efi64 installer
    
}

_build_syslinux_efi32() {
    
    rm -rf "${srcdir}/${pkgname}-${pkgver}-efi32/" || true
    cp -r "${srcdir}/${pkgname}-${pkgver}" "${srcdir}/${pkgname}-${pkgver}-efi32"
    
    mkdir -p "${srcdir}/${pkgname}-${pkgver}-efi32/OBJDIR/efi32/"
    cd "${srcdir}/${pkgname}-${pkgver}-efi32/gnu-efi/gnu-efi-3.0/"
    
    msg "Unset all compiler FLAGS for gnu-efi efi32 build"
    unset CFLAGS
    unset CPPFLAGS
    unset CXXFLAGS
    unset LDFLAGS
    unset MAKEFLAGS
    
    msg "Run make gnu-efi for syslinux efi32"
    make ARCH="ia32" -j1
    
    msg "Run make install gnu-efi for syslinux efi32"
    make ARCH="ia32" PREFIX="${srcdir}/${pkgname}-${pkgver}-efi32/OBJDIR/efi32/" install
    
    cd "${srcdir}/${pkgname}-${pkgver}-efi32/"
    
    msg "Unset all compiler FLAGS for efi32 build"
    unset CFLAGS
    unset CPPFLAGS
    unset CXXFLAGS
    unset LDFLAGS
    unset MAKEFLAGS
    
    msg "Run make efi32"
    make PYTHON="python2" OBJDIR="${srcdir}/${pkgname}-${pkgver}-efi32/OBJDIR" efi32
    
    msg "Run make efi32 installer"
    make PYTHON="python2" OBJDIR="${srcdir}/${pkgname}-${pkgver}-efi32/OBJDIR" efi32 installer
    
}

build() {
    
    cd "${srcdir}/${pkgname}-${pkgver}/"
    
    if [[ "${CARCH}" == "x86_64" ]]; then
        msg "Build syslinux efi64"
        _build_syslinux_efi64
        
        if [[ "${_IA32_EFI_IN_ARCH_X64}" == "1" ]]; then
            msg "Build syslinux efi32"
            _build_syslinux_efi32
        fi
    fi
    
    if [[ "${CARCH}" == "i686" ]]; then
        msg "Build syslinux efi32"
        _build_syslinux_efi32
    fi
    
    msg "Build syslinux bios"
    _build_syslinux_bios
    
}

_package_syslinux_bios() {
    
    cd "${srcdir}/${pkgname}-${pkgver}-bios/"
    
    msg "Install Syslinux bios"
    make INSTALLROOT="${pkgdir}/" AUXDIR="/usr/lib/syslinux/bios/" OBJDIR="${srcdir}/${pkgname}-${pkgver}-bios/OBJDIR" bios install
    
    msg "Remove syslinux.exe,syslinux64.exe,syslinux.com and dosutil dir"
    rm "${pkgdir}/usr/lib/syslinux/bios"/syslinux.{com,exe} || true
    rm "${pkgdir}/usr/lib/syslinux/bios/syslinux64.exe" || true
    rm -rf "${pkgdir}/usr/lib/syslinux/bios/dosutil/" || true
    
    msg "Remove com32 and diag dirs"
    rm -rf "${pkgdir}/usr/lib/syslinux/bios/diag/" || true
    rm -rf "${pkgdir}/usr/lib/syslinux/bios/com32/" || true
    
    msg "Move extlinux binary to /usr/bin"
    install -d "${pkgdir}/usr/bin"
    mv "${pkgdir}/sbin/extlinux" "${pkgdir}/usr/bin/extlinux"
    rm -rf "${pkgdir}/sbin/"
    
    msg "Install syslinux docs"
    install -d "${pkgdir}/usr/share/doc"
    cp -ar "${srcdir}/${pkgname}-${pkgver}/doc" "${pkgdir}/usr/share/doc/syslinux"
    
    msg "Install syslinux.cfg"
    install -D -m0644 "${srcdir}/syslinux.cfg" "${pkgdir}/boot/syslinux/syslinux.cfg"
    
    msg "Install the syslinux-install_update script"
    ## This script is maintained at https://gist.github.com/pyther/772138
    ## Script not yet updated for syslinux-efi
    install -D -m0755 "${srcdir}/syslinux-install_update" "${pkgdir}/usr/bin/syslinux-install_update"
    
}

_package_syslinux_efi() {
    
    cd "${srcdir}/${pkgname}-${pkgver}/"
    
    if [[ "${CARCH}" == "x86_64" ]]; then
        cd "${srcdir}/${pkgname}-${pkgver}-efi64/"
        msg "Install Syslinux efi64"
        make INSTALLROOT="${pkgdir}/" AUXDIR="/usr/lib/syslinux/" OBJDIR="${srcdir}/${pkgname}-${pkgver}-efi64/OBJDIR" efi64 install
        
        if [[ "${_IA32_EFI_IN_ARCH_X64}" == "1" ]]; then
            cd "${srcdir}/${pkgname}-${pkgver}-efi32/"
            msg "Install Syslinux efi32"
            make INSTALLROOT="${pkgdir}/" AUXDIR="/usr/lib/syslinux/" OBJDIR="${srcdir}/${pkgname}-${pkgver}-efi32/OBJDIR" efi32 install
        fi
    fi
    
    if [[ "${CARCH}" == "i686" ]]; then
        cd "${srcdir}/${pkgname}-${pkgver}-efi32/"
        msg "Install Syslinux efi32"
        make INSTALLROOT="${pkgdir}/" AUXDIR="/usr/lib/syslinux/" OBJDIR="${srcdir}/${pkgname}-${pkgver}-efi32/OBJDIR" efi32 install
    fi
    
}

package() {
    
    cd "${srcdir}/${pkgname}-${pkgver}/"
    
    msg "Package Syslinux efi"
    _package_syslinux_efi
    
    msg "Package Syslinux bios"
    _package_syslinux_bios
    
}
