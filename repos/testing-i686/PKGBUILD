# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-power-manager
pkgver=2.24.0
pkgrel=1
pkgdesc="Session daemon that makes it easy to manage your laptop or desktop system."
arch=(i686 x86_64)
url="http://www.gnome.org/projects/gnome-power-manager/"
license=('GPL')
depends=('libnotify>=0.4.4' 'gnome-panel>=2.24.0' 'gstreamer0.10>=0.10.20' 'policykit-gnome>=0.9.2')
makedepends=('gettext' 'perlxml' 'pkgconfig' 'gnome-doc-utils>=0.14.0')
options=(!emptydirs)
install=gnome-power-manager.install
groups=(gnome-extra)
source=(http://ftp.gnome.org/pub/GNOME/sources/gnome-power-manager/2.24/${pkgname}-${pkgver}.tar.bz2)
md5sums=('074d67b0c229ba0f792bcac6dd775d11')

build() {
  cd ${startdir}/src/${pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var \
	      --libexecdir=/usr/lib/gnome-power-manager \
	      --enable-policykit \
	      --with-dpms-ext \
              --disable-scrollkeeper \
              --with-dbus-sys=/etc/dbus-1/system.d \
              --with-dbus-services=/usr/share/dbus-1/services \
              --enable-applets || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR=${startdir}/pkg install || return 1

  install -m755 -d ${startdir}/pkg/usr/share/gconf/schemas
  gconf-merge-schema ${startdir}/pkg/usr/share/gconf/schemas/${pkgname}.schemas ${startdir}/pkg/etc/gconf/schemas/*.schemas || return 1
  rm -f ${startdir}/pkg/etc/gconf/schemas/*.schemas
}
