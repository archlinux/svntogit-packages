# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=folks
pkgver=0.9.5
pkgrel=1
pkgdesc="Library to aggregates people into metacontacts"
arch=(i686 x86_64)
url="http://telepathy.freedesktop.org/wiki/Folks"
license=(LGPL2.1)
depends=(telepathy-glib libgee libxml2 evolution-data-server zeitgeist libtracker-sparql)
makedepends=(gobject-introspection intltool vala)
install=folks.install
options=('!libtool' '!emptydirs')
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('924c440f16a8c9b0d0d832588fa77a1553fa2a5d2659c4c7d3178a7ef42222af')

build() {
  cd "$pkgname-$pkgver"
  export PKG_CONFIG_PATH="${srcdir}/${pkgname}-${pkgver}/folks"
  ./configure --prefix=/usr --disable-schemas-compile --enable-tracker-backend \
    --disable-libsocialweb-backend --disable-fatal-warnings
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
