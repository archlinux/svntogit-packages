# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=gjs
pkgver=1.34.0
pkgrel=1
pkgdesc="Javascript Bindings for GNOME"
arch=('i686' 'x86_64')
url="http://live.gnome.org/Gjs"
license=('GPL')
depends=('cairo' 'dbus-glib' 'gobject-introspection' 'js')
options=('!libtool')
source=(http://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('3d62c717a0522a36c6faa1eb021872255e3cd285a72786afe23b7cca2e3084cb')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's|python|python2|' scripts/make-tests
  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
