# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributer: Jan de Groot <jgc@archlinux.org>

pkgname=gmp
pkgver=6.0.0
pkgrel=2
pkgdesc="A free library for arbitrary precision arithmetic"
arch=('i686' 'x86_64')
url="http://gmplib.org/"
depends=('gcc-libs' 'sh')
license=('LGPL3' 'GPL')
install=gmp.install
source=(https://gmplib.org/download/gmp/gmp-${pkgver}a.tar.xz{,.sig})
md5sums=('1e6da4e434553d2811437aa42c7f7c76'
         'SKIP')
validpgpkeys=('343C2FF0FBEE5EC2EDBEF399F3599FF828C67298')   # Niels MÃ¶ller

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --build=${CHOST} \
    --prefix=/usr \
    --enable-fat --enable-cxx
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
