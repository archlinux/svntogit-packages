# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=bluedevil
pkgver=2.0
pkgrel=2
epoch=1
pkgdesc='KDE bluetooth framework'
arch=('i686' 'x86_64')
url="https://projects.kde.org/projects/extragear/base/bluedevil"
license=('GPL')
depends=('kdebase-runtime' 'libbluedevil')
makedepends=('cmake' 'automoc4')
optdepends=('kdebase-workspace: to run the plasmoid')
install="${pkgname}.install"
source=("http://download.kde.org/stable/${pkgname}/${pkgver}/src/${pkgname}-${pkgver}.tar.xz"
        'KDEBUG-337193.patch')
md5sums=('c318b94d6de3d516598208e32cbccad6'
         '9b89d5741c951b3d940faa4cfda0a738')

prepare() {
  mkdir build

  cd ${pkgname}-${pkgver}
  patch -p1 -i "${srcdir}"/KDEBUG-337193.patch
}

build(){
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
