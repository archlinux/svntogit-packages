# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kcalc
pkgver=15.12.3
pkgrel=2
pkgdesc='Scientific Calculator'
url='http://kde.org/applications/utilities/kcalc/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kinit')
makedepends=('extra-cmake-modules' 'python' 'kdoctools')
conflicts=('kdeutils-kcalc')
replaces=('kdeutils-kcalc')
groups=('kde-applications' 'kdeutils')
install=${pkgname}.install
source=("http://download.kde.org/stable/applications/${pkgver}/src/kcalc-${pkgver}.tar.xz" kcalc-qt5.6.patch)
sha1sums=('3a702934281d8cece1efd877b344f14f51e9ce36'
          'd22bc7c30802cf7d7bec8d9da9049679ad5b754c')

prepare() {
  mkdir -p build

# Workaround for null size window with Qt 5.6 (Fedora) https://bugs.kde.org/show_bug.cgi?id=360105
  cd $pkgname-$pkgver
  patch -p1 -i ../kcalc-qt5.6.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
