# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=elisa
pkgver=21.08.0
pkgrel=2
pkgdesc='A simple music player aiming to provide a nice experience for its users'
url='https://apps.kde.org/elisa/'
arch=(x86_64)
license=(LGPL3)
groups=(kde-applications kde-multimedia)
depends=(baloo kirigami2 kdeclarative vlc) # upnp-player-qt
makedepends=(extra-cmake-modules kdoctools)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        https://invent.kde.org/multimedia/elisa/-/commit/618cf9b589ef9dd58e3ec3dd7450a80f10e118f8.patch)
sha256sums=('d57b46c0d1518b50f2875c5eca20006389f64e91560146fda7f0a87852e7f493'
            'SKIP'
            'd6c116d8769c8a2593e4e9955085fefdcfbb68d1efe421478513db859735564e')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

prepare() {
  # Upstream suggested backport to fix broken "Files" view
  patch -d $pkgname-$pkgver -p1 < 618cf9b589ef9dd58e3ec3dd7450a80f10e118f8.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF 
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
