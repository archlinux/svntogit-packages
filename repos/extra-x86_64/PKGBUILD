# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=libmpdclient
pkgver=2.20
pkgrel=3
pkgdesc="C library to implement a MPD client"
arch=(x86_64)
url="https://www.musicpd.org/libs/libmpdclient/"
license=(BSD)
depends=(glibc)
makedepends=(meson)
provides=(libmpdclient.so)
source=(https://www.musicpd.org/download/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz{,.sig})
sha512sums=('3271c8eae017402976dbfabc51c7cb8d39912c8a3fd6cd075adc978d490f2eb850d86ccec9f7bccbda48c9d6ca1cda89918ac7c76b21f7f63ca1a276b67e94b6'
            'SKIP')
b2sums=('d80c343d7655ccddb253275694d40665d286bf9c59729d0c836c927ab479b1ab4819f36a69c62a29c3ad220fcbfa712da5418cf8c3cc9f9e24af4e3c39ce1e11'
        'SKIP')
validpgpkeys=('0392335A78083894A4301C43236E8A58C6DB4512') # Max Kellermann <max@blarg.de>

build() {
  arch-meson build $pkgname-$pkgver
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  install -vdm 755 "$pkgdir/usr/share/licenses/$pkgname"
  ln -sv /usr/share/doc/$pkgname/COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
