# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-oclock
pkgver=1.0.3
pkgrel=2
pkgdesc="Round X clock"
arch=(i686 x86_64)
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libx11' 'libxmu' 'libxext' 'libxkbfile')
makedepends=('xorg-util-macros')
source=(http://xorg.freedesktop.org/archive/individual/app/oclock-${pkgver}.tar.bz2)
sha1sums=('1aa05864019a55610aca5471c5206858dde9a34a')

build() {
  cd "${srcdir}/oclock-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/oclock-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
