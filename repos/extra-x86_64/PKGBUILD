# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Zuf <kontakt.zuf@gmail.com>
# Contributor: Darwin Bautista <djclue917@gmail.com>

pkgname=kdenlive
pkgver=0.9.10
pkgrel=2
pkgdesc="A non-linear video editor for Linux"
arch=('i686' 'x86_64')
# http://download.kde.org/stable/kdenlive/
url="http://www.kdenlive.org/"
license=('GPL')
depends=('hicolor-icon-theme' 'kdelibs' 'mlt' \
	 'qimageblitz' 'qjson' 'shared-mime-info')
makedepends=('cmake' 'automoc4' 'gettext' 'glew')
optdepends=('cdrkit: for creation of DVD ISO images'
	    'dvdauthor: for creation of DVD'
	    'dvgrab: for firewire capture'
	    'ffmpeg: for webcam capture'
	    'recordmydesktop: for screen capture'
	    'xine-lib: for DVD preview'
	    'oxygen-icons: if some icons does not appear')
install=kdenlive.install
options=('docs')
source=(http://download.kde.org/stable/kdenlive/$pkgver/src/kdenlive-$pkgver.tar.bz2)
md5sums=('40d7ee8e388cb084f5eb1ad1596a14b2')

build() {
  export LDFLAGS="$LDFLAGS -lX11"
  cd "${srcdir}"
  mkdir -p build
  cd build
  cmake ../kdenlive-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=/usr/lib \
    -DLINK_LIBRARIES=-lX11 \
    -DWITH_Nepomuk=OFF
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
