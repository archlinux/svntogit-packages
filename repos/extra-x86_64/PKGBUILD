# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libx11
pkgver=1.3.4
pkgrel=1
pkgdesc="X11 client-side library"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
depends=('libxcb>=1.6' 'kbproto>=1.0.4')
makedepends=('xproto>=7.0.17' 'xextproto>=7.1.1' 'xf86bigfontproto>=1.2.0' 'xtrans>=1.2.5' 'inputproto>=2.0' 'xorg-util-macros>=1.8.0')
options=('!libtool')
license=('custom:XFREE86')
source=(${url}/releases/individual/lib/libX11-${pkgver}.tar.bz2
        xorg.sh)
sha1sums=('5aab6dc8ed5ec0e86419626715efc3edd34d1db4'
          'b59214fe7d1d213f86b074a677eb624e52142ad7')

build() {
  cd "${srcdir}/libX11-${pkgver}"
  ./configure --prefix=/usr \
      --with-xcb --disable-static || return 1
  make || return 1
  make check || return 1
  make DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/etc/profile.d"
  install -m755 "${srcdir}/xorg.sh" "${pkgdir}/etc/profile.d/" || return 1

  install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/" || return 1
}
