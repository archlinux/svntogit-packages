# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=fping
pkgver=4.0
pkgrel=2
pkgdesc="A utility to ping multiple hosts at once"
arch=('x86_64')
url="http://www.fping.org/"
license=('custom') 
depends=('glibc')
install="$pkgname.install"
source=("https://www.fping.org/dist/${pkgname}-${pkgver}.tar.gz")
sha256sums=('67eb4152b98ad34f99d2eec4e1098a0bb52caf13c0c89cd147349d08190fe8ce')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr \
              --sbindir=/usr/bin \
              --mandir=/usr/share/man \
              --enable-ipv4 \
              --enable-ipv6
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="${pkgdir}" install

  # install license
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/$pkgname/LICENSE"
}
