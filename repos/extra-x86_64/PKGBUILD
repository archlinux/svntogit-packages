# Maintainer:
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=talloc
pkgver=2.1.16
pkgrel=1
pkgdesc="Hierarchical pool based memory allocator with destructors"
arch=('x86_64')
license=('GPL3')
url="http://talloc.samba.org/"
depends=('glibc')
makedepends=('python2' 'docbook-xsl')
optdepends=('python2: for python bindings')
source=(http://samba.org/ftp/${pkgname}/${pkgname}-${pkgver}.tar.{gz,asc}
        fix_test_path.patch)
sha1sums=('69fe815888eddcbe2fb6fc3270ff9e977461000d'
          'SKIP'
          '6117e3dffb24bf03d54325c13b7db3b8a7eb2850')
validpgpkeys=(9147A339719518EE9011BCB54793916113084025) # samba-bugs@samba.org

prepare() {
   cd "${srcdir}/${pkgname}-${pkgver}"
   #patch -p1 < ../fix_test_path.patch
   sed -i -e 's#python3#python2#g' buildtools/bin/waf
}

build() {
   cd "${srcdir}/${pkgname}-${pkgver}"
   ./configure --prefix=/usr \
     --sysconfdir=/etc/samba \
     --localstatedir=/var \
     --bundled-libraries=NONE \
     --builtin-libraries=replace \
     --enable-talloc-compat1
   make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() {
   cd "${srcdir}/${pkgname}-${pkgver}"
   make DESTDIR="${pkgdir}" install
}
