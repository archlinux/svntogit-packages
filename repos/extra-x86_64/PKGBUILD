# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Michel Brabants <michel.linux@tiscali.be>

pkgname=seahorse
pkgver=2.22.3
pkgrel=1
pkgdesc="GNOME application for managing PGP keys."
arch=(i686 x86_64)
license=('GPL')
url="http://seahorse.sourceforge.net/"
depends=('libgnomeui>=2.22.1' 'libnotify>=0.4.4' 'libsoup>=2.4.1' 'gpgme' 'desktop-file-utils' 'zip')
makedepends=('gettext' 'libldap' 'perlxml' 'pkgconfig' 'gnome-doc-utils>=0.12.2' 'nautilus>=2.22.4' 'epiphany>=2.22.2' 'gnome-panel>=2.22.2' 'gedit>=2.22.3')
options=('!libtool' '!emptydirs' 'xulrunner>=1.9')
groups=('gnome-extra')
replaces=('gnome-keyring-manager')
install=seahorse.install
source=(http://ftp.gnome.org/pub/GNOME/sources/seahorse/2.22/seahorse-${pkgver}.tar.bz2
	09_xul1.9.patch)

md5sums=('7fdab30dbf8684881199e15246c3765a')

build() {
  cd ${startdir}/src/${pkgname}-${pkgver}
  patch -Np1 -i ${startdir}/src/09_xul1.9.patch || return 1
  autoconf || return 1
  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var --disable-static \
	      --enable-ldap --enable-hkp \
	      --enable-gedit --enable-nautilus \
	      --enable-agent --enable-libnotify \
	      --with-gecko=libxul-unstable \
	      --disable-scrollkeeper --disable-update-mime-database || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR=${startdir}/pkg install || return 1

  install -m755 -d ${startdir}/pkg/usr/share/gconf/schemas
  gconf-merge-schema ${startdir}/pkg/usr/share/gconf/schemas/${pkgname}.schemas ${startdir}/pkg/etc/gconf/schemas/*.schemas || return 1
  rm -f ${startdir}/pkg/etc/gconf/schemas/*.schemas
}
