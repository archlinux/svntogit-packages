# $Id: PKGBUILD 168943 2012-10-16 16:31:44Z heftig $
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=tali
pkgver=3.10.0
pkgrel=1
pkgdesc="Beat the odds in a poker-style dice game"
arch=('i686' 'x86_64')
license=('GPL')
depends=('desktop-file-utils' 'hicolor-icon-theme' 'gtk3' 'librsvg')
makedepends=('intltool' 'itstool' 'gobject-introspection')
conflicts=('gnome-games')
replaces=('gnome-games')
options=('!emptydirs' '!libtool')
install=tali.install
url="https://live.gnome.org/Tali"
groups=('gnome-extra')
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('2e276e6965057fe3136fe831809b2cedab3e8f59346b0fbe37f3f383a25c68c6')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --disable-schemas-compile \
      --with-scores-user=root --with-scores-group=games
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  # Remove all scores, we generate them from postinstall
  rm -rf "$pkgdir/var"
}
