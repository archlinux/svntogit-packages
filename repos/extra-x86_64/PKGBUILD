# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=xorg-setxkbmap
pkgver=1.3.4
pkgrel=1
pkgdesc='Set the keyboard using the X Keyboard Extension'
url='https://xorg.freedesktop.org/'
license=('custom')
arch=('x86_64')
depends=('libxkbfile' 'libxrandr')
makedepends=('xorg-util-macros')
source=(${url}/releases/individual/app/setxkbmap-${pkgver}.tar.xz{,.sig})
sha512sums=('f4d026e7707656173f54d79b95ff3370ae06eaf252b89b79bc7175edeaf3ef79073d4d7d8a35fdee95608bf85d7d23514d74f00e55b613810ef6c3ccc0cf498b'
            'SKIP')
#validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # Alan Coopersmith <alan.coopersmith@oracle.com>
validpgpkeys=('3C2C43D9447D5938EF4551EBE23B7E70B467F0BF') # Peter Hutterer (Who-T) <office@who-t.net>

groups=('xorg-apps' 'xorg')

build() {
	cd setxkbmap-${pkgver}
	./configure --prefix=/usr
	make
}

package() {
	cd setxkbmap-${pkgver}
	make DESTDIR="${pkgdir}" install
	install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
