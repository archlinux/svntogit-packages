# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=knights
pkgver=19.12.2
pkgrel=1
pkgdesc="Chess board by KDE with XBoard protocol support"
arch=(x86_64)
url="https://www.kde.org/applications/games/knights/"
license=(GPL LGPL FDL)
groups=(kde-applications kdegames)
depends=(libkdegames kplotting plasma-framework hicolor-icon-theme)
optdepends=('gnuchess: for playing against the computer')
makedepends=(extra-cmake-modules kdoctools)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('77a854b35d2564c91b7d5086e08c1bcd2d471c8b5dd8ede7213c109ab97c6fa2'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF   
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
