# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: AndyRTR <andyrtr@archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=xfce4-eyes-plugin
pkgver=4.4.4
pkgrel=1
pkgdesc="A rolling eyes (following mouse pointer) plugin for the Xfce panel"
arch=('i686' 'x86_64')
url="http://goodies.xfce.org/projects/panel-plugins/xfce4-eyes-plugin"
license=('GPL2')
groups=('xfce4-goodies')
depends=('xfce4-panel' 'hicolor-icon-theme')
makedepends=('intltool' 'perl-xml-parser')
install=$pkgname.install
source=(http://archive.xfce.org/src/panel-plugins/$pkgname/4.4/$pkgname-$pkgver.tar.bz2)
sha256sums=('240ce85b68d3d161f276ebbea97072dd6ee3df77062fd073bf6eeb4d3d1400ca')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
