# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=libpeas
pkgver=1.9.0
pkgrel=1
pkgdesc="A GObject-based plugins engine"
arch=(i686 x86_64)
url="http://www.gtk.org/"
license=(GPL2)
depends=(gtk3 hicolor-icon-theme gobject-introspection)
makedepends=(gtk-doc intltool python-gobject python2-gobject gjs seed glade)
optdepends=('gjs: gobject-based plugin engine - gjs runtime loader'
            'seed: gobject-based plugin engine - seed runtime loader')
options=('!libtool')
install=libpeas.install
source=(http://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-${pkgver}.tar.xz)
sha256sums=('008747ccdf954dc7b8e3207fffede527a589363541873f906c8e0166f6f7df8d')


build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
