# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=json-glib
pkgver=1.2.2+4+gd725fb5
pkgrel=1
pkgdesc="JSON library built on GLib"
arch=('i686' 'x86_64')
url="https://wiki.gnome.org/Projects/JsonGlib"
license=('GPL')
depends=('glib2')
makedepends=('gobject-introspection' 'git' 'gtk-doc')
_commit=d725fb511b646521213474bc9dadc4d5a3a1cf9c
source=("git://git.gnome.org/json-glib#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}
  
build(){
  cd $pkgname
  ./configure --prefix=/usr --enable-gtk-doc
  make
}

package(){
  cd $pkgname
  make DESTDIR="${pkgdir}" install
}

check(){
  cd $pkgname
  make check
}
