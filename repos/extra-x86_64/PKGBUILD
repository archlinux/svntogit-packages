# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-power-manager
pkgver=2.24.2
pkgrel=2
pkgdesc="Session daemon that makes it easy to manage your laptop or desktop system."
arch=(i686 x86_64)
url="http://www.gnome.org/projects/gnome-power-manager/"
license=('GPL')
depends=('libnotify>=0.4.5' 'gstreamer0.10>=0.10.21' 'policykit-gnome>=0.9.2' 'libgnomeui')
makedepends=('gettext' 'intltool' 'pkgconfig' 'gnome-doc-utils>=0.14.0' 'gnome-panel>=2.24.1')
optdepends=('gnome-panel')
options=(!emptydirs)
install=gnome-power-manager.install
groups=(gnome-extra)
source=(http://ftp.gnome.org/pub/GNOME/sources/gnome-power-manager/2.24/${pkgname}-${pkgver}.tar.bz2)
md5sums=('c8971e0c4ed060e0182785321f19b961')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  export LDFLAGS="${LDFLAGS} -Wl,--as-needed"
  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var \
	      --libexecdir=/usr/lib/gnome-power-manager \
	      --enable-policykit \
	      --with-dpms-ext \
              --disable-scrollkeeper \
              --with-dbus-services=/usr/share/dbus-1/services \
              --enable-applets || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
