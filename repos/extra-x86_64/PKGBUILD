# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>

pkgname=xfce4-taskmanager
pkgver=1.4.0
pkgrel=1
pkgdesc="Easy to use task manager"
arch=('x86_64')
url="https://docs.xfce.org/apps/xfce4-taskmanager"
license=('GPL2')
depends=('libwnck3' 'libxmu')
makedepends=('intltool')
groups=('xfce4-goodies')
source=(https://archive.xfce.org/src/apps/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=('655684ddfc15fc09071f78f9fce5439d20bbd2d80ecfc5ba0a08fb38313e7d43')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
