# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Tobias Kieslich <tobias@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>

pkgname=libvorbis
pkgver=1.3.5
pkgrel=1
pkgdesc='Vorbis codec library'
arch=('i686' 'x86_64')
url='http://www.xiph.org/vorbis/'
license=('BSD')
depends=('libogg')
provides=('libvorbisfile.so' 'libvorbis.so' 'libvorbisenc.so')
source=(http://downloads.xiph.org/releases/vorbis/$pkgname-$pkgver.tar.gz)
md5sums=('7220e089f3be3412a2317d6fde9e3944')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-static
  make
}

check() {
  cd $pkgname-$pkgver
  make -j1 check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
