# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=k3b
pkgver=2.0.2
pkgrel=10
pkgdesc="Feature-rich and easy to handle CD burning application"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/k3b/"
license=('GPL')
depends=('kdebase-runtime' 'libkcddb' 'libsamplerate' 'libmad'
         'ffmpeg' 'taglib' 'libmpcdec' 'libdvdread' 'cdrkit' 'libxft')
makedepends=('cmake' 'automoc4' 'docbook-xml')
optdepends=('dvd+rw-tools: for dvd burning support'
            'vcdimager: for vcd burning support'
            'transcode: for advanced mpeg conversion support'
            'emovix: for bootable multimedia cd/dvd support'
            'cdrdao: for disk-at-once (DAO) mode support'
            'cdparanoia: for cd ripping support')
install=k3b.install
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.bz2 \
        k3b-2.0.2-ffmpeg.patch k3b-2.0.2-libavformat54.patch k3b-2.0.2-libav9.patch)
sha1sums=('8b30a4d07942e82559b01bc07dea6bcf2defd532'
          '8120c0e22e6c41ea285ca6060be13723ed91c52b'
          '2951518013192f6eefd0754e74dee494e32def24'
          '02db0666db8d793860c93ef9e2f19a00c9451945')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i "${srcdir}/k3b-2.0.2-ffmpeg.patch"
  patch -p1 -i "${srcdir}/k3b-2.0.2-libavformat54.patch"
  patch -p1 -i "${srcdir}/k3b-2.0.2-libav9.patch"
}

build() {
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
