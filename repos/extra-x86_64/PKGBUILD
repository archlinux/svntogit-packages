# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=pipewire
pkgver=0.1.7
pkgrel=1
pkgdesc="Server and user space API to deal with multimedia pipelines"
url="http://pipewire.org"
license=(LGPL2.1)
arch=(x86_64)
depends=(gstreamer gst-plugins-base v4l-utils)
makedepends=(git meson doxygen graphviz xmltoman ffmpeg jack2 libva)
optdepends=(ffmpeg jack2 libva)
install=pipewire.install
_commit=58f086b1653ccc0c1eb1d6cf988acff103daaede  # tags/0.1.7
source=("git+https://github.com/PipeWire/pipewire#commit=$_commit")
sha256sums=('SKIP')


pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  mkdir build
  cd $pkgname
  # Reduce docs size
  printf '%s\n' >>doc/Doxyfile.in \
    HAVE_DOT=yes DOT_IMAGE_FORMAT=svg INTERACTIVE_SVG=yes
}

build() {
  arch-meson $pkgname build \
    -Denable_docs=true \
    -Denable_man=true \
    -Denable_gstreamer=true
  ninja -C build
}

check() {
  cd build
  meson test
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}
