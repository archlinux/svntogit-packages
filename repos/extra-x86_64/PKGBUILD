# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgbase=plasma-desktop
pkgname=(plasma-desktop knetattach)
pkgver=5.11.4
pkgrel=2
pkgdesc='KDE Plasma Desktop'
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
        kdebug-387199.patch::"https://cgit.kde.org/plasma-desktop.git/patch/?id=0d3f000f")
depends=(polkit-kde-agent libcanberra libxkbfile kmenuedit appstream-qt systemsettings ksysguard kpeople baloo qt5-graphicaleffects)
makedepends=(extra-cmake-modules kdoctools boost xf86-input-evdev xf86-input-synaptics xorg-server-devel
             libibus scim python kdesignerplugin)
groups=(plasma)
sha256sums=('38efe3488375093fe2efbd50ddd9ea2d2b0381867dfcaefb7ca70b54a24bac29'
            'SKIP'
            '0d5b8c55ddcba94c31fbb7e676cd790f5f1a7d725b6dd58dca986df026a21ac4')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell
              '348C8651206633FD983A8FC4DEACEA00075E1D76'  # KDE Neon
              'D07BD8662C56CB291B316EB2F5675605C74E02CF') # David Edmundson

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../kdebug-387199.patch # Fix double context menu in folder view with Qt 5.10 https://bugs.kde.org/show_bug.cgi?id=387199
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package_plasma-desktop() {
  depends+=(knetattach)
  optdepends=('plasma-nm: Network manager applet'
              'powerdevil: power management'
              'ibus: kimpanel IBUS support'
              'scim: kimpanel SCIM support'
              'discover: manage applications installation from the launcher')

  cd build
  make DESTDIR="$pkgdir" install

# Split knetattach
  rm "$pkgdir"/usr/{bin/knetattach,share/applications/org.kde.knetattach.desktop}
}

package_knetattach() {
  pkgdesc='Wizard which makes it easier to integrate network resources with the Plasma Desktop'
  depends=(kdelibs4support)

  cd build/knetattach
  make DESTDIR="$pkgdir" install
}
