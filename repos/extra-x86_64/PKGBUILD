# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=libkgapi
pkgver=22.12.3
pkgrel=1
pkgdesc='A KDE-based library for accessing various Google services via their public API'
url='https://www.kde.org/'
arch=(x86_64)
license=(GPL LGPL FDL)
depends=(kcalendarcore kcontacts kwallet)
makedepends=(extra-cmake-modules qt5-tools)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('97cc31fba4bed9b49d8edccc27c3413f7759414a54e2025d06a6af5a19845738'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
options=(!lto) # https://bugs.kde.org/show_bug.cgi?id=411425

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
