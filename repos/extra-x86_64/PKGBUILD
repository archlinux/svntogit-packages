# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=egl-wayland
pkgver=1.1.4
pkgrel=2
pkgdesc="EGLStream-based Wayland external platform"
arch=(x86_64)
url=https://github.com/NVIDIA/egl-wayland
license=(MIT)
depends=(wayland eglexternalplatform)
makedepends=(meson git)
_commit=47d96a84b04d21cd0a2f23b59a4936c829a65364  # tags/1.1.4
source=("git+$url#commit=$_commit"
        glvnd.diff
        10_nvidia_wayland.json)
sha256sums=('SKIP'
            'e6d965551829448ffc2ea0fdf97d7d44c3d35be0b56fc025e6cc343f29f7b9d6'
            '5cccf1905a266e8e34d5ad4aad4be85390e60b1a0850a29dd9d64adc641de412')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname

  # Fix build with headers from libglvnd
  git apply -3 ../glvnd.diff
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
  install -Dt "$pkgdir/usr/share/egl/egl_external_platform.d" -m644 10_nvidia_wayland.json
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 $pkgname/COPYING
}

# vim:set sw=2 et:
