# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>

pkgname=geeqie
pkgver=1.2.1
pkgrel=1
pkgdesc="A lightweight image browser and viewer"
arch=('i686' 'x86_64')
url="http://www.geeqie.org/"
license=('GPL2')
depends=('exiv2' 'gtk2' 'lcms' 'desktop-file-utils')
makedepends=('intltool' 'doxygen' 'gnome-doc-utils' 'git')
optdepends=('librsvg: SVG rendering')
install=geeqie.install
source=('git://www.geeqie.org/geeqie.git#commit=2b87884fe370ec63dc38e924ceb89a124cca10b8'
        'fix-fullscreen.patch')
sha1sums=('SKIP'
          'dab15bbda9b329f663276b25bf1989b410596644')

prepare() {
  cd "${srcdir}/${pkgname}"
  patch -p1 -i ../fix-fullscreen.patch
}

build() {
  cd "${srcdir}/${pkgname}"
  ./autogen.sh --prefix=/usr
  make
}

package(){
  cd "${srcdir}/${pkgname}"
  make DESTDIR="${pkgdir}" install
}
