# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

pkgname=libvpx
pkgver=1.4.0
pkgrel=2
pkgdesc='VP8 and VP9 codec'
arch=('i686' 'x86_64')
url='http://www.webmproject.org/'
license=('BSD')
depends=('glibc' 'gcc-libs')
makedepends=('yasm' 'git')
provides=('libvpx.so')
source=($pkgname::git+http://code.google.com/p/webm.libvpx#tag=v$pkgver)
md5sums=('SKIP')

build() {
  cd $pkgname
  ./configure --prefix=/usr \
    --enable-vp8 \
    --enable-vp9 \
    --enable-runtime-cpu-detect \
    --enable-shared \
    --enable-postproc \
    --enable-pic \
    --disable-install-docs \
    --disable-install-srcs
  make
}

package() {
  cd $pkgname
  make DIST_DIR="$pkgdir/usr" install
  install -Dm0644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
