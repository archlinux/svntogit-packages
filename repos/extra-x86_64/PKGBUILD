# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kwallet
pkgver=5.66.0
pkgrel=2
pkgdesc='Secure and unified container for user passwords'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(knotifications kiconthemes kservice gpgme)
makedepends=(extra-cmake-modules kdoctools boost doxygen qt5-tools qt5-doc)
optdepends=('kwalletmanager: Configuration GUI')
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
         kwallet-falkon-1.patch::"https://cgit.kde.org/kwallet.git/patch/?id=d4980443"
         kwallet-falkon-2.patch::"https://cgit.kde.org/kwallet.git/patch/?id=02ab54ea")
sha256sums=('6e48070e22a822a4003e1f7a739fe1e335bcf46982cbf4909d23a6e40689a827'
            'SKIP'
            'c4699f82fa8120c38487f32a18381df141df0e6d3c60448099571c168f7d7d62'
            'eda63d7d1a3fe514648ccae332749c6aab249915dee98d3f390b07eb38cad740')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build

# Fix password filling in falkon
  cd $pkgname-$pkgver
  patch -p1 -i ../kwallet-falkon-1.patch
  patch -p1 -i ../kwallet-falkon-2.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
