# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=tracker-miners
pkgver=2.3.5
pkgrel=1
pkgdesc="Collection of data extractors for Tracker/Nepomuk"
url="https://wiki.gnome.org/Projects/Tracker"
arch=(x86_64)
license=(GPL)
depends=(tracker gst-plugins-base-libs exempi flac libexif libcue libgrss libgsf libgxps libiptcdata
         libosinfo libvorbis poppler-glib totem-plparser giflib libgexiv2 gupnp-dlna upower
         libseccomp)
makedepends=(intltool git meson vala)
checkdepends=(python-gobject gst-plugins-good gst-plugins-base gst-libav)
groups=(gnome)
_commit=58fd82136d561e20adf71ab8a4ce02076ae7bf92  # tags/2.3.5^0
source=("git+https://gitlab.gnome.org/GNOME/tracker-miners.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname

  # Fix autostart=true
  git cherry-pick -n e7b6d5f8b7541a4693c61be5d210fad85a5c9d06
}

build() {
  arch-meson $pkgname build -D autostart=true
  meson compile -C build
}

check() {
  # Intermittent test failures: https://gitlab.gnome.org/GNOME/tracker-miners/-/issues/79
  dbus-run-session meson test -C build --print-errorlogs -t 3 || :
}

package() {
  depends+=(libtracker-{miner,sparql}-2.0.so)
  DESTDIR="$pkgdir" meson install -C build
}
