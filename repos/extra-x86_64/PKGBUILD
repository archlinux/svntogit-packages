# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: François Charette <firmicus ατ gmx δοτ net>
# Contributor: Alex Dioso <adioso->gmail*com>

pkgname=perl-clone
pkgver=0.41
pkgrel=1
pkgdesc='Recursive copy of nested objects.'
arch=('x86_64')
url='http://search.cpan.org/~RDF/Clone'
license=('GPL' 'PerlArtistic')
depends=('perl')
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/G/GA/GARU/Clone-$pkgver.tar.gz")
sha512sums=('fa370fbede205449687b00a255f71295b2056c7897a343758564f80810d2534b8a8eff248f3adf8e96e1a7eded22a2237edb22fb610d0cd84d2fab1352b40dcb')

build() {
  cd  Clone-${pkgver}
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd  Clone-${pkgver}
  make DESTDIR=${pkgdir} install
  find ${pkgdir} -name '.packlist' -delete
  find ${pkgdir} -name '*.pod' -delete
}
