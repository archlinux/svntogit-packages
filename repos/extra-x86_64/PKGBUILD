# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=gdk-pixbuf2
pkgver=2.42.0
pkgrel=2
pkgdesc="An image loading library"
arch=(x86_64)
url="https://wiki.gnome.org/Projects/GdkPixbuf"
license=(LGPL)
depends=(glib2 libpng libtiff libjpeg shared-mime-info)
makedepends=(gtk-doc gobject-introspection git meson)
provides=(libgdk_pixbuf-2.0.so)
install=gdk-pixbuf2.install
_commit=43ce0077ed7162a32680ff8cd9c0c91ff0afce20  # tags/2.42.0^0
source=("git+https://gitlab.gnome.org/GNOME/gdk-pixbuf.git#commit=$_commit"
        gdk-pixbuf-query-loaders.hook)
sha256sums=('SKIP'
            '9fb71d95e6a212779eb0f88dde5a3cee0df7f4d9183f8f9ce286f8cdc14428f0')

pkgver() {
  cd gdk-pixbuf
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd gdk-pixbuf
}

build() {
  arch-meson gdk-pixbuf build \
    -D docs=true \
    -D installed_tests=false
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
  install -Dt "$pkgdir/usr/share/libalpm/hooks" -m644 *.hook
}

# vim:set ts=2 sw=2 et:
