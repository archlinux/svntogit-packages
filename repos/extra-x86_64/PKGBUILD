# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gst-libav
pkgver=1.10.0
pkgrel=1
pkgdesc="GStreamer Multimedia Framework ffmpeg Plugin"
url="https://gstreamer.freedesktop.org/"
arch=(i686 x86_64)
license=(GPL)
depends=(gst-plugins-base-libs bzip2 ffmpeg)
makedepends=(python gtk-doc autoconf-archive git)
options=(!emptydirs)
provides=("gst-ffmpeg=$pkgver-$pkgrel")
_commit=50ffd5a8057d40e573b4b15b62d3a3321e168d89  # tags/1.10.0^0
source=("git://anongit.freedesktop.org/gstreamer/gst-libav#commit=$_commit"
        "gst-common::git://anongit.freedesktop.org/gstreamer/common"
        "git://source.ffmpeg.org/ffmpeg")
sha256sums=('SKIP'
            'SKIP'
            'SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname

  git submodule init
  git config --local submodule.common.url "$srcdir/gst-common"
  git config --local submodule.gst-libs/ext/libav.url "$srcdir/ffmpeg"
  git submodule update

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib \
    --with-package-name="GStreamer ffmpeg Plugin (Arch Linux)" \
    --with-package-origin="http://www.archlinux.org/" \
    --with-system-libav --with-libav-extra-configure="--enable-runtime-cpudetect" \
    --enable-experimental --enable-gtk-doc --disable-static

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
