# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: György Balló <ballogy@freestart.hu>

pkgname=gssdp
pkgver=0.14.11
pkgrel=1
pkgdesc="A GObject-based API for handling resource discovery and announcement over SSDP"
arch=(i686 x86_64)
url="http://gupnp.org/"
license=(LGPL)
depends=(libsoup)
makedepends=(gobject-introspection gtk3 gtk-doc vala)
optdepends=('gtk3: gssdp-device-sniffer')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver:0:4}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('7bf5aeaf2119fe0bec5f3632ecf39dae15bc85276c72c2ad8dd4b0e345c6535a')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
