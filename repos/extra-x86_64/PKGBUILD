# $Id$
# Maintainer: Tom Gundersen <teg@jklm.no>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: ice-man <icemanf@gmail.com>
# Contributor: sergeantspoon <sergeantspoon@archlinux.us>

pkgname=libssh
pkgver=0.5.5
pkgrel=2
pkgdesc="Library for accessing ssh client services through C libraries"
url="http://www.libssh.org/"
license=('LGPL')
arch=('i686' 'x86_64')
depends=('zlib' 'openssl')
makedepends=('cmake' 'doxygen')
source=(https://red.libssh.org/attachments/download/51/${pkgname}-${pkgver}.tar.gz
        0004-reset-global-request-status.patch
        0005-multi-reverse-fwd.patch)
md5sums=('bb308196756c7255c0969583d917136b'
         '0d8c28906b07e31466157b1fda441f4a'
         '6fa3a1a4f448e85a7eb39360f4a72ce9')

prepare() {
  cd ${pkgname}-${pkgver}
  # add multiple channels on a single SSH connection required by X2goclient
  # see http://bugs.debian.org/cgi-bin/bugreport.cgi?bug=736231
  patch -Np1 -i ${srcdir}/0004-reset-global-request-status.patch
  patch -Np1 -i ${srcdir}/0005-multi-reverse-fwd.patch
}

build() {
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package(){
  cd build
  make DESTDIR="${pkgdir}" install
}
