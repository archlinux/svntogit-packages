# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=dbus-glib
pkgver=0.94
pkgrel=2
pkgdesc="GLib bindings for DBUS"
arch=(i686 x86_64)
license=('GPL')
url="http://www.freedesktop.org/wiki/Software/DBusBindings"
depends=('dbus-core' 'glib2')
makedepends=('pkgconfig')
options=('!libtool' '!emptydirs')
source=(http://dbus.freedesktop.org/releases/${pkgname}/${pkgname}-${pkgver}.tar.gz
        fix_network_manager.patch)
md5sums=('e1f1506a6f4941e67bffd614b1ad5af6'
         '2a3598342dd25722d452e7beaf21b5f3')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/fix_network_manager.patch"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --enable-static=no --enable-bash-completion=no
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
