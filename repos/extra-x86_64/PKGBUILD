# Maintainer: Angel Velasquez <angvp@archlinux.org> 
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>

pkgname=serf
pkgver=1.3.9
pkgrel=3
pkgdesc="High-performance asynchronous HTTP client library"
arch=('x86_64')
url="https://serf.apache.org"
license=('Apache')
depends=('apr-util')
makedepends=('scons')
source=(https://www.apache.org/dist/serf/${pkgname}-${pkgver}.tar.bz2{,.asc})
validpgpkeys=('C236283C970D2C3CFEEB4936BF35CF0078383142')
sha256sums=('549c2d21c577a8a9c0450facb5cca809f26591f048e466552240947bdf7a87cc'
            'SKIP')

build() {
  cd ${pkgname}-${pkgver}
  scons CFLAGS="${CFLAGS}" \
        LINKFLAGS="${LDFLAGS}" \
        PREFIX=/usr GSSAPI=/usr/bin/krb5-config
}

check() {
  cd ${pkgname}-${pkgver}
  # Runs into infinite loop
  #scons check
}

package() {
  cd ${pkgname}-${pkgver}
  install -d "${pkgdir}/usr"
  scons install --install-sandbox="${pkgdir}"
}
