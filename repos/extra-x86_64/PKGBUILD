# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Andrew Simmons <andrew.simmons@gmail.com>

pkgname=thunar
pkgver=4.16.3
pkgrel=1
pkgdesc="Modern file manager for Xfce"
arch=('x86_64')
url="https://docs.xfce.org/xfce/thunar/start"
license=('GPL2' 'LGPL2.1')
groups=('xfce4')
depends=('desktop-file-utils' 'libexif' 'hicolor-icon-theme' 'libnotify'
         'libgudev' 'exo' 'libxfce4util' 'libxfce4ui' 'libpng')
makedepends=('intltool' 'gobject-introspection' 'xfce4-panel')
optdepends=('gvfs: trash support, mounting with udisk and remote filesystems'
            'tumbler: thumbnail previews'
            'thunar-volman: removable device management'
            'thunar-archive-plugin: archive creation and extraction'
            'thunar-media-tags-plugin: view/edit ID3/OGG tags')
source=(https://archive.xfce.org/src/xfce/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=('5524dc89da6f1492a5644214be210c4c2d359723a1f8be3e795ea974f2d1ff72')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --enable-gio-unix \
    --enable-gudev \
    --enable-notifications \
    --enable-exif \
    --enable-pcre \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
