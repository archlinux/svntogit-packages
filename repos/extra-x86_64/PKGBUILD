# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kimageformats
pkgver=5.103.0
pkgrel=1
pkgdesc='Image format plugins for Qt5'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(qt5-base)
makedepends=(extra-cmake-modules karchive libavif openexr libheif libjxl libraw)
optdepends=('karchive: plugin for Krita and OpenRaster images'
            'libavif: AVIF format support'
            'openexr: EXR format support'
            'libheif: HEIF format support'
            'libjxl: JPEG-XL format support'
            'libraw: RAW format support')
groups=(kf5)
source=(https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('dd7dc417aacc9dfe74745e4dfd9750129051c36502fa1cd18b92c4035d721e7e'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DKIMAGEFORMATS_HEIF=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
