# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=frameworkintegration
pkgver=5.14.0
pkgrel=2
pkgdesc='Framework providing components to allow applications to integrate with a KDE Workspace'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/frameworkintegration'
license=('LGPL')
depends=('kio' 'ttf-oxygen' 'libxcursor')
makedepends=('extra-cmake-modules')
groups=('kf5')
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz" bug350758.patch)
md5sums=('d5094cba8ca4d280d6e817d334fb2dc1'
         '83e03a5a5f79de524e290b691efbbd77')

prepare() {
  mkdir -p build

# Fix file dialogs sometimes freezing applications https://bugs.kde.org/show_bug.cgi?id=350758
  cd $pkgname-$pkgver
  patch -p1 -i ../bug350758.patch
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
