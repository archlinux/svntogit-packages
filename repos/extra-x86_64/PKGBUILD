# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=libnma
pkgver=1.8.34
pkgrel=1
pkgdesc="NetworkManager GUI client library"
url="https://gitlab.gnome.org/GNOME/libnma"
arch=(x86_64)
license=(GPL LGPL)
depends=(libnm gcr gtk3 iso-codes mobile-broadband-provider-info)
makedepends=(gobject-introspection git gtk-doc meson vala)
provides=(libnma.so)
_commit=98d55a765d6aff0243d26d1ef5e655ebb6f78fa8  # tags/1.8.34^0
source=("git+https://gitlab.gnome.org/GNOME/libnma.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd libnma
  git describe --tags | sed 's/-dev/dev/;s/-/+/g'
}

prepare() {
  cd libnma
}

build() {
  arch-meson libnma build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
