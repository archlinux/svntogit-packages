# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=libmodplug
pkgver=0.8.8.4
pkgrel=2
pkgdesc="A MOD playing library"
arch=('i686' 'x86_64')
url="http://modplug-xmms.sourceforge.net/"
license=('custom')
depends=('gcc-libs')
options=('!libtool')
source=(http://downloads.sourceforge.net/modplug-xmms/${pkgname}-${pkgver}.tar.gz
        libmodplug-CVE-2013-4233-Fix.patch libmodplug-CVE-2013-4234-Fix.patch)
sha1sums=('df4deffe542b501070ccb0aee37d875ebb0c9e22'
          'daee7fba80f633236a3d09ad19225c57013140e9'
          '2e870747261a86dce5056cbf077c5914e9e8b287')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p2 -i "${srcdir}/libmodplug-CVE-2013-4233-Fix.patch"
  patch -p2 -i "${srcdir}/libmodplug-CVE-2013-4234-Fix.patch"
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
