# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=phonon-vlc
pkgver=0.7.1
pkgrel=2
arch=('i686' 'x86_64')
url='http://phonon.kde.org/'
pkgdesc="Phonon VLC backend"
license=('LGPL')
depends=('vlc')
makedepends=('cmake' 'automoc4' 'phonon-qt4')
provides=('phonon-backend')
source=("http://download.kde.org/stable/phonon/phonon-backend-vlc/${pkgver}/phonon-backend-vlc-${pkgver}.tar.xz")
md5sums=('3c41a3d141d40cedd2819676cdca5ea4')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../phonon-backend-vlc-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON \
    -DPLUGIN_INSTALL_DIR=/usr/lib/kde4
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
