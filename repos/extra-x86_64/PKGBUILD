# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=xfce4-screensaver
pkgver=4.18.1
pkgrel=1
pkgdesc='Xfce Screensaver'
arch=('x86_64')
url='https://docs.xfce.org/apps/screensaver/start'
license=('GPL2')
groups=('xfce4-goodies')
depends=('libxss' 'libxklavier' 'libwnck3' 'garcon' 'dbus-glib' 'python' 'python-gobject')
makedepends=('intltool')
backup=('etc/pam.d/xfce4-screensaver')
source=("https://archive.xfce.org/src/apps/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('b731e8abd68a18cc5d3b523351aa8f9f293c9f63290c1781c9155259deb04a5c')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}
