# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
pkgname=gdk-pixbuf2
pkgver=2.23.5
pkgrel=1
pkgdesc="An image loading library for gtk2"
arch=('i686' 'x86_64')
url="http://www.gtk.org/"
license=('GPL2')
depends=('glib2' 'libpng' 'libtiff' 'libjpeg' 'libx11')
makedepends=('gtk-doc' 'gobject-introspection')
options=('!libtool')
install=gdk-pixbuf2.install
source=(http://download.gnome.org/sources/gdk-pixbuf/2.23/gdk-pixbuf-${pkgver}.tar.xz)
sha256sums=('7a6c3550426f38f6b0eeeb49295654d9e706ce8f1786a4d0932f9d2c0d0bc500')

build() {
  cd "${srcdir}/gdk-pixbuf-${pkgver}"

  ./configure --prefix=/usr \
    --without-libjasper \
    --with-included-loaders=png
  make
}

package() {
  cd "${srcdir}/gdk-pixbuf-${pkgver}"

  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
