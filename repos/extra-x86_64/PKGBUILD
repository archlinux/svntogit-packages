# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=breeze
pkgver=5.21.5
pkgrel=2
arch=(x86_64)
pkgdesc='Artwork, styles and assets for the Breeze visual style for the Plasma Desktop'
url='https://kde.org/plasma-desktop/'
license=(LGPL)
depends=(frameworkintegration kdecoration breeze-icons kwayland hicolor-icon-theme)
makedepends=(extra-cmake-modules kcmutils)
optdepends=('breeze-gtk: Breeze widget style for GTK applications'
            'kcmutils: for breeze-settings')
groups=(plasma)
source=(https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz{,.sig}
        https://invent.kde.org/plasma/breeze/commit/40d558e9.patch)
sha256sums=('9774c695da7d51c45116f85358e002663a1ccc5681b04c9afc7a57e8b075980c'
            'SKIP'
            '6ec28fea1bd84521d1bb7d5cdbe0081e3efe6147779934f4438f521c573d695b')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  patch -d $pkgname-$pkgver -p1 < 40d558e9.patch # Fix cursor getting stuck in split mode
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
