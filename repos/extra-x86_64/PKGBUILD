# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kimap
pkgver=15.08.0
pkgrel=1
pkgdesc="Job-based API for interacting with IMAP servers"
arch=(i686 x86_64)
url="https://projects.kde.org/$pkgname"
license=(LGPL)
depends=(kmime kdelibs4support)
makedepends=(extra-cmake-modules python boost kdoctools)
source=("http://download.kde.org/stable/applications/15.07.80/src/$pkgname-$pkgver.tar.xz")
md5sums=('22c14eb3038281492cc51f38c4123cb8')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
