# $Id$
# Maintainer: dorphell <dorphell@archlinux.org>
# Maintainer: Travis Willard <travis@archlinux.org>
# Maintainer: Douglas Soares de Andrade <douglas@archlinux.org>

pkgname=libpng
pkgver=1.2.31
pkgrel=1
pkgdesc="A collection of routines used to create PNG format graphics files"
arch=('i686' 'x86_64')
url="http://www.libpng.org/pub/png/libpng.html"
license=('custom')
depends=('zlib')
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.bz2
        # http://littlesvr.ca/apng/diff/${pkgname}-${pkgver}-apng.patch
	    ${pkgname}-${pkgver}-apng.patch )
options=('!libtool')

md5sums=('78d2f8c4e0d64f8948819563587302d3'
         'fab42a4ad4de6fcb87dcca6a7a19aefb')

build() {
  cd ${startdir}/src/${pkgname}-${pkgver}

  # Add animated PNG (apng) support
  patch -Np0 -i $startdir/src/${pkgname}-${pkgver}-apng.patch || return 1

  libtoolize --force --copy || return 1
  aclocal || return 1
  autoconf || return 1
  automake --add-missing || return 1

  ./configure --prefix=/usr || return 1
  make ECHO=echo || return 1
  make ECHO=echo DESTDIR=${startdir}/pkg install || return 1

  cd contrib/pngminus
  make ECHO=echo PNGLIB="-L${startdir}/pkg/usr/lib -lpng" -f makefile.std png2pnm pnm2png
  install -m755 png2pnm pnm2png ${startdir}/pkg/usr/bin/
  install -m755 -d ${startdir}/pkg/usr/share/licenses/${pkgname}
  install -m644 ../../LICENSE ${startdir}/pkg/usr/share/licenses/${pkgname}/ || return 1
}
