# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=erofs-utils
pkgver=1.4
pkgrel=2
pkgdesc="Userspace utilities for linux-erofs file system"
arch=(x86_64)
url="https://git.kernel.org/pub/scm/linux/kernel/git/xiang/erofs-utils.git"
license=(GPL2)
# TODO: add xz once microlzma is included
# https://bugs.archlinux.org/task/73331
depends=(fuse2 glibc lz4)
makedepends=(util-linux)
source=("https://git.kernel.org/pub/scm/linux/kernel/git/xiang/${pkgname}.git/snapshot/${pkgname}-${pkgver}.tar.gz")
sha512sums=('ab95d6a7b2d278ee443d1e378c62354db66ce7ab5ce03b3a8d9004cf498c4e43e3e8ced6524444d2ea4871c4db0195489f033180c8a2082c2cba69c46c09692f')
b2sums=('aef1dca8cb95e6104d73a84590319d3c55aba1a4ef5dbdbf470662cb86ee1b66a5707dc1c453470115ec6f2bf1246ee4b6b28aa1cb83b4cb9c8eed45e88668d4')

prepare() {
  cd "$pkgname-$pkgver"
  autoreconf -fiv
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr --enable-fuse
  make
}

check() {
  make -k check -C $pkgname-$pkgver
}

package() {
  depends+=(libuuid.so)

  make DESTDIR="$pkgdir/" install -C $pkgname-$pkgver
  install -vDm 644 $pkgname-$pkgver/{AUTHORS,ChangeLog,README} -t "$pkgdir/usr/share/doc/$pkgname/"
}
