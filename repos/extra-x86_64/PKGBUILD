# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgbase=pyqt6-3d
pkgname=(python-pyqt6-3d)
pkgver=6.3.0
pkgrel=1
pkgdesc='Python bindings for Qt3D'
arch=(x86_64)
url='https://www.riverbankcomputing.com/software/pyqt3d/intro'
license=(GPL3)
groups=(pyqt6)
depends=(qt6-3d python-pyqt6)
makedepends=(sip pyqt-builder)
source=(https://pypi.python.org/packages/source/P/PyQt6-3D/PyQt6_3D-$pkgver.tar.gz)
sha256sums=('fab024b7fb3245d9b463029e0000f46cff95f0bdab603b875fabcaa53d9fe63f')
options=(debug)

build() {
  cd PyQt6_3D-$pkgver
  sip-build \
    --no-make \
    --qmake=/usr/bin/qmake6
  cd build
  make
}

package_python-pyqt6-3d(){
  cd PyQt6_3D-$pkgver/build
  make INSTALL_ROOT="$pkgdir" install
}
