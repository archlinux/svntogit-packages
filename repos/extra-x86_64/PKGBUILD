# $Id$
# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Andrej Gelenberg <andrej.gelenberg@udo.edu>

pkgname=elfutils
pkgver=0.161
pkgrel=2
pkgdesc="Libraries and utilities to handle ELF object files and DWARF debugging information"
arch=('i686' 'x86_64')
url="https://fedorahosted.org/elfutils/"
license=('LGPL3' 'GPL' 'GPL3')
depends=('gcc-libs' 'zlib' 'bzip2' 'xz')
source=(https://fedorahosted.org/releases/e/l/elfutils/${pkgver}/elfutils-${pkgver}.tar.bz2{,.sig}
        0001-tests-Make-deleted-and-vdsosyms-testcases-work-with-.patch)
options=('staticlibs')
sha1sums=('85d48e18359c51e843c49b1894b2f54b85e88ae2'
          'SKIP'
          '86947fb8d0f51a65e19142350925f428ad0c7cb1')
validpgpkeys=('47CC0331081B8BC6D0FD4DA08370665B57816A6A')  # Mark J. Wielaard <mark@klomp.org>

prepare() {
  cd ${pkgname}-${pkgver}

  # https://lists.fedorahosted.org/pipermail/elfutils-devel/2015-January/004541.html
  patch -p1 < "$srcdir"/0001-tests-Make-deleted-and-vdsosyms-testcases-work-with-.patch
}

build() {
  cd ${pkgname}-${pkgver}

  CFLAGS+=" -g"  # required for test-suite success
  ./configure --prefix=/usr --program-prefix="eu-"

  make
}

check() {
  cd ${pkgname}-${pkgver}

  make check
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install
}
