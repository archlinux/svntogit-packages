# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=audiocd-kio
pkgver=17.04.3
pkgrel=1
pkgdesc="Kioslave for accessing audio CDs"
url='https://www.kde.org/applications/multimedia/'
arch=(i686 x86_64)
license=(GPL LGPL FDL)
depends=(kdelibs4support libkcddb libkcompactdisc lame cdparanoia)
makedepends=(extra-cmake-modules kdoctools kdesignerplugin python)
groups=(kde-applications kdemultimedia)
conflicts=(kdemultimedia-audiocd-kio)
source=("https://download.kde.org/stable/applications/${pkgver}/src/audiocd-kio-${pkgver}.tar.xz"{,.sig})
sha256sums=('2ee39bba90939b1aa7e8447acb6960e20db997ff7c9bf5c025049074c5a53fe0'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
