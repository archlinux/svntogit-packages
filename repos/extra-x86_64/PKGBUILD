# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: György Balló <ballogy@freestart.hu>

pkgname=gupnp
pkgver=1.4.3
pkgrel=1
epoch=1
pkgdesc="An object-oriented UPNP framework"
url="https://wiki.gnome.org/Projects/GUPnP"
arch=(x86_64)
license=(LGPL)
depends=(glib2 gssdp util-linux libxml2)
makedepends=(gobject-introspection gtk-doc vala meson git)
optdepends=('python: gupnp-binding-tool')
_commit=1e752b11dc51673c71c70bbf6deb34c96e3abd19  # tags/gupnp-1.4.3^0
source=("git+https://gitlab.gnome.org/GNOME/gupnp.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd gupnp
  git describe --tags | sed 's/^gupnp-//;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd gupnp
  
  # Unbreak man page build
  git revert -n 8795626a07963a91400773cfe04d4f2f1c37ff14
}

build() {
  arch-meson gupnp build -D gtk_doc=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  depends+=(libg{lib,object,io,module}-2.0.so libgssdp-1.2.so libsoup-2.4.so
            libuuid.so)
  provides+=(libgupnp-1.2.so)

  meson install -C build --destdir "$pkgdir"
}
