# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=libinstpatch
pkgver=1.1.6
pkgrel=2
pkgdesc="Instrument file software library"
arch=(x86_64)
url="https://github.com/swami/libinstpatch"
license=(LGPL2.1)
depends=(glibc)
makedepends=(cmake glib2 libsndfile python)
provides=(libinstpatch-1.0.so)
source=($url/archive/v$pkgver/$pkgname-v$pkgver.tar.gz)
sha512sums=('0e22165fc3c045bbc3ab2e1c090728232ed9142797ddbd067b682b9bcfee93850deb76c23b78f49a776e3d099be25fa029673ce8e4a909b2f48860dcdc55803c')
b2sums=('c5881e6559a7fbcb40e1c43c7dbbfc1df9ac9445e83c70044855c192c3b04945ae125c0e06253ba76d3922a1f8fc6965810700ed57018864b319076ee9528d55')

build() {
  local cmake_options=(
    -B build
    -D CMAKE_INSTALL_PREFIX=/usr
    -D LIB_INSTALL_DIR=/usr/lib
    -D CMAKE_BUILD_TYPE=None
    -S $pkgname-$pkgver
    -W no-dev
  )

  cmake "${cmake_options[@]}"
  cmake --build build --verbose
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  depends+=(
    glib2 libglib-2.0.so libgobject-2.0.so
    libsndfile libsndfile.so
  )

  DESTDIR="$pkgdir" cmake --install build
  install -vDm 644 $pkgname-$pkgver/{AUTHORS,ChangeLog,README.md,TODO.tasks} -t "$pkgdir/usr/share/doc/$pkgname/"
}
