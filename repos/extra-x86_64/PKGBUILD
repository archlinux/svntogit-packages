# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=libnumbertext
pkgver=1.0.11
pkgrel=1
pkgdesc="Number to number name and money text conversion library"
arch=('x86_64')
url="https://github.com/Numbertext/libnumbertext"
license=('LGPL3' 'custom')
depends=('gcc-libs')
source=(${pkgname}-${pkgver}::https://github.com/Numbertext/libnumbertext/archive/$pkgver.tar.gz)
sha256sums=('80aad7cab123edc614f904d9f145d1d15cf465084a1a15fca2117525dc746034')

prepare() {
  cd "$pkgname-$pkgver"
  autoreconf -vfi
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd "$pkgname-$pkgver"
  make -k check
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  # add dual license file
  install -Dt "${pkgdir}"/usr/share/licenses/${pkgname} -m644 COPYING
}
