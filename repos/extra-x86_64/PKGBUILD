# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-xcursorgen
pkgver=1.0.6
pkgrel=1
pkgdesc="Create an X cursor file from PNG images"
arch=('i686' 'x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libx11' 'libxcursor' 'libpng')
makedepends=('xorg-util-macros')
groups=('xorg-apps' 'xorg')
source=(https://xorg.freedesktop.org/archive/individual/app/xcursorgen-${pkgver}.tar.bz2{,.sig})
sha256sums=('31c8910f54eb175a8a74a60e7662697467e21a8bf948220a6048a93924b3f66c'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # "Alan Coopersmith <alan.coopersmith@oracle.com>"

build() {
  cd xcursorgen-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd xcursorgen-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
