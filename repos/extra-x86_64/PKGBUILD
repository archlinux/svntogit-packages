# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=coordgen
pkgver=3.0.1
pkgrel=1
pkgdesc='Schrodinger-developed 2D Coordinate Generation'
arch=(x86_64)
url='https://github.com/schrodinger/coordgenlibs'
license=(BSD)
depends=(maeparser)
makedepends=(cmake boost)
source=(https://github.com/schrodinger/coordgenlibs/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('737fd081bcb8a6913aa00b375be96458fe2821a58209c98e7a7e86a64d73a900')

build() {
  cmake -B build -S coordgenlibs-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCOORDGEN_USE_MAEPARSER=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 coordgenlibs-$pkgver/templates.mae -t "$pkgdir"/usr/share/coordgen
  install -Dm644 coordgenlibs-$pkgver/LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
