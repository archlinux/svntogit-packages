# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Andrew Simmons <andrew.simmons@gmail.com>

pkgname=thunar-media-tags-plugin
pkgver=0.4.0
pkgrel=1
pkgdesc="Adds special features for media files to the Thunar File Manager"
arch=('x86_64')
license=('GPL')
url="https://docs.xfce.org/xfce/thunar/media-tags"
groups=('xfce4-goodies')
depends=('taglib' 'thunar')
makedepends=('intltool')
source=(https://archive.xfce.org/src/thunar-plugins/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=('9592111e11699e449c1fbd5f72d1633ef39b00f17a988cd251ba228dab4fc90a')

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-debug
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
