# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=maeparser
pkgver=1.2.3
pkgrel=1
pkgdesc="Maestro file parser"
arch=(x86_64)
url="https://github.com/schrodinger/maeparser"
license=(MIT)
depends=(boost-libs)
makedepends=(cmake boost)
source=($pkgname-$pkgver.tar.gz::"https://github.com/schrodinger/maeparser/archive/v$pkgver.tar.gz")
sha256sums=('ca2e2dead8a1fdd74b624ca0f0c4dde205d0b8abbbfdd62de0786f2d3f6f291c')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir"/$pkgname-$pkgver/LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname
}
