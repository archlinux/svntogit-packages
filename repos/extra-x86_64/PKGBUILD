# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: kevin <kevin@archlinux.org>
# Contributor: Manolis Tzanidakis

pkgname=perl-digest-nilsimsa
pkgver=0.06
pkgrel=23
pkgdesc="Perl version of Nilsimsa code"
arch=('x86_64')
license=('LGPL')
url="https://search.cpan.org/dist/Digest-Nilsimsa/"
depends=('perl')
options=('!emptydirs')
source=("https://www.cpan.org/authors/id/V/VI/VIPUL/Digest-Nilsimsa-$pkgver.tar.gz")
sha256sums=('cd3762cd76803729fd42022d382bc93b26f9b14aed9732eef85b44a9576d2d1e')

build() {
  cd Digest-Nilsimsa-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Digest-Nilsimsa-$pkgver
  make test
}

package() {
  cd Digest-Nilsimsa-$pkgver
  make install DESTDIR="$pkgdir"
}
# vim: ts=2 sw=2 et ft=sh
