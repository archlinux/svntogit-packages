# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Andrew Simmons <andrew.simmons@gmail.com>

pkgname=thunar
pkgver=1.6.7
pkgrel=2
pkgdesc="Modern file manager for Xfce"
arch=('i686' 'x86_64')
url="http://thunar.xfce.org"
license=('GPL2' 'LGPL2.1')
groups=('xfce4')
depends=('desktop-file-utils' 'libexif' 'hicolor-icon-theme' 'libnotify'
         'udev' 'gtk2' 'exo' 'libxfce4util' 'libxfce4ui' 'libpng')
makedepends=('intltool' 'gtk-doc' 'xfce4-panel')
optdepends=('gvfs: for trash support, mounting with udisk and remote filesystems'
            'xfce4-panel: for trash applet'
            'tumbler: for thumbnail previews'
            'thunar-volman: manages removable devices'
            'thunar-archive-plugin: create and deflate archives'
            'thunar-media-tags-plugin: view/edit id3/ogg tags')
install=$pkgname.install
source=(http://archive.xfce.org/src/xfce/$pkgname/${pkgver%.*}/Thunar-$pkgver.tar.bz2
        0001-Do-not-perform-the-complete-folder-reload-in-idle-bu.patch
        0001-Update-F5-binding-for-new-ThunarWindow-reload-signal.patch)
sha256sums=('e35f05d5413ffd26bfe866f47b1d199045e3b42912da47c831d3689e12336ee5'
            '08b0c01412b00c5d9011c8c3d03d436e57484538800179990e5742d49da2e43b'
            '5b7d66872b4bab7ee4ef6ef439280f7b2bbef83190f2a8f624713065f2038a49')

prepare() {
  cd "$srcdir/Thunar-$pkgver"

  # https://bugzilla.xfce.org/show_bug.cgi?id=11849
  patch -Np1 -i ../0001-Do-not-perform-the-complete-folder-reload-in-idle-bu.patch

  # https://bugzilla.xfce.org/show_bug.cgi?id=11859
  patch -Np1 -i ../0001-Update-F5-binding-for-new-ThunarWindow-reload-signal.patch
}

build() {
  cd "$srcdir/Thunar-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --enable-gio-unix \
    --enable-dbus \
    --enable-startup-notification \
    --enable-gudev \
    --enable-notifications \
    --enable-exif \
    --enable-pcre \
    --enable-gtk-doc \
    --disable-debug
  make
}

package() {
  cd "$srcdir/Thunar-$pkgver"

  make DESTDIR=${pkgdir} install
  sed -i 's:x-directory/gnome-default-handler;::' \
    "$pkgdir/usr/share/applications/Thunar-folder-handler.desktop"
}

# vim:set ts=2 sw=2 et:
