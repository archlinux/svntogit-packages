# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kactivities
pkgver=4.13.0
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc='API for using and interacting with the Activity Manager'
url='https://projects.kde.org/projects/kde/kdelibs/kactivities'
license=('GPL' 'FDL')
depends=('baloo')
makedepends=('cmake' 'automoc4' 'mesa')
source=("http://download.kde.org/stable/${pkgver}/src/${pkgname}-${pkgver}.tar.xz")
sha1sums=('4be8bf412c1288e60ee773b192922dccd40be042')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_nepomuk=OFF
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
