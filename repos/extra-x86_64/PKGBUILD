# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kcron
pkgver=16.08.2
pkgrel=1
pkgdesc='Configure and schedule tasks'
url='http://userbase.kde.org/KCron'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdeadmin')
depends=('kio' 'cron')
makedepends=('extra-cmake-modules' 'kdoctools' 'python')
conflicts=('kdeadmin-kcron')
replaces=('kdeadmin-kcron')
source=("http://download.kde.org/stable/applications/${pkgver}/src/kcron-${pkgver}.tar.xz")
sha1sums=('ff1cab92263cb6479e05ac9d33e6ac73a305c5d7')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
