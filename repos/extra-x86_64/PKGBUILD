# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
pkgname=cifs-utils
pkgver=6.3
pkgrel=2
pkgdesc="CIFS filesystem user-space tools"
arch=(i686 x86_64)
url="http://wiki.samba.org/index.php/LinuxCIFS_utils"
license=('GPL')
depends=('libcap-ng' 'keyutils' 'krb5' 'talloc' 'libwbclient' 'pam')
source=(ftp://ftp.samba.org/pub/linux-cifs/cifs-utils/$pkgname-$pkgver.tar.bz2
        '0003-cifskey-better-use-snprintf.patch'
        '0004-cifscreds-better-error-handling-when-key_search-fail.patch'
        '0005-cifscreds-better-error-handling-for-key_add.patch')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  # add fedora patches
  # 40789 CVE-2014-2830
  patch -Np1 -i "$srcdir/0003-cifskey-better-use-snprintf.patch"
  patch -Np1 -i "$srcdir/0004-cifscreds-better-error-handling-when-key_search-fail.patch"
  patch -Np1 -i "$srcdir/0005-cifscreds-better-error-handling-for-key_add.patch"
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  # systemd support is broken in mount.cifs
  # https://bugs.archlinux.org/task/30958
  ./configure --prefix=/usr --sbindir=/usr/bin --disable-systemd
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" ROOTSBINDIR=/usr/bin install
  mkdir -p $pkgdir/etc/request-key.d
  install -m 644 contrib/request-key.d/cifs.idmap.conf $pkgdir/etc/request-key.d
  install -m 644 contrib/request-key.d/cifs.spnego.conf $pkgdir/etc/request-key.d
  # set mount.cifs uid, to enable none root mounting form fstab
  chmod +s $pkgdir/usr/bin/mount.cifs
}
md5sums=('93697dbc043cb4d5c66e15e281f872e5'
         'cc13c6d1b734a446d0f4384e0ce32748'
         '350491f336dc931f9b192228909e5924'
         'ac2b3367363fbc79f8f7fdfcae008a8c')
