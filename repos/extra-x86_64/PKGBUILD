# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=appstream-glib
pkgver=0.5.10
pkgrel=1
pkgdesc="Provides GObjects and helper methods to make it easy to read and write AppStream metadata"
arch=('i686' 'x86_64')
url="http://people.freedesktop.org/~hughsient/appstream-glib/"
license=('LGPL')
depends=('gtk3' 'libyaml' 'pacman' 'gcab')
makedepends=('intltool')
provides=('appdata-tools')
conflicts=('appdata-tools')
replaces=('appdata-tools')
source=(http://people.freedesktop.org/~hughsient/appstream-glib/releases/$pkgname-$pkgver.tar.xz)
md5sums=('9e6aa5c3cebca5b7e1f4fa4cc9670d78')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --enable-alpm

  #https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

check() {
  cd $pkgname-$pkgver
  make -k check || :
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
