# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kiconthemes
pkgver=5.104.0
pkgrel=2
pkgdesc='Support for icon themes'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(qt5-svg kconfigwidgets karchive)
makedepends=(extra-cmake-modules doxygen qt5-tools qt5-doc)
optdepends=('breeze-icons: fallback icon theme')
groups=(kf5)
source=(https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz{,.sig}
        https://invent.kde.org/frameworks/kiconthemes/-/commit/d5d04e3c.patch)
sha256sums=('70f4d9f950acdaa28b539838226ed1cff7e70cdcf177272185bac7c75a6cccdf'
            'SKIP'
            '9d328143a09e6ffc78cec768a4071d78c2862494775adc165f27ddbf8cda25c4')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  patch -d $pkgname-$pkgver -p1 < d5d04e3c.patch # Fix syntax error in enum
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
