# Maintainer: Eric BÃ©langer <eric@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=k3b
pkgver=21.12.3
pkgrel=2
epoch=1
pkgdesc='Feature-rich and easy to handle CD burning application'
arch=(x86_64)
url='https://apps.kde.org/k3b/'
license=(GPL)
depends=(libkcddb kcmutils knotifyconfig libmad kfilemetadata knewstuff
         libmpcdec libdvdread libburn libsamplerate qt5-webkit)
makedepends=(extra-cmake-modules kdoctools)
optdepends=('cdrtools: for CD burning with cdrecord'
            'dvd+rw-tools: for DVD burning support'
            'vcdimager: for VCD burning support'
            'emovix: for bootable multimedia CD/DVD support'
            'cdrdao: for disk-at-once (DAO) mode support'
            'cdparanoia: for CD ripping support' 'transcode: for DVD ripping support')
groups=(kde-applications kde-multimedia)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        ffmpeg5.patch)
sha256sums=('acdbf40e1bccff837c50ba0a9d5e83370c24bc759c51d61385429ac3bf59f845'
            'SKIP'
            '4855cef052eebccbbcfabbb59b197cf9ed1703bbf548187a3596692821d89657')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
options=(debug)

prepare() {
  patch -d $pkgname-$pkgver -p1 < ffmpeg5.patch # Fix build with FFmpeg 5 - patch from Bernhard Rosenkraenzer
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
