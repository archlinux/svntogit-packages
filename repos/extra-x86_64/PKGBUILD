# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=four-in-a-row
pkgver=3.32.0
pkgrel=1
pkgdesc="Make lines of the same color to win"
url="https://wiki.gnome.org/Apps/Four-in-a-row"
arch=(x86_64)
license=(GPL3)
depends=(gtk3 libcanberra librsvg)
makedepends=(meson gobject-introspection yelp-tools appstream-glib vala git)
groups=(gnome-extra)
_commit=2be2ec50d0bc51b2d6ec144ff0c20d9ba4e43951  # tags/3.32.0^0
source=("git+https://gitlab.gnome.org/GNOME/four-in-a-row.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  meson test -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
