# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Eduardo Martins Lopes < edumlopes at gmail dot com >
# Contributor: Poco <poco@atelo.org> (Patched 0.5.4 with atmega16u2 support)
# Contributor: Eric Anderson <ejona86@gmail.com>

pkgname=dfu-programmer
pkgver=1.0.0
pkgrel=1
pkgdesc='Programmer for Atmel chips with a USB bootloader'
url='https://github.com/dfu-programmer/dfu-programmer'
arch=('x86_64')
license=('GPL2')
depends=('glibc' 'libusb' 'libusb-1.0.so')
source=(${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('abb73970e10017f111b118c5be0b98a3fe7ef8c95794b5ca701f21b5a40f518f46ff8a392f09e7eea2ad9a4eb6c43ecc9514bee817c01acf0d6135a5f8f9048f')

prepare() {
  cd ${pkgname}-${pkgver}
  touch ./ChangeLog
  autoreconf -fiv
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
