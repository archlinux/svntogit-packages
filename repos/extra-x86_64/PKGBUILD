# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Silvio Knizek (killermoehre)

pkgname=gnome-disk-utility
pkgver=44.0
pkgrel=1
pkgdesc="Disk Management Utility for GNOME"
url="https://gitlab.gnome.org/GNOME/gnome-disk-utility"
arch=(x86_64)
license=(GPL)
depends=(
  gtk3
  libcanberra
  libdvdread
  libhandy
  libnotify
  libpwquality
  libsecret
  parted
  systemd
  udisks2
)
makedepends=(
  docbook-xsl
  git
  meson
)
groups=(gnome)
_commit=68c13081e29973b31ee37245626b1755b8cd53a6  # tags/44.0^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-disk-utility.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
