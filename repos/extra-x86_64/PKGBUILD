# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=grilo
pkgver=0.3.16
pkgrel=1
pkgdesc="Framework that provides access to various sources of multimedia content"
url="https://wiki.gnome.org/Projects/Grilo"
arch=(x86_64)
license=(LGPL)
depends=(
  liboauth
  libsoup3
  libxml2
  totem-pl-parser
)
makedepends=(
  git
  gobject-introspection
  gtk-doc
  gtk3
  meson
  vala
)
optdepends=(
  'grilo-plugins: Plugins for grilo'
  'gtk3: Test UI'
)
provides=(libgr{ilo,lnet,lpls}-0.3.so)
_commit=923397361b36e95ddd57cff70ba7b0a8cb8d5e8c  # tags/grilo-0.3.16^0
source=("git+https://gitlab.gnome.org/GNOME/grilo.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd grilo
  git describe --tags | sed 's/^grilo-//;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd grilo
}

build() {
  arch-meson grilo build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
