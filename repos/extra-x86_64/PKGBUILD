# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgbase=sof-bin
pkgname=(sof-firmware sof-tools)
pkgver=2.2.3
pkgrel=1
pkgdesc="Sound Open Firmware"
url="https://www.sofproject.org/"
arch=(x86_64)
license=(custom:BSD custom:ISC)
options=(debug)
_binver=v${pkgver}
_tplgver=v${pkgver}
source=(https://github.com/thesofproject/sof-bin/releases/download/$_binver/sof-bin-$_binver.tar.gz)
sha256sums=('591ffd66f1e2b327038c91edf3c8502b4172c1dc850cfe44050eadf8e4fadb42')

package_sof-firmware() {
  options+=(!strip)

  local fwdir="$pkgdir/usr/lib/firmware/intel"
  mkdir -p "$fwdir"

  cd sof-bin-$_binver

  cp -at "$fwdir" sof-$_binver
  ln -sr "$fwdir"/{sof-$_binver,sof}

  cp -at "$fwdir" sof-tplg-$_tplgver
  ln -sr "$fwdir"/{sof-tplg-$_tplgver,sof-tplg}

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 LICENCE* Notice*
}

package_sof-tools() {
  pkgdesc+=" - tools"
  depends=(alsa-lib)

  cd sof-bin-$_binver

  install -Dt "$pkgdir/usr/bin" tools-$_binver/*
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 LICENCE* Notice*
}

# vim:set sw=2 et:
