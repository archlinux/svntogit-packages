# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-2048
pkgver=3.18.0
pkgrel=2
pkgdesc="Obtain the 2048 tile"
url="https://wiki.gnome.org/Apps/2048"
arch=(x86_64 i686)
license=(GPL3)
depends=(clutter-gtk libgee libgames-support)
makedepends=(intltool vala itstool)
groups=(gnome-extra)
install=gnome-2048.install
options=(!emptydirs)
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz
        '0001-game-Make-new_game-work-in-the-case-of-game-not-alre.patch')
sha256sums=('87cf703f5dad02bb22ba464faa5fdf8a0784e64652de23b7f1c8685fc3d69938'
            'bb9eb8e5a977828aeadb974324742c0a5b16abf2a42533db95b1a372c85b16de')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i "../0001-game-Make-new_game-work-in-the-case-of-game-not-alre.patch"
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
