# $Id$
# Maintainer:
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=feh
pkgver=1.8
pkgrel=1
pkgdesc="A fast, lightweight image viewer which uses imlib2"
arch=('i686' 'x86_64')
url="https://derf.homelinux.org/~derf/projects/feh/"
license=('MIT')
depends=('libxinerama' 'giblib' 'perl')
makedepends=('libxt')
source=("https://derf.homelinux.org/~derf/projects/${pkgname}/${pkgname}-${pkgver}.tar.bz2")
md5sums=('fe81334805f4da9b5f1e35b45c444a1a')
sha1sums=('147b5608281b81be8d1f7cf944fd761e8b86ae06')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i -e "s:/usr/local:/usr:g" config.mk
  make || return 1
}

package(){
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install || return 1
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/$pkgname/LICENSE" || return 1
}
