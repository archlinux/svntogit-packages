# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-characters
pkgver=3.20.0
pkgrel=1
pkgdesc="Simple utility application to find and insert unusual characters"
arch=(i686 x86_64)
url="http://gnome.org"
license=('GPL2')
depends=(gjs gtk3 libunistring gnome-desktop)
makedepends=(intltool gobject-introspection)
install=gnome-characters.install
groups=('gnome-extra')
source=(http://download.gnome.org/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('a80dd3e7247aa8a8343fee8b06d4d04561b9ebf5eb41d68ab38ed758afc55b87')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' -e 's/    if test "$export_dynamic" = yes && test -n "$export_dynamic_flag_spec"; then/      func_append compile_command " -Wl,-O1,--as-needed"\n      func_append finalize_command " -Wl,-O1,--as-needed"\n\0/' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
