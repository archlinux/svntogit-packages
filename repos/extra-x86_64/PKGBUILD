# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=libaccounts-glib
pkgver=1.12
pkgrel=1
pkgdesc="Glib-based client library for accessing the online accounts database"
arch=('i686' 'x86_64')
url="http://code.google.com/p/accounts-sso/"
license=('LGPL')
depends=('dbus-glib' 'libxml2' 'sqlite3')
makedepends=('python2-gobject')
options=('!libtool')
source=(http://accounts-sso.googlecode.com/files/$pkgname-$pkgver.tar.gz)
sha1sums=('165e0b89a0bc47459711b1e7e077fb9e14848503')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static \
              PYTHON=python2
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  # Install GI overrides for python 3 as well
  install -dm755 "$pkgdir/usr/lib/python3.3/site-packages/gi/overrides"
  ln -s ../../../../python2.7/site-packages/gi/overrides/Accounts.py "$pkgdir/usr/lib/python3.3/site-packages/gi/overrides/Accounts.py"

  # We don't have Backup Framework on Arch Linux
  rm -r "$pkgdir/usr/share/backup-framework"
}
