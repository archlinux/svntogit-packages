# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=xaw3d
pkgver=1.6.5
pkgrel=1
pkgdesc="Three-D Athena widgets"
arch=('x86_64')
url="https://directory.fsf.org/project/xaw3d/"
license=('MIT')
depends=('libxmu')
makedepends=('xorg-util-macros')
source=(https://xorg.freedesktop.org/archive/individual/lib/libXaw3d-${pkgver}.tar.xz{,.sig})
sha512sums=('b2fbceffcbaff7c332f37769fd6b496ac820de8a80fbf9777416f1895b74a07e4efc3f2bd64db3f584983b24d2b10e9b60cf4aae3ec4a30c06510ae7c213a663'
            'SKIP')
validpgpkeys=('3BB639E56F861FA2E86505690FDD682D974CA72A') # "Matt Turner <mattst88@gmail.com>"
#validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') #  "Alan Coopersmith <alan.coopersmith@oracle.com>"

build() {
  cd libXaw3d-${pkgver}
  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd libXaw3d-${pkgver}
  make DESTDIR="${pkgdir}" install
  
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
