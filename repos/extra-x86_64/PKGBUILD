# $Id$
# Maintainer: Douglas Soares de Andrade <douglas@archlinux.org>

pkgname=glade
pkgver=3.10.2
pkgrel=1
pkgdesc="User interface builder for GTK+ and GNOME."
arch=(i686 x86_64)
license=('GPL' 'LGPL')
depends=('gtk3' 'libxml2' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('intltool' 'python2' 'gtk-doc' 'gobject-introspection' 'python2-gobject2')
optdepends=('python2: Python widgets support')
options=('!libtool')
url="http://glade.gnome.org/"
install=glade.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('0f59ffbd7e1e595396593fedcdb85c51fd01ed05d5ef7c6f724f3a0bcf24d5c4')

build() {
  cd "${srcdir}/$pkgname-${pkgver}"
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static
  make
}

package() {
  cd "${srcdir}/$pkgname-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
