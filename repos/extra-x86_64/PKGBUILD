# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdepim-runtime
pkgver=22.04.3
pkgrel=1
pkgdesc='Extends the functionality of kdepim'
arch=(x86_64)
url='https://kontact.kde.org'
license=(GPL LGPL FDL)
depends=(libkolabxml kdav kholidays akonadi-calendar knotifyconfig kmbox pimcommon akonadi-notes akonadi qca-qt5 qt5-networkauth)
makedepends=(extra-cmake-modules kdoctools libetebase boost)
optdepends=('libetebase: EteSync resource')
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        no-qxmlpatterns.patch)
sha256sums=('475845d0321af2507e90ec9876434998e61748bc847fe6aa4295762b9b6313d2'
            'SKIP'
            '8a97b782f0aa44cf2071556d94395ba326117b8f3634f9793a5943fe538aa568')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
options=(debug)

prepare() {
  patch -d $pkgname-$pkgver -p1 < no-qxmlpatterns.patch # Drop unused dependency
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
