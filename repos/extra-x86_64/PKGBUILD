# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgbase=marble
pkgname=(marble libastro marble-data)
pkgver=15.08.2
pkgrel=1
pkgdesc="Desktop Globe"
arch=(i686 x86_64)
url="https://www.kde.org/applications/system/marble"
license=(GPL)
makedepends=(extra-cmake-modules gpsd quazip-qt5 libwlocate phonon-qt5 qt5-webkit krunner kparts knewstuff qt5-tools)
source=("http://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"
	plugin-dir.patch::"https://quickgit.kde.org/?p=marble.git&a=commitdiff&h=f59d559&o=plain")
md5sums=('85c1b7b07c9fc8952a1a221f4d45c88e'
         '5b2bbae7e54c33230eafbc960f6dd5a6')

prepare() {
  mkdir -p build

# fix installation designer plugins path https://bugs.kde.org/show_bug.cgi?id=348554
  cd $pkgname-$pkgver
  patch -p1 -i ../plugin-dir.patch
  cd ..

  # only build the KDE app
  sed -e '/mobile/d' \
      -e '/qt/d'  \
      -e '/Quick/d' \
      -e '/touch/d' \
      -i $pkgname-$pkgver/src/apps/CMakeLists.txt
}

build() {
  cd build
  cmake ../$pkgbase-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DQT_PLUGINS_DIR=lib/qt/plugins \
    -DBUILD_TESTING=OFF \
    -DBUILD_MARBLE_EXAMPLES=OFF \
    -DBUILD_MARBLE_TOOLS=ON \
    -DBUILD_MARBLE_TESTS=OFF \
    -DMOBILE=OFF
  make
}

package_libastro() {
  pkgdesc='Marble astronomy library'
  depends=(gcc-libs)
  conflicts=('kdeedu-marble<15.07' marble-qt)

  cd build/src/lib/astro
  make DESTDIR="$pkgdir" install
}

package_marble() {
  depends=(libastro marble-data qt5-webkit kparts knewstuff hicolor-icon-theme quazip-qt5)
  optdepends=('gpsd: GPS based geolocation'
            'libwlocate: WLAN based geolocation'
            'krunner: Krunner plugin')
  conflicts=('kdeedu-marble<15.04.3-3' marble-qt)
  replaces=(kdeedu-marble 'marble-qt<15.07')
  groups=(kde-applications kdeedu)
  install=marble.install

  cd build
  make DESTDIR="$pkgdir" install

# provided by libastro
  rm -r "$pkgdir"/usr/include/astro
  rm "$pkgdir"/usr/lib/libastro.*

# provided by marble-data
  rm -r "$pkgdir"/usr/share/{icons,marble/data}
}

package_marble-data() {
  pkgdesc='Data for Marble'
  conflicts=('kdeedu-marble<15.07' 'marble<15.07.80-3' marble-qt)

  cd build/data
  make DESTDIR="$pkgdir" install
}
