# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdepim-runtime
pkgver=15.08.1
pkgrel=2
pkgdesc='Extends the functionality of kdepim'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kdepim-runtime'
license=('GPL' 'LGPL' 'FDL')
depends=(hicolor-icon-theme qt5-quick1 libkgapi libkolab akonadi-socialutils akonadi-calendar
         knotifyconfig kross kalarmcal kmbox kimap kde-syndication)
makedepends=(extra-cmake-modules kdoctools boost)
install=$pkgname.install
source=("http://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz" kdepim-runtime-bug338658.patch)
sha1sums=('fcab275175a82152ee2345a8657e0ab238f7033a'
          '4d0bcdb3887f1146d289471d7e2494e2a76bb8bd')

prepare() {
  mkdir -p build

# Fix https://bugs.kde.org/show_bug.cgi?id=338658
  cd $pkgname-$pkgver
  patch -p1 -i ../kdepim-runtime-bug338658.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
