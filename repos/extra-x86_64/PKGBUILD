# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=signon-ui
pkgver=0.17+20171022
pkgrel=1
_commit=4368bb77d9d1abc2978af514225ba4a42c29a646
pkgdesc='UI component responsible for handling the user interactions which can happen during the login process of an online account'
arch=(x86_64)
url="https://launchpad.net/online-accounts-signon-ui"
license=(GPL)
depends=(libaccounts-qt qt5-webengine signond libnotify)
source=("https://gitlab.com/accounts-sso/signon-ui/repository/archive.tar.gz?ref=$_commit")
sha256sums=('58aa8f7a16e56d20c47471f08ae63ac8dce6a3c8cda8274dfdc233041d541145')

prepare() {
  cd signon-ui-*

  # Do not install tests
  sed -e 's|src \\|src|' -e '/tests/d' -i signon-ui.pro
}

build() {
  cd signon-ui-*

  qmake-qt5 PREFIX=/usr LIBDIR=/usr/lib
  make
}

package() {
  cd signon-ui-*

  make INSTALL_ROOT="$pkgdir" install
}

