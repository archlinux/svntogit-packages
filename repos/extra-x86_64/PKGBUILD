# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=falkon
pkgver=23.04.1
pkgrel=1
pkgdesc='Cross-platform QtWebEngine browser'
arch=(x86_64)
url='https://www.falkon.org/'
license=(GPL3)
depends=(qt5-webengine qt5-x11extras qt5-svg karchive)
makedepends=(extra-cmake-modules qt5-tools kio purpose libgnome-keyring pyside2 shiboken2)
optdepends=('kio: KDE integration'
            'purpose: KDE integration'
            'libgnome-keyring: gnome keyring integration'
            'pyside2: Python plugins')
groups=(kde-applications kde-network)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('c56bc0224d6df90ae9959a8ee81dfe5ef570db2452040c7219d2939237019f02'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
