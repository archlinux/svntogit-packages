# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Joeny Ang <ang(dot)joeny(at)gmail(dot)com>
# Contributor: Brad Fanella <bradfanella@archlinux.us>

pkgname=libraw
pkgver=0.17.2
pkgrel=1
pkgdesc="A library for reading RAW files obtained from digital photo cameras (CRW/CR2, NEF, RAF, DNG, and others)"
arch=('i686' 'x86_64')
url="http://www.libraw.org/"
license=(CDDL LGPL custom)
depends=(lcms2 jasper)
source=("http://www.libraw.org/data/LibRaw-${pkgver}.tar.gz" libraw-gcc6.patch)
sha256sums=('92b0c42c7666eca9307e5e1f97d6fefc196cf0b7ee089e22880259a76fafd15c'
            '317af364c48fd346eef0b116264a8c048aed923e2d850c0f2dd2d30a34b3771a')

prepare() {
  cd LibRaw-$pkgver
# Fix build with GCC 6 (Fedora)
  patch -p0 -i ../libraw-gcc6.patch
}

build() {
  cd LibRaw-$pkgver
  ./configure --prefix=/usr \
    --disable-examples
  make
}

package() {
  cd LibRaw-$pkgver
  make DESTDIR="$pkgdir" install

  install -d "$pkgdir"/usr/share/licenses/$pkgname
  install -D -m644 "$srcdir"/LibRaw-$pkgver/LICENSE.LibRaw.pdf \
    "$pkgdir"/usr/share/licenses/$pkgname
}
