# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Yue

pkgname=libetonyek
pkgver=0.1.2
pkgrel=1
pkgdesc='Library and a set of tools for reading and converting Apple Keynote presentations'
arch=('i686' 'x86_64')
url="https://wiki.documentfoundation.org/DLP/Libraries/libetonyek"
license=('MPL')
depends=('libxml2' 'zlib' 'libwpd' 'boost-libs' 'librevenge')
makedepends=('boost' 'cppunit' 'gperf' 'doxygen' 'glm')
source=(http://dev-www.libreoffice.org/src/$pkgname/$pkgname-$pkgver.tar.xz
        0001-fix-test-on-i386.patch)
md5sums=('c74195efe487a495aba090bf39d072d3'
         '9847660df43a32f6752d8fdfeb039c25')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ${srcdir}/0001-fix-test-on-i386.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
