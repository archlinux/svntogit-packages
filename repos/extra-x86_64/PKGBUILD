# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgbase=gnome-builder
pkgname=(gnome-builder)
pkgver=43.6
pkgrel=1
pkgdesc="An IDE for writing GNOME-based software"
url="https://wiki.gnome.org/Apps/Builder"
arch=(x86_64)
license=(GPL3)
depends=(
  autoconf-archive
  clang
  cmark
  ctags
  d-spy
  desktop-file-utils
  devhelp
  editorconfig-core-c
  flatpak-builder
  gjs
  gspell
  gtk4
  gtksourceview5
  jsonrpc-glib
  libadwaita
  libgit2-glib
  libpanel
  libpeas
  libportal-gtk4
  meson
  python-gobject
  python-lxml
  python-sphinx
  sysprof
  template-glib
  vala
  vte4
  webkit2gtk-5.0
)
makedepends=(
  appstream-glib
  git
  gobject-introspection
  jedi-language-server
  llvm
  mm-common
  python-sphinx_rtd_theme
  yelp-tools
)
checkdepends=(xorg-server-xvfb)
_commit=f1222fae9d5f198502d8a61c3f870fe53edafa98  # tags/43.6^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-builder.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd $pkgbase
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgbase
}

build() {
  local meson_options=(
    -D help=true
  )

  arch-meson $pkgbase build "${meson_options[@]}"
  meson compile -C build
}

check() {
  dbus-run-session xvfb-run -s '-nolisten local' \
    meson test -C build --print-errorlogs
}

package_gnome-builder() {
  depends+=(libgit2.so)
  optdepends=(
    'bash-language-server: Shell code assistance'
    'jedi-language-server: Python code assistance'
  )
  groups=(gnome-extra)

  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
