# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Antonio Rojas

pkgname=plasma-desktop
pkgver=5.1.1
pkgrel=1
pkgdesc='KDE Plasma Desktop'
arch=('i686' 'x86_64')
url='https://www.kde.org/workspaces/plasmadesktop/'
license=('LGPL')
depends=('qt5-graphicaleffects' 'powerdevil' 'polkit-kde-frameworks' 'libcanberra'
         'kwin' 'libusb-compat' 'libxkbfile' 'kmenuedit' 'systemsettings' 'ksysguard')
makedepends=('extra-cmake-modules' 'kdoctools')
optdepends=('plasma-nm: Network manager applet')
conflicts=('kdebase-workspace' 'kdebase-kdepasswd')
install="${pkgname}.install"
groups=('plasma-next')
source=("http://download.kde.org/stable/plasma/${pkgver}/$pkgname-$pkgver.tar.xz")
md5sums=('fab1bfca0feda8e6cece0ba880a49cdb')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DLIBEXEC_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DSYSCONF_INSTALL_DIR=/etc \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
