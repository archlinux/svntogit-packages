# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: AndyRTR <andyrtr@archlinux.org>
# Contributor: Suzy Williams <suzanne.williams3@verizon.net>

pkgname=xfce4-mailwatch-plugin
pkgver=1.2.0
pkgrel=8
pkgdesc="Multi-protocol, multi-mailbox mail watcher for the Xfce4 panel"
arch=('x86_64')
url="http://goodies.xfce.org/projects/panel-plugins/xfce4-mailwatch-plugin"
license=('GPL2')
groups=('xfce4-goodies')
depends=('xfce4-panel' 'gnutls' 'libgcrypt')
makedepends=('intltool')
source=(https://archive.xfce.org/src/panel-plugins/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=('624acc8229a8593c0dfeb28f883f4958119a715cc81cecdbaf29efc8ab1edcad')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
