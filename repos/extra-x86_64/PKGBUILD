# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ndctl
pkgver=65
pkgrel=1
pkgdesc='Utility library for managing the libnvdimm (non-volatile memory device) sub-system in the Linux kernel'
arch=('x86_64')
url="https://github.com/pmem/ndctl"
license=('LGPL')
depends=('json-c' 'kmod' 'systemd-libs' 'libutil-linux')
makedepends=('asciidoctor' 'xmlto' 'bash-completion')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pmem/ndctl/archive/v$pkgver.tar.gz")
sha512sums=('3e468d0c87a27c45f25cc8e165bab5e2914748a3a755a6393b35655178b91a6a9d6fbb16c3ff06df5463caa4fe29432f53130b30ba5cda4faf165d72f382db46')

prepare() {
  # fix location of modprobe.d conf
  sed -i 's|^modprobedir =.*|modprobedir = /usr/lib/modprobe.d/|' $pkgname-$pkgver/Makefile.am
}

build() {
  cd "$srcdir"/$pkgname-$pkgver
  ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
