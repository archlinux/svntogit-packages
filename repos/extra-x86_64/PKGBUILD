# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=cbindgen
pkgver=0.8.7
pkgrel=1
pkgdesc="A tool for generating C bindings to Rust code"
url="https://github.com/eqrion/cbindgen"
arch=(x86_64)
license=(MPL2)
depends=(gcc-libs)
makedepends=(cargo git)
_commit=23a991a5b21e89aa1dcdc70f1371be20c93ece8e  # tags/v0.8.7^0
source=("git+https://github.com/eqrion/cbindgen#commit=$_commit")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  cd $pkgname
  cargo build --release
}

check() {
  cd $pkgname
  # test_expand* fails; needs nightly rust
  cargo test --release || :
}

package() {
  cd $pkgname
  install -Dt "$pkgdir/usr/bin" target/release/cbindgen
  install -Dt "$pkgdir/usr/share/doc/$pkgname" -m644 README.md
}
