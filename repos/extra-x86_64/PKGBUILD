# $Id$
# Maintainer: judd <jvinet@zeroflux.org>
pkgname=alsa-utils
pkgver=1.0.20
pkgrel=2
pkgdesc="An alternative implementation of Linux sound support"
arch=('i686' 'x86_64')
url="http://www.alsa-project.org"
#can not use >=$pkgver due to use of letters in alsa-lib update versioning
depends=("alsa-lib>1.0.19.99" 'dialog' 'pciutils' 'ncurses')
makedepends=('xmlto' 'docbook-xsl')
license=('GPL')
source=(ftp://ftp.alsa-project.org/pub/utils/$pkgname-${pkgver}.tar.bz2
        alsa
        alsaconf.patch
        alsa.conf.d
        alsactl-init-fix-headphone.patch)
backup=(etc/conf.d/alsa)

build() {
  cd $startdir/src/$pkgname-${pkgver}
  patch -Np1 -i ../alsaconf.patch || return 1
  patch -Np1 -i ../alsactl-init-fix-headphone.patch || return 1
  ./configure --prefix=/usr
  make || return 1
  make DESTDIR=$startdir/pkg install
  install -D -m755 ../alsa $startdir/pkg/etc/rc.d/alsa
  install -D -m644 ../alsa.conf.d $startdir/pkg/etc/conf.d/alsa
}
md5sums=('6837e673ef19da96d8bd2f9e18cd9574'
         '9dff78913b13b10e67c4195e90395ed2'
         '434102eb210cc452e3dfd2a4a9841881'
         '93089f0d8872078939f76dd2acaebc2b'
         '7ac2e3ddf559c7383806c6f5844ad583')
