# $Id$
# Maintainer : Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Morgan LEFIEUX <comete@archlinuxfr.org>

pkgname=hplip
pkgver=3.9.2
pkgrel=2
pkgdesc="Drivers for HP DeskJet, OfficeJet, Photosmart, Business Inkjet and some LaserJet."
arch=('i686' 'x86_64')
url="http://hplipopensource.com"
license=('GPL')
makedepends=('pyqt3' 'pkgconfig' 'sane' 'portmap' 'dbus-python')
depends=('cups' 'python' 'ghostscript' 'foomatic-db' 'foomatic-db-hpijs' 'foomatic-db-engine' 'libcups' 'net-snmp>=5.4')
replaces=('hpijs')
options=('!libtool')
install=hplip.install
source=(http://downloads.sourceforge.net/sourceforge/hplip/$pkgname-$pkgver.tar.gz)
optdepends=('dbus-python: for dbus support'
            'sane: for scanner support'
            'portmap: for network support'
            'pyqt3: for running hp-toolbox')

build() {
 cd $srcdir/$pkgname-$pkgver
 ./configure --prefix=/usr --enable-foomatic-rip-hplip-install --enable-foomatic-ppd-install
 make || return 1
 make DESTDIR=$pkgdir/ install

 rm -rf $pkgdir/etc/sane.d
 # remove autostart of hp-daemon
 rm -r $pkgdir/etc/xdg
 # remove non zipped ppd files
 rm $pkgdir/usr/share/ppd/HP/*.ppd
 # move udev rules to /lib/udev/rules.d
 mkdir -p $pkgdir/lib/udev/rules.d
 mv $pkgdir/etc/udev/rules.d/* $pkgdir/lib/udev/rules.d/
 rm -r $pkgdir/etc/udev
 # remove lp user, it's not used on archlinux
 sed -i 's|OWNER="lp", ||g' $pkgdir/lib/udev/rules.d/55-hpmud.rules
}
md5sums=('581224f556a23ac5545dde541b6f54ca')
