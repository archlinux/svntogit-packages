# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Andr√© Klitzing <aklitzing@online.de>

pkgname=exempi
pkgver=2.3.0
pkgrel=1
pkgdesc="A library to parse XMP metadata"
arch=(i686 x86_64)
url="http://libopenraw.freedesktop.org/wiki/Exempi"
license=('custom:BSD')
depends=('gcc-libs' 'expat' 'zlib')
makedepends=('boost')
source=(http://libopenraw.freedesktop.org/download/$pkgname-$pkgver.tar.bz2{,.asc})
md5sums=('1cc8c7f005fd4ef631e9000b3d5433a8'
         'SKIP')
validpgpkeys=('6C44DB3E0BF3EAF5B433239A5FEE05E6A56E15A3') #Hubert Figuiere

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -d "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 "$srcdir/$pkgname-$pkgver/COPYING" \
  	"$pkgdir/usr/share/licenses/$pkgname"
}
