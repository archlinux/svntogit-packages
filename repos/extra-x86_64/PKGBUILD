# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kactivities
pkgver=4.12.2
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc='API for using and interacting with the Activity Manager'
url='https://projects.kde.org/projects/kde/kdelibs/kactivities'
license=('GPL' 'FDL')
depends=('nepomuk-core')
makedepends=('cmake' 'automoc4' 'mesa')
source=("http://download.kde.org/stable/${pkgver}/src/${pkgname}-${pkgver}.tar.xz")
sha1sums=('e5a247270947f0a7a7be45b0c231200cff3f8bc6')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $srcdir/build
  make DESTDIR=$pkgdir install
}
