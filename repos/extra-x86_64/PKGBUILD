# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=baobab
pkgver=3.18.1
pkgrel=1
pkgdesc="A graphical directory tree analyzer"
arch=(i686 x86_64)
url="http://gnome.org"
license=('GPL2')
depends=('dconf' 'gtk3' 'gsettings-desktop-schemas' 'hicolor-icon-theme' 'desktop-file-utils' 'librsvg')
makedepends=('intltool' 'itstool' 'vala' 'gobject-introspection')
groups=('gnome')
install=$pkgname.install
source=(http://download.gnome.org/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('c2ac90426390e77147446a290c1480c49936c0a224f740b555ddaec2675b44b5')

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  # https://bugs.archlinux.org/task/31861
  sed -i '/MimeType/d' "$pkgdir/usr/share/applications/org.gnome.baobab.desktop"
}

# vim:set ts=2 sw=2 et:
