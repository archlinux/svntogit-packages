# $Id$
# Maintainer: damir <damir@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=vcdimager
pkgver=0.7.24
pkgrel=5
pkgdesc="A full-featured mastering suite for authoring disassembling and analyzing Video CD's and Super Video CD's"
arch=('i686' 'x86_64')
url="http://www.vcdimager.org/"
license=('GPL')
depends=('libcdio' 'libxml2' 'popt')
install=vcdimager.install
source=(ftp://ftp.gnu.org/gnu/vcdimager/${pkgname}-${pkgver}.tar.gz
        vcdimager-0.7.24-libcdio-0.83.patch)
md5sums=('3af22978fd79c79d5fda6513b6811145'
         'f4e7376fd9d975951d2b93a5e9123fdc')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i "$srcdir/vcdimager-0.7.24-libcdio-0.83.patch"
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
