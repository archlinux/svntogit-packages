# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=kmail-account-wizard
pkgver=19.08.0
pkgrel=1
pkgdesc="KMail account wizard"
arch=(x86_64)
url="https://kontact.kde.org"
license=(GPL2)
depends=(kidentitymanagement libkleo pimcommon kmailtransport kross)
makedepends=(extra-cmake-modules boost kdoctools qt5-tools knotifyconfig ktexteditor)
groups=(kde-applications kdepim)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('5eddd4029047b3c37146c49e043d17c1f9086bc738726d5da2756cbe9ad28622'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
