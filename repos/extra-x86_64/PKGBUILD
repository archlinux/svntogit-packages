# Maintainer:  Anatol Pomozov <anatol@archlinux.org>

pkgname=virglrenderer
pkgver=0.10.3
pkgrel=2
pkgdesc='A virtual 3D GPU library, that allows the guest operating system to use the host GPU to accelerate 3D rendering'
arch=(x86_64)
url='https://virgil3d.github.io/'
license=(MIT)
depends=(libepoxy mesa libva)
makedepends=(python meson ninja)
checkdepends=(check)
_tag=virglrenderer-$pkgver
source=(virglrenderer-$pkgver.tar.bz2::https://gitlab.freedesktop.org/virgl/virglrenderer/-/archive/$_tag/virglrenderer-$_tag.tar.bz2)
sha256sums=('b8a1f13e128033083713519e4c936bc9df0afe76109f1f2bd5e077b8c13a2d44')

build() {
  cd virglrenderer-$_tag
  meson --prefix=/usr build -Dvideo=true # -Dtests=true
  ninja -C build
}

check() {
  cd virglrenderer-$_tag
  #ninja -C build test  TODO: figure out why tests fail in chroot environment
}

package() {
  cd virglrenderer-$_tag
  DESTDIR="$pkgdir" ninja -C build install
  install -D -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
