# $Id$
# Maintainer: andyrtr <andyrtr funnychar archlinux.org>
# Contributor: Andrew Simmons <andrew.simmons@gmail.com>

pkgname=thunar
pkgver=1.2.2
pkgrel=3
pkgdesc="modern file manager for Xfce"
arch=('i686' 'x86_64')
license=('GPL2' 'LGPL2.1')
url="http://thunar.xfce.org"
groups=('xfce4')
depends=('desktop-file-utils' 'libexif' 'hicolor-icon-theme' 'libnotify>=0.7.1' 'udev'
         'gtk2' 'exo>=0.6.0' 'libxfce4util>=4.8.1' 'libxfce4ui' 'libpng')
makedepends=('intltool' 'gtk-doc' 'xfce4-panel>=4.8.0')
optdepends=('gvfs: for trash support, mounting with udisk and remote filesystems'
            'xfce4-panel: for trash applet'
            'tumbler: for thumbnail previews'
            'thunar-volman: manages removable devices'
            'thunar-archive-plugin: create and deflate archives'
            'thunar-media-tags-plugin: view/edit id3/ogg tags')
options=('!libtool')
install=${pkgname}.install
backup=('etc/polkit-1/localauthority/50-local.d/org.freedesktop.udisks.pkla')
source=(http://archive.xfce.org/src/xfce/${pkgname}/1.2/Thunar-${pkgver}.tar.bz2
	org.freedesktop.udisks.pkla
        fix-gvfs.patch
	fix-samba.patch)

build() {
  cd ${srcdir}/Thunar-${pkgver}
  patch -Np1 -i ../fix-gvfs.patch
  patch -Np1 -i ../fix-samba.patch
  ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--libexecdir=/usr/lib \
	--localstatedir=/var \
	--disable-static \
	--enable-gio-unix \
	--enable-dbus \
	--enable-startup-notification \
	--enable-gudev \
	--enable-notifications \
	--enable-exif \
	--enable-pcre \
	--enable-gtk-doc \
	--disable-debug
  make
}

package() {
  cd ${srcdir}/Thunar-${pkgver}
  make DESTDIR=${pkgdir} install
  sed -i 's:x-directory/gnome-default-handler;::' \
    ${pkgdir}/usr/share/applications/Thunar-folder-handler.desktop

  # install udisks permission file
  install -dm700 ${pkgdir}/etc/polkit-1/localauthority
  install -dm755 ${pkgdir}/etc/polkit-1/localauthority/50-local.d
  install -m644 ${srcdir}/org.freedesktop.udisks.pkla ${pkgdir}/etc/polkit-1/localauthority/50-local.d/
}

md5sums=('a86df0212db71e61f459bda6bc7b7fb6'
         'a7ddb5eec02d9a8e91a2997862e73cd8'
         '167c79462cddc5aaa1fcdc814041350b'
         'c8063873958d89402046f29b78c1d8d4')
