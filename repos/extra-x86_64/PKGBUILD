# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gdb
pkgver=7.0.1
pkgrel=3
pkgdesc="The GNU Debugger"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/gdb/"
license=('GPL3')
depends=('ncurses' 'expat' 'python')
makedepends=('texinfo')
backup=('etc/gdb/gdbinit')
options=('!libtool')
install=gdb.install
source=(http://ftp.gnu.org/gnu/gdb/${pkgname}-${pkgver}.tar.bz2
        gdb-readline-6.0.patch)
md5sums=('53ebade39bfdc1d06744e5188b6438b1'
         '82e9eac057954899092405e9ef97d029')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -Np1 -i $srcdir/gdb-readline-6.0.patch
  
  ./configure --prefix=/usr --disable-nls \
    --with-system-readline \
    --with-system-gdbinit=/etc/gdb/gdbinit
  make || return 1
  make DESTDIR=${pkgdir} install || return 1

  # install "custom" system gdbinit
  install -dm755 $pkgdir/etc/gdb
  touch $pkgdir/etc/gdb/gdbinit

  # resolve conflicts with binutils
  rm -f ${pkgdir}/usr/include/{bfd,dis-asm,symcat,ansidecl,bfdlink}.h
  rm -f ${pkgdir}/usr/lib/{libiberty,libbfd,libopcodes}.a
  rm -f ${pkgdir}/usr/share/info/{bfd.info,configure.info,standards.info}
}
