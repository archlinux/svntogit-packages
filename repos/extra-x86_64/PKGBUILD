# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Dan McGee <dan@archlinux.org>
# Contributor: Florian Zeitz <florob at babelmonkeys dot de>

pkgname=numactl
pkgver=2.0.12
pkgrel=2
pkgdesc="Simple NUMA policy support"
arch=('x86_64')
url="http://oss.sgi.com/projects/libnuma/"
license=('LGPL2.1' 'GPL2')
depends=('perl')
source=("https://github.com/numactl/numactl/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('316b10f50e6eee4cee9cf191f19381ec4a61980354eec3f9fe43fef21465bee28a82adeb9c235d4877e82950ccd14c9473b79fd1e2f5a7f598ec2fac230780ff')

prepare(){
  cd "${pkgname}-${pkgver}"
  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

check() {
  cd "${pkgname}-${pkgver}"
  make -k test || msg "Tests known to fail depending on system load."
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir" install
  # move_pages(2) is provided by man-pages and is more up-to-date there anyway
  rm -rf "$pkgdir/usr/share/man/man2"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
