# Maintainer: RÃ©my Oudompheng <remy@archlinux.org>
# Contributor: Firmicus <francois.archlinux.org>
# Contributor: bender02 at gmx dot com

pkgname=asymptote
pkgver=2.47+14+g38a59370
pkgrel=1
pkgdesc="A vector graphics language (like metapost)"
arch=('x86_64')
url="http://asymptote.sourceforge.net/"
license=("GPL3")
depends=('texlive-core' 'gc' 'freeglut' 'glu' 'gsl' 'fftw' 'libsigsegv')
makedepends=('git' 'ghostscript' 'imagemagick'
             'mesa'               # For OpenGL headers
             'texlive-plainextra' # For texinfo
             'python-pyqt5'       # For xasy GUI generation
            )
optdepends=('python-pyqt5: for the xasy GUI'
            'python-cson:  for the xasy GUI')
#source=("https://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.src.tgz")
#sha1sums=('89b6eaf4148ebe9cae61a171a7a7ce24f38a7cbb')
_commit=38a59370dc5ac720c29e1424614a10f7384b943f
source=("git+https://github.com/vectorgraphics/asymptote#commit=$_commit")
sha1sums=(SKIP)

prepare() {
  cd "${srcdir}/${pkgname}"
  ./autogen.sh
}

build() {
  cd "${srcdir}/${pkgname}"

  ./configure --enable-gc=/usr \
              --prefix=/usr \
              --with-latex=/usr/share/texmf/tex/latex \
              --with-context=/usr/share/texmf/tex/context \
              --enable-offscreen
  make all
}

check() {
  cd "${srcdir}/${pkgname}"
  make check-all
}

package() {
  cd "${srcdir}/${pkgname}"
  make -j1 DESTDIR="${pkgdir}" install-all
  # this dir contains png files that are already embedded in the pdf documentation:
  rm -rf ${pkgdir}/usr/share/info/asymptote

  # copy some data files to their correct location
  mkdir -p "$pkgdir"/usr/share/emacs/site-lisp
  cp "$pkgdir"/usr/share/asymptote/*.el "$pkgdir"/usr/share/emacs/site-lisp
  mkdir -p "$pkgdir"/usr/share/vim/vimfiles/syntax
  cp "$pkgdir"/usr/share/asymptote/*.vim "$pkgdir"/usr/share/vim/vimfiles/syntax
  mkdir -p "$pkgdir"/usr/share/org.kde.syntax-highlighting/syntax
  cd "$pkgdir"/usr/share/asymptote/
  sh asy-kate.sh
  mv asymptote.xml "$pkgdir"/usr/share/org.kde.syntax-highlighting/syntax
}
