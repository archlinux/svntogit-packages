# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=gjs
pkgver=1.38.1
pkgrel=1
pkgdesc="Javascript Bindings for GNOME"
arch=(i686 x86_64)
url="http://live.gnome.org/Gjs"
license=(GPL)
depends=(cairo gobject-introspection 'js>=17.0.0')
options=('!libtool')
source=(http://ftp.gnome.org/pub/gnome/sources/gjs/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('6b2065a0e7bd1db562a70c5fd52ea2499097f1cf02c9c279d1b82b660bfb8176')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-static --libexecdir=/usr/lib
  make
}

check() {
  cd $pkgname-$pkgver
  # Needs a display
  make -k check || :
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
