# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=eog-plugins
pkgver=44.0
pkgrel=1
pkgdesc="Plugins for Eye of Gnome"
url="https://wiki.gnome.org/Apps/EyeOfGnome/Plugins"
arch=(x86_64)
license=(GPL2)
depends=(
  eog
  libchamplain
  libgdata
  python-gobject
)
makedepends=(
  git
  gobject-introspection
  meson
)
_commit=41f9d7d597cbd7423b186ab443b34a3e0feb9e94  # tags/44.0^0
source=("git+https://gitlab.gnome.org/GNOME/eog-plugins.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
