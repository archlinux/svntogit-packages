# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=gutenprint
pkgver=5.2.3
pkgrel=3
pkgdesc="Top quality printer drivers for POSIX systems"
arch=('i686' 'x86_64')
license=('GPL')
install=gutenprint.install
depends=('readline' 'gnutls>=2.4.2')
makedepends=('gimp>=2.6.3' 'gtk2>=2.14.6' 'cups>=1.3.9-3' 'foomatic-db-engine' 'ghostscript>=8.63-4')
optdepends=('cups:		to use cups printer spooler(recommended)'
            'foomatic-db-engine:	to use foomatic spooler'
            'ghostscript:	adds postscript support for ijsgutenprint'
            'gimp:		adds gutenprint plugin to gimp')
source=(http://downloads.sourceforge.net/gimp-print/$pkgname-$pkgver.tar.bz2)
url="http://gimp-print.sourceforge.net/"
replaces=('gimp-print')
options=('!libtool' '!emptydirs')
md5sums=('4db2605739b3cbc6fc72bb1665731aeb')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr --disable-samples --with-cups --with-gimp2 \
	--disable-translated-cups-ppds --with-gimp2-as-gutenprint \
	--disable-libgutenprintui  --enable-libgutenprintui2 --disable-gtktest \
	--enable-cups-ppds --enable-cups-ppds-at-top-level \
	--with-foomatic --with-foomatic3 --with-ghostscript \
	--disable-static --disable-static-genppd --with-ijs \
	--disable-globalized-cups-ppds || return 1
  make || return 1
  make DESTDIR=${pkgdir} install || return 1
}

