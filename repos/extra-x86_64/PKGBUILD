# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=farstream-0.1
_pkgname=farstream
pkgver=0.1.2
pkgrel=3
pkgdesc="Audio/Video Communications Framework (legacy version)"
arch=('i686' 'x86_64')
url="http://www.freedesktop.org/wiki/Software/Farstream"
license=('LGPL2.1')
depends=('libnice' 'gstreamer0.10-base' 'gstreamer0.10-bad-plugins'
         'gstreamer0.10-good' 'gstreamer0.10-ffmpeg')
makedepends=('gobject-introspection' 'gstreamer0.10-python' 'python2-gobject2' 'git' 'gtk-doc')
source=(git://git.collabora.co.uk/git/farstream.git#commit=54e28e95bed4f04bc95dae509c3724399c79fa29
        farstream-0.1.2-prefer-vp8.patch
        codec-discovery-intersect.patch
        rtp-memleak.patch)
sha256sums=('SKIP')

prepare() {
  cd farstream
  patch -Np1 -i ../farstream-0.1.2-prefer-vp8.patch
  patch -Np1 -i ../codec-discovery-intersect.patch
  patch -Np1 -i ../rtp-memleak.patch
}

build() {
  cd farstream
  NOCONFIGURE=1 ./autogen.sh
  ./configure \
    --prefix=/usr \
    --with-package-name='Arch Linux Farstream 0.1 Package' \
    --with-package-origin='httpw://www.archlinux.org/' \
    --enable-gtk-doc --disable-static
  make -j1
}

package() {
  cd farstream
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
sha256sums=('SKIP'
            '5f8f2876bde0f21383b3d059c94914a2fcd63d45a1bb1b5cd2e2208cd2536da5'
            '385d7c72e269bf48bd41937442d819a1e6d0f2472b903670a8a5555f5cb96586'
            'b966312ccad90dd30b78ee2df49e9637b3b573e498668bcc58ac208f254440db')
