# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Troy C < rstrox -ta yahoo -tod com >

pkgname=python-wrapt
pkgver=1.13.3
pkgrel=1
pkgdesc="A Python module for decorators, wrappers and monkey patching"
arch=("x86_64")
url="https://pypi.python.org/pypi/wrapt"
license=("BSD")
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://github.com/GrahamDumpleton/wrapt/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('c6cc83baacf2688aa49ec0fe44db612edc61e50982a667de177df0d3a972e9bd5f38f4d5166eefc6075a5cfae194298835beca93ee98eefb50eec5aad7f36a1c')

build() {
  cd wrapt-$pkgver
  python setup.py build
}

check() {
  cd wrapt-$pkgver
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-3.10:$PYTHONPATH" py.test
}

package() {
  cd wrapt-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
