# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Jonahtan Schaeffer <joschaeffer@gmail.com>
# COntributor: Drake Arconis <lightdrake@gmail.com>

pkgname=jsoncpp
pkgver=0.6.0rc2
_pkgver=0.6.0-rc2
pkgrel=1
pkgdesc='A C++ library for interacting with JSON'
url='https://github.com/open-source-parsers/jsoncpp'
license=('MIT' 'custom:Public_Domain')
arch=('i686' 'x86_64')
depends=('gcc-libs')
makedepends=('scons')
source=("https://github.com/open-source-parsers/${pkgname}/archive/svn-release-${_pkgver}.tar.gz")
md5sums=('29f44fa88d6f06d1b906fa64411eeb4d')

build() {
  cd ${pkgname}-svn-release-${_pkgver}
  scons platform=linux-gcc
}

package() {
  cd ${pkgname}-svn-release-${_pkgver}

  install -d "${pkgdir}"/usr/include/jsoncpp
  cp -r include/json "${pkgdir}"/usr/include/jsoncpp/json

  install -Dm644 buildscons/linux-gcc-*/src/lib_json/libjson_linux-gcc*libmt.so \
    "${pkgdir}"/usr/lib/libjsoncpp.so
  ln -s /usr/lib/libjsoncpp.so "${pkgdir}"/usr/lib/libjsoncpp.so.0
  ln -s /usr/lib/libjsoncpp.so.0 "${pkgdir}"/usr/lib/libjsoncpp.so.0.0.0

  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

