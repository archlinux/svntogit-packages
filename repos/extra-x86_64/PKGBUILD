# Maintainer: Firmicus <firmicus āt gmx dōt net>

pkgname=texlive-fontsextra
pkgver=2008.13014
pkgrel=1
pkgdesc="TeX Live - all sorts of extra fonts"
license=('GPL')
arch=('i686' 'x86_64')
depends=('texlive-core')
makedepends=('lzma-utils')
groups=('texlive-most')
url='http://tug.org/texlive/'
source=("ftp://ftp.archlinux.org/other/texlive/$pkgname-$pkgver-src.zip")
options=('!emptydirs')
install=texlive.install
md5sums=('199b44fdcd890374cc154c68b7bad93b')

build() {
   cd $srcdir
   install -m755 -d $pkgdir/usr/share/texmf-var/arch/installedpkgs || return 1
   sed -i '/^#/d' CONTENTS || return 1
   _revnr=`echo $pkgver | sed 's/2008\.//'`
   install -m644 CONTENTS $pkgdir/usr/share/texmf-var/arch/installedpkgs/${pkgname}_${_revnr}.pkgs || return 1
   for p in *.tar.lzma; do 
	   lzma --force -dc $p | tar xf - || return 1
   done
   install -m755 -d $pkgdir/usr/share || return 1
   find texmf* -type d -exec install -d -m755 $pkgdir/usr/share/'{}' \; || return 1
   find texmf* -type f -exec install -m644 '{}' $pkgdir/usr/share/'{}' \; || return 1
   
   # remove stuff depending on omega
   rm -rf $pkgdir/usr/share/texmf-dist/omega
   find $pkgdir/ -wholename '*ocherokee*' -delete
   find $pkgdir/ -wholename '*oinuit*' -delete
}
