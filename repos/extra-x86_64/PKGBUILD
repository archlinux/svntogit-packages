# $Id$
# Maintainer: tobias <tobias@archlinux.org>
# Contributor: Low Kian Seong <fastmail_low@speedymail.org>

pkgname=bogofilter
pkgver=1.2.2
pkgrel=1
pkgdesc="A fast Bayesian spam filtering tool"
arch=('i686' 'x86_64')
license=(GPL3)
url="http://bogofilter.sourceforge.net"
depends=('db' 'perl' 'gsl')
backup=(etc/bogofilter/bogofilter.cf)
source=(http://sourceforge.net/projects/${pkgname}/files/${pkgname}-current/${pkgname}-${pkgver}/${pkgname}-${pkgver}.tar.bz2)
md5sums=('4bcabdf8c5e7efefcb508eda7e80eebc')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr \
              --sysconfdir=/etc/bogofilter  \
              --localstatedir=/var \
              --enable-transactions
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  mv ${pkgdir}/etc/bogofilter/bogofilter.cf.example ${pkgdir}/etc/bogofilter/bogofilter.cf

  install -dm755 ${pkgdir}/usr/share/${pkgname}/contrib
  install -m644 contrib/* ${pkgdir}/usr/share/${pkgname}/contrib/
}
