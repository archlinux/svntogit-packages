# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=akonadi-calendar
pkgver=15.08.3
pkgrel=1
pkgdesc="Akonadi calendar integration"
arch=(i686 x86_64)
url="https://projects.kde.org/$pkgname"
license=(LGPL)
depends=(kmailtransport kcalutils akonadi-contact)
makedepends=(extra-cmake-modules kdoctools python boost)
source=("http://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
md5sums=('ef006a1821b20bc1ecdf78ef7f9b13b2')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
