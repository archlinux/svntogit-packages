# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kate
pkgver=22.12.3
pkgrel=1
arch=(x86_64)
license=(GPL LGPL FDL)
pkgdesc='Advanced text editor'
groups=(kde-applications kde-utilities)
url='https://apps.kde.org/kate/'
depends=(knewstuff ktexteditor kactivities kuserfeedback hicolor-icon-theme)
optdepends=('konsole: open a terminal in Kate'
            'clang: C and C++ LSP support'
            'python-lsp-server: Python LSP support'
            'texlab: LaTeX LSP support'
            'rust: Rust LSP support'
            'git: git-blame plugin'
            'markdownpart: Markdown preview'
            'svgpart: SVG preview')
makedepends=(extra-cmake-modules kdoctools)
conflicts=(kwrite)
provides=(kwrite)
replaces=(kwrite)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('5bfb05b8dad0c3c7301da0a1998d41de7acf0ec18f40912a59b3e855188f443e'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
