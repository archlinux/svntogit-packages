# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-x11perf
pkgver=1.6.0
pkgrel=1
pkgdesc="Simple X server performance benchmarker"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libx11' 'libxmu' 'libxrender' 'libxft' 'libxext')
makedepends=('xorg-util-macros')
groups=('xorg-apps' 'xorg')
source=(http://xorg.freedesktop.org/archive/individual/app/x11perf-${pkgver}.tar.bz2{,.sig})
sha256sums=('e87098dec1947572d70c62697a7b70bde1ab5668237d4660080eade6bc096751'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # Alan Coopersmith <alan.coopersmith@oracle.com>

build() {
  cd x11perf-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd x11perf-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
