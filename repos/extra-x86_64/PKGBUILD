# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=flatpak-builder
pkgver=1.0.14
pkgrel=2
pkgdesc="Tool to build flatpaks from source"
url="https://flatpak.org"
arch=(x86_64)
license=(LGPL)
depends=(flatpak binutils elfutils unzip tar git bzr patch rpmextract cpio appstream-glib)
makedepends=(intltool gobject-introspection git docbook-xsl xmlto libdwarf)
checkdepends=(valgrind)
replaces=('flatpak<0.9.10')
_commit=a5df218bae3a019bb398a99dd8a3897b1849218c  # tags/1.0.14^0
source=("git+https://github.com/flatpak/flatpak-builder#commit=$_commit"
        "git+https://gitlab.gnome.org/GNOME/libglnx.git")
sha256sums=('SKIP'
            'SKIP')

prepare() {
  cd $pkgname

  git submodule init
  git submodule set-url libglnx "$srcdir/libglnx"
  git submodule update

  # https://github.com/flatpak/flatpak/issues/267
  sed -i '/locale\/C\./d' tests/make-test-runtime.sh

  NOCONFIGURE=1 ./autogen.sh
}

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

build() {
  cd $pkgname

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib \
    --disable-static \
    --with-dwarf-header=/usr/include/libdwarf

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

check() {
  cd $pkgname
  make -k check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
