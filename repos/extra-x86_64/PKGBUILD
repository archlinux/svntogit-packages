# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jonathan Steel <jsteel@aur.archlinux.org>
# Contributor: Justin "juster" Davis <jrcd83@gmail.com>
# Contributor: Fran√ßois Charette <francois.archlinux.org>

pkgname=perl-xml-libxml
pkgver=2.0202
pkgrel=1
pkgdesc="Interface to the libxml library"
arch=('x86_64')
url="https://search.cpan.org/dist/XML-LibXML"
license=('GPL')
depends=('perl-alien-libxml2' 'perl-xml-sax' 'perl-xml-namespacesupport')
checkdepends=('perl-test-pod' 'perl-test-leaktrace' 'perl-cpan-changes' 'perl-uri')
provides=('perl-libxml' 'perl-xml-libxml-common')
replaces=('perl-libxml' 'perl-xml-libxml-common')
options=('!emptydirs')
install=perl-xml-libxml.install
source=(https://www.cpan.org/authors/id/S/SH/SHLOMIF/XML-LibXML-$pkgver.tar.gz)
sha512sums=('1b2b0f3469ef2205998645024be378542fec121931c7b09a8600223efcee92bb9a3619751955e5ade9b06cd70f49ecb759ef31e71da36a9cefbeb87e5e588961')

build() {
  cd XML-LibXML-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd XML-LibXML-$pkgver
  make test
}

package() {
  cd XML-LibXML-$pkgver
  make pure_install doc_install DESTDIR="$pkgdir"/
}
