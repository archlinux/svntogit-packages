# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=tracker3-miners
pkgver=3.1.2
pkgrel=2
pkgdesc="Collection of data extractors for Tracker/Nepomuk"
url="https://wiki.gnome.org/Projects/Tracker"
arch=(x86_64)
license=(GPL)
depends=(tracker3 gst-plugins-base-libs exempi libexif libcue libgrss libgsf
         libgxps libiptcdata libosinfo poppler-glib totem-plparser giflib
         libgexiv2 gupnp-dlna upower libseccomp libnm)
makedepends=(git meson vala asciidoc)
checkdepends=(python-gobject python-tappy gst-plugins-good gst-plugins-base
              gst-libav)
groups=(gnome)
_commit=61ad8d4348b56d105648c9b162097385afc868cd  # tags/3.1.2^0
source=("git+https://gitlab.gnome.org/GNOME/tracker-miners.git#commit=$_commit"
        fix-rpath.diff)
sha256sums=('SKIP'
            'd23ee61df2a8eabb1b4faf7efeedeca228baee549097d5db11e513fcedf7e464')

pkgver() {
  cd tracker-miners
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd tracker-miners
  git apply -3 ../fix-rpath.diff
}

build() {
  arch-meson tracker-miners build -D tests_tap_protocol=true
  meson compile -C build
}

check() {
  # Intermittent test failures: https://gitlab.gnome.org/GNOME/tracker-miners/-/issues/79
  dbus-run-session meson test -C build --print-errorlogs -t 3 || :
}

package() {
  depends+=(libtracker-sparql-3.0.so)

  meson install -C build --destdir "$pkgdir"
}
