# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=knotes
pkgver=20.12.1
pkgrel=2
pkgdesc="Popup notes"
arch=(x86_64)
url="https://kontact.kde.org"
license=(GPL2)
depends=(kdepim-runtime kdnssd kontactinterface)
makedepends=(extra-cmake-modules kdoctools boost)
groups=(kde-applications kdepim)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
         knotes-config-dialog.patch::"https://invent.kde.org/pim/knotes/commit/7be8013b.patch")
sha256sums=('60625a616d2d6665ba9a6b6d01918cf70bdd8eab3ed9f93a7e420122ae98ee85'
            'SKIP'
            'c05963962b5cb2d01b92957d4d43850837698ffb31c62fced742c3a30ff4ac26')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  patch -d $pkgname-$pkgver -p1 -i ../knotes-config-dialog.patch # Fix configuration dialog
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
