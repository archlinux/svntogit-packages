# $Id$
# Maintainer: Aaron Griffin <aaron@archlinux.org>
# Original TU: Jeff Mickey <j@codemac.net>
# Contributor: ciccio.a

pkgname=squashfs-tools
pkgver=4.1
pkgrel=1
pkgdesc="Tools for squashfs, a highly compressed read-only filesystem for Linux."
url="http://squashfs.sourceforge.net"
license=("GPL")
arch=('i686' 'x86_64')
depends=('zlib' 'glibc' 'lzo2' 'xz')
source=(http://downloads.sourceforge.net/sourceforge/squashfs/squashfs${pkgver}.tar.gz)
md5sums=('8e1b2b96f5d5f3fe48fef226ae8cd341')

build() {
    cd ${srcdir}/squashfs${pkgver}/${pkgname}
    sed -i 's|^#XZ_SUPPORT = 1|XZ_SUPPORT = 1|' Makefile
    sed -i 's|^#LZO_SUPPORT = 1|LZO_SUPPORT = 1|' Makefile
    make
}

package() {
    cd ${srcdir}/squashfs${pkgver}/${pkgname}
    install -Dm755 mksquashfs ${pkgdir}/sbin/mksquashfs
    install -m755 unsquashfs ${pkgdir}/sbin/unsquashfs
}
