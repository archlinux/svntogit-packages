# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=nvidia-settings
pkgver=367.18
pkgrel=1
pkgdesc="Tool for configuring the NVIDIA graphics driver"
arch=('i686' 'x86_64')
url="https://github.com/NVIDIA/nvidia-settings"
license=('GPL2')
depends=('jansson' 'gtk3' 'libxv' 'libvdpau' 'nvidia-utils')
makedepends=('git' 'inetutils' 'gtk2')
source=("git+https://github.com/NVIDIA/nvidia-settings#tag=$pkgver")
sha1sums=('SKIP')

prepare() {
  export PREFIX=/usr
  export NV_USE_BUNDLED_LIBJANSSON=0
}

build() {
  cd ${pkgname}
  make
}

package() {
  cd ${pkgname}
  make DESTDIR="${pkgdir}" install

  install -D -m644 doc/nvidia-settings.desktop "${pkgdir}/usr/share/applications/nvidia-settings.desktop"
  install -D -m644 doc/nvidia-settings.png "${pkgdir}/usr/share/pixmaps/nvidia-settings.png"
  sed -e 's:__UTILS_PATH__:/usr/bin:' -e 's:__PIXMAP_PATH__:/usr/share/pixmaps:' -i "${pkgdir}/usr/share/applications/nvidia-settings.desktop"

  rm "$pkgdir/usr/lib/libnvidia-gtk2.so.$pkgver"
}
