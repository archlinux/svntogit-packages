# $Id$
# Maintainer: Angel Velasquez <angvp@archlinux.org> 
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>

pkgname=serf
pkgver=1.3.6
pkgrel=1
pkgdesc="High-performance asynchronous HTTP client library"
arch=('i686' 'x86_64')
url="http://code.google.com/p/serf/"
license=('Apache')
depends=('apr-util')
makedepends=('scons')
options=('!staticlibs')
source=(http://serf.googlecode.com/svn/src_releases/${pkgname}-${pkgver}.zip)
md5sums=('a9f7445530ea7e2755b6df1225b25a4a')

build() {
  cd ${pkgname}-${pkgver}
  scons PREFIX=/usr GSSAPI=/usr/bin/krb5-config
}

check() {
  cd ${pkgname}-${pkgver}
  scons check
}

package() {
  cd ${pkgname}-${pkgver}
  install -d "${pkgdir}/usr"
  scons PREFIX="${pkgdir}/usr" install
}
