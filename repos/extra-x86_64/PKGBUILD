# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=libproxy
pkgname=(libproxy libproxy-mozjs libproxy-webkit)
pkgver=0.4.15+33+g454a499
pkgrel=4
pkgdesc="Automatic proxy configuration management library"
url="https://libproxy.github.io/libproxy/"
arch=(x86_64)
license=(LGPL)
depends=(gcc-libs glib2 dbus)
makedepends=(cmake python python2 perl webkit2gtk git js60)
_commit=454a499692b8fd36c376d4555ddf343316bfddd4  # master
source=("git+https://github.com/libproxy/libproxy#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgbase
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgbase
}

build() {
  cmake -H$pkgbase -Bbuild \
    -DBIPR=0 \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_CXX_FLAGS="$CXXFLAGS" \
    -DCMAKE_C_FLAGS="$CFLAGS" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON \
    -DLIBEXEC_INSTALL_DIR=/usr/lib \
    -DPERL_VENDORINSTALL=yes \
    -DPYTHON2_EXECUTABLE:FILEPATH=/usr/bin/python2 \
    -DWITH_MOZJS=ON \
    -DWITH_WEBKIT3=ON
  cmake --build build
}

check() {
  cmake --build build --target test
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

package_libproxy() {
  optdepends=('networkmanager: NetworkManager configuration module'
              'perl: Perl bindings'
              'python2: Python 2.x bindings'
              'python: Python 3.x bindings'
              'libproxy-mozjs: PAC proxy support (via MozJS)'
              'libproxy-webkit: PAC proxy support (via WebKit)')
  provides+=(libproxy.so)

  DESTDIR="$pkgdir" cmake --build build --target install

### Split
  _pick mozjs "$pkgdir"/usr/lib/libproxy/*/modules/pacrunner_mozjs.so
  _pick webkit "$pkgdir"/usr/lib/libproxy/*/modules/pacrunner_webkit.so
}

package_libproxy-mozjs() {
  pkgdesc="SpiderMonkey-based PAC proxy support for libproxy"
  depends=(libproxy js60)
  mv mozjs/* "$pkgdir"
}

package_libproxy-webkit() {
  pkgdesc="JavaScriptCore-based PAC proxy support for libproxy"
  depends=(libproxy webkit2gtk)
  mv webkit/* "$pkgdir"
}
