# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=xfce4-netload-plugin
pkgver=1.4.1
pkgrel=1
pkgdesc="A netload plugin for the Xfce panel"
arch=('x86_64')
url="https://docs.xfce.org/panel-plugins/xfce4-netload-plugin"
license=('GPL')
groups=('xfce4-goodies')
depends=('xfce4-panel')
makedepends=('intltool')
source=(https://archive.xfce.org/src/panel-plugins/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=('9fac3a3ad52e18584bfb127cd1721d56de1004b9fdd140915fded89704ccb44e')

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-debug
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
