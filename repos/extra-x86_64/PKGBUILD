# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: aurelien <aurelien@archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=xfce4-battery-plugin
pkgver=1.1.5
pkgrel=1
pkgdesc="A battery monitor plugin for the Xfce panel"
arch=('x86_64')
license=('GPL2')
url="https://docs.xfce.org/panel-plugins/xfce4-battery-plugin"
groups=('xfce4-goodies')
depends=('xfce4-panel')
makedepends=('intltool')
source=(https://archive.xfce.org/src/panel-plugins/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=('752233bfb320ee1e26104a656cbb868299f562733063e2b9a18f0966585ce213')

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-debug
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
