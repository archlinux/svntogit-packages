# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=libxft
pkgver=2.3.5
pkgrel=1
pkgdesc="FreeType-based font drawing library for X"
arch=('x86_64')
license=('custom')
url="https://xorg.freedesktop.org/"
depends=('fontconfig' 'libxrender')
makedepends=('pkgconfig')
source=(${url}/releases/individual/lib/libXft-${pkgver}.tar.xz{,.sig})
sha512sums=('e1e037dbc4f8bbf8204bdf99a199c82b5f1182875d28bd9f0a4ec2aec95b05de21e97029ec036b9a3f70f1d6e8a42f0052f4ab30809aee7411fa2e438899db9e'
            'SKIP')
#validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # "Alan Coopersmith <alan.coopersmith@oracle.com>"
validpgpkeys=('19882D92DDA4C400C22C0D56CC2AF4472167BE03') # Thomas E. Dickey (use for email) <dickey@his.com>

build() {
  cd libXft-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static
  make
}

package() {
  cd libXft-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"

}
