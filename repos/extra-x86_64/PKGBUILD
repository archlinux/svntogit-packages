# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=geocode-glib
pkgver=3.26.1
pkgrel=1
pkgdesc="Helper library for geocoding services"
url="https://gitlab.gnome.org/GNOME/geocode-glib"
arch=(x86_64)
license=(LGPL)
depends=(glib2 json-glib libsoup)
makedepends=(gobject-introspection git gtk-doc meson)
_commit=8280732c9994e56931e751540f2890603182364a  # tags/3.26.1^0
source=("git+https://gitlab.gnome.org/GNOME/geocode-glib.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  sed -i 's/gnome/Adwaita/' icons/meson.build
}

build() {
  arch-meson $pkgname build -D enable-installed-tests=false
  ninja -C build
}

check() {
  # Needs sv_SE.utf8, will not pass in chroot
  : meson test -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}

