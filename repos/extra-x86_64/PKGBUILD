# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=akonadi-calendar
pkgver=20.12.1
pkgrel=1
pkgdesc="Akonadi calendar integration"
arch=(x86_64)
url="https://kontact.kde.org"
license=(LGPL)
depends=(kmailtransport kcalutils akonadi-contacts)
makedepends=(extra-cmake-modules boost)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('85db4f06e8af322eb65ad1dd37a8a2eec6277b99d0012f3323f2fa3f8023fe7f'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
