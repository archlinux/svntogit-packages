# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=prison
pkgver=5.25.0.git20160814
_commit=f109d48
pkgrel=1
pkgdesc="A barcode API to produce QRCode barcodes and DataMatrix barcodes"
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kdesupport/prison'
license=('GPL')
depends=('qt5-base' 'libdmtx' 'qrencode')
makedepends=('extra-cmake-modules')
conflicts=('prison-frameworks')
replaces=('prison-frameworks')
source=($pkname-$pkgver::"https://quickgit.kde.org/?p=prison.git&a=snapshot&h=${_commit}&fmt=tgz")
md5sums=('d7f703b53801d88dc913eefb38a39326')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../prison \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
