# Maintainer: Laurent Carlier <lordheavym@gmail.com>

pkgname=spirv-tools
pkgver=2020.6
pkgrel=1
pkgdesc="API and commands for processing SPIR-V modules"
arch=('x86_64')
url="https://www.khronos.org/vulkan/"
license=('custom')
groups=(vulkan-devel)
depends=('gcc-libs')
makedepends=('cmake' 'python' 'git' 'ninja')
source=("git+https://github.com/KhronosGroup/SPIRV-Tools.git#tag=v${pkgver}"
        "git+https://github.com/KhronosGroup/SPIRV-Headers.git#commit=f027d53ded7e230e008d37c8b47ede7cd308e19d") # for v2020.6
sha256sums=('SKIP'
            'SKIP')

prepare() {
  cd SPIRV-Tools

  rm -rf build && mkdir build
}

build() {
  cd SPIRV-Tools/build

  cmake .. \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DCMAKE_BUILD_TYPE=Release \
      -DSPIRV_WERROR=Off \
      -DBUILD_SHARED_LIBS=ON \
      -DSPIRV_TOOLS_BUILD_STATIC=OFF \
      -DSPIRV-Headers_SOURCE_DIR="${srcdir}"/SPIRV-Headers
  ninja
}

package() {
  cd SPIRV-Tools/build

  DESTDIR="${pkgdir}" ninja install

  install -dm755 "${pkgdir}"/usr/share/licenses/${pkgname}
  install -m644 ../LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/
}
