# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Troy C < rstrox -ta yahoo -tod com >

pkgbase=python-wrapt
pkgname=(python-wrapt python2-wrapt)
pkgver=1.12.0
pkgrel=1
pkgdesc="A Python module for decorators, wrappers and monkey patching"
arch=("x86_64")
url="https://pypi.python.org/pypi/wrapt"
license=("BSD")
makedepends=('python' 'python2')
checkdepends=('python-pytest' 'python2-pytest')
source=("$pkgname-$pkgver.tar.gz::https://github.com/GrahamDumpleton/wrapt/archive/$pkgver.tar.gz")
sha512sums=('56a88e223dfe3d1b0f5fa01219b0a9a710d5a67cacd87d8766cf122498c47a41f4e64d2fcb0d3c939bc775893a7edcac68295754819b37ca469b301f1e98df41')

prepare() {
  cp -a wrapt-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/wrapt-$pkgver
  python setup.py build

  cd "$srcdir"/wrapt-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/wrapt-$pkgver
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-3.8:$PYTHONPATH" py.test

  cd "$srcdir"/wrapt-$pkgver-py2
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-2.7:$PYTHONPATH" py.test2
}

package_python-wrapt() {
  depends=('python')

  cd wrapt-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-wrapt() {
  depends=('python2')

  cd wrapt-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
