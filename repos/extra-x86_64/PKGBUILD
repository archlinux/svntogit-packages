# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=libuninameslist
pkgver=20221022
pkgrel=1
pkgdesc='Large, sparse array mapping each unicode code point to the annotation data for it'
url='https://github.com/fontforge/libuninameslist'
license=('custom')
arch=('x86_64')
source=("https://github.com/fontforge/${pkgname}/releases/download/${pkgver}/${pkgname}-dist-${pkgver}.tar.gz")
sha256sums=('92c833936d653b2f205fb5e7ac82818311824dabdc7abdc2e81f07c3a0ea39bb')

prepare() {
  cd ${pkgname}-${pkgver}
  autoreconf -i
  automake --foreign -Wall
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
