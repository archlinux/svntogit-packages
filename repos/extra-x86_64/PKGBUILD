# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=libqrtr-glib
pkgver=1.2.0
pkgrel=1
pkgdesc="Qualcomm IPC Router protocol helper library"
arch=(x86_64)
url="https://gitlab.freedesktop.org/mobile-broadband/libqrtr-glib"
license=(GPL2)
depends=(glib2)
makedepends=(gtk-doc meson git gobject-introspection)
provides=(libqrtr-glib.so)
_commit=7586514fbe1580e042039519bcc6dec537ded40a  # tags/1.2.0^0
source=("git+https://gitlab.freedesktop.org/mobile-broadband/libqrtr-glib.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-rc/rc/;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
