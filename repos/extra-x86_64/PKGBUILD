# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=sushi
pkgver=3.20.0
pkgrel=1
pkgdesc="A quick previewer for Nautilus"
arch=(i686 x86_64)
url="https://git.gnome.org/browse/sushi"
license=(GPL2)
groups=(gnome)
depends=(clutter-gtk clutter-gst gjs libmusicbrainz5 webkit2gtk gtksourceview3 evince)
optdepends=('unoconv: OpenDocument formats')
makedepends=(intltool gobject-introspection)
source=(https://download.gnome.org/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('6e729c789e9e7f02505e25d4ac6cfed47e676366f0942fca740094f7fe9eae9e')

prepare() {
  cd $pkgname-$pkgver
}

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr --libexecdir=/usr/lib/$pkgname

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' -e 's/    if test "$export_dynamic" = yes && test -n "$export_dynamic_flag_spec"; then/      func_append compile_command " -Wl,-O1,--as-needed"\n      func_append finalize_command " -Wl,-O1,--as-needed"\n\0/' libtool

  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
