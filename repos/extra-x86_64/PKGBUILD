# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>

pkgname=xfce4-session
pkgver=4.18.1
pkgrel=2
pkgdesc="Xfce's session manager"
arch=('x86_64')
url="https://docs.xfce.org/xfce/xfce4-session/start"
license=('GPL2')
groups=('xfce4')
depends=('libxfce4ui' 'libwnck3' 'xfconf' 'libsm' 'polkit' 'xorg-iceauth'
         'xorg-xinit' 'xorg-xrdb' 'polkit-gnome' 'hicolor-icon-theme')
makedepends=('intltool')
optdepends=('gnome-keyring: for keyring support when GNOME compatibility is enabled'
            'xfce4-screensaver: for locking screen with xflock4'
            'xscreensaver: for locking screen with xflock4'
            'light-locker: for locking screen with xflock4')
source=(https://archive.xfce.org/src/xfce/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2
        $pkgname-fix-xfconf-memory-management.patch::https://gitlab.xfce.org/xfce/xfce4-session/-/commit/911f5b8101de.patch
        $pkgname-fix-gqueue-memory-management.patch::https://gitlab.xfce.org/xfce/xfce4-session/-/commit/cff23d0fadd5.patch
        source-system-xinitrc-scripts.patch
        xfce-polkit-gnome-authentication-agent-1.desktop)
sha256sums=('1c5a957eeef259db3e8bbd9bb50c2b120146141e99b17a271e58c40fd825c5cf'
            '384ec4deb2e01ba8a3aa50e99f3e1e7409194053f0a3868405d51e80dedab64a'
            '8fa4b70dac18834b8839feff0d172636329c97ac6ad16a67241831cd4868e0aa'
            '6f14d529e4c4f30cd547110bd444cee8dc70c90511a397de18acb6c1fd63ea3e'
            '74c94c5f7893d714e04ec7d8b8520c978a5748757a0cdcf5128492f09f31b643')

prepare() {
  cd $pkgname-$pkgver

  # https://gitlab.xfce.org/xfce/xfce4-session/-/issues/166
  patch -Np1 -i ../$pkgname-fix-xfconf-memory-management.patch
  patch -Np1 -i ../$pkgname-fix-gqueue-memory-management.patch

  # https://bugzilla.xfce.org/show_bug.cgi?id=15440
  patch -Np1 -i ../source-system-xinitrc-scripts.patch
}

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-debug
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  # Provide a default PolicyKit Authentication Agent (FS#42569)
  install -d "$pkgdir/etc/xdg/autostart"
  cp "$srcdir/xfce-polkit-gnome-authentication-agent-1.desktop" \
    "$pkgdir/etc/xdg/autostart/"
}

# vim:set ts=2 sw=2 et:
