# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=qt5-imageformats
_qtver=5.13.0
pkgver=${_qtver/-/}
pkgrel=2
arch=('x86_64')
url='https://www.qt.io'
license=('GPL3' 'LGPL3' 'FDL' 'custom')
pkgdesc='Plugins for additional image formats: TIFF, MNG, TGA, WBMP'
depends=('qt5-base' 'jasper' 'libmng' 'libwebp')
makedepends=()
groups=('qt' 'qt5')
_pkgfqn="${pkgname/5-/}-everywhere-src-${_qtver}"
source=("https://download.qt.io/official_releases/qt/${pkgver%.*}/${_qtver}/submodules/${_pkgfqn}.tar.xz"
        qtimageformats-system-libtiff.patch::"https://code.qt.io/cgit/qt/qtimageformats.git/patch/?id=626c3053")
sha256sums=('91cb638e5f856acc17ab5a2e1a052dc91bb1af2bdbd7d99ba6a8a48f4a15f499'
            '272e6b1e4eaeb11ea97ae9e563c6375ed1a202bf21339cdd85dae9341dbd9130')

prepare() {
  mkdir -p build

  cd $_pkgfqn
  patch -p1 -i ../qtimageformats-system-libtiff.patch # Fix using system libtiff
}

build() {
  cd build

  qmake ../${_pkgfqn}
  make
}

package() {
  cd build
  make INSTALL_ROOT="$pkgdir" install

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt5-base "$pkgdir"/usr/share/licenses/${pkgname}
}
