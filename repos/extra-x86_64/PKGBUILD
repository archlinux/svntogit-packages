# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-maps
pkgver=3.28.0
pkgrel=1
pkgdesc="A simple GNOME 3 maps application"
url="https://wiki.gnome.org/Apps/Maps"
arch=(x86_64)
license=(GPL)
depends=(geocode-glib geoclue2 gjs libchamplain gnome-themes-standard libgee folks gfbgraph)
makedepends=(intltool gobject-introspection gnome-common git)
groups=(gnome)
_commit=ccfaa624b184ae1c23b2ec43b5efed4ecc75d82b  # tags/v3.28.0^0
source=("git+https://git.gnome.org/browse/gnome-maps#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --disable-schemas-compile
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname
  make DESTDIR="${pkgdir}" install
}
