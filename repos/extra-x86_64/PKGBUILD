# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Aaron Griffin <aaron@archlinux.org>
# Original TU: Jeff Mickey <j@codemac.net>
# Contributor: ciccio.a

pkgname=squashfs-tools
pkgver=4.6
pkgrel=2
pkgdesc='Tools for squashfs, a highly compressed read-only filesystem for Linux'
arch=(x86_64)
url='https://github.com/plougher/squashfs-tools'
license=(GPL2)
depends=(
  glibc
  gcc-libs
  lz4
  lzo
  xz
  zlib
  zstd
)
makedepends=(help2man)
source=("$url/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        '0001-xattr-fix-the-name-in-error-message.patch')
sha512sums=('3a9effb9a5cf46fbb9f393e58bd938874dc4121828b77c67d659117ee84643917998a8503d629f46f1eff1826f6d7ae59ac2d803a5cdc3cfb1006ef2b3abf8c8'
            'c0f86bf3ab686fb889ccd302faeff60aa9d9188a35cf553e5ad53c638066e603adb80a51ef61fad16baf283b67b834a0dd120a90091c2f87c61c8216b2eee57f')
b2sums=('1c04a8f6149863667151e76b3c1ecfdd5e9a181b305bea694af1d4968ac361114c9e926b351dcee2647796a21bc8cfc55b3d95f020ad88c3388007460de26053'
        'fbf7008f37c630a5a95ecaa0b05f0b808508f1b23b838e040f77aebbe8ed459a5a24f728c3730c4e22479f5711b8984380c5ff6e602d9a1b2bad0e25a5aa1804')

prepare() {
  cd $pkgname-$pkgver/

  patch -Np1 < ../0001-xattr-fix-the-name-in-error-message.patch
}

build() {
  local make_options=(
    GZIP_SUPPORT=1
    LZ4_SUPPORT=1
    LZMA_XZ_SUPPORT=1
    LZO_SUPPORT=1
    XATTR_SUPPORT=1
    XZ_SUPPORT=1
    ZSTD_SUPPORT=1
    -C $pkgname-$pkgver/$pkgname
  )

  make "${make_options[@]}"
}

package() {
  local make_options=(
    INSTALL_PREFIX="$pkgdir/usr"
    INSTALL_MANPAGES_DIR='$(INSTALL_PREFIX)/share/man/man1'
    install
    -C $pkgname-$pkgver/$pkgname
  )

  make "${make_options[@]}"
  install -vDm 644 $pkgname-$pkgver/{ACTIONS-README,CHANGES,"README-$pkgver",USAGE*} -t "$pkgdir/usr/share/doc/$pkgname/"
}
