# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-builder
pkgver=3.18.1
pkgrel=2
pkgdesc="An IDE for writing GNOME-based software"
url="https://wiki.gnome.org/Apps/Builder"
arch=(i686 x86_64)
license=(GPL3)
depends=(gtksourceview3 devhelp libgit2-glib gjs python-gobject clang desktop-file-utils
         ctags libpeas vte3 vala python-jedi)
makedepends=(intltool llvm gobject-introspection gtk-doc)
groups=(gnome-extra)
install=gnome-builder.install
source=(http://download.gnome.org/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz
        git1.patch git2.patch)
sha256sums=('501c95220dcf8ca44a5748e863492377fe2c3aee78a95973d6819b1836e5407c'
            'b1a7361c668762a390b63359bf9733e348a2d93c25709669a5eaae178e7127a8'
            'a06214efcac9b14e2633c3d69b8a75df1f39f15a13f844d213d04c98c40dcd6d')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../git1.patch
  patch -Np1 -i ../git2.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib --enable-gtk-doc
  make
}

check() {
  cd $pkgname-$pkgver
  make -k check || :
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
