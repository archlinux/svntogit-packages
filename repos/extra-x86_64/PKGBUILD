# $Id$
# Maintainer: Hugo Doria <hugo@archlinux.org>

pkgname=banshee
pkgver=1.5.3
pkgrel=1
pkgdesc="Music management and playback for GNOME"
arch=('i686' 'x86_64')
url="http://banshee-project.org/"
license=('MIT')
depends=('sqlite3>=3.6.20' 'ndesk-dbus-glib>=0.4.1' 'mono-addins>=0.4' 'taglib-sharp>=2.0.3.2' 'gstreamer0.10-good-plugins>=0.10.17' 'gstreamer0.10-base-plugins>=0.10.25' 'gnome-sharp>=2.24.1' 'libmtp>=0.3.7' 'ipod-sharp>=0.8.5' 'boo' 'mono-zeroconf' 'libxxf86vm' 'gnome-icon-theme>=2.28.0' 'desktop-file-utils')
makedepends=('pkgconfig' 'intltool')
optdepends=('gstreamer0.10-ugly-plugins: Extra media codecs'
	    'gstreamer0.10-bad-plugins: Extra media codecs'
	    'gstreamer0.10-ffmpeg: Extra media codecs'
	    'brasero: CD burning'
	    'notify-sharp-svn: Display Banshee tray icon')
options=('!libtool' '!makeflags' '!emptydirs')
install=banshee.install
source=(http://download.banshee-project.org/${pkgname}/stable/${pkgver}/${pkgname}-1-${pkgver}.tar.bz2)
md5sums=('953124a1be0e9299f092dbf2f9742376')

build() {
  export MONO_SHARED_DIR="${srcdir}/.wabi"
  mkdir -p "${MONO_SHARED_DIR}"
  cd "${srcdir}/${pkgname}-1-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --disable-docs --disable-static || return 1
  make || return 1
}

package() {
  export MONO_SHARED_DIR="${srcdir}/.wabi"
  mkdir -p "${MONO_SHARED_DIR}"
  cd "${srcdir}/${pkgname}-1-${pkgver}"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/" || return 1

  install -d -m755 "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain banshee-1 ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
