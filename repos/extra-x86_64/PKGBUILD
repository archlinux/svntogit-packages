# $Id$
# Contributor: Dale Blount <dale@archlinux.org>
# Contributor: Gregor Ibic <gregor.ibic@intelicom.si>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=clamav
pkgver=0.97.3
pkgrel=1
pkgdesc='Anti-virus toolkit for Unix'
arch=('i686' 'x86_64')
depends=('bzip2' 'zlib' 'libtool')
options=('!libtool')
license=('GPL')
backup=('etc/clamav/clamd.conf' 'etc/clamav/freshclam.conf' 'etc/conf.d/clamav')
url='http://www.clamav.net/'
source=("http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        'rc.d'
        'conf.d'
        'logrotate'
        'config.patch')
sha1sums=('263c7b8b7dc1536652ab64e32263d1c60e138655'
          '7f15f0b13a1c11235bc99ef0add01efd8a442f07'
          'cb116cdab49a810381a515cbcfb6a6c148547f07'
          'be3310d2b41a68ce06e33c84ab68ffe59fdce104'
          '701a61571788d10ff7af01597785835c6bfea918')

install=install

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	patch -p1 -i ../config.patch
	./configure \
		--prefix=/usr \
		--sysconfdir=/etc/clamav \
		--with-dbdir=/var/lib/clamav \
		--disable-clamav \

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	# make sure conf files get installed, because make install
	# doesn't do that if clamav is already installed upon building.
	install -D -m644 etc/clamd.conf "${pkgdir}/etc/clamav/clamd.conf"
	install -D -m644 etc/freshclam.conf "${pkgdir}/etc/clamav/freshclam.conf"

	install -D -m644 ../logrotate "${pkgdir}/etc/logrotate.d/clamav"
	install -D -m644 ../conf.d "${pkgdir}/etc/conf.d/clamav"
	install -D -m755 ../rc.d "${pkgdir}/etc/rc.d/clamav"

	# un-distribute databases to require freshclam
	rm "${pkgdir}"/var/lib/clamav/*.cvd
}
