# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kjournald
pkgver=23.04.1
pkgrel=1
pkgdesc='Framework for interacting with systemd-journald'
url='https://invent.kde.org/system/kjournald'
arch=(x86_64)
license=(GPL3)
groups=(kde-applications kde-system)
depends=(systemd kcoreaddons ki18n kirigami2)
makedepends=(extra-cmake-modules)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('cb2bfd1e112cea57a742a1b4667422d785e4479b16b23027c87a6d35a9b37956'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
