# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kcharselect
pkgver=15.04.3
pkgrel=1
pkgdesc='Character Selector'
url="http://kde.org/applications/utilities/kcharselect/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kxmlgui')
makedepends=('extra-cmake-modules' 'python' 'kdoctools')
groups=('kde-applications' 'kdeutils')
conflicts=('kdeutils-kcharselect')
replaces=('kdeutils-kcharselect')
source=("http://download.kde.org/stable/applications/${pkgver}/src/kcharselect-${pkgver}.tar.xz")
sha1sums=('d906ea3b9444ba49ed9470c1d62b2d35131144b1')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../kcharselect-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd $srcdir/build
  make DESTDIR=$pkgdir install
}
