# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=lib2geom
pkgver=1.2.2
pkgrel=2
pkgdesc='Easy to use 2D geometry library in C++'
arch=('x86_64')
url='https://gitlab.com/inkscape/lib2geom'
license=('GPL')
depends=('glib2' 'gsl' 'double-conversion' 'cairo' 'ragel')
makedepends=('cmake' 'boost' 'gtest')
source=("https://gitlab.com/inkscape/${pkgname}/-/archive/${pkgver}/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('1c22a7be571d2e2f3ee98424614ffd6a42fe73dedbd2ccfb0967ecb02f77edeb')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  mkdir -p build
  cd build
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -D2GEOM_BOOST_PYTHON=NO \
    -D2GEOM_BUILD_SHARED=YES \
    -DCMAKE_BUILD_TYPE=Release
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}/build/"

  make test
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/build/"

  make DESTDIR="${pkgdir}/" install
}
