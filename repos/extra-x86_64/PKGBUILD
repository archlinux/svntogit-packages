# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=gdk-pixbuf2
pkgver=2.36.0+2+ga7c869a
pkgrel=1
pkgdesc="An image loading library"
arch=('i686' 'x86_64')
url="http://www.gtk.org/"
license=('LGPL2.1')
depends=('glib2' 'libpng' 'libtiff' 'libjpeg' 'libx11' 'jasper')
makedepends=('gtk-doc' 'gobject-introspection' 'git')
install=gdk-pixbuf2.install
_commit=a7c869a8882336d9a720c2ca607173acbdfffb47
source=("git://git.gnome.org/gdk-pixbuf#commit=$_commit"
        gdk-pixbuf-query-loaders.hook)
sha256sums=('SKIP'
            '963afcfacde9fd36031d93c8aa4c94a669ab1dbe9ba0d2cf17599b7d358e54c2')

pkgver() {
  cd gdk-pixbuf
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd gdk-pixbuf
  NOCONFIGURE=1 ./autogen.sh
}
  
build() {
    cd gdk-pixbuf
    ./configure --prefix=/usr \
        --with-x11 \
        --with-libjasper \
        --with-included-loaders=png \
        --enable-gtk-doc
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
    make
}

package() {
    cd gdk-pixbuf
    make DESTDIR="$pkgdir" install
    install -Dm644 ../gdk-pixbuf-query-loaders.hook "$pkgdir/usr/share/libalpm/hooks/gdk-pixbuf-query-loaders.hook"
}

# vim:set et sw=4:
