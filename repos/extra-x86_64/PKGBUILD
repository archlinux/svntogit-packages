# $Id$
# Maintainer: kevin <kevin@archlinux.org>

pkgname=expect
pkgver=5.44.1.10
pkgrel=1
pkgdesc="A tool for automating interactive applications"
arch=(i686 x86_64)
url="http://expect.nist.gov/"
depends=('tcl>=8.5.3')
makedepends=('tk>=8.5.3')
# CVS checkout on gerolde.
source=(ftp://ftp.archlinux.org/other/expect/expect-${pkgver}.tar.bz2)
md5sums=('da02c862015da2d67f72f1693adab0db')
#source=(http://expect.nist.gov/src/expect-${pkgver}.tar.bz2)
license=('custom')
md5sums=('da02c862015da2d67f72f1693adab0db')

build() {
  # stuff to remove.
  BIN="expectk multixterm tknewsbiff tkpasswd xkibitz xpstat"
  MAN="multixterm tknewsbiff xkibitz"

  cd ${srcdir}/expect-${pkgver}
  sed 's#/usr/local/bin#/usr/bin#' -i expect.man
  sed '/^PACKAGE_/ s/5\.44\.1\.9/5.44.1.10/' -i configure

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --mandir=/usr/share/man --with-tcl=/usr/lib \
      --with-tclinclude=/usr/include --with-tk=/usr/lib \
      --with-tkinclude=/usr/include \

  # Keep $startdir/pkg out of library search paths.
  sed -e 's/-rpath,${LIB_RUNTIME_DIR}:/-rpath,/' -i Makefile
  make || return 1
  make DESTDIR=${pkgdir} install

  # license.
  install -D -m644 README ${pkgdir}/usr/share/licenses/${pkgname}/README

  # Remove X related stuff.
  for i in ${BIN}; do
      rm -f ${pkgdir}/usr/bin/${i}
  done
  for i in ${MAN}; do
      rm -f ${pkgdir}/usr/share/man/man1/${i}.1
  done
}
