# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: anex <assassin.anex[@]gmail.com

pkgname=signon-kwallet-extension
pkgver=22.04.0
pkgrel=1
pkgdesc='KWallet integration for signon framework'
arch=(x86_64)
url='https://www.kde.org'
license=(LGPL)
groups=(kde-applications kde-network)
depends=(signond kwallet)
makedepends=(extra-cmake-modules)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('33d19b1b758a21e568ce508cc168e568e34716f148cd83e214fb8a3f3194d2e8'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
options=(debug)

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
