# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-xinput
pkgver=1.5.99.1
pkgrel=1
pkgdesc="Small commandline tool to configure devices"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libx11' 'libxi>=1.5.99.2' 'xorg-xrandr' 'libxinerama')
makedepends=('xorg-util-macros' 'inputproto')
groups=('xorg-apps' 'xorg')
source=(http://xorg.freedesktop.org/archive/individual/app/xinput-${pkgver}.tar.bz2)
sha1sums=('f979d9a4005d71db7e58064dff6ca6738bd9a345')

build() {
  cd "${srcdir}/xinput-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/xinput-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
