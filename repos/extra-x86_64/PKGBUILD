# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Alexander F Rødseth <xyproto@archlinux.org>
# Contributor: Bartłomiej Piotrowski
# Contributor: AndyRTR <andyrtr@archlinux.org>
# Contributor: Ronald van Haren <ronald.archlinux.org>

pkgname=ristretto
pkgver=0.13.1
pkgrel=1
pkgdesc="A fast and lightweight picture viewer for Xfce"
arch=('x86_64')
url="https://docs.xfce.org/apps/ristretto/start"
license=('GPL')
groups=('xfce4-goodies')
depends=('libxfce4ui' 'xfconf' 'tumbler' 'libexif' 'file')
makedepends=('python')
source=("https://archive.xfce.org/src/apps/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2")
sha256sums=('d71affbf15245067124725b153c908a53208c4ca1ba2d4df1ec5a1308d53791e')

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-debug
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
