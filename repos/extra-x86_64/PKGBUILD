# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=libgusb
pkgver=0.3.2
pkgrel=2
pkgdesc='GObject wrapper for libusb1'
url=https://github.com/hughsie/libgusb
arch=(x86_64)
license=(LGPL2.1)
depends=(
  glib2
  libusb
)
makedepends=(
  git
  gobject-introspection
  gtk-doc
  meson
  vala
)
source=(git+https://github.com/hughsie/libgusb#tag=bda40aca1f11f841524ad6ed16c352329b8a8d41)
sha256sums=(SKIP)

prepare() {
  cd libgusb

  # https://github.com/hughsie/libgusb/issues/24
  git cherry-pick -n 3813921b7248a5f8a340f3395ff0ddf304bcf64e
}

pkgver() {
  cd libgusb

  git describe --tags
}

build() {
  arch-meson libgusb build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
