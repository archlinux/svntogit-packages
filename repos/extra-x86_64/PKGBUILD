# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Patrick Griffis <tingping@tingping.se>

pkgname=xdg-desktop-portal-gtk
pkgver=0.5
pkgrel=2
pkgdesc='A GTK+ backend for xdg-desktop-portal'
arch=('i686' 'x86_64')
url='http://flatpak.org'
license=('LGPL2.1')
depends=('xdg-desktop-portal' 'gtk3')
source=(https://github.com/flatpak/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz)
sha256sums=('790744654f361203c5578fd04d7576b1c5fa2cad9cdd2f595672dd07415c4591')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --libexec=/usr/lib
  make 
}

package() {
  cd $pkgname-$pkgver
  DESTDIR="$pkgdir" make install
}
