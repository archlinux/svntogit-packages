# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: DrZaius <lou[at]fakeoutdoorsman[dot]com>
# Contributor: pumbur

pkgname=opus
pkgver=1.1.2
pkgrel=1
pkgdesc="Codec designed for interactive speech and audio transmission over the Internet"
arch=(i686 x86_64)
url="http://www.opus-codec.org/"
license=(BSD)
depends=(glibc)
source=(http://downloads.us.xiph.org/releases/$pkgname/$pkgname-$pkgver.tar.gz)
md5sums=('1f08a661bc72930187893a07f3741a91')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --enable-custom-modes --disable-static \
    --enable-intrinsics --enable-rtcd
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
