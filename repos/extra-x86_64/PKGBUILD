# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>

pkgname=expect
pkgver=5.45.3
pkgrel=1
pkgdesc='A tool for automating interactive applications'
arch=(x86_64)
url='http://www.nist.gov/el/msid/expect.cfm'
license=(custom)
depends=(tcl)
source=(http://downloads.sourceforge.net/project/expect/Expect/$pkgver/expect${pkgver}.tar.gz)
sha256sums=('c520717b7195944a69ce1492ec82ca0ac3f3baf060804e6c5ee6d505ea512be9')

build() {
  cd expect${pkgver}
  ./configure --prefix=/usr --mandir=/usr/share/man 
  make
}

check() {
  cd expect${pkgver}
  make test
}

package() {
  cd expect${pkgver}
  make DESTDIR="$pkgdir" install
  install -Dm644 README "$pkgdir/usr/share/licenses/$pkgname/README"
}
