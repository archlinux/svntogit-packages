# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=gvfs
pkgname=('gvfs' 'gvfs-smb' 'gvfs-afc' 'gvfs-gphoto2' 'gvfs-obexftp')
pkgver=1.6.2
pkgrel=1
arch=('i686' 'x86_64')
license=('LGPL')
makedepends=('libsoup-gnome>=2.30.1' 'libcdio>=0.82' 'fuse>=2.8.3' 'bluez>=4.63' 'smbclient>=3.5.2' 'libgphoto2>=2.4.9' 'libarchive>=2.8.3' 'gnome-disk-utility>=2.30.1' 'pkgconfig' 'intltool' 'libimobiledevice>=1.0.1')
url="http://www.gnome.org"
options=(!libtool)
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgbase}/1.6/${pkgbase}-${pkgver}.tar.bz2)
sha256sums=('3a422a16b6758ec405b79d8021e115fddf6a88a0278a5d35dd6bcd022cea7248')


build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --libexecdir=/usr/lib/gvfs \
      --with-bash-completion-dir=/etc/bash_completion.d || return 1
  make || return 1
}

package_gvfs() {
  pkgdesc="Userspace virtual filesystem implemented as a pluggable module for gio"
  depends=('libsoup-gnome>=2.30.0' 'gnome-disk-utility>=2.30.1' 'libcdio>=0.82' 'fuse>=2.8.3' 'libarchive>=2.8.3')
  optdepends=('gvfs-afc: AFC (mobile devices) support'
              'gvfs-smb: SMB/CIFS (Windows client) support'
              'gvfs-gphoto2: gphoto2 (PTP camera/MTP media player) support'
              'gvfs-obexftp: ObexFTP (bluetooth) support')
  install=gvfs.install

  cd "${srcdir}/${pkgbase}-${pkgver}"
  sed -e 's/^am__append_3/#am__append_3/' \
      -e 's/^am__append_4/#am__append_4/' \
      -i monitor/Makefile || return 1
  make DESTDIR="${pkgdir}" install || return 1
  cd "${pkgdir}"
  rm -f usr/lib/gvfs/gvfsd-{smb,smb-browse,afc,gphoto2,obexftp}
  rm -f usr/share/gvfs/mounts/{smb,smb-browse,afc,gphoto2,obexftp}.mount
}

package_gvfs-smb() {
  pkgdesc="SMB/CIFS (Windows client) backend for gvfs"
  depends=("gvfs=${pkgver}" 'smbclient>=3.5.2' 'libgnome-keyring>=2.30.1')
  install=gvfs-module.install

  cd "${srcdir}/${pkgbase}-${pkgver}/daemon"
  install -m755 -d "${pkgdir}/usr/lib/gvfs"
  install -m755 -d "${pkgdir}/usr/share/gvfs/mounts"

  install -m755 .libs/gvfsd-smb{,-browse} "${pkgdir}/usr/lib/gvfs/" || return 1
  install -m644 smb{,-browse}.mount "${pkgdir}/usr/share/gvfs/mounts/" || return 1
}

package_gvfs-afc() {
  pkgdesc="AFC (mobile devices) backend for gvfs"
  depends=("gvfs=${pkgver}" 'libimobiledevice>=1.0.1')
  install=gvfs-module.install

  cd "${srcdir}/${pkgbase}-${pkgver}/daemon"
  install -m755 -d "${pkgdir}/usr/lib/gvfs"
  install -m755 -d "${pkgdir}/usr/share/gvfs/mounts"

  install -m755 .libs/gvfsd-afc "${pkgdir}/usr/lib/gvfs/" || return 1
  install -m644 afc.mount "${pkgdir}/usr/share/gvfs/mounts/" || return 1

  cd "${srcdir}/${pkgbase}-${pkgver}/monitor/afc"
  make DESTDIR="${pkgdir}" install || return 1
}

package_gvfs-gphoto2() {
  pkgdesc="gphoto2 (PTP camera/MTP media player) backend for gvfs"
  depends=("gvfs=${pkgver}" 'dbus-core>=1.2.24' 'libgphoto2>=2.4.9' 'udev>=151')
  install=gvfs-module.install

  cd "${srcdir}/${pkgbase}-${pkgver}/daemon"
  install -m755 -d "${pkgdir}/usr/lib/gvfs"
  install -m755 -d "${pkgdir}/usr/share/gvfs/mounts"

  install -m755 .libs/gvfsd-gphoto2 "${pkgdir}/usr/lib/gvfs/" || return 1
  install -m644 gphoto2.mount "${pkgdir}/usr/share/gvfs/mounts/" || return 1

  cd "${srcdir}/${pkgbase}-${pkgver}/monitor/gphoto2"
  make DESTDIR="${pkgdir}" install || return 1
}

package_gvfs-obexftp() {
  pkgdesc="ObexFTP (bluetooth) backend for gvfs"
  depends=("gvfs=${pkgver}" 'dbus-glib>=0.82' 'bluez>=4.63' 'obex-data-server')
  install=gvfs-module.install

  cd "${srcdir}/${pkgbase}-${pkgver}/daemon"
  install -m755 -d "${pkgdir}/usr/lib/gvfs"
  install -m755 -d "${pkgdir}/usr/share/gvfs/mounts"

  install -m755 .libs/gvfsd-obexftp "${pkgdir}/usr/lib/gvfs/" || return 1
  install -m644 obexftp.mount "${pkgdir}/usr/share/gvfs/mounts/" || return 1
}
