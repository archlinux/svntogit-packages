# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-session
pkgver=2.24.1
pkgrel=1
pkgdesc="The GNOME Session Handler"
arch=(i686 x86_64)
license=('GPL' 'LGPL')
depends=('libgnomeui>=2.24.0' 'policykit-gnome>=0.9.2' 'gnome-settings-daemon>=2.24.0' 'at-spi>=1.24.0' 'startup-notification>=0.9' 'dbus>=1.2.3-3')
makedepends=('perlxml' 'pkgconfig' 'intltool')
options=(!emptydirs)
install=gnome-session.install
url="http://www.gnome.org"
groups=('gnome')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.24/${pkgname}-${pkgver}.tar.bz2
	gnome-session.sh)
md5sums=('5bf6f5753eab034638aee80d993801d2'
         '06f84ea99c2c1c686756bf7aac33661a')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
	      --with-at-spi-registryd-directory=/usr/lib/at-spi || return 1
  make || return 1
  make DESTDIR="${pkgdir}" GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 install || return 1

  install -d -m755 "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas

  mv "${pkgdir}/usr/bin/gnome-session" "${pkgdir}/usr/lib/gnome-session/" || return 1
  install -m755 "${srcdir}/gnome-session.sh" "${pkgdir}/usr/bin/gnome-session" || return 1
}
