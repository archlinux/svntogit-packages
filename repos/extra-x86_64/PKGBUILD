# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jonathan Steel <jsteel@aur.archlinux.org>
# Contributor: Justin "juster" Davis <jrcd83@gmail.com>
# Contributor: Fran√ßois Charette <francois.archlinux.org>

pkgname=perl-xml-libxml
pkgver=2.0205
pkgrel=1
pkgdesc="Interface to the libxml library"
arch=('x86_64')
url="https://search.cpan.org/dist/XML-LibXML"
license=('GPL')
depends=('perl-alien-libxml2' 'perl-xml-sax' 'perl-xml-namespacesupport')
checkdepends=('perl-test-pod' 'perl-test-leaktrace' 'perl-cpan-changes' 'perl-uri')
provides=('perl-libxml' 'perl-xml-libxml-common')
replaces=('perl-libxml' 'perl-xml-libxml-common')
options=('!emptydirs')
install=perl-xml-libxml.install
source=(https://www.cpan.org/authors/id/S/SH/SHLOMIF/XML-LibXML-$pkgver.tar.gz)
sha512sums=('d99865abcf1d118867a75d945b9eb03da2e9ca61199f13d2913638bf9d0bd1dc2450c058c5a83a1cdc04e8a08e68e413318b4607d369f63f76066d989ef0221a')

build() {
  cd XML-LibXML-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd XML-LibXML-$pkgver
  make test
}

package() {
  cd XML-LibXML-$pkgver
  make pure_install doc_install DESTDIR="$pkgdir"/
}
