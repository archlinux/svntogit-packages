# $Id$
# Maintainer: Dan McGee <dan@archlinux.org>
# Maintainer: Allan McRae <allan@archlinux.org>

pkgname=valgrind
pkgver=3.8.1
pkgrel=4
pkgdesc="A tool to help find memory-management problems in programs"
arch=('i686' 'x86_64')
license=('GPL')
url="http://valgrind.org/"
depends=('glibc>=2.18' 'glibc<2.19' 'perl')
makedepends=('gdb')
options=('!emptydirs')
source=(http://valgrind.org/downloads/${pkgname}-${pkgver}.tar.bz2
        valgrind-3.8.1-glibc-2.18.patch
        valgrind-3.8.1-ptrace-build-fix.patch
        valgrind-3.8.1-xaddb.patch)
md5sums=('288758010b271119a0ffc0183f1d6e38'
         'f3f911e6f5b0ab9d176d40b47d6cae24'
         '9d429e62255621a0c190a8180d8ab1a4'
         'aad7751a3e4ee899093dcfa9f70a449b')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}

  patch -p1 -i $srcdir/valgrind-3.8.1-glibc-2.18.patch
  
  # based on svn commits 13471 & 13482
  patch -p0 -i $srcdir/valgrind-3.8.1-ptrace-build-fix.patch
  
  # https://bugs.kde.org/show_bug.cgi?id=307106
  patch -p0 -i $srcdir/valgrind-3.8.1-xaddb.patch
  
  ./autogen.sh
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  if [[ ${CARCH} = "x86_64" ]]; then
    ./configure --prefix=/usr --mandir=/usr/share/man --enable-only64bit
  else
    ./configure --prefix=/usr --mandir=/usr/share/man
  fi

  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
