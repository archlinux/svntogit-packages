# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libsoup
pkgver=2.60.3
pkgrel=1
pkgdesc="HTTP client/server library for GNOME"
url="https://wiki.gnome.org/Projects/libsoup"
arch=(x86_64)
license=(LGPL)
depends=(glib2 libxml2 glib-networking sqlite krb5)
makedepends=(intltool gobject-introspection python vala git gtk-doc)
checkdepends=(apache php-apache)
_commit=e2ec3f6958e18893bb48af0d781948824d52b56a  # tags/2.60.3^0
source=("git+https://git.gnome.org/browse/libsoup#commit=$_commit"
        0001-Configure-PHP-to-load-the-XMLRPC-extension-if-presen.patch
        0002-xmlrpc-server.php-Don-t-try-to-count-NULL-params.patch)
sha256sums=('SKIP'
            '5833b210a3116241a16449df28c1b74e2c078456b53edfc3c55d2f2ad35e5d2a'
            '4b68ddf7164e5469c367ee5b875a72cca62ed3d036c13d9deda455c141ea639d')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname

  # https://bugzilla.gnome.org/show_bug.cgi?id=782410
  patch -Np1 -i ../0001-Configure-PHP-to-load-the-XMLRPC-extension-if-presen.patch

  # tests fail with php 7.2
  patch -Np1 -i ../0002-xmlrpc-server.php-Don-t-try-to-count-NULL-params.patch

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --enable-gtk-doc
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
