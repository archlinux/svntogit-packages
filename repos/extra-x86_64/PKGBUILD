# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=syslog-ng
pkgver=3.10.1
pkgrel=2
pkgdesc="Next-generation syslogd with advanced networking and filtering capabilities"
arch=('i686' 'x86_64')
url="http://www.balabit.com/network-security/syslog-ng/"
license=('GPL2' 'LGPL2.1')
depends=('awk' 'eventlog' 'systemd' 'glib2' 'libdbi')
makedepends=('python2' 'libxslt' 'json-c' 'libmongoc')
optdepends=('logrotate: for rotating log files'
	    'json-c: for json-plugin'
            'curl: for the HTTP module'
            'libmongoc: for the MongoDB plugin')
backup=('etc/syslog-ng/scl.conf'
        'etc/syslog-ng/syslog-ng.conf'
        'etc/logrotate.d/syslog-ng')
source=(https://github.com/balabit/syslog-ng/releases/download/syslog-ng-$pkgver/$pkgname-$pkgver.tar.gz
        syslog-ng.conf syslog-ng.logrotate)
sha1sums=('2325da17d9b6837aae8484ef3b8d5a25ee992ee9'
          '09b4ae56d083fdae3f8310f0a374dfe7f339a5bf'
          '949128fe3d7f77a7aab99048061f885bc758000c')

prepare() {
  cd $pkgname-$pkgver
  sed -i -e 's,/bin/,/usr/bin/,' -e 's,/sbin/,/bin/,' contrib/systemd/syslog-ng.service
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc/syslog-ng --libexecdir=/usr/lib \
    --sbindir=/usr/bin --localstatedir=/var/lib/syslog-ng --datadir=/usr/share \
    --with-pidfile-dir=/run --disable-spoof-source --enable-ipv6 --enable-sql \
    --enable-systemd --with-systemdsystemunitdir=/usr/lib/systemd/system \
    --enable-manpages --with-jsonc=system --with-mongoc=system
  make
}

check() {
  cd $pkgname-$pkgver
  [[ ${CARCH} == 'i686' ]] || make check # one failure on i686
}

package() {
  make -C $pkgname-$pkgver DESTDIR="$pkgdir" install
  install -dm755 "$pkgdir/var/lib/syslog-ng" "$pkgdir/etc/syslog-ng/patterndb.d"
  install -Dm644 "$srcdir/syslog-ng.conf" "$pkgdir/etc/syslog-ng/syslog-ng.conf"
  install -Dm644 "$srcdir/syslog-ng.logrotate" "$pkgdir/etc/logrotate.d/syslog-ng"

# See http://lists.balabit.hu/pipermail/syslog-ng/2016-February/022667.html
  rm -r "$pkgdir"/usr/share/syslog-ng/include/scl/cim
}
