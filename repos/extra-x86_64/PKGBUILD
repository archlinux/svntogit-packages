# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=tcpdump
pkgver=4.9.0
pkgrel=1
pkgdesc="A tool for network monitoring and data acquisition"
arch=('i686' 'x86_64')
url="http://www.tcpdump.org"
license=('BSD')
depends=('libpcap' 'openssl')
#source=(http://www.tcpdump.org/release/${pkgname}-${pkgver}.tar.gz{,.sig})
source=(http://www.tcpdump.org/4.9.0-u82xFZBjZxWv/${pkgname}-${pkgver}.tar.gz{,.sig})
validpgpkeys=('1F166A5742ABB9E0249A8D30E089DEF1D9C15D0D') # The Tcpdump Group
sha256sums=('eae98121cbb1c9adbedd9a777bf2eae9fa1c1c676424a54740311c8abcee5a5e'
            'SKIP')

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr \
    --sbindir=/usr/bin

  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  rm -f "$pkgdir"/usr/bin/tcpdump.$pkgver
}
