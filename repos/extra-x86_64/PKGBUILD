# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=tk
pkgver=8.5.8
pkgrel=2

pkgdesc="A windowing toolkit for use with tcl"
arch=('i686' 'x86_64')
url="http://tcl.sourceforge.net/"
license=('custom')
depends=("tcl=${pkgver}" 'libxss' 'libxft')
source=(http://downloads.sourceforge.net/sourceforge/tcl/tk${pkgver}-src.tar.gz
        tkUnixWm.diff)
md5sums=('13bf90602e16fc530e05196431021dc6'
         'cd72a2c181052163fa76352476fcca86')


build() {
  cd ${srcdir}/tk${pkgver}/unix  
  # FS#17501, upstream bug 2902814
  patch -Np0 -i $srcdir/tkUnixWm.diff
  
  if [ "$CARCH" = "x86_64" ]; then
    ./configure --prefix=/usr --mandir=/usr/share/man --enable-threads --enable-64bit
  else
    ./configure --prefix=/usr --mandir=/usr/share/man --enable-threads --disable-64bit
  fi
  
  make
  make INSTALL_ROOT=${pkgdir} install install-private-headers
  ln -sf wish8.5 ${pkgdir}/usr/bin/wish
  
  # install private headers (FS#14388)
  cd ${srcdir}/tk${pkgver}
  for dir in compat generic generic/ttk unix; do
    install -dm755 ${pkgdir}/usr/include/tk-private/$dir
    install -m644 -t ${pkgdir}/usr/include/tk-private/$dir $dir/*.h
  done

  # install license
  install -Dm644 license.terms ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE

  # remove buildroot traces / fixes #3602
  sed -i \
  -e "s,^TK_BUILD_LIB_SPEC='-L.*/unix,TK_BUILD_LIB_SPEC='-L/usr/lib," \
  -e "s,^TK_SRC_DIR='.*',TK_SRC_DIR='/usr/include'," \
  -e "s,^TK_BUILD_STUB_LIB_SPEC='-L.*/unix,TK_BUILD_STUB_LIB_SPEC='-L/usr/lib," \
  -e "s,^TK_BUILD_STUB_LIB_PATH='.*/unix,TK_BUILD_STUB_LIB_PATH='/usr/lib," \
  ${pkgdir}/usr/lib/tkConfig.sh
}
