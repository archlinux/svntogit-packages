# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libpwquality
pkgver=1.3.0
pkgrel=1
pkgdesc="Library for password quality checking and generating random passwords"
arch=('i686' 'x86_64')
url="https://fedorahosted.org/libpwquality/"
license=('GPL')
depends=('cracklib' 'pam')
optdepends=('python2: Python bindings')
makedepends=('python2')
source=(https://fedorahosted.org/releases/l/i/libpwquality/$pkgname-$pkgver.tar.bz2)
md5sums=('2a3d4ba1d11b52b4f6a7f39622ebf736')

build() {
    cd "$pkgname-$pkgver"
    ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --with-python-rev=2.7 \
        --with-python-binary=python2
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
    make
}

package() {
    cd "$pkgname-$pkgver"
    make DESTDIR="$pkgdir" install
}
