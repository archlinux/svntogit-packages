# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=dbus-python
pkgver=1.2.16
pkgrel=4
pkgdesc="Python bindings for DBUS"
url="https://www.freedesktop.org/wiki/Software/DBusBindings"
arch=(x86_64)
license=('GPL' 'LGPL')
depends=('dbus' 'glib2')
makedepends=('python' 'autoconf-archive' 'python-sphinx_rtd_theme' 'python-sphinx')
source=(https://dbus.freedesktop.org/releases/${pkgname}/${pkgname}-${pkgver}.tar.gz{,.asc})
sha256sums=('11238f1d86c995d8aed2e22f04a1e3779f0d70e587caffeab4857f3c662ed5a4'
            'SKIP')
validpgpkeys=('DA98F25C0871C49A59EAFF2C4DE8FF2A63C7CC90')
replaces=('python-dbus' 'python-dbus-common')
conflicts=('python-dbus' 'python-dbus-common')
provides=("python-dbus=$pkgver" "python-dbus-common=$pkgver")

prepare() {
  cd $pkgname-$pkgver
  autoreconf -vfi
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="${pkgdir}" install
}
