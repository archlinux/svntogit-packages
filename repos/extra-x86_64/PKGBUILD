# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-xcalc
pkgver=1.1.0
pkgrel=1
pkgdesc="Scientific calculator for X"
arch=('x86_64')
url="https://cgit.freedesktop.org/xorg/app/xcalc/"
license=('custom')
depends=('libxaw')
optdepends=('xorg-fonts-100dpi: for some symbols or'
           'xorg-fonts-75dpi: for some symbols')
makedepends=('xorg-util-macros')
source=(https://xorg.freedesktop.org/archive/individual/app/xcalc-${pkgver}.tar.bz2{,.sig})
sha512sums=('877aab7bb08f409a21c26b841f4d91eb6993914ed0d26512e47d6b3d2b3e0b231158901415a106cfb45de43f03f253bf2a8733f986538d4ee810afe0e54f98d5'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # Alan Coopersmith <alan.coopersmith@oracle.com>

build() {
  cd xcalc-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd xcalc-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
