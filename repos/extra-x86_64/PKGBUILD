# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Anton Leontiev <bunder@t-25.ru>

pkgname=graphicsmagick
pkgver=1.3.7
pkgrel=3
pkgdesc="Image processing system"
arch=('i686' 'x86_64')
url="http://www.graphicsmagick.org/"
license=('MIT')
makedepends=('perl')
depends=('bzip2' 'freetype2' 'ghostscript' 'jasper' 'lcms' 'libsm' 
	'libtiff' 'libwmf' 'libxml2' 'libtool')
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-history/1.3/GraphicsMagick-${pkgver}.tar.gz)
md5sums=('ac019a9ece8c45f5213bf31981588c63')

build() {
  cd "${srcdir}/GraphicsMagick-$pkgver"

  ./configure --prefix=/usr --with-perl --enable-shared --with-gs-font-dir=/usr/share/fonts/Type1 || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1

  # Install MIT license
  install -Dm644 "Copyright.txt" "${pkgdir}/usr/share/licenses/$pkgname/Copyright.txt" || return 1

  # Install perl bindings
  # The patching was introduced in order to build perl module without installing package itself and
  # not to introduce unnecessary path into LD_RUN_PATH
  cd PerlMagick || return 1
  sed -i -e "s:'LDDLFLAGS'  => \"\(.*\)\":'LDDLFLAGS'  => \"-L${pkgdir}/usr/lib \1\":" Makefile.PL
  perl Makefile.PL INSTALLDIRS=vendor PREFIX=/usr DESTDIR="${pkgdir}" || return 1
  sed -i -e "s/LDLOADLIBS =/LDLOADLIBS = -lGraphicsMagick/" Makefile
  make || return 1
  make install || return 1

  # Remove perllocal.pod and .packlist
  rm -rf "${pkgdir}/usr/lib/perl5/core_perl"
  rm "${pkgdir}/usr/lib/perl5/vendor_perl/auto/Graphics/Magick/.packlist"
}