# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgbase=snowball
pkgname=(snowball libstemmer)
pkgver=0+713
pkgrel=1
pkgdesc="String processing language for creating stemming algorithms"
arch=(x86_64)
url="https://snowballstem.org/"
license=(BSD)
depends=(glibc)
makedepends=(git)
_commit=8c73aa900e168a96e046ec9ed6f13739c3dfacb0  # master
source=("git+https://github.com/snowballstem/snowball#commit=$_commit"
        dynamiclib.patch)
sha256sums=('SKIP'
            'ea34c201bf619d14b0eb1207589641ca1f1cb8131efb4b422b3dd3501c49231e')

pkgver() {
  cd $pkgbase
  echo "0+$(git rev-list --count HEAD)"
}

prepare() {
  cd $pkgbase
  patch -Np1 -i ../dynamiclib.patch
}

build() {
  cd $pkgbase
  make
}

check() {
  cd $pkgbase
  # Disabled - needs external test data
  #make check
}

package_snowball() {
  cd $pkgbase
  install -d "$pkgdir/usr/bin"
  install -t "$pkgdir/usr/bin" snowball stemwords
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/snowball/LICENSE"
}

package_libstemmer() {
  pkgdesc="Stemming library supporting several languages"
  cd $pkgbase
  install -Dm644 {.,"$pkgdir"/usr}/include/libstemmer.h
  install -D {.,"$pkgdir"/usr/lib}/libstemmer.so.0.0.0
  ln -s libstemmer.so.0.0.0 "$pkgdir/usr/lib/libstemmer.so.0"
  ln -s libstemmer.so.0 "$pkgdir/usr/lib/libstemmer.so"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/libstemmer/LICENSE"
}
