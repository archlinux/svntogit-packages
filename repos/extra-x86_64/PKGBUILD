# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gst-plugins-good
pkgver=1.8.2
pkgrel=1
pkgdesc="GStreamer Multimedia Framework Good Plugins"
arch=(i686 x86_64)
license=(LGPL)
url="http://gstreamer.freedesktop.org/"
depends=(libpulse jack libsoup gst-plugins-base-libs wavpack aalib taglib libdv libshout libvpx
         gdk-pixbuf2 libcaca libavc1394 libiec61883 libxdamage v4l-utils cairo libgudev mpg123)
makedepends=(gstreamer speex flac libraw1394 python gtk-doc)
options=(!emptydirs)
source=(${url}/src/$pkgname/$pkgname-$pkgver.tar.xz)
sha256sums=('8d7549118a3b7a009ece6bb38a05b66709c551d32d2adfd89eded4d1d7a23944')

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --enable-experimental --enable-gtk-doc \
    --with-package-name="GStreamer Good Plugins (Arch Linux)" \
    --with-package-origin="http://www.archlinux.org/"

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

# Disabled for now: souphttpsrc times out, and takes a damn long time to do so
#check() {
#  cd $pkgname-$pkgver
#  make check
#}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="${pkgdir}" install
}
