# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Maxime de Roucy <maxime.deroucy@gmail.com>
# Contributor: Darwin Bautista <djclue917@gmail.com>

pkgname=gsm
pkgver=1.0.21
pkgrel=1
pkgdesc="Shared libraries for GSM 06.10 lossy speech compression"
arch=('x86_64')
url="http://www.quut.com/gsm/"
license=('custom')
depends=('glibc')
source=("http://www.quut.com/$pkgname/$pkgname-$pkgver.tar.gz" gsm-shared.patch)
sha512sums=('2ad92a8acea7cb47c3237f49805f21cdc180009f6a31ede3e7befbb361968f5235e5b0664503b46f74dd4ef85c602e60ac9c37029075b026ceeed33a72848e0f'
            'a0c7116d3800806eb5faa1f381a739b29b2e6f73cdeff8377c44da03cb0cba99004eb44681815a8dd5507ca50124fa8b5e24aed5b68c779706917915a56478bf')

prepare() {
  cd $pkgname-${pkgver%.*}-pl${pkgver##*.}

  patch -p0 -i ../gsm-shared.patch
}

build() {
  cd $pkgname-${pkgver%.*}-pl${pkgver##*.}

  make CCFLAGS="-c ${CFLAGS} -fPIC"
}

package() {
  cd $pkgname-${pkgver%.*}-pl${pkgver##*.}

  # Prepare directories
  install -m755 -d "$pkgdir"/usr/{bin,lib,include/gsm,share/{licenses/$pkgname,man/man{1,3}}}

  make -j1 INSTALL_ROOT="$pkgdir"/usr \
    GSM_INSTALL_INC="$pkgdir"/usr/include/gsm \
    GSM_INSTALL_MAN="$pkgdir"/usr/share/man/man3 \
    TOAST_INSTALL_MAN="$pkgdir"/usr/share/man/man1 \
    install

  # Install license
  install -m644 COPYRIGHT "$pkgdir"/usr/share/licenses/$pkgname/license.txt
}
