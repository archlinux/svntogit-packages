# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kwalletmanager
pkgver=20.08.2
pkgrel=1
pkgdesc='Wallet management tool'
arch=(x86_64)
url='https://www.kde.org/applications/system/kwalletmanager/'
license=(LGPL)
depends=(kio hicolor-icon-theme)
makedepends=(extra-cmake-modules kcmutils kdoctools)
groups=(kde-applications kde-utilities)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('7950e250c5351a9b8e3b36165fa2003baa044bb2d3553a32360000a322bddad8'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}

