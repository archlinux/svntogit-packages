# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=vino
pkgver=3.20.2
pkgrel=1
pkgdesc="A VNC server for the GNOME desktop"
url="https://wiki.gnome.org/Projects/Vino"
arch=(i686 x86_64)
license=(GPL)
depends=(libnotify libxtst libsm telepathy-glib gtk3 libsecret avahi gnutls)
makedepends=(intltool networkmanager)
groups=(gnome)
options=(!emptydirs)
source=(https://download.gnome.org/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('660488adc1bf577958e783d13f61dbd99c1d9c4e81d2ca063437ea81d39e4413')

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/vino \
      --localstatedir=/var \
      --disable-http-server --with-secret

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
