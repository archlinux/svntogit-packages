# $Id$
# Maintainer:
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve> 

pkgbase=pycurl
pkgname=('python-pycurl' 'python2-pycurl')
pkgver=7.19.3
pkgrel=1
arch=('i686' 'x86_64')
url="http://pycurl.sourceforge.net/"
license=('LGPL' 'MIT')
makedepends=('python' 'python2' 'curl')
source=("http://pycurl.sourceforge.net/download/${pkgbase}-${pkgver}.tar.gz")
sha1sums=('5d8b7a7f869362421061d72f6d234bf805ebebb5')

prepare() {
  cp -r ${pkgbase}-${pkgver} python2-${pkgbase}-${pkgver}

  cd "${srcdir}/${pkgbase}-${pkgver}"
  find . -name '*.py' -exec sed -i -e "s|#! /usr/bin/env python|#!/usr/bin/env python3|" {} +
  sed -i 's/"share", "doc", PACKAGE/"share", "doc", "python-pycurl"/' setup.py
  cd "${srcdir}/python2-${pkgbase}-${pkgver}"
  find . -name '*.py' -exec sed -i -e "s|#! /usr/bin/env python|#!/usr/bin/env python2|" {} +
  sed -i 's/"share", "doc", PACKAGE/"share", "doc", "python2-pycurl"/' setup.py
}

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  python3 setup.py build --with-ssl
  cd "${srcdir}/python2-${pkgbase}-${pkgver}"
  python2 setup.py build --with-ssl
}

package_python-pycurl() {
  pkgdesc="A Python 3.x interface to libcurl"
  depends=('python' 'curl')

  cd ${pkgbase}-${pkgver}
  python setup.py install --root="${pkgdir}"
  install -D -m644 COPYING-MIT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-pycurl() {
  pkgdesc="A Python 2.x interface to libcurl"
  depends=('python2' 'curl')

  cd python2-${pkgbase}-${pkgver}
  python2 setup.py install --root="${pkgdir}"
  install -D -m644 COPYING-MIT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
