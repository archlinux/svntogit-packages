# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: AndyRTR <andyrtr@archlinux.org>
# Contributor: Abhishek Dasgupta <abhidg@gmail.com>

pkgname=xfce4-time-out-plugin
pkgver=1.1.3
pkgrel=1
pkgdesc="Take a break from your computer with this plugin for Xfce4"
arch=('x86_64')
url="https://docs.xfce.org/panel-plugins/xfce4-time-out-plugin"
groups=('xfce4-goodies')
license=('GPL')
depends=('xfce4-panel')
makedepends=('intltool')
source=(https://archive.xfce.org/src/panel-plugins/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=('5a1ca36361e95ec718bbd887ea5be6a270ab458d1c2d672186721522a7228ee8')

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-debug
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
