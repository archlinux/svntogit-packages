# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=at-spi2-atk
pkgver=2.20.1
pkgrel=2
pkgdesc="A GTK+ module that bridges ATK to D-Bus at-spi"
arch=('i686' 'x86_64')
url="https://wiki.gnome.org/Accessibility"
license=('GPL2')
depends=('at-spi2-core' 'libx11' 'atk')
makedepends=('intltool')
provides=('at-spi')
replaces=('at-spi')
conflicts=('at-spi')
source=(http://download.gnome.org/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('2358a794e918e8f47ce0c7370eee8fc8a6207ff1afe976ec9ff547a03277bf8e')

build() {
  cd $pkgname-$pkgver

  sed -i -e '/AC_PATH_XTRA/d' configure.ac
  autoreconf --force --install

  ./configure --prefix=/usr --sysconfdir=/etc \
    --disable-schemas-compile
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
