#$Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Kritoke <typeolinux@yahoo.com>

pkgbase=gconfmm
pkgname=('gconfmm' 'gconfmm-docs')
pkgver=2.28.2
pkgrel=1
arch=(i686 x86_64)
license=('LGPL')
makedepends=('pkgconfig' 'gtkmm>=2.20.3' 'glibmm-docs' 'gconf>=2.28.1' 'glibmm>=2.24.2')
options=('!libtool')
url="http://gtkmm.sourceforge.net/"
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.28/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('3e9575208ed1f43f0d0f42e62169a4e14d367a69829b3a531b943655f9db2ab9')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var || return 1
  make || return 1
}

package_gconfmm() {
  pkgdesc="C++ bindings for GConf"
  depends=('gconf>=2.28.1' 'glibmm>=2.24.2')

  cd "${srcdir}/${pkgbase}-${pkgver}"
  sed -i -e 's/^doc_subdirs/#doc_subdirs/' Makefile || return 1
  make DESTDIR="${pkgdir}" install || return 1
}

package_gconfmm-docs() {
  pkgdesc="Developer documentation for gconfmm"

  cd "${srcdir}/${pkgbase}-${pkgver}/docs"
  make DESTDIR="${pkgdir}" install || return 1
}
