# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Mirco Tischler <mt-ml at gmx dot de>

pkgname=ostree
_commit=73744a3a4dfc7aa9ab183a9700273eee2f49750a  # tags/v2022.6
pkgver=2022.6
pkgrel=1
pkgdesc="Operating system and container binary deployment and upgrades"
url="https://ostreedev.github.io/ostree/"
arch=(x86_64)
license=(GPL)
depends=(fuse3 glibc libgpg-error libsodium util-linux openssl xz zlib)
makedepends=(avahi e2fsprogs git glib2 gobject-introspection gpgme gtk-doc
libarchive libsoup libxslt mkinitcpio python systemd)
checkdepends=(cpio elfutils gjs parallel python-yaml syslinux)
options=(debug)
source=(
  git+https://github.com/ostreedev/ostree#tag=$_commit
  git+https://github.com/mendsley/bsdiff
  git+https://gitlab.gnome.org/GNOME/libglnx.git
  $pkgname-2022.6-use_fuse3.patch
)
b2sums=('SKIP'
        'SKIP'
        'SKIP'
        '78f728dc7b6ef820a746c049076830049c5f21f56d8553ee739033591dccc55f624fe76f086e90f437107f3eb9759d574db64d511c217ad5397f9c199a4b7b58')
# upstream ticket about chain of trust:
# https://github.com/ostreedev/ostree/issues/2349
validpgpkeys=('1CEC7A9DF7DA85ABEF843DC0A866D7CCAE087291') # Colin Walters <walters@redhat.com>

prepare() {
  # use fusemount3 (fuse3)
  patch -Np1 -d $pkgname -i ../$pkgname-2022.6-use_fuse3.patch

  cd $pkgname
  git submodule init
  git submodule set-url bsdiff "$srcdir/bsdiff"
  git submodule set-url libglnx "$srcdir/libglnx"
  git submodule update

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib \
    --with-builtin-grub2-mkconfig \
    --with-ed25519-libsodium \
    --with-dracut \
    --with-mkinitcpio \
    --with-openssl \
    --enable-experimental-api \
    --disable-static \
    --enable-gtk-doc

  # circumvent overlinking
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

check() {
  make check -k -C $pkgname || echo "Known to fail: https://github.com/ostreedev/ostree/issues/2313"
}

package() {
  depends+=(libarchive.so libavahi-client.so libavahi-common.so
            libavahi-glib.so libgio-2.0.so libglib-2.0.so libgobject-2.0.so
            libgpgme.so libsoup-2.4.so libsystemd.so)
  provides=(libostree-1.so)

  make DESTDIR="$pkgdir" install -C $pkgname
}
