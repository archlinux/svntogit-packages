# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: Ike Devolder <ike.devolder@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=taglib
pkgver=1.11.1
pkgrel=3
pkgdesc="A Library for reading and editing the meta-data of several popular audio formats"
arch=(x86_64)
url="https://taglib.github.io/"
license=(LGPL MPL)
depends=(zlib)
makedepends=(cmake boost)
source=("https://taglib.github.io/releases/$pkgname-$pkgver.tar.gz"
        taglib-ogg-corruption.patch::"https://github.com/taglib/taglib/commit/9336c82d.patch")
sha256sums=('b6d1a5a610aae6ff39d93de5efd0fdc787aa9e9dc1e7026fa4c961b26563526b'
            'bf15f9d99263ca9e3cd96ee16dbfe38ca02932a0e8e2d77696d7188679bda272')

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../taglib-ogg-corruption.patch # Fix occasional OGG file corruption
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_SHARED_LIBS=ON \
    -DWITH_MP4=ON \
    -DWITH_ASF=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
