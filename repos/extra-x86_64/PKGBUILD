# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Link Dupont <link@subpop.net>
#
pkgname=dbus
pkgver=1.6.8
pkgrel=1
pkgdesc="Freedesktop.org message bus system"
url="http://www.freedesktop.org/Software/dbus"
arch=(i686 x86_64)
license=('GPL' 'custom')
depends=("dbus-core>=$pkgver" 'libx11')
source=(http://dbus.freedesktop.org/releases/$pkgname/$pkgname-$pkgver.tar.gz #{,.asc}
        30-dbus)
md5sums=('3bf059c7dd5eda5f539a1b7cfe7a14a2'
         '9fafe8b28460aeaa6054309ef4c5ed92')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/usr/lib/dbus-1.0 --with-dbus-user=81 \
      --with-system-pid-file=/var/run/dbus.pid \
      --disable-verbose-mode --disable-static \
      --enable-inotify --disable-dnotify \
      --disable-tests --disable-asserts
  make -C tools dbus-launch
}

package() {
  cd $pkgname-$pkgver
  install -m755 -d "$pkgdir/usr/bin"
  install -m755 -d "$pkgdir/usr/share/man/man1"
  install -m755 tools/dbus-launch "$pkgdir/usr/bin/"
  install -m644 doc/dbus-launch.1 "$pkgdir/usr/share/man/man1/"

  install -m755 -d "$pkgdir/etc/X11/xinit/xinitrc.d"
  install -m755 ../30-dbus "$pkgdir/etc/X11/xinit/xinitrc.d/"

  install -d -m755 "$pkgdir/usr/share/licenses/dbus"
  install -m644 COPYING "$pkgdir/usr/share/licenses/dbus/"
}
