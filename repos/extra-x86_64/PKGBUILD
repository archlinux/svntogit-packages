# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kleopatra
pkgver=22.08.0
pkgrel=1
pkgdesc='Certificate Manager and Unified Crypto GUI'
arch=(x86_64)
url='https://apps.kde.org/kleopatra/'
license=(GPL LGPL FDL)
depends=(libkleo kidentitymanagement kmailtransport)
makedepends=(extra-cmake-modules kdoctools)
groups=(kde-applications kde-pim)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        https://invent.kde.org/pim/kleopatra/-/commit/87d8b00d4b2286489d5fadc9cfa07f1d721cdfe3.patch)
sha256sums=('2f86f65754d2466f5b91522c8017f7932265d34f8ee10c59854441862d9588db'
            'SKIP'
            '3c651fb56587a4cc355befc1942c680bb6c7c592b35c2b432f6ae68ac76b7ad3')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
options=(debug)

prepare() {
  cd $pkgname-$pkgver
  # Release version doesn't build with qgpgme 1.18.0
  patch -p1 -i ../87d8b00d4b2286489d5fadc9cfa07f1d721cdfe3.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
