# $Id$
# Maintainer: SÃ©bastien Luttringer

pkgname=ipset
pkgver=6.27
pkgrel=1
pkgdesc='Administration tool for IP sets'
arch=('i686' 'x86_64')
url='http://ipset.netfilter.org'
license=('GPL2')
depends=('glibc' 'libmnl')
backup=("etc/$pkgname.conf")
source=("http://ipset.netfilter.org/$pkgname-$pkgver.tar.bz2"
        "$pkgname.service")
md5sums=('c39f8958908b1396e2c69f082b75d924'
         'e20fe62881993078591f1bb8b2fa22bb')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sbindir=/usr/bin --with-kmod=no
  make
}

package() {
  pushd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  # install doc
  install -dm755 "$pkgdir/usr/share/doc/$pkgname"
  install -m644 README UPGRADE ChangeLog "$pkgdir/usr/share/doc/$pkgname"
  # install pkgconfig file
  install -Dm644 lib/libipset.pc "$pkgdir/usr/lib/pkgconfig/libipset.pc"
  popd
  # systemd
  install -Dm 644 $pkgname.service \
    "$pkgdir/usr/lib/systemd/system/$pkgname.service"
  # default config file
  install -Dm 644 /dev/null "$pkgdir/etc/$pkgname.conf"
}

# vim:set ts=2 sw=2 et:
