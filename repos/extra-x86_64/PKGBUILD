# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libxft
pkgver=2.3.1
pkgrel=2
pkgdesc="FreeType-based font drawing library for X"
arch=('i686' 'x86_64')
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('fontconfig' 'libxrender')
makedepends=('pkgconfig')
source=("${url}/releases/individual/lib/libXft-${pkgver}.tar.bz2"
        freetype_header.patch)
sha256sums=('7fce32b92dcb7b2869bed567af2abc7bbad0d5d6fcf471b8a3e137964a31bbbd'
            '991c91998e61e83061c6c08218a2e6ac5643f16183750f73ee74f29fdd440be9')

build() {
  cd "${srcdir}/libXft-${pkgver}"
  # fix build for freetype header moved - http://cgit.freedesktop.org/xorg/lib/libXft/patch/?id=4acfdaf95adb0a05c2a25550bdde036c865902f4
  patch -Np1 -i ${srcdir}/freetype_header.patch
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static
  make
}

package() {
  cd "${srcdir}/libXft-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"

}
