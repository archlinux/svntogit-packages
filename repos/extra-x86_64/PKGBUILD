# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: G_Syme <demichan(at)mail(dot)upb(dot)de>

pkgname=libass
pkgver=0.12.3
pkgrel=1
pkgdesc="A portable library for SSA/ASS subtitles rendering"
arch=('i686' 'x86_64')
url="https://github.com/libass/libass/"
license=('BSD')
depends=('fribidi' 'fontconfig' 'enca')
makedepends=('pkg-config' 'yasm')
source=("https://github.com/${pkgname}/${pkgname}/archive/${pkgver}.tar.gz")
md5sums=('1b53e739ab389335ce46fd626777ec61')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./autogen.sh
  ./configure --prefix=/usr \
              --enable-enca \
              --enable-harfbuzz \
              --enable-fontconfig
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
