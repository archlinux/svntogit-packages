# $Id: PKGBUILD 261838 2016-03-16 07:36:15Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kactivities-stats
pkgver=5.44.0
pkgrel=1
arch=('x86_64')
pkgdesc="A library for accessing the usage data collected by the activities system"
url='https://community.kde.org/Frameworks'
license=('LGPL')
depends=('kactivities')
makedepends=('extra-cmake-modules' 'boost' 'doxygen' 'qt5-tools')
groups=('kf5')
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('cfa5e1e462c4d3416728248f7e1594d06d6879194c6e26d0d2aa758cb8fd3bdc'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
