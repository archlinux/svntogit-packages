# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=kmail
pkgver=19.12.2
pkgrel=1
pkgdesc="KDE mail client"
arch=(x86_64)
url="https://kontact.kde.org"
license=(GPL2)
depends=(kdepim-runtime kmail-account-wizard akonadi-import-wizard mbox-importer
         pim-data-exporter pim-sieve-editor kontactinterface ktnef)
makedepends=(extra-cmake-modules kdoctools boost)
optdepends=('kleopatra: mail encryption support' 'kdepim-addons: additional plugins')
groups=(kde-applications kdepim)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('1168620b9571e29218332781cddd29e642cb05d3e186f6b294589e008a417025'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
