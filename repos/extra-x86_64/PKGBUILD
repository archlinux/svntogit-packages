# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=fluidsynth
pkgver=2.0.7
pkgrel=1
pkgdesc="A real-time software synthesizer based on the SoundFont 2 specifications"
arch=('x86_64')
url="http://www.fluidsynth.org/"
license=('LGPL2.1')
groups=('pro-audio')
depends=('glib2' 'libpulse' 'portaudio')
makedepends=('cmake' 'ladspa' 'doxygen')
optdepends=('pulseaudio: PulseAudio sound support')
provides=('libfluidsynth.so' 'soundfont-synthesizer')
backup=("etc/conf.d/$pkgname")
source=("$pkgname-$pkgver.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/v$pkgver.tar.gz")
sha512sums=('c939c77277767541b90ae8bc24687cd9cd11b375264e6b5017658f5fd9c5637a3ce09bc4ad837a349e4a63883fe5ecbe80e65dd91d2cbf71072bcbcfc068614e')

prepare() {
  cd "${pkgname}-${pkgver}"
  mkdir -v build
}

build() {
  cd "${pkgname}-${pkgver}"
  (
    cd build
    cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
             -DFLUID_DAEMON_ENV_FILE=/etc/conf.d/fluidsynth \
             -Denable-ladspa=ON \
             -Denable-portaudio=ON \
             -DLIB_SUFFIX=""
  )
  make -C build VERBOSE=1
}

check() {
  cd "${pkgname}-${pkgver}"
  make -C build -k check
}

package() {
  cd "${pkgname}-${pkgver}"
  make -C build DESTDIR="$pkgdir" install
  # systemd user unit
  install -vDm 644 "build/${pkgname}.service" \
    -t "$pkgdir/usr/lib/systemd/user/"
  # system-wide configuration file
  install -vDm 644 "build/${pkgname}.conf" "${pkgdir}/etc/conf.d/${pkgname}"
  # docs
  install -vDm 644 {AUTHORS,ChangeLog,NEWS,README.md,THANKS,TODO} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}

