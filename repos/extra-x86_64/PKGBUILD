# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Fabian Bornschein <fabiscafe-cat-mailbox-dog-org>
# Contributor: Igor Dyatlov <dyatlov.igor@gmail.com>

pkgname=gnome-console
pkgver=44.0
pkgrel=1
pkgdesc="A simple user-friendly terminal emulator for the GNOME desktop"
url="https://gitlab.gnome.org/GNOME/console"
arch=(x86_64)
license=(GPL3)
depends=(
  libadwaita
  libgtop
  vte4
)
makedepends=(
  appstream-glib
  git
  meson
)
groups=(gnome)
_commit=d3fd2f461a99e1f516d40077595021395f171310  # tags/44.0^0
source=("$pkgname::git+https://gitlab.gnome.org/GNOME/console.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  local meson_options=(
    -D tests=true
  )

  arch-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
