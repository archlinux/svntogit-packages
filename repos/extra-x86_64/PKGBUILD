# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=audiotube
pkgver=23.04.0
pkgrel=2
pkgdesc='Client for YouTube Music'
url='https://apps.kde.org/audiotube/'
arch=(x86_64)
license=(GPL LGPL)
groups=(kde-applications kde-multimedia)
depends=(kirigami-addons qt5-svg ki18n kcrash kwindowsystem yt-dlp python-ytmusicapi)
makedepends=(extra-cmake-modules pybind11 python)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        https://invent.kde.org/multimedia/audiotube/-/commit/090129d9.patch
        https://invent.kde.org/multimedia/audiotube/-/commit/bb4a24e2.patch
        https://invent.kde.org/multimedia/audiotube/-/commit/58460cf3.patch)
sha256sums=('b122b887e0c87a3bb9779e322f35c4beb548a4457d4dc8a4ad64fc26955e9d51'
            'SKIP'
            '8061d24d0b94554f320efeeac2019a7ff5d44b957b6837e5a912bdbfda604936'
            '78bd847cc9998c83cd8d011670684d76c64e4bda3e088b33651bb6eac7b30e6f'
            'f877eb4d9c061e367e3dda8ef3efa73a877e01407e6c8386cbdb67cc1d7d148d')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

prepare() {
# Backport bug fixes
  patch -d $pkgname-$pkgver -p1 < 090129d9.patch
  patch -d $pkgname-$pkgver -p1 < bb4a24e2.patch
  patch -d $pkgname-$pkgver -p1 < 58460cf3.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
