# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kirigami-gallery
pkgver=22.12.3
pkgrel=1
pkgdesc='Gallery application built using Kirigami'
url='https://apps.kde.org/kirigami2.gallery/'
arch=(x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kdesdk)
depends=(kirigami2 kitemmodels qt5-svg)
makedepends=(extra-cmake-modules qt5-tools)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('9a960d0e94cc13102ecdaacb46c1721ffeeb598bc7ca09b1e313db22b4b37afd'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
