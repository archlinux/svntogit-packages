# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgbase=malcontent
pkgname=(malcontent libmalcontent)
pkgver=0.10.5
pkgrel=1
pkgdesc="Parental control support for applications"
url="https://gitlab.freedesktop.org/pwithnall/malcontent"
arch=(x86_64)
license=(LGPL2.1)
depends=(accountsservice dbus flatpak glib2 gtk3 polkit appstream-glib)
makedepends=(git meson gobject-introspection gtk-doc libglib-testing yelp-tools)
options=(debug)
_commit=9d1e8ac3c5afc523413980456b50474c3e5956a5  # tags/0.10.5^0
source=("git+https://gitlab.freedesktop.org/pwithnall/malcontent.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

package_malcontent() {
  depends+=("libmalcontent=$pkgver-$pkgrel")
  groups=(gnome)

  meson install -C build --destdir "$pkgdir"

  cd "$pkgdir"

  _pick libs usr/include
  _pick libs usr/lib/{girepository-1.0,libmalcontent*,pkgconfig}
  _pick libs usr/share/gir-1.0

  install -d -o root -g 102 -m 750 usr/share/polkit-1/rules.d
}

package_libmalcontent() {
  pkgdesc="Parental control library"
  depends=(libg{lib,object,io}-2.0.so gcc-libs)
  provides=(libmalcontent{,-ui}-${pkgver%%.*}.so)
  mv libs/* "$pkgdir"
}

# vim:set sw=2 et:
