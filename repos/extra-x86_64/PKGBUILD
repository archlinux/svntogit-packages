# Maintainer:Antonio Rojas <arojas@archlinux.org>

pkgname=plasma-disks
pkgver=5.27.0
pkgrel=1
pkgdesc='Monitors S.M.A.R.T. capable devices for imminent failure'
arch=(x86_64)
url='https://kde.org/plasma-desktop/'
license=(LGPL)
depends=(smartmontools kinfocenter)
makedepends=(extra-cmake-modules)
groups=(plasma)
source=(https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('8bc2c4ed3e36c4419ad1af877ab8ad07d829cd8758bf7be48a009e1b4082f0d4'
            'SKIP')
validpgpkeys=('E0A3EB202F8E57528E13E72FD7574483BB57B18D'  # Jonathan Esk-Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
