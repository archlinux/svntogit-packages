# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

pkgname=xorg-xinit
pkgver=1.4.0
pkgrel=1
pkgdesc="X.Org initialisation program "
arch=('x86_64')
license=('custom')
url="https://xorg.freedesktop.org/"
depends=('libx11' 'xorg-xauth' 'xorg-xrdb' 'xorg-xmodmap' 'inetutils')
makedepends=('xorg-util-macros')
optdepends=('xorg-twm' 'xterm')
backup=('etc/X11/xinit/xserverrc'
        'etc/X11/xinit/xinitrc')
source=(${url}/releases/individual/app/xinit-${pkgver}.tar.bz2{,.sig}
        fs25361.patch
        fs46369.patch
        xserverrc)
options=(!emptydirs)
sha512sums=('53a29081130c1e195eb441ee77ccaa044b18b4cca3d2d5da3a6d67aa421dfd9718fa18b6be6232a41e40cf260c8190064c4d8d9ab771177bd5cd12e77a8fa79e'
            'SKIP'
            'cd52aa831154a8ed4f88c695b17fc692dc6ded30818b0e2bc28cb0174f4148bb1fb945c54e0ef2613e1dd459e36da69f03863b33b6ec16dc1d8466ff5e480a6a'
            '8a36bff3c472763a9a46b9a36b8b4a15f03e6fb0387b12efba27f15dc500faa2a3f92c0f7e217a0b00da7c78682c7af6357cc6b88b550ece9bc89c477412b7e0'
            'f86d96d76bcb340021e7904925f0029f8662e4dfc32489198b3a8695dca069da496539e2287249c763fe9c4d8d5d591fd18fe49a0bee822cbbd0eb712efbb89b')
validpgpkeys=('7C2608F8E5F9E78D7A3F26FF177544B49AF313FB') #  "Hans de Goede <jwrdegoede@fedoraproject.org>"
validpgpkeys+=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # "Alan Coopersmith <alan.coopersmith@oracle.com>"

prepare() {
  cd xinit-${pkgver}
  patch -Np1 -i ../fs25361.patch
  patch -Np1 -i ../fs46369.patch
  sed -i -e 's/XSLASHGLOB.sh/XSLASHGLOB/' xinitrc.cpp
}

build() {
  cd xinit-${pkgver}
  autoreconf -fi
  ./configure --prefix=/usr --with-xinitdir=/etc/X11/xinit
  make
}

package() {
  cd xinit-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -m644 "${srcdir}/xserverrc" "${pkgdir}/etc/X11/xinit/xserverrc"
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
