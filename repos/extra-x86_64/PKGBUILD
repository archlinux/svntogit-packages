# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=libplacebo
pkgver=1.21.0
pkgrel=1
pkgdesc='Reusable library for GPU-accelerated video/image rendering primitives'
url='https://github.com/haasn/libplacebo'
arch=('x86_64')
license=('LGPL2.1')
depends=('vulkan-icd-loader' 'glslang' 'lcms2' 'shaderc' 'libshaderc_shared.so')
makedepends=('meson' 'ninja' 'vulkan-headers')
provides=('libplacebo.so')
source=(https://code.videolan.org/videolan/libplacebo/-/archive/v${pkgver}/libplacebo-v${pkgver}.tar.gz)
sha512sums=('5de95b8cc48ba4ec9bc3afb8c8f2c4fae0d53ce6e69ffb53884c3a3ee2580998bae46b8934e462740b6c9c2e66e3c5d5f009f9b0f8ad2cb7b4fba829891cf333')

build() {
  cd ${pkgbase}-v${pkgver}
  CXXFLAGS+=" -I/usr/include/glslang"
  arch-meson build \
    -D tests=true \
    -D vulkan=enabled \
    -D glslang=enabled \
    -D shaderc=enabled \
    -D lcms=enabled
  ninja -C build
}

check() {
  cd ${pkgbase}-v${pkgver}
  ninja -C build test
}

package() {
  cd ${pkgbase}-v${pkgver}
  DESTDIR="${pkgdir}" ninja -C build install
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
