# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=telepathy-qt
pkgver=0.9.3
pkgrel=8
pkgdesc="A library for Qt-based Telepathy clients"
arch=('i686' 'x86_64')
url="http://telepathy.freedesktop.org/wiki/"
license=('LGPL')
depends=('qt4' 'telepathy-farstream' 'gstreamer0.10-base')
makedepends=('libxslt' 'python2' 'cmake' 'doxygen')
conflicts=('telepathy-qt4')
replaces=('telepathy-qt4')
provides=('telepathy-qt4')
source=("http://telepathy.freedesktop.org/releases/${pkgname}/${pkgname}-${pkgver}.tar.gz"{,.asc}
        'farstream-0.2.patch'
        'cmake.patch')
md5sums=('94ac93aedf5f6fff49837bc8368e5a37'
         'SKIP'
         '6b2421c59d20d29fece8d5e6445354c7'
         '50e9db71c0e23b59dba1f20a592e4b8d')

prepare() {
  mkdir build

  cd ${pkgname}-${pkgver}
  patch -p1 -i "${srcdir}"/farstream-0.2.patch
  patch -p1 -i "${srcdir}"/cmake.patch

}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DPYTHON_EXECUTABLE=/usr/bin/python2 \
    -DENABLE_TESTS=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
