# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kross
pkgver=5.2.0
pkgrel=1
pkgdesc='Kross'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kross'
license=('LGPL')
depends=('kparts')
makedepends=('extra-cmake-modules' 'kdoctools' 'qt5-tools')
groups=('kf5-aids')
source=("http://download.kde.org/stable/frameworks/${pkgver}/portingAids/${pkgname}-${pkgver}.tar.xz")
md5sums=('4442cd3c75960f03ff325141e399cb06')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DECM_MKSPECS_INSTALL_DIR=/usr/share/qt/mkspecs/modules \
    -DLOCALE_INSTALL_DIR=/usr/share/locale/kf5 \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
