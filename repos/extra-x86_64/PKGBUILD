# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Laurent Carlier <lordheavym@gmail.com>

pkgname=telepathy-kde-contact-list
_pkgname=ktp-contact-list
pkgver=0.8.1
pkgrel=2
pkgdesc="Telepathy contact list application"
arch=('i686' 'x86_64')
url="http://community.kde.org/Real-Time_Communication_and_Collaboration"
license=('GPL')
depends=('telepathy-kde-common-internals' 'kdebase-runtime')
makedepends=('cmake' 'automoc4' 'boost')
groups=('kde-telepathy')
install=${pkgname}.install
source=("http://download.kde.org/stable/kde-telepathy/${pkgver}/src/${_pkgname}-${pkgver}.tar.bz2")
sha1sums=('d8ac89630ae7c44800b2bcb87b307d4ccf323b99')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../${_pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
