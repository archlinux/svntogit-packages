# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: kevin <kevin@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>

pkgname=graphviz
pkgver=2.38.0
pkgrel=6
pkgdesc='Graph visualization software'
url='http://www.graphviz.org/'
license=('CPL')
arch=('i686' 'x86_64')
depends=('libltdl' 'gd' 'librsvg' 'libxaw' 'ghostscript' 'pango' 'gts')
makedepends=('swig' 'mono' 'guile' 'lua51' 'ocaml' 'perl' 'php' 'python2' 'r' 'tk' 'qt4' 'gtk2')
optdepends=('mono: sharp bindings'
            'guile: guile bindings'
            'lua51: lua bindings'
            'ocaml: ocaml bindings'
            'perl: perl bindings'
            'php: php bindings'
            'python2: python bindings'
            'r: r bindings'
            'tcl: tcl bindings'
            'qt4: gvedit'
            'gtk2: gtk output plugin')
source=("${url}/pub/${pkgname}/stable/SOURCES/${pkgname}-${pkgver}.tar.gz"
        'format-string.patch'
        'ghostscript918.patch')
sha1sums=('053c771278909160916ca5464a0a98ebf034c6ef'
          '271d116bc022596a64b6ee061f3d2e50fa2e6025'
          'bcce75a535c277556e196638e59ea019b0a63fa1')

install=install

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	patch -p1 -i ../format-string.patch # FS#42983
	patch -p1 -i ../ghostscript918.patch
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	export LIBPOSTFIX=/
	export PYTHON=python2
	export LUA=lua5.1

	./configure --prefix=/usr
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	# Workaround symlink upgrade bug (fixed in pacman trunk)
	# Also deduplicates the tcl libraries (upstream bug?)
	# Might have been this commit: https://github.com/ellson/graphviz/commit/f11aead
	rm -r "${pkgdir}/usr/lib/tcl8.6/graphviz"
	ln -s ../graphviz/tcl "$pkgdir/usr/lib/tcl8.6/graphviz"
}
