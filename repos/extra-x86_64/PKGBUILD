# $Id$
# Maintainer: Dave Reisner <dreisner@archlinux.org>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Douglas Soares de Andrade
# Contributor: Roberto Alsina <ralsina@kde.org>

pkgname=varnish
pkgver=4.1.2
pkgrel=1
pkgdesc="High-performance HTTP accelerator"
arch=('i686' 'x86_64')
url="http://www.varnish-cache.org/"
license=('BSD')
depends=('gcc' 'libedit' 'pcre')
makedepends=('python-docutils')
optdepends=('python: needed for vmod development')
backup=('etc/varnish/default.vcl')
install=$pkgname.install
source=("http://repo.varnish-cache.org/source/$pkgname-$pkgver.tar.gz"
        varnish-vcl-reload
        varnish.service
        varnish.sysusers)
md5sums=('51d446c0193dd773f5a881f7c0beb304'
         'b494b41b2a605f406abe63ed02dc117f'
         '8334d2e108695c121c41833b53838a05'
         'da8263571f450e3c25fdd3c3b4a1e259')

build() {
  cd "$pkgname-$pkgver"

  # https://github.com/varnish/Varnish-Cache/pull/88
  [[ $CARCH == i686 ]] && CFLAGS+=' -ffloat-store -fexcess-precision=standard'

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var/lib \
    --sbindir=/usr/bin

  make
}

check() {
  cd "$pkgname-$pkgver"

  # Sometimes fails on i686 in address remapping test. Not sure if flaky
  # test or otherwise...
  make check
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  install -m755 "$srcdir/varnish-vcl-reload" "$pkgdir/usr/bin"
  install -Dm644 "$srcdir/$pkgname.service" "$pkgdir/usr/lib/systemd/system/$pkgname.service"

  # create varnish:varnish user/group
  install -Dm644 "$srcdir/$pkgname.sysusers" "$pkgdir/usr/lib/sysusers.d/varnish.conf"

  # config
  install -Dm644 "etc/example.vcl" "$pkgdir/etc/varnish/default.vcl"
  install -Dm644 "etc/builtin.vcl" "$pkgdir/usr/share/doc/varnish/builtin.vcl"

  # license
  install -Dm644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
