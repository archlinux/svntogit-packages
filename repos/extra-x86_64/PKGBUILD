# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=grilo-plugins
pkgver=0.2.14
pkgrel=3
pkgdesc="Plugins for Grilo"
url="http://www.gnome.org"
arch=(i686 x86_64)
license=(LGPL)
depends=(grilo lua libmediaart libgdata libtracker-sparql gom libdmapsharing)
makedepends=(intltool itstool yelp-tools)
optdepends=('dleyna-server: DLNA plugin'
            'tracker: Tracker plugin')
options=('!emptydirs')
groups=('gnome')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver:0:3}/${pkgname}-${pkgver}.tar.xz
        local-metadata-crash.patch)
sha256sums=('d0f18116675058c7e8c178b1fe26d6d73998a0452f85a73b1696df740c83d3ab'
            '221f770b064309635b52095d21d325707d45de03772da5cc579252b7795ced1b')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../local-metadata-crash.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
