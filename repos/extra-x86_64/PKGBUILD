# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=librsvg
pkgver=2.40.1
pkgrel=3
epoch=1
pkgdesc="A SVG viewing library"
arch=(i686 x86_64)
license=('LGPL')
depends=('gdk-pixbuf2' 'pango' 'libcroco')
makedepends=('intltool' 'gtk3' 'gobject-introspection' 'vala')
optdepends=('gtk3: for rsvg-view-3')
options=('!emptydirs')
url="https://live.gnome.org/LibRsvg"
install=librsvg.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('8813b4fe776d5e7acbce28bacbaed30ccb0cec3734eb3632c711a16ebe2961d7')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-static --enable-vala
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
