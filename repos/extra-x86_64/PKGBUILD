# Maintainer:
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=brltty
pkgver=6.0
pkgrel=1
pkgdesc="Braille display driver for Linux/Unix"
arch=(x86_64)
url="http://mielke.cc/brltty"
license=(GPL LGPL)
depends=(libxaw gpm icu tcl bluez-libs)
makedepends=(at-spi2-core tcl speech-dispatcher cython espeak)
optdepends=('at-spi2-core: X11/GNOME Apps accessibility'
            'atk: ATK bridge for X11/GNOME accessibility'
            'speech-dispatcher: speech-dispatcher driver'
            'espeak: espeak driver')
backup=(etc/brltty.conf)
options=('!emptydirs')
install=brltty.install
source=(http://mielke.cc/brltty/archive/brltty-$pkgver.tar.xz
        brltty.service brltty.sysusers)
sha256sums=('cfa80c2d171a4d92d7a327b1942e6fc463669b1c3bb658bfb3323b723f9096f7'
            'a342a5e9d36f00a320e1d23d91c482bd5445af52588334307b7e9c09a8cc38ec'
            '0df1ab5cca644c6deb1090976a4e459243c15668fd5c3bafb278aa629878ec07')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --mandir=/usr/share/man \
    --with-tables-directory=/usr/share/brltty \
    --with-screen-driver=lx \
    --enable-gpm \
    --disable-java-bindings \

  make
}

package() {
  cd $pkgname-$pkgver
  make INSTALL_ROOT="$pkgdir" install
  install -Dm644 Documents/brltty.conf "$pkgdir/etc/brltty.conf"
  install -Dm644 ../brltty.service "$pkgdir/usr/lib/systemd/system/brltty.service"
  install -Dm644 ../brltty.sysusers "$pkgdir"/usr/lib/sysusers.d/brltty.conf
}
