# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: trya <tryagainprod@gmail.com>

pkgname=quazip
pkgver=1.2
pkgrel=1
pkgdesc='C++ wrapper for the ZIP/UNZIP C package'
url='https://stachenov.github.io/quazip/'
license=(LGPL)
arch=(x86_64)
depends=(qt5-base)
makedepends=(cmake)
source=(https://github.com/stachenov/$pkgname/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('2dfb911d6b27545de0b98798d967c40430312377e6ade57096d6ec80c720cb61')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
