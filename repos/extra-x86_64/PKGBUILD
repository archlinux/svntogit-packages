# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Gilles CHAUVIN <gcnweb@gmail.com>

pkgname=live-media
pkgver=2010.03.08
pkgrel=1
pkgdesc="A set of C++ libraries for multimedia streaming"
arch=('i686' 'x86_64')
license=('LGPL')
url="http://live555.com/liveMedia"
depends=('gcc-libs')
source=(ftp://ftp.archlinux.org/other/live-media/live.${pkgver}.tar.gz)
md5sums=('2c685c004695afce064ec780e58409cc')

build()
{
  cd ${srcdir}/live
   sed \
       -e 's/$(INCLUDES) -I. -O2 -DSOCKLEN_T/$(INCLUDES) -I. -O2 -I. -fPIC -DSOCKLEN_T/g' \
       -e 's/\(LIBRARY_LINK =\).*/\1 $(LD) -o/g' \
       -e 's/\(LIBRARY_LINK_OPTS =\).*/\1 $(LINK_OPTS) -r -Bstatic/g' \
       -i config.linux
  ./genMakefiles linux
  make || return 1

  for dir in BasicUsageEnvironment UsageEnvironment groupsock liveMedia; do
    mkdir -p ${pkgdir}/usr/lib/live/${dir}
    cp -r ${dir}/*.a ${dir}/include ${pkgdir}/usr/lib/live/${dir}
  done

  mkdir -p ${pkgdir}/usr/bin
  for testprog in `find testProgs -type f -perm 755`; do
    install ${testprog} ${pkgdir}/usr/bin
  done
}
