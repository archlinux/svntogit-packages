# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=freecell-solver
pkgver=5.4.0
pkgrel=1
pkgdesc="A program that automatically solves layouts of Freecell and similar variants of Card Solitaire"
arch=(x86_64)
url="https://fc-solve.shlomifish.org"
license=(MIT)
depends=(bash)
makedepends=(cmake gperf perl-path-tiny perl-template-toolkit python-random2 python-six python-pysol_cards)
optdepends=('python-random2: for the Python interface' 'python-six: for the Python interface'
            'python-pysol_cards: for the Python interface')
source=(https://fc-solve.shlomifish.org/downloads/fc-solve/$pkgname-$pkgver.tar.xz)
sha256sums=('6ebd455261c37f32f567e3860d19b593bb679974de766ab4107cb06f8a929ab9')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DFCS_WITH_TEST_SUITE=OFF \
    -DBUILD_STATIC_LIBRARY=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir"/$pkgname-$pkgver/COPYING.html "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.html
}

