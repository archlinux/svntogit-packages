# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Aurélien Wailly <aurelien.wailly@gmail.com>

pkgname=protobuf-c
pkgver=1.3.0
pkgrel=3
pkgdesc='Protocol Buffers implementation in C'
arch=('x86_64')
url='https://github.com/protobuf-c/protobuf-c'
license=('BSD')
depends=('protobuf')
provides=('libprotobuf-c.so')
source=("$url/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz"
        add-std-namespace-to-some-types.patch
        protobuf36.patch)
sha256sums=('5dc9ad7a9b889cf7c8ff6bf72215f1874a90260f60ad4f88acf21bb15d2752a1'
            '8dcb76d45bee8ae0a8652546f213c17a9b8fa774f16f50db18ed5ddad313aeb3'
            '1330ee4a72f268805ffa3d154c561a03b3086f962cc4e92f2740627db37dd769')

prepare() {
  cd "$pkgname-$pkgver"

  # https://github.com/protobuf-c/protobuf-c/pull/309
  patch -Np1 -i ../add-std-namespace-to-some-types.patch

  # https://github.com/protobuf-c/protobuf-c/pull/328
  patch -Np1 -i ../protobuf36.patch
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr --disable-static
  make
}

check() {
  make -C "$pkgname-$pkgver" check
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
