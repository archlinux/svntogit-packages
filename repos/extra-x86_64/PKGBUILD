# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=totem
pkgver=3.12.1
pkgrel=1
pkgdesc="GNOME3 movie player based on GStreamer"
url="http://www.gnome.org"
arch=(i686 x86_64)
license=(GPL2 custom)
depends=(totem-plparser desktop-file-utils iso-codes libpeas clutter-gtk clutter-gst grilo
         hicolor-icon-theme gnome-icon-theme gsettings-desktop-schemas dconf
         python-gobject python-xdg
         gst-plugins-base gst-plugins-good gst-plugins-bad)
makedepends=(intltool libnautilus-extension lirc-utils vala zeitgeist
             itstool docbook-xsl python-pylint)
optdepends=('gst-plugins-ugly: Extra media codecs'
            'gst-libav: Extra media codecs'
            'lirc-utils: Infrared Remote Control plugin'
            'grilo-plugins: Media discovery'
            'zeitgeist: Zeitgeist plugin')
groups=(gnome)
options=('!emptydirs')
backup=(etc/totem/browser-plugins.ini)
install=totem.install
conflicts=(totem-plugin)
replaces=(totem-plugin)
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz
        browser-plugins.ini bugreport.patch)
sha256sums=('dd168cdd4051d01131d47c24fa45bfd08b6ccf45900ac4b64bae47f6f47a35e3'
            'a50a3bbf35f0535f7e8e20af1893446a2e5711015484f9ae6d1ff91af3b23c4e'
            '30481dab7e21b66257e3238f5a8b708e1a5c4797d7da904f7ebaba4c2c2ef167')

prepare() {
  cd "$pkgname-$pkgver"

  # Make compatible with Python 3 and Gstreamer 1.0
  patch -Np1 -i ../bugreport.patch
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/totem \
    --localstatedir=/var \
    --disable-static \
    --enable-python \
    --enable-nautilus

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="${pkgdir}" install

  install -Dm644 ../browser-plugins.ini "$pkgdir/etc/totem/browser-plugins.ini"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/totem/COPYING"
}
