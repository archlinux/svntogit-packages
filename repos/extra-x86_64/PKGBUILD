# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xf86-video-intel
pkgver=2.99.914
pkgrel=4
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
install=$pkgname.install
pkgdesc="X.org Intel i810/i830/i915/945G/G965+ video drivers"
depends=('intel-dri' 'libxvmc' 'pixman' 'xcb-util>=0.3.9')
makedepends=('xorg-server-devel' 'X-ABI-VIDEODRV_VERSION=18' 'libx11' 'libxrender'
             # additional deps for intel-virtual-output
            'libxrandr' 'libxinerama' 'libxcursor' 'libxtst')
optdepends=('libxrandr: for intel-virtual-output'
            'libxinerama: for intel-virtual-output'
            'libxcursor: for intel-virtual-output'
            'libxtst: for intel-virtual-output')
replaces=('xf86-video-intel-uxa' 'xf86-video-intel-sna')
provides=('xf86-video-intel-uxa' 'xf86-video-intel-sna')
conflicts=('xorg-server<1.16' 'X-ABI-VIDEODRV_VERSION<18' 'X-ABI-VIDEODRV_VERSION>=19'
           'xf86-video-intel-sna' 'xf86-video-intel-uxa' 'xf86-video-i810' 'xf86-video-intel-legacy')
groups=('xorg-drivers' 'xorg')
source=(${url}/archive/individual/driver/${pkgname}-${pkgver}.tar.bz2
        fix-i810-segfault-2.99.914.patch
	sna-glyphs-Fallback-to-masked-glyphs-if-rendering-to.patch
        sna-clear-cursor-image-after-rotation.patch)
sha256sums=('78a22e5efd460b790c634caaf1afbb756046dd890482e204bb0d179baad27e46'
            'a7c27ce28e6db47971aea2762496fdb05f93413c418756f5e7f6cf2e76b91041'
            'a40ee7d923ce77057d4add2f9dae61bc7870e64b2cf1f49d2b2a6d729102b7ae')

prepare() {
  cd ${pkgname}-${pkgver}
  # fix segfaulting on i810 chipsets - merged upstream
  patch -Np1 -i ../fix-i810-segfault-2.99.914.patch
  # fix FS#40949 - merged upstream
  patch -Np1 -i ../sna-glyphs-Fallback-to-masked-glyphs-if-rendering-to.patch
  # fix FS#41317 - merged upstream
  patch -Np1 -i ../sna-clear-cursor-image-after-rotation.patch
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr \
    --libexecdir=/usr/lib \
    --enable-glamor \
    --disable-dri3
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
sha256sums=('78a22e5efd460b790c634caaf1afbb756046dd890482e204bb0d179baad27e46'
            'a7c27ce28e6db47971aea2762496fdb05f93413c418756f5e7f6cf2e76b91041'
            'a40ee7d923ce77057d4add2f9dae61bc7870e64b2cf1f49d2b2a6d729102b7ae'
            'bc5383abd2dd680e830600da3d3da1d9c56b0b270efffd3aec94b23bfd9179c9')
