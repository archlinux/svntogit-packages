# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ndctl
pkgver=72
pkgrel=1
pkgdesc='Utility library for managing the libnvdimm (non-volatile memory device) sub-system in the Linux kernel'
arch=('x86_64')
url="https://github.com/pmem/ndctl"
license=('LGPL')
depends=('iniparser' 'json-c' 'keyutils' 'kmod' 'systemd-libs' 'util-linux-libs')
makedepends=('asciidoctor' 'bash-completion' 'systemd' 'xmlto')
source=("https://github.com/pmem/ndctl/archive/v$pkgver/$pkgname-$pkgver.tar.gz"
        ndctl-iniparser-include.patch::https://github.com/pmem/ndctl/pull/186.patch)
sha512sums=('0e27aebe9d700589703fdfaa8768e47c01af02dd8a252eeef32d2ec394cb4108dfa1a7142340335cb3e7f1e935915247821f9033e46c3f2f2e008d6beb51dd95'
            '1ccc43fbd017a790f1f36b96b6ab061cafbbcb8c2dc127c81c968cdf5e4a75c7594a8bc53dee5a1f9a81f4b113137959671d6d63448b331982feb9b3b723bd7b')

prepare() {
  # fix location of modprobe.d conf
  sed -i 's|^modprobedir =.*|modprobedir = /usr/lib/modprobe.d/|' $pkgname-$pkgver/Makefile.am

  patch -d $pkgname-$pkgver -p1 < ndctl-iniparser-include.patch
}

build() {
  cd "$srcdir"/$pkgname-$pkgver
  ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
