# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-bdftopcf
pkgver=1.1.1
pkgrel=1
pkgdesc="Convert X font from Bitmap Distribution Format to Portable Compiled Format"
arch=(x86_64)
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('glibc')
makedepends=('xorg-util-macros' 'xorgproto')
groups=('xorg' 'xorg-apps')
source=(https://xorg.freedesktop.org/archive/individual/util/bdftopcf-${pkgver}.tar.xz{,.sig})
sha512sums=('257dc9ae29f2cf1496aad91ccba534348d87f881d75431aa6611a8154cb817d17588c464ab6c6c6bc51e6060baa8dcfcb9fd8c037cdaea8d18287651fa6b7ea9'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # "Alan Coopersmith <alan.coopersmith@oracle.com>"
#validpgpkeys=('995ED5C8A6138EB0961F18474C09DD83CAAA50B2') # "Adam Jackson <ajax@nwnk.net>"

build() {
  cd bdftopcf-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd bdftopcf-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
