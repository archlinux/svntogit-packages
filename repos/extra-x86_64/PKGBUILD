# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=dconf-editor
pkgver=3.38.0
pkgrel=1
pkgdesc="dconf Editor"
arch=(x86_64)
url="https://wiki.gnome.org/Projects/dconf"
license=(GPL3)
groups=(gnome-extra)
depends=(gtk3 dconf)
makedepends=(vala meson appstream-glib git)
_commit=0f03220e259d32141aad4870d846c7676429a318  # tags/3.38.0^0
source=("git+https://gitlab.gnome.org/GNOME/dconf-editor.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}

# vim:set ts=2 sw=2 et:
