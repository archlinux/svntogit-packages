# Maintainer: Firmicus <firmicus āt gmx dōt net>

pkgname=texlive-pstricks
pkgver=2008.11169
pkgrel=1
pkgdesc="TeX Live - Additional PSTricks packages"
license=('GPL')
arch=('i686' 'x86_64')
depends=('texlive-core')
makedepends=('lzma-utils')
groups=('texlive-most')
url='http://tug.org/texlive/'
source=("ftp://ftp.archlinux.org/other/texlive/$pkgname-src.zip")
install=texlive.install
md5sums=('79eedb5fe6ffd1e3754cfecd03d2d2ff')

build() {
   cd $srcdir
   install -m755 -d $pkgdir/usr/share/texmf-var/arch/installedpkgs || return 1
   sed -i '/^#/d' CONTENTS || return 1
   _revnr=`echo $pkgver | sed 's/2008\.//'`
   install -m644 CONTENTS $pkgdir/usr/share/texmf-var/arch/installedpkgs/${pkgname}_${_revnr}.pkgs || return 1
   for p in *.tar.lzma; do 
	   lzma --force -dc $p | tar xf - || return 1
   done
   install -m755 -d $pkgdir/usr/share || return 1
   find texmf* -type d -exec install -d -m755 $pkgdir/usr/share/'{}' \; || exit 1
   find texmf* -type f -exec install -m644 '{}' $pkgdir/usr/share/'{}' \; || exit 1
   chmod a+x $pkgdir/usr/share/texmf-dist/scripts/pst2pdf/pst2pdf.pl
}
