# $Id$
# Maintainer: Roman Kyrylych <roman@archlinux.org>

pkgname=gnome-user-share
pkgver=3.10.2
pkgrel=3
pkgdesc="Easy to use user-level file sharing for GNOME."
arch=('i686' 'x86_64')
url="http://ftp.gnome.org/pub/GNOME/sources/gnome-user-share/"
license=('GPL')
depends=('mod_dnssd' 'dconf' 'gtk3' 'hicolor-icon-theme' 'libnotify' 'gnome-bluetooth' 'libcanberra')
makedepends=('intltool' 'libnautilus-extension' 'itstool' 'docbook-xsl')
options=('!emptydirs')
groups=('gnome')
install=gnome-user-share.install
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz
        fix-no-password-mode.patch)
sha256sums=('1d1ea57a49224c36e7cba04f80265e835639377f474a7582c9e8ac946eda0f8f'
            'c35ba44284d5797fee3dfafd879333e5ec26f5b3fa18703e6235aa487c0e25bd')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i ../fix-no-password-mode.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/gnome-user-share \
      --localstatedir=/var --enable-bluetooth
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
