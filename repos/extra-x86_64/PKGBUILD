# Maintainer: Laurent Carlier <lordheavym@gmail.com>

pkgbase=vulkan-lunarg-tools
pkgname=(vulkan-extra-tools vulkan-extra-layers)
_tag=1.3.243.0
pkgver=1.3.243.0
pkgrel=1
arch=(x86_64)
url="http://lunarg.com/"
license=('custom')
depends=()
makedepends=(git cmake python libx11 libxrandr wayland qt5-svg qt5-webengine)
#source=("https://github.com/LunarG/VulkanTools/archive/v${pkgver}.tar.gz")
source=("VulkanTools-sdk::git+https://github.com/LunarG/VulkanTools.git#tag=sdk-${_tag}")
sha256sums=('SKIP')

_install() {
  local src f dir
  for src; do
    f="${src#fakeinstall/}"
    dir="${pkgdir}/${f%/*}"
    install -m755 -d "${dir}"
    mv -v "${src}" "${dir}/"
  done
}

pkgver() {
 cd "${srcdir}"/VulkanTools*

 #echo ${_tag}+$(git rev-list --count HEAD)+$(git rev-parse --short HEAD)
 echo ${_tag}
}

build() {
  cd "${srcdir}"/VulkanTools*

  ./update_external_sources.sh
  rm -rf build ; mkdir build ; cd build
  ../scripts/update_deps.py

  cmake -C helper.cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_SYSCONFDIR=/etc \
    -DCMAKE_INSTALL_DATADIR=/usr/share \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_SKIP_RPATH=True \
    -DBUILD_WSI_XCB_SUPPORT=On \
    -DBUILD_WSI_XLIB_SUPPORT=On \
    -DBUILD_WSI_WAYLAND_SUPPORT=On \
    -DBUILD_TESTS=OFF \
    -DBUILD_VIA=ON \
    -DCMAKE_BUILD_TYPE=Release \
    ..
  make
  make DESTDIR="${srcdir}/fakeinstall" install
}

package_vulkan-extra-tools() {
  pkgdesc="Vulkan lunarg tools"
  depends=(vulkan-validation-layers qt5-svg qt5-webengine)
  conflicts=('vulkan-trace<1.2.148.1')
  groups=('vulkan-devel')

  _install fakeinstall/usr/bin/*

  install -dm755 ${pkgdir}/usr/share/{doc,licenses}/${pkgname}
  install -m644 VulkanTools*/LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/
  install -m644 VulkanTools*/vkconfig/*.md "${pkgdir}"/usr/share/doc/${pkgname}/
}

package_vulkan-extra-layers() {
  pkgdesc="Extra layers for Vulkan development"
  depends=(vulkan-validation-layers)
  groups=('vulkan-devel')
  
  _install fakeinstall/usr/lib/libVkLayer_api_dump.so
  _install fakeinstall/usr/lib/libVkLayer_monitor.so
  _install fakeinstall/usr/lib/libVkLayer_screenshot.so

  _install fakeinstall/etc/vulkan/explicit_layer.d/VkLayer_api_dump.json
  _install fakeinstall/etc/vulkan/explicit_layer.d/VkLayer_monitor.json
  _install fakeinstall/etc/vulkan/explicit_layer.d/VkLayer_screenshot.json

  install -dm755 ${pkgdir}/usr/share/{doc,licenses}/${pkgname}
  install -m644 VulkanTools*/LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/
  install -m644 VulkanTools*/layersvt/README.md "${pkgdir}"/usr/share/doc/${pkgname}/
}

