# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgbase=icedtea-web
pkgname=('icedtea-web' 'icedtea-web-doc')
pkgver=1.1.4
pkgrel=1
arch=('i686' 'x86_64')
url="http://icedtea.classpath.org/wiki/IcedTea-Web"
license=('GPL2')
makedepends=('openjdk6' 'zip' 'xulrunner')
source=(http://icedtea.classpath.org/download/source/$pkgname-$pkgver.tar.gz
	icedtea-web-1.1.4-npapi-fix.patch)
md5sums=('1ed24b7e58da26471187dfae80b020b6'
         '81ac36e05ea1b55f1580bdb28c24f4db')

build() {
  _javaver=6
  _jvmdir=/usr/lib/jvm/java-${_javaver}-openjdk

  cd "$srcdir/$pkgname-$pkgver"
  
  # fix build with Xulrunner 8
  patch -Np1 -i ${srcdir}/icedtea-web-1.1.4-npapi-fix.patch
  
  ./configure --prefix=/usr/lib/jvm/java-6-openjdk \
      --datarootdir=/usr/share
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make -k check
}


package_icedtea-web() {

  pkgdesc="provides a Free Software web browser plugin running applets written in the Java programming language and an implementation of Java Web Start, originally based on the NetX project"
  depends=('openjdk6' 'gtk2' 'desktop-file-utils')
  install=$pkgname.install

  _javaver=6
  _jvmdir=/usr/lib/jvm/java-${_javaver}-openjdk

  cd "$srcdir/$pkgname-$pkgver"
  # possible make target (see bottom of Makefile.am: install-exec-local install-data-local
  make DESTDIR="$pkgdir" install-exec-local install-data-local

  # Install desktop files.
  install -m755 -d ${pkgdir}/usr/share/{applications,pixmaps}
  install -m644 javaws.png ${pkgdir}/usr/share/pixmaps
  install -m644 {javaws,itweb-settings}.desktop ${pkgdir}/usr/share/applications
  # remove splitted doc files
  rm -rf ${pkgdir}/usr/share/doc

  # link binaries into /usr/bin + jre/bin
  install -m755 -d ${pkgdir}/usr/bin
  install -m755 -d ${pkgdir}/${_jvmdir}/jre/bin
  pushd ${pkgdir}/${_jvmdir}/bin
  for file in *; do
    ln -sf ${_jvmdir}/bin/${file} ${pkgdir}/usr/bin
    ln -sf ${_jvmdir}/bin/${file} ${pkgdir}/${_jvmdir}/jre/bin
  done
  popd

  # link the mozilla-plugin - test it here http://www.java.com/en/download/help/testvm.xml
  install -m755 -d ${pkgdir}/usr/lib/mozilla/plugins/
  ln -sf ${_jvmdir}/lib/IcedTeaPlugin.so ${pkgdir}/usr/lib/mozilla/plugins/
}

package_icedtea-web-doc() {

  pkgdesc="icedtea-web browser plugin + Java WebStart - documentation files"

  _javaver=6
  _jvmdir=/usr/lib/jvm/java-${_javaver}-openjdk

  cd "$srcdir/$pkgbase-$pkgver"
#  install -m755 -d $pkgdir/${_jvmdir}/jre/lib
  make DESTDIR="$pkgdir" install-data-local
  # remove javaws about and man page
  rm -rf ${pkgdir}/usr/lib
  rm -rf ${pkgdir}/usr/share/man
  rm -rf ${pkgdir}/usr/share/icedtea-web # conflicting and unneeded file it seems
}
