# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=libsamplerate
pkgver=0.1.8
pkgrel=3
pkgdesc="Secret Rabbit Code - aka Sample Rate Converter for audio"
arch=('i686' 'x86_64')
url="http://www.mega-nerd.com/SRC/index.html"
license=('GPL')
depends=('libsndfile')
source=(http://www.mega-nerd.com/SRC/libsamplerate-${pkgver}.tar.gz{,.asc})
sha1sums=('e5fe82c4786be2fa33ca6bd4897db4868347fe70'
          'SKIP')
validpgpkeys=('73571E85C19F4281D8C97AA86CA41A7743B8D6C8')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
