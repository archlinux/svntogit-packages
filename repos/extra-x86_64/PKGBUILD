# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=lcms2
pkgver=2.6
pkgrel=1
pkgdesc="Small-footprint color management engine, version 2"
arch=('i686' 'x86_64')
url="http://www.littlecms.com"
license=('MIT')
depends=('libtiff')
source=(http://downloads.sourceforge.net/sourceforge/lcms/${pkgname}-${pkgver}.tar.gz)
sha1sums=('b0ecee5cb8391338e6c281d1c11dcae2bc22a5d2')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
