# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: James An <james@jamesan.ca>
# Contributor: Daniel Nagy <danielnagy at gmx de>

pkgname=libde265
pkgver=1.0.10
pkgrel=2
pkgdesc='Open h.265 video codec implementation'
arch=(x86_64)
url='https://github.com/strukturag/libde265'
license=(LGPL3)
depends=(gcc-libs)
makedepends=(ffmpeg sdl qt5-base)
optdepends=('ffmpeg: for sherlock265'
            'qt5-base: for sherlock265'
            'sdl: dec265 YUV overlay output')
source=(https://github.com/strukturag/libde265/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz
        https://github.com/strukturag/libde265/commit/388b6145.patch)
sha256sums=('8761333dd823a30fe19668b5290686d5438e0448d0c7d2e81ad49a75741c34a4'
            '5fdf96a39248d428f778c81a8a9ee380b0e613433d6bce29c388473a00955f3c')

prepare() {
  patch -d $pkgname-$pkgver -Rp1 < 388b6145.patch # Revert pc file breakage
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --enable-static=no
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

# Remove useless tests binary
  rm "$pkgdir"/usr/bin/tests
}
