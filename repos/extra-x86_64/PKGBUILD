# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=liferea
pkgver=1.6.6b
pkgrel=1
pkgdesc="A desktop news aggregator for online news feeds and weblogs"
arch=('i686' 'x86_64')
url="http://liferea.sourceforge.net/"
license=('GPL')
depends=('gconf' 'libwebkit' 'libglade' 'hicolor-icon-theme')
makedepends=('pkg-config' 'intltool' 'lua' 'libnotify')
optdepends=('lua: for lua scripting support'
            'libnotify: for notification support')
options=('!libtool' '!emptydirs')
install=liferea.install
source=(http://downloads.sourceforge.net/sourceforge/liferea/liferea-${pkgver}.tar.gz
        libnotify-0.7.patch)
md5sums=('1db10281f5ade2fa350f2a245733a74e'
         '9cab1ef7ba1c1e815e2acb275c59cc80')
sha1sums=('e483a1356cc7ff653a178dde520b21faa18a2ce5'
          'a88fd7682e040b7f09936b641967496dc35c67a3')

build() {
  cd "${srcdir}/liferea-${pkgver}"
  patch -Np1 -i "$srcdir/libnotify-0.7.patch"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --disable-schemas-install
  make
}

package() {
  cd "${srcdir}/liferea-${pkgver}"
  make DESTDIR="${pkgdir}" install
  
  install -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" "${pkgdir}"/etc/gconf/schemas/*.schemas
  rm -f "${pkgdir}"/etc/gconf/schemas/*.schemas
}
