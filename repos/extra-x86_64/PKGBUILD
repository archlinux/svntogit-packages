# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Jeremy Cowgar <jeremy@cowgar.com>

pkgname=check
pkgver=0.9.13
pkgrel=1
pkgdesc="A unit testing framework for C"
arch=('i686' 'x86_64')
url="http://check.sourceforge.net/"
license=('LGPL')
install=$pkgname.install
depends=('awk')
source=(http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz)
md5sums=('95530868f81a9496b2518fd2b713008a')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-static
  make
}

check() {
  cd $pkgname-$pkgver
  # Extremely long
  #make -k check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  # get rid of the package's info directory,
  # install-info adds entries for us at install-time
  rm "$pkgdir/usr/share/info/dir"

  # svn log file is too big
  rm "$pkgdir"/usr/share/doc/check/*ChangeLog*
}
