# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-xvidtune
pkgver=1.0.4
pkgrel=1
pkgdesc="Video mode tuner for Xorg"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libxxf86vm' 'libxt' 'libxaw' 'libxmu' 'libx11')
makedepends=('xorg-util-macros')
source=(https://xorg.freedesktop.org/archive/individual/app/xvidtune-${pkgver}.tar.xz{,.sig})
sha512sums=('747d5358f4f7d3d7db508c3f034711ce4b9dd278160d35312e9c4d1604623e84a2438d039b536546fd2df7db8c5ea1907e8a6101985fad2932b6dee5a62c7b80'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # Alan Coopersmith <alanc@freedesktop.org>

build() {
  cd xvidtune-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd xvidtune-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
