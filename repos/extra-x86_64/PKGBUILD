# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Adam Walters <abwalters@abwalters.us>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: RÃ©my Oudompheng <remy@archlinux.org>

pkgname=libatomic_ops
pkgver=7.4.2
_tag=$pkgname-${pkgver//./_}
pkgrel=1
pkgdesc="Provides semi-portable access to hardware provided atomic memory operations"
arch=('i686' 'x86_64')
url="https://github.com/ivmai/libatomic_ops"
depends=('glibc')
license=('GPL2' 'MIT')
source=(https://github.com/ivmai/libatomic_ops/archive/$_tag.zip)
sha1sums=('9a4ebc2a946a75379634f4ca53fb57249cdb008a')

build() {
  cd $pkgname-$_tag
  ./autogen.sh
  ./configure --prefix=/usr --disable-static --enable-shared
  make
}

check() {
  cd $pkgname-$_tag
  make check
}

package() {
  cd $pkgname-$_tag
  make DESTDIR="$pkgdir" install
  install -D -m644 doc/LICENSING.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  rm -r "$pkgdir/usr/share/libatomic_ops"
}
