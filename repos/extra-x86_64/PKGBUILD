# $Id$
# Maintainer:
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Gilles CHAUVIN <gcnweb@gmail.com>

pkgname=live-media
pkgver=2017.10.28
pkgrel=1
pkgdesc="A set of C++ libraries for multimedia streaming"
arch=('x86_64')
license=('LGPL')
url="http://live555.com/liveMedia"
depends=('gcc-libs')
options=('staticlibs')
source=("http://live555.com/liveMedia/public/live.${pkgver}.tar.gz")
sha256sums=('d8eaec9ded34321aa655d3c9007217dd447218c54cb48c97827e58ecd5edb338')

prepare() {
  cd live
  sed -e '/xlocale.h/d' -i liveMedia/include/Locale.hh # fix build with glibc 2.26
}

build() {
  cd live

  sed \
      -e 's/$(INCLUDES) -I. -O2 -DSOCKLEN_T/$(INCLUDES) -I. -O2 -I. -fPIC -DRTSPCLIENT_SYNCHRONOUS_INTERFACE=1 -DSOCKLEN_T/g' \
      -i config.linux
  ./genMakefiles linux
  make
}

package() {
  cd live

  for dir in BasicUsageEnvironment groupsock liveMedia UsageEnvironment; do
    install -dm755 ${pkgdir}/usr/{bin,lib,include/${dir}}
    install -m644 ${dir}/*.a "${pkgdir}/usr/lib"
    install -m644 ${dir}/include/*.h* "${pkgdir}/usr/include/${dir}"
  done

  for testprog in `find testProgs -type f -perm 755`; do
    install ${testprog} "${pkgdir}/usr/bin"
  done
}
