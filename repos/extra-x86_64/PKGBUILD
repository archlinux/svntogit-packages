# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=slang
pkgver=2.3.1a
pkgrel=2
pkgdesc="S-Lang is a powerful interpreted language"
arch=('x86_64')
url="http://www.jedsoft.org/slang/"
license=('GPL')
depends=('pcre')
backup=('etc/slsh.rc')
options=('!makeflags')
source=(http://www.jedsoft.org/releases/slang/${pkgname}-${pkgver}.tar.bz2{,.asc}
        0001-pre2.3.2-19-Added-support-for-the-new-ncurses-32-bit.patch)
sha1sums=('a8ea7f1b5736160a94efb67b137a0f5b9916bdf2'
          'SKIP'
          '94548d7d996f5d75e02d8e158e261c0b3e013599')
validpgpkeys=('AE962A02D29BFE4A4BB2805FDE401E0D5873000A')  # John E. Davis

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i ../0001-pre2.3.2-19-Added-support-for-the-new-ncurses-32-bit.patch
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

check() {
  cd ${pkgname}-${pkgver}

  [[ $CARCH = "i686" ]] || make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install-all
}
