# Maintainer: Maxime Gauduin <alucryd@archlonux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=libva
pkgver=2.3.0
pkgrel=1
pkgdesc='Video Acceleration (VA) API for Linux'
arch=('x86_64')
url='https://01.org/linuxmedia/vaapi'
license=('MIT')
depends=('libdrm' 'libgl' 'libx11' 'libxext' 'libxfixes' 'wayland')
makedepends=('git' 'libglvnd' 'mesa' 'meson')
optdepends=('libva-vdpau-driver: backend for Nvidia and AMD cards'
            'libva-intel-driver: backend for Intel cards')
provides=('libva-drm.so' 'libva-glx.so' 'libva-wayland.so' 'libva-x11.so' 'libva.so')
source=("git+https://github.com/intel/libva.git#tag=${pkgver}")
sha256sums=('SKIP')

build() {
  arch-meson libva build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
  install -Dm 644 libva/COPYING -t "${pkgdir}"/usr/share/licenses/libva
}

# vim: ts=2 sw=2 et:
