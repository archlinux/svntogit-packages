# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=calendarsupport
pkgver=18.08.2
pkgrel=1
pkgdesc="Calendar support library"
arch=(x86_64)
url="https://kontact.kde.org"
license=(LGPL)
depends=(kdepim-apps-libs kholidays akonadi-calendar)
makedepends=(extra-cmake-modules boost qt5-tools)
conflicts=('libkdepim<16')
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('4c975356eced1b54e5d83f54dbd7781c711fb89b714f6c42554babb7ea53e910'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
