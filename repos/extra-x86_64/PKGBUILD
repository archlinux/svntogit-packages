# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: Link Dupont <link@subpop.net>

pkgname=mpc
pkgver=0.34
pkgrel=3
pkgdesc="Minimalist command line interface to MPD"
arch=(x86_64)
url="https://www.musicpd.org/clients/mpc/"
license=(GPL2)
depends=(glibc)
makedepends=(libmpdclient meson python-sphinx rsync)
source=(https://www.musicpd.org/download/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz{,.sig})
sha512sums=('d43d5547134ffb24f2a2972882f6e264c1011a94d1033369d329487f59414d32d9842835afdd05da96fd8ed28f823d2ae2f46d8d8d24b68f3695badca2a9bbb2'
            'SKIP')
b2sums=('709f54ceebd66f703e5b1bf6ff8deb890e484fdc29c0b2198922763e347caac89d6eec5f74101f9fa860436e7493e2b02cc6b1b26e800e783546fe5a0c6e0d8f'
        'SKIP')
validpgpkeys=('0392335A78083894A4301C43236E8A58C6DB4512') # Max Kellermann <max@blarg.de>


build() {
  arch-meson build $pkgname-$pkgver
  ninja -C build
}

package() {
  depends+=(
    libmpdclient libmpdclient.so
  )

  DESTDIR="$pkgdir" ninja -C build install
  install -vDm 644 $pkgname-$pkgver/contrib/mpc-completion.bash "$pkgdir/usr/share/bash-completion/completions/mpc"
  # the html documentation is not reproducible and only the man page is needed
  rm -rvf "$pkgdir/usr/share/doc/$pkgname/"{html,contrib/*.bash}
}
