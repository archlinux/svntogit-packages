# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Leslie P. Polzer <leslie.polzer@gmx.net>

pkgname=xcompmgr
pkgver=1.1.6
pkgrel=2
pkgdesc="Composite Window-effects manager for X.org"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libxcomposite' 'libxdamage' 'libxrender' 'libxext')
conflicts=('xapps')
replaces=('xapps')
source=(http://xorg.freedesktop.org/releases/individual/app/${pkgname}-${pkgver}.tar.bz2
       fix_broken_shadows.diff)
sha1sums=('a9cf78ea32bac51ff584115381f195566b164fe3'
          '435f9f0ad652bdd759f822514bf1559abf119ffe')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # fix broken shadows in openbox - patch takern from https://bugs.freedesktop.org/show_bug.cgi?id=46285
  patch -Np0 -i ${srcdir}/fix_broken_shadows.diff
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
