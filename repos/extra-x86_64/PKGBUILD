# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgbase=pyqt6-datavisualization
pkgname=(python-pyqt6-datavisualization)
pkgver=6.3.0
pkgrel=1
pkgdesc='Python bindings for QtDataVisualization'
arch=(x86_64)
url='https://www.riverbankcomputing.com/software/pyqtdatavisualization/intro'
license=(GPL3)
groups=(pyqt6)
depends=(qt6-datavis3d python-pyqt6)
makedepends=(qt6-declarative sip pyqt-builder)
source=(https://pypi.python.org/packages/source/P/PyQt6-DataVisualization/PyQt6_DataVisualization-$pkgver.tar.gz)
sha256sums=('f3e39f04e15fa022d8343db108af03c462de81d98c07efccb0e4b6d29292f537')
options=(debug)

build() {
  cd PyQt6_DataVisualization-$pkgver
  sip-build \
    --no-make \
    --qmake=/usr/bin/qmake6
  cd build
  make
}

package_python-pyqt6-datavisualization(){
  cd PyQt6_DataVisualization-$pkgver/build
  make INSTALL_ROOT="$pkgdir" install
}
