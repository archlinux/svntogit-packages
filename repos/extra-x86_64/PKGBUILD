# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>

pkgname=libtorrent-rasterbar
pkgver=0.15.5
pkgrel=2
pkgdesc="A C++ library that aims to be a good alternative to all the other bittorrent implementations around"
url="http://www.rasterbar.com/products/libtorrent/"
arch=('i686' 'x86_64')
license=('custom')
depends=('boost-libs' 'python2')
makedepends=('boost')
source=(http://libtorrent.googlecode.com/files/${pkgname}-${pkgver}.tar.gz)
md5sums=('f1504a619e876c2731ee9a6b7d19cb1d')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  export CFLAGS="${CFLAGS} -DBOOST_FILESYSTEM_VERSION=2"
  export CXXFLAGS="${CXXFLAGS} -DBOOST_FILESYSTEM_VERSION=2"
  ./configure --prefix=/usr --enable-python-binding
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -D COPYING "${pkgdir}/usr/share/licenses/$pkgname/LICENSE"
}
