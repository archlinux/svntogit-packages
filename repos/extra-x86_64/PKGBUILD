# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: kevin <kevin@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>

pkgname=perl-libintl-perl
_realname=libintl-perl
pkgver=1.23
pkgrel=3
pkgdesc="Perl Module: Localization support"
arch=('i686' 'x86_64')
license=('LGPL')
url="http://search.cpan.org/dist/${_realname}/"
depends=('gettext' 'perl')
options=('!emptydirs')
source=(http://www.cpan.org/authors/id/G/GU/GUIDO/${_realname}-$pkgver.tar.gz)
md5sums=('2e79dc842af1c9efc14fbe6664dc89bf')

build() {
  cd "${srcdir}/${_realname}-$pkgver"
  # install module in vendor directories.
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "${srcdir}/${_realname}-$pkgver"
  make test
}

package() {
  cd "${srcdir}/${_realname}-$pkgver"
  make install DESTDIR="${pkgdir}"
}
