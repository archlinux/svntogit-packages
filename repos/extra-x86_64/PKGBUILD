# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: Mark Rosenstand <mark@archlinux.org>

pkgbase=fuse3
pkgname=(fuse-common fuse3)
pkgver=3.0.2
pkgrel=1
arch=('i686' 'x86_64')
url='https://github.com/libfuse/libfuse'
license=('GPL2')
makedepends=('pkg-config')
options=(!emptydirs)
source=(https://github.com/libfuse/libfuse/releases/download/fuse-$pkgver/fuse-$pkgver.tar.gz{,.asc}
	'fuse.conf')
sha1sums=('9cd178d3e5e0f2304d0f86ae096958527d23b098'
          'SKIP'
          '3b42e37a741d4651099225987dc40e7f02a716ad')
validpgpkeys=(ED31791B2C5C1613AF388B8AD113FCAC3C4E599F) # Nikolaus Rath <Nikolaus@rath.org>

build() {
  cd fuse-$pkgver

  export MOUNT_FUSE_PATH=/usr/bin
  ./configure --prefix=/usr --libdir=/usr/lib --enable-lib --enable-util --disable-example
  make
}

package_fuse-common() {
  pkgdesc="Common files for fuse2/3 packages"
  backup=(etc/fuse.conf)

  cd fuse-$pkgver
  make DESTDIR=${pkgdir} install

  install -Dm644 ${srcdir}/fuse.conf ${pkgdir}/etc/fuse.conf

  # static device nodes are handled by udev
  rm -r ${pkgdir}/dev

  # Remove init script in wrong path
  # Don't add our own for now, as fusectl fs oopses on 2.6.18
  rm -r ${pkgdir}/usr/etc/init.d

  # part of fuse3 package
  rm -r ${pkgdir}/usr/{bin,include,lib/{pkgconfig,libfuse3.so*,*.a},share/man/man1/fusermount3.1}
}

package_fuse3() {
  pkgdesc="A library that makes it possible to implement a filesystem in a userspace program."
  depends=('glibc' 'fuse-common')

  cd fuse-$pkgver

  make DESTDIR=${pkgdir} install

  # Remove init script in wrong path
  # Don't add our own for now, as fusectl fs oopses on 2.6.18
  rm -r ${pkgdir}/usr/etc/init.d

  # static device nodes are handled by udev
  rm -r ${pkgdir}/dev

  # part of fuse-common package
  rm -r ${pkgdir}/usr/lib/udev/rules.d
  rm ${pkgdir}/usr/share/man/man8/mount.fuse.8
}
