# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=fsarchiver
pkgver=0.6.17
pkgrel=3
pkgdesc='Safe and flexible file-system backup and deployment tool'
arch=('i686' 'x86_64')
url='http://www.fsarchiver.org/'
license=('GPL2')
depends=('bzip2' 'e2fsprogs' 'lzo2' 'xz' 'libgcrypt')
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz)
sha1sums=('3b1049b674f3be45ecda5a91a584c7ddc3781e6b')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sbindir=/usr/bin
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
