# $Id$
# Maintainer: Tom Gundersen <teg@jklm.no>
# Contributor: AndyRTR <andyrtr@archlinux.org>
# Contributor: Lawrence Lee <valheru@facticius.net>

pkgname=rasqal
epoch=1
pkgver=0.9.30
pkgrel=2
pkgdesc="A free C library that handles Resource Description Framework (RDF) query syntaxes, query construction and query execution returning result bindings"
url="http://librdf.org/rasqal"
license=('GPL' 'LGPL')
arch=('i686' 'x86_64')
depends=('raptor' 'mpfr' 'util-linux')
source=(http://download.librdf.org/source/${pkgname}-${pkgver}.tar.gz)
md5sums=('b12c5f9cfdb6b04efce5a4a186b8416b')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr \
	--disable-static \
	--enable-release
  make
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make -k check || true # still fails some checks
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
