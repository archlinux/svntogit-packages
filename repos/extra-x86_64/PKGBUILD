# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kiconthemes
pkgver=5.7.0
pkgrel=2
pkgdesc='Support for icon themes'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kiconthemes'
license=('LGPL')
depends=('qt5-svg' 'kconfigwidgets' 'kitemviews')
makedepends=('extra-cmake-modules' 'python')
groups=('kf5')
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"
        'b0a6df6fbd9117b41a7f4e3bc861e20fbadb1956.patch')
md5sums=('d41b5a4f68e350b1d36e05c99f060e53'
         'd0b13de27754ed275b0a6ab8d41eb47a')

prepare() {
  mkdir -p build

  cd ${pkgname}-${pkgver}
  # FS#43940
  patch -p1 -i ../b0a6df6fbd9117b41a7f4e3bc861e20fbadb1956.patch
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
