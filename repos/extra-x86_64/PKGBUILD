# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=libphonenumber
pkgver=8.10.15
pkgrel=1
pkgdesc="Google's common library for parsing, formatting, and validating international phone numbers"
url="https://github.com/googlei18n/libphonenumber"
arch=(x86_64)
license=(Apache)
depends=(icu protobuf boost-libs)
makedepends=(boost cmake gtest git 'java-environment>=9')
_commit=2ae18c163a5096eedce60c4b10ae3145e91c2297  # tags/v8.10.15^0
source=("git+$url#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^libphonenumber-\|^v//;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  CXXFLAGS+=" -Wno-error=deprecated-declarations"  # readdir_r deprecation
  CXXFLAGS+=" -Wno-error=unused-variable"  # this is nuts
  cmake -H$pkgname/cpp -Bbuild \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  cmake --build build
}

check() {
  cmake --build build --target tests
}

package() {
  DESTDIR="$pkgdir" cmake --build build --target install
}
