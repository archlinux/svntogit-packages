# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-characters
pkgver=44.0
pkgrel=1
pkgdesc="A character map application"
url="https://gitlab.gnome.org/GNOME/gnome-characters"
arch=(x86_64)
license=(GPL2)
depends=(
  emoji-font
  gjs
  gnome-desktop-4
  gtk4
  libadwaita
  libunistring
)
makedepends=(
  appstream-glib
  git
  gobject-introspection
  gperf
  meson
)
groups=(gnome)
_commit=9bbbe9fc4d62f1d7a642d504beb48df79b2e7ab5  # tags/44.0^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-characters.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
