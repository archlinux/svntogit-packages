# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>

pkgname=xfce4-settings
pkgver=4.10.0
pkgrel=3
pkgdesc="Settings manager for xfce"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('GPL2')
groups=('xfce4')
depends=('exo' 'garcon' 'libxfce4ui' 'libnotify' 'libxklavier'
         'gnome-icon-theme' 'gtk-engines')
makedepends=('intltool')
optdepends=('libcanberra: for sound control')
source=(http://archive.xfce.org/src/xfce/$pkgname/4.10/$pkgname-$pkgver.tar.bz2
        xfce4-settings-4.10.0-fix-gtk3-theme-detection.patch
        xfce4-settings-4.10.0-check-service-name-of-nameownerchanged.patch
        xfce4-settings-xml-4.10.0.patch)
sha256sums=('0843f09ba9673f1d1b5df8dce4a17b63c183a9ba3be75fb6ef99a67fc8c1f77e'
            '28482a3410d587fe051b84080ab5366b4fbe2af8684ee46384ff899b73d3f349'
            '5be974f8fba6ba9dc14e136762cf752fe46eac99f71511d77d910610b5401244'
            '78e68ef2cbf19760707ba24f3d57dd59fbf40a540d50d20e7cc62ba02196aaaa')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # enable gnome icon theme, clearlooks theme and font hinting by default
  # (taken from Fedora)
  patch -Np1 -i "$srcdir/xfce4-settings-xml-4.10.0.patch"

  # https://bugzilla.xfce.org/show_bug.cgi?id=9272
  patch -Np1 -i "$srcdir/xfce4-settings-4.10.0-fix-gtk3-theme-detection.patch"

  # https://bugzilla.xfce.org/show_bug.cgi?id=9273
  patch -Np1 -i "$srcdir/xfce4-settings-4.10.0-check-service-name-of-nameownerchanged.patch"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static \
    --enable-xrandr \
    --enable-xcursor \
    --enable-libnotify \
    --enable-libxklavier \
    --enable-pluggable-dialogs \
    --enable-sound-settings \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
