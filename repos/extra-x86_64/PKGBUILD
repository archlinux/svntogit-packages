# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=signon-plugin-oauth2
_pkgname=signon-oauth2
pkgver=0.23
pkgrel=2
pkgdesc='OAuth 2 plugin for signon'
arch=(i686 x86_64)
url="https://gitlab.com/accounts-sso/signon-plugin-oauth2"
license=(LGPL)
depends=(signon)
makedepends=(qt5-xmlpatterns)
source=("$pkgname-$pkgver.tar.gz::https://gitlab.com/accounts-sso/signon-plugin-oauth2/repository/archive.tar.gz?ref=VERSION_$pkgver")
md5sums=('772197567b8778552bba34ace41aec7d')

prepare() {
  cd signon-plugin-oauth2-VERSION_$pkgver-*

  sed -i '/-Werror/d' common-project-config.pri

  # Do not install tests and example
  echo 'INSTALLS =' >>tests/tests.pro
  echo 'INSTALLS =' >>example/example.pro
}

build() {
  cd signon-plugin-oauth2-VERSION_$pkgver-*

  qmake-qt5 PREFIX=/usr LIBDIR=/usr/lib
  make
}

package() {
  cd signon-plugin-oauth2-VERSION_$pkgver-*

  make INSTALL_ROOT="$pkgdir" install
}

