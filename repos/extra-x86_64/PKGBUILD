# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=mailcommon
pkgver=20.08.1
pkgrel=1
pkgdesc="KDE PIM library providing support for mail applications"
arch=(x86_64)
url="https://kontact.kde.org"
license=(LGPL)
depends=(mailimporter messagelib phonon-qt5)
makedepends=(extra-cmake-modules boost qt5-tools)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('d4ee996f74a749ad626ca5029821546da82dbfc1c81864ff39b5f51d6dcf1d88'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
