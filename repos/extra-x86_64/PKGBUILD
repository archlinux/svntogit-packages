# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-calendar
pkgver=41.2+r5+g2222a4ee
pkgrel=1
pkgdesc="Simple and beautiful calendar application designed to perfectly fit the GNOME desktop"
url="https://wiki.gnome.org/Apps/Calendar"
arch=(x86_64)
license=(GPL)
depends=(evolution-data-server gsettings-desktop-schemas gnome-control-center
         libdazzle libhandy)
makedepends=(git meson)
groups=(gnome)
_commit=2222a4ee55fc8f42bdc0f833e1a37a94f7ddc2bf  # gnome-41
source=("git+https://gitlab.gnome.org/GNOME/gnome-calendar.git#commit=$_commit"
        reduce-location-accuracy.diff)
sha256sums=('SKIP'
            '190f627d7a518f9104d54a4a15f3d6c8b14a08472ef3b28e63d15328d7e106e1')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname

  # Fix build with Meson 0.61
  git cherry-pick -n e35dbc1acd03dd1751b755b6c8e1932a692ad4e1

  # Reduce accuracy of location requests
  git apply -3 ../reduce-location-accuracy.diff
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 et:
