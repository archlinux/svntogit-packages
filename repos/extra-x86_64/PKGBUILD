# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>

pkgname=libxshmfence
pkgver=1.0
pkgrel=1
pkgdesc="a library that exposes a event API on top of Linux futexes"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org/"
license=('GPL')
#groups=()
depends=('glibc')
makedepends=('xorg-util-macros')
source=(${url}/releases/individual/lib/${pkgname}-${pkgver}.tar.bz2
        COPYING)
sha256sums=('625089553939baa20e665873a6c8f3d694cfde78fb3110aa59da92fd2b14f14f'
            '5e46864ede2fda46c3cb7cfe3a7b9f9e4bd36d8d21d8010a284caa64b81d465a')

build() {
	cd $pkgname-$pkgver
	
	# fix some libtools errors
	autoreconf -vfi
	
	./configure --prefix=/usr
	make
}

check() {
	cd $pkgname-$pkgver
	make -k check
}

package() {
	cd $pkgname-$pkgver
	make DESTDIR="$pkgdir/" install
	
    # license is still missing
    # COPYING is taken from top of /usr/include/X11/xshmfence.h
    install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
    install -m644 ${srcdir}/COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"

}
