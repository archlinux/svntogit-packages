# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=hitori
pkgver=44.0
pkgrel=1
pkgdesc="GTK+ application to generate and let you play games of Hitori"
url="https://wiki.gnome.org/Apps/Hitori"
arch=(x86_64)
license=(GPL)
depends=(gtk3)
makedepends=(
  appstream-glib
  git
  meson
  yelp-tools
)
groups=(gnome-extra)
_commit=53db1397d6a711862023d7a3070e785235a98c87  # tags/44.0^0
source=("git+https://gitlab.gnome.org/GNOME/hitori.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd hitori
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd hitori
}

build() {
  arch-meson hitori build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
