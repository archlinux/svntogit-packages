# $Id$
# Contributor: dorphell <dorphell@archlinux.org>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=libao
pkgver=1.1.0
pkgrel=2
pkgdesc='Cross-platform audio output library and plugins'
url='http://xiph.org/ao/'
arch=('i686' 'x86_64')
license=('GPL')
options=('!libtool')
depends=('alsa-lib')
makedepends=('libpulse')
backup=('etc/libao.conf')
source=("http://downloads.xiph.org/releases/ao/${pkgname}-${pkgver}.tar.gz"
        'libao.conf')
sha1sums=('9301bc4886f170c7122ab62677fb71cf001c04fd'
          '558b3d297e0956af6959565db477023dbd492ca0')

conflicts=('libao-pulse')
provides=("libao-pulse=${pkgver}-${pkgrel}")
replaces=('libao-pulse')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	./configure \
		--prefix=/usr \
		--enable-alsa-mmap \

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	install -Dm644 "${srcdir}"/libao.conf "${pkgdir}"/etc/libao.conf
}
