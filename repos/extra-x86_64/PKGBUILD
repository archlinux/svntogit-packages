# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-robots
pkgver=3.36.1
pkgrel=1
pkgdesc="Avoid the robots and make them crash into each other"
url="https://wiki.gnome.org/Apps/Robots"
arch=(x86_64)
license=(GPL)
depends=(gtk3 gsound librsvg libgnome-games-support)
makedepends=(meson gobject-introspection yelp-tools appstream-glib git)
groups=(gnome-extra)
_commit=bd3fb762f80052036159b53dfd0505277707d337  # tags/3.36.1^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-robots.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
