# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-xauth
pkgver=1.0.6
pkgrel=1
pkgdesc="X.Org authorization settings program"
arch=(i686 x86_64)
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('libxmu')
makedepends=('xorg-util-macros')
groups=('xorg-apps' 'xorg')
source=(${url}/releases/individual/app/xauth-${pkgver}.tar.bz2)
sha1sums=('10a68367ecc60a641b9aa89e6f24f7ca71f23874')

build() {
  cd "${srcdir}/xauth-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/xauth-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
