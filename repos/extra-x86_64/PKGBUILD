# $Id$
# Maintainer:
# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgbase=lpeg
pkgname=(lua-lpeg lua51-lpeg)
pkgver=0.12
pkgrel=2
arch=('i686' 'x86_64')
url='http://www.inf.puc-rio.br/~roberto/lpeg'
license=('MIT')
makedepends=('lua' 'lua51')
source=(http://www.inf.puc-rio.br/~roberto/$pkgbase/$pkgbase-$pkgver.tar.gz
        lpeg-0.12-makefile.patch
        LICENSE)
md5sums=('4abb3c28cd8b6565c6a65e88f06c9162'
         '75ea7deffd790f482c9473ff2e9e1cdf'
         'dccb97b431c3a3ccaacc216c6242e9fd')

prepare() {
  pushd $pkgbase-$pkgver
  patch -p1 -i ../lpeg-0.12-makefile.patch
  popd

  cp -r $pkgbase-$pkgver $pkgbase-$pkgver-51
}

build() {
  cd $pkgbase-$pkgver
  make LUADIR=/usr/include

  cd "$srcdir"/$pkgbase-$pkgver-51
  make LUADIR=/usr/include/lua5.1
}

package_lua-lpeg() {
  pkgdesc='Pattern-matching library for Lua 5.2'
  depends=('lua')

  cd $pkgbase-$pkgver
  install -Dm755 $pkgbase.so "$pkgdir"/usr/lib/lua/5.2/$pkgbase.so
  install -Dm644 re.lua "$pkgdir"/usr/share/lua/5.2/re.lua
  install -Dm644 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_lua51-lpeg() {
  pkgdesc='Pattern-matching library for Lua 5.1'
  depends=('lua51')

  cd $pkgbase-$pkgver-51
  install -Dm755 $pkgbase.so "$pkgdir"/usr/lib/lua/5.1/$pkgbase.so
  install -Dm644 re.lua "$pkgdir"/usr/share/lua/5.1/re.lua
  install -Dm644 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
