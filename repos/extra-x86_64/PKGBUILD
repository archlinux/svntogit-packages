# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=jack-example-tools
pkgver=2
pkgrel=1
pkgdesc="Official JACK example clients and tools"
arch=(x86_64)
url="https://github.com/jackaudio/jack-example-tools"
license=(GPL2 GPL3)
groups=(pro-audio)
depends=(glibc opus)
makedepends=(alsa-lib git jack2 libsamplerate libsndfile meson readline
zita-alsa-pcmi zita-resampler)
options=(debug)
source=(git+https://github.com/jackaudio/jack-example-tools.git#tag=$pkgver?signed)
sha256sums=('SKIP')
validpgpkeys=('62B11043D2F6EB6672D93103CDBAA37ABC74FBA0') # falkTX <falktx@falktx.com>

build() {
  arch-meson build $pkgname "${_meson_args[@]}"
  ninja -vC build
}

package() {
  depends+=(libasound.so libjack.so libsamplerate.so libsndfile.so
  libreadline.so libzita-alsa-pcmi.so libzita-resampler.so)

  DESTDIR="$pkgdir" meson install -C build
}
