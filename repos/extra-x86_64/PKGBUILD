# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=p11-kit
pkgver=0.18.2
pkgrel=1
pkgdesc="Library to work with PKCS#11 modules"
arch=(i686 x86_64)
url="http://p11-glue.freedesktop.org"
license=('BSD')
depends=('glibc' 'libtasn1')
options=(!libtool)
source=($url/releases/$pkgname-$pkgver.tar.gz{,.sig})
md5sums=('7bb1703f38fb778054bf57477c78dc8f'
         'SKIP')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
    --with-module-path=/usr/lib/pkcs11
  make
}

package() {
  cd  $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING $pkgdir/usr/share/licenses/$pkgname/COPYING
}

# vim:set ts=2 sw=2 et:
