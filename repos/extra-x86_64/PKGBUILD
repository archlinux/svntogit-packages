# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=networkmanager-vpnc
pkgver=1.0.6
pkgrel=1
pkgdesc="NetworkManager VPN plugin for vpnc"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnome.org/projects/NetworkManager/"
depends=('libnm-gtk' 'vpnc' 'hicolor-icon-theme' 'gtk3' 'libsecret')
makedepends=('intltool')
optdepends=('network-manager-applet: GNOME frontends to NetWorkmanager')
install=networkmanager-vpnc.install
source=(http://ftp.gnome.org/pub/GNOME/sources/NetworkManager-vpnc/${pkgver::3}/NetworkManager-vpnc-${pkgver}.tar.xz)
sha256sums=('538298d72845863bf16435940333d30a32a3b28b03b67f9f46c8a0c32cbdcc43')

build() {
  cd NetworkManager-vpnc-${pkgver}
  ./configure --prefix=/usr \
  	--sysconfdir=/etc \
	--localstatedir=/var \
	--libexecdir=/usr/lib/networkmanager \
	--enable-more-warnings=yes \
	--disable-static
  make
}

package() {
  cd NetworkManager-vpnc-${pkgver}
  make DESTDIR="${pkgdir}" install
}
