# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=telepathy-kde-send-file
_pkgname=ktp-send-file
pkgver=0.7.1
pkgrel=1
pkgdesc="A File manager plugin to launch a file transfer job with a specified contact"
arch=('i686' 'x86_64')
url="http://community.kde.org/Real-Time_Communication_and_Collaboration"
license=('GPL')
depends=('telepathy-kde-filetransfer-handler' 'telepathy-kde-contact-list')
makedepends=('cmake' 'automoc4')
groups=('kde-telepathy')
install=${pkgname}.install
source=("http://download.kde.org/stable/kde-telepathy/${pkgver}/src/${_pkgname}-${pkgver}.tar.bz2")
sha1sums=('a1b029ea0ce0f43fcbba5e6e395270c073fc668b')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../${_pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
