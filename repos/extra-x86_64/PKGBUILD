# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=libqaccessibilityclient
pkgver=0.2.0
pkgrel=3
arch=(x86_64)
pkgdesc="Helper library to make writing accessibility tools easier"
url='https://www.kde.org'
license=(LGPL2.1)
depends=(qt5-base)
makedepends=(cmake)
conflicts=(libkdeaccessibilityclient)
source=("https://download.kde.org/unstable/libqaccessibilityclient/$pkgname-$pkgver.tar.xz"{,.sig}
        libqaccessibilityclient-gcc8.patch::"https://cgit.kde.org/libqaccessibilityclient.git/patch/?id=34f6084f")
sha256sums=('d5e8f4dc91bc1b56029e12328a9e02d5c310939b01998f030202203c5edb2e61'
            'SKIP'
            '8df173dd87d055e978edd84e8f95efe820de1bad03f74c16c0d158e830e68331')
validpgpkeys=(FB25861861063C64C97B89AE125C53EB96EC0C92) # Frederik Gladhorn <frederik@gladhorn.de>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../libqaccessibilityclient-gcc8.patch # Fix build with GCC 8
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
