# $Id$
# Maintainer:
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Yejun Yang <yejunx AT gmail DOT com>
# Contributor: Michal Krenek <mikos@sg1.cz>

pkgname=libsrtp
pkgver=15.1c9bd90
pkgrel=1
pkgdesc="Open-source implementation of the Secure Real-time Transport Protocol (SRTP)"
url="http://srtp.sourceforge.net/srtp.html"
arch=('i686' 'x86_64')
license=('BSD')
depends=('glibc')
makedepends=('git')
source=("git://git.linphone.org/srtp.git#commit=1c9bd90")
md5sums=('SKIP')

pkgver() {
  cd srtp
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd srtp
  autoconf
  ./configure --prefix=/usr
  make
}

package() {
  cd srtp
  make DESTDIR="${pkgdir}" install

  install -Dm0644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
