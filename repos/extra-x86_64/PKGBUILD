# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=audacious
pkgver=4.0.2
pkgrel=1
pkgdesc="Lightweight, advanced audio player focused on audio quality"
arch=('x86_64')
url="https://audacious-media-player.org/"
license=('BSD')
depends=('qt5-base' 'glib2' 'libguess' 'libsm' 'audacious-plugins'
         'hicolor-icon-theme' 'desktop-file-utils')
makedepends=('python') # for gdbus-codegen
optdepends=('unzip: zipped skins support')
provides=('audacious-player')
conflicts=('audacious-player')
replaces=('audacious-player')
source=(https://distfiles.audacious-media-player.org/$pkgname-$pkgver.tar.bz2)
sha256sums=('18a52bd9f1f96d17bf75ad6d40b0602fc29899ef2ccd9d2a6729dfa5fc7f5a67')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --with-buildstamp='Arch Linux'
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
