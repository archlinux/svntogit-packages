# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=gdk-pixbuf2
pkgver=2.34.0
pkgrel=2
pkgdesc="An image loading library"
arch=('i686' 'x86_64')
url="http://www.gtk.org/"
license=('LGPL2.1')
depends=('glib2' 'libpng' 'libtiff' 'libjpeg' 'libx11' 'jasper')
makedepends=('gtk-doc' 'gobject-introspection')
install=gdk-pixbuf2.install
source=(http://download.gnome.org/sources/gdk-pixbuf/${pkgver:0:4}/gdk-pixbuf-$pkgver.tar.xz
        gdk-pixbuf-query-loaders.hook)
sha256sums=('d55e5b383ee219bd0e23bf6ed4427d56a7db5379729a6e3e0a0e0eba9a8d8879'
            '963afcfacde9fd36031d93c8aa4c94a669ab1dbe9ba0d2cf17599b7d358e54c2')

build() {
    cd gdk-pixbuf-$pkgver
    ./configure --prefix=/usr \
        --with-x11 \
        --with-libjasper \
        --with-included-loaders=png
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
    make
}

package() {
    cd gdk-pixbuf-$pkgver
    make DESTDIR="$pkgdir" install
    install -Dm644 ../gdk-pixbuf-query-loaders.hook "$pkgdir/usr/share/libalpm/hooks/gdk-pixbuf-query-loaders.hook"
}

# vim:set et sw=4:
