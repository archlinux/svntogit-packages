# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Andrew Wright <andreww@photism.org>

pkgname=wxpython
pkgver=2.8.11.0
pkgrel=1
pkgdesc="A wxWidgets GUI toolkit for Python"
arch=('i686' 'x86_64')
license=('custom:wxWindows')
url="http://www.wxpython.org"
depends=('wxgtk' 'python')
makedepends=('mesa')
source=(http://downloads.sourceforge.net/wxpython/wxPython-src-${pkgver}.tar.bz2 editra-aui.diff)
md5sums=('63f73aae49e530852db56a31b57529fa' '06c637c34c1570d8c82b870ce19c0622')
sha1sums=('18982edc236a1d87e4eef018928358c925aa5b17' '077fe341bf8822647934d6b1ea82ce9b0adbf93a')

build() {
  cd "${srcdir}/wxPython-src-${pkgver}"
  patch -p1 < ../editra-aui.diff || return 1
  ./configure --prefix=/usr --libdir=/usr/lib --with-gtk=2 --with-opengl --enable-unicode \
    --enable-graphics_ctx --with-gnomeprint --disable-optimize --enable-mediactrl \
    --with-libpng=sys --with-libxpm=sys --with-libjpeg=sys --with-libtiff=sys || return 1
  cd "${srcdir}/wxPython-src-${pkgver}/wxPython"
  python setup.py WXPORT=gtk2 UNICODE=1 build || return 1
}

package() {
  cd "${srcdir}/wxPython-src-${pkgver}/wxPython"
  python setup.py WXPORT=gtk2 UNICODE=1 install --root="${pkgdir}" || return 1
  install -D -m644 ../docs/licence.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE" || return 1
}
