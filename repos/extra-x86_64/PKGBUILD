# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=eog
pkgver=3.20.1
pkgrel=1
pkgdesc="Eye of Gnome: An image viewing and cataloging program"
arch=(i686 x86_64)
license=(GPL)
depends=(gnome-desktop libexif lcms2 desktop-file-utils exempi libpeas librsvg dconf)
makedepends=(intltool itstool gobject-introspection)
install=eog.install
groups=(gnome)
options=('!emptydirs')
url="https://wiki.gnome.org/Apps/EyeOfGnome"
source=(https://download.gnome.org/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('4f021988f1b24b4d575f85fc789596194a6e890af3fa9b2b8e4f493fc3df8352')

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
