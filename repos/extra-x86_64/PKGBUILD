# Maintainer: Daniel Isenmann <daniel@archlinux.org>

pkgname=gegl
pkgver=0.4.12
pkgrel=1
pkgdesc="Graph based image processing framework"
arch=('x86_64')
url="http://www.gegl.org/"
license=('GPL3' 'LGPL3')
depends=('babl' 'libspiro' 'json-glib' 'libgexiv2' 'libraw' 'openexr' 'librsvg' 'libtiff' 'lua' 'jasper' 'lensfun' 'suitesparse')
makedepends=('intltool' 'ruby' 'mesa' 'glu' 'ffmpeg' 'exiv2' 'vala' 'python2' 'gobject-introspection' 'git')
optdepends=('ffmpeg: ffmpeg plugin')
_commit=59c7371ee04b98908fe3047a47abe18b01c1c6eb  # tags/GEGL_0_4_12^0
source=("git+https://gitlab.gnome.org/GNOME/gegl.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^GEGL_//;s/_/./g;s/-/+/g'
}

prepare() {
  mkdir path
  ln -s /usr/bin/python2 path/python

  cd ${pkgname}
  git cherry-pick -n c1c3541caab485384087bbbcf1c83ef2d5bd4d43
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  export PATH="$srcdir/path:$PATH" PYTHON=/usr/bin/python2
  cd ${pkgname}
  ./configure  --prefix=/usr  --with-sdl --with-librsvg \
    --with-libavformat --with-jasper --disable-docs \
    --enable-workshop

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

check() {
  export PATH="$srcdir/path:$PATH" PYTHON=/usr/bin/python2
  cd ${pkgname}
  make -k check || :
}

package() {
  export PATH="$srcdir/path:$PATH" PYTHON=/usr/bin/python2
  cd ${pkgname}
  make DESTDIR="${pkgdir}" install
}
