# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Merk Matthias <macem@chello.at>

pkgname=xfce4-sensors-plugin
pkgver=1.0.0
pkgrel=2
pkgdesc="A lm_sensors plugin for the Xfce panel"
arch=('i686' 'x86_64')
license=('GPL2')
url="http://xfce-goodies.berlios.de/"
groups=('xfce4-goodies')
depends=('xfce4-panel>=4.6.0' 'lm_sensors>=3.1.0' 'libnotify' 'hicolor-icon-theme')
makedepends=('pkgconfig' 'intltool' 'hddtemp>=0.3.beta15.45-2' 'gnu-netcat')
optdepends=('gnu-netcat: for hddtemp access')
options=(!libtool)
install=${pkgname}.install
source=(http://archive.xfce.org/src/panel-plugins/${pkgname}/1.0/${pkgname}-${pkgver}.tar.bz2)
md5sums=('74d83628246536d575f954c76724982b')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc \
	--libexecdir=/usr/lib --localstatedir=/var \
	--disable-static --with-pathhddtemp=/usr/sbin/hddtemp
  make || return 1
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install || return 1
}
