# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>

pkgbase=qscintilla
pkgname=('qscintilla-qt5' 'python-qscintilla-qt5')
pkgver=2.11.6
pkgrel=1
license=('GPL')
arch=('x86_64')
url="https://www.riverbankcomputing.com/software/qscintilla/intro"
makedepends=('python-pyqt5' 'qt5-tools' 'sip5' 'pyqt-builder')
source=("https://www.riverbankcomputing.com/static/Downloads/QScintilla/$pkgver/QScintilla-$pkgver.tar.gz")
sha256sums=('e7346057db47d2fb384467fafccfcb13aa0741373c5d593bc72b55b2f0dd20a7')

build() {
  cd QScintilla-${pkgver}
  export QMAKEFEATURES=$PWD/Qt4Qt5/features/

  cd Qt4Qt5
  qmake-qt5
  make

  cd ../designer-Qt4Qt5
  qmake-qt5 INCLUDEPATH+=../Qt4Qt5 QMAKE_LIBDIR+=../Qt4Qt5
  make

  cd ..
  sip-build \
    --no-make \
    --qsci-features-dir Qt4Qt5/features \
    --qsci-include-dir Qt4Qt5 \
    --qsci-library-dir Qt4Qt5 \
    --api-dir /usr/share/qt/qsci/api/python
  cd build
  make
}

package_qscintilla-qt5() {
  pkgdesc="A port to Qt5 of Neil Hodgson's Scintilla C++ editor class"
  depends=(qt5-base)

  cd QScintilla-${pkgver}/Qt4Qt5
  make DESTDIR="${pkgdir}" INSTALL_ROOT="${pkgdir}" install

  cd ../designer-Qt4Qt5
  make DESTDIR="${pkgdir}" INSTALL_ROOT="${pkgdir}" install
}

package_python-qscintilla-qt5() {
  pkgdesc="Python bindings for QScintilla2"
  depends=(qscintilla-qt5 python-pyqt5)
  replaces=(python-qscintilla-qt5-common)

  cd QScintilla-${pkgver}/build
  make INSTALL_ROOT="$pkgdir" install -j1
}
