# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Laurent Carlier <lordheavym@gmail.com>

pkgname=telepathy-kde-text-ui
_pkgname=ktp-text-ui
pkgver=0.8.0.1
_pkgver=0.8.0
pkgrel=1
pkgdesc="Telepathy handler for Text Chats"
arch=('i686' 'x86_64')
url="http://community.kde.org/Real-Time_Communication_and_Collaboration"
license=('GPL')
depends=('telepathy-kde-contact-list' 'qjson')
makedepends=('cmake' 'automoc4' 'boost')
groups=('kde-telepathy')
source=("http://download.kde.org/stable/kde-telepathy/${_pkgver}/src/${_pkgname}-${pkgver}.tar.bz2")
sha1sums=('f10f79cc7a19def9a4d4db5e6f9664a46c8101a3')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../${_pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
