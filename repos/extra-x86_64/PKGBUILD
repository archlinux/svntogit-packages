# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=prrte
pkgver=2.0.2
pkgrel=1
pkgdesc="PMIx Reference RunTime Environment"
arch=(x86_64)
url="https://github.com/openpmix/prrte"
license=(BSD)
depends=(glibc hwloc libevent openpmix)
makedepends=(perl)
provides=(libprrte.so)
options=(debug)
source=($pkgname-$pkgver.tar.gz::https://github.com/openpmix/$pkgname/releases/download/v$pkgver/prte-$pkgver.tar.gz)
sha512sums=('b36ee5dfa7c09ca9b0384183d3aa09f959df394fe5a3da191485472ffacdaa70e1ee323f8092b9b2d83730c91524d98cc5866a5c3bf33b7374b696dfb8555e4e')
b2sums=('bf71efb9a0be064e4e07e20467dbe1f299c87bcccee6fc396341d42146cddfc653d4a38699f316df5936f3966c7ef79f808cab51bb669a9d5efe6879a41805b2')

prepare() {
  cd $pkgname-$pkgver
  ./autogen.pl
}

build() {
  cd $pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc/$pkgname
  # prevent excessive overlinking due to libtool
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make V=1
}

check() {
  make -k check -C $pkgname-$pkgver
}

package() {
  depends+=(libpmix.so)

  make DESTDIR="$pkgdir" install -C $pkgname-$pkgver
  install -vDm 644 $pkgname-$pkgver/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
