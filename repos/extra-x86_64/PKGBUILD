# Maintainer: Laurent Carlier <lordheavym@gmail.com>

pkgname=vulkan-icd-loader
pkgver=1.1.85+2969+5abee6173
pkgrel=1
arch=(x86_64)
pkgdesc="Vulkan Installable Client Driver (ICD) Loader"
url="https://www.khronos.org/vulkan/"
license=('custom')
makedepends=(cmake python-lxml libx11 libxrandr wayland vulkan-headers git)
depends=(glibc)
optdepends=('vulkan-driver: packaged vulkan driver') # vulkan-driver: vulkan-intel/vulkan-radeon/nvidia-utils/....
#source=("https://github.com/KhronosGroup/Vulkan-Loader/archive/sdk-${pkgver}.tar.gz"
source=("git+https://github.com/KhronosGroup/Vulkan-Loader.git#commit=5abee6173f9656e881f859f7f823568a4ce19f0e")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/Vulkan-Loader"

  echo 1.1.85+$(git rev-list --count HEAD)+$(git rev-parse --short HEAD)
}

build() {
  cd "${srcdir}/Vulkan-Loader"

  rm -rf build ; mkdir build ; cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_SYSCONFDIR=/etc \
    -DCMAKE_INSTALL_DATADIR=/share \
    -DCMAKE_SKIP_RPATH=True \
    -DBUILD_TESTS=Off \
    -DBUILD_WSI_XCB_SUPPORT=On \
    -DBUILD_WSI_XLIB_SUPPORT=On \
    -DBUILD_WSI_WAYLAND_SUPPORT=On \
    -DBUILD_WSI_MIR_SUPPORT=Off \
    -DCMAKE_BUILD_TYPE=Release \
    ..
  make
}

package() {
  cd "${srcdir}/Vulkan-Loader/build"
  
  make DESTDIR="${pkgdir}" install
  
  install -dm755 ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 ../LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/
}
