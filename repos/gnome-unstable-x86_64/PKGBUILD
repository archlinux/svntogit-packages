# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-calculator
pkgver=3.13.92
pkgrel=1
pkgdesc="GNOME Scientific calculator"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gtk3' 'dconf' 'gtksourceview3')
makedepends=('intltool' 'yelp-tools' 'vala')
groups=('gnome')
replaces=('gcalctool')
conflicts=('gcalctool')
options=(!emptydirs)
url="http://www.gnome.org"
install=gnome-calculator.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver:0:4}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('558b6285301f46e15cde9a06a40885b562c90f60415ce428d9b48d21728bab82')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --disable-schemas-compile \
      --localstatedir=/var --libexecdir=/usr/lib/gnome-calculator
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="${pkgdir}" install
}
