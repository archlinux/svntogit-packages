# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-terminal
pkgver=2.30.0
pkgrel=1
pkgdesc="The GNOME Terminal Emulator"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gconf>=2.28.0' 'vte>=0.24.0' 'libgnome' 'libsm')
makedepends=('pkgconfig' 'gnome-doc-utils>=0.19.5' 'intltool') 
options=('!emptydirs')
url="http://www.gnome.org"
groups=('gnome-extra')
install=gnome-terminal.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.30/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('989e251a8407952b8c42fa74c8049a485504dfe3c82663946c7b538199b0c5d5')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-scrollkeeper || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1
  
  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gnome-terminal ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
