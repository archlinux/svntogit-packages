# $Id$
# Maintainer:  Ionut Biru <ibiru@archlinux.org>
# Contributor: Kessia 'even' Pinheiro <kessiapinheiro at gmail.com
# Contributor: Bjorn Lindeijer <bjorn lindeijer nl>

pkgname=telepathy-glib
pkgver=0.20.0
pkgrel=1
pkgdesc="GLib bindings for the Telepathy D-Bus protocol"
arch=('i686' 'x86_64')
url="http://telepathy.freedesktop.org"
license=('LGPL2.1')
options=('!libtool' '!emptydirs')
depends=('dbus-glib')
makedepends=('libxslt' 'vala' 'gobject-introspection')
source=(http://telepathy.freedesktop.org/releases/$pkgname/$pkgname-$pkgver.tar.gz)
md5sums=('9d7b2bcc2d9f8e0352c8789952717d83')

build() {
    cd $pkgname-$pkgver
    PYTHON=/usr/bin/python2 ./configure --prefix=/usr \
        --libexecdir=/usr/lib/telepathy \
        --enable-vala-bindings \
        --enable-static=no
    make
}

package() {
    cd $pkgname-$pkgver
    make DESTDIR="$pkgdir" install
}
