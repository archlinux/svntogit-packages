# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=grilo-plugins
pkgver=0.2.0
pkgrel=1
pkgdesc="Plugins for Grilo"
url="http://www.gnome.org"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('grilo')
makedepends=(gupnp-av libgdata libquvi sqlite gmime libgcrypt rest libtracker-sparql
             totem-plparser gnome-common)
optdepends=('gupnp-av: uPnP plugin'
            'libgdata: Youtube plugin'
            'libquvi: Youtube plugin'
            'sqlite: Podcasts plugin'
            'gmime: Podcasts plugin'
            'sqlite: Bookmarks plugin'
            'sqlite: Metadata store plugin'
            'libgcrypt: Vimeo plugin'
            'rest: Blip.tv plugin'
            'libtracker-sparql: Tracker plugin'
            'totem-plparser: Optical media plugin')
options=('!libtool' '!emptydirs')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('86ecabe5b34a15e9c00285b599461e7eb590e2fff0a8b284157b789b192db4f7')

build() {
  cd $pkgname-$pkgver
  
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static \
    --enable-shoutcast
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
