# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=gnome-online-accounts
pkgver=3.48.0
pkgrel=1
pkgdesc="Single sign-on framework for GNOME"
url="https://wiki.gnome.org/Projects/GnomeOnlineAccounts"
arch=(x86_64)
license=(LGPL)
depends=(
  gcr
  json-glib
  krb5
  libnotify
  librest
  libsecret
  webkit2gtk-4.1
)
makedepends=(
  git
  gobject-introspection
  gtk-doc
  meson
  vala
)
optdepends=(
  'gvfs-goa: Virtual file systems, e.g. OwnCloud'
  'gvfs-google: Google Drive'
)
provides=(
  libgoa-1.0.so
  libgoa-backend-1.0.so
)
_commit=ebab108d4d8505b56cc313acad341520ed18df8f  # tags/3.48.0^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-online-accounts.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  local meson_options=(
    -D gtk_doc=true
    -D man=true
    -D media_server=true
  )

  arch-meson $pkgname build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
