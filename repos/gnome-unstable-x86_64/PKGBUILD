# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=grilo-plugins
pkgver=0.3.3+7+gab5596b
pkgrel=1
pkgdesc="A collection of plugins for the Grilo framework"
url="https://git.gnome.org/browse/grilo-plugins"
arch=(i686 x86_64)
license=(LGPL)
depends=(grilo lua libmediaart libgdata libtracker-sparql gom libdmapsharing chromaprint
         gst-plugins-bad)
makedepends=(intltool itstool yelp-tools git gnome-common python)
optdepends=('dleyna-server: DLNA plugin'
            'tracker: Tracker plugin')
groups=(gnome)
_commit=ab5596b7372a3d9b4a74fc3ff4daa669797a6c5a
source=("git://git.gnome.org/grilo-plugins#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^grilo-plugins-//;s/-/+/g'
}

prepare() {
  cd $pkgname
  # Add missing tag for pkgver()
  git tag -f grilo-plugins-0.3.3 7a29a3ba6b0874e39fc664407cfcb2298906163a
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd $pkgname
  make -k check || :
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
