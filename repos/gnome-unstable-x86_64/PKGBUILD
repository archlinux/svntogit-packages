# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=gnome-2048
pkgver=3.38.2
pkgrel=2
pkgdesc="Obtain the 2048 tile"
url="https://wiki.gnome.org/Apps/2048"
arch=(x86_64)
license=(GPL3)
depends=(
  clutter-gtk
  libgee
  libgnome-games-support
)
makedepends=(
  appstream-glib
  git
  meson
  vala
  yelp-tools
)
groups=(gnome-extra)
_commit=82f155f326a9e1973d7283bfa3f7cbb756a9984a  # tags/3.38.2^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-2048.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname

  # Fix build with newer meson
  git cherry-pick -n 194e22699f7166a016cd39ba26dd719aeecfc868
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
