# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Michel Brabants <michel.linux@tiscali.be>

pkgname=seahorse
pkgver=43.0
pkgrel=2
epoch=1
pkgdesc="GNOME application for managing PGP keys."
url="https://wiki.gnome.org/Apps/Seahorse"
arch=(x86_64)
license=(GPL)
depends=(
  gcr
  gpgme
  gtk3
  libhandy
  libpwquality
  libsecret
  libsoup3
  org.freedesktop.secrets
)
makedepends=(
  git
  gobject-introspection
  libldap
  meson
  vala
  yelp-tools
)
provides=(x11-ssh-askpass)
groups=(gnome-extra)
_commit=5303f3ff9b3ce69c7e88ee4678328b8ce892ccdb  # tags/43.0^0
source=("git+https://gitlab.gnome.org/GNOME/seahorse.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd seahorse
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd seahorse
}

build() {
  arch-meson seahorse build -D manpage=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
