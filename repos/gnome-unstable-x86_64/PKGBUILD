# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=vino
pkgver=3.12.0
pkgrel=1
pkgdesc="a VNC server for the GNOME desktop"
arch=(i686 x86_64)
license=(GPL)
depends=(libnotify libxtst libsm telepathy-glib gtk3 libsecret avahi desktop-file-utils gnutls)
makedepends=(intltool networkmanager gnome-common)
groups=(gnome)
url="http://www.gnome.org"
options=(!emptydirs)
install=vino.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('86c9d8b60d79982e4488815db0d441c398e011ad8262659789afecc97a01ca5b')

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/vino \
      --localstatedir=/var \
      --disable-http-server --with-secret

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
