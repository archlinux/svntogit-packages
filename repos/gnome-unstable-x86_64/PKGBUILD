# $Id$
# Maintainer: tobias <tobias funnychar archlinux.org>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>

pkgname=xfce4-settings
pkgver=4.6.4
pkgrel=2
pkgdesc="Settings manager for xfce"
arch=('i686' 'x86_64')
license=('GPL2')
url="http://www.xfce.org/"
groups=('xfce4')
depends=('exo>=0.3.106' 'libnotify>=0.4.5' 'libxfcegui4>=4.6.1' 'libxklavier>=5.0' 'libwnck>=2.30.0')
makedepends=('intltool')
optdepends=('libcanberra: for sound control')
conflicts=('xfce-mcs-manager')
replaces=('xfce-mcs-manager')
source=(http://archive.xfce.org/src/xfce/${pkgname}/4.6/${pkgname}-${pkgver}.tar.bz2
        xklavier-5.patch)
md5sums=('04985407e8e5b916c44780314a177e96'
         '41306a7a8f7117f5a1ec8bf8207878e5')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -Np1 -i "${srcdir}/xklavier-5.patch" || return 1
  autoheader || return 1
  autoconf || return 1
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --enable-libxklavier --enable-sound-settings || return 1
  make || return 1
  make DESTDIR=${pkgdir} install || return 1
}
