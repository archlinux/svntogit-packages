# $Id$
# Maintainer:  Ionut Biru <ibiru@archlinux.org>
# Contributor: Ju Liu <liuju86 at gmail dot com>
# Contributor: Bjorn Lindeijer <bjorn lindeijer nl>
# Contributor: Andreas Zwinkau <beza1e1@web.de>
pkgname=telepathy-mission-control
pkgver=5.7.5
pkgrel=1
pkgdesc="A Telepathy component providing abstraction of some of the details of connection managers."
arch=('i686' 'x86_64')
url="http://mission-control.sourceforge.net/"
license=('LGPL2.1')
depends=('telepathy-glib' 'libgnome-keyring')
makedepends=('libxslt' 'python2')
options=('!libtool')
source=(http://telepathy.freedesktop.org/releases/telepathy-mission-control/$pkgname-$pkgver.tar.gz)
md5sums=('7a0ec83fd7f780b97f6078477f97cc9a')

build() {
    cd "$srcdir/$pkgname-$pkgver"
    ./configure --prefix=/usr \
    	--libexecdir=/usr/lib/telepathy \
	--enable-gnome-keyring
    make
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    make DESTDIR="$pkgdir" install
}
