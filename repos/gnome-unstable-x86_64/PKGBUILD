# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Roman Kyrylych <roman@archlinux.org>

pkgname=gnome-bluetooth-3.0
pkgver=42.0
pkgrel=1
pkgdesc="The GNOME Bluetooth Subsystem"
url="https://wiki.gnome.org/Projects/GnomeBluetooth"
arch=(x86_64)
license=(GPL LGPL)
depends=(gtk4 libadwaita libnotify bluez gsound systemd pulseaudio-bluetooth
         upower)
makedepends=(gobject-introspection gtk-doc docbook-xsl git meson)
checkdepends=(python-dbusmock)
provides=(libgnome-bluetooth{,-ui}-3.0.so)
options=(debug)
_commit=fc488724cc543af991fb0c2c5677c58207369aaa  # tags/42.0^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-bluetooth.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd gnome-bluetooth
  git describe --tags | sed 's/^GNOMEBT_V_//;s/_fixed//;s/_/./g;s/-/+/g'
}

prepare() {
  cd gnome-bluetooth
}

build() {
  arch-meson gnome-bluetooth build -D gtk_doc=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 et:
