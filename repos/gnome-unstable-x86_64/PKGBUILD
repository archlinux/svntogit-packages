# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=folks
pkgver=0.6.7
pkgrel=2
pkgdesc="Library to aggregates people into metacontacts"
arch=(i686 x86_64)
url="http://telepathy.freedesktop.org/wiki/Folks"
license=('LGPL2.1')
depends=('telepathy-glib' 'libgee' 'libxml2' 'evolution-data-server' 'libsocialweb')
makedepends=('gobject-introspection' 'intltool' 'vala')
options=('!libtool' '!emptydirs')
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/0.6/$pkgname-$pkgver.tar.xz
        stop_spam::http://git.gnome.org/browse/folks/patch/?id=31e803448d206b75c820b5147364adac453dce50)
sha256sums=('d9ce3fd1f0ea3e0cb3780521cfda161062cf7ed1a016261e8d244fe41cbae8f3'
            '68b947ebe72c2c73e705db26f6bfb50856fa4c6d4644710e97205ff005a0b9fb')

build() {
  cd "$pkgname-$pkgver"

  patch -Np1 -i $srcdir/stop_spam
  find . -name '*.stamp' -delete
  ./configure --prefix=/usr
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
