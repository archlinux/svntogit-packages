# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kpublictransport
pkgver=22.03.90
pkgrel=1
pkgdesc='Library to assist with accessing public transport timetables and other data'
arch=(x86_64)
url='https://www.kde.org'
license=(LGPL)
depends=(qt5-declarative)
makedepends=(extra-cmake-modules protobuf qt5-tools doxygen)
source=(https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('a5783dca474e9a5cf24645d23bcaa7e5c438bb48e000d5b8ed8c420c5bc9c75f'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
options=(debug)

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
