# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=kmail
pkgver=20.11.90
pkgrel=1
pkgdesc="KDE mail client"
arch=(x86_64)
url="https://kontact.kde.org"
license=(GPL2)
depends=(kdepim-runtime kmail-account-wizard akonadi-import-wizard mbox-importer
         pim-data-exporter pim-sieve-editor kontactinterface ktnef)
makedepends=(extra-cmake-modules kdoctools boost)
optdepends=('kleopatra: mail encryption support' 'kdepim-addons: additional plugins')
groups=(kde-applications kdepim)
source=("https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('91235d1e8ebe70be55f40148c695bab98df7c64d896ee397876105c62e535817'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
