# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kidentitymanagement
pkgver=23.03.80
pkgrel=1
pkgdesc='KDE PIM libraries'
arch=(x86_64)
url='https://kontact.kde.org'
license=(LGPL)
depends=(kpimtextedit)
makedepends=(extra-cmake-modules doxygen qt5-tools)
source=(https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('7ff389890ea229672ea5b0a60d37d2cdb47dd2e586c88b327fe804f2fe1aad54'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

# https://bugs.kde.org/show_bug.cgi?id=467503
  cp "$pkgdir"/usr/lib/cmake/KPim5IdentityManagement/KPim5IdentityManagementQchTargets.cmake "$pkgdir"/usr/lib/cmake/KF5IdentityManagement/
}
