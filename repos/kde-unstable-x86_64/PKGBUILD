# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kontrast
pkgver=22.03.90
pkgrel=1
pkgdesc='Tool to check contrast for colors that allows verifying that your colors are correctly accessible'
arch=(x86_64)
url='https://apps.kde.org/kontrast/'
license=(GPL)
depends=(kirigami2 kdeclarative qt5-svg hicolor-icon-theme)
makedepends=(extra-cmake-modules)
groups=(kde-applications kde-accessibility)
source=(https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('3f29c1b863a00fa9da5540b01316a19575dce50c5f6186c19650b56566c16a29'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
options=(debug)

build() {
  cmake -B build -S $pkgname-$pkgver \
   -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}

