# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kwordquiz
pkgver=17.07.80
pkgrel=1
pkgdesc="Flash Card Trainer"
url="https://kde.org/applications/education/kwordquiz/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdeedu')
depends=('knotifyconfig' 'libkeduvocdocument' 'knewstuff' 'kdelibs4support')
makedepends=('extra-cmake-modules' 'python' 'kdoctools' 'kdesignerplugin')
conflicts=('kdeedu-kwordquiz')
replaces=('kdeedu-kwordquiz')
source=("https://download.kde.org/unstable/applications/${pkgver}/src/kwordquiz-${pkgver}.tar.xz"{,.sig})
sha256sums=('0a7e622ae0d74daa172bb9539451ed3fd39af65a4bb8eeab7bc69cfcb7ab5483'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  mkdir -p build
  cd build
  cmake ../kwordquiz-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
