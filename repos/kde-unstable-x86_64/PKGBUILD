# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=qt6-multimedia
pkgname=(qt6-multimedia qt6-multimedia-gstreamer qt6-multimedia-ffmpeg)
_qtver=6.4.0-beta1
pkgver=${_qtver/-/}
pkgrel=4
arch=(x86_64)
url='https://www.qt.io'
license=(GPL3 LGPL3 FDL custom)
pkgdesc='Classes for audio, video, radio and camera functionality'
makedepends=(cmake ninja qt6-declarative qt6-shadertools gst-plugins-base libpulse ffmpeg qt6-quick3d)
groups=(qt6)
options=(debug)
_pkgfn=${pkgname/6-/}-everywhere-src-$_qtver
source=(https://download.qt.io/development_releases/qt/${pkgver%.*}/$_qtver/submodules/$_pkgfn.tar.xz
        fix-ffmpeg-plugin-name.patch::https://code.qt.io/cgit/qt/qtmultimedia.git/patch/?id=da0c3cb2)
sha256sums=('54fda3e0bdecefb75058331ad55b8888787ef19c0442da67b3a4c15215533b32'
            'ef7f32cd54d30008a1ef7ce4128918cb6e870c5680c3d0a8de491186debeb82b')

prepare() {
  patch -d $_pkgfn -p1 < fix-ffmpeg-plugin-name.patch # Fix ffmpeg plugin name
}

build() {
  cmake -B build -S $_pkgfn -G Ninja \
    -DCMAKE_MESSAGE_LOG_LEVEL=STATUS
  cmake --build build
}

package_qt6-multimedia() {
  depends=(qt6-base qt6-multimedia-backend)
  optdepends=('qt6-declarative: QML bindings'
              'qt6-quick3d: for QtQuick3DAudio')

  DESTDIR="$pkgdir" cmake --install build
  rm -r "$pkgdir"/usr/lib/qt6/plugins/ # Split plugins

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt6-base "$pkgdir"/usr/share/licenses/$pkgname
}

package_qt6-multimedia-gstreamer() {
  pkgdesc='Gstreamer backend for qt6-multimedia'
  depends=(qt6-multimedia gst-plugins-base)
  provides=(qt6-multimedia-backend)
  optdepends=('gst-plugins-good: camera support, additional plugins'
              'gst-plugins-bad: camera support, additional plugins'
              'gst-plugins-ugly: additional plugins'
              'gst-libav: additional plugins')
  
  install -Dm755 build/lib/qt6/plugins/multimedia/libgstreamermediaplugin.so -t "$pkgdir"/usr/lib/qt6/plugins/multimedia
}

package_qt6-multimedia-ffmpeg() {
  pkgdesc='FFMpeg backend for qt6-multimedia'
  depends=(qt6-multimedia ffmpeg libva)
  provides=(qt6-multimedia-backend)

  install -Dm755 build/lib/qt6/plugins/multimedia/libffmpegmediaplugin.so -t "$pkgdir"/usr/lib/qt6/plugins/multimedia 
}
