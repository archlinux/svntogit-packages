# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kjobwidgets
pkgver=r5.ac0ff82
pkgrel=1
pkgdesc='KJobWidgets'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kjobwidgets'
license=('LGPL')
depends=('kcoreaddons' 'kwidgetsaddons' 'qt5-x11extras')
makedepends=('extra-cmake-modules' 'git')
groups=('kf5')
source=('git://anongit.kde.org/kjobwidgets.git')
md5sums=('SKIP')

pkgver() {
  cd kjobwidgets
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kjobwidgets \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
