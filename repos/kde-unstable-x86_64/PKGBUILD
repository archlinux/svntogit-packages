# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdf
pkgver=23.03.80
pkgrel=1
url='https://apps.kde.org/kdf/'
arch=(x86_64)
pkgdesc='View Disk Usage'
license=(GPL LGPL FDL)
depends=(kio hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools kcmutils)
groups=(kde-applications kde-utilities)
source=(https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('88eb72401b3ccd43774476b53b6161cf3791a9cadca1b29cdf36383d881996b5'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
