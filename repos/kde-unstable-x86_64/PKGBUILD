# Maintainer: Antonio Rojas <arojas@archlinux,org>

pkgname=krita
_pkgver=5.1.0-beta1
pkgver=${_pkgver/-/}
pkgrel=1
pkgdesc='Edit and paint images'
arch=(x86_64)
url='https://krita.org'
license=(GPL3)
depends=(kitemviews kitemmodels ki18n kcompletion kguiaddons kcrash qt5-svg qt5-multimedia quazip
         gsl libraw exiv2 openexr fftw openjpeg2 opencolorio libwebp hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools boost eigen poppler-qt5 python-pyqt5 libheif
             qt5-tools sip kseexpr libmypaint libjxl xsimd)
optdepends=('poppler-qt5: PDF filter' 'ffmpeg: to save animations'
            'python-pyqt5: for the Python plugins' 'libheif: HEIF filter' 'libjxl: JPEG-XL filter'
            'kseexpr: SeExpr generator layer' 'kimageformats: PSD support' 'libmypaint: support for MyPaint brushes'
            'krita-plugin-gmic: GMic plugin')
source=(https://download.kde.org/unstable/krita/$_pkgver/$pkgname-$_pkgver.tar.gz{,.sig}
        https://invent.kde.org/graphics/krita/-/commit/d88e952f.patch)
sha256sums=('bff5d60b87f908b6e3f4d9d0413a6037c8322ed4795755e74754f07e361c578f'
            'SKIP'
            '7edc38a73598350e0b2f95f753df38035b06789f4725f1ee2b5797474259dda7')
validpgpkeys=('05D00A8B73A686789E0A156858B9596C722EA3BD'  # Boudewijn Rempt <foundation@krita.org>
              'E9FB29E74ADEACC5E3035B8AB69EB4CF7468332F'  # Dmitry Kazakov (main key) <dimula73@gmail.com>
              '064182440C674D9F8D0F6F8B4DA79EDA231C852B') # Stichting Krita Foundation <foundation@krita.org>
options=(debug)

prepare() {
  patch -d $pkgname-$_pkgver -p1 < d88e952f.patch # Fix build with xsimd 8.1
}

build() {
  cmake -B build -S $pkgname-$_pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_KRITA_QT_DESIGNER_PLUGINS=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
