# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgbase=calligra
pkgname=('calligra-filters'
         'calligra-interfaces'
         'calligra-libs'
         'calligra-plugins'
         'calligra-pics'
         'calligra-servicetypes'
         'calligra-templates'
         'calligra-tools'
         'calligra-karbon'
         'calligra-kexi'
         'calligra-kounavail'
         'calligra-plan'
         'calligra-stage'
         'calligra-krita'
         'calligra-tables'
         'calligra-words'
         'calligra-handbook'
         'calligra-thesaurus-doc'
         'calligra-braindump'
         'calligra-flow')
pkgver=2.3.84
pkgrel=1
arch=('i686' 'x86_64')
url='http://www.calligra-suite.org'
license=('FDL1.2' 'GPL2' 'LGPL')
makedepends=('kdepimlibs' 'eigen2' 'freetds' 'kdegraphics-okular' 'libgsf'
             'libwpd' 'libwpg' 'pstoedit' 'glew' 'gsl' 'cmake' 'automoc4'
             'boost' 'libkdcraw' 'libpqxx' 'fftw' 'opengtl' 'lcms2')
groups=('calligra')
source=("http://download.kde.org/unstable/${pkgbase}-${pkgver}/${pkgbase}-${pkgver}.tar.bz2")
md5sums=('6888603d1ff09843a8581eef66baad41')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgbase}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_active=OFF \
    -DBUILD_mobile=OFF \
    -DBUILD_mysql=OFF \
    -DKDCRAW_LOCAL_FOUND=/usr/lib/libkdcraw.so \
    -DWITH_Marble=OFF
  make
}

package_calligra-filters() {
  pkgdesc="Filters for the Calligra office suite"
  optdepends=('libgsf: Microsoft Word document importer'
              'libwpd: WordPerfect document importer'
              'libwpg: Corel WordPerfect Graphics image importer'
              'pstoedit: Karbon EPS import filter'
              'libkdcraw: support for raw images')
  conflicts=('koffice-filters')
  replaces=('koffice-filters')
  install=calligra-filters.install

  cd "${srcdir}"/build/filters
  make DESTDIR="${pkgdir}" install
}

package_calligra-interfaces() {
  pkgdesc="Interfaces for the Calligra office suite"
  depends=('calligra-libs')
  conflicts=('koffice-interfaces')
  replaces=('koffice-interfaces')

  cd "${srcdir}"/build/interfaces
  make DESTDIR="${pkgdir}" install
}

package_calligra-libs() {
  pkgdesc="Libraries for the Calligra office suite"
  depends=('kdepimlibs' 'lcms2' 'calligra-servicetypes')
  conflicts=('koffice-libs')
  replaces=('koffice-libs')

  cd "${srcdir}"/build/libs
  make DESTDIR="${pkgdir}" install
}

package_calligra-plugins() {
  pkgdesc="Plugins for the Calligra office suite"
  depends=('calligra-libs')
  conflicts=('koffice-plugins' 'koffice-kchart')
  replaces=('koffice-plugins' 'koffice-kchart')

  cd "${srcdir}"/build/plugins
  make DESTDIR="${pkgdir}" install
}

package_calligra-pics() {
  pkgdesc="Icons for the Calligra office suite"
  depends=('calligra-libs')
  conflicts=('koffice-pics')
  replaces=('koffice-pics')
  install=calligra.install

  cd "${srcdir}"/build/pics
  make DESTDIR="${pkgdir}" install
}

package_calligra-servicetypes() {
  pkgdesc="Servicetypes for the Calligra office suite"
  conflicts=('koffice-servicetypes')
  replaces=('koffice-servicetypes')
  install=calligra.install

  cd "${srcdir}"/build/servicetypes
  make DESTDIR="${pkgdir}" install
}

package_calligra-templates() {
  pkgdesc="Templates for the Calligra office suite"
  conflicts=('koffice-templates')
  replaces=('koffice-templates')

  cd "${srcdir}"/build/templates
  make DESTDIR="${pkgdir}" install
}

package_calligra-tools() {
  pkgdesc="Tools for the Calligra office suite"
  depends=('calligra-libs')
  conflicts=('koffice-tools')
  replaces=('koffice-tools')
  install=calligra-tools.install

  cd "${srcdir}"/build/tools
  make DESTDIR="${pkgdir}" install
}

#### package common files done #####

#### package apps and their docs #####

package_calligra-karbon() {
  pkgdesc="Create scalable vector drawings"
  depends=('calligra-libs' 'calligra-templates')
  conflicts=('koffice-karbon' 'koffice-karbon-doc' 'calligra-karbon-doc')
  replaces=('koffice-karbon' 'koffice-karbon-doc' 'calligra-karbon-doc')
  install=calligra-karbon.install

  cd "${srcdir}"/build/karbon
  make DESTDIR="${pkgdir}" install
}

package_calligra-kexi() {
  pkgdesc="Develop desktop database applications"
  depends=('calligra-libs' 'calligra-templates')
  conflicts=('koffice-kexi' 'koffice-kexi-doc' 'calligra-kexi-doc')
  replaces=('koffice-kexi' 'koffice-kexi-doc' 'calligra-kexi-doc')
  install=calligra-kexi.install

  cd "${srcdir}"/build/kexi
  make DESTDIR="${pkgdir}" install
}

package_calligra-kounavail() {
  pkgdesc="Unavailable Calligra Document"
  depends=('calligra-libs')
  conflicts=('koffice-kounavail')
  replaces=('koffice-kounavail')

  cd "${srcdir}"/build/kounavail
  make DESTDIR="${pkgdir}" install
}

package_calligra-plan() {
  pkgdesc="Calligra Project Management Component"
  depends=('calligra-words')
  conflicts=('koffice-kplato' 'calligra-kplato')
  replaces=('koffice-kplato' 'calligra-kplato')
  install=calligra-plan.install

  cd "${srcdir}"/build/plan
  make DESTDIR="${pkgdir}" install
}

package_calligra-stage() {
  pkgdesc="Write presentation documents"
  depends=('calligra-libs' 'calligra-templates')
  optdepends=('calligra-filters: import/export filters')
  conflicts=('koffice-kpresenter' 'koffice-kpresenter-doc' 'calligra-stage-doc')
  replaces=('koffice-kpresenter' 'koffice-kpresenter-doc' 'calligra-stage-doc')
  install=calligra-stage.install

  cd "${srcdir}"/build/stage
  make DESTDIR="${pkgdir}" install
}

package_calligra-krita() {
  pkgdesc="Edit and paint images"
  depends=('calligra-libs' 'calligra-templates' 'calligra-plugins' 'glew'
           'qimageblitz' 'poppler-qt' 'openexr' 'opengtl')
  optdepends=('calligra-filters: import/export filters')
  conflicts=('koffice-krita' 'calligra-krita-doc')
  replaces=('koffice-krita' 'calligra-krita-doc')
  install=calligra-krita.install

  cd "${srcdir}"/build/krita
  make DESTDIR="${pkgdir}" install
}

package_calligra-tables() {
  pkgdesc="Write spreadsheet documents"
  depends=('calligra-libs' 'calligra-templates' 'calligra-plugins' 'gsl')
  optdepends=('calligra-filters: import/export filters')
  conflicts=('koffice-kspread' 'koffice-kspread-doc' 'calligra-tables-doc')
  replaces=('koffice-kspread' 'koffice-kspread-doc' 'calligra-tables-doc')
  install=calligra-tables.install

  cd "${srcdir}"/build/tables
  make DESTDIR="${pkgdir}" install
  cd "${srcdir}"/build/doc/tables
  make DESTDIR="${pkgdir}" install
}

package_calligra-words() {
  pkgdesc="Word Processor"
  depends=('calligra-libs' 'calligra-templates' 'calligra-plugins' 'calligra-pics'
           'calligra-kounavail' 'kdegraphics-okular')
  optdepends=('calligra-filters: import/export filters')
  conflicts=('koffice-kword')
  replaces=('koffice-kword')
  install=calligra-words.install

  cd "${srcdir}"/build/words
  make DESTDIR="${pkgdir}" install
}

package_calligra-handbook() {
  pkgdesc="Documentation for Calligra"
  conflicts=('koffice-handbook')
  replaces=('koffice-handbook')

  cd "${srcdir}"/build/doc/calligra
  make DESTDIR="${pkgdir}" install
}

package_calligra-thesaurus-doc() {
  pkgdesc="Documentation for Thesaurus"
  conflicts=('koffice-thesaurus-doc')
  replaces=('koffice-thesaurus-doc')

  cd "${srcdir}"/build/doc/thesaurus
  make DESTDIR="${pkgdir}" install
}

package_calligra-braindump() {
  pkgdesc="Notes and idea gathering"
  install=calligra-braindump.install

  cd "${srcdir}"/build/braindump
  make DESTDIR="${pkgdir}" install
}

package_calligra-flow() {
  pkgdesc="Flowchart & Diagram Editing"
  install=calligra-flow.install
  conflicts=('calligra-flow-doc')
  replaces=('calligra-flow-doc')

  cd "${srcdir}"/build/flow
  make DESTDIR="${pkgdir}" install
}

##### package apps done #####
