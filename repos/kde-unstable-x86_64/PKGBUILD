# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=itinerary
pkgver=22.03.90
pkgrel=1
pkgdesc='Itinerary and boarding pass management application'
arch=(x86_64)
url='https://apps.kde.org/itinerary/'
license=(LGPL BSD)
depends=(kitinerary kio networkmanager-qt kosmindoormap khealthcertificate qt5-location qqc2-desktop-style prison
         kitemmodels kunitconversion hicolor-icon-theme)
makedepends=(extra-cmake-modules)
groups=(kde-applications kde-pim)
source=(https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('5fad93684400d133a031b2695b63a36481845cdf107c86e288d12acd343836ea'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
options=(debug)

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 $pkgname-$pkgver/LICENSES/* -t "$pkgdir"/usr/share/licenses/$pkgname
}
