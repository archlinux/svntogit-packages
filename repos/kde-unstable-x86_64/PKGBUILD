# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kmix
pkgver=20.11.90
pkgrel=1
pkgdesc="KDE volume control program"
url='https://kde.org/applications/multimedia/kmix/'
arch=(x86_64)
license=(GPL LGPL FDL)
depends=(knotifications kxmlgui solid hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools plasma-framework)
groups=(kde-applications kde-multimedia)
source=("https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('fa8c5592ce3ec4f91cac7769c0c84ddd3576ea9ee221aa5fad828db6637d2ee4'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
