# $Id: PKGBUILD 241398 2015-06-29 17:50:28Z arojas $
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=picmi
pkgver=16.07.90
pkgrel=1
pkgdesc="A nonogram logic game for KDE"
url="http://kde.org/applications/games/picmi/"
arch=(i686 x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kdegames)
depends=(libkdegames hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools python)
conflicts=(kdegames-picmi)
replaces=(kdegames-picmi)
source=("http://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
sha1sums=('56102a1e79a6dfb084ac752acd0a8f452fcdde86')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
