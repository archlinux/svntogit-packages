# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kmime
pkgver=23.03.90
pkgrel=1
pkgdesc='Library for handling mail messages and newsgroup articles'
arch=(x86_64)
url='https://kontact.kde.org'
license=(LGPL)
depends=(ki18n kcodecs)
makedepends=(extra-cmake-modules doxygen qt5-tools)
source=(https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        fix-qch-install.patch)
sha256sums=('51f05e70c132f17c52700605739c9974170d745c8461bfe7d44117a5cddb049e'
            'SKIP'
            '8e5149c54bddcba90aa2a02cc80accafea593a396169e934796bf1d60e97e6d0')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

prepare() {
  patch -d $pkgname-$pkgver -p1 < fix-qch-install.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
