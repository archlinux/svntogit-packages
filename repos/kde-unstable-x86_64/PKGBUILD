# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=elisa
pkgver=20.11.90
pkgrel=1
pkgdesc='A simple music player aiming to provide a nice experience for its users'
url='https://community.kde.org/Elisa'
arch=(x86_64)
license=(LGPL3)
groups=(kde-applications kde-multimedia)
depends=(baloo kirigami2 kdeclarative vlc) # upnp-player-qt
makedepends=(extra-cmake-modules kdoctools)
source=("https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('ceceb82b47ff439b3f385134f3928786d8924b26fa1c8ccf7d83d1ff67b8638a'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF 
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
