# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Laurent Carlier <lordheavym@gmail.com>

pkgname=telepathy-kde-send-file
_pkgname=ktp-send-file
pkgver=18.11.90
pkgrel=1
pkgdesc='A File manager plugin to launch a file transfer job with a specified contact'
arch=(x86_64)
url='https://community.kde.org/Real-Time_Communication_and_Collaboration'
license=(GPL)
groups=(kde-applications kdenetwork telepathy-kde)
depends=(telepathy-kde-common-internals)
makedepends=(extra-cmake-modules kdoctools)
source=("https://download.kde.org/unstable/applications/$pkgver/src/$_pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('03aba2a2a359c4c70c52f04a6ad21bb1cbd6f29c8d53f8909c7cde3cf71c8c33'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$_pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
