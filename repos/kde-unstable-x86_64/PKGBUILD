# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=qt6-webengine
_qtver=6.4.0-beta3
pkgver=${_qtver/-/}
pkgrel=1
arch=(x86_64)
url='https://www.qt.io'
license=(GPL3 LGPL3 FDL custom)
pkgdesc='Provides support for web applications using the Chromium browser project'
depends=(qt6-webchannel qt6-positioning libxcomposite libxrandr libxkbfile 
         snappy nss libxslt minizip ffmpeg re2 libvpx libxtst ttf-font) # pciutils
makedepends=(cmake ninja python-html5lib gperf jsoncpp qt6-tools pipewire nodejs qt6-websockets libepoxy git)
optdepends=('pipewire: WebRTC desktop sharing under Wayland')
groups=(qt6)
options=(debug)
_pkgfn=${pkgname/6-/}-everywhere-src-$_qtver
source=(https://download.qt.io/development_releases/qt/${pkgver%.*}/$_qtver/submodules/$_pkgfn.tar.xz
        find-gio.patch::https://code.qt.io/cgit/qt/qtwebengine.git/patch?id=4d83c829
        fix-gio.patch::https://code.qt.io/cgit/qt/qtwebengine-chromium.git/patch/?id=7ed043a1)
sha256sums=('32287f170b7ec08813b6d4c08a868276e1de28f04017bcb188a22774a1c7ce31'
            '6153c13fa0ed512f9ae293c6c330d6bb91c339edaa3da204e14ed2544c6f95bb'
            '3f9a1de57254702f60495dcb3c6bcd2089fb6a9787d355c7b74fd3aa4b6a8b9b')

prepare() {
  patch -d $_pkgfn -p1 < find-gio.patch # Fix build
  patch -d $_pkgfn/src/3rdparty -p1 < fix-gio.patch
}

build() {
  cmake -B build -S $_pkgfn -G Ninja \
    -DCMAKE_MESSAGE_LOG_LEVEL=STATUS \
    -DCMAKE_TOOLCHAIN_FILE=/usr/lib/cmake/Qt6/qt.toolchain.cmake \
    -DQT_FEATURE_webengine_system_ffmpeg=ON \
    -DQT_FEATURE_webengine_system_icu=ON \
    -DQT_FEATURE_webengine_system_libevent=ON \
    -DQT_FEATURE_webengine_proprietary_codecs=ON \
    -DQT_FEATURE_webengine_kerberos=ON \
    -DQT_FEATURE_webengine_webrtc_pipewire=ON \
    -DQT_FEATURE_webengine_full_debug_info=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 "$srcdir"/${_pkgfn}/src/3rdparty/chromium/LICENSE "$pkgdir"/usr/share/licenses/${pkgname}/LICENSE.chromium
}
