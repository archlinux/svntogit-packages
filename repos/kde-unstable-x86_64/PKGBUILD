# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegraphics-thumbnailers
pkgver=19.03.80
pkgrel=1
pkgdesc="Thumbnailers for various graphics file formats"
url="https://www.kde.org/applications/graphics/"
arch=(x86_64)
license=(GPL LGPL FDL)
depends=(libkexiv2 libkdcraw kio ghostscript)
makedepends=(extra-cmake-modules)
groups=(kdegraphics kde-applications)
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('592c8a20f51182ec3eac9934d663ecad054ff717f3c4b9d542fb4328917a4442'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
