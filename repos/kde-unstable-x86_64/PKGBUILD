# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=calendarsupport
pkgver=23.03.80
pkgrel=1
pkgdesc='Calendar support library'
arch=(x86_64)
url='https://kontact.kde.org'
license=(LGPL)
depends=(kholidays akonadi-calendar akonadi-notes)
makedepends=(extra-cmake-modules qt5-tools doxygen)
source=(https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('0ceb23bbc43aeedea1b70b9f495914b04a280ebcaf07542a39bff94eabd526d1'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

# https://bugs.kde.org/show_bug.cgi?id=467503
  cp "$pkgdir"/usr/lib/cmake/KPim5CalendarSupport/KPim5CalendarSupportQchTargets.cmake "$pkgdir"/usr/lib/cmake/KF5CalendarSupport/
}
