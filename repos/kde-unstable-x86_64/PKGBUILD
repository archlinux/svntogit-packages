# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kontactinterface
pkgver=23.03.80
pkgrel=1
pkgdesc='Kontact Plugin Interface Library'
arch=(x86_64)
url='https://kontact.kde.org'
license=(LGPL)
depends=(kparts)
makedepends=(extra-cmake-modules doxygen qt5-tools)
source=(https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('b09032159091a2e8bccef5bee82a4fd3bda5898332a4213bb9482bc721259316'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

# https://bugs.kde.org/show_bug.cgi?id=467503
  cp "$pkgdir"/usr/lib/cmake/KPim5KontactInterface/KPim5KontactInterfaceQchTargets.cmake "$pkgdir"/usr/lib/cmake/KF5KontactInterface/
}
