# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux,org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=dolphin-plugins
pkgver=18.03.80
pkgrel=1
pkgdesc="Extra Dolphin plugins"
arch=(x86_64)
url="https://www.kde.org/applications/system/dolphin/"
license=(LGPL)
depends=(dolphin kdelibs4support)
makedepends=(extra-cmake-modules kdoctools ktexteditor kdesignerplugin)
optdepends=('ktexteditor: Mercurial plugin')
conflicts=(kdesdk-dolphin-plugins)
replaces=(kdesdk-dolphin-plugins)
groups=(kde-applications kdesdk)
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('690d3dc931b033c2d60b9448e95851e95849aacc128db184463fc137edc4dc86'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
