# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=mailimporter
pkgver=22.11.80
pkgrel=1
pkgdesc='Mail importer library'
arch=(x86_64)
url='https://kontact.kde.org'
license=(LGPL)
depends=(pimcommon)
makedepends=(extra-cmake-modules qt5-tools doxygen)
source=(https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('c96a88c84e653158dac52e7a2cc28ec0650da82908034d0c75f865ea3097e304'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
options=(debug)

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
