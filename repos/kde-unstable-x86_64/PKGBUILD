# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=mailimporter
pkgver=20.11.90
pkgrel=1
pkgdesc="Mail importer library"
arch=(x86_64)
url="https://kontact.kde.org"
license=(LGPL)
depends=(pimcommon)
makedepends=(extra-cmake-modules boost)
source=("https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('f2fdfcc534f2478331a0abae7f7dfa8a426c70b8c0240f3a627f21d572a90233'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
