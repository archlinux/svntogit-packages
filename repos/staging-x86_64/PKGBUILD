# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kig
pkgver=19.12.1
pkgrel=2
pkgdesc="Interactive Geometry"
url="https://kde.org/applications/education/kig/"
arch=(x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kdeedu)
depends=(ktexteditor qt5-xmlpatterns boost-libs hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools boost)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
         pykig-python3.patch)
sha256sums=('507d89cddc0e128ab398ce0f551af22af0ba1583a4419574296cfefb96d944ee'
            'SKIP'
            'b801af24d560dd3928657cbcaed879b794b13038e0785681f5ba82368d1a8fd4')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../pykig-python3.patch # Port pykig to python 3
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
