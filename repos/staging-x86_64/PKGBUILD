# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=python-patiencediff
pkgver=0.2.13
pkgrel=2
pkgdesc='Patiencediff Python and C implementations'
arch=(x86_64)
url=https://www.breezy-vcs.org/
license=(GPL2)
depends=(python)
makedepends=(
  git
  python-setuptools
)
_tag=834fad2d280fa0634d00d658c61c15d82031f519
source=(git+https://github.com/breezy-team/patiencediff.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd patiencediff
  git describe --tags | sed 's/^v//'
}

build() {
  cd patiencediff
  python setup.py build
}

package() {
  cd patiencediff
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

# vim: ts=2 sw=2 et:
