# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=qmlkonsole
pkgver=23.04.1
pkgrel=1
pkgdesc='Terminal app for Plasma Mobile'
url='https://apps.kde.org/qmlkonsole/'
arch=(x86_64)
license=(GPL LGPL)
depends=(kirigami-addons qmltermwidget qt5-svg ki18n kconfig kcoreaddons kwindowsystem)
makedepends=(extra-cmake-modules)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('7109b8f2f400d84991b14e92329d92d8f532434c6f60fc18e78180ad66ec01d8'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
