# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgbase=libteam
pkgname=(libteam libteam-docs)
pkgver=1.31
pkgrel=8
pkgdesc="Library for controlling team network device"
url="http://libteam.org/"
arch=(x86_64)
license=(LGPL)
depends=(
  bash
  jansson
  libdaemon
  libdbus
  libnl
  zeromq
)
makedepends=(
  doxygen
  git
  graphviz
)
_commit=3ee12c6d569977cf1cd30d0da77807a07aa77158  # tags/v1.31
source=("git+https://github.com/jpirko/libteam#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd libteam
  git describe --tags | sed 's/^v//;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd libteam

  # https://bugs.archlinux.org/task/68486
  git cherry-pick -n 61efd6de2fbb8ee077863ee5a355ac3dfd9365b9

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd libteam
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --libexecdir=/usr/lib
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make all html
}

check() {
  cd libteam
  make check
}

package_libteam() {
  provides=(libteam.so libteamdctl.so)

  cd libteam
  make DESTDIR="$pkgdir" install

  install -Dt "$pkgdir/usr/lib/systemd/system" -m644 teamd/redhat/systemd/teamd@.service
  install -Dt "$pkgdir/usr/share/dbus-1/system.d" -m644 teamd/dbus/teamd.conf
}

package_libteam-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mkdir -p "$pkgdir/usr/share/doc/$pkgbase"
  cp -a libteam/doc/api "$pkgdir/usr/share/doc/$pkgbase"
}

# vim:set sw=2 sts=-1 et:
