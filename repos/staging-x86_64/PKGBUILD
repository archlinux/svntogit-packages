# Maintainer:
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=talloc
pkgver=2.2.0
pkgrel=3
pkgdesc="Hierarchical pool based memory allocator with destructors"
arch=('x86_64')
license=('GPL3')
url="https://talloc.samba.org/"
depends=('glibc')
makedepends=('python' 'docbook-xsl')
optdepends=('python: for python bindings')
source=(https://www.samba.org/ftp/talloc/talloc-$pkgver.tar.{gz,asc}
        samba-update-waf-for-py38.patch::https://gitlab.com/samba-team/samba/commit/aabdcc9151.patch
        wafsamba-use-native-waf-timer.patch)
sha1sums=('d806e2e4e9b973e8ab6521d1dd20a03abae32c99'
          'SKIP'
          'ab8f8bd63c722e2e3b709e89fa3c1e222daac988'
          '2e688ce37d2f5027ba6e4eb52503353d9707fdae')
validpgpkeys=(9147A339719518EE9011BCB54793916113084025) # samba-bugs@samba.org

prepare() {
   cd "${srcdir}/${pkgname}-${pkgver}"

   # https://bugzilla.samba.org/show_bug.cgi?id=13960
   patch -Np1 -i ../samba-update-waf-for-py38.patch

   # https://bugzilla.samba.org/show_bug.cgi?id=13998
   patch -Np1 -i ../wafsamba-use-native-waf-timer.patch
}

build() {
   cd "${srcdir}/${pkgname}-${pkgver}"
   ./configure --prefix=/usr \
     --sysconfdir=/etc/samba \
     --localstatedir=/var \
     --bundled-libraries=NONE \
     --builtin-libraries=replace \
     --enable-talloc-compat1
   make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() {
   cd "${srcdir}/${pkgname}-${pkgver}"
   make DESTDIR="${pkgdir}" install
}
