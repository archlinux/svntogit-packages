# $Id$
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=evolution-ews
pkgver=3.26.2
pkgrel=2
pkgdesc="MS Exchange integration through Exchange Web Services"
url="https://wiki.gnome.org/Apps/Evolution/EWS"
arch=(x86_64)
license=(LGPL2.1)
depends=(libmspack evolution)
makedepends=(git cmake ninja intltool python)
options=(!emptydirs)
_commit=fb9752275bfb34054f6ba6df3cb2b3bb87d82662  # tags/EVOLUTION_EWS_3_26_2^0
source=("git+https://git.gnome.org/browse/evolution-ews#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^EVOLUTION_EWS_//;s/_/./g;s/-/+/g'
}

build() {
  cd $pkgname
  cmake . -G Ninja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIBEXEC_INSTALL_DIR=/usr/lib/evolution \
    -DSYSCONF_INSTALL_DIR=/etc
  ninja
}

package() {
  cd $pkgname
  DESTDIR="$pkgdir" ninja install
}
