# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=signon-ui
pkgver=0.17+20150611
pkgrel=2
_commit=3acb6541
pkgdesc='UI component responsible for handling the user interactions which can happen during the login process of an online account'
arch=(x86_64)
url="https://launchpad.net/online-accounts-signon-ui"
license=(GPL)
depends=(libaccounts-qt qt5-webkit signond libnotify)
source=("https://gitlab.com/accounts-sso/signon-ui/repository/archive.tar.gz?ref=$_commit")
md5sums=('3a70743aac1bae75f4c3efb133bd8dc1')

prepare() {
  cd signon-ui-*

  # Do not install tests
  echo 'INSTALLS =' >>tests/unit/tst_inactivity_timer.pro
  echo 'INSTALLS =' >>tests/unit/tst_signon_ui.pro
}

build() {
  cd signon-ui-*

  qmake-qt5 PREFIX=/usr LIBDIR=/usr/lib
  make
}

package() {
  cd signon-ui-*

  make INSTALL_ROOT="$pkgdir" install
}

