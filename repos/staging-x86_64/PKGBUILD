# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Tobias Powalowski <t.powa@gmx.de>

pkgname=pstoedit
pkgver=3.70
pkgrel=3
pkgdesc="Translates PostScript and PDF graphics into other vector formats"
arch=('i686' 'x86_64')
url="http://www.pstoedit.net/"
license=('GPL')
depends=('gcc-libs' 'plotutils' 'gd' 'imagemagick')
makedepends=('ghostscript')
options=('!makeflags')
source=("http://downloads.sourceforge.net/sourceforge/pstoedit/pstoedit-${pkgver}.tar.gz")
sha1sums=('657f8f7070fde1432cd65a34b6b1c4b5b42f8b50')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i 's/-pedantic//' configure
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
