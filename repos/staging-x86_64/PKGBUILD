# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>

pkgname=libtorrent-rasterbar
pkgver=1.2.10
pkgrel=3
epoch=1
pkgdesc="A C++ BitTorrent library that aims to be a good alternative to all the other implementations around"
url="https://www.rasterbar.com/products/libtorrent/"
arch=('x86_64')
license=('BSD')
depends=('boost-libs')
makedepends=('boost' 'python2' 'python')
options=('!emptydirs')
source=(https://github.com/arvidn/libtorrent/releases/download/libtorrent-$pkgver/$pkgname-$pkgver.tar.gz
        $pkgname-cxx14-fix.patch::https://github.com/arvidn/libtorrent/pull/5026.patch)
sha512sums=('50f912d85eefa94c560613cf53d9a2cb00bf4ed800f15e7a43dca35dd941a931a681d1d9ac7305cde0b435bdcf7c6e76a71fbb60d95aefbdca6f982d57b3951f'
            '94ddc3bdfc33f7b5300cc01b7c821dbd9a1433cae93bb7d467a1a732e84e7be4a6e75fbd69e88802e2eecca9c8541ecd1856d4ca2b508af224562cb05de80cca')

prepare() {
  mkdir py2 py3
  cd $pkgname-$pkgver
  # https://bugs.archlinux.org/task/67754
  patch -p1 -i ../$pkgname-cxx14-fix.patch

  # Avoid depending on newer processors
  sed -i 's/-msse4.2//' configure.ac

  autoreconf -if
}

_build() (
  cd py$1

  # FS#50745
  _boost="boost_python"
  if [ $1 -eq 3 ]; then _boost="boost_python3"; fi

  PYTHON=/usr/bin/python$1 \
  ../$pkgname-$pkgver/configure \
    --prefix=/usr \
    --enable-python-binding \
    --enable-examples \
    --disable-static \
    --with-libiconv \
    --with-boost-python=$_boost
)

build() {
  _build 2
  _build 3
}

package() {
  make -C py2 DESTDIR="$pkgdir" install
  make -C py3 DESTDIR="$pkgdir" install
  install -Dm644 $pkgname-$pkgver/COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # Remove most example binaries
  rm "$pkgdir"/usr/bin/{*_test,*_tester,simple_client,stats_counters}
}
