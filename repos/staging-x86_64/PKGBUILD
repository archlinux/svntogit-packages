# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>

pkgname=libxslt
pkgver=1.1.26
pkgrel=2
pkgdesc="XML stylesheet transformation library"
arch=('i686' 'x86_64')
url="http://xmlsoft.org/XSLT/"
license=('custom')
depends=('libxml2>=2.7.7' 'libgcrypt>=1.4.6')
makedepends=('python2')
options=('!libtool')
source=(ftp://xmlsoft.org/libxslt/${pkgname}-${pkgver}.tar.gz)
md5sums=('e61d0364a30146aaa3001296f853b2b9')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --with-python=/usr/bin/python2.7
  make
  make DESTDIR="${pkgdir}" install

  for f in pyxsltproc.py extfunc.py exslt.py extelem.py basic.py; do
      sed -e 's|#!/usr/bin/python|#!/usr/bin/python2|' \
          -i ${pkgdir}/usr/share/doc/libxslt-python-1.1.26/examples/${f}
  done

  install -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
