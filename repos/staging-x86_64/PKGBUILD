# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Andreas Radke <andyrtr at archlinux.org>
# Contributor: Ben Mazer <contrasutra@myrealbox.com>

pkgname=xfce4-notes-plugin
pkgver=1.8.1.r235.g0ad1e5f
_commit=0ad1e5f87831b3d82785dae37789ba5554ba3fcd
pkgrel=1
pkgdesc="Notes plugin for the Xfce panel"
arch=('x86_64')
license=('GPL2')
url="https://docs.xfce.org/panel-plugins/xfce4-notes-plugin"
groups=('xfce4-goodies')
depends=('xfce4-panel' 'hicolor-icon-theme')
makedepends=('intltool' 'vala' 'git' 'xfce4-dev-tools')
#source=(https://archive.xfce.org/src/panel-plugins/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
source=(git+https://gitlab.xfce.org/panel-plugins/xfce4-notes-plugin#commit=$_commit)
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$pkgname"
  git describe | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "$srcdir/$pkgname"

  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
