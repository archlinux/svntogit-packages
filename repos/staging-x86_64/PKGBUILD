# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libxrender
pkgver=0.9.10
pkgrel=2
pkgdesc="X Rendering Extension client library"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libx11>=1.3.4' 'renderproto')
makedepends=('pkgconfig')
source=(${url}/releases/individual/lib/libXrender-${pkgver}.tar.bz2{,.sig})
sha256sums=('c06d5979f86e64cabbde57c223938db0b939dff49fdb5a793a1d3d0396650949'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') #Alan Coopersmith <alan.coopersmith@oracle.com>
validpgpkeys+=('C41C985FDCF1E5364576638B687393EE37D128F8') # Matthieu Herrb <matthieu.herrb@laas.fr>

build() {
  cd libXrender-${pkgver}
  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd libXrender-${pkgver}
  make DESTDIR=${pkgdir} install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
