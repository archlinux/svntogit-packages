# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: nesl247 <nesl247@gmail.com>

pkgbase=pycups
pkgname=('python2-pycups' 'python-pycups')
pkgver=1.9.68
pkgrel=1
arch=('i686' 'x86_64')
url="http://cyberelk.net/tim/software/pycups/"
license=('GPL')
makedepends=('libcups' 'python2' 'python')
source=(http://cyberelk.net/tim/data/${pkgbase}/${pkgbase}-${pkgver}.tar.bz2{,.asc})
md5sums=('eae8a0a3cb0925eb6b1db8cb4645f3a3'
         'SKIP')

prepare() {
  cp -r ${pkgbase}-${pkgver} ${pkgbase}-${pkgver}-python2
}

build() {
  cd ${pkgbase}-${pkgver}
  python setup.py build
  cd "${srcdir}"/${pkgbase}-${pkgver}-python2
  python2 setup.py build
}

package_python2-pycups() {
  pkgdesc="Python 2 CUPS Bindings"
  depends=('libcups' 'python2')

  cd ${pkgbase}-${pkgver}-python2
  python2 setup.py install --root="${pkgdir}" --optimize=1
}

package_python-pycups() {
  pkgdesc="Python 3 CUPS Bindings"
  depends=('libcups' 'python')

  cd ${pkgbase}-${pkgver}
  python setup.py install --root="${pkgdir}" --optimize=1
}
