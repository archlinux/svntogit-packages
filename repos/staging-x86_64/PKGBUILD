# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=calindori
pkgver=23.04.1
pkgrel=1
pkgdesc='Calendar for Plasma Mobile'
url='https://apps.kde.org/calindori/'
arch=(x86_64)
license=(GPL LGPL)
depends=(kirigami2 qt5-svg kcalendarcore kpeople knotifications kdbusaddons)
makedepends=(extra-cmake-modules)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('5ae2f905f276823c6f47b64c15a6e4a4c593dee9dae146a6fe3a6f40433520ce'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
