# $Id$
# Contributor: Tom Killian <tomk@runbox.com>
# Contributor: francois at archlinux dot org

pkgname=perl-dbd-sqlite
_cpanname=DBD-SQLite
pkgver=1.35
pkgrel=2
pkgdesc="Self-contained RDBMS in a DBI driver"
arch=('i686' 'x86_64')
url="http://search.cpan.org/dist/${_cpanname}"
license=('GPL' 'PerlArtistic')
depends=('perl-dbi>=1.61' 'sqlite')
options=('!emptydirs')
source=(http://www.cpan.org/authors/id/A/AD/ADAMK/${_cpanname}-${pkgver}.tar.gz)
md5sums=('d9752e4a26fa54e74aa893dafd02ceda')

build() {
  cd "${srcdir}/${_cpanname}-${pkgver}"
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "${srcdir}/${_cpanname}-${pkgver}"
  make test
}

package() {
  cd "${srcdir}/${_cpanname}-${pkgver}"
  make install DESTDIR="${pkgdir}"
}
