# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=php-xcache
pkgver=3.1.0
pkgrel=4
arch=('i686' 'x86_64')
pkgdesc='A PHP opcode cacher'
url='http://xcache.lighttpd.net/'
depends=('php')
license=('custom')
source=("http://xcache.lighttpd.net/pub/Releases/${pkgver}/xcache-${pkgver}.tar.bz2"
        'xcache.ini' 'xcache-r1486.patch')
backup=('etc/php/conf.d/xcache.ini')
md5sums=('e5816d47d52be200b959bf69a673ff74'
         '8fd9ce537ada9463c4b0c042243158c0'
         '552024a7fdcfd5d3f9c069135e35a52e')

prepare() {
	cd $srcdir/xcache-$pkgver
	# Patch by Debian
	# http://anonscm.debian.org/cgit/pkg-php/xcache.git/tree/debian/patches
	patch -p1 -i $srcdir/xcache-r1486.patch
}

build() {
	cd $srcdir/xcache-$pkgver
	phpize
	./configure --prefix=/usr
	make
}

check() {
	cd $srcdir/xcache-$pkgver
	export NO_INTERACTION=1
	make test
	echo
}

package() {
	cd $srcdir/xcache-$pkgver
	make INSTALL_ROOT=$pkgdir install

	install -Dm644 $srcdir/xcache.ini $pkgdir/etc/php/conf.d/xcache.ini

	install -dm755 $pkgdir/usr/share/webapps/php-xcache
	cp -r htdocs/* $pkgdir/usr/share/webapps/php-xcache/

	install -Dm644 COPYING $pkgdir/usr/share/licenses/php-xcache/COPYING
}
