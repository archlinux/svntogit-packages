# $Id$
# Maintainer: kevin <kevin@archlinux.org>
# Contributor: Manolis Tzanidakis
# Contributor: Firmicus <francois.archlinux.org> 

pkgname=perl-html-parser
_realname=HTML-Parser
pkgver=3.68
pkgrel=1
pkgdesc="Perl HTML parser class"
arch=('i686' 'x86_64')
license=('PerlArtistic')
url="http://search.cpan.org/dist/${_realname}/"
depends=('perl-html-tagset' 'perl>=5.12.1')
options=(!emptydirs)
source=(http://www.cpan.org/authors/id/G/GA/GAAS/${_realname}-${pkgver}.tar.gz)
md5sums=('5550b2da7aa94341f1e8a17a4ac20c68')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  # install module in vendor directories.
  perl Makefile.PL INSTALLDIRS=vendor
  make
  make test
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make install DESTDIR="${pkgdir}"
}
# vim: ts=2 sw=2 et ft=sh
