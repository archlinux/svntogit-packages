# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xf86-input-vmmouse
pkgver=13.0.0
pkgrel=3
pkgdesc="X.org VMWare Mouse input driver"
arch=(i686 x86_64)
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('glibc' 'sh')
makedepends=('xorg-server-devel' 'X-ABI-XINPUT_VERSION=20' 'resourceproto' 'scrnsaverproto')
conflicts=('xorg-server<1.15.0' 'X-ABI-XINPUT_VERSION<20' 'X-ABI-XINPUT_VERSION>=21')
groups=('xorg-drivers' 'xorg')
backup=('etc/X11/xorg.conf.d/50-vmmouse.conf')
source=(${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('04cfb60366008d4db815c550d8fb8d0a4270c75fa7a20fa3bddc9ecbd355612c')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr \
    --with-xorg-conf-dir=/etc/X11/xorg.conf.d \
    --with-udev-rules-dir=/usr/lib/udev/rules.d
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
  rm -rfv ${pkgdir}/usr/{lib,share}/hal
}
