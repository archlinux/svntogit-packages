# Maintainer:  Anatol Pomozov <anatol@archlinux.org>

pkgname=virglrenderer
pkgver=0.8.0
pkgrel=1
pkgdesc='A virtual 3D GPU library, that allows the guest operating system to use the host GPU to accelerate 3D rendering'
arch=(x86_64)
url='https://virgil3d.github.io/'
license=(MIT)
depends=(libepoxy mesa)
makedepends=(python)
source=(https://github.com/freedesktop/virglrenderer/archive/virglrenderer-$pkgver.zip)
sha1sums=('44f673cabc2e4d3eea6c22f70fb6814bd0c1d732')

build() {
  cd virglrenderer-virglrenderer-$pkgver 
  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd virglrenderer-virglrenderer-$pkgver
  make DESTDIR="$pkgdir" install
  install -D -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
