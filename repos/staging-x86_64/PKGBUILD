# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Sarah Hay <sarah@archlinux.org>

pkgname=glib-perl
pkgver=1.329
pkgrel=2
pkgdesc="Perl wrappers for glib 2.x, including GObject"
arch=(x86_64)
license=('LGPL')
url="http://gtk2-perl.sourceforge.net/"
options=('!emptydirs')
depends=('glib2')
makedepends=('perl-extutils-depends' 'perl-extutils-pkgconfig')
source=(https://downloads.sourceforge.net/sourceforge/gtk2-perl/Glib-${pkgver}.tar.gz
        glib2.59-comment-linebreaks.patch)
sha256sums=('7b4ce1c8cf884849189efaa0b5722ecfb0d87a15f94fccc6a6017d3b3f982a35'
            'b816306015f9b1f32ec2191ebeaa5f5f92f2eea60829ffb638aa5dae9fb946ed')

prepare() {
  cd Glib-$pkgver

  # https://gitlab.gnome.org/GNOME/perl-glib/merge_requests/1
  patch -Np1 -i ../glib2.59-comment-linebreaks.patch
}

build() {
  cd Glib-$pkgver
  PERL_USE_UNSAFE_INC=1 \
  perl Makefile.PL INSTALLDIRS=vendor
  PERL_USE_UNSAFE_INC=1 \
  make
}

check() {
  cd Glib-$pkgver
  make test
}

package() {
  cd Glib-$pkgver
  make DESTDIR="${pkgdir}" install
}
