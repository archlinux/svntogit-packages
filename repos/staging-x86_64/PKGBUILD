# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdevelop-pg-qt
pkgver=2.1.0
pkgrel=2
pkgdesc="KDevelop Parser Generator, a LL(1) parser generator used by KDevelop language plugins"
arch=(x86_64)
url="http://www.kdevelop.org/"
license=(GPL)
depends=(qt5-base)
makedepends=(extra-cmake-modules)
source=("http://download.kde.org/stable/$pkgname/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('20d84d25bd40610bd6c0964e4fe0642e56c41b76a65575122dc5196649621e5d'
            'SKIP')
validpgpkeys=('329FD02C5AA48FCC77A4BBF0AC44AC6DB29779E6'  # Sven Brauch <mail@svenbrauch.de>
              '364EFA5F639572907D392999C64CF56B13CACE5D') # Kevin Funk <kfunk@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
