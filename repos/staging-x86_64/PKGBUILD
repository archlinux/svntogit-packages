# $Id$
# Maintainer: Aaron Griffin <aaron@archlinux.org>

pkgname=goffice
pkgver=0.8.17
pkgrel=1
pkgdesc="A library of document-centric objects and utilities built on top of GLib and Gtk+"
arch=('i686' 'x86_64')
url="http://www.gnome.org"
license=('GPL')
options=('!libtool')
depends=('gtk2' 'gconf' 'libgsf')
makedepends=('pkgconfig' 'intltool' 'gtk-doc')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz
        use-apiver-for-dirs.patch)
sha256sums=('165070beb67b84580afe80a8a100b674a81d553ab791acd72ac0c655f4fadb15'
            '4507a49f6ccb13e55c0b3cc6831b7e0dcec7badb58a3661190e843f8607fa270')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np0 -i "${srcdir}/use-apiver-for-dirs.patch"
  autoreconf -fi
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
