# Maintainer: Daniel Isenmann <daniel@archlinux.org>

pkgname=gegl
pkgver=0.4.30
pkgrel=6
pkgdesc="Graph based image processing framework"
arch=('x86_64')
url="https://www.gegl.org/"
license=('GPL3' 'LGPL3')
depends=('babl' 'libspiro' 'json-glib' 'libgexiv2' 'libraw' 'openexr' 'librsvg' 'libtiff' 'luajit' 'jasper' 'lensfun' 'suitesparse' 'poppler-glib')
makedepends=('intltool' 'ruby' 'mesa' 'glu' 'ffmpeg' 'exiv2' 'vala' 'gobject-introspection' 'git' 'meson' 'python-gobject')
optdepends=('ffmpeg: ffmpeg plugin' 'graphviz: for gegl-introspect')
_commit=5ea11ee00d9061ca2cd61f83ece25dfcfe0e9596
source=("git+https://gitlab.gnome.org/GNOME/gegl.git#commit=$_commit"
        gegl-openexr3.patch)
sha256sums=('SKIP'
            '49f0870650d2c412d66e7479e2c516f3a3903a801db1c221cf262de77d45b3dd')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^GEGL_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd ${pkgname}
  git cherry-pick -n c1c3541caab485384087bbbcf1c83ef2d5bd4d43

  patch -p1 < ../gegl-openexr3.patch # Fix build with OpenEXR 3
}

build() {
  mkdir -p build
  cd build
  arch-meson ../$pkgname \
    -Dworkshop=true \
    -Dmrg=disabled \
    -Dmaxflow=disabled
  ninja
}

check() {
  cd build
  ninja test || :
}

package() {
  cd build
  DESTDIR="${pkgdir}" ninja install
}
