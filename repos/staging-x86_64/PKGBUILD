# Maintainer: Maxime Gauduin <alucrydarchlinux.org>
# Contributor: Padfoot <padfoot@exemail.com.au>

pkgname=lightdm-gtk-greeter
pkgver=2.0.7
pkgrel=2
epoch=1
pkgdesc='GTK+ greeter for LightDM'
arch=(x86_64)
url=https://launchpad.net/lightdm-gtk-greeter
license=(
  GPL3
  LGPL3
)
depends=(
  gtk3
  lightdm
)
makedepends=(
  exo
  gobject-introspection
  intltool
)
backup=(etc/lightdm/lightdm-gtk-greeter.conf)
source=(https://launchpad.net/lightdm-gtk-greeter/${pkgver%.?}/${pkgver}/+download/lightdm-gtk-greeter-${pkgver}.tar.gz)
sha256sums=('bae293f21286e93113ee7965a3a8734bbee6b94f3c74d8723dcae13dda60fcbc')

build() {
  cd lightdm-gtk-greeter-${pkgver}

  export CFLAGS="$CFLAGS -Wno-declaration-after-statement"

  ./configure \
    --prefix=/usr \
    --libexecdir=/usr/lib/lightdm \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --with-libxklavier \
    --enable-kill-on-sigterm \
    --disable-libido \
    --disable-libindicator \
    --disable-static
  make
}

package() {
  cd lightdm-gtk-greeter-${pkgver}

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
