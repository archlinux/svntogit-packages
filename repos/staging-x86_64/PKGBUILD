# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kitinerary
pkgver=22.04.3
pkgrel=3
pkgdesc='Data model and extraction system for travel reservation information'
arch=(x86_64)
url='https://kontact.kde.org'
license=(LGPL)
depends=(kmime kpkpass kcalendarcore kcontacts poppler libphonenumber zxing-cpp)
makedepends=(extra-cmake-modules qt5-tools doxygen)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        https://invent.kde.org/pim/kitinerary/-/commit/50319138.patch)
sha256sums=('9fc6970e633584c88c5283dd2a5d3a4638f704439229abd9c6267d907a6ab3cb'
            'SKIP'
            '52b339ecc4bc3acdee0df9e82c78275173af99c756ef2eb4a5c588c91a8c3bc1')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
options=(debug)

prepare() {
  patch -d $pkgname-$pkgver -p1 < 50319138.patch # ZXing 1.4 support
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
