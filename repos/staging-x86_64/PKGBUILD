# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kitinerary
pkgver=20.08.3
pkgrel=2
pkgdesc="Data model and extraction system for travel reservation information"
arch=(x86_64)
url="https://kontact.kde.org"
license=(LGPL)
depends=(kmime kpkpass kcalendarcore kcontacts poppler libphonenumber zxing-cpp)
makedepends=(extra-cmake-modules)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('a6bebf2b08d9c02ef6429d0e46af0435ea81746ab34d9191f26b4aef8aaf3799'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
