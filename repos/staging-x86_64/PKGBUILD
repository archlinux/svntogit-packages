# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-session
pkgver=3.8.0
pkgrel=2
pkgdesc="The GNOME Session Handler"
arch=(i686 x86_64)
license=(GPL LGPL)
depends=(systemd dconf gconf gsettings-desktop-schemas gtk3 gnome-desktop
         hicolor-icon-theme json-glib libgl libsm libxtst upower)
makedepends=(intltool mesa gtk-doc)
options=('!emptydirs')
install=gnome-session.install
url="http://www.gnome.org"
groups=(gnome)
source=(http://download.gnome.org/sources/$pkgname/${pkgver::3}/$pkgname-$pkgver.tar.xz
        timeout.patch)
sha256sums=('7892c4ef5049bf5f4ac75bc82b554c1c5f962249108c8467df1be0f947b5a99f'
            '9eaf31857b41db417475c3b14adc11b10c8226ed76978cdf96dd648fa6e505fc')

build() {
  cd $pkgname-$pkgver

  # Increase timeout, for slow machines
  patch -Np1 -i ../timeout.patch

  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --libexecdir=/usr/lib/gnome-session \
      --disable-schemas-compile --enable-systemd
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
