# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=moc
pkgver=20110528
pkgrel=4
pkgdesc="An ncurses console audio player with support for the mp3, ogg, and wave formats"
arch=('i686' 'x86_64')
url="http://moc.daper.net/"
license=('GPL')
depends=('libmad' 'libid3tag' 'jack' 'curl' 'libltdl' 'file')
makedepends=('speex' 'ffmpeg' 'taglib' 'libmpcdec' 'wavpack' 'libmodplug')
optdepends=('speex: for using the speex plugin' 
	    'ffmpeg: for using the ffmpeg plugin'
	    'taglib: for using the musepack plugin' 
	    'libmpcdec: for using the musepack plugin' 
            'wavpack: for using the wavpack plugin' 
	    'libmodplug: for using the modplug plugin')
options=('!libtool')
source=(ftp://ftp.archlinux.org/other/moc/${pkgname}-${pkgver}.tar.xz \
        gcc-undefined-symbols.diff moc-ffmpeg.patch)
sha1sums=('f79049136ce6616bfd6af2f5e08246a5921441cf'
          'a811a4ac7e049914aab528d3f06a6be6634c2720'
          'f7e8eb17a89fea1c08c8a68f5afd1ffdc641ce9d')

build() {
  cd "${srcdir}/${pkgname}"
  patch -p0 -i ../gcc-undefined-symbols.diff
  patch -p0 -i ../moc-ffmpeg.patch

# Disabling aac to use the external ffmpeg to play them (FS#13164)
  ./autogen.sh
  ./configure --prefix=/usr --without-rcc --without-aac \
    --with-oss  --with-alsa  --with-jack --with-mp3 \
    --with-musepack --with-vorbis --with-flac --with-wavpack \
    --with-sndfile --with-modplug --with-ffmpeg --with-speex \
    --with-samplerate --with-curl --disable-debug
  make
}

package() {
  cd "${srcdir}/${pkgname}"
  make DESTDIR="${pkgdir}" install
}
