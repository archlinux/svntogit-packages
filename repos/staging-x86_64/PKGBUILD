# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gsound
pkgver=1.0.2
pkgrel=4
pkgdesc="Small library for playing system sounds"
url="https://wiki.gnome.org/Projects/GSound"
license=('LGPL')
arch=('x86_64')
depends=(glib2 libcanberra)
makedepends=(gobject-introspection vala git gtk-doc autoconf-archive)
_commit=a86946b494aa32782b3f6334344d07b5c11d5219  # tags/1.0.2^0
source=("git+https://gitlab.gnome.org/GNOME/gsound.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --disable-static --enable-gtk-doc \
    --enable-compile-warnings=yes
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  make -C $pkgname check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
