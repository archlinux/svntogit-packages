# $Id$
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=xapian-core
epoch=1
pkgver=1.2.21
pkgrel=2
pkgdesc='Open source search engine library.'
arch=('i686' 'x86_64')
url='http://www.xapian.org/'
license=('GPL')
depends=('util-linux')
# xapian config requires libxapian.la
options=('libtool')
source=("http://oligarchy.co.uk/xapian/${pkgver}/${pkgname}-${pkgver}.tar.xz")
md5sums=('276497a458a8ae63e7dceb372cc1bc37')

build() {
  # FS#40614
  if [ "${CARCH}" = "i686" ]; then
    SSE2="--disable-sse"
  fi

  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr ${SSE2}
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
