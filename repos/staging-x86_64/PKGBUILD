# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: kusakata <shohei atmark kusakata period com>

pkgname=libixion
pkgver=0.13.0
pkgrel=4
pkgdesc="A general purpose formula parser & interpreter"
arch=('x86_64')
url="https://gitlab.com/ixion/ixion/blob/master/README.md"
license=('custom')
depends=('boost-libs' 'python')
makedepends=('boost' 'mdds')
source=("https://kohei.us/files/ixion/src/libixion-${pkgver}.tar.xz")
sha256sums=('5ae360c52ba2d17c4abf5ae21fa947f75925459e085acef5972395f77333c7e5')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr \
    --disable-static
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make -k check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  # LICENSE file is missing in tarball
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

