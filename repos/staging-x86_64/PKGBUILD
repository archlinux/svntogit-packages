# Maintainer : Tom Gundersen <teg@jklm.no>
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Gabriel Martinez < reitaka at gmail dot com >

pkgname=libplist
pkgver=2.0.0+11+gec9ba8b
pkgrel=4
pkgdesc="A library to handle Apple Property List format whereas it's binary or XML"
url="https://libimobiledevice.org/"
arch=('x86_64')
license=('GPL2' 'LGPL2.1')
depends=('glib2')
makedepends=('python2' 'cython2' 'python' 'cython' 'autoconf-archive' git python-setuptools python2-setuptools)
options=('!makeflags')
_commit=ec9ba8bff6a4469aa75f6d570ab5109c061c6a9e  # master
source=("git+https://github.com/libimobiledevice/libplist#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

check() {
  cd build-py2
  make check
}

prepare() {
  cd $pkgname
  # Update ac_python_devel.m4 to newer ax_python_devel.m4
  git cherry-pick -n 42bb64ba96
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  mkdir build-py2
  pushd build-py2
  PYTHON=/usr/bin/python2 CYTHON=/usr/bin/cython2 ../$pkgname/configure --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
  popd

  mkdir build-py3
  pushd build-py3
  PYTHON=/usr/bin/python CYTHON=/usr/bin/cython ../$pkgname/configure --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
  popd
}

package() {
  pushd build-py2
  make DESTDIR="$pkgdir" install
  popd
  pushd build-py3/cython
  make DESTDIR="$pkgdir" install
  popd
  install -D -m644 $pkgname/cython/plist.pxd "${pkgdir}"/usr/include/plist/cython/plist.pxd
}
