# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gom
pkgver=0.4
pkgrel=3
pkgdesc="A GObject to SQLite object mapper"
arch=(x86_64)
url="https://wiki.gnome.org/Projects/Gom"
license=(LGPL2.1)
depends=(glib2 sqlite)
makedepends=(gtk-doc gobject-introspection gdk-pixbuf2 git meson python-gobject)
provides=(libgom-1.0.so)
_commit=26816b74a29b9d2d8dc169ed2f9649a4a159bc7c  # tags/0.4^0
source=("git+https://gitlab.gnome.org/GNOME/gom.git#commit=$_commit")
sha256sums=('SKIP')
 
pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build -D enable-gtk-doc=true
  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}

# vim:set ts=2 sw=2 et:
