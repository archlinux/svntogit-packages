# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Mirco Tischler <mt-ml at gmx dot de>

pkgname=ostree
_commit=46c51b3537c0c02e815d9624479e461c84f7d374  # tags/v2022.7
pkgver=2022.7
pkgrel=1
pkgdesc="Operating system and container binary deployment and upgrades"
url="https://ostreedev.github.io/ostree/"
arch=(x86_64)
license=(custom:LGPL2)
depends=(fuse3 glibc libgpg-error libsodium util-linux zlib)
makedepends=(
  avahi
  curl
  e2fsprogs
  git
  glib2
  gobject-introspection
  gpgme
  gtk-doc
  libarchive
  libsoup
  libxslt
  mkinitcpio
  openssl
  python
  systemd
  xz
)
checkdepends=(cpio elfutils gjs parallel python-yaml syslinux)
provides=(libostree-1.so)
source=(
  git+https://github.com/ostreedev/ostree#tag=$_commit
  git+https://github.com/mendsley/bsdiff
  git+https://gitlab.gnome.org/GNOME/libglnx.git
  $pkgname-2022.6-use_fuse3.patch
)
b2sums=('SKIP'
        'SKIP'
        'SKIP'
        '82a8f557f4f21a0a769709c8e2ffb9165a8f3ad60e2d9b6722f3d06a34b102a22cd137f77530e6dc9d84a9dcdf429c5907653022f07f368c2bb2703711aab435')
# upstream ticket about chain of trust:
# https://github.com/ostreedev/ostree/issues/2349
validpgpkeys=('1CEC7A9DF7DA85ABEF843DC0A866D7CCAE087291') # Colin Walters <walters@redhat.com>

prepare() {
  cd $pkgname

  # remove failing test: https://github.com/ostreedev/ostree/issues/2790
  sed -e '/test-signed-pull-summary.sh/d' -i Makefile-tests.am

  # use fusemount3 (fuse3)
  git apply -3 ../$pkgname-2022.6-use_fuse3.patch

  git submodule init
  git submodule set-url bsdiff "$srcdir/bsdiff"
  git submodule set-url libglnx "$srcdir/libglnx"
  git -c protocol.file.allow=always submodule update

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  local configure_options=(
    --prefix=/usr
    --sysconfdir=/etc
    --localstatedir=/var
    --sbindir=/usr/bin
    --libexecdir=/usr/lib
    --disable-static
    --enable-experimental-api
    --enable-gtk-doc
    --with-builtin-grub2-mkconfig
    --with-curl
    --with-dracut
    --with-ed25519-libsodium
    --with-mkinitcpio
    --with-openssl
  )

  cd $pkgname
  ./configure "${configure_options[@]}"
  # prevent overlinking due to libtool
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  make check -k -C $pkgname
}

package() {
  depends+=(
    avahi libavahi-client.so libavahi-common.so libavahi-glib.so
    curl libcurl.so
    glib2 libgio-2.0.so libglib-2.0.so libgobject-2.0.so
    gpgme libgpgme.so
    libarchive libarchive.so
    libsoup libsoup-2.4.so
    openssl libcrypto.so
    systemd-libs libsystemd.so
    xz liblzma.so
  )

  make DESTDIR="$pkgdir" install -C $pkgname
  install -vDm 644 $pkgname/COPYING -t "$pkgdir/usr/share/licenses/$pkgname/"
}
