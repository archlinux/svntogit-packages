# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kross
pkgver=r5.c2cccc2
pkgrel=1
pkgdesc='Kross'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kross'
license=('LGPL')
depends=('kparts')
makedepends=('extra-cmake-modules' 'git' 'kdoctools')
groups=('kf5')
source=('git://anongit.kde.org/kross.git')
md5sums=('SKIP')

pkgver() {
  cd kross
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build

  # There's something wrong with our kdoctools but I don't get it
  sed -i 's/add_subdirectory(docs)/#add_subdirectory(docs)/' \
    kross/CMakeLists.txt
}

build() {
  cd build
  cmake ../kross \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
