# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgbase=pyqt6-charts
pkgname=(python-pyqt6-charts)
pkgver=6.3.1
pkgrel=1
pkgdesc='Python bindings for QtChart'
arch=(x86_64)
url='https://www.riverbankcomputing.com/software/pyqtchart/intro'
license=(GPL3)
groups=(pyqt6)
depends=(qt6-charts python-pyqt6)
makedepends=(sip pyqt-builder)
source=(https://pypi.python.org/packages/source/P/PyQt6-Charts/PyQt6_Charts-$pkgver.tar.gz)
sha256sums=('e6bbb17a3d5503508cb28a7b8f44dfedd659c43ff62adb64182a004fbd968f2f')
options=(debug)

build() {
  cd PyQt6_Charts-$pkgver
  sip-build \
    --no-make \
    --qmake=/usr/bin/qmake6
  cd build
  make
}

package_python-pyqt6-charts(){
  cd PyQt6_Charts-$pkgver/build
  make INSTALL_ROOT="$pkgdir" install
}
