# $Id$
# Maintainer: Tom Gundersen <teg@jklm.no>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=ypbind-mt
pkgver=1.37.1
pkgrel=1
pkgdesc='Linux NIS daemon'
url='http://www.linux-nis.org/nis/ypbind-mt/'
license=('GPL2')
arch=('i686' 'x86_64')
makedepends=('networkmanager')
depends=('rpcbind' 'openslp' 'dbus-glib' 'yp-tools')
backup=('etc/yp.conf' 'etc/conf.d/ypbind' 'etc/conf.d/nisdomainname')
source=("http://www.linux-nis.org/download/${pkgname}/${pkgname}-${pkgver}.tar.bz2"
	'ypbind.service')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	./configure \
		--prefix=/usr \
		--enable-dbus-nm \

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make DESTDIR="${pkgdir}" install

	install -D -m644 etc/yp.conf "${pkgdir}"/etc/yp.conf
	install -D -m644 ../ypbind.service "${pkgdir}"/usr/lib/systemd/system/ypbind.service
	install -d -m755 "${pkgdir}"/var/yp/binding
}
md5sums=('8af8d35e7b9a7fcc3a1576697a04bd82'
         '88a721095e334cd442f4649a151ba9be')
