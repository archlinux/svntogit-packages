# $Id$
# Maintainer: judd <jvinet@zeroflux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=ypbind-mt
pkgver=1.32
pkgrel=1
pkgdesc='Linux NIS daemon'
arch=('i686' 'x86_64')
url='http://www.linux-nis.org/nis/ypbind-mt/'
license=('GPL2')
depends=('rpcbind' 'openslp')
backup=('etc/yp.conf' 'etc/conf.d/ypbind')
source=("ftp://ftp.kernel.org/pub/linux/utils/net/NIS/$pkgname-$pkgver.tar.gz"
        'ypbind.conf'
        'ypbind')
sha1sums=('136d1cb1499d32d01f7efa216352911980a7dda8'
          '07dee386d001fb9e9e6b76dda8af5b2092e5a4a2'
          '66f6ea2f622e0724e6017bf835d4f7f4a5bf9534')

build() {
	cd "$srcdir/$pkgname-$pkgver"
	./configure --prefix=/usr --disable-dbus-nm
	make
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install
	install -D -m644 etc/yp.conf "$pkgdir"/etc/yp.conf
	install -D -m755 ../ypbind "$pkgdir"/etc/rc.d/ypbind
	install -D -m644 ../ypbind.conf "$pkgdir"/etc/conf.d/ypbind
	install -d -m755 "$pkgdir"/var/yp/binding
}
