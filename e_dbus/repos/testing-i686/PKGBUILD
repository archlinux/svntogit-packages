# $Id$
# Maintainer: Ronald van Haren <ronald@archlinux.org>
# Contributor: Gustavo Sverzut Barbieri <barbieri@gmail.com>

pkgname=e_dbus
pkgver=1.7.9
pkgrel=2
pkgdesc="D-Bus integration with Ecore"
arch=('i686' 'x86_64')
url="http://www.enlightenment.org"
license=('BSD')
depends=('efl' 'dbus-core')
options=('!emptydirs')
source=(http://download.enlightenment.org/releases/$pkgname-$pkgver.tar.gz)
sha1sums=('d1c30cf95cf6eea07c5be54bd8cfbf7eba9fb20b')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  export CFLAGS="${CFLAGS} -fvisibility=hidden"

  ./configure \
    --prefix=/usr \
    --disable-static \
    --disable-ebluez \
    --disable-econnman0_7x \
    --disable-ehal \
    --enable-enotify \
    --disable-eofono \
    --enable-eukit \
    --disable-edbus-test \
    --disable-edbus-test-client \
    --disable-edbus-bluez-test \
    --disable-edbus-connman0_7x-test \
    --disable-edbus-ofono-test \
    --disable-edbus-async-test \
    --disable-edbus-performance-test \
    --disable-doc
  make
}

package(){
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  # install license files
  install -Dm644 "${srcdir}/${pkgname}-${pkgver}/COPYING" \
        "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

