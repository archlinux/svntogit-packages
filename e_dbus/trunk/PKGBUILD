# $Id$
# Maintainer: Ronald van Haren <ronald@archlinux.org>
# Contributor: Gustavo Sverzut Barbieri <barbieri@gmail.com>

pkgname=e_dbus
pkgver=1.7.2
pkgrel=1
pkgdesc="D-Bus integration with Ecore"
arch=('i686' 'x86_64')
url="http://www.enlightenment.org"
license=('BSD')
depends=('ecore' 'dbus-core')
options=('!libtool' '!emptydirs')
source=(http://download.enlightenment.org/releases/$pkgname-$pkgver.tar.gz)
sha1sums=('119af7ddeabf65e294c998ba27a7486027a5e67c')

build() {
  cd $srcdir/$pkgname-$pkgver

  export CFLAGS="-fvisibility=hidden -ffunction-sections -fdata-sections ${CFLAGS}"
  export CXXFLAGS="-fvisibility=hidden -ffunction-sections -fdata-sections ${CXXFLAGS}"
  export LDFLAGS="-fvisibility=hidden -ffunction-sections -fdata-sections -Wl,--gc-sections -Wl,--as-needed  ${LDFLAGS}"

  ./configure \
    --prefix=/usr \
    --disable-static \
    --disable-ebluez \
    --disable-econnman0_7x \
    --disable-ehal \
    --enable-enotify \
    --disable-eofono \
    --disable-eukit \
    --disable-edbus-test \
    --disable-edbus-test-client \
    --disable-edbus-bluez-test \
    --disable-edbus-connman0_7x-test \
    --disable-edbus-ofono-test \
    --disable-edbus-async-test \
    --disable-edbus-performance-test \
    --disable-doc
  make
}

package(){
  cd $srcdir/$pkgname-$pkgver

  make DESTDIR=$pkgdir install

  # install license files
  install -Dm644 $srcdir/$pkgname-$pkgver/COPYING \
        $pkgdir/usr/share/licenses/$pkgname/COPYING
}
