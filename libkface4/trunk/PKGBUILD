# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=libkface4
_pkgname=libkface
pkgver=15.08.3
pkgrel=3
pkgdesc="A Qt/C++ wrapper around LibFace library to perform face recognition and detection over pictures"
url="https://projects.kde.org/projects/kde/kdegraphics/libs/libkface"
arch=(i686 x86_64)
license=(GPL LGPL FDL)
depends=(kdelibs opencv)
makedepends=(cmake automoc4)
replaces=('libkface<15.11')
conflicts=('libkface<15.11')
install=$pkgname.install
source=("http://download.kde.org/stable/applications/$pkgver/src/$_pkgname-$pkgver.tar.xz"
        libkface4-opencv3.patch libkface4-opencv3.1.patch)
sha1sums=('cc65ff362ffad872917aac0a184c6903a90b3cbe'
          '5bfab370d1bb90bcf14bc75ed31f8d53971eb7fb'
          '244bb5ec8b2f4784b98532ec2db75aad362bef29')

prepare() {
  mkdir -p build

  cd $_pkgname-$pkgver
# Support opencv 3
  patch -p1 -i ../libkface4-opencv3.patch
# Fix build with opencv 3.1
  patch -p1 -i ../libkface4-opencv3.1.patch
}

build() {
  cd build
  cmake ../$_pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_OPENCV3=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
