# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=freetype2
pkgver=2.5.0.1
pkgrel=1
pkgdesc="TrueType font rendering library"
arch=(i686 x86_64)
license=('GPL')
url="http://freetype.sourceforge.net"
depends=('zlib' 'bzip2' 'sh')
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/freetype/freetype-${pkgver}.tar.bz2
        freetype-2.2.1-enable-valid.patch
        freetype-2.3.0-enable-spr.patch
        freetype-2.4.11-enable-sph.patch)
md5sums=('c72e9010b1d986d556fc0b2b5fcbf31a'
         '214119610444c9b02766ccee5e220680'
         '816dc8619a6904a7385769433c0a8653'
         '2422f8902224fedb46ae9eec08c8ceac')

prepare() {
  cd "${srcdir}/freetype-${pkgver}"
  patch -Np1 -i "${srcdir}/freetype-2.2.1-enable-valid.patch"
  patch -Np1 -i "${srcdir}/freetype-2.3.0-enable-spr.patch"

  # Disabled for now due to resistance
  # Kept here for easier rebuilds via ABS
  # https://bugs.archlinux.org/task/35274
  #patch -Np1 -i "${srcdir}/freetype-2.4.11-enable-sph.patch"
}

build() {
  cd "${srcdir}/freetype-${pkgver}"

  # PNG support is useless if FT_CONFIG_OPTION_USE_PNG is disabled
  ./configure --prefix=/usr --disable-static \
    --without-png

  make
}

check() {
  cd "${srcdir}/freetype-${pkgver}"
  make -k check
}

package() {
  cd "${srcdir}/freetype-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
