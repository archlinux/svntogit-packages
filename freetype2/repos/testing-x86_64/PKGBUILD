# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=freetype2
pkgname=('freetype2' 'freetype2-demos' 'freetype2-docs')
pkgver=2.10.2
pkgrel=1
pkgdesc="Font rasterization library"
arch=(x86_64)
license=('GPL')
url="https://www.freetype.org/"
# adding harfbuzz for improved OpenType features auto-hinting
# introduces a cycle dep to harfbuzz depending on freetype wanted by upstream
depends=('zlib' 'bzip2' 'sh' 'libpng' 'harfbuzz')
makedepends=('libx11')
source=(https://download-mirror.savannah.gnu.org/releases/freetype/freetype-${pkgver}.tar.xz{,.sig}
        https://download-mirror.savannah.gnu.org/releases/freetype/freetype-doc-${pkgver}.tar.xz{,.sig}
        https://download-mirror.savannah.gnu.org/releases/freetype/ft2demos-${pkgver}.tar.xz{,.sig}
        0001-Enable-table-validation-modules.patch
        0002-Enable-infinality-subpixel-hinting.patch
        0003-Enable-long-PCF-family-names.patch
        freetype-2.5.2-more-demos.patch
        freetype2.sh)
sha1sums=('b074d5c34dc0e3cc150be6e7aa6b07c9ec4ed875'
          'SKIP'
          '97060579e8e66ff870e07567cba3c21e5b6f0e1a'
          'SKIP'
          '7b2001a0269be6c619d81d21944dd054fa9f2328'
          'SKIP'
          '43919bfbf8fcc0e8df2659af6a1f09d338286fcf'
          'a3267cfeb115a06ad5dd6e4a74b44778e3e6bbb6'
          'c3903dc78dd0c233075fb33077d1e134b9d148d8'
          '72cfecbe738085eec475e012617661ad0cc9b76f'
          'bc6df1661c4c33e20f5ce30c2da8ad3c2083665f')
validpgpkeys=('58E0C111E39F5408C5D3EC76C1A60EACE707FDA5')

prepare() {
  # Rename source dir to allow building the demos
  mv freetype-${pkgver} freetype2
  mv ft2demos-${pkgver} freetype2-demos

  cd freetype2
  patch -Np1 -i ../0001-Enable-table-validation-modules.patch
  patch -Np1 -i ../0002-Enable-infinality-subpixel-hinting.patch
  patch -Np1 -i ../0003-Enable-long-PCF-family-names.patch

  cd ../freetype2-demos
  # enable more demos
  patch -Np1 -i ../freetype-2.5.2-more-demos.patch

  # Suppress RPATH
  sed -i '/X11_LIB:%=-R%/d' graph/x11/rules.mk
}

build() {
  cd freetype2
  ./configure --prefix=/usr --disable-static
  make

  # Build demos
  cd ../freetype2-demos
  make
}

check() {
  cd freetype2
  make -k check
}

package_freetype2() {
  provides=('libfreetype.so')
  install=freetype2.install
  backup=('etc/profile.d/freetype2.sh')

  cd freetype2
  make DESTDIR="${pkgdir}" install
  install -Dt "${pkgdir}/etc/profile.d" -m644 ../freetype2.sh
}

package_freetype2-demos() {
  pkgdesc="Freetype tools and demos"
  depends=('freetype2' 'libx11')

  cd freetype2-demos
  install -d "${pkgdir}/usr/bin"
  for _i in bin/{f,t}t*; do
    libtool --mode=install install $_i "${pkgdir}/usr/bin"
  done
}

package_freetype2-docs() {
  pkgdesc="Freetype documentation"
  depends=('freetype2')

  cd freetype2
  install -d "${pkgdir}/usr/share/doc"
  cp -a docs "${pkgdir}/usr/share/doc/freetype2"
}

# vim:set ts=2 sw=2 et:
