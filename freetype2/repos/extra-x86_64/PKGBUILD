# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=freetype2
pkgver=2.3.12
pkgrel=1
pkgdesc="TrueType font rendering library"
arch=(i686 x86_64)
license=('GPL')
url="http://freetype.sourceforge.net"
depends=('zlib')
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/freetype/freetype-${pkgver}.tar.bz2
        bytecode.patch
        freetype-2.3.0-enable-spr.patch
        freetype-2.2.1-enable-valid.patch
        freetype-2.2.1-memcpy-fix.patch)
md5sums=('e974a82e5939be8e05ee65f07275d7c5'
         '9ff19e742968c29e3ba52b08d6bf0a50'
         '816dc8619a6904a7385769433c0a8653'
         '214119610444c9b02766ccee5e220680'
         '6fb6606d28082ecb8e0c6d986b0b26aa')

build() {
  cd "${srcdir}/freetype-${pkgver}"
  patch -Np0 -i "${srcdir}/bytecode.patch" || return 1
  patch -Np1 -i "${srcdir}/freetype-2.3.0-enable-spr.patch" || return 1
  patch -Np1 -i "${srcdir}/freetype-2.2.1-enable-valid.patch" || return 1
  patch -Np1 -i "${srcdir}/freetype-2.2.1-memcpy-fix.patch" || return 1

  ./configure --prefix=/usr || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
}
