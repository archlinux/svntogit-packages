# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=freetype2
pkgver=2.5.3
pkgrel=2
pkgdesc="TrueType font rendering library"
arch=(i686 x86_64)
license=('GPL')
url="http://freetype.sourceforge.net"
# adding harfbuzz for improved OpenType features auto-hinting 
# introduces a cycle dep to harfbuzz depending on freetype wanted by upstream
depends=('zlib' 'bzip2' 'sh' 'libpng' 'harfbuzz')
source=(http://downloads.sourceforge.net/sourceforge/freetype/freetype-${pkgver}.tar.bz2{,.sig}
        freetype-2.2.1-enable-valid.patch
        freetype-2.5.1-enable-spr.patch
        freetype-2.5.1-enable-sph.patch
        fix_segfault_with_harfbuzz.diff)
sha1sums=('d3c26cc17ec7fe6c36f4efc02ef92ab6aa3f4b46'
          'SKIP'
          'f279d922a873d62a8af50bfc873051839d194dca'
          '13ee8d558593db991ad29fa090b461f914536104'
          'c31fa3d342ead56f3acfa1f267b474a7686d0014'
          'c99013b4c19494cd26031878368191cd2b4459b0')

prepare() {
  cd "${srcdir}/freetype-${pkgver}"
  patch -Np1 -i "${srcdir}/freetype-2.2.1-enable-valid.patch"
  patch -Np1 -i "${srcdir}/freetype-2.5.1-enable-spr.patch"
  
  # fix segfaults # https://bugs.archlinux.org/task/39365
  # http://git.savannah.gnu.org/cgit/freetype/freetype2.git/commit/?id=23367ff97f33ef6a2b7e1fced1157c87a46d9596
  patch -Np1 -i "${srcdir}/fix_segfault_with_harfbuzz.diff"
  
  # Disabled for now due to resistance
  # Kept here for easier rebuilds via ABS
  # https://bugs.archlinux.org/task/35274
  #patch -Np1 -i "${srcdir}/freetype-2.5.1-enable-sph.patch"
}

build() {
  cd "${srcdir}/freetype-${pkgver}"
  ./configure --prefix=/usr --disable-static
  make
}

check() {
  cd "${srcdir}/freetype-${pkgver}"
  make -k check
}

package() {
  cd "${srcdir}/freetype-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
