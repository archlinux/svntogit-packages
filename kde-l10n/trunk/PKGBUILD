# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kde-l10n
pkgver=15.08.1
pkgrel=1
arch=('any')
url='http://www.kde.org/'
license=('GPL')
makedepends=('extra-cmake-modules' 'kdoctools' 'kdelibs' 'automoc4' 'kconfig' 'ki18n' 'qt5-tools')
options=('docs')

_languages=(
  'ar     "Arabic"'
  'bg     "Bulgarian"'
  'bs     "Bosnian"'
  'ca     "Catalan"'
  'ca@valencia     "Catalan (Valencian)"'
  'cs     "Czech"'
  'da     "Danish"'
  'de     "German"'
  'el     "Greek"'
  'en_GB  "British English"'
  'eo     "Esperanto"'
  'es     "Spanish"'
  'et     "Estonian"'
  'eu     "Basque"'
  'fa     "Farsi"'
  'fi     "Finnish"'
  'fr     "French"'
  'ga     "Irish"'
  'gl     "Galician"'
  'he     "Hebrew"'
  'hi     "Hindi"'
  'hr     "Croatian"'
  'hu     "Hungarian"'
  'ia     "Interlingua"'
  'id     "Indonesian"'
  'is     "Icelandic"'
  'it     "Italian"'
  'ja     "Japanese"'
  'kk     "Kazakh"'
  'km     "Khmer"'
  'ko     "Korean"'
  'lt     "Lithuanian"'
  'lv     "Latvian"'
  'mr     "Marathi"'
  'nb     "Norwegian Bokmal"'
  'nds    "Low Saxon"'
  'nl     "Dutch"'
  'nn     "Norwegian Nynorsk"'
  'pa     "Panjabi/Punjabi"'
  'pl     "Polish"'
  'pt     "Portuguese"'
  'pt_BR  "Brazilian Portuguese"'
  'ro     "Romanian"'
  'ru     "Russian"'
  'sk     "Slovak"'
  'sl     "Slovenian"'
  'sr     "Serbian"'
  'sv     "Swedish"'
  'tr     "Turkish"'
  'ug     "Uyghur"'
  'uk     "Ukrainian"'
  'wa     "Walloon"'
  'zh_CN  "Chinese Simplified"'
  'zh_TW  "Chinese Traditional"'
)

pkgname=()
source=()

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}
  _pkgname=kde-l10n-${_locale,,}
  
  pkgname+=($_pkgname)
  source+=("http://download.kde.org/stable/applications/$pkgver/src/$pkgbase/$pkgbase-$_locale-$pkgver.tar.xz")
  eval "package_$_pkgname() {
    _package $_lang
  }"
done

md5sums=('483afd1391ab805a97a4b7a2b50494b3'
         '79420d2ce52080908b03f9b519381305'
         'dc5b64af3ef9037b9d223a28e97cdec4'
         '408fcfe87a5165947cf9849cd1298594'
         'cd18c92302648cf1c315cd0ccc80e4bc'
         '4d5f7d0cff7fd4a3234c3a4657f9967a'
         '7516d09131ccf14ea50d19ef1f1364f5'
         '5ec493bab72890c40118c222b97cd956'
         'fd4813af484f2a392f702e483dce5dd4'
         '1aa0aff785020a09fa55f1775d57e57d'
         '8dd5236a3f445162aad86d58756ec5a5'
         '9465e25a69e9b84fc714780ae2537802'
         '6afb5726ef1c866035ededccf44a735b'
         '2ea8b03e0ea33d3c6490417faeb30424'
         '19ac3abbdb262b8d4e932b444590949f'
         '182fa87407b67143f49043623f05214b'
         'a6984a2a348e4f67e3162fe19c2a28c1'
         '413a78029ba45fee1105c9ba5b9af996'
         'acb2d10c4847f6a0e459d787ff0158c9'
         'd2d2a8161a60e6c8c861ee8245952dad'
         '70d15c6542fb1e4612bf7d2cfdd4cdce'
         'd4cac55d6932ae3ad64ed339dea62cb4'
         'e64cb929451833147836827d65d51b35'
         'a1efebfb18bc3aac8639db050835a19c'
         '51b9f127300ec8690603cbc4cc1375b3'
         '9f653e2868a75a34d59ac014f81d8493'
         'a375ca9bd52d664dca50101ced4526a3'
         'a2de0538fc1f0c89d6afaa5604aa1baa'
         '4d10ea5826e759a1c0051afeb8485714'
         '4868210635500f59c4ae807a3a3e851c'
         '94698c11025a19c621cf4d958cbcf222'
         '3548d0d1a5bd3b34ab10f1f01eb5ea6d'
         '5e1a852edb0cc681a669b6608b507bff'
         '8c5cbbe4f96551cbd40b7b24f9af4b0e'
         '14536c2c792d88c2e332e1c65edad940'
         '8dc63e4eb61b89e13858c973fa01d586'
         '91ea71f79eed8320a39a916eb44f16f2'
         '509b9adc683d380e0160994fd4b72d53'
         '3d67c2be279542200e04f702e217c6ff'
         '9871283b7af43cacf916d53035248e9c'
         '4cf05bff1e62da88e99c727a72cf87c2'
         '128c88e200e1d3c74a0d27a9fdf27d10'
         'd086dd8ac05f26cff712a3489bd811db'
         'f2ac464bdcab11e1822bb7dac864ca34'
         '3e4ccd1527fe3842610d9bee7d56258d'
         '3a2e31511a98be88cc81f3dcdb9d8061'
         'dc552a7aa48c47a0f0dd971f39c2be7f'
         'b30edbbdc27de65ace307ad4645c143d'
         'c8f2083816ab054f1e80d361042ac859'
         '91b9b9b889a126aa5907ba100a21d998'
         '3a60ce9a749af0c263a876ef0b5ebc24'
         '5cd7cccb38253140cc5221e7896a0108'
         'f8344f45d9cd98c8971eb458c3ff0a7e'
         '00d14b1035f8b649542b7d6e4dc3b78f')

prepare() {
  for _lang in "${_languages[@]}"; do
    _locale=${_lang%% *}

# Remove kde-runtime and kde-workspace docs, conflicts with plasma-desktop
  sed -e '/kde-runtime/d' -e '/kde-workspace/d' -i $pkgbase-$_locale-$pkgver/4/$_locale/docs/CMakeLists.txt || true

# Provide KDE4 PIM translations TODO: Remove in 15.12
  for _m in kdepim kdepim-runtime kdepimlibs ; do
    if [[ -d "$pkgbase-$_locale-$pkgver/5/$_locale/messages/$_m" ]] ; then
      cp -r $pkgbase-$_locale-$pkgver/{5,4}/$_locale/messages/$_m
      cp $pkgbase-$_locale-$pkgver/4/$_locale/messages/{kdelibs,$_m}/CMakeLists.txt
      echo "add_subdirectory($_m)" >> $pkgbase-$_locale-$pkgver/4/$_locale/messages/CMakeLists.txt
    fi
  done
  done
}

build() {
  for _lang in "${_languages[@]}"; do
    _locale=${_lang%% *}
    
    mkdir -p build/$_locale
    cd build/$_locale
    cmake $srcdir/$pkgbase-$_locale-$pkgver \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX=/usr
    make
    cd ../..
  done
}

_package() {
  pkgdesc="$2 localization for KDE"
  cd build/$1
  make DESTDIR="$pkgdir" install
}
