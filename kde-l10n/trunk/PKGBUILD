# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kde-l10n
pkgver=15.04.1
pkgrel=1
arch=('any')
url='http://www.kde.org/'
license=('GPL')
makedepends=('extra-cmake-modules' 'kdoctools' 'kdelibs' 'automoc4' 'kconfig' 'ki18n' 'qt5-tools')
options=('docs')

_languages=(
  'ar     "Arabic"'
  'bg     "Bulgarian"'
  'bs     "Bosnian"'
  'ca     "Catalan"'
  'ca@valencia     "Catalan (Valencian)"'
  'cs     "Czech"'
  'da     "Danish"'
  'de     "German"'
  'el     "Greek"'
  'en_GB  "British English"'
  'eo     "Esperanto"'
  'es     "Spanish"'
  'et     "Estonian"'
  'eu     "Basque"'
  'fa     "Farsi"'
  'fi     "Finnish"'
  'fr     "French"'
  'ga     "Irish"'
  'gl     "Galician"'
  'he     "Hebrew"'
  'hi     "Hindi"'
  'hr     "Croatian"'
  'hu     "Hungarian"'
  'ia     "Interlingua"'
  'id     "Indonesian"'
  'is     "Icelandic"'
  'it     "Italian"'
  'ja     "Japanese"'
  'kk     "Kazakh"'
  'km     "Khmer"'
  'ko     "Korean"'
  'lt     "Lithuanian"'
  'lv     "Latvian"'
  'mr     "Marathi"'
  'nb     "Norwegian Bokmal"'
  'nds    "Low Saxon"'
  'nl     "Dutch"'
  'nn     "Norwegian Nynorsk"'
  'pa     "Panjabi/Punjabi"'
  'pl     "Polish"'
  'pt     "Portuguese"'
  'pt_BR  "Brazilian Portuguese"'
  'ro     "Romanian"'
  'ru     "Russian"'
  'sk     "Slovak"'
  'sl     "Slovenian"'
  'sr     "Serbian"'
  'sv     "Swedish"'
  'tr     "Turkish"'
  'ug     "Uyghur"'
  'uk     "Ukrainian"'
  'wa     "Walloon"'
  'zh_CN  "Chinese Simplified"'
  'zh_TW  "Chinese Traditional"'
)

pkgname=()
source=()

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}
  _pkgname=kde-l10n-${_locale,,}
  
  pkgname+=($_pkgname)
  source+=("http://download.kde.org/stable/applications/$pkgver/src/$pkgbase/$pkgbase-$_locale-$pkgver.tar.xz")
  eval "package_$_pkgname() {
    _package $_lang
  }"
done

md5sums=('6cb33a3acc67095f4a67f2bade03c348'
         '0a60bedcbe3902831f125f3d8c2d47b2'
         '017ced382f2c4026ea189255fd4f1946'
         '53d8155b3fd8676fa3dd56d8c8c7876a'
         'c956c4a3488e4f0325aac2e63c612323'
         '678f6eb8ebfff91eaca910a128865a54'
         '431c4da4476a098610e4cfa8bfa88006'
         '863c3119925946df153aae16f939709d'
         '6c7debcb96ed354f6c22dd165ba6c52c'
         'dac6480124aa4ce9ce9ca4aa14576d20'
         '848fd3a6546efc04ac5c9eec25304c89'
         '6d5ece204807ce12508f06e7cb9f8651'
         '4c783ae8e8d480648e6b6546e3cf027b'
         '51a15e3c24245a05124bdcc1863d4845'
         'b4d55bf3a594d08479e4894a940dd39e'
         'b4abd527a524bcb68fa5cc7473942cd5'
         '3a4adac34f2b4c38891cbedf1d3cd28b'
         '15e1f9b08b1972ef0d79348287772dc8'
         '807d905256b0741214889bb4992d8c69'
         '4ecffc2cdca805315915618d5ec89956'
         'ff3c63290161a2b2bc6005df1a2b6488'
         '486d29540faf591a1f05040643b0aebc'
         '60f09eb203235a296d6706d9fe4196ac'
         '09db8e05bf2f67afe8fe53278c9d6d90'
         'f7b882761142861ac322b92c503840cc'
         '0d9716e34e776f949f436d67828af463'
         '4a0d2c7d16219a229128f899d5d09d3b'
         '07bec257da58407dc0c9050ce1bad656'
         'a62a4521c211677d5dfb896024de0ba1'
         '40708e5d490f6aeda80edd0f3eb5c87e'
         '532510be073fae0040a178f328b4f521'
         'f20f0654b874f6bd11b33c31f13c01e1'
         '8c95f8d6d39647afd634c05d740b118f'
         '3f7df4e07cc54af2a24a090130bab9b3'
         '161b40e7c1c724882c8e1ace50e0241f'
         '7c56937bd30aaafc842a51bed1561ac0'
         'bce68cd3b97fb8a5c5b5e33692aeaa7b'
         '07eae764b0807d511cda2e8db87505d9'
         '8cddc22bb1dbfc1d9813d4fed24aaa09'
         'da7873d0f5027247a47b32bcd9fadb9b'
         'b88c02183bd814986a07e45d94c5a834'
         '7c8dc96dd6662d3c208ad4956bc118bc'
         '7dd9edd682ba08b108219cb122bc0c3c'
         'ca4ecf6c32ac497a9fb57cb151b9935c'
         'ebcb15253e920c234ae446126e25dbbd'
         '2dc39b993dc9c21611097c5d5ed1ea3c'
         'f8b26a0ba290a3d7934e1fa832605ab2'
         '3c5e7c1acbb58c692230946710c544c4'
         '9d91c7723f135060d86e0195798ba1b9'
         '41f92b3baf7b9a4fadc3d1a2702bf8b1'
         '5e84d7ce3978649511702abb5460065f'
         '3f46881d445021a6d14d05937f9bed22'
         '18d1b1185f982067a482cc7a5b172de8'
         '82c9cf0f7f49d9646c846137a9e77454')

prepare() {
  for _lang in "${_languages[@]}"; do
    _locale=${_lang%% *}

# Remove kde-runtime and kde-workspace docs, conflicts with plasma-desktop
  sed -e '/kde-runtime/d' -e '/kde-workspace/d' -i $pkgbase-$_locale-$pkgver/4/$_locale/docs/CMakeLists.txt || true

  done
}

build() {
  for _lang in "${_languages[@]}"; do
    _locale=${_lang%% *}
    
    mkdir -p build/$_locale
    cd build/$_locale
    cmake $srcdir/$pkgbase-$_locale-$pkgver \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX=/usr
    make
    cd ../..
  done
}

_package() {
  pkgdesc="$2 localization for KDE"
  cd build/$1
  make DESTDIR="$pkgdir" install
}

