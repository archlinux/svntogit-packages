# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kde-l10n
pkgver=14.12.3
pkgrel=2
arch=('any')
url='http://www.kde.org/'
license=('GPL')
makedepends=('extra-cmake-modules' 'kdoctools' 'kdelibs' 'automoc4' 'kconfig' 'ki18n' 'qt5-tools')
options=('docs')

_languages=(
  'ar     "Arabic"'
  'bg     "Bulgarian"'
  'bs     "Bosnian"'
  'ca     "Catalan"'
  'ca@valencia     "Catalan (Valencian)"'
  'cs     "Czech"'
  'da     "Danish"'
  'de     "German"'
  'el     "Greek"'
  'en_GB  "British English"'
  'es     "Spanish"'
  'et     "Estonian"'
  'eu     "Basque"'
  'fi     "Finnish"'
  'fr     "French"'
  'ga     "Irish"'
  'gl     "Galician"'
  'he     "Hebrew"'
  'hi     "Hindi"'
  'hr     "Croatian"'
  'hu     "Hungarian"'
  'ia     "Interlingua"'
  'id     "Indonesian"'
  'is     "Icelandic"'
  'it     "Italian"'
  'ja     "Japanese"'
  'kk     "Kazakh"'
  'km     "Khmer"'
  'ko     "Korean"'
  'lt     "Lithuanian"'
  'lv     "Latvian"'
  'mr     "Marathi"'
  'nb     "Norwegian Bokmal"'
  'nds    "Low Saxon"'
  'nl     "Dutch"'
  'nn     "Norwegian Nynorsk"'
  'pa     "Panjabi/Punjabi"'
  'pl     "Polish"'
  'pt     "Portuguese"'
  'pt_BR  "Brazilian Portuguese"'
  'ro     "Romanian"'
  'ru     "Russian"'
  'sk     "Slovak"'
  'sl     "Slovenian"'
  'sr     "Serbian"'
  'sv     "Swedish"'
  'tr     "Turkish"'
  'ug     "Uyghur"'
  'uk     "Ukrainian"'
  'wa     "Walloon"'
  'zh_CN  "Chinese Simplified"'
  'zh_TW  "Chinese Traditional"'
)

pkgname=()
source=()

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}
  _pkgname=kde-l10n-${_locale,,}
  
  pkgname+=($_pkgname)
  source+=("http://download.kde.org/stable/applications/$pkgver/src/$pkgbase/$pkgbase-$_locale-$pkgver.tar.xz")
  eval "package_$_pkgname() {
    _package $_lang
  }"
done

md5sums=('1941628deb54f1dcba1d0bfcc02a6d90'
         '06876dca240bbb8311dc1e0fbce494ca'
         '251eb97fde1e69bcfb919971ac62151a'
         '6e4c40ab65699f10d3279361582f3a13'
         '7a182e253de577a8afa6c6ed85488888'
         '7c4ca4555707296f4e62760e793c954e'
         '215699381737b0e1870975da69c69fa5'
         '1bb2f2321169ab008a44675e221ba0f6'
         'ea418103d5aa97c3ae3a56399b25d288'
         '585d83593bc4e1653f81b8ce3b2eb161'
         '4c0bc643a28595511d894b8f5ec63a50'
         '64e9d731bbf593e25fea2ecd73e92452'
         'bd202a09c56ee192c9c70beb3bc86b0e'
         'abf153cf3e0726b3f52f02c3ee1de1d1'
         'a541b7703134594535c413a3b51a92f7'
         '07bc9a50885fcda2514bd3e3a9294249'
         'fadc566a4fbf3f48977a69fb03b8053a'
         'b653b0a310d4495b837235f24f9e8e18'
         'be51d45b60077092c00dda4aa0b54abf'
         'a7d69e5e379a75814e37b370f1b79bae'
         '6173f7e8de7626b4d47ae8c8c148faf5'
         'dbdf5776aee86c061def3bd924074130'
         'bd62febc1dbf1c48f1e5b4b906c88ab9'
         'bf3e6db1d9f68f8af6e020f16e74a511'
         '0ad5556bfa643452642eb95a62676c00'
         '79bb3860395ec165093dcdd30655bb2f'
         '8873df0b90e0edab2e112189a30e661a'
         'a10c781e35ce4d20865b1d9ff738497f'
         'c99b0a86031e426895df0e678533b08f'
         'c605f2b5b56bb2d51ada5758da980752'
         '55a76b7493d23e2ee937e55cde0af6e2'
         '777fac24dd1f4e4b520a444e17e6ea1b'
         '2570be8f90eadf52d121b04cd2ef0c5e'
         '465786102fd64e3df2b6b4b31c20337f'
         '56c82566a52318d2feb92d51a2fd7e04'
         '7bbf86b88bb2b2c67f5ce4ebf1bbe595'
         '1203db53f35513f9fff4b0c66ed8bd3b'
         '0365f6ad50dd8db8767fff6542c880c1'
         '2a937e7b8793d4bb5e1eec5b3a4fccdb'
         '82ebd2a33ed1020df79183a4e600bbb9'
         '9118c3f73251f1d3318e59704113a953'
         'bd4cdc2b627390e781414a0a47b3526a'
         'd32d59d08f68a022aade69950e8ad937'
         '4f3a9cdb396e7e2eaf0c4b52a5abf3e3'
         '9aca0a1f2d73c4e1705d662828aafa83'
         'de8bb68881771fcc042eb6ede3f29da0'
         '54b0a2d0963296aedece92dc641faa3e'
         '16eec389a0a7fbc070e6da51b401244a'
         '26ddd563bed5b42e22de150dff451d39'
         '4e4619281f59fe5cf34349145ca64070'
         '30d2757e2e3dd8c38e6f6f2eb85ee713'
         '565e14d09f7d47a1b0772fce37937fdd')

build() {
  for _lang in "${_languages[@]}"; do
    _locale=${_lang%% *}
    
    mkdir -p build/$_locale
    cd build/$_locale
    cmake $srcdir/$pkgbase-$_locale-$pkgver \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX=/usr
    make
    cd ../..
  done
}

_package() {
  pkgdesc="$2 localization for KDE"
  cd build/$1
  make DESTDIR="$pkgdir" install

# conflicts with telepathy-kde
  rm -f "$pkgdir"/usr/share/locale/kde4/$1/LC_MESSAGES/plasma_applet_org.kde.ktp*
}

