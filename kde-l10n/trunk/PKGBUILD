# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kde-l10n
pkgver=15.08.2
pkgrel=1
arch=('any')
url='http://www.kde.org/'
license=('GPL')
makedepends=('extra-cmake-modules' 'kdoctools' 'kdelibs' 'automoc4' 'kconfig' 'ki18n' 'qt5-tools')
options=('docs')

_languages=(
  'ar     "Arabic"'
  'bg     "Bulgarian"'
  'bs     "Bosnian"'
  'ca     "Catalan"'
  'ca@valencia     "Catalan (Valencian)"'
  'cs     "Czech"'
  'da     "Danish"'
  'de     "German"'
  'el     "Greek"'
  'en_GB  "British English"'
  'eo     "Esperanto"'
  'es     "Spanish"'
  'et     "Estonian"'
  'eu     "Basque"'
  'fa     "Farsi"'
  'fi     "Finnish"'
  'fr     "French"'
  'ga     "Irish"'
  'gl     "Galician"'
  'he     "Hebrew"'
  'hi     "Hindi"'
  'hr     "Croatian"'
  'hu     "Hungarian"'
  'ia     "Interlingua"'
  'id     "Indonesian"'
  'is     "Icelandic"'
  'it     "Italian"'
  'ja     "Japanese"'
  'kk     "Kazakh"'
  'km     "Khmer"'
  'ko     "Korean"'
  'lt     "Lithuanian"'
  'lv     "Latvian"'
  'mr     "Marathi"'
  'nb     "Norwegian Bokmal"'
  'nds    "Low Saxon"'
  'nl     "Dutch"'
  'nn     "Norwegian Nynorsk"'
  'pa     "Panjabi/Punjabi"'
  'pl     "Polish"'
  'pt     "Portuguese"'
  'pt_BR  "Brazilian Portuguese"'
  'ro     "Romanian"'
  'ru     "Russian"'
  'sk     "Slovak"'
  'sl     "Slovenian"'
  'sr     "Serbian"'
  'sv     "Swedish"'
  'tr     "Turkish"'
  'ug     "Uyghur"'
  'uk     "Ukrainian"'
  'wa     "Walloon"'
  'zh_CN  "Chinese Simplified"'
  'zh_TW  "Chinese Traditional"'
)

pkgname=()
source=()

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}
  _pkgname=kde-l10n-${_locale,,}
  
  pkgname+=($_pkgname)
  source+=("http://download.kde.org/stable/applications/$pkgver/src/$pkgbase/$pkgbase-$_locale-$pkgver.tar.xz")
  eval "package_$_pkgname() {
    _package $_lang
  }"
done

md5sums=('c3399443162b300bdc971408e77d5053'
         'ea17a9b0398149ae3f16bf8072d909e8'
         '720edcaf3185f0f97c266fbd7ea47457'
         '1069606916c6499f3f2f710232421989'
         '9881917348360168bcfc128bf21d4787'
         '3959164b8186f6a012341c69a57f889c'
         'a54cef4086a02b868bcb404015d00ba5'
         'd5cac551dd5d269aef064d741648f23b'
         'f84e64c001107e87e41b4db1f30d40f0'
         '4b6e1003e30c7dd398ebf3ee7dbdd9ad'
         '4b503a8660af81c86cf85c1e81e553a1'
         '87e915fe8061e8030afdebfd8858d762'
         '9817d6c2c9452be45b5b24860736ced5'
         '887cae9dbaddfd188f4d6d86e39250a5'
         '14e13200e46dc874468990ff069048fe'
         '3f797d12f8b7903d22176eaa03ff5a5b'
         '32e0fdb0e8c78860202eb2b48dc56a14'
         'fb48e8a8bf7e44c9bf2fbba589cfa279'
         'a067c77e6dcce4c5b0fd4dd467e474a2'
         'e7bb790da29275fa9610276797e22b9d'
         '49ba31205e3eda1aa2b7df3115754cb4'
         '0bbe13ed469c2f0e21efb21bf0a8c720'
         'f87c7b27714af83ee39948eb5be4a519'
         '97dcfb56b1d28791a330bf33211a411a'
         '739d683d58e6e11616cf649306bbe1d5'
         '59e1e9cc495ac6b630a3367f6690e9a9'
         'e5fcdb1742d396b0dae72f813ae0da55'
         '4563305d143c09788f8d6ccfa04ab931'
         '77a9dc89048a3f379d0fb04d47fe9a5d'
         'a5c5ad3c04d1607487377cf2b807a9f0'
         'ea651def2443089bf75bb38002ffa37a'
         'cbdb8ae61ff6c5f6294186a624db438f'
         '4df2ef6e66c2aabb548cd0690ab6cbe4'
         'e57880b86b8de978a39c4f47790a74f9'
         '1e3345fcfe8532003d784fec6b7db875'
         'bb9d2a78b8c6e9c19d2a79bc18e7cd26'
         'e7a810adc7135446f5a878481250c485'
         '8f8fe0de873be2497a608fc77ceef8a2'
         '096b1fd74f26f4284b2fdc8d41fc54f4'
         '90af0894892b969e14cd23618dbcae85'
         '585eeaeb03d3b92dfc70b6ebf609e24c'
         '469def7cce12dcfcb4e689c661a0b887'
         '26297424f66f0c74fea42ee5769fcf13'
         '1f316e97f11d6a1989e8701207c86fa0'
         '0eea9297cb747fa26c858eb6c055ae41'
         '003826f6092fdeb9b6c560f8593e1fa3'
         '2b0433bbed9b10df29349daff6da04f8'
         'd2ae27d77a88251cf051675913c63ccb'
         'b6913ed41823dd91bc91a48baf9a58ad'
         'd196c7b768e0d2e1d20b44b5a8e5c475'
         '8ce3e09d442179b18a3dab774f06c430'
         '3be60f45f993ba3dffa32f254dc8b7ec'
         '56eb01a786852acbd164bd1d0f58242f'
         '077110a444691f5a39da0953612c2e95')

prepare() {
  for _lang in "${_languages[@]}"; do
    _locale=${_lang%% *}

# Remove kde-runtime and kde-workspace docs, conflicts with plasma-desktop
  sed -e '/kde-runtime/d' -e '/kde-workspace/d' -i $pkgbase-$_locale-$pkgver/4/$_locale/docs/CMakeLists.txt || true

# Provide KDE4 PIM translations TODO: Remove in 15.12
  for _m in kdepim kdepim-runtime kdepimlibs ; do
    if [[ -d "$pkgbase-$_locale-$pkgver/5/$_locale/messages/$_m" ]] ; then
      cp -r $pkgbase-$_locale-$pkgver/{5,4}/$_locale/messages/$_m
      cp $pkgbase-$_locale-$pkgver/4/$_locale/messages/{kdelibs,$_m}/CMakeLists.txt
      echo "add_subdirectory($_m)" >> $pkgbase-$_locale-$pkgver/4/$_locale/messages/CMakeLists.txt
    fi
  done
  done
}

build() {
  for _lang in "${_languages[@]}"; do
    _locale=${_lang%% *}
    
    mkdir -p build/$_locale
    cd build/$_locale
    cmake $srcdir/$pkgbase-$_locale-$pkgver \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX=/usr
    make
    cd ../..
  done
}

_package() {
  pkgdesc="$2 localization for KDE"
  cd build/$1
  make DESTDIR="$pkgdir" install
}
