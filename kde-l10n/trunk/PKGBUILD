# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kde-l10n
pkgver=15.08.3
pkgrel=1
arch=('any')
url='http://www.kde.org/'
license=('GPL')
makedepends=('extra-cmake-modules' 'kdoctools' 'kdelibs' 'automoc4' 'kconfig' 'ki18n' 'qt5-tools')
options=('docs')

_languages=(
  'ar     "Arabic"'
  'bg     "Bulgarian"'
  'bs     "Bosnian"'
  'ca     "Catalan"'
  'ca@valencia     "Catalan (Valencian)"'
  'cs     "Czech"'
  'da     "Danish"'
  'de     "German"'
  'el     "Greek"'
  'en_GB  "British English"'
  'eo     "Esperanto"'
  'es     "Spanish"'
  'et     "Estonian"'
  'eu     "Basque"'
  'fa     "Farsi"'
  'fi     "Finnish"'
  'fr     "French"'
  'ga     "Irish"'
  'gl     "Galician"'
  'he     "Hebrew"'
  'hi     "Hindi"'
  'hr     "Croatian"'
  'hu     "Hungarian"'
  'ia     "Interlingua"'
  'id     "Indonesian"'
  'is     "Icelandic"'
  'it     "Italian"'
  'ja     "Japanese"'
  'kk     "Kazakh"'
  'km     "Khmer"'
  'ko     "Korean"'
  'lt     "Lithuanian"'
  'lv     "Latvian"'
  'mr     "Marathi"'
  'nb     "Norwegian Bokmal"'
  'nds    "Low Saxon"'
  'nl     "Dutch"'
  'nn     "Norwegian Nynorsk"'
  'pa     "Panjabi/Punjabi"'
  'pl     "Polish"'
  'pt     "Portuguese"'
  'pt_BR  "Brazilian Portuguese"'
  'ro     "Romanian"'
  'ru     "Russian"'
  'sk     "Slovak"'
  'sl     "Slovenian"'
  'sr     "Serbian"'
  'sv     "Swedish"'
  'tr     "Turkish"'
  'ug     "Uyghur"'
  'uk     "Ukrainian"'
  'wa     "Walloon"'
  'zh_CN  "Chinese Simplified"'
  'zh_TW  "Chinese Traditional"'
)

pkgname=()
source=()

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}
  _pkgname=kde-l10n-${_locale,,}
  
  pkgname+=($_pkgname)
  source+=("http://download.kde.org/stable/applications/$pkgver/src/$pkgbase/$pkgbase-$_locale-$pkgver.tar.xz")
  eval "package_$_pkgname() {
    _package $_lang
  }"
done

md5sums=('73b3dc8ff87a9fbbf848374b118bc8f1'
         '6264954abab5373b111c21122b834ff0'
         '89b337655336cf67788adf0311506933'
         'cc4101f85d1f48a2402cbd408c800bd6'
         '28af207baec6906bb0edea7616ac5915'
         'c05a81000e40390852b97fe52f28e040'
         '1f2b629ea856465d8a96a89f39caf3f2'
         'eb3a698a2fcce29fa353b1d113d24161'
         '5d5efb474cc33a51dead73f4d3fbb71b'
         '7c2516d21b19faca9872a537eedb5d43'
         '2737e07305749116dcdc10d03b305dbd'
         '55ecf2ca5e823c3adb677e7b70a5e4bd'
         '857214cddc2ae806a502680da231b8ec'
         'b662ca07a90fe8e1267a58a401740b78'
         '996419b96c36b8a7d5898e5bf1d369c4'
         '5a071d953f8f731016433c7cf88be52a'
         '4480ebda837cf976dba1e7c2aa5553e6'
         '9ac49e9395764ed5cf8843b682eff40c'
         '68e2e4c7a42bfc2d33a8e5a7b24124cb'
         'd90177d40b1b9871a694f30a54cd2daa'
         '1d95c63c5d80ca0c7d2a1771430c8e23'
         '584c2495e2ac0f160b2a2e3c9406aa83'
         '7bbedb098b13a1c51aef101406e2087b'
         '1076b1da91385fd7213fc342ad83db42'
         'fc9248306b9e70ef414eb4028009748c'
         '6392e1f5aa1382bd2434f97c2cf4ad2a'
         'a0c7e728e9d3c330df12095e61fb6c29'
         'af3c4ba5d4c6783c21dbfe35ba325152'
         '248308da9b62f603f32356a873fb85db'
         '7b6c0a76cb5ea49bac0984be908c105e'
         'adee8c11179d77284efba546005e4897'
         '28961a6d3319babba3b94b773de88d13'
         '63e4f511eaca67ac8310fe69c51ee939'
         'cbdb936b46fc5514728b73d79f74e673'
         '46811c0ef55aa34b6b9bb587e7e08ccc'
         '36ab59215f1d9c03c6d0130f0d0ce43d'
         '68be08f76d450efdbeb3f0e74ccd99aa'
         '7e7cfb28f1b95b18dd678e96c7cf3a96'
         '0c48ef06440fa1a00a14a3201ef6c5fd'
         '6608607c7fee463d6e0918993cca56d7'
         '1f56b7b5c2ab92c23b7c66a4a284040a'
         '95dc54587c5517f1ce86535bd6d9bf8d'
         '100f9824180f3289cd955f540dff4c56'
         'f3fa1661d77e234b3771fc5ab9c4103d'
         '65c0c0bc62026769dc8a3169452be3cf'
         '44260f37ba2eeb065208d2dd1eeb4b1f'
         '9ffa84971cf6ff2fece4aac405375d8e'
         '31b0321125892840af3a8a12fc803430'
         'e00df8c2d9affa091ad21f4fae26fedf'
         'bb4ce9d6a9f3a7513b0f4da364ce4d0f'
         'deb3d5799e9499e1618f79b9ff8d7615'
         '9dabc9185c1c7aba8e781aed8df5afc8'
         'f3c074e6869c65e5e6835a794abd30ea'
         '0be145e8df262daa84340d71e96d43b4')

prepare() {
  for _lang in "${_languages[@]}"; do
    _locale=${_lang%% *}

# Remove kde-runtime and kde-workspace docs, conflicts with plasma-desktop
  sed -e '/kde-runtime/d' -e '/kde-workspace/d' -i $pkgbase-$_locale-$pkgver/4/$_locale/docs/CMakeLists.txt || true

# Provide KDE4 PIM translations TODO: Remove in 15.12
  for _m in kdepim kdepim-runtime kdepimlibs ; do
    if [[ -d "$pkgbase-$_locale-$pkgver/5/$_locale/messages/$_m" ]] ; then
      cp -r $pkgbase-$_locale-$pkgver/{5,4}/$_locale/messages/$_m
      cp $pkgbase-$_locale-$pkgver/4/$_locale/messages/{kdelibs,$_m}/CMakeLists.txt
      echo "add_subdirectory($_m)" >> $pkgbase-$_locale-$pkgver/4/$_locale/messages/CMakeLists.txt
    fi
  done
  done
}

build() {
  for _lang in "${_languages[@]}"; do
    _locale=${_lang%% *}
    
    mkdir -p build/$_locale
    cd build/$_locale
    cmake $srcdir/$pkgbase-$_locale-$pkgver \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX=/usr
    make
    cd ../..
  done
}

_package() {
  pkgdesc="$2 localization for KDE"
  cd build/$1
  make DESTDIR="$pkgdir" install
}
