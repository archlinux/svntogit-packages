# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kde-l10n
pkgver=15.04.2
pkgrel=1
arch=('any')
url='http://www.kde.org/'
license=('GPL')
makedepends=('extra-cmake-modules' 'kdoctools' 'kdelibs' 'automoc4' 'kconfig' 'ki18n' 'qt5-tools')
options=('docs')

_languages=(
  'ar     "Arabic"'
  'bg     "Bulgarian"'
  'bs     "Bosnian"'
  'ca     "Catalan"'
  'ca@valencia     "Catalan (Valencian)"'
  'cs     "Czech"'
  'da     "Danish"'
  'de     "German"'
  'el     "Greek"'
  'en_GB  "British English"'
  'eo     "Esperanto"'
  'es     "Spanish"'
  'et     "Estonian"'
  'eu     "Basque"'
  'fa     "Farsi"'
  'fi     "Finnish"'
  'fr     "French"'
  'ga     "Irish"'
  'gl     "Galician"'
  'he     "Hebrew"'
  'hi     "Hindi"'
  'hr     "Croatian"'
  'hu     "Hungarian"'
  'ia     "Interlingua"'
  'id     "Indonesian"'
  'is     "Icelandic"'
  'it     "Italian"'
  'ja     "Japanese"'
  'kk     "Kazakh"'
  'km     "Khmer"'
  'ko     "Korean"'
  'lt     "Lithuanian"'
  'lv     "Latvian"'
  'mr     "Marathi"'
  'nb     "Norwegian Bokmal"'
  'nds    "Low Saxon"'
  'nl     "Dutch"'
  'nn     "Norwegian Nynorsk"'
  'pa     "Panjabi/Punjabi"'
  'pl     "Polish"'
  'pt     "Portuguese"'
  'pt_BR  "Brazilian Portuguese"'
  'ro     "Romanian"'
  'ru     "Russian"'
  'sk     "Slovak"'
  'sl     "Slovenian"'
  'sr     "Serbian"'
  'sv     "Swedish"'
  'tr     "Turkish"'
  'ug     "Uyghur"'
  'uk     "Ukrainian"'
  'wa     "Walloon"'
  'zh_CN  "Chinese Simplified"'
  'zh_TW  "Chinese Traditional"'
)

pkgname=()
source=()

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}
  _pkgname=kde-l10n-${_locale,,}
  
  pkgname+=($_pkgname)
  source+=("http://download.kde.org/stable/applications/$pkgver/src/$pkgbase/$pkgbase-$_locale-$pkgver.tar.xz")
  eval "package_$_pkgname() {
    _package $_lang
  }"
done

md5sums=('919ccaf02dff2caeb1c2cb0cdb7fe59a'
         '138ba5921f60ebb439aeec3679ff9522'
         'bef9694c1928db172670139ec5a67dcb'
         '49d95f48e94df4839fbfe335fcb2250d'
         '7047398711b923b7f08b402221874629'
         'c00ebaafd0c1a809afcca83a56c00423'
         '2422a6b0045253ed7edd0b230dd3bb1d'
         'a5ad149beaea3c59ed68ffc15643052d'
         'f9209fd641b09df3f20b48cd08f36cf2'
         'eebbbe43ee6c3eca7134c6b63f5fe955'
         'd946736b52980f300a81152ec91ac9dc'
         '38b3175c2078ad41a1276e2b769e8db6'
         '21a9485e82b9d04655abaf323dd515d0'
         '84b641426cdf15ce3282f44817c5b149'
         '9f956efcab5f163b61558d4c814395aa'
         '0882718014b542a93d7b8ad7f7c906f0'
         'c5aa7c12dbd4d6783e52d05b31c70349'
         '1f8c0ca07c22c4f6d7abdd940291390a'
         '230ec90957f150c24ae2df35fd497d2b'
         'd30c906ca9487e5089df773e86aec5ef'
         '125143b1bad466c0218f353be016759b'
         '940506279f0b5b092a710532c2316601'
         '99215bd1d466418d25fa4b4f1df0601b'
         '3429f751c6c4ec7a01e3c7881947ec7c'
         '0d628d79ca78af0f33d99a61fdf4964a'
         '382d368beec8dccd0f75730cf58e6d5e'
         'ca15d6d7855b0611b6e36bce485628e9'
         'de2406d31c62037e5dc573c67843a414'
         '0fa521cea05d93a37f2a5e65427fdbfe'
         '38b05433231b844e8606dd45a8c5cc28'
         '69b96c3d0540ede9476f1bcf6a625a77'
         '8987df3c886286ee373b2449f73e39da'
         'ae4bbe2d0158b7e7b6e5aa95484a2d7c'
         '04de71b49585196046da4fc9800e427c'
         'ade0eddf21165632ac1b915b266a5d0f'
         'e5f332b10b8bab1c242849edf7d0d0b8'
         '24888d16def2627c486353b20cbe352e'
         '168c2ee36fccc402ad0e2b49e05d9267'
         'dd5e48a650b926575a1d2aab32cbed99'
         '64aaf10d33b2c4b9378819c820b26d4e'
         '8d99a7f7fc7bcc4a6f4c9cd8fdf1718b'
         '1fe2cefd3749ba246a2b48ca0a7f72e7'
         '825c1b3eac82c096e8fc876e9a08759f'
         '0d36e503e4c6a2bff7c6bffef798be97'
         '456e9eab732ac9098f7fcb5132bbff16'
         'bd9fcff0bf41827a63f0c0944b086fe9'
         '5c94d7639866c39807b9a37c5f99be14'
         '61a9480bffa5396c9c80d9abb46bc29e'
         'cc224d3647c5c2b53708429bc2fbc17d'
         '928e75805ee2124a8513a265c9c0d5fb'
         '2052536cf2690fbdbc83907f70d682bb'
         '3ec4a8ad1d4902d0aede4520a3d2c9bb'
         '9cd3a0b76fd2ca16b00c5d568e8ab23b'
         '255bce1aa8d310e78009de2c5d669369')

prepare() {
  for _lang in "${_languages[@]}"; do
    _locale=${_lang%% *}

# Remove kde-runtime and kde-workspace docs, conflicts with plasma-desktop
  sed -e '/kde-runtime/d' -e '/kde-workspace/d' -i $pkgbase-$_locale-$pkgver/4/$_locale/docs/CMakeLists.txt || true

  done
}

build() {
  for _lang in "${_languages[@]}"; do
    _locale=${_lang%% *}
    
    mkdir -p build/$_locale
    cd build/$_locale
    cmake $srcdir/$pkgbase-$_locale-$pkgver \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX=/usr
    make
    cd ../..
  done
}

_package() {
  pkgdesc="$2 localization for KDE"
  cd build/$1
  make DESTDIR="$pkgdir" install
}

