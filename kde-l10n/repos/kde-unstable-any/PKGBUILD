# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kde-l10n
pkgver=15.11.90
pkgrel=1
arch=('any')
url='http://www.kde.org/'
license=('GPL')
makedepends=('extra-cmake-modules' 'kdoctools' 'kdelibs' 'automoc4' 'kconfig' 'ki18n' 'qt5-tools')
options=('docs')

_languages=(
  'ar     "Arabic"'
  'bg     "Bulgarian"'
  'bs     "Bosnian"'
  'ca     "Catalan"'
  'ca@valencia     "Catalan (Valencian)"'
  'cs     "Czech"'
  'da     "Danish"'
  'de     "German"'
  'el     "Greek"'
  'en_GB  "British English"'
  'eo     "Esperanto"'
  'es     "Spanish"'
  'et     "Estonian"'
  'eu     "Basque"'
  'fa     "Farsi"'
  'fi     "Finnish"'
  'fr     "French"'
  'ga     "Irish"'
  'gl     "Galician"'
  'he     "Hebrew"'
  'hi     "Hindi"'
  'hr     "Croatian"'
  'hu     "Hungarian"'
  'ia     "Interlingua"'
  'id     "Indonesian"'
  'is     "Icelandic"'
  'it     "Italian"'
  'ja     "Japanese"'
  'kk     "Kazakh"'
  'km     "Khmer"'
  'ko     "Korean"'
  'lt     "Lithuanian"'
  'lv     "Latvian"'
  'mr     "Marathi"'
  'nb     "Norwegian Bokmal"'
  'nds    "Low Saxon"'
#  'nl     "Dutch"'
  'nn     "Norwegian Nynorsk"'
  'pa     "Panjabi/Punjabi"'
  'pl     "Polish"'
  'pt     "Portuguese"'
  'pt_BR  "Brazilian Portuguese"'
  'ro     "Romanian"'
  'ru     "Russian"'
  'sk     "Slovak"'
  'sl     "Slovenian"'
  'sr     "Serbian"'
  'sv     "Swedish"'
  'tr     "Turkish"'
  'ug     "Uyghur"'
  'uk     "Ukrainian"'
  'wa     "Walloon"'
  'zh_CN  "Chinese Simplified"'
  'zh_TW  "Chinese Traditional"'
)

pkgname=()
source=()

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}
  _pkgname=kde-l10n-${_locale,,}
  
  pkgname+=($_pkgname)
  source+=("http://download.kde.org/unstable/applications/$pkgver/src/$pkgbase/$pkgbase-$_locale-$pkgver.tar.xz")
  eval "package_$_pkgname() {
    _package $_lang
  }"
done

md5sums=('db4c318943733a3cacec958a4371ed54'
         '4900d429555734a14f7a2c81317668fd'
         '73fae1d6695bacb96b9b2ef188ac595e'
         'a749ca1b24720608460267cbd5885ac5'
         '390a408282c34d6b876e1b82c925f15c'
         '151a7868fd19718eee997dbd9d6208fd'
         '3a373d6ffcb16ddb1ce353574427233c'
         'e408fc0d340bf43d4c86520e36574c26'
         '3da1a85cd82167159b1af760dfb7dcaa'
         '638d368a16803d44ca08a94e8afce8a0'
         '139a2d3de7b76c1c597ed678ea5cfc7d'
         '04e3d425231c062508bb043f426421cd'
         '160af3361a4b654056fe40ed0672afed'
         '033b136b4902ee74c63666a1c44b37ec'
         'e1a7f0be3ec43d2d3666d25811d079ef'
         'a7873a79ac8f4d7f4d373cbba2abe885'
         'a32deab5f2e9bf75b8643b12e65a3918'
         '8b33cdf3d58f658c797cb61926547414'
         'f5ea5f94cbf9136fff58df3c9995ea53'
         '255213727de7ae5fb17c14a188d4f44f'
         '59eb7c0f54de827851b3cd78ad3ed008'
         '30c30a2d598bb48e2e31a3a2a7dac0a2'
         '453a96307e5554aa359015a4883e4dcc'
         '9359d404862e4464418fa53c93a731d9'
         'ed884b0f784294e2ac99e397b3138199'
         'd2dffbf92ebf7cbe9873de349a25d029'
         'fb24e52967b16d6f9860c17503721e26'
         'c2a13ac56e909ef535ef237d4f69cb9f'
         '55843e85b25b61dd47089d80770d52f9'
         'bbc89b03ba9fab82d78a9c8583d3598b'
         '04140c124990f59a576cf4dca119a5d5'
         '27d371e87b36e15bee1c43e138908985'
         '8baa6bc1dac099b3fc0b61071d697610'
         '33d62baa37f6e62d406ccb8aece6f907'
         '8817e353fb925a9285298babd1cfc700'
         '5718590cf9224709801b5429671ea19e'
         'fa86543c9fb290583d24fb73002e82e5'
         '5e222d32c4e5ed4c0a14c540430ecf9f'
         '71de4688b10cb998019b38b5104a137e'
         '1212992604cd105227efba8c3c4c78dd'
         '5675a0db22d48d2355b8adaa19ad8535'
         '1cad32e01aa13f25fbc91e94b3aabcc7'
         '70528f192770fcca38ef465ebf8e9d7f'
         'f24c77ead1a1a6ac05921dc8139b8fbc'
         '9b82ce369f156b75fc52f23bc3394068'
         '9c2da400bf34f101b801dea5a1dcf505'
         '886ab1f87bd99cea67b56ca846524fc5'
         'af7858642b79f274fefd42dd1913545d'
         'ef4af2b793774c9377762c4cbace4fee'
         'a44a4ebf87859af3ec83e6e439f93a2e'
         '7c54e475db612218045d0837139f29c2'
         '7ef69b05d3dbb732dc3231f110fbe352'
         '4d839d901b8648cf063ad1c5fc71888f')

prepare() {
  for _lang in "${_languages[@]}"; do
    _locale=${_lang%% *}

# Remove kde-runtime and kde-workspace docs, conflicts with plasma-desktop
  sed -e '/kde-runtime/d' -e '/kde-workspace/d' -i $pkgbase-$_locale-$pkgver/4/$_locale/docs/CMakeLists.txt || true

  done
}

build() {
  for _lang in "${_languages[@]}"; do
    _locale=${_lang%% *}
    
    mkdir -p build/$_locale
    cd build/$_locale
    cmake $srcdir/$pkgbase-$_locale-$pkgver \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX=/usr
    make
    cd ../..
  done
}

_package() {
  pkgdesc="$2 localization for KDE"
  cd build/$1
  make DESTDIR="$pkgdir" install
}
