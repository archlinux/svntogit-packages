# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kde-l10n
pkgver=15.04.0
pkgrel=1
arch=('any')
url='http://www.kde.org/'
license=('GPL')
makedepends=('extra-cmake-modules' 'kdoctools' 'kdelibs' 'automoc4' 'kconfig' 'ki18n' 'qt5-tools')
options=('docs')

_languages=(
  'ar     "Arabic"'
  'bg     "Bulgarian"'
  'bs     "Bosnian"'
  'ca     "Catalan"'
  'ca@valencia     "Catalan (Valencian)"'
  'cs     "Czech"'
  'da     "Danish"'
  'de     "German"'
  'el     "Greek"'
  'en_GB  "British English"'
  'eo     "Esperanto"'
  'es     "Spanish"'
  'et     "Estonian"'
  'eu     "Basque"'
  'fa     "Farsi"'
  'fi     "Finnish"'
  'fr     "French"'
  'ga     "Irish"'
  'gl     "Galician"'
  'he     "Hebrew"'
  'hi     "Hindi"'
  'hr     "Croatian"'
  'hu     "Hungarian"'
  'ia     "Interlingua"'
  'id     "Indonesian"'
  'is     "Icelandic"'
  'it     "Italian"'
  'ja     "Japanese"'
  'kk     "Kazakh"'
  'km     "Khmer"'
  'ko     "Korean"'
  'lt     "Lithuanian"'
  'lv     "Latvian"'
  'mr     "Marathi"'
  'nb     "Norwegian Bokmal"'
  'nds    "Low Saxon"'
  'nl     "Dutch"'
  'nn     "Norwegian Nynorsk"'
  'pa     "Panjabi/Punjabi"'
  'pl     "Polish"'
  'pt     "Portuguese"'
  'pt_BR  "Brazilian Portuguese"'
  'ro     "Romanian"'
  'ru     "Russian"'
  'sk     "Slovak"'
  'sl     "Slovenian"'
  'sr     "Serbian"'
  'sv     "Swedish"'
  'tr     "Turkish"'
  'ug     "Uyghur"'
  'uk     "Ukrainian"'
  'wa     "Walloon"'
  'zh_CN  "Chinese Simplified"'
  'zh_TW  "Chinese Traditional"'
)

pkgname=()
source=()

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}
  _pkgname=kde-l10n-${_locale,,}
  
  pkgname+=($_pkgname)
  source+=("http://download.kde.org/stable/applications/$pkgver/src/$pkgbase/$pkgbase-$_locale-$pkgver.tar.xz")
  eval "package_$_pkgname() {
    _package $_lang
  }"
done

md5sums=('a040dfea67f0a4db2bf0378ca814c1b0'
         'f9dffb8d0f469e07ae2f2afa7ca25472'
         '52d968e78ddd4d595583718587a8f3db'
         '4e25d4ff80241e7072bbb95abb6e4ba0'
         '1c530af3fb0ecba602d74c33e38ec9ec'
         '34dc61c9d7f8825091e2018f9369ac6d'
         '41bacb5a252c9fa18e021f6b0c313633'
         '34269a51e667252bc5fc8a9d7f01d446'
         '2aa00245b726d2647b7bf1457a2f58d4'
         'ff35d818706aa891ab5503e18a230d78'
         'b87dbd63abcd678de71b2b076d8c460f'
         '77693f76d300185ba847e79268bdec28'
         '6648b7ee98612c6d8644c7de78d2c274'
         'bb72223c73524a8e83b1703b60652b39'
         'ff6041aa60d4cf44cb33d521d374fa0d'
         '0050e69dcf082ab0c247d10e85559046'
         '209159b3039ebdb32ffa52b8603e01ac'
         'e6d5cbad04a73eb5b0f61a3978b65dcc'
         '66665b11bf015c6baf8060076cede3c7'
         '92d8197575531ccd2822fd20a5bf038a'
         '88e2abad263d192b74cad52f6fbdf934'
         'ccb036948a9ece84678c13048d9906de'
         '373eb50aec4c607c80a74024ac81746c'
         '04463977bb0c3f82272afddadc77549f'
         '180a8e23fcd6f8e98a0ac1816ca83244'
         'e1f9a37baeb0b6f967563e0af435d366'
         '4fcd7f458a36da90cd6341edef9a0c69'
         '75951f407555e88adaec8b71ce6dd901'
         '93886a3631eeb23f79cdf1e36ed9b5b2'
         'b414dae9695cdcd975acec58e99e0cf4'
         '52db647d500f6e2d281cc91d103513d7'
         '99b4079b101e93445ae42202c4cd1391'
         'f1ca103d585932d6308802d2219135cb'
         '3f45754d5260cdadf2a8f0f577ff4801'
         '653f217a82382dc5c31a2faecd77699a'
         '6972e7f85ca4608a8119dc63d1235bd9'
         'ed78f90ec6f4bbd8b85d5443fae8f76d'
         'f1e809b7f527adcaa4167c380e248cc5'
         '9932035613d233a742cbac11d5d17ad8'
         '2125b9b25364c6f6ba172dbaed7e4b9f'
         '7f109fa9b5d5a2b75f1765449d69532d'
         'a177d809f5d372ed1cbeb04f2ad06bfc'
         'a78208bca2f3a68dc0880014e48c7028'
         'ebc89963bc7d585ed0eeee5a5b792215'
         'ffd128696c52c5c11c1ff3bc82fab574'
         '2227e566abe426af44cba8d62bfd30af'
         '9998ddd606654fec00941b4a2b7f40e2'
         '64be56170400859edf9571e928918836'
         'b6b90024fa25c68218d43f54a8d220dd'
         '9db56bc33086bcc5a46c1c1f4f5e204d'
         '7ac7894678a9ff1c9337bd0fd9b2ec16'
         '6a31467fa1c7b080988d74ff869e180a'
         'a76512fff2a7809c5ef9da6ca5243d19'
         '8f0060303864770b612b9e5136445ea3')

prepare() {
  for _lang in "${_languages[@]}"; do
    _locale=${_lang%% *}

# Remove kde-runtime and kde-workspace docs, conflicts with plasma-desktop
  sed -e '/kde-runtime/d' -e '/kde-workspace/d' -i $pkgbase-$_locale-$pkgver/4/$_locale/docs/CMakeLists.txt || true

  done
}

build() {
  for _lang in "${_languages[@]}"; do
    _locale=${_lang%% *}
    
    mkdir -p build/$_locale
    cd build/$_locale
    cmake $srcdir/$pkgbase-$_locale-$pkgver \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX=/usr
    make
    cd ../..
  done
}

_package() {
  pkgdesc="$2 localization for KDE"
  cd build/$1
  make DESTDIR="$pkgdir" install
}

