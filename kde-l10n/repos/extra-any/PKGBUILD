# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kde-l10n
pkgver=15.08.0
pkgrel=1
arch=('any')
url='http://www.kde.org/'
license=('GPL')
makedepends=('extra-cmake-modules' 'kdoctools' 'kdelibs' 'automoc4' 'kconfig' 'ki18n' 'qt5-tools')
options=('docs')

_languages=(
  'ar     "Arabic"'
  'bg     "Bulgarian"'
  'bs     "Bosnian"'
  'ca     "Catalan"'
  'ca@valencia     "Catalan (Valencian)"'
  'cs     "Czech"'
  'da     "Danish"'
  'de     "German"'
  'el     "Greek"'
  'en_GB  "British English"'
  'eo     "Esperanto"'
  'es     "Spanish"'
  'et     "Estonian"'
  'eu     "Basque"'
  'fa     "Farsi"'
  'fi     "Finnish"'
  'fr     "French"'
  'ga     "Irish"'
  'gl     "Galician"'
  'he     "Hebrew"'
  'hi     "Hindi"'
  'hr     "Croatian"'
  'hu     "Hungarian"'
  'ia     "Interlingua"'
  'id     "Indonesian"'
  'is     "Icelandic"'
  'it     "Italian"'
  'ja     "Japanese"'
  'kk     "Kazakh"'
  'km     "Khmer"'
  'ko     "Korean"'
  'lt     "Lithuanian"'
  'lv     "Latvian"'
  'mr     "Marathi"'
  'nb     "Norwegian Bokmal"'
  'nds    "Low Saxon"'
  'nl     "Dutch"'
  'nn     "Norwegian Nynorsk"'
  'pa     "Panjabi/Punjabi"'
  'pl     "Polish"'
  'pt     "Portuguese"'
  'pt_BR  "Brazilian Portuguese"'
  'ro     "Romanian"'
  'ru     "Russian"'
  'sk     "Slovak"'
  'sl     "Slovenian"'
  'sr     "Serbian"'
  'sv     "Swedish"'
  'tr     "Turkish"'
  'ug     "Uyghur"'
  'uk     "Ukrainian"'
  'wa     "Walloon"'
  'zh_CN  "Chinese Simplified"'
  'zh_TW  "Chinese Traditional"'
)

pkgname=()
source=()

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}
  _pkgname=kde-l10n-${_locale,,}
  
  pkgname+=($_pkgname)
  source+=("http://download.kde.org/stable/applications/$pkgver/src/$pkgbase/$pkgbase-$_locale-$pkgver.tar.xz")
  eval "package_$_pkgname() {
    _package $_lang
  }"
done

md5sums=('e5903849df90c741f61c8895839fd479'
         'ea790426e1eef1b00551a40e6394974e'
         'bd7a1c9fd4061530681a89625858003e'
         '10632a311223509960429297775a0244'
         '87fa2ae6c6dc589e3189d25600f63d53'
         '04bf34e488d8a7f1d2476d6165452d8c'
         'adbe7d76603078ee5c3acb9e30d39dfc'
         '87ccbe1f1bd5360010b6871e0dae13af'
         '49154b26205cacb47b5bd2ce0906669b'
         '63ff5f0a958eb88dbadb46f82ae2bcd4'
         'ee6e546c5b5099c64cac2729488d1f5f'
         'a3a8e4f1125d83e2b5685802699a3aa8'
         '31224eefa7f5680198209adea717a114'
         'b707a018dcf74a24c286b0eadeddc2fd'
         '774fb5cd6fb1b139c033cd997b913852'
         'd2339de4ed3606f28746eb6c4c87a6a2'
         'be09e9ba01127d9234cf26684cafc086'
         '9e9c23f87c2fd0f647847b20fe7eeced'
         '74a0407a12840464ef80f07fdbce1aa0'
         '52bd669f6c5f93b7aa798d674e0b6f8a'
         '85a3b888ed90b5021802d28db2fee2f1'
         'f313d14afe62398665a5132c392a728f'
         '28d966ec6828a5dc8b4f334839075a5b'
         '51a9cfe23299f5b6a8420fe3989e5069'
         'c03942522c5e36a9b22730d2b4b409e1'
         '11bc004c34463a96ae74db0931823a44'
         '1c35384ade467df3358df45ce803e069'
         '9f23c9224601b37cbb5e4d8e6d5c95f0'
         '396144d2cc1199701f73e1c4910db1bd'
         '1c1c1db31c90c7f7f04d65462a14b57d'
         'cff23cae2bd3bc107d6fc07cb6ad044c'
         'fd7786a6f51efe6141cbbd43d36c70e7'
         '543387991a0f727c3644baf8ece5548f'
         '86f99c4eb633a40d1ca69523dbc200ad'
         '904982e4c9f77b3be3573c07c3e295e0'
         'fcede11d83121a26d6a0352b00958908'
         'da44f5435848065d9aa9cc1618e184da'
         '1b732caf38af8cf696e28fb455a1ca31'
         'fbbdcf8efd703f3cd883f2ea3ee0d5f8'
         '8be4f4e0216762db2fb38cf82e15e883'
         '87a040ba02e1517b03e35cf7c6543894'
         'b3641ae90ec977dbc1bc10e20ed4780c'
         'becb25e7b486ef7c12ea2e3d6376a9c0'
         '993a1f67ad78acf292067ba9d4634ef3'
         'fc6d2c188eb8bb158aab76a8f524ff13'
         'bdff92eb97cd423a6174381804fad523'
         '257c7bfd22c43975ad01a0d5d319deaa'
         'de0529a9c4c6e1d6c1a476ab44e2ff99'
         '0e57b500a9049877edc181cc7553c631'
         '2b656ae23eb24afe89511728df60a75b'
         'bb726aae5b7073cd15a76c4c137b457c'
         '576cb6ed392b4f2f9349b69ee20f3e7e'
         '567691cd44dfeeb8f4e45fd0d6e125c4'
         '61eb2f9672d36bc14a62350c607e1ce1')

prepare() {
  for _lang in "${_languages[@]}"; do
    _locale=${_lang%% *}

# Remove kde-runtime and kde-workspace docs, conflicts with plasma-desktop
  sed -e '/kde-runtime/d' -e '/kde-workspace/d' -i $pkgbase-$_locale-$pkgver/4/$_locale/docs/CMakeLists.txt || true

  done
}

build() {
  for _lang in "${_languages[@]}"; do
    _locale=${_lang%% *}
    
    mkdir -p build/$_locale
    cd build/$_locale
    cmake $srcdir/$pkgbase-$_locale-$pkgver \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX=/usr
    make
    cd ../..
  done
}

_package() {
  pkgdesc="$2 localization for KDE"
  cd build/$1
  make DESTDIR="$pkgdir" install
}

