# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
pkgname=gdm
pkgver=3.0.0
pkgrel=3
pkgdesc="Gnome Display Manager (a reimplementation of xdm)"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnome.org"
backup=('etc/pam.d/gdm' 'etc/pam.d/gdm-autologin' 'etc/gdm/custom.conf')
groups=('gnome-extra')
options=('!libtool')
depends=('libcanberra' 'libxklavier' 'tcp_wrappers' 'gnome-session' 'upower' 'accountsservice' 'consolekit')
makedepends=('intltool' 'gnome-doc-utils' 'xorg-server')
conflicts=('fast-user-switch-applet')
replaces=('fast-user-switch-applet')
install=gdm.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2
        2.91_fix_external_program_directories.patch
        gdm-vt-allocation-hack.patch
        gdm.pam
        gdm-autologin.pam
        gdm
        fix_crasher_with_gtk307.patch)
sha256sums=('2d6443912272c0aa339a06b58d797cc11ce6b0854dadfcc027408f546da7aeff'
            '92c5eb913b9556cffe9b5bb89e5c3435703e929addfb98145442f58af5d532c1'
            '3c8b588d4af08d94dc93bcd5e4c2a983c3f4fbbbe40833bceac2a1df4f1e8215'
            'f1dfa4d88288d4b0a631a68a51b46c2da537bee8fe5a99f9f288c8ff75a50b19'
            '3daff680ff6b7ea56f84f40843e46e72477c81e9e405028203c942af04d07ae5'
            '272c08d8e8b50bf424d0705ac864d4c18c47ec4f6893b1af732c2efbc86c9550'
            '6d08951919dcb1f928dd67c0b5a5c209b32464c2374ee7d2369ed8e914d541c9')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/2.91_fix_external_program_directories.patch"
  patch -Np1 -i "${srcdir}/gdm-vt-allocation-hack.patch"

  #https://bugzilla.gnome.org/show_bug.cgi?id=646498
  patch -Np1 -i "${srcdir}/fix_crasher_with_gtk307.patch"

  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/gdm --localstatedir=/var --disable-static \
      --with-at-spi-registryd-directory=/usr/lib/at-spi \
      --disable-scrollkeeper
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gdm ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -r "${pkgdir}/etc/gconf/schemas/"

  install -m644 "${srcdir}/gdm.pam" "${pkgdir}/etc/pam.d/gdm"
  install -m644 "${srcdir}/gdm-autologin.pam" "${pkgdir}/etc/pam.d/gdm-autologin"

  install -m755 -d "${pkgdir}/etc/rc.d"
  install -m755 "${srcdir}/gdm" "${pkgdir}/etc/rc.d/"

  rmdir "${pkgdir}/var/gdm"
  chmod 1770 "${pkgdir}/var/log/gdm"
  rm -rf "${pkgdir}/var/run"
}
