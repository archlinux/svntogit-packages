# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gdm
pkgver=3.2.1
pkgrel=1
pkgdesc="Gnome Display Manager (a reimplementation of xdm)"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnome.org"
backup=('etc/pam.d/gdm-autologin' 'etc/pam.d/gdm-fingerprint' 'etc/pam.d/gdm' 'etc/pam.d/gdm-password'
        'etc/pam.d/gdm-smartcard' 'etc/pam.d/gdm-welcome' 'etc/gdm/custom.conf')
groups=('gnome-extra')
options=('!libtool')
depends=('libcanberra' 'libxklavier' 'gnome-session' 'gnome-settings-daemon' 'metacity' 'upower' 'accountsservice' 'consolekit' 'xorg-xrdb' 'nss')
makedepends=('intltool' 'gnome-doc-utils' 'xorg-server' 'gobject-introspection')
optdepends=('gnome-shell: new login interface')
install=gdm.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz
        fix_external_program_directories.patch
        gdm-vt-allocation-hack.patch
        gdm
        gdm-autologin.pam gdm-fingerprint.pam gdm.pam gdm-password.pam gdm-smartcard.pam gdm-welcome.pam)
sha256sums=('f98d00819d05185b20bd03c02f7b97ca63a63cab110b94fadfe8f58d4d4475f0'
            '2014ae3a6077546f7c07e80a6acc8a1c80c6765ad319ec389f52500c5a4fb363'
            '3c8b588d4af08d94dc93bcd5e4c2a983c3f4fbbbe40833bceac2a1df4f1e8215'
            '272c08d8e8b50bf424d0705ac864d4c18c47ec4f6893b1af732c2efbc86c9550'
            'b30dfa217718b43ff3aa4e8af08985963175b79ff30698ec54e8396d2905922d'
            '5bc3ff3ea7b31219dfcb7d9fc0eb2819eca1c5573a0f426d288a17560a69633e'
            'f1dfa4d88288d4b0a631a68a51b46c2da537bee8fe5a99f9f288c8ff75a50b19'
            '92aded0804bad8a609fd150448ad4a2edb8166c17cb4e2b581559e80603bbb6c'
            'df74b0dcfb17ea43cc1e268a27cd256c552672457db1af40b285452d16b3e2fd'
            '39bf3824cc505043dc3136356da16dab9df9f369f1077435a324be2ac16cf834')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/fix_external_program_directories.patch"
  patch -Np1 -i "${srcdir}/gdm-vt-allocation-hack.patch"

  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/gdm \
      --localstatedir=/var \
      --with-at-spi-registryd-directory=/usr/lib/at-spi2-core \
      --disable-scrollkeeper \
      --disable-static \
      --without-tcp-wrappers
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' -e 's/    if test "$export_dynamic" = yes && test -n "$export_dynamic_flag_spec"; then/      func_append compile_command " -Wl,-O1,--as-needed"\n      func_append finalize_command " -Wl,-O1,--as-needed"\n\0/' libtool
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gdm ${pkgdir}/etc/gconf/schemas/*.schemas

  for i in gdm-autologin.pam gdm-fingerprint.pam gdm.pam gdm-password.pam gdm-smartcard.pam gdm-welcome.pam;do
      install -m644 "${srcdir}/${i}" "${pkgdir}/etc/pam.d/${i%.pam}"
  done

  install -Dm755 "${srcdir}/gdm" "${pkgdir}/etc/rc.d/gdm"

  chmod 1770 "${pkgdir}/var/log/gdm"
  chmod 700 "${pkgdir}/var/lib/gdm/.config/dconf"
  rm -rf "${pkgdir}/var/run" "${pkgdir}/var/gdm" "${pkgdir}/etc/gconf"
}
