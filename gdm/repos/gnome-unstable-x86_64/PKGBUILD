# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
pkgname=gdm
pkgver=2.91.92
pkgrel=1
pkgdesc="Gnome Display Manager (a reimplementation of xdm)"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnome.org"
backup=('etc/pam.d/gdm' 'etc/pam.d/gdm-autologin' 'etc/gdm/custom.conf')
groups=('gnome')
options=('!libtool')
depends=('libcanberra' 'libxklavier' 'tcp_wrappers' 'gnome-session' 'upower' 'accountsservice' 'consolekit')
makedepends=('intltool' 'gnome-doc-utils' 'xorg-server')
conflicts=('fast-user-switch-applet')
replaces=('fast-user-switch-applet')
install=gdm.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.91/${pkgname}-${pkgver}.tar.bz2
        2.91_fix_external_program_directories.patch
        gdm-vt-allocation-hack.patch
        gdm.pam
        gdm-autologin.pam
        gdm)
sha256sums=('0a03d84f955af713fccb1606303ec6910ac085f81e978ec2b961b6b5e2349d2d'
            '92c5eb913b9556cffe9b5bb89e5c3435703e929addfb98145442f58af5d532c1'
            '3c8b588d4af08d94dc93bcd5e4c2a983c3f4fbbbe40833bceac2a1df4f1e8215'
            'f1dfa4d88288d4b0a631a68a51b46c2da537bee8fe5a99f9f288c8ff75a50b19'
            '3daff680ff6b7ea56f84f40843e46e72477c81e9e405028203c942af04d07ae5'
            '272c08d8e8b50bf424d0705ac864d4c18c47ec4f6893b1af732c2efbc86c9550')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/2.91_fix_external_program_directories.patch"
  patch -Np1 -i "${srcdir}/gdm-vt-allocation-hack.patch"
  autoreconf -fi

  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/gdm --localstatedir=/var --disable-static \
      --with-at-spi-registryd-directory=/usr/lib/at-spi \
      --disable-scrollkeeper
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gdm ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -r "${pkgdir}/etc/gconf/schemas/"

  install -m644 "${srcdir}/gdm.pam" "${pkgdir}/etc/pam.d/gdm"
  install -m644 "${srcdir}/gdm-autologin.pam" "${pkgdir}/etc/pam.d/gdm-autologin"

  install -m755 -d "${pkgdir}/etc/rc.d"
  install -m755 "${srcdir}/gdm" "${pkgdir}/etc/rc.d/"

  rmdir "${pkgdir}/var/gdm"
  chmod 1770 "${pkgdir}/var/log/gdm"
}
