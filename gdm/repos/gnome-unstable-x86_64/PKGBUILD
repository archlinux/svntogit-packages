# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=gdm
pkgname=(gdm libgdm)
pkgver=3.9.92
pkgrel=1
pkgdesc="Gnome Display Manager"
arch=(i686 x86_64)
license=(GPL)
url="http://www.gnome.org"
options=('!libtool')
depends=(gnome-shell gnome-session upower xorg-xrdb xorg-server xorg-xhost)
makedepends=(itstool intltool yelp-tools gobject-introspection)
checkdepends=('check')
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgbase/${pkgver::3}/$pkgbase-$pkgver.tar.xz
        fix_external_program_directories.patch arch-pam.patch)
sha256sums=('ba9035a943f58ca3c9e14c5a3891642a8725f244765d53599b73d5fccdc4d9d8'
            'a878680734e35c1d66252cbcfe678c3961b5ff0fa16302f8796a5e7e65ffe4a2'
            'b5c225784c3107564cb9685e41dfecdb20b8ea2415905c4119c96748c4e68003')

prepare() {
  cd $pkgbase-$pkgver
  patch -Np1 -i ../fix_external_program_directories.patch
  patch -Np1 -i ../arch-pam.patch
  AUTOPOINT='intltoolize --automake -c' autoreconf -fi
}

build() {
  cd $pkgbase-$pkgver
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/gdm \
    --localstatedir=/var \
    --disable-static \
    --disable-schemas-compile \
    --with-at-spi-registryd-directory=/usr/lib/at-spi2-core \
    --with-check-accelerated-directory=/usr/lib/gnome-session \
    --with-authentication-agent-directory=/usr/lib/polkit-gnome \
    --without-tcp-wrappers \
    --without-console-kit \
    --with-systemd \
    --with-default-pam-config=arch

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' -e 's/    if test "$export_dynamic" = yes && test -n "$export_dynamic_flag_spec"; then/      func_append compile_command " -Wl,-O1,--as-needed"\n      func_append finalize_command " -Wl,-O1,--as-needed"\n\0/' libtool

  make -j1
}

check() {
  cd $pkgbase-$pkgver
  make check
}

package_gdm() {
  depends+=(libgdm)
  optdepends=('fprintd: fingerprint authentication')
  backup=(etc/pam.d/gdm-autologin etc/pam.d/gdm-fingerprint etc/pam.d/gdm-launch-environment
          etc/pam.d/gdm-password etc/pam.d/gdm-smartcard etc/gdm/custom.conf
          etc/gdm/Xsession
          etc/gdm/PostSession/Default etc/gdm/PreSession/Default)
  groups=(gnome)
  install=gdm.install

  cd $pkgbase-$pkgver
  make DESTDIR="$pkgdir" install

  chmod 711 "$pkgdir/var/log/gdm"
  rm -r "$pkgdir/var/run"

### Split libgdm
  make -C gui/libgdm DESTDIR="$pkgdir" uninstall
}

package_libgdm() {
  pkgdesc="GDM support library"
  depends=(systemd)

  cd $pkgbase-$pkgver
  make -C gui/libgdm DESTDIR="$pkgdir" install
}
