# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kpatience
pkgver=17.07.80
pkgrel=1
pkgdesc="Offers a selection of solitaire card games"
url="https://kde.org/applications/games/kpatience/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegames')
depends=('libkdegames' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'python' 'kdoctools')
conflicts=('kdegames-kpatience')
replaces=('kdegames-kpatience')
source=("https://download.kde.org/unstable/applications/${pkgver}/src/kpat-${pkgver}.tar.xz"{,.sig})
sha256sums=('f595e55c81610fe43ffe7518abdc7e98da285af7140b987c3ddbad2ab43912e4'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kpat-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
