# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kpatience
pkgver=15.11.80
pkgrel=1
pkgdesc="Offers a selection of solitaire card games"
url="http://kde.org/applications/games/kpatience/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegames')
depends=('libkdegames' 'knotifyconfig' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'python' 'kdoctools')
install=${pkgname}.install
conflicts=('kdegames-kpatience')
replaces=('kdegames-kpatience')
source=("http://download.kde.org/unstable/applications/${pkgver}/src/kpat-${pkgver}.tar.xz")
sha1sums=('cb13fa41f3a0e6b7892df247fbb3a4c02d9e7a5c')

build() {
  mkdir -p build
  cd build
  cmake ../kpat-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
