# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kpatience
pkgver=16.04.2
pkgrel=1
pkgdesc="Offers a selection of solitaire card games"
url="http://kde.org/applications/games/kpatience/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegames')
depends=('libkdegames' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'python' 'kdoctools' 'kdesignerplugin')
conflicts=('kdegames-kpatience')
replaces=('kdegames-kpatience')
source=("http://download.kde.org/stable/applications/${pkgver}/src/kpat-${pkgver}.tar.xz")
sha1sums=('d8e018ecf0a1a21e086120c0e53ff724b0ed7110')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kpat-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
