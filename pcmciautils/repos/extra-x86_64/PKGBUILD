# $Id$
# Maintainer: Tom Gundersen <teg@jklm.no>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>
pkgname=pcmciautils
pkgver=018
pkgrel=8
pkgdesc="Utilities for inserting and removing PCMCIA cards"
arch=('x86_64')
url="http://kernel.org/pub/linux/utils/kernel/pcmcia/pcmcia.html"
license=('GPL')
groups=('base')
depends=('systemd')
conflicts=('pcmcia-cs')
# source=(http://kernel.org/pub/linux/utils/kernel/pcmcia/${pkgname}-${pkgver}.tar.bz2) broken due to breakin
source=(https://sources.archlinux.org/other/$pkgname/$pkgname-$pkgver.tar.xz
        initcpio-install-pcmcia)
options=(!makeflags)

build() {
  cd "$pkgname-$pkgver"
  sed -i -e 's,/lib/udev,/usr/lib/udev,g' Makefile
  sed -i -e 's,/sbin,/usr/bin,g' Makefile
  make
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install

  # install the mkinitpcio hook
  install -Dm644 initcpio-install-pcmcia "$pkgdir/usr/lib/initcpio/install/pcmcia"
}
sha512sums=('c7c1ecde41a60d3d1fcb12e3be309f8287d0242ad93e17b1286bf2818a0de1f415c89f63fec0571264e5ff87e99ba7ded905e6b671c2f6795ea910a0045261f5'
            '8e486c7432264bdce6698c3831f89b1473400b99dc5aad70df70b2b01cd4219066595fe3f7bc062c56cc2487ad9d65bb6d27d843e20ebfb03e5479f69a9d5299')
