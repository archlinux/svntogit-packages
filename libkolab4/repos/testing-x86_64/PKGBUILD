# $Id$
# Maintainer:
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=libkolab4
pkgver=0.6.0
pkgrel=4
pkgdesc="Advanced Kolab Object Handling Library"
url='http://git.kolab.org/libkolab/'
arch=('i686' 'x86_64')
license=('GPL')
depends=('libkolabxml' 'kdepimlibs4')
makedepends=('cmake' 'automoc4' 'boost')
conflicts=('libkolab')
replaces=('libkolab<1')
source=("http://mirror.kolabsys.com/pub/releases/libkolab-${pkgver}.tar.gz"{,.gpg} 'check_for_generic_tag.patch')
md5sums=('3c4fc1fec2dfcc0841f29de8f6ff0cd3'
         '237fe4fda389cb8cf167e84aea2b90b0'
         '6588bc470183e56ca77f12e1dbb5b1ee')

prepare() {
  mkdir -p build

  cd libkolab-$pkgver
# fix build against kdepimlibs 4.14
  patch -p1 -i "$srcdir"/check_for_generic_tag.patch
}

build() {
  cd build
  cmake ../libkolab-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
