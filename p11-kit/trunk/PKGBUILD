# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=p11-kit
pkgver=0.23.19
pkgrel=1
pkgdesc="Provides a way to load and enumerate PKCS#11 modules"
arch=(x86_64)
url="https://p11-glue.freedesktop.org"
license=(BSD)
depends=(glibc libtasn1 libffi systemd)
makedepends=(gtk-doc git meson)
install=p11-kit.install
source=("git+https://github.com/p11-glue/p11-kit?signed#tag=$pkgver"
        0001-Build-and-install-libnssckbi-p11-kit.so.patch)
sha256sums=('SKIP'
            'e832eece10587ac50ae42ca4515786b51e67fea0647716061e51cd94f5e058cd')
validpgpkeys=('C0F67099B808FB063E2C81117BFB1108D92765AF'  # Stef Walter
              '462225C3B46F34879FC8496CD605848ED7E69871') # Daiki Ueno

prepare() {
  cd p11-kit

  # Build and install an additional library (libnssckbi-p11-kit.so) which
  # is a copy of p11-kit-trust.so but uses the same label for root certs as
  # libnssckbi.so ("Builtin Object Token" instead of "Default Trust")
  # https://bugs.freedesktop.org/show_bug.cgi?id=66161
  patch -Np1 -i ../0001-Build-and-install-libnssckbi-p11-kit.so.patch
}

build() {
  arch-meson p11-kit build \
    --buildtype debugoptimized \
    -D gtk_doc=true \
    -D man=true \
    -D trust_paths=/etc/ca-certificates/trust-source:/usr/share/ca-certificates/trust-source
  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 p11-kit/COPYING
  ln -srf "$pkgdir/usr/bin/update-ca-trust" "$pkgdir/usr/lib/p11-kit/trust-extract-compat"
}

# vim:set ts=2 sw=2 et:
