# Maintainer: AndyRTR <andyrtr@archlinux.org>

pkgname=liblangtag
pkgver=0.6.4
pkgrel=1
pkgdesc="interface library to access/deal with tags for identifying languages"
arch=('x86_64')
url="https://bitbucket.org/tagoh/liblangtag/wiki/Home"
license=('LGPL3' 'MPL') # actually MPL2.0
depends=('libxml2' 'glib2')
makedepends=('gobject-introspection')
optdepends=('gobject-introspection-runtime: use with liblangtag-gobject')
source=(https://bitbucket.org/tagoh/liblangtag/downloads/$pkgname-$pkgver.tar.bz2)
sha256sums=('5701062c17d3e73ddaa49956cbfa5d47d2f8221988dec561c0af2118c1c8a564')

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd "$pkgname-$pkgver"
  make -k check
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
