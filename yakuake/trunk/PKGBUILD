# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: James Rayner <iphitus@gmail.com>
# Contributor: leeghoofd <abcdefg@solcon.nl>

pkgname=yakuake
pkgver=3.0.5
pkgrel=2
pkgdesc="A drop-down terminal emulator based on KDE konsole technology"
arch=('x86_64')
url='https://yakuake.kde.org/'
license=('GPL')
depends=('konsole' 'kwayland' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules')
source=("https://download.kde.org/stable/${pkgname}/${pkgver}/src/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha256sums=('08e23bd3ed58732bec44bf1b6797990bbdc58fad0725da7215db39f86c4d2a08'
            'SKIP')
validpgpkeys=('DA6B31C4B98A877DBB2EF9A292220328C632316E') # Eike Hein <hein@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
