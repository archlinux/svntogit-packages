# $Id$
# Maintainer: Ray Rashif <schivmeister@gmail.com>
# Contributor: galiyosha@gmail.com
# Contributor: Jon Kristian Nilsen <jokr.nilsen@gmail.com>

pkgname=libffado
pkgver=2.0.1
pkgrel=1
pkgdesc="Driver for FireWire audio devices"
arch=('i686' 'x86_64')
url="http://www.ffado.org/"
license=('GPL')
depends=('libiec61883' 'libavc1394' 'libsigc++'
          'libxml++' 'alsa-lib' 'dbus')
makedepends=('scons' 'pyqt')
optdepends=('pyqt: mixer applet')
source=(http://www.ffado.org/files/$pkgname-$pkgver.tar.gz)
md5sums=('786f31facd417e6207e429f50af0e15e')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # hack to use CFLAGS without optimising
  sed -i 's/-O2//g' SConstruct

  if [ "$CARCH" = "x86_64" ]; then
    sed -i "s/-m64/-m64 $CFLAGS/g" SConstruct
  else
    sed -i "s/-m32/-m32 $CFLAGS/g" SConstruct
  fi

  scons PREFIX=/usr #ENABLE_OPTIMIZATIONS=True
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  scons DESTDIR="$pkgdir/" WILL_DEAL_WITH_XDG_MYSELF="True" install

  # desktop files & icons
  install -Dm644 support/xdg/ffado.org-ffadomixer.desktop \
    "$pkgdir/usr/share/applications/ffadomixer.desktop"

  install -Dm644 support/xdg/hi64-apps-ffado.png \
	      "$pkgdir/usr/share/pixmaps/$pkgname.png"
}

# vim:set ts=2 sw=2 et:
