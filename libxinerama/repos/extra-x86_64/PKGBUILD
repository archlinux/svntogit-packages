# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

pkgname=libxinerama
pkgver=1.1.2
pkgrel=1
pkgdesc="X11 Xinerama extension library"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libxext' 'xineramaproto')
makedepends=('xorg-util-macros')
options=('!libtool')
source=("${url}/releases/individual/lib/libXinerama-${pkgver}.tar.bz2")
sha1sums=('06a5b636305725ce09f6c3a4d5a15f2f188b5afd')

build() {
  cd "${srcdir}/libXinerama-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/libXinerama-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
