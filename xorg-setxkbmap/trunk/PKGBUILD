# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-setxkbmap
pkgver=1.3.1
pkgrel=2
pkgdesc='Set the keyboard using the X Keyboard Extension'
url='https://xorg.freedesktop.org/'
license=('custom')
arch=('x86_64')
depends=('libxkbfile')
makedepends=('xorg-util-macros')
source=(${url}/releases/individual/app/setxkbmap-${pkgver}.tar.bz2{,.sig})
sha256sums=('a9ddb3963f263ba13f0ea105d8c45a531832140530217cc559587bb94f02d3e1'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # Alan Coopersmith <alan.coopersmith@oracle.com>
groups=('xorg-apps' 'xorg')

build() {
	cd setxkbmap-${pkgver}
	./configure --prefix=/usr
	make
}

package() {
	cd setxkbmap-${pkgver}
	make DESTDIR="${pkgdir}" install
	install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
