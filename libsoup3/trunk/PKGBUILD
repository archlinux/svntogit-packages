# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=libsoup3
pkgver=3.0.8
pkgrel=1
pkgdesc="HTTP client/server library for GNOME"
url="https://wiki.gnome.org/Projects/libsoup"
arch=(x86_64)
license=(LGPL)
depends=(glib2 glib-networking sqlite krb5 libpsl brotli libnghttp2
         libsysprof-capture)
makedepends=(gobject-introspection python vala git gtk-doc meson samba
             python-quart)
checkdepends=(apache php-apache)
optdepends=('samba: Windows Domain SSO')
options=(debug)
_commit=66b1deac20ce3857d0c6cf225d4517027e532c0b  # tags/3.0.8^0
source=("git+https://gitlab.gnome.org/GNOME/libsoup.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd libsoup
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd libsoup
}

build() {
  arch-meson libsoup build \
    -D gtk_doc=true \
    -D autobahn=disabled
  meson compile -C build
}

check() {
  # Python's output buffering messes with the tests reading stdout lines from
  # http2-server.py through a pipe
  PYTHONUNBUFFERED=1 meson test -C build --print-errorlogs
}

package() {
  depends+=(libg{lib,object,io}-2.0.so libgssapi_krb5.so libpsl.so
            libbrotlidec.so)
  provides+=(libsoup-3.0.so)

  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
