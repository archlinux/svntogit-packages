# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kimageformats
pkgver=5.99.0
pkgrel=3
pkgdesc='Image format plugins for Qt5'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(qt5-base)
makedepends=(extra-cmake-modules karchive libavif openexr libheif libjxl libraw)
optdepends=('karchive: plugin for Krita and OpenRaster images'
            'libavif: AVIF format support'
            'openexr: EXR format support'
            'libheif: HEIF format support'
            'libjxl: JPEG-XL format support'
            'libraw: RAW format support')
groups=(kf5)
source=(https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz{,.sig}
        kimageformats-avif-fix-1.patch::https://invent.kde.org/frameworks/kimageformats/-/commit/350ce1b990460cb2178f369f22fe80803f5645f3.patch
        kimageformats-avif-fix-2.patch::https://invent.kde.org/frameworks/kimageformats/-/commit/1190e53e9b69da6f9663ceb75c4813c5708b7cbd.patch
        https://invent.kde.org/frameworks/kimageformats/-/commit/f475a4b2.patch)
sha256sums=('06e636d4fe03d8d1b0bfed728d00c83fa639e2990fd5664a7101bd77621db1ee'
            'SKIP'
            'a1404be57e245147b01d448b89ed544d865d539d8d09c8cfb9fed6888faf3814'
            'eab2193bf7e00bc2ee7bd53ef7f553bb3cbc86c86f3ba46585c9589024bb76f9'
            'a9b4e458d237333cd3c638e9fea832ef2eb752149b3e9d12c0e8127a9f12565f')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>
options=(debug)

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../kimageformats-avif-fix-1.patch
  patch -p1 -i ../kimageformats-avif-fix-2.patch
  patch -p1 -i ../f475a4b2.patch # Fix build with libavif 0.11
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DKIMAGEFORMATS_HEIF=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
