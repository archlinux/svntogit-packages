# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-xkbevd
pkgver=1.1.4
pkgrel=1
pkgdesc="XKB event daemon"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libxkbfile')
makedepends=('xorg-util-macros')
groups=('xorg-apps' 'xorg')
source=(${url}/releases/individual/app/xkbevd-${pkgver}.tar.bz2{,.sig})
sha256sums=('2430a2e5302a4cb4a5530c1df8cb3721a149bbf8eb377a2898921a145197f96a'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # Alan Coopersmith <alan.coopersmith@oracle.com>

build() {
  cd xkbevd-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd xkbevd-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
