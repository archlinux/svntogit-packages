# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Massimiliano Torromeo < massimiliano DOT torromeo AT gmail DOT com >

pkgbase=python-asn1crypto
pkgname=('python-asn1crypto' 'python2-asn1crypto')
pkgver=1.3.0
pkgrel=1
arch=('any')
license=('MIT')
pkgdesc="Python ASN.1 library with a focus on performance and a pythonic API"
url="https://github.com/wbond/asn1crypto"
makedepends=('python-setuptools' 'python2-setuptools')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/wbond/asn1crypto/archive/$pkgver.tar.gz")
sha512sums=('822eb3b7d2d73383f271d3c2c3eeff7353b3869cdec31166f0b6ce4751271843c41b136abbf5829ce9de82f8ec49121e8aac21ed84d795976706243dbc1200ea')

prepare() {
  cp -a asn1crypto-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/asn1crypto-$pkgver
  python setup.py build

  cd "$srcdir"/asn1crypto-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/asn1crypto-$pkgver
  python run.py tests

  cd "$srcdir"/asn1crypto-$pkgver-py2
  python2 run.py tests
}

package_python-asn1crypto() {
  depends=('python')

  cd asn1crypto-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/python-asn1crypto/LICENSE
}

package_python2-asn1crypto() {
  depends=('python2')

  cd asn1crypto-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/python2-asn1crypto/LICENSE
}
