# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=oxygen-gtk3
pkgver=1.0.4
pkgrel=1
pkgdesc="Port of the default KDE widget theme (Oxygen) to GTK3"
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/playground/artwork/oxygen-gtk/'
license=('LGPL')
depends=('gtk3' 'dbus-glib')
makedepends=('cmake')
source=("http://download.kde.org/stable/${pkgname}/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2")
md5sums=('793786ee97f2d0601ed50a82fc95db25')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
