# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jan de Groot
# Contributor: Charles Mauch <cmauch@gmail.com>

pkgname=perl-mime-types
pkgver=2.16
pkgrel=1
pkgdesc="Perl/CPAN Module MIME::Types : Information and processing MIME types"
arch=('any')
url="http://search.cpan.org/dist/MIME-Types/"
license=('PerlArtistic')
depends=('perl')
options=('!emptydirs')
source=("http://www.cpan.org/authors/id/M/MA/MARKOV/MIME-Types-${pkgver}.tar.gz")
sha512sums=('9a42c44eb4ed50da7518505f5b09c8958f2e2b2163909f2043f7a13ac3c275f994e9534a517781a7548ec5331e60dadee8219bba716c0b4515a1764110b13cb5')

build() {
  cd MIME-Types-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd MIME-Types-${pkgver}
  make test
}

package() {
  cd MIME-Types-${pkgver}
  make DESTDIR="${pkgdir}" install
}
