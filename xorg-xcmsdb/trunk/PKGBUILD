# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-xcmsdb
pkgver=1.0.4
pkgrel=1
pkgdesc="Device Color Characterization utility for X Color Management System"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libx11')
makedepends=('xorg-util-macros')
groups=('xorg-apps' 'xorg')
source=(http://xorg.freedesktop.org/archive/individual/app/xcmsdb-${pkgver}.tar.bz2)
sha1sums=('a633f76af38ea40b33d55c3ac820fd19f17f9428')

build() {
  cd "${srcdir}/xcmsdb-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/xcmsdb-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
