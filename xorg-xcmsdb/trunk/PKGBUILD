# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-xcmsdb
pkgver=1.0.5
pkgrel=2
pkgdesc="Device Color Characterization utility for X Color Management System"
arch=(x86_64)
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libx11')
makedepends=('xorg-util-macros')
groups=('xorg-apps' 'xorg')
source=(https://xorg.freedesktop.org/archive/individual/app/xcmsdb-${pkgver}.tar.bz2{,.sig})
sha512sums=('e47bfce87d7531e7809c300e01120c3e8d6001130d9a6c9f6de5bc19651d8712d273a6f54452223bd8c4bff4612b5ecae8db45eddb022da945ed6e14c9c84ae4'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # Alan Coopersmith <alan.coopersmith@oracle.com>

build() {
  cd xcmsdb-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd xcmsdb-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
