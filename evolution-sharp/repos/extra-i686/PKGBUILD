# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=evolution-sharp
pkgver=0.21.1
pkgrel=1
pkgdesc="C# bindings for Novell Evolution"
arch=(i686 x86_64)
license=('GPL')
url="http://ftp.gnome.org/pub/GNOME/sources/evolution-sharp/"
depends=('evolution-data-server>=2.30.2')
makedepends=('pkg-config' 'gtk-sharp-2>=2.12.8')
options=('!libtool')
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/0.21/${pkgname}-${pkgver}.tar.bz2
        evolution-sharp-0.21.1-fix-retarded-version-check.patch
        evolution-data-server-lib-target.patch)
sha256sums=('3cb78088e332f71b2a7958d2e8c6af3c9ff168731c8fb423aad7038cbdef3a1c'
            '80f98baf3336383241deac73c3f0d3a0905c9a2a3790db7ec4bf6089a577cf5b'
            '687d95adcd70d510ee8b0975a782918175b8c080bcf019742fbcdae0f2b842c7')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -Np1 -i "${srcdir}/evolution-sharp-0.21.1-fix-retarded-version-check.patch"
  patch -Np1 -i "${srcdir}/evolution-data-server-lib-target.patch"
  autoreconf -fi

  export MONO_SHARED_DIR="${startdir}/src/.wabi"
  mkdir -p "${MONO_SHARED_DIR}"

  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var --disable-static
  make DESTDIR="${pkgdir}" install
  rm -f "${pkgdir}/usr/lib/libevolutionglue.a"
} 

