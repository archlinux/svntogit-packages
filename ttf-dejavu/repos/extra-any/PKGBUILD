# $Id$
# Maintainer:
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Michal Krenek <mikos@sg1.cz>

pkgname=ttf-dejavu
pkgver=2.33
pkgrel=2
pkgdesc="Font family based on the Bitstream Vera Fonts with a wider range of characters"
arch=('any')
url="http://dejavu-fonts.org/wiki/Main_Page"
license=('custom')
depends=('fontconfig' 'xorg-fonts-encodings' 'xorg-mkfontscale' 'xorg-mkfontdir')
install=ttf-dejavu.install
source=(http://downloads.sourceforge.net/project/dejavu/dejavu/${pkgver}/dejavu-{,lgc-}fonts-ttf-${pkgver}.tar.bz2)
md5sums=('8b601e91725b6d69141b0fcf527948c0'
         'fde03af699ccf8833f09d7002e3060ef')

package() {
  install -dm755 "${pkgdir}"/etc/fonts/conf.avail
  install -dm755 "${pkgdir}"/usr/share/fonts/TTF

  for i in dejavu-{,lgc-}fonts-ttf; do
    cd "${srcdir}"/${i}-${pkgver}
    install -m644 ttf/*.ttf "${pkgdir}"/usr/share/fonts/TTF/
    install -m644 fontconfig/*.conf "${pkgdir}"/etc/fonts/conf.avail/
  done

  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
