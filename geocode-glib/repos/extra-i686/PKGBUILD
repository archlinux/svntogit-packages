# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=geocode-glib
pkgver=3.20.1
pkgrel=2
pkgdesc="Geocode Helper library"
url="https://git.gnome.org/browse/geocode-glib/"
arch=(i686 x86_64)
license=(LGPL)
depends=(glib2 json-glib libsoup)
makedepends=(intltool gobject-introspection gnome-common)
options=(!emptydirs)
source=(https://download.gnome.org/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('669fc832cabf8cc2f0fc4194a8fa464cdb9c03ebf9aca5353d7cf935ba8637a2')

prepare() {
  cd $pkgname-$pkgver
  sed -i 's/gnome/Adwaita/g' icons/Makefile.am
  NOCONFIGURE=1 gnome-autogen.sh
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="${pkgdir}" install
}

