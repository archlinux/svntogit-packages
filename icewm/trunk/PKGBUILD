# Maintainer:
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=icewm
pkgver=1.6.2
pkgrel=1
pkgdesc="A Window Manager designed for speed, usability, and consistency"
arch=('x86_64')
url="https://ice-wm.org/"
license=('LGPL')
depends=('libxrandr' 'libxinerama' 'libsndfile' 'libao' 'fribidi' 'libxft' 'libxcomposite' 'libxdamage' 'gdk-pixbuf2' 'libxpm')
makedepends=('xorg-mkfontdir' 'asciidoctor' 'librsvg')
optdepends=('perl: for icewm-menu-xrandr')
source=(https://github.com/ice-wm/icewm/releases/download/$pkgver/$pkgname-$pkgver.tar.lz)
sha256sums=('8ff09ae111075f220dbf5cdb13401a2c2a051ccab8c81167d234d6e450c6aa24')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
