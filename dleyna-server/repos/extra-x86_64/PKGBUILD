# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=dleyna-server
pkgver=0.7.0
pkgrel=2
pkgdesc="Library to discover, browse and manipulate Digital Media Servers"
url="https://github.com/phako/dleyna-server"
arch=(x86_64)
license=(LGPL2.1)
depends=(dleyna-connector-dbus gupnp-av gupnp-dlna libsoup)
makedepends=(git meson)
_commit=c8b7ae8a71b506ff37ed65fe0597a78dfe019465  # tags/v0.7.0^0
source=("git+https://github.com/phako/dleyna-server#commit=$_commit"
        fix-rpath.diff)
sha256sums=('SKIP'
            'f815805e63a913e2d25a5d8db8741e72837243068a4af5e0d0680032476c304e')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $pkgname
  git apply -3 ../fix-rpath.diff
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
