# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=dleyna-server
pkgver=0.7.2
pkgrel=1
pkgdesc="Library to discover, browse and manipulate Digital Media Servers"
url="https://github.com/phako/dleyna-server"
arch=(x86_64)
license=(LGPL2.1)
depends=(dleyna-connector-dbus gupnp-av gupnp-dlna libsoup)
makedepends=(git meson)
_commit=48c54e14f14a89b65c85fc210cfb6debc60d5606  # tags/v0.7.2^0
source=("git+https://github.com/phako/dleyna-server#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
