# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Tobias Kieslich <tobias (at) archlinux.org>

pkgname=xfce4-screenshooter
pkgver=1.8.0
pkgrel=2
pkgdesc="Plugin that makes screenshots for the Xfce panel"
arch=('i686' 'x86_64')
url="http://goodies.xfce.org/projects/applications/xfce4-screenshooter"
license=('GPL2')
groups=('xfce4-goodies')
depends=('xfce4-panel' 'libsoup' 'hicolor-icon-theme')
makedepends=('intltool')
install=$pkgname.install
options=('!libtool')
source=(http://archive.xfce.org/src/apps/$pkgname/1.8/$pkgname-$pkgver.tar.bz2
        xfce4-screenshooter-1.7.9-dsofix.patch
        fs-25873-segfault-in-awesome-wm.patch)
conflicts=('xfce4-screenshooter-plugin')
replaces=('xfce4-screenshooter-plugin')
sha256sums=('68748a42ae68c5a8e9ed1c14ec5c741a344ab30b5b325b8812220539548ad83e'
            '6902495c0394af19a76e2f90399ca7295e8dfb3ffb29d3b4f3b3a9fb7489c464'
            '76cc9127167c9f4a800b01e9d16b85bf3af335d0eb44588d859739aa9d457e38')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -Np1 -i "$srcdir/xfce4-screenshooter-1.7.9-dsofix.patch"

  # segfaults trying to capture screenshot of active window in awesome WM
  # https://bugs.archlinux.org/task/25873
  # patch by Bruno Ramos @ https://bugzilla.xfce.org/show_bug.cgi?id=8080#c2
  patch -Np1 -i "$srcdir/fs-25873-segfault-in-awesome-wm.patch"

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
