# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
 
pkgname=libportal
pkgver=0.0.2
pkgrel=1
pkgdesc="GIO-style async APIs for most Flatpak portals"
url="https://github.com/flatpak/libportal"
arch=(x86_64)
license=(LGPL)
depends=(glib2)
makedepends=(git meson gtk-doc)
_commit=e44087afb0c3f2311530e0559debe096abf61fd3  # tags/0.0.2^0
source=("git+https://github.com/flatpak/libportal#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}

# vim:set sw=2 et:
