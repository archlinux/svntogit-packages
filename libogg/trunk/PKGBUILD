# $Id$
# Maintainer: 
# Contributor: John Proctor <jproctor@prium.net>

pkgname=libogg
pkgver=1.2.2
pkgrel=1.1
pkgdesc="Ogg bitstream and framing library"
arch=('i686' 'x86_64')
url="http://www.xiph.org/ogg/"
license=('BSD')
depends=('glibc')
options=('!libtool')
source=(http://downloads.xiph.org/releases/ogg/$pkgname-$pkgver.tar.xz
        config_types.h)
md5sums=('203c28a1d356b641253c960365e4cc48'
         'a4d786b1b1955cb85b64fd16cbbee3f9')

build() {
    cd $srcdir/${pkgname}-${pkgver}
    ./configure --prefix=/usr 
    make
}

package() {
    cd $srcdir/${pkgname}-${pkgver}
    make DESTDIR=$pkgdir install

    # install BSD license
    install -Dm644 $srcdir/$pkgname-$pkgver/COPYING \
	$pkgdir/usr/share/licenses/$pkgname/COPYING

    # Resolve multilib conflict
    if [[ $CARCH == "x86_64" ]]; then
      mv "$pkgdir"/usr/include/ogg/config_types{,-64}.h
      install -m 644 "$srcdir/config_types.h" \
        "${pkgdir}/usr/include/ogg/config_types.h"
    fi
}
