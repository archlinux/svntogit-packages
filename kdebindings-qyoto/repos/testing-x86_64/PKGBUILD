# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdebindings-qyoto
pkgver=4.9.3
pkgrel=1
pkgdesc=".NET/Mono bindings for the Qt libraries"
url="https://projects.kde.org/projects/kde/kdebindings/csharp/qyoto"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kdebindings')
depends=('kdebindings-smokeqt' 'mono')
makedepends=('cmake' 'automoc4' 'kdebindings-smokegen')
conflicts=('kdebindings-csharp')
source=("http://download.kde.org/stable/${pkgver}/src/qyoto-${pkgver}.tar.xz"
        'cmake2810.patch')
sha1sums=('24397a09c50ccc2e87dcd5d07f33d74cc9c5113f'
          'f7745cb0fc4768dc8cab0e0d58b1159fb19805f5')

build() {
  cd qyoto-${pkgver}
  # KDEBUG#209652
  patch -p1 -i "${srcdir}"/cmake2810.patch
  cd ../
 
  mkdir build
  cd build
  cmake ../qyoto-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
