# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdebindings-qyoto
pkgver=4.11.90
pkgrel=1
pkgdesc=".NET/Mono bindings for the Qt libraries"
url="https://projects.kde.org/projects/kde/kdebindings/csharp/qyoto"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kdebindings')
depends=('kdebindings-smokeqt' 'mono')
makedepends=('cmake' 'automoc4' 'kdebindings-smokegen')
conflicts=('kdebindings-csharp')
source=("http://download.kde.org/unstable/${pkgver}/src/qyoto-${pkgver}.tar.xz")
sha1sums=('4d57a5628bec9cc726fd426d30691ae261e40730')

build() {
  mkdir build
  cd build
  cmake ../qyoto-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
