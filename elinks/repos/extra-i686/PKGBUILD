# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Ben <contrasutra@myrealbox.com>

pkgname=elinks
pkgver=0.11.7
pkgrel=3
pkgdesc="An advanced and well-established feature-rich text mode web browser."
arch=("i686" "x86_64")
url="http://elinks.or.cz"
license=('GPL')
depends=('bzip2' 'expat>=2.0' 'gpm>=1.20.4' 'openssl' 'lua>=5.1.1' 'libidn' 'spidermonkey>=1.7.0')
source=("$url/download/${pkgname}-${pkgver}.tar.bz2"
	"${pkgname}.desktop"
	'elinks-0.11.3-lua-5.patch'
	'elinks-0.11.2-lua-5.1.patch')
md5sums=('fcd087a6d2415cd4c6fd1db53dceb646'
         'ef70f6bb7457799a979fb201615369e6'
	 '6b9897e285d87c92b443eb8f3d874696'
	 '73312c85d9651ba30070765138759f4a')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

 # fix 0.11.0 /config.charset not found:
 #  sed -i 's|$(srcdir)/config.charset|$(srcdir)config.charset|g' \
 #        ${startdir}/src/${pkgname}-${pkgver}/src/intl/gettext/Makefile || return 1

  #FS#10515
  patch -Np1 -i ${srcdir}/elinks-0.11.3-lua-5.patch || return 1
  patch -Np1 -i ${srcdir}/elinks-0.11.2-lua-5.1.patch || return 1
  sed -i 's/-llualib$suffix//' configure

  ./configure --prefix=/usr --mandir=/usr/share/man \
              --sysconfdir=/etc \
              --enable-smb --without-x --enable-cgi \
              --enable-leds --enable-256-colors --enable-html-highlight \
              --without-zlib
 # info: 
 # configure: WARNING: Forcing --disable-smb because of vulnerability CVE-2006-5925.
 # If you want to use SMB, please wait for ELinks 0.12.0 or see bug 844.

  make || return 1
  make DESTDIR=${pkgdir} install || return 1
  rm -f ${pkgdir}/usr/share/locale/locale.alias

  # make it nice:
  install -D -m644 ${srcdir}/${pkgname}.desktop \
    ${pkgdir}/usr/share/applications/${pkgname}.desktop
}
