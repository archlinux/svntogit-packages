# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>

pkgname=libxfce4util
pkgver=4.18.1
pkgrel=1
pkgdesc="Basic utility non-GUI functions for Xfce"
arch=('x86_64')
url="https://www.xfce.org/"
license=('GPL2')
depends=('glib2')
makedepends=('intltool' 'gobject-introspection' 'vala')
source=(https://archive.xfce.org/src/xfce/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=('8a52063a5adc66252238cad9ee6997909b59983ed21c77eb83c5e67829d1b01f')

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --localstatedir=/var \
    --disable-debug
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
