# $Id$
# Contributor: John Sowiak <john@archlinux.org>
# Maintainer: tobias <tobias@archlinux.org>

# Apparently, the blender guys refuse to release source tarballs for
# intermediate releases that deal mainly with binaries but incorporate tiny
# minor changes from cvs. Since I'm sick and tired of the urges of users that
# look for release numbers only we make a messy PKGBUILD that can checkout cvs
# tags if necessary.
#_cvs=true
_cvs=false

pkgname=blender
pkgver=2.57
pkgrel=1
pkgdesc="A fully integrated 3D graphics creation suite"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.blender.org"
depends=('libjpeg' 'libpng' 'openjpeg' 'libtiff' 'openexr' 'sdl' 'python' 'desktop-file-utils' \
         'shared-mime-info' 'hicolor-icon-theme' 'xdg-utils' 'gettext' 'libxi' 'libxmu' 'mesa' \
         'freetype2' 'openal' 'libsndfile' 'libsamplerate' 'ffmpeg')
makedepends=('cmake' 'cvs')
install=blender.install
if [ ! $_cvs -o $_cvs = false ]; then
  makedepends=('cmake')
  source=(http://download.blender.org/source/$pkgname-$pkgver.tar.gz)
  md5sums=('e6cb5523323a71c070051e6fe6efe497')
fi

build() {
  if [ $_cvs = true ]; then
    cd "$srcdir/"
    #cvs -d:pserver:anonymous@cvs.blender.org:/cvsroot/bf-blender login
    cvs -z3 -d:pserver:anonymous@cvs.blender.org:/cvsroot/bf-blender checkout $pkgname
    _cvsrel=$pkgname-$(echo $pkgver | sed 's/\./-/')-release
    cvs -d:pserver:anonymous@cvs.blender.org:/cvsroot/bf-blender update -r $_cvsrel
    find $pkgname/{bin,release/scripts} -name 'CVS' -exec rm -rf {} \; 2> /dev/null
    cd $pkgname
  else
    cd "$srcdir/$pkgname-$pkgver"
  fi

  mkdir build
  cd build

  cmake .. \
    -DCMAKE_INSTALL_PREFIX:PATH=/usr \
    -DCMAKE_BUILD_TYPE:STRING=Release \
    -DWITH_INSTALL_PORTABLE:BOOL=OFF \
    -DWITH_PYTHON_INSTALL:BOOL=OFF \
    -DWITH_OPENCOLLADA:BOOL=OFF \
    -DWITH_GAMEENGINE:BOOL=ON \
    -DWITH_PLAYER:BOOL=ON \
    -DPYTHON_VERSION:STRING=3.2 \
    -DPYTHON_LIBPATH:STRING=/usr/lib \
    -DPYTHON_LIBRARY:STRING=python3.2mu \
    -DPYTHON_INCLUDE_DIRS:STRING=/usr/include/python3.2mu

  make $MAKEFLAGS
}

package() {
  if [ $_cvs = true ]; then
    cd "$srcdir/$pkgname"
  else
    cd "$srcdir/$pkgname-$pkgver"
  fi

  cd build
  make DESTDIR="${pkgdir}" install
  python -m compileall "${pkgdir}/usr/share/blender"

  for i in 128 16 192 22 32 48 64 96 ; do
    install -D -m644 ../release/freedesktop/icons/${i}x${i}/blender.png \
      "${pkgdir}/usr/share/icons/hicolor/${i}x${i}/apps/blender.png"
  done

  install -D -m644 ../release/freedesktop/icons/scalable/blender.svg \
    "${pkgdir}/usr/share/icons/hicolor/scalable/apps/blender.svg"
}
