# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org> 
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Daniel Bainton <dpb [at] backarrow.org>

pkgname=ncmpc
pkgver=0.22
pkgrel=1
pkgdesc='Fully featured MPD client using ncurses'
url='http://www.musicpd.org/clients/ncmpc/'
arch=('i686' 'x86_64')
license=('GPL')
depends=('ncurses' 'glib2' 'libmpdclient')
optdepends=('python2: to enable lyrics plugin'
            'ruby: to enable lyrics plugin')
source=("http://www.musicpd.org/download/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"
        "po::git://git.musicpd.org/master/ncmpc.git#commit=a0dcef5c1685813aa7f70bf7b7fd5f95f441d1b1")
makedepends=('git')
sha1sums=('addb9ae527a1a2fe3dffe0ef2b6b4db79aeaeee4'
          'SKIP')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	cp -a ../po/po/*.po po
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--enable-lyrics-screen \
		--with-lyrics-plugin-dir=/usr/share/ncmpc/lyrics
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	for i in lyrics/*; do install -Dm755 "$i" "${pkgdir}/usr/share/ncmpc/$i"; done
}
