# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org> 
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Daniel Bainton <dpb [at] backarrow.org>

pkgname=ncmpc
pkgver=0.30
pkgrel=2
pkgdesc='Fully featured MPD client using ncurses'
url='https://www.musicpd.org/clients/ncmpc/'
arch=('x86_64')
license=('GPL')
depends=('ncurses' 'glib2' 'libmpdclient')
optdepends=('python2: to enable lyrics plugin'
            'ruby: to enable lyrics plugin')
makedepends=('meson' 'python-sphinx')
validpgpkeys=('0392335A78083894A4301C43236E8A58C6DB4512')
source=("https://www.musicpd.org/download/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"{,.sig}
        'progressbar.patch')
sha256sums=('e3fe0cb58b8a77f63fb1645c2f974b334f1614efdc834ec698ee7d861f1b12a3'
            'SKIP'
            '5ccc53da4f8e6a3fa2a2f7abad878bae846737a89cbde550170b25fa46d0292e')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	sed "s/curses == 'ncursesw'/false/g" -i meson.build
	patch -p1 -i ../progressbar.patch
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	arch-meson . build -Dlyrics_screen=true
	ninja -C build
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	DESTDIR="${pkgdir}" ninja -C build install
}
