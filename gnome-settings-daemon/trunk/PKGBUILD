# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-settings-daemon
pkgver=2.29.90
pkgrel=1
pkgdesc="The GNOME Settings daemon"
arch=(i686 x86_64)
license=('GPL')
depends=('libgnomekbd>=2.29.5' 'gnome-desktop>=2.29.90' 'libnotify>=0.4.5' 'libxxf86misc' 'gstreamer0.10-base>=0.10.26' 'hicolor-icon-theme')
makedepends=('intltool' 'pkgconfig')
conflicts=('gnome-control-center<2.22.0')
options=(!emptydirs !libtool)
install=gnome-settings-daemon.install
url="http://www.gnome.org"
groups=('gnome')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.29/${pkgname}-${pkgver}.tar.bz2
        gstreamer-volume.patch)
sha256sums=('d0267a6ef0a276768353aaeb23197178f658cdf92d33669b9a8e6e5847e3ac0f'
            'b0a77d17b095432b4c3858dff0ba192578d8ae92705e5b23f636268684fab516')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/gstreamer-volume.patch" || return 1
  libtoolize --force || return 1
  aclocal || return 1
  autoconf || return 1
  automake || return 1
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/usr/bin --disable-static || return 1

  make || return 1
  make DESTDIR="${pkgdir}" GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 install || return 1

  install -d -m755 "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gnome-settings-daemon ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
