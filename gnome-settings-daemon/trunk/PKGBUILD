# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-settings-daemon
pkgver=2.30.0
pkgrel=1
pkgdesc="The GNOME Settings daemon"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libgnomekbd>=2.30.0' 'gnome-desktop>=2.29.92' 'libnotify>=0.4.5' 'libxxf86misc' 'gstreamer0.10-base>=0.10.26' 'hicolor-icon-theme')
makedepends=('intltool' 'pkgconfig')
conflicts=('gnome-control-center<2.22.0')
options=(!emptydirs !libtool)
install=gnome-settings-daemon.install
url="http://www.gnome.org"
groups=('gnome')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.30/${pkgname}-${pkgver}.tar.bz2
        gstreamer-volume-2.30.patch)
sha256sums=('cb759f2aa403ad10f7b0a196c523fa0b3842cf4432b26a4b308ed1e5bf67e6aa'
            '4b2a14c0e238f3cde71619b74f178544151bb319dee84b52133a254b85548582')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/gstreamer-volume-2.30.patch" || return 1
  libtoolize --force || return 1
  aclocal || return 1
  autoconf || return 1
  automake || return 1
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/usr/bin --disable-static || return 1

  make || return 1
  make DESTDIR="${pkgdir}" GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 install || return 1

  install -d -m755 "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gnome-settings-daemon ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
