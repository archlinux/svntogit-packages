# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-settings-daemon
pkgver=2.32.0
pkgrel=1
pkgdesc="The GNOME Settings daemon"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libgnomekbd>=2.31.92' 'gnome-desktop>=2.32.0' 'libnotify>=0.4.5' 'gstreamer0.10-base>=0.10.30' 'hicolor-icon-theme')
makedepends=('intltool' 'pkg-config')
options=(!emptydirs !libtool)
install=gnome-settings-daemon.install
url="http://www.gnome.org"
groups=('gnome')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.32/${pkgname}-${pkgver}.tar.bz2
        gstreamer-volume-2.30.patch)
sha256sums=('d5eb0270028f072e8d36c61e4677d55844669d9ab09d525f6c76b4980ec81c9e'
            '4b2a14c0e238f3cde71619b74f178544151bb319dee84b52133a254b85548582')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/gstreamer-volume-2.30.patch"
  libtoolize --force
  aclocal
  autoconf
  automake
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/usr/bin --disable-static
  make
  make DESTDIR="${pkgdir}" GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 install

  install -d -m755 "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gnome-settings-daemon ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
