# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>

pkgname=tar
pkgver=1.27.1
pkgrel=1
pkgdesc="Utility used to store, backup, and transport files"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/tar/tar.html"
license=('GPL3')
groups=('base')
depends=('glibc' 'sh')
options=('!emptydirs')
install=tar.install
source=(ftp://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.xz{,.sig})
md5sums=('e0382a4064e09a4943f3adeff1435978'
         'SKIP')

build() {
  cd ${srcdir}/$pkgname-$pkgver
  ./configure --prefix=/usr --libexecdir=/usr/lib/tar
  make
}

check() {
  cd ${srcdir}/$pkgname-$pkgver
  make check
}
  
package() {
  cd ${srcdir}/$pkgname-$pkgver
  make DESTDIR=${pkgdir} install
}
