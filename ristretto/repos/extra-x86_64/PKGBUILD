# $Id$
# Maintainer:
# Contributor: AndyRTR <andyrtr@archlinux.org>
# Contributor: Ronald van Haren <ronald.archlinux.org>

pkgname=ristretto
pkgver=0.3.4
pkgrel=1
pkgdesc="A fast and lightweight picture-viewer for Xfce"
arch=('i686' 'x86_64')
url="http://goodies.xfce.org/projects/applications/ristretto"
license=('GPL')
depends=('libxfce4ui' 'libexif' 'exo' 'desktop-file-utils')
makedepends=('intltool')
groups=('xfce4-goodies')
install=ristretto.install
source=("http://archive.xfce.org/src/apps/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2")
sha1sums=('7da6c287c45949193fd6046e82a099fd0469278c')

build() {
  cd "${srcdir}/$pkgname-$pkgver"
  ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--localstatedir=/var \
	--disable-debug
  make
}

package() {
  cd "${srcdir}/$pkgname-$pkgver"
  make DESTDIR="${pkgdir}" install
}
