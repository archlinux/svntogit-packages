# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=epiphany
pkgver=3.28.1.1
pkgrel=1
pkgdesc="A GNOME web browser based on the WebKit rendering engine"
url="https://wiki.gnome.org/Apps/Web"
arch=(x86_64)
license=(GPL)
depends=(webkit2gtk gcr icu)
makedepends=(itstool docbook-xml startup-notification lsb-release
             gobject-introspection yelp-tools appstream-glib git meson)
groups=(gnome)
_commit=a7c3e91e83acf368b621f175d962bf193a0cf5dd  # tags/3.28.1.1^0
source=("git+https://git.gnome.org/browse/epiphany#commit=$_commit"
        pluginsdir.diff)
sha256sums=('SKIP'
            'b6c8ee6ace934c053f2fd89758e0b587cb8953c6b0246c1359aecaae4de70289')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  patch -Np1 -i ../pluginsdir.diff
}

build() {
  arch-meson $pkgname build \
    -D distributor_name="Arch LinuxÂ®"
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}
