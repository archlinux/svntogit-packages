# $Id$
# Maintainer: dale <dale@archlinux.org>

pkgname=epiphany
pkgver=2.28.1
pkgrel=2
install=epiphany.install
pkgdesc="A GNOME2 web browser based on the WebKit rendering engine."
arch=(i686 x86_64)
license=('GPL')
depends=('libwebkit>=1.1.15.3-2' 'startup-notification>=0.10' 'iso-codes>=3.10.1' 'desktop-file-utils' 'libxslt>=1.1.24' 'libnotify>=0.4.5' 'avahi>=0.6.25' 'libsoup-gnome>=2.28.0')
makedepends=('gnome-doc-utils>=0.18.0' 'pkgconfig' 'networkmanager' 'intltool')
options=('!libtool' '!emptydirs')
groups=('gnome')
url="http://www.gnome.org/projects/epiphany/"
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/2.28/${pkgname}-${pkgver}.tar.bz2
        execute-javascript.patch
        mailto-fix.patch
        ephy-download-mime.patch)
sha256sums=('95ccdd36bc7fdf95c6085da3b76cee6467f3c9a8745541cfb09fdd8d33a1f04d'
            'b9e47e1fb6a38d76f529bc7ae8f754adea05eee4f8c024d302a0e84dd6147ef3'
            '0ba4e4b6708fd2394ff2868a6f793627906f45230d0ef80d268c10b69502fe73'
            '3e8a13d14afd5fb16486fffbafdd8bc47b117d948e13870712bd067586133728')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/execute-javascript.patch" || return 1
  patch -Np1 -i "${srcdir}/mailto-fix.patch" || return 1
  patch -Np1 -i "${srcdir}/ephy-download-mime.patch" || return 1
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var \
      --disable-scrollkeeper \
      --enable-network-manager || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain epiphany ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
