# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=epiphany
pkgver=3.1.90
pkgrel=1
install=epiphany.install
pkgdesc="A GNOME web browser based on the WebKit rendering engine."
arch=('i686' 'x86_64')
license=('GPL')
depends=('libsoup-gnome' 'libnotify' 'gsettings-desktop-schemas' 'libwebkit3' 'nss'
         'iso-codes' 'dconf' 'gobject-introspection' 'desktop-file-utils'
         'hicolor-icon-theme' 'seed')
makedepends=('intltool' 'networkmanager' 'gnome-doc-utils' 'startup-notification')
options=('!libtool' '!emptydirs')
groups=('gnome')
url="http://www.gnome.org/projects/epiphany/"
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('00351a3f23c53b83ff8fffe8ed6dbec7f300588622d51c8326b89b93b5d5abc6')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var \
      --disable-scrollkeeper \
      --enable-network-manager \
      --enable-seed
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
