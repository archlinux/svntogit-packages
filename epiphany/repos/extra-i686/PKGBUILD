# $Id$
# Maintainer: dale <dale@archlinux.org>

pkgname=epiphany
pkgver=2.24.1
pkgrel=1
install=epiphany.install
pkgdesc="A GNOME2 web browser based on the mozilla rendering engine."
arch=(i686 x86_64)
license=('GPL')
depends=('xulrunner>=1.9.0.3' 'gnome-desktop>=2.24.1' 'iso-codes>=2.0' 'desktop-file-utils' 'libxslt>=1.1.24' 'enchant>=1.4.2' 'libnotify>=0.4.4' 'libcanberra')
makedepends=('perlxml' 'gnome-doc-utils>=0.14.0' 'gnome-python>=2.22.3' 'pkgconfig' 'libnetworkmanager>=0.6.6')
options=('!libtool' '!emptydirs')
groups=('gnome')
url="http://www.gnome.org/projects/epiphany/"
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/2.24/${pkgname}-${pkgver}.tar.bz2)
md5sums=('3276f4ca4bef98606f84fc64ce46c9c4')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
        --localstatedir=/var \
        --enable-python \
  	--enable-certificate-manager \
	--enable-dbus \
        --with-gecko=libxul-embedding \
	--disable-scrollkeeper \
	--enable-network-manager \
	--enable-spell-checker || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
