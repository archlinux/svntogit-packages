# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=epiphany
pkgver=3.20.1
pkgrel=2
pkgdesc="A GNOME web browser based on the WebKit rendering engine"
arch=(i686 x86_64)
license=(GPL)
depends=(libsoup libnotify gsettings-desktop-schemas webkit2gtk nss iso-codes dconf gcr gnome-desktop libwnck3 gnome-themes-standard)
makedepends=(intltool itstool docbook-xml startup-notification gobject-introspection)
options=(!emptydirs)
groups=(gnome)
url="https://wiki.gnome.org/Apps/Web"
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('7171f67d5edde0e82b8bad4761638622aa969d6ffcea896cd94bbe509af9c9e7')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --libexecdir=/usr/lib/$pkgname

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
