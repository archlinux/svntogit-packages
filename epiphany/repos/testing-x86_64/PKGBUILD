# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=epiphany
pkgver=3.28.3.1
pkgrel=2
pkgdesc="A GNOME web browser based on the WebKit rendering engine"
url="https://wiki.gnome.org/Apps/Web"
arch=(x86_64)
license=(GPL)
depends=(webkit2gtk gcr icu)
makedepends=(itstool docbook-xml startup-notification lsb-release
             gobject-introspection yelp-tools appstream-glib git meson)
groups=(gnome)
_commit=a5a9a7e2904e2206b3cc863ab13748255247e3eb  # tags/3.28.3.1^0
source=("git+https://gitlab.gnome.org/GNOME/epiphany.git#commit=$_commit"
        pluginsdir.diff)
sha256sums=('SKIP'
            'b6c8ee6ace934c053f2fd89758e0b587cb8953c6b0246c1359aecaae4de70289')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  patch -Np1 -i ../pluginsdir.diff
}

build() {
  arch-meson $pkgname build \
    -D distributor_name="Arch LinuxÂ®"
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}
