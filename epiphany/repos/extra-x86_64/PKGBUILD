# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=epiphany
pkgver=3.24.2
pkgrel=1
pkgdesc="A GNOME web browser based on the WebKit rendering engine"
url="https://wiki.gnome.org/Apps/Web"
arch=(i686 x86_64)
license=(GPL)
depends=(webkit2gtk gcr gnome-desktop icu)
makedepends=(intltool itstool docbook-xml startup-notification lsb-release
             gobject-introspection yelp-tools autoconf-archive appstream-glib git)
groups=(gnome)
_commit=d0e5c8fff89633b504cb9dc02638b1c25f227c38  # tags/3.24.2^0
source=("git+https://git.gnome.org/browse/epiphany#commit=$_commit"
        "git+https://git.gnome.org/browse/libgd"
        "git+https://git.gnome.org/browse/gvdb"
        pluginsdir.diff)
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            '3bbd4b36e697a3a9165e0b71d5af566fe96a8f1237d854b9dee9a7e0b9fef8e3')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  patch -Np1 -i ../pluginsdir.diff

  git submodule init
  git config --local submodule.libgd.url "$srcdir/libgd"
  git config --local submodule.gvdb.url "$srcdir/gvdb"
  git submodule update

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --libexecdir=/usr/lib/$pkgname --enable-compile-warnings=yes

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
