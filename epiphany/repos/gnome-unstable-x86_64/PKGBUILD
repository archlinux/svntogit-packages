# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=epiphany
pkgver=3.28.0.1+4+g051d4f616
pkgrel=1
pkgdesc="A GNOME web browser based on the WebKit rendering engine"
url="https://wiki.gnome.org/Apps/Web"
arch=(x86_64)
license=(GPL)
depends=(webkit2gtk gcr libdazzle icu libhttpseverywhere)
makedepends=(itstool docbook-xml startup-notification lsb-release
             gobject-introspection yelp-tools appstream-glib git meson)
groups=(gnome)
_commit=051d4f6167651a2de54b5822c5f968f4c236079d  # gnome-3-28
source=("git+https://git.gnome.org/browse/epiphany#commit=$_commit"
        pluginsdir.diff)
sha256sums=('SKIP'
            'b6c8ee6ace934c053f2fd89758e0b587cb8953c6b0246c1359aecaae4de70289')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  patch -Np1 -i ../pluginsdir.diff
}

build() {
  arch-meson $pkgname build \
    -D https_everywhere=true \
    -D distributor_name="Arch LinuxÂ®"
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}
