# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kio
pkgver=r11.621a455
pkgrel=1
pkgdesc='KIO'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kio'
license=('LGPL')
depends=('krb5' 'solid' 'knotifications' 'kjobwidgets' 'kbookmarks'
         'qt5-script')
makedepends=('extra-cmake-modules' 'git')
groups=('kf5')
install=kio.install
source=('git://anongit.kde.org/kio.git')
md5sums=('SKIP')

pkgver() {
  cd kio
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build

  # There's something wrong with our kdoctools but I don't get it
  sed -i 's/add_subdirectory(docs)/#add_subdirectory(docs)/' \
    kio/CMakeLists.txt
}

build() {
  cd build
  cmake ../kio \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
#    -DSYSCONF_INSTALL_DIR=/etc
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
