# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>

pkgname=gnupg2
pkgver=2.0.18
pkgrel=1
pkgdesc="GNU Privacy Guard 2 - a PGP replacement tool"
arch=('i686' 'x86_64')
depends=('libldap' 'curl' 'bzip2' 'zlib' 'libksba' 'libgcrypt'
	'pth' 'libusb-compat' 'libassuan' 'texinfo' 'readline' 'pinentry' 'dirmngr')
license=('GPL')
url="http://www.gnupg.org/"
install=${pkgname}.install
source=(ftp://ftp.gnupg.org/gcrypt/gnupg/gnupg-$pkgver.tar.bz2{,.sig})
sha1sums=('5ec2f718760cc3121970a140aeea004b64545c46'
          'c1b15a6c204434081e2bd8249dde233b6c88c4d0')

build() {
  cd ${srcdir}/gnupg-$pkgver
  ./configure --prefix=/usr --libexecdir=/usr/lib/gnupg2
  make
}

check() {
  cd ${srcdir}/gnupg-$pkgver
  make check
}

package() {
  cd ${srcdir}/gnupg-$pkgver
  make DESTDIR=${pkgdir} install
  # move conflicting files
  mv ${pkgdir}/usr/share/gnupg{,2}
}
md5sums=('2f37e0722666a0fedbe4d9f9227ac4d7')
