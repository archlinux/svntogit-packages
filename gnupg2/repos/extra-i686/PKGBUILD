# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>

pkgname=gnupg2
pkgver=2.0.15
pkgrel=1
pkgdesc="GNU Privacy Guard 2 - a PGP replacement tool"
arch=('i686' 'x86_64')
depends=('libldap' 'curl' 'bzip2' 'zlib' 'libksba' 'libgcrypt'
	'pth' 'libusb' 'libassuan' 'texinfo' 'readline' 'pinentry' 'dirmngr')
license=('GPL')
url="http://www.gnupg.org/"
install=${pkgname}.install
source=(ftp://ftp.gnupg.org/gcrypt/gnupg/gnupg-$pkgver.tar.bz2)
md5sums=('c1286e85b66349879dc4b760dd83e2f1')

build() {
  cd ${srcdir}/gnupg-$pkgver
  ./configure --prefix=/usr --libexecdir=/usr/lib/gnupg2 #$EXTRAOPTS
  make || return 1
}

package() {
  cd ${srcdir}/gnupg-$pkgver
  make DESTDIR=${pkgdir} install || return 1
  # move conflicting files
  mv ${pkgdir}/usr/share/gnupg{,2}
  rm -f ${pkgdir}/usr/share/info/dir
}
