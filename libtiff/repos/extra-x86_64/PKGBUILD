# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=libtiff
pkgver=4.3.0
pkgrel=1
pkgdesc='Library for manipulation of TIFF images'
url='http://www.simplesystems.org/libtiff/'
arch=('x86_64')
license=('custom')
depends=('glibc' 'libjpeg' 'libjpeg.so' 'zlib' 'xz' 'zstd')
makedepends=('freeglut' 'glu' 'mesa' 'jbigkit')
optdepends=('freeglut: for using tiffgt')
provides=('libtiff.so' 'libtiffxx.so')
source=(https://download.osgeo.org/libtiff/tiff-${pkgver}.tar.gz{,.sig})
sha256sums=('0e46e5acb087ce7d1ac53cf4f56a09b221537fc86dfc5daaad1c2e89e1b37ac8'
            'SKIP')
b2sums=('433a087249541f6142dd25b6dc807018639908175b0d73b4af3aa301af2dee3d76184e4689e7b2f1f9b73567ac78d50456ee94fffe00bad6da05375cf2d94d92'
        'SKIP')
validpgpkeys=('EBDFDB21B020EE8FD151A88DE301047DE1198975'  # Bob Friesenhahn <bfriesen@simple.dallas.tx.us>
	      'B1FA7D81EEB8E66399178B9733EBBFC47B3DD87D') # Even Rouault <even.rouault@spatialys.com>

build() {
  cd tiff-${pkgver}
  ./configure \
    --prefix=/usr \
	  --sysconfdir=/etc \
	  --mandir=/usr/share/man \
    --with-docdir=/usr/share/doc/${pkgname}
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd tiff-${pkgver}
  make check
}

package() {
  cd tiff-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 COPYRIGHT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
