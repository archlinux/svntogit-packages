# $Id$
# Maintainer: Jan de Groot  <jgc@archlinux.org>

pkgname=libgweather
pkgver=3.0.0
pkgrel=1
pkgdesc="Provides access to weather information from the net"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('gconf' 'libsoup-gnome' 'gnome-icon-theme')
makedepends=('pkgconfig' 'intltool' 'gtk-doc' 'gobject-introspection')
options=('!libtool' '!emptydirs')
url="http://www.gnome.org/"
install=libgweather.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2
        gettext-not-xml.patch)
sha256sums=('f13eb69c6ec8c4db08a78ffe980a5dd0e194894d411691a6821a068c650d1545'
            'f21d55ab7916a50cad10d1c0bb3785a262e8ad8f35b9fb4c02ae23f1b0783727')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    patch -Np1 -i "${srcdir}/gettext-not-xml.patch"
    gtkdocize
    autoreconf -fi

    ./configure --prefix=/usr --sysconfdir=/etc \
        --localstatedir=/var --disable-static \
        --enable-locations-compression
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

    install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
    gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain libgweather ${pkgdir}/etc/gconf/schemas/*.schemas
    rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
