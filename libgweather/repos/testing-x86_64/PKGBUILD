# $Id$
# Maintainer: Jan de Groot  <jgc@archlinux.org>
pkgname=libgweather
pkgver=2.24.0
pkgrel=1
pkgdesc="Provides access to weather information from the net"
arch=(i686 x86_64)
license=('LGPL')
depends=('gconf>=2.24.0' 'libsoup>=2.24.0.1')
makedepends=('pkgconfig' 'perlxml')
options=('!libtool' '!emptydirs')
conflicts=('gnome-applets<2.22.0')
url="http://www.gnome.org/"
install=libgweather.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.24/${pkgname}-${pkgver}.tar.bz2)
md5sums=('d006328ec5cebc5dbf3a18834db95bba')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var --disable-static || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
