# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=libnm-qt
pkgver=0.9.8.1
pkgrel=1
pkgdesc='Qt-only wrapper for NetworkManager DBus API'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/extragear/libs/libnm-qt'
license=('LGPL')
depends=('libmm-qt' 'networkmanager')
makedepends=('cmake' 'doxygen')
source=("http://download.kde.org/unstable/networkmanager-qt/${pkgver}/src/${pkgname}-${pkgver}.tar.xz")
sha256sums=('cde8bed2beb57015cb5f6772c1fe0843aab299b9529578c5406ba7fe614af23d')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
