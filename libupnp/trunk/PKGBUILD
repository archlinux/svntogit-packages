# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: kastor <kastor@fobos.org.ar>

_name=pupnp
pkgname=libupnp
pkgver=1.14.2
pkgrel=1
pkgdesc='Portable Open Source UPnP Development Kit'
url='http://pupnp.sourceforge.net/'
arch=('x86_64')
license=('BSD')
depends=('glibc')
provides=('libixml.so' 'libupnp.so')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${_name}/${_name}/archive/release-${pkgver}.tar.gz")
sha512sums=('f2571dd749c5977db1edf4b01adc06391f6c4e2d8795cf7ca5eaedf60e4b00375fdd3f989b6ab05c86469f50fbe01ae2de4bada903bb9d8778d8ac4f8fba0c3d')
b2sums=('012b51cefda2654aadb8206ad28b0f72fb794a20c8f79cfaf1730b249a1f61a12caadf216573b7d35a2550e76df87bebb0856dd87f8d5e4e269c77c796a4498d')

prepare() {
  mv -v ${_name}-release-${pkgver} ${pkgname}-${pkgver}
  cd ${pkgname}-${pkgver}
  autoreconf -fiv
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make -k check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  install -vDm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 {ChangeLog,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
