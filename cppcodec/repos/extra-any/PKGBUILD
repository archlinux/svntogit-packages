# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=cppcodec
_gitcommit=302dc28f8fd5c8bf2ea8d7212aed3be884d5d166
pkgver=0.2
pkgrel=1
pkgdesc='Header-only C++11 library to encode/decode base64, base64url, base32, base32hex and hex'
url='https://github.com/tplgy/cppcodec'
arch=('any')
license=('MIT')
makedepends=('git' 'cmake' 'catch2')
source=("${pkgname}::git+https://github.com/tplgy/cppcodec#commit=${_gitcommit}?signed")
sha512sums=('SKIP')
validpgpkeys=('5DE3E0509C47EA3CF04A42D34AEE18F83AFDEB23') # GitHub (web-flow commit signing) <noreply@github.com>

pkgver() {
  cd ${pkgname}
  git describe --always --tags | sed 's/^v//;s/-/+/g'
}


prepare() {
  cd ${pkgname}
  mkdir -p build
}

build() {
  cd ${pkgname}/build
  cmake .. \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr
  make
}

check() {
  cd ${pkgname}/build
  make test
}

package() {
  cd ${pkgname}
  make -C build DESTDIR="${pkgdir}" install
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 example/* -t "${pkgdir}/usr/share/doc/${pkgname}/example"
}

# vim: ts=2 sw=2 et:
