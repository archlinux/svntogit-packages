# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kdepim
pkgname=(akonadiconsole akregator blogilo konsolekalendar kaddressbook kalarm kmail knotes kontact korganizer ktnef kdepim-common)
pkgver=16.04.1
pkgrel=1
arch=('i686' 'x86_64')
url='http://pim.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdepim')
makedepends=(extra-cmake-modules kdepim-runtime qt5-tools kdoctools knewstuff kpimtextedit
		ktexteditor kdnssd kldap libktnef kontactinterface kxmlrpcclient kblog akonadi-search boost
		libgravatar mailcommon incidenceeditor libksieve python)
source=("http://download.kde.org/stable/applications/$pkgver/src/$pkgbase-$pkgver.tar.xz")
sha1sums=('29507cacca3bdf9a948c55ee6b8081a41abd4d80')

prepare() {
  mkdir -p build
}

build() {
	cd build
	cmake ../$pkgbase-$pkgver \
		-DCMAKE_BUILD_TYPE=Release \
		-DBUILD_TESTING=OFF \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_INSTALL_LIBDIR=lib \
		-DKDE_INSTALL_LIBEXECDIR=lib
	make
}

package_akonadiconsole() {
	pkgdesc='Akonadi Management and Debugging Console'
	depends=(calendarsupport messagelib hicolor-icon-theme)
	conflicts=("kdepim-$pkgname")
	replaces=("kdepim-$pkgname")
	url='http://pim.kde.org'

	cd build/akonadiconsole
	make DESTDIR="$pkgdir" install
}

package_akregator() {
	pkgdesc='A Feed Reader for KDE'
	depends=('kdepim-common' 'kontactinterface')
        conflicts=("kdepim-$pkgname")
        replaces=("kdepim-$pkgname")
	url="http://kde.org/applications/internet/akregator/"

	cd build/akregator
	make DESTDIR="$pkgdir" install

	cd "$srcdir"/build/doc/akregator
	make DESTDIR="$pkgdir" install
}

package_blogilo() {
	pkgdesc='A KDE Blogging Client'
	depends=('kdepim-common' 'kblog' 'ktexteditor')
        conflicts=("kdepim-$pkgname")
        replaces=("kdepim-$pkgname")
	url="http://kde.org/applications/internet/blogilo/"
	cd build/blogilo
	make DESTDIR="$pkgdir" install
	cd "$srcdir"/build/doc/blogilo
	make DESTDIR="$pkgdir" install
}

package_konsolekalendar() {
	pkgdesc='Command line tool for accessing calendar files'
	depends=(calendarsupport hicolor-icon-theme)
	conflicts=('kdepim-console')
	replaces=('kdepim-console')
	url='http://pim.kde.org'

	cd build/console
	make DESTDIR="$pkgdir" install

        cd "$srcdir"/build/doc/konsolekalendar
        make DESTDIR="$pkgdir" install
}

package_kaddressbook() {
	pkgdesc='Contact Manager'
	depends=('kdepim-common' 'kontactinterface')
        optdepends=('kdepim-addons: additional plugins')
        conflicts=("kdepim-$pkgname")
        replaces=("kdepim-$pkgname")
	url="http://kde.org/applications/office/kaddressbook/"

	cd "$srcdir"/build/kaddressbook
	make DESTDIR="$pkgdir" install
}

package_kalarm() {
	pkgdesc='Personal Alarm Scheduler'
	depends=('kdepim-common')
        conflicts=("kdepim-$pkgname")
        replaces=("kdepim-$pkgname")
	url="http://kde.org/applications/utilities/kalarm/"

	cd build/kalarm
	make DESTDIR="$pkgdir" install

	cd "$srcdir"/build/doc/kalarm
	make DESTDIR="$pkgdir" install
}

package_kmail() {
	pkgdesc='Mail Client'
	depends=(kdepim-common kontactinterface kio-pim libksieve kdepim-addons)
        optdepends=('kleopatra: mail encryption support' 'kdepim-addons: additional plugins')
        conflicts=("kdepim-$pkgname")
        replaces=("kdepim-$pkgname")
	url="http://kde.org/applications/internet/kmail/"

	for _i in kmail agents/archivemailagent agents/mailfilteragent \
	  storageservicemanager importwizard grantleeeditor mboximporter sieveeditor; do
	  cd "$srcdir"/build/$_i
	  make DESTDIR="$pkgdir" install
	done

        for _i in akonadi_archivemail_agent kmail importwizard sieveeditor; do
          cd "$srcdir"/build/doc/$_i
          make DESTDIR="$pkgdir" install
        done
}

package_knotes() {
	pkgdesc='Popup Notes'
	depends=('kdepim-common' 'kontactinterface')
        conflicts=("kdepim-$pkgname")
        replaces=("kdepim-$pkgname")
	url="http://kde.org/applications/utilities/knotes/"

        cd "$srcdir"/build/knotes
        make DESTDIR="$pkgdir" install

        for _i in knotes akonadi_notes_agent; do
          cd "$srcdir"/build/doc/$_i
          make DESTDIR="$pkgdir" install
        done
}

package_kontact() {
	pkgdesc='Personal Information Manager'
	depends=('kdepim-common' 'kontactinterface')
        conflicts=("kdepim-$pkgname")
        replaces=("kdepim-$pkgname")
	url="http://kde.org/applications/office/kontact/"

	cd build/kontact
	make DESTDIR="$pkgdir" install

	cd "$srcdir"/build/doc/kontact
	make DESTDIR="$pkgdir" install
        cd "$srcdir"/build/doc/kontact-admin
        make DESTDIR="$pkgdir" install
}

package_korganizer() {
	pkgdesc='Calendar and Scheduling Program'
	depends=('kdepim-common' 'kontactinterface')
        optdepends=('kdepim-addons: additional plugins')
        conflicts=("kdepim-$pkgname")
        replaces=("kdepim-$pkgname")
	url="http://kde.org/applications/office/korganizer"

	for _i in korganizer korgac; do
	  cd "$srcdir"/build/$_i
	  make DESTDIR="$pkgdir" install
	done

	cd "$srcdir"/build/doc/korganizer
	make DESTDIR="$pkgdir" install
}

package_ktnef() {
	pkgdesc='A viewer/extractor for TNEF files'
	depends=('libktnef' 'hicolor-icon-theme')
        conflicts=("kdepim-$pkgname")
        replaces=("kdepim-$pkgname")

	cd build/ktnef
	make DESTDIR="$pkgdir" install

	cd "$srcdir"/build/doc/ktnef
	make DESTDIR="$pkgdir" install
}

package_kdepim-common() {
	pkgdesc='Common libraries and resources for KDE PIM applications'
        groups=()
	depends=(incidenceeditor mailcommon kdepim-runtime kdnssd libktnef)
        conflicts=('kdepim-libkdepim' 'kdepim-knode' 'kdepim-kresources' 'kdepim-ktimetracker' 'libkdepim<16')
	url='http://pim.kde.org'
	for _i in agents/sendlateragent accountwizard agents/followupreminderagent plugins/messageviewer pimsettingexporter; do
		cd "$srcdir"/build/$_i
		make DESTDIR="$pkgdir" install
	done
        for _i in akonadi_sendlater_agent akonadi_followupreminder_agent contactthemeeditor headerthemeeditor pimsettingexporter; do 
          cd "$srcdir"/build/doc/$_i
          make DESTDIR="$pkgdir" install
        done

	mkdir -p "$pkgdir"/etc/xdg
	install -m644 "$srcdir"/$pkgbase-$pkgver/kdepim.categories "$pkgdir"/etc/xdg
}
