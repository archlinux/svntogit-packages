# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kdepim
pkgname=(akonadiconsole akregator blogilo konsolekalendar kaddressbook kalarm kleopatra kmail knotes
         kontact korganizer ktnef libkdepim)
pkgver=15.11.80
pkgrel=1
arch=('i686' 'x86_64')
url='http://pim.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdepim')
makedepends=(extra-cmake-modules kdepim-runtime qt5-tools kdoctools knewstuff khtml
		ktexteditor kdnssd kldap libktnef kontactinterface kxmlrpcclient kblog akonadi-search)
source=("http://download.kde.org/unstable/applications/$pkgver/src/$pkgbase-$pkgver.tar.xz")
sha1sums=('2a84737776334aacb8443887c2f97d5cf58ed961')

prepare() {
  mkdir -p build
}

build() {
	cd build
	cmake ../$pkgbase-$pkgver \
		-DCMAKE_BUILD_TYPE=Release \
		-DBUILD_TESTING=OFF \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_INSTALL_LIBDIR=lib \
		-DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
		-DLIBEXEC_INSTALL_DIR=lib
	make
}

package_akonadiconsole() {
	pkgdesc='Akonadi Management and Debugging Console'
	depends=('libkdepim')
	conflicts=("kdepim-$pkgname")
	url='http://pim.kde.org'
	install='kdepim.install'
	cd build/akonadiconsole
	make DESTDIR="$pkgdir" install
}

package_akregator() {
	pkgdesc='A Feed Reader for KDE'
	depends=('libkdepim' 'khtml' 'kontactinterface')
        conflicts=("kdepim-$pkgname")
	url="http://kde.org/applications/internet/akregator/"
	install='akregator.install'
	cd build/akregator
	make DESTDIR="$pkgdir" install
	cd "$srcdir"/build/doc/akregator
	make DESTDIR="$pkgdir" install
}

package_blogilo() {
	pkgdesc='A KDE Blogging Client'
	depends=('libkdepim' 'kblog' 'ktexteditor')
        conflicts=("kdepim-$pkgname")
	url="http://kde.org/applications/internet/blogilo/"
	install='kdepim.install'
	cd build/blogilo
	make DESTDIR="$pkgdir" install
	cd "$srcdir"/build/composereditor-ng
	make DESTDIR="$pkgdir" install
	cd "$srcdir"/build/doc/blogilo
	make DESTDIR="$pkgdir" install
}

package_konsolekalendar() {
	pkgdesc='Command line tool for accessing calendar files'
	depends=('libkdepim')
	conflicts=('kdepim-console')
	replaces=('kdepim-console>15')
	url='http://pim.kde.org'
	install='kdepim.install'
	cd build/console
	make DESTDIR="$pkgdir" install
        cd "$srcdir"/build/doc/konsolekalendar
        make DESTDIR="$pkgdir" install
}

package_kaddressbook() {
	pkgdesc='Contact Manager'
	depends=('libkdepim' 'kontactinterface')
        conflicts=("kdepim-$pkgname")
	url="http://kde.org/applications/office/kaddressbook/"
	install='kaddressbook.install'
	for _i in kaddressbook plugins/kaddressbook; do
	    cd "$srcdir"/build/$_i
	    make DESTDIR="$pkgdir" install
	done
}

package_kalarm() {
	pkgdesc='Personal Alarm Scheduler'
	depends=('libkdepim')
        conflicts=("kdepim-$pkgname")
	url="http://kde.org/applications/utilities/kalarm/"
	install='kdepim.install'
	cd build/kalarm
	make DESTDIR="$pkgdir" install
	cd "$srcdir"/build/doc/kalarm
	make DESTDIR="$pkgdir" install
}

package_kleopatra() {
	pkgdesc='Certificate Manager and Unified Crypto GUI'
	depends=('libkdepim')
        conflicts=("kdepim-$pkgname")
	install=kleopatra.install
	url="http://kde.org/applications/utilities/kleopatra/"
	cd build/kleopatra
	make DESTDIR="$pkgdir" install
	cd "$srcdir"/build/doc/kleopatra
	make DESTDIR="$pkgdir" install
	cd "$srcdir"/build/doc/kwatchgnupg
	make DESTDIR="$pkgdir" install
}

package_kmail() {
	pkgdesc='Mail Client'
	depends=('libkdepim' 'kontactinterface' 'kio-pim')
        conflicts=("kdepim-$pkgname")
	url="http://kde.org/applications/internet/kmail/"
	install='kmail.install'
	for _i in kmail agents/archivemailagent agents/mailfilteragent \
	  storageservicemanager importwizard libksieve messagelist grantleeeditor mboximporter sieveeditor; do
	  cd "$srcdir"/build/$_i
	  make DESTDIR="$pkgdir" install
	done
        for _i in akonadi_archivemail_agent kmail importwizard sieveeditor; do
          cd "$srcdir"/build/doc/$_i
          make DESTDIR="$pkgdir" install
        done
}

package_knotes() {
	pkgdesc='Popup Notes'
	depends=('libkdepim' 'kontactinterface')
        conflicts=("kdepim-$pkgname")
	url="http://kde.org/applications/utilities/knotes/"
	install='kdepim.install'
	cd "$srcdir"/build/knotes
	make DESTDIR="$pkgdir" install
	cd "$srcdir"/build/agents/notesagent
	make DESTDIR="$pkgdir" install
	cd "$srcdir"/build/doc/knotes
	make DESTDIR="$pkgdir" install
        cd "$srcdir"/build/doc/akonadi_notes_agent
        make DESTDIR="$pkgdir" install
}

package_kontact() {
	pkgdesc='Personal Information Manager'
	depends=('libkdepim' 'kontactinterface')
        conflicts=("kdepim-$pkgname")
	url="http://kde.org/applications/office/kontact/"
	install='kdepim.install'
	cd build/kontact
	make DESTDIR="$pkgdir" install
	cd "$srcdir"/build/doc/kontact
	make DESTDIR="$pkgdir" install
        cd "$srcdir"/build/doc/kontact-admin
        make DESTDIR="$pkgdir" install
}

package_korganizer() {
	pkgdesc='Calendar and Scheduling Program'
	depends=('libkdepim' 'kontactinterface')
        conflicts=("kdepim-$pkgname")
	url="http://kde.org/applications/office/korganizer"
	install='korganizer.install'
	for _i in korganizer korgac; do
	  cd "$srcdir"/build/$_i
	  make DESTDIR="$pkgdir" install
	done
	cd "$srcdir"/build/doc/korganizer
	make DESTDIR="$pkgdir" install
}

package_ktnef() {
	pkgdesc='A viewer/extractor for TNEF files'
	depends=('libktnef' 'hicolor-icon-theme')
        conflicts=("kdepim-$pkgname")
	url="https://projects.kde.org/projects/kde/kdepim/ktnef"
	install=ktnef.install
	cd build/ktnef
	make DESTDIR="$pkgdir" install
	cd "$srcdir"/build/doc/ktnef
	make DESTDIR="$pkgdir" install
}

package_libkdepim() {
	pkgdesc='Common libraries and resources for KDE PIM applications'
        groups=()
	depends=('kdepim-runtime' 'grantlee-qt5' 'akonadi-search' 'knewstuff' 'kldap' 'kdnssd' 'libktnef')
        conflicts=('kdepim-libkdepim' 'kdepim-kjots' 'kdepim-knode' 'kdepim-kresources' 'kdepim-ktimetracker')
	url='http://pim.kde.org'
	install=kdepim.install
	for _i in agents/sendlateragent calendarsupport grantleetheme incidenceeditor-ng kdgantt2 libkdepim \
	  accountwizard icons libkdepimdbusinterfaces libkleo libkpgp mailcommon mailimporter \
	  messagecomposer messagecore messageviewer noteshared templateparser calendarviews agents/followupreminderagent \
	  plugins/messageviewer pimcommon pimsettingexporter kaddressbookgrantlee; do
		cd "$srcdir"/build/$_i
		make DESTDIR="$pkgdir" install
	done
        for _i in akonadi_sendlater_agent akonadi_followupreminder_agent contactthemeeditor headerthemeeditor pimsettingexporter; do 
          cd "$srcdir"/build/doc/$_i
          make DESTDIR="$pkgdir" install
        done

	mkdir -p "$pkgdir"/etc/xdg
	install -m644 "$srcdir"/$pkgbase-$pkgver/kdepim.categories "$pkgdir"/etc/xdg
}
