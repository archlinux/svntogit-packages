# $Id$
# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

pkgname=go-tools
epoch=2
pkgver=1.10+2306+25101aad
pkgrel=1
pkgdesc='Developer tools for the Go programming language'
arch=(x86_64)
url='http://golang.org/'
license=(BSD)
depends=(glibc)
makedepends=(git go-pie)
_commit=25101aadb97aa42907eee6a238d6d26a6cb3c756
_net_commit=136a25c244d3019482a795d728110278d6ba09a4
source=(git+https://go.googlesource.com/tools#commit=$_commit
        git+https://github.com/golang/net#commit=$_net_commit)
md5sums=('SKIP'
         'SKIP')

_tools=(benchcmp callgraph cover digraph eg fiximports godex godoc goimports
        gomvpkg gorename gotype goyacc guru heapview html2article present
        ssadump stringer)

pkgver() {
  local _gover=1.10
  cd tools
  printf "%s+%s+%s" $_gover "$(git rev-list --count HEAD)" \
    "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p gopath/src/golang.org/x build
  cp -r tools net gopath/src/golang.org/x/
}

build() {
  export GOPATH="$srcdir/gopath"

  cd build
  for tool in ${_tools[@]}; do
    go build -v -a golang.org/x/tools/cmd/$tool
  done
}

check() {
  export GOPATH="$srcdir/gopath"

  for tool in ${_tools[@]}; do
    go test golang.org/x/tools/cmd/$tool
  done
}

package() {
  for tool in ${_tools[@]}; do
    install -Dm755 build/$tool "$pkgdir/usr/bin/$tool"
  done

  install -Dm644 tools/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
