# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=gnome-tweak-tool
pkgver=3.10.0
pkgrel=1
pkgdesc="A tool to customize advanced GNOME 3 options."
arch=(any)
url="https://live.gnome.org/GnomeTweakTool"
license=('GPL')
depends=('gnome-settings-daemon' 'python2-gobject' 'gconf' 'hicolor-icon-theme')
makedepends=('intltool')
install=gnome-tweak-tool.install
source=(http://download.gnome.org/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('6764b1cc609d347acc73a5aad431ff7e0edca5f3fbe409bab8ae12fff929c921')

build() {
  cd "$pkgname-$pkgver"

  PYTHON=/usr/bin/python2 ./configure --prefix=/usr
  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  sed -i "s|#!/usr/bin/env python|#!/usr/bin/env python2|" "$pkgdir"/usr/bin/gnome-tweak-tool
}

# vim:set ts=2 sw=2 et:
