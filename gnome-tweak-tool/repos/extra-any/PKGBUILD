# $Id$
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=gnome-tweak-tool
pkgver=3.26.4
pkgrel=1
pkgdesc="Customize advanced GNOME 3 options"
url="https://wiki.gnome.org/Apps/GnomeTweakTool"
arch=(any)
license=(GPL)
depends=(gnome-settings-daemon python-gobject nautilus)
makedepends=(git meson)
groups=(gnome-extra)
_commit=fb52aee275c3085cd59bdb779e6522f3ee8eb45e  # tags/3.26.4^0
source=("git+https://git.gnome.org/browse/gnome-tweak-tool#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  cd build
  meson test
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}

# vim:set ts=2 sw=2 et:
