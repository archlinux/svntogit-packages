# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-clocks
pkgver=3.12.0
pkgrel=1
pkgdesc="Clocks applications for GNOME"
arch=(i686 x86_64)
url="https://www.gnome.org/"
license=(GPL)
depends=(gtk3 libcanberra libgweather gnome-desktop
         hicolor-icon-theme gnome-themes-standard geoclue2 geocode-glib)
makedepends=(intltool vala gobject-introspection itstool)
groups=(gnome-extra)
install=gnome-clocks.install
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('3fc0ce2b7b2540d6c2d791ff63ab1670f189a339c804fcf24c9010a478314604')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
