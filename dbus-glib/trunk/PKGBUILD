# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=dbus-glib
pkgver=0.110
pkgrel=1
pkgdesc="GLib bindings for DBUS"
url="https://www.freedesktop.org/wiki/Software/DBusBindings"
arch=(x86_64)
license=('GPL')
depends=('dbus' 'glib2')
makedepends=('python' 'gtk-doc')
source=(https://dbus.freedesktop.org/releases/${pkgname}/${pkgname}-${pkgver}.tar.gz{,.asc})
sha256sums=('7ce4760cf66c69148f6bd6c92feaabb8812dee30846b24cd0f7395c436d7e825'
            'SKIP')
validpgpkeys=('DA98F25C0871C49A59EAFF2C4DE8FF2A63C7CC90')

build() {
  cd $pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib \
    --enable-gtk-doc \
    --disable-static
  make
}

package() {
  cd $pkgname-$pkgver
  _c=/usr/share/bash-completion/completions
  make DESTDIR="$pkgdir" install completiondir=$_c
  mv "$pkgdir$_c"/{dbus-bash-completion.sh,dbus-send}
}
