# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kdepimlibs
pkgname=(akonadi-contact akonadi-mime akonadi-notes akonadi-socialutils kio-pim)
pkgver=16.04.2
pkgrel=1
pkgdesc="KDE PIM Libraries"
arch=(i686 x86_64)
url='https://community.kde.org/KDE_PIM'
license=(GPL LGPL)
makedepends=(extra-cmake-modules qt5-tools kcalcore kmbox kcontacts kldap akonadi prison-frameworks kdoctools boost python grantlee-qt5
             kdesignerplugin)
source=("http://download.kde.org/stable/applications/$pkgver/src/$pkgbase-$pkgver.tar.xz")
sha1sums=('9fcf330aba0f98dafed58d632b07e418169ca198')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgbase-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package_akonadi-contact() {
  depends=(akonadi kmime kcontacts prison-frameworks qt5-webkit grantlee-qt5)

  cd build/akonadi-contact
  make DESTDIR="$pkgdir" install
}

package_akonadi-mime() {
  depends=(akonadi kmime)

  cd build/akonadi-mime
  make DESTDIR="$pkgdir" install
}

package_akonadi-notes() {
  depends=(kmime)

  cd build/akonadi-notes
  make DESTDIR="$pkgdir" install
}

package_akonadi-socialutils() {
  depends=(akonadi)

  cd build/akonadi-socialutils
  make DESTDIR="$pkgdir" install
}

package_kio-pim() {
  pkgdesc='PIM kioslaves'
  depends=(kldap kio)

  cd build/kioslave
  make DESTDIR="$pkgdir" install
}
