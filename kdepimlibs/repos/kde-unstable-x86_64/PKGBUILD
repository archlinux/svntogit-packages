# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kdepimlibs
pkgname=(akonadi-client akonadi-contact akonadi-mime akonadi-notes akonadi-socialutils kio-pim)
pkgver=15.11.80
pkgrel=1
pkgdesc="KDE PIM Libraries"
arch=(i686 x86_64)
url='https://projects.kde.org/kdepimlibs'
license=(GPL LGPL)
makedepends=(extra-cmake-modules qt5-tools kcalcore kmbox kcontacts kldap akonadi prison-frameworks kdoctools)
source=("http://download.kde.org/unstable/applications/$pkgver/src/$pkgbase-$pkgver.tar.xz")
sha1sums=('128c3defea25c21ceb2739dae67c250a45a6a1f1')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgbase-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package_akonadi-client() {
  depends=(akonadi)

  cd build/akonadi
  make DESTDIR="$pkgdir" install
}

package_akonadi-contact() {
  depends=(akonadi-client kmime kcontacts kcalcore prison-frameworks)

  cd build/akonadi-contact
  make DESTDIR="$pkgdir" install
}

package_akonadi-mime() {
  depends=(akonadi-client kmime)
  install=akonadi-mime.install

  cd build/akonadi-mime
  make DESTDIR="$pkgdir" install
}

package_akonadi-notes() {
  depends=(kmime)

  cd build/akonadi-notes
  make DESTDIR="$pkgdir" install
}

package_akonadi-socialutils() {
  depends=(akonadi-client)
  install=akonadi-socialutils.install

  cd build/akonadi-socialutils
  make DESTDIR="$pkgdir" install
}

package_kio-pim() {
  pkgdesc='PIM kioslaves'
  depends=(kldap kio)

  cd build/kioslave
  make DESTDIR="$pkgdir" install
}
