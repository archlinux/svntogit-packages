# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdepimlibs
pkgver=4.9.80
pkgrel=1
pkgdesc="KDE PIM Libraries"
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kdepimlibs'
license=('GPL' 'LGPL')
depends=('kdelibs' 'gpgme' 'akonadi' 'libical' 'prison' 'qjson')
makedepends=('cmake' 'automoc4' 'boost' 'cyrus-sasl')
install='kdepimlibs.install'
source=("http://download.kde.org/unstable/${pkgver}/src/${pkgname}-${pkgver}.tar.xz")
sha1sums=('f82593cc2290bfa636b34b28daed1e09333f62f2')

build() {
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install

  # Fix conflict with qjson
  rm "${pkgdir}"/usr/share/apps/cmake/modules/FindQJSON.cmake
}
