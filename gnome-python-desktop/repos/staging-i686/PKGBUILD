# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Arjan Timmerman <arjan@soulfly.nl>
# Contributor: Wael Nasreddine <gandalf@siemens-mobiles.org>

pkgbase=gnome-python-desktop
pkgname=('gnome-python-desktop' 'python2-gnomedesktop' 'python2-gnomekeyring' 'python2-gtop' 'python2-rsvg' 'python2-totem-plparser' 'python2-wnck')
pkgver=2.32.0
pkgrel=15
arch=(i686 x86_64)
license=('GPL' 'LGPL')
makedepends=('intltool' 'pkg-config' 'gnome-python' 'libgtop' 'totem-plparser' 'gnome-desktop2' 'librsvg' 'libwnck')
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgbase}/2.32/${pkgbase}-${pkgver}.tar.bz2
        01_wnck_enums.patch)
sha256sums=('09dbd580bf3b0ef60f91b090eafe6d08ddcc50a609e2b425a7f8eca46d4e0ee9'
            'bb51a303bc745f13f9386c734c0467267a19cb193c330eff0b7d39aad0edb1c1')

prepare() {
  cd $pkgbase-$pkgver
  patch -Np1 -i ../01_wnck_enums.patch
}

build() {
  cd $pkgbase-$pkgver
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --disable-metacity
  make
}

package_gnome-python-desktop() {
  depends=('python2-gnomedesktop' 'python2-gnomekeyring' 'python2-gtop' 'python2-rsvg' 'python2-totem-plparser' 'python2-wnck' 'gnome-python')
  pkgdesc="Python bindings for the GNOME desktop environment"

  cd $pkgbase-$pkgver
  make install-pkgconfigDATA DESTDIR="${pkgdir}"
}

package_python2-gnomedesktop() {
  depends=('pygtk' 'gnome-desktop2' 'python2-gnomevfs')
  pkgdesc="Python bindings for gnome-desktop 2.x"
  conflicts=('python-gnomedesktop<=2.32.0-10')
  replaces=('python-gnomedesktop<=2.32.0-10')
  
  cd $pkgbase-$pkgver
  make -C gnomedesktop install DESTDIR="${pkgdir}"
}

package_python2-gnomekeyring() {
  depends=('pygtk' 'libgnome-keyring')
  pkgdesc="Python bindings for libgnome-keyring"
  conflicts=('python-gnomekeyring<=2.32.0-10')
  replaces=('python-gnomekeyring<=2.32.0-10')

  cd $pkgbase-$pkgver
  make -C gnomekeyring install DESTDIR="${pkgdir}"
}

package_python2-gtop() {
  depends=('pygtk' 'libgtop')
  pkgdesc="Python bindings for libgtop"
  conflicts=('python-gtop<=2.32.0-10')
  replaces=('python-gtop<=2.32.0-10')

  cd $pkgbase-$pkgver
  make -C gtop install DESTDIR="${pkgdir}"
}

package_python2-rsvg() {
  depends=('pygtk' 'librsvg')
  pkgdesc="Python bindings for librsvg"
  conflicts=('python-rsvg<=2.32.0-10')
  replaces=('python-rsvg<=2.32.0-10')

  cd $pkgbase-$pkgver
  make -C rsvg install DESTDIR="${pkgdir}"
  install -m755 -d "${pkgdir}/usr/share/pygtk/2.0/defs"
  install -m644 rsvg/rsvg.defs "${pkgdir}/usr/share/pygtk/2.0/defs/"
}

package_python2-totem-plparser() {
  depends=('pygtk' 'totem-plparser' 'python2-gnomevfs')
  pkgdesc="Python bindings for totem-plparser"
  conflicts=('python-totem-plparser<=2.32.0-10')
  replaces=('python-totem-plparser<=2.32.0-10')

  cd $pkgbase-$pkgver
  make -C totem install DESTDIR="${pkgdir}"
  install -m755 -d "${pkgdir}/usr/share/pygtk/2.0/defs"
  install -m644 totem/plparser.defs "${pkgdir}/usr/share/pygtk/2.0/defs/"
}

package_python2-wnck() {
  depends=('pygtk' 'libwnck')
  pkgdesc="Python bindings for libwnck"
  conflicts=('python-wnck<=2.32.0-10')
  replaces=('python-wnck<=2.32.0-10')

  cd $pkgbase-$pkgver
  make -C wnck install DESTDIR="${pkgdir}"
}
