# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Arjan Timmerman <arjan@soulfly.nl>
# Contributor: Wael Nasreddine <gandalf@siemens-mobiles.org>

pkgbase=gnome-python-desktop
pkgname=('gnome-python-desktop' 'python-evolution' 'python-gnomedesktop' 'python-gnomekeyring' 'python-gtop' 'python-metacity' 'python-rsvg' 'python-totem-plparser' 'python-wnck' 'python-gnomeprint')
pkgver=2.32.0
pkgrel=7
arch=(i686 x86_64)
license=('GPL' 'LGPL')
options=('!libtool')
makedepends=('intltool' 'pkg-config' 'gnome-python' 'evolution-data-server' 'libgnomeprintui' 'libgtop' 'totem-plparser' 'gnome-desktop2' 'metacity' 'librsvg')
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgbase}/2.32/${pkgbase}-${pkgver}.tar.bz2)
sha256sums=('09dbd580bf3b0ef60f91b090eafe6d08ddcc50a609e2b425a7f8eca46d4e0ee9')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --enable-metacity
  make
}

package_gnome-python-desktop() {
  depends=('python-evolution' 'python-gnomedesktop' 'python-gnomekeyring' 'python-gtop' 'python-metacity' 'python-rsvg' 'python-totem-plparser' 'python-wnck' 'python-gnomeprint' 'gnome-python')
  pkgdesc="Python bindings for the GNOME desktop environment"

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make install-pkgconfigDATA DESTDIR="${pkgdir}"
}

package_python-evolution() {
  depends=('pygtk' 'evolution-data-server' 'python2-bonobo')
  pkgdesc="Python bindings for evolution"

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make -C evolution install DESTDIR="${pkgdir}"
  PYTHON=/usr/bin/python2 ./py-compile --destdir "${pkgdir}" --basedir /usr/lib/python2.7/site-packages/gtk-2.0 evolution/__init__.py
  install -m644 evolution/ecal.defs "${pkgdir}/usr/share/pygtk/2.0/defs/"
}

package_python-gnomedesktop() {
  depends=('pygtk' 'gnome-desktop2' 'python2-gnomevfs')
  pkgdesc="Python bindings for gnome-desktop 2.x"

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make -C gnomedesktop install DESTDIR="${pkgdir}"
}

package_python-gnomekeyring() {
  depends=('pygtk' 'libgnome-keyring')
  pkgdesc="Python bindings for libgnome-keyring"

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make -C gnomekeyring install DESTDIR="${pkgdir}"
}

package_python-gtop() {
  depends=('pygtk' 'libgtop')
  pkgdesc="Python bindings for libgtop"

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make -C gtop install DESTDIR="${pkgdir}"
}

package_python-metacity() {
  depends=('pygtk' 'metacity')
  pkgdesc="Python bindings for metacity"

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make -C metacity install DESTDIR="${pkgdir}"
}

package_python-rsvg() {
  depends=('pygtk' 'librsvg')
  pkgdesc="Python bindings for librsvg"

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make -C rsvg install DESTDIR="${pkgdir}"
  install -m755 -d "${pkgdir}/usr/share/pygtk/2.0/defs"
  install -m644 rsvg/rsvg.defs "${pkgdir}/usr/share/pygtk/2.0/defs/"
}

package_python-totem-plparser() {
  depends=('pygtk' 'totem-plparser' 'python2-gnomevfs')
  pkgdesc="Python bindings for totem-plparser"

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make -C totem install DESTDIR="${pkgdir}"
  install -m755 -d "${pkgdir}/usr/share/pygtk/2.0/defs"
  install -m644 totem/plparser.defs "${pkgdir}/usr/share/pygtk/2.0/defs/"
}

package_python-wnck() {
  depends=('pygtk' 'libwnck')
  pkgdesc="Python bindings for libwnck"

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make -C wnck install DESTDIR="${pkgdir}"
}

package_python-gnomeprint() {
  depends=('pygtk' 'libgnomeprintui')
  pkgdesc="Python bindings for libgnomeprintui"

  cd "${srcdir}/${pkgbase}-${pkgver}"
  for dir in gnomeprint docs/gnomeprint docs/gnomeprintui; do
    make -C ${dir} install DESTDIR="${pkgdir}"
  done
}
