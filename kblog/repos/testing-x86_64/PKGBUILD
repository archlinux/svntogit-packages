# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kblog
pkgver=19.08.0
pkgrel=1
pkgdesc="A blogging library for KDE"
arch=(x86_64)
url="https://kontact.kde.org"
license=(LGPL)
depends=(kxmlrpcclient syndication kcalcore)
makedepends=(extra-cmake-modules)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('15abd5dfd0499633703273823ceddc8e64b52b0217b04bea3d86cb9ac54da189'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
