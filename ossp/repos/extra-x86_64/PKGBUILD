# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jonathan Liu <net147@gmail.com>

pkgname=ossp
pkgver=1.3.2+11+g9e94d67
pkgrel=1
pkgdesc="Emulate OSS device using CUSE"
arch=(x86_64)
url="https://sourceforge.net/projects/osspd/"
license=(GPL2)
depends=('fuse2>=2.8.0')
makedepends=(libpulse alsa-lib git)
optdepends=("libpulse: PulseAudio backend"
            "alsa-lib: ALSA backend")
_commit=9e94d673070623c955646e802d42950906b6e184  # master
source=("git+https://github.com/heftig/osspd#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd osspd
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd osspd
  sed -i 's|/sbin|/bin|g' Makefile
  sed -i 's|GROUP="audio"|SUBSYSTEM=="cuse", MODE="0666"|' 98-osscuse.rules
}

build() {
  cd osspd
  make
}

package() {
  cd osspd
  make install \
    DESTDIR="$pkgdir" \
    prefix=/usr \
    UDEVDIR=/usr/lib/udev/rules.d
  install -Dt "$pkgdir/usr/share/doc/$pkgname" -m644 README

  install -Dm644 /dev/stdin "$pkgdir/usr/lib/systemd/system/osspd.service" <<END
[Unit]
Description=OSS Userspace Bridge

[Service]
ExecStart=/usr/bin/osspd -f

[Install]
WantedBy=multi-user.target
END

  install -Dm644 /dev/stdin "$pkgdir/usr/lib/modules-load.d/osspd.conf" <<END
cuse
snd-seq-oss
END

}
