# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
#Contributed: eliott <eliott@solarblue.net>, Andre Naumann <anaumann@SPARCed.org> 
pkgname=nxclient
pkgver=3.4.0.7
pkgrel=1
pkgdesc="Nomachine's nxclient for nx servers"
arch=(i686 x86_64)
url="http://nomachine.com"
makedepends=('rpmextract')
license=('custom:nomachine')
depends=('nx-common' 'libxext' 'libxft' 'xorg-xauth')
optdepends=('cups: for printing support')
[ "$CARCH" = "x86_64" ] && source=(http://64.34.161.181/download/3.4.0/Linux/$pkgname-3.4.0-7.x86_64.rpm nomachine.key)
[ "$CARCH" = "x86_64" ] && md5sums=('474f4d699a44262c1d15ae3cfd346b66' 'cb5df74aff5160eab9cd4313019ae228')
[ "$CARCH" = "i686" ] && source=(http://64.34.161.181/download/3.4.0/Linux/$pkgname-3.4.0-7.i386.rpm nomachine.key)
[ "$CARCH" = "i686" ] &&  md5sums=('d1b7cc510a949db36c07c874c378b339' 'cb5df74aff5160eab9cd4313019ae228')
install=nxclient.install

build() {
  cd ${srcdir}/
  rpmextract.sh *.rpm
  sed -i -e 's,/usr/NX,/opt/NX,g' etc/profile.d/nx.sh etc/profile.d/nx.csh
  sed -i -e 's,/usr/NX/,/opt/NX/,g' ${srcdir}/usr/NX/share/applnk/network/*.desktop
  install -D -m 755 ${srcdir}/etc/profile.d/nx.sh ${pkgdir}/etc/profile.d/nx.sh 
  install -D -m 755 ${srcdir}/etc/profile.d/nx.csh ${pkgdir}/etc/profile.d/nx.csh 
  install -D -m 644 ${srcdir}/usr/NX/share/applnk/network/nxclient-wizard.desktop ${pkgdir}/usr/share/applications/nxclient-wizard.desktop
  install -D -m 644 ${srcdir}/usr/NX/share/applnk/network/nxclient.desktop ${pkgdir}/usr/share/applications/nxclient.desktop
  install -D -m 644 ${srcdir}/usr/NX/share/applnk/network/nxclient-admin.desktop ${pkgdir}/usr/share/applications/nxclient-admin.desktop
  rm -r ${srcdir}/usr/NX/share/applnk
  cd usr
  mkdir -p ${pkgdir}/opt
  ### HACK: fix libpng linking until nomachine fixes upstream!
  mkdir -p ${pkgdir}/usr/NX/lib/
  cp -a NX/lib/libpng* ${pkgdir}/usr/NX/lib/
  rm -r NX/lib
  # provided by nx-common
  rm NX/bin/nxssh NX/bin/nxesd
  # provide default key file later
  rm NX/share/keys/server.id_dsa.key
  # move files to /opt
  cp -a NX ${pkgdir}/opt
  # add default key file
  install -D -m 644 ${srcdir}/nomachine.key ${pkgdir}/opt/NX/share/keys/server.id_dsa.key
  # add license file
  mkdir -p ${pkgdir}/usr/share/licenses/nxclient
  mv ${pkgdir}/opt/NX/share/documents/client/license-info ${pkgdir}/usr/share/licenses/nxclient/LICENSE
}
