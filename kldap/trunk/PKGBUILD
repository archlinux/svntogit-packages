# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kldap
pkgver=19.12.1
pkgrel=1
pkgdesc="LDAP access API for KDE"
arch=(x86_64)
url="https://kontact.kde.org"
license=(LGPL)
depends=(kcompletion)
optdepends=('kio: LDAP kioslave')
makedepends=(extra-cmake-modules kio kdoctools)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('5595f840c2b97e96f265ffd91fb007f4beb198aaf67a0dbfd941108acbcb9aa3'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
