# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kldap
pkgver=22.12.2
pkgrel=1
pkgdesc='LDAP access API for KDE'
arch=(x86_64)
url='https://kontact.kde.org'
license=(LGPL)
depends=(kcompletion qtkeychain-qt5)
optdepends=('kio: LDAP kioslave')
makedepends=(extra-cmake-modules kio kdoctools doxygen qt5-tools)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('ecea0908da9c70bd0a99fece87f8aaab332c81c0bc0967d73f52cc5b76043222'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
