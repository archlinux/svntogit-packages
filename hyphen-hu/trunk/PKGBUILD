# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: György Balló <ballogy@freestart.hu>

pkgname=hyphen-hu
pkgver=20100512
pkgrel=1
pkgdesc="Hungarian hyphenation rules"
arch=('any')
url="http://extensions.services.openoffice.org/project/hu_dicts"
license=('LGPL' 'GPL' 'MPL')
optdepends=('hyphen: offers hyphenation library functions')
source=(http://pkgs.fedoraproject.org/repo/pkgs/mythes-hu/dict-hu.oxt/3974adee5dccc005caef2ff9db007e11/dict-hu.oxt)
md5sums=('3974adee5dccc005caef2ff9db007e11')

package() {
  cd "$srcdir"
  install -Dm0644 hyph_hu_HU.dic $pkgdir/usr/share/hyphen/hyph_hu_HU.dic

  # the symlinks
  install -dm755 ${pkgdir}/usr/share/myspell/dicts
  pushd $pkgdir/usr/share/myspell/dicts
    for file in $pkgdir/usr/share/hyphen/*; do
      ln -sv /usr/share/hyphen/$(basename $file) .
    done
  popd

  install -Dm0644 README_hyph_hu_HU.txt $pkgdir/usr/share/doc/$pkgname/README_hyph_hu_HU.txt
}
