# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=akonadi-import-wizard
pkgver=18.11.90
pkgrel=1
pkgdesc="Import data from other mail clients to KMail"
arch=(x86_64)
url="https://kontact.kde.org"
license=(GPL2)
depends=(mailcommon)
makedepends=(extra-cmake-modules boost kdoctools)
optdepends=('kdepim-addons: additional import plugins')
conflicts=('kmail<16.11')
groups=(kde-applications kdepim)
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('f5926e0acda3039414a5ba730b6c8b526d8c7cef6884d6b1cff10a46c5e74af9'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
