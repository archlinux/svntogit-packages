# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=oxygen-icons
pkgname=('oxygen-icons'
         'oxygen-icons-svg')
pkgver=5.16.0
epoch=1
pkgrel=1
pkgdesc="The Oxygen Icon Theme"
arch=('any')
url='http://www.oxygen-icons.org/'
license=('LGPL')
makedepends=('cmake')
source=("http://download.kde.org/stable/frameworks/${pkgver}/src/${pkgname}5-${pkgver}.tar.xz")
sha1sums=('37610314d7e3fd3edde8a6e013ca2681855bdf50')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../${pkgbase}5-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package_oxygen-icons() {
  groups=('kf5')

  cd build
  make DESTDIR="$pkgdir" install
}

package_oxygen-icons-svg() {
  pkgdesc="The Oxygen Icon Theme (Scalable Vector Graphics)"

  cd ${pkgbase}5-${pkgver}
  find scalable -type f ! -name '*.sh' -exec \
    install -D -m644 "{}" "${pkgdir}/usr/share/icons/oxygen/{}" \;
}
