# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=python-patiencediff
pkgver=0.2.12
pkgrel=1
pkgdesc='Patiencediff Python and C implementations'
arch=(x86_64)
url=https://www.breezy-vcs.org/
license=(GPL2)
depends=(python)
makedepends=(
  git
  python-setuptools
)
_tag=5f419a0b2b7bb3000f66481538da8226d57ceb7a
source=(git+https://github.com/breezy-team/patiencediff.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd patiencediff
  git describe --tags | sed 's/^v//'
}

build() {
  cd patiencediff
  python setup.py build
}

package() {
  cd patiencediff
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

# vim: ts=2 sw=2 et:
