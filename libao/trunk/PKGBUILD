# $Id$
# Maintainer:
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=libao
pkgver=1.1.0
pkgrel=1
pkgdesc="A cross-platform audio output library and plugins"
url="http://www.xiph.org/ao"
arch=('i686' 'x86_64')
license=('GPL')
depends=('glibc' 'alsa-lib')
makedepends=('libpulse')
backup=('etc/libao.conf')
options=('!libtool')
conflicts=('libao-pulse')
provides=('libao-pulse=$pkgver-$pkgrel')
replaces=('libao-pulse')
source=("http://downloads.xiph.org/releases/ao/${pkgname}-${pkgver}.tar.gz"
        'libao.conf')
md5sums=('2b2508c29bc97e4dc218fa162cf883c8'
         '3ae8f3e3f1492210b3519af0f1f3c572')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr \
              --enable-alsa09-mmap \
              --enable-pulseaudio
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  # Add conf file
  install -Dm644 "$srcdir/libao.conf" "$pkgdir/etc/libao.conf"
}

# vim:set ts=2 sw=2 et:
