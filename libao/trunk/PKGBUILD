# $Id$
# Contributor: dorphell <dorphell@archlinux.org>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=libao
pkgver=1.2.2
pkgrel=1
pkgdesc='Cross-platform audio output library and plugins'
url='http://xiph.org/ao/'
arch=('i686' 'x86_64')
license=('GPL')
depends=('alsa-lib')
makedepends=('libpulse' 'git')
backup=('etc/libao.conf')
#source=("http://downloads.xiph.org/releases/ao/${pkgname}-${pkgver}.tar.gz"
source=("git+https://git.xiph.org/libao.git#commit=3f4b02f046550aca54d7f11a5d9c56ac421f0b66"
        'libao.conf')
sha256sums=('SKIP'
            '95f650a9d49ed43cff2f78b6171b0fc3dfa3d82e269c544bd87ed1ee8d31b492')

conflicts=('libao-pulse')
provides=("libao-pulse=${pkgver}-${pkgrel}")
replaces=('libao-pulse')

build() {
#	cd "${srcdir}/${pkgname}-${pkgver}"
	cd "${srcdir}/${pkgname}"
	./autogen.sh

	./configure \
		--prefix=/usr \
		--enable-alsa-mmap \

	make
}

package() {
#	cd "${srcdir}/${pkgname}-${pkgver}"
	cd "${srcdir}/${pkgname}"

	make DESTDIR="${pkgdir}" install
	install -Dm644 "${srcdir}"/libao.conf "${pkgdir}"/etc/libao.conf
}
