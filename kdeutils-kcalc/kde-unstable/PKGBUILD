# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeutils-kcalc
pkgver=4.8.97
pkgrel=1
pkgdesc='Scientific Calculator'
url='http://kde.org/applications/utilities/kcalc/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
groups=('kde' 'kdeutils')
install=${pkgname}.install
source=("http://download.kde.org/unstable/${pkgver}/src/kcalc-${pkgver}.tar.xz")
sha1sums=('d9d83233755e705aa385ec5a39abfafb80922001')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../kcalc-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $srcdir/build
  make DESTDIR=$pkgdir install
  cd $srcdir/build/doc
  make DESTDIR=$pkgdir install
}
