# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-xlsfonts
pkgver=1.0.6
pkgrel=1
pkgdesc='List available X fonts'
url='https://xorg.freedesktop.org/'
license=('custom')
arch=('x86_64')
depends=('libx11')
makedepends=('xorg-util-macros')
source=(https://xorg.freedesktop.org/archive/individual/app/xlsfonts-${pkgver}.tar.bz2{,.sig})
sha512sums=('052a3b1dec23cb20fdfacd4500cb836feb3b888bf568a548ea71deb58a4ba10a1ad5392d658e95272c6d4a1832edf1f19c3de2498b78dca82c43ee52800a30c0'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # Alan Coopersmith <alan.coopersmith@oracle.com>

build() {
	cd xlsfonts-${pkgver}
	./configure --prefix=/usr
	make
}

package() {
	cd xlsfonts-${pkgver}
	make DESTDIR="${pkgdir}" install
	install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
