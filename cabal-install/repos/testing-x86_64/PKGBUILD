# Maintainer: Thomas Dziedzic <gostrc@gmail.com>
# Maintainer: Vesa Kaihlavirta <vesa@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=cabal-install
pkgver=1.22.2.0
pkgrel=2
pkgdesc="The command-line interface for Cabal and Hackage."
url="http://hackage.haskell.org/package/cabal-install"
license=('custom:BSD3')
arch=('i686' 'x86_64')
makedepends=('ghc' 'cabal-install')
depends=('gmp' 'zlib')
source=("https://hackage.haskell.org/package/cabal-install-${pkgver}/cabal-install-${pkgver}.tar.gz")
md5sums=('bad431378bec8a0005fb1a3f5a92cd1b')

# the bootstrap script was broken on several releases
# build with a cabal sandbox to avoid unnecessary patching & headaches
build() {
  cd cabal-install-${pkgver}

  cabal update

  cabal sandbox init

  cabal install --only-dependencies

  cabal build
}

package() {
  cd cabal-install-${pkgver}

  install -d ${pkgdir}/usr/bin
  install -m755 dist/build/cabal/cabal \
    ${pkgdir}/usr/bin


  install -d ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 LICENSE \
    ${pkgdir}/usr/share/licenses/${pkgname}

  # add bash completion
  install -d ${pkgdir}/usr/share/bash-completion/completions
  install -m644 bash-completion/cabal \
    ${pkgdir}/usr/share/bash-completion/completions
}
