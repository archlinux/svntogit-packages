# Maintainer: Vesa Kaihlavirta <vesa@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>
pkgname=cabal-install
pkgver=0.10.2
pkgrel=1.1
pkgdesc="The command-line interface for Cabal and Hackage."
url="http://hackage.haskell.org/package/cabal-install"
license=('custom:BSD3')
arch=('i686' 'x86_64')
makedepends=('ghc=7.0.3' 'haskell-http>=4000.1.1' 'haskell-zlib')
depends=('gmp' zlib)
options=('strip')
source=(http://hackage.haskell.org/packages/archive/cabal-install/$pkgver/cabal-install-$pkgver.tar.gz)

build() {
    cd $srcdir/cabal-install-$pkgver
    runhaskell Setup configure --enable-shared --enable-split-objs --enable-executable-stripping --prefix=/usr
    runhaskell Setup build
}

package() {
    cd $srcdir/cabal-install-$pkgver
    runhaskell Setup copy --destdir=$pkgdir
    install -D -m644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}

md5sums=('bc906ef0bed79cbb33fdb36b73514281')
