# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-terminal
pkgver=3.38.3
pkgrel=1
pkgdesc="The GNOME Terminal Emulator"
url="https://wiki.gnome.org/Apps/Terminal"
arch=(x86_64)
license=(GPL)
depends=('vte3>=0.60.0' gsettings-desktop-schemas)
makedepends=(itstool docbook-xsl libnautilus-extension appstream-glib
             gnome-shell vala yelp-tools git)
groups=(gnome)
_commit=3be01f5fa7991bcd28a1a9d4b4390de3d4c16cb8  # tags/3.38.3^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-terminal.git#commit=$_commit"
        0001-screen-Manually-handle-env-inheritance.patch
        0002-screen-Move-env-sanitization-from-client.patch
        0003-screen-Ignore-some-more-environment-variables.patch)
sha256sums=('SKIP'
            'c350db9831c3bbe5c28ab5f959076cbeacc1b2bac12ca37623db2bf7d9461ecf'
            '4dc0260da875d22830440c3295aef2c73a1625eeeadf94747ac22920d0ddcdca'
            'abc5e7b30979a12dc6790d17efc47ec4a112fa6e3e3555ff16c0769b0178c599')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname

  # https://bugs.archlinux.org/task/68318
  # https://gitlab.gnome.org/GNOME/gnome-terminal/-/issues/303
  git apply -3 ../0001-screen-Manually-handle-env-inheritance.patch
  git apply -3 ../0002-screen-Move-env-sanitization-from-client.patch
  git apply -3 ../0003-screen-Ignore-some-more-environment-variables.patch

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib --disable-static --with-nautilus-extension
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
