# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: laloch <laloch@atlas.cz>

pkgname=qt-gstreamer
pkgver=0.10.3
pkgrel=1
pkgdesc="Qt4 bindings for GStreamer"
arch=('i686' 'x86_64')
url='http://gstreamer.freedesktop.org/modules/qt-gstreamer.html'
license=('LGPL2.1')
depends=('qt4' 'gstreamer0.10-base' 'libgles')
makedepends=('cmake' 'boost' 'mesa' 'doxygen')
source=("http://gstreamer.freedesktop.org/src/${pkgname}/${pkgname}-${pkgver}.tar.gz")
md5sums=('a5f73dfa50270a23a0b07af32304e162')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DQTGSTREAMER_EXAMPLES=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
