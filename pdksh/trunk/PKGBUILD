# $Id$
# Maintainer: Aaron Griffin <aaron@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=pdksh
pkgver=5.2.14
pkgrel=6
pkgdesc="A public domain clone of the AT&T Korn shell"
arch=('i686' 'x86_64')
url="http://www.cs.mun.ca/~michael/pdksh/"
license=('custom')
depends=('glibc')
install=pdksh.install
#ftp://ftp.cs.mun.ca/pub/$pkgname/$pkgname-$pkgver.tar.gz
source=(http://www.cs.mun.ca/~michael/pdksh/files/$pkgname-$pkgver.tar.gz pdksh-5.2.14.patch usr_ksh.sh)
md5sums=('871106b3bd937e1afba9f2ef7c43aef3' 'f11d1e711627d21a547c09e344e30dd3'\
         '56084a657ecafd5dd0813a6056036712')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -i ../$pkgname-$pkgver.patch
  ./configure --prefix=/ --mandir=/usr/share/man
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make prefix="$pkgdir" mandir="$pkgdir/usr/share/man/man1" install
  install -D -m 755 "${srcdir}/usr_ksh.sh" "${pkgdir}/usr/bin/ksh"
  install -D -m644 LEGAL "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
