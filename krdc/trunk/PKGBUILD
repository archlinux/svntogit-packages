# $Id: PKGBUILD 261475 2016-03-15 10:26:40Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=krdc
pkgver=16.04.1
pkgrel=1
pkgdesc='Remote Desktop Client'
url='http://kde.org/applications/internet/krdc/'
arch=(i686 x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kdenetwork)
depends=(kcmutils kdnssd knotifyconfig)
makedepends=(extra-cmake-modules libvncserver freerdp kdoctools python)
optdepends=('libvncserver: VNC support'
            'freerdp: RDP support'
            'kdebase-keditbookmarks: to edit bookmarks')
conflicts=(kdenetwork-krdc)
replaces=(kdenetwork-krdc)
source=("http://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
sha1sums=('2069c576804884b7e9a515ef44d8b00c70b7e5d7')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
