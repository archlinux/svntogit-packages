# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=krdc
pkgver=20.11.90
pkgrel=1
pkgdesc='Remote Desktop Client'
url='https://kde.org/applications/internet/krdc/'
arch=(x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kde-network)
depends=(kcmutils kdnssd knotifyconfig)
makedepends=(extra-cmake-modules libvncserver freerdp kdoctools)
optdepends=('libvncserver: VNC support'
            'libssh: VNC support'
            'freerdp: RDP support'
            'keditbookmarks: to edit bookmarks')
source=("https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('0b13be5a0b80943a47b6313536ddff3f848bf34729cd1149c66929963a02958e'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
