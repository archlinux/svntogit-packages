# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Tobias Kieslich <tobias@archlinux.org>

pkgname=fvwm-devel
pkgver=2.5.31
pkgrel=1
pkgdesc="A multiple large virtual desktop window manager originally derived from twm"
arch=('i686' 'x86_64')
url="http://www.fvwm.org"
license=('GPL' 'custom')
depends=('imlib' 'fribidi' 'perl' 'libstroke' 'libxpm' 'readline' 'librsvg')
makedepends=('pkg-config')
conflicts=('fvwm')
provides=("fvwm=${pkgver}")
changelog=ChangeLog
source=(ftp://ftp.fvwm.org/pub/fvwm/version-2/fvwm-${pkgver}.tar.bz2 fvwm-devel.desktop)
md5sums=('bc0a64cee2b76a91b1b3f1aa716f564d' '3f5bcb246e035ff29aa5bf54487a3db4')
sha1sums=('0a59ac1c17f37dbfb1e042f4df3f86a5eb2d719c' '17db773870637b410ceb4f68743dbd517bc5c599')

build() {
  cd "${srcdir}/fvwm-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib --with-stroke-library
  make
}

package() {
  cd "${srcdir}/fvwm-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -d "${pkgdir}/etc/fvwm"
  install -D -m644 sample.fvwmrc/* "${pkgdir}/etc/fvwm"
  rm -f "${pkgdir}"/etc/fvwm/Makefile*
  install -D -m644 ../fvwm-devel.desktop "${pkgdir}/etc/X11/sessions/fvwm-devel.desktop"
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
