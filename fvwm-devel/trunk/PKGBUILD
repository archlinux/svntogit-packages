# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Tobias Kieslich <tobias@archlinux.org>

pkgname=fvwm-devel
pkgver=2.5.27
pkgrel=2
pkgdesc="A multiple large virtual desktop window manager originally derived from twm"
arch=('i686' 'x86_64')
url="http://www.fvwm.org"
license=('GPL' 'custom')
depends=('imlib' 'fribidi' 'perl' 'libstroke' 'libxpm' 'readline>=6.0.00' 'librsvg')
makedepends=('pkgconfig')
conflicts=('fvwm')
provides=("fvwm=${pkgver}")
source=(ftp://ftp.fvwm.org/pub/fvwm/version-2/fvwm-${pkgver}.tar.bz2 fvwm-devel.desktop)
md5sums=('d8d8c1998290c3e82edb43a5b90845be' '3f5bcb246e035ff29aa5bf54487a3db4')
sha1sums=('52a3300c5f13468fa61205477a07f0e5d4767a6b' '17db773870637b410ceb4f68743dbd517bc5c599')

build() {
  cd "${srcdir}/fvwm-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib --with-stroke || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1

  install -d "${pkgdir}/etc/fvwm"
  install -D -m644 sample.fvwmrc/* "${pkgdir}/etc/fvwm" || return 1
  rm -f "${pkgdir}"/etc/fvwm/{Makefile,Makefile.am,Makefile.in}
  install -D -m644 ../fvwm-devel.desktop "${pkgdir}/etc/X11/sessions/fvwm-devel.desktop" || return 1
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING" || return 1
}
