# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=fvwm-devel
pkgver=2.7.0
pkgrel=2
pkgdesc="A multiple large virtual desktop window manager originally derived from twm"
arch=('i686' 'x86_64')
url="http://www.fvwm.org"
license=('GPL' 'custom')
depends=('imlib' 'fribidi' 'perl' 'libstroke' 'libxpm' 'librsvg' 'libxinerama' 'libxcursor')
makedepends=('libxslt')
conflicts=('fvwm')
provides=("fvwm=${pkgver}")
options=('!emptydirs')
source=(ftp://ftp.fvwm.org/pub/fvwm/version-2/fvwm-${pkgver}.tar.bz2 fvwm-devel.desktop)
md5sums=('c7dbb0c067f570e478005edb95dcccc3'
         '3f5bcb246e035ff29aa5bf54487a3db4')
sha1sums=('bc8f84902b0926df171d059eff75780a25c8a97a'
          '17db773870637b410ceb4f68743dbd517bc5c599')

build() {
  cd "${srcdir}/fvwm-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib --with-stroke-library
  make
}

package() {
  cd "${srcdir}/fvwm-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -d "${pkgdir}/etc/fvwm"
  install -D -m644 sample.fvwmrc/* "${pkgdir}/etc/fvwm"
  rm -f "${pkgdir}"/etc/fvwm/Makefile*
  install -D -m644 ../fvwm-devel.desktop "${pkgdir}/usr/share/xsessions/fvwm-devel.desktop"
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
