# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: mezcal <fila at pruda dot com>

pkgname=perl-time-hires
pkgver=1.9725
pkgrel=2
pkgdesc="Perl module: high resolution time, sleep, and alarm"
arch=('i686' 'x86_64')
license=('PerlArtistic')
url="http://search.cpan.org/dist/Time-HiRes/"
depends=('perl')
options=(!emptydirs)
source=("http://search.cpan.org/CPAN/authors/id/Z/ZE/ZEFRAM/Time-HiRes-${pkgver}.tar.gz")
md5sums=('cb1108c076d26f3d2fed0bb91313135a')

build() {
  cd "${srcdir}/Time-HiRes-${pkgver}"

  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor

  make
}

check() {
  cd "${srcdir}/Time-HiRes-${pkgver}"

  make test
}

package() {
  cd "${srcdir}/Time-HiRes-${pkgver}"

  make install DESTDIR="${pkgdir}"
}
