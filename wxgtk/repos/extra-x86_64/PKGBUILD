# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Original Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# New Version - Contributor: Bob Finch <w9ya@arrl.net>
# with help from J. Santiago Hirschfeld <jsantiagoh@yahoo.com.ar> aka Angelus

pkgname=wxgtk
pkgver=2.8.11
pkgrel=1
pkgdesc="GTK+ implementation of wxWidgets API for GUI"
arch=('i686' 'x86_64')
url="http://wxwidgets.org"
license=('custom:wxWindows')
depends=('gtk2' 'gstreamer0.10-base' 'libgl')
makedepends=('libgnomeprintui' 'gstreamer0.10-base-plugins' 'gconf' 'ccache' 'mesa')
options=('ccache') # required to build libwx_gtk2u_mmedia
#source=(http://downloads.sourceforge.net/wxpython/wxPython-src-${pkgver}.tar.bz2)
source=(http://downloads.sourceforge.net/wxwindows/wxGTK-${pkgver}.tar.bz2)
md5sums=('6040933d200037f90f6aa1c5169e7ec6')
sha1sums=('e979d9d2b1ee5bd10e90fd00b02f3306327c661a')

build() {
#  cd "${srcdir}/wxPython-src-${pkgver}"
  cd "${srcdir}/wxGTK-${pkgver}"
  ./configure --prefix=/usr --libdir=/usr/lib --with-gtk=2 --with-opengl --enable-unicode \
    --enable-graphics_ctx --with-gnomeprint --disable-optimize --enable-mediactrl \
    --with-libpng=sys --with-libxpm=sys --with-libjpeg=sys --with-libtiff=sys || return 1
  make || return 1
  make -C locale allmo || return 1
  cd contrib/src
  make || return 1
}

package() {
  cd "${srcdir}/wxGTK-${pkgver}"
  make DESTDIR="${pkgdir}" install || return 1
  cd contrib/src
  make DESTDIR="${pkgdir}" install || return 1
  install -D -m644 ../../docs/licence.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE" || return 1
}
