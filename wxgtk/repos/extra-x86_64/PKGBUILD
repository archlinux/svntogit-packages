# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=wxgtk
pkgver=2.8.12.1
pkgrel=4
pkgdesc="GTK+ implementation of wxWidgets API for GUI"
arch=('i686' 'x86_64')
url="http://wxwidgets.org"
license=('custom:wxWindows')
depends=('gtk2' 'gstreamer0.10-base' 'libgl' 'libxxf86vm' 'libsm' 'sdl')
makedepends=('gstreamer0.10-base-plugins' 'gconf' 'mesa' 'glu' 'libxt')
#source=(http://downloads.sourceforge.net/wxwindows/wxGTK-${pkgver}.tar.bz2)
source=(http://downloads.sourceforge.net/wxpython/wxPython-src-${pkgver}.tar.bz2)
sha1sums=('05688dc03d61631750f5904273122bb40a2115f5')

build() {
  cd "${srcdir}/wxPython-src-${pkgver}"
#  cd "${srcdir}/wxGTK-${pkgver}"
  ./configure --prefix=/usr --libdir=/usr/lib --with-gtk=2 --with-opengl --enable-unicode \
    --enable-graphics_ctx  --disable-optimize --enable-mediactrl --with-regex=builtin \
    --with-libpng=sys --with-libxpm=sys --with-libjpeg=sys --with-libtiff=sys \
    --with-sdl --disable-precomp-headers
  make
  make -C locale allmo
  cd contrib/src
  make
}

package() {
  cd "${srcdir}/wxPython-src-${pkgver}"
#  cd "${srcdir}/wxGTK-${pkgver}"
  make DESTDIR="${pkgdir}" install
  cd contrib/src
  make DESTDIR="${pkgdir}" install
  install -D -m644 ../../docs/licence.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
