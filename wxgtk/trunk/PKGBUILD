# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=wxgtk
pkgver=3.0.2
pkgrel=6
pkgdesc="GTK+ implementation of wxWidgets API for GUI"
arch=('i686' 'x86_64')
url="https://wxwidgets.org"
license=('custom:wxWindows')
depends=('gtk2' 'gst-plugins-base-libs' 'libsm')
makedepends=('gst-plugins-base' 'gconf' 'webkitgtk2' 'glu')
optdepends=('webkitgtk2: for webview support')
options=('!emptydirs')
source=(https://downloads.sourceforge.net/wxwindows/wxWidgets-${pkgver}.tar.bz2
        make-abicheck-non-fatal.patch wxgtk-gcc6.patch gst1.0.patch)
sha1sums=('6461eab4428c0a8b9e41781b8787510484dea800'
          'dfe38650c655395b90bf082b5734c4093508bfa3'
          '498202334c69d49c1ebbc9d36ad382cfa237f3f8'
          '07d47f2fb2efd400ab54302b3bb55da141d76d61')

prepare() {
  cd wxWidgets-${pkgver}

  # C++ ABI check is too strict and breaks with GCC 5.1
  # https://bugzilla.redhat.com/show_bug.cgi?id=1200611
  patch -Np1 -i ../make-abicheck-non-fatal.patch

  # Fix build with GCC 6
  patch -p1 -i ../wxgtk-gcc6.patch

  # Gstreamer 0.10 -> 1.0 patch
  patch -Np1 -i ../gst1.0.patch
}

build() {
  cd wxWidgets-${pkgver}
  ./autogen.sh
  ./configure --prefix=/usr --libdir=/usr/lib --with-gtk=2 --with-opengl --enable-unicode \
    --enable-graphics_ctx --enable-mediactrl --enable-webview --with-regex=builtin \
    --with-libpng=sys --with-libxpm=sys --with-libjpeg=sys --with-libtiff=sys \
    --disable-precomp-headers
  make
  make -C locale allmo
}

package() {
  cd wxWidgets-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -D -m644 docs/licence.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
