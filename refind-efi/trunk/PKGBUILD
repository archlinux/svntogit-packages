# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Keshav P R <(the.ridikulus.rat) (aatt) (gemmaeiil) (ddoott) (ccoomm)>

pkgname="refind-efi"
pkgver="0.4.7"
pkgrel="1"
pkgdesc="Rod Smith's fork of rEFIt UEFI Boot Manager"
url="http://www.rodsbooks.com/refind/index.html"
arch=('any')
license=('GPL3' 'custom')
makedepends=('gnu-efi-libs>=3.0q')
depends=('dosfstools' 'efibootmgr')
optdepends=('mactel-boot: For bless command in Apple Mac systems')
conflicts=('refind-efi-x86_64' 'refind-efi-i386')
provides=('refind-efi-x86_64' 'refind-efi-i386')
replaces=('refind-efi-x86_64' 'refind-efi-i386')
options=('!strip' 'docs')
install="${pkgname}.install"
source=("http://downloads.sourceforge.net/refind/refind-src-${pkgver}.zip"
        'refind_include_more_shell_paths.patch'
        'refind_linux.conf')

build() {	
	if [[ "${CARCH}" != "x86_64" ]]; then
		msg "refind-efi can be built only in an x86_64 system. Exiting."
		exit 1
	fi
	
	cd "${srcdir}/refind-${pkgver}/"
	patch -Np1 -i "${srcdir}/refind_include_more_shell_paths.patch"
	
	sed 's|/usr/local/include/efi|/usr/include/efi|g' -i "${srcdir}/refind-${pkgver}/Make.common"
	sed 's|/usr/local/lib|/usr/lib|g' -i "${srcdir}/refind-${pkgver}/Make.common"
	sed 's|ARCH            :=|ARCH            ?=|g' -i "${srcdir}/refind-${pkgver}/Make.common"
	sed 's|ARCH            =|ARCH            ?=|g' -i "${srcdir}/refind-${pkgver}/refind/Makefile"
	
	cp -r "${srcdir}/refind-${pkgver}" "${srcdir}/refind-${pkgver}-x86_64"
	cd "${srcdir}/refind-${pkgver}-x86_64/"
	ARCH="x86_64" make gnuefi
	
	cp -r "${srcdir}/refind-${pkgver}" "${srcdir}/refind-${pkgver}-i386"
	cd "${srcdir}/refind-${pkgver}-i386/"
	ARCH="ia32" make gnuefi
}

package() {	
	## install the rEFInd UEFI applications
	install -d "${pkgdir}/usr/lib/refind/"
	install -D -m0644 "${srcdir}/refind-${pkgver}-x86_64/refind/refind_x64.efi" "${pkgdir}/usr/lib/refind/refindx64.efi"
	install -D -m0644 "${srcdir}/refind-${pkgver}-i386/refind/refind_ia32.efi" "${pkgdir}/usr/lib/refind/refindia32.efi"
	
	## install the rEFInd config file
	install -d "${pkgdir}/usr/lib/refind/config/"
	install -D -m0644 "${srcdir}/refind-${pkgver}/refind.conf-sample" "${pkgdir}/usr/lib/refind/config/refind.conf"
	install -D -m0644 "${srcdir}/refind_linux.conf" "${pkgdir}/usr/lib/refind/config/refind_linux.conf"
	
	## install the rEFInd icons
	install -d "${pkgdir}/usr/share/refind/icons/"
	install -D -m0644 "${srcdir}/refind-${pkgver}/icons"/* "${pkgdir}/usr/share/refind/icons/"
	
	## install the rEFInd docs
	install -d "${pkgdir}/usr/share/refind/docs/html/"
	install -d "${pkgdir}/usr/share/refind/docs/Styles/"
	install -D -m0644 "${srcdir}/refind-${pkgver}/docs/refind"/* "${pkgdir}/usr/share/refind/docs/html/"
	install -D -m0644 "${srcdir}/refind-${pkgver}/docs/Styles"/* "${pkgdir}/usr/share/refind/docs/Styles/"
	install -D -m0644 "${srcdir}/refind-${pkgver}/README.txt" "${pkgdir}/usr/share/refind/docs/README"
	install -D -m0644 "${srcdir}/refind-${pkgver}/NEWS.txt" "${pkgdir}/usr/share/refind/docs/NEWS"
	rm -f "${pkgdir}/usr/share/refind/docs/html/.DS_Store"
	
	## install the rEFIt license file, since rEFInd is a fork of rEFIt
	install -d "${pkgdir}/usr/share/licenses/refind/"
	install -D -m0644 "${srcdir}/refind-${pkgver}/LICENSE.txt" "${pkgdir}/usr/share/licenses/refind/LICENSE"	
}
md5sums=('d79f875ab2a2c20696581ee3cc6f6739'
         '8da259d08ea1dc0a3a4c572913e453f9'
         'a83e45b10d5efbca1c93a63851728712')
