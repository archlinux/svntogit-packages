# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-dbi
pkgver=1.633
pkgrel=1
pkgdesc="Database independent interface for Perl"
arch=('i686' 'x86_64')
url="http://search.cpan.org/dist/DBI/"
license=('GPL' 'PerlArtistic')
depends=('perl')
checkdepends=('perl-test-pod' 'perl-test-pod-coverage')
options=('!emptydirs')
source=(http://www.cpan.org/authors/id/T/TI/TIMB/DBI-${pkgver}.tar.gz)
md5sums=('b4fe13b9a51c1446c5f3cf93e69a2044')

build() {
  cd DBI-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd DBI-${pkgver}
  make test
}

package() {
  cd DBI-${pkgver}
  make DESTDIR="${pkgdir}" install
}
