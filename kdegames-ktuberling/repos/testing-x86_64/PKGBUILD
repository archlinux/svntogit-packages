# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-ktuberling
pkgver=15.08.1
pkgrel=1
pkgdesc="A simple constructor game suitable for children and adults alike"
url="http://kde.org/applications/games/ktuberling/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegames')
depends=('kdebase-runtime' 'libkdegames4')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/applications/${pkgver}/src/ktuberling-${pkgver}.tar.xz")
sha1sums=('edf5607123670426e3e537ea882a184b1f4427aa')

build() {
  mkdir -p build
  cd build
  cmake ../ktuberling-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
