# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=libkdcraw
pkgver=16.04.1
pkgrel=1
pkgdesc="A C++ interface used to decode RAW picture"
url="https://www.kde.org/applications/graphics/"
arch=(i686 x86_64)
license=(GPL LGPL FDL)
depends=(qt5-base libraw)
makedepends=(extra-cmake-modules)
source=("http://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
sha1sums=('ec1136fe6f1ba061b6d20a1074a79c98d678871e')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
