# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=logrotate
pkgver=3.8.1
pkgrel=2
pkgdesc="Rotates system logs automatically"
arch=('i686' 'x86_64')
url="https://fedorahosted.org/logrotate/"
license=('GPL')
groups=('base')
depends=('popt' 'cron' 'gzip')
backup=('etc/logrotate.conf')
source=("https://fedorahosted.org/releases/l/o/logrotate/logrotate-${pkgver}.tar.gz"
        'logrotate-3.8.1-noasprintf.patch'
        'logrotate.conf'
        'logrotate.cron.daily')
md5sums=('bd2e20d8dc644291b08f9215397d28a5'
         'fc8e4e15211d8aefd431d0e2e96223c4'
         '462a5f364717461537eb2ae6394ad23e'
         'aa8ac8283908b6114483a293adcb650f')

build() {
  cd "$srcdir/${pkgname}-${pkgver}"

  patch -p1 -i "$srcdir"/logrotate-3.8.1-noasprintf.patch

  sed -e 's|#define DEFAULT_MAIL_COMMAND .*|#define DEFAULT_MAIL_COMMAND "/usr/bin/mail"|'\
      -e 's|#define COMPRESS_COMMAND .*|#define COMPRESS_COMMAND "/usr/bin/gzip"|'\
      -e 's|#define UNCOMPRESS_COMMAND .*|#define UNCOMPRESS_COMMAND "/usr/bin/gunzip"|'\
      -i config.h

  make RPM_OPT_FLAGS="$CFLAGS" EXTRA_LDFLAGS="$LDFLAGS"
}

check() {
  cd "$srcdir/${pkgname}-${pkgver}"
  make test
}

package() {
  cd "$srcdir/${pkgname}-${pkgver}"
  make PREFIX="$pkgdir" MANDIR="usr/share/man" install

  install -Dm644 "$srcdir/logrotate.conf" "$pkgdir/etc/logrotate.conf"
  install -Dm744 "$srcdir/logrotate.cron.daily" "$pkgdir/etc/cron.daily/logrotate"
}
