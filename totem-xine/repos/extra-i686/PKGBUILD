# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=totem-xine
pkgver=2.22.2
pkgrel=3
url="http://www.hadess.net/totem.php3"
pkgdesc="A GNOME2 integrated movie player based on xine."
arch=(i686 x86_64)
license=('GPL')
depends=('xine-lib>=1.1.12' 'totem-plparser>=2.22.3' 'libxxf86vm' 'libxtst' 'desktop-file-utils' 'libepc>=0.3.5' 'libgnomeui>=2.22.1' 'startup-notification' 'iso-codes>=2.0' 'python-gdata')
makedepends=('gnome-control-center>=2.22.0' 'nautilus>=2.22.0' 'xulrunner>=1.9' 'pkgconfig' 'bluez-libs' 'lirc-utils' 'gnome-doc-utils>=0.12.2' 'pygtk' 'gnome-common' 'intltool')
options=('!libtool' '!emptydirs')
conflicts=('totem')
provides=("totem=${pkgver}")
install=totem.install
source=(http://ftp.gnome.org/pub/gnome/sources/totem/2.22/totem-${pkgver}.tar.bz2
	30_totem_xulrunner19.patch
	video-flv.patch)
md5sums=('6062080e81b4859f87fee8a592b92489'
         'ce0b0bdd822ad404bdc24e2fce3e7e21'
         '8c66edc0e4fd64612189e76f57c40873')

build() {
  cd ${srcdir}/totem-${pkgver}
  patch -Np1 -i ${srcdir}/30_totem_xulrunner19.patch || return 1
  patch -Np1 -i ${srcdir}/video-flv.patch || return 1
  libtoolize --force --copy || return 1
  aclocal || return 1
  autoconf || return 1
  automake --add-missing || return 1
  ./configure --enable-python --enable-xine || return 1
  ./configure --prefix=/usr --sysconfdir=/etc \
              --libexecdir=/usr/lib/totem \
              --localstatedir=/var --disable-static \
	      --enable-nautilus --enable-xine \
	      --enable-mozilla --with-dbus \
	      --enable-python --with-gecko=mozilla \
	      --disable-scrollkeeper || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 \
       DESTDIR=${pkgdir} install || return 1

  rm -rf ${pkgdir}/usr/lib/mozilla

  install -m755 -d ${pkgdir}/usr/share/gconf/schemas
  gconf-merge-schema ${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
