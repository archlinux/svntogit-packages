#$Id$
# Maintainer: Jan De Groot <jgc@archlinux.org>

pkgname=gnome-keyring
pkgver=3.6.1
pkgrel=1
pkgdesc="GNOME Password Management daemon"
arch=(i686 x86_64)
license=('GPL' 'LGPL')
depends=('gtk3' 'gcr' 'libcap-ng')
makedepends=('intltool')
groups=('gnome')
options=('!libtool' '!emptydirs')
url="http://www.gnome.org"
install=gnome-keyring.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('0c3a2ccc1744ce681fa7eb8d5e3e94d1712e932068a36ce7076222eb82b25aa5')

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --libexecdir=/usr/lib/gnome-keyring \
      --with-pam-dir=/usr/lib/security --with-root-certs=/etc/ssl/certs \
      --disable-schemas-compile
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
