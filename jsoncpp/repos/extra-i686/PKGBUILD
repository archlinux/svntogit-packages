# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Jonahtan Schaeffer <joschaeffer@gmail.com>
# COntributor: Drake Arconis <lightdrake@gmail.com>

pkgname=jsoncpp
pkgver=0.5.0
pkgrel=4
pkgdesc="A JSON C++ library"
url='http://jsoncpp.sourceforge.net/'
license=('custom:Public_Domain')
arch=('i686' 'x86_64')
depends=('gcc-libs')
makedepends=('scons')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-src-${pkgver}.tar.gz")
md5sums=('24482b67c1cb17aac1ed1814288a3a8f')

build() {
  cd ${pkgname}-src-${pkgver}
  scons platform=linux-gcc
}

package() {
  cd ${pkgname}-src-${pkgver}

  install -d "${pkgdir}"/usr/include/jsoncpp
  cp -r include/json "${pkgdir}"/usr/include/jsoncpp/json

  install -Dm644 buildscons/linux-gcc-*/src/lib_json/libjson_linux-gcc*libmt.so \
    "${pkgdir}"/usr/lib/libjsoncpp.so

  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

