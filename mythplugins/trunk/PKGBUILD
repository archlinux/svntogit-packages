# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>

pkgbase=mythplugins
pkgname=('mythplugins-mytharchive'
         'mythplugins-mythbrowser'
         'mythplugins-mythgallery'
         'mythplugins-mythgame'
         'mythplugins-mythmovies'
         'mythplugins-mythmusic'
         'mythplugins-mythnetvision'
         'mythplugins-mythnews'
         'mythplugins-mythvideo'
         'mythplugins-mythweather'
         'mythplugins-mythweb'
         'mythplugins-mythzoneminder')
pkgver=25574
pkgrel=2
arch=('i686' 'x86_64')
url="http://www.mythtv.org"
license=('GPL')
makedepends=("mythtv>=${pkgver}" 'mplayer' 'fftw' 'dvdauthor' 'libexif'
             'python-imaging' 'libcdaudio' 'taglib' 'perl-xml-simple'
             'libvisual' 'perl-xml-xpath' 'perl-image-size' 'perl-class-inspector' 'perl-datetime-format-iso8601')
source=(ftp://ftp.archlinux.org/other/mythtv/${pkgbase}-${pkgver}.tar.xz
        mtd qt-4.7-buildfixes.diff)
md5sums=('933f0c8ac21652af1d83d502c0ca6516'
         '476c12ba074794ad7f4ae092bdf949d6'
	 '65fad68e51c52bf5176a2ade35e71bc3')

build() {
  cd "${srcdir}/${pkgbase}"
  patch -Np4 -i "${srcdir}/qt-4.7-buildfixes.diff"
  find . -name '*.py' -type f | xargs sed -i 's@^#!.*python$@#!/usr/bin/python2@' 
  ./configure --prefix=/usr \
              --enable-all

  qmake mythplugins.pro
  make
}

package_mythplugins-mytharchive() {
  pkgdesc="MythTV plugin that lets you create DVDs from or archive your recorded shows"
  depends=("mythtv>=${pkgver}" 'libxmu' 'pil' 'dvdauthor')
  replaces=('mytharchive')
  cd "${srcdir}/${pkgbase}/mytharchive"
  make INSTALL_ROOT="${pkgdir}" install
}

package_mythplugins-mythbrowser() {
  pkgdesc="Mini web browser for MythTV"
  depends=("mythtv>=${pkgver}")
  replaces=('mythbrowser')
  cd "${srcdir}/${pkgbase}/mythbrowser"
  make INSTALL_ROOT="${pkgdir}" install
}

package_mythplugins-mythgallery() {
  pkgdesc="Image gallery plugin for MythTV"
  depends=("mythtv>=${pkgver}" 'libexif')
  replaces=('mythgallery')
  cd "${srcdir}/${pkgbase}/mythgallery"
  make INSTALL_ROOT="${pkgdir}" install
}

package_mythplugins-mythgame() {
  pkgdesc="Game emulator module for MythTV"
  depends=("mythtv>=${pkgver}" 'zlib')
  replaces=('mythgame')
  cd "${srcdir}/${pkgbase}/mythgame"
  make INSTALL_ROOT="${pkgdir}" install
}

package_mythplugins-mythmovies() {
  pkgdesc="Local movie listings via MythTV"
  depends=("mythtv>=${pkgver}")
  replaces=('mythmovies')
  cd "${srcdir}/${pkgbase}/mythmovies"
  make INSTALL_ROOT="${pkgdir}" install
}

package_mythplugins-mythmusic() {
  pkgdesc="Music playing plugin for MythTV"
  depends=("mythtv>=${pkgver}" 'flac' 'libcdaudio' 'cdparanoia' 'taglib' 'libvisual')
  replaces=('mythmusic')
  cd "${srcdir}/${pkgbase}/mythmusic"
  make INSTALL_ROOT="${pkgdir}" install
}

package_mythplugins-mythnetvision() {
  pkgdesc="MythNetvision plugin for MythTV"
  depends=("mythtv>=${pkgver}")
  cd "${srcdir}/${pkgbase}/mythnetvision"
  make INSTALL_ROOT="${pkgdir}" install
}

package_mythplugins-mythnews() {
  pkgdesc="News checking plugin for MythTV"
  depends=("mythtv>=${pkgver}")
  replaces=('mythnews')
  cd "${srcdir}/${pkgbase}/mythnews"
  make INSTALL_ROOT="${pkgdir}" install
}

package_mythplugins-mythvideo() {
  pkgdesc="Video playback and browsing plugin for MythTV"
  depends=("mythtv>=${pkgver}" 'mplayer' 'perl-xml-simple')
  replaces=('mythvideo')
  cd "${srcdir}/${pkgbase}/mythvideo"
  make INSTALL_ROOT="${pkgdir}" install
  install -D -m755 ${srcdir}/mtd "${pkgdir}/etc/rc.d/mtd"
}

package_mythplugins-mythweather() {
  pkgdesc="Weather checking plugin for MythTV"
  depends=("mythtv>=${pkgver}" 'perl-xml-xpath' 'perl-image-size' 'perl-class-inspector' 'perl-datetime-format-iso8601')
  replaces=('mythweather')
  cd "${srcdir}/${pkgbase}/mythweather"
  make INSTALL_ROOT="${pkgdir}" install
}

package_mythplugins-mythweb() {
  pkgdesc="Web interface for MythTV's backend"
  depends=("mythtv>=${pkgver}")
  optdepends=('php-apache'
              'lighttpd')
  replaces=('mythweb')
  mkdir -p ${pkgdir}/srv/http/mythweb/{image_cache,php_sessions}
  cp -r ${srcdir}/mythplugins/mythweb/* ${pkgdir}/srv/http/mythweb
  chown -R http:http ${pkgdir}/srv/http/mythweb
  chmod g+rw ${pkgdir}/srv/http/mythweb/{image_cache,php_sessions,data}
}

package_mythplugins-mythzoneminder() {
  pkgdesc="Allows for viewing of CCTV cameras through zoneminder"
  depends=("mythtv>=${pkgver}")
  replaces=('mythzoneminder')
  install=mythplugins-mythzoneminder.install
  cd "${srcdir}/${pkgbase}/mythzoneminder"
  make INSTALL_ROOT="${pkgdir}" install
}
