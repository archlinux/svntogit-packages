# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Steve Holmes <steve.holmes88@gmail.com>
# Contributor: Giulio Bai <giulio@hewle.com>

pkgname=accerciser
pkgver=3.22.0
pkgrel=3
pkgdesc="Interactive Python accessibility explorer for the GNOME desktop"
arch=(any)
url="https://wiki.gnome.org/Apps/Accerciser"
license=(BSD)
depends=(python gtk3 python-atspi ipython libwnck3 python-cairo)
makedepends=(itstool docbook-xsl intltool gnome-common git)
groups=(gnome-extra)
_commit=e271fc21ca45edc5c030cd3c0397ff39373ea37e  # tags/3.22.0^0
source=("git+https://gitlab.gnome.org/GNOME/accerciser.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --disable-schemas-compile
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
  install -m 755 -d "$pkgdir"/usr/share/licenses/$pkgname
  install -m 644 COPYING "$pkgdir"/usr/share/licenses/$pkgname
}
