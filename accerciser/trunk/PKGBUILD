# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Steve Holmes <steve.holmes88@gmail.com>
# Contributor: Giulio Bai <giulio@hewle.com>

pkgname=accerciser
pkgver=3.13.92
pkgrel=1
pkgdesc="Interactive Python accessibility explorer for the GNOME desktop"
arch=('any')
url="http://live.gnome.org/Accerciser"
license=('BSD')
depends=('python' 'gtk3' 'python-atspi' 'ipython' 'hicolor-icon-theme' 'libwnck3')
makedepends=('itstool' 'docbook-xsl' 'intltool')
install=accerciser.install
groups=('gnome-extra')
source=(http://ftp.gnome.org/pub/GNOME/sources/accerciser/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('2cdaf442f5f89ff1b6563da854135c9444c2064ca3e8eb255d611e3a267cb61b')

build () {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
                 --disable-schemas-compile
  make
}

package () {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -m 755 -d "$pkgdir"/usr/share/licenses/$pkgname
  install -m 644 COPYING "$pkgdir"/usr/share/licenses/$pkgname
}
