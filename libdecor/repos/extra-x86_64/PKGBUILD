# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Adrian Perez de Castro <aperez@igalia.com>
pkgdesc='Client-side decorations library for Wayland clients'
pkgname=libdecor
pkgver=0.1.1
pkgrel=1
url="https://gitlab.gnome.org/jadahl/libdecor"
license=('MIT')
arch=('x86_64')
depends=('wayland' 'pango' 'dbus' 'egl-wayland' 'libxkbcommon')
makedepends=('cmake' 'ninja' 'meson' 'wayland-protocols')
source=("https://gitlab.gnome.org/jadahl/libdecor/-/archive/${pkgver}/libdecor-${pkgver}.tar.gz")
sha512sums=('40b1e355f28d4b18ce35b1c096592c81015a1fd4b2db0f3d58f14825388f6614e6dd28efa0368b7c133e094f6aca9db6a7a27dbd4ffdd349b5e8d7619fd3e0be')

build () {
  rm -rf _build
  arch-meson _build "${pkgname}-${pkgver}"
  meson compile -C _build
}

check () {
  meson test -C _build
}

package () {
  meson install -C _build --destdir "${pkgdir}"
  install -Dm644 "${pkgname}-${pkgver}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
