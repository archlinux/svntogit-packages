# $Id: PKGBUILD,v 1.20 2008/12/29 15:30:49 BaSh Exp $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgbase=system-config-printer
pkgname=('system-config-printer-common'
	'system-config-printer-gnome')
pkgver=1.2.5
pkgrel=4
pkgdesc="A CUPS printer configuration tool and status applet"
url="http://cyberelk.net/tim/software/system-config-printer/"
arch=('any')
license=('GPL')
makedepends=('intltool' 'python2' 'xmlto' 'docbook-xsl' 'desktop-file-utils' 'libcups' 'libxml2')
source=("http://cyberelk.net/tim/data/${pkgbase}/1.2/${pkgbase}-${pkgver}.tar.bz2")
md5sums=('71c01b73c458033795b218c4779f9811')

build() {
  cd ${srcdir}/${pkgbase}-${pkgver}
  
  sed -i -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
    -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
    $(find . -name '*.py')

  ./configure --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --with-udev-rules

  make
}

package_system-config-printer-common() {
  pkgdesc='Pygtk CUPS Configuration'
  depends=('pycups' 'dbus-python' 'pysmbc')
  optdepends=('system-config-printer-gnome: for the GTK frontend'
	'kdeadmin-system-config-printer-kde: for the administration tool in KDE System Settings')
  replaces=('system-config-printer')
  provides=("system-config-printer=${pkgver}")
  conflicts=('system-config-printer')

  cd ${srcdir}/${pkgbase}-${pkgver}
  install -d ${pkgdir}/usr/share/${pkgbase}
  install -m644 config.py \
    debug.py \
    installpackage.py \
    monitor.py \
    PhysicalDevice.py \
    ppdippstr.py \
    probe_printer.py \
    SearchCriterion.py \
    smburi.py \
    statereason.py \
    XmlHelper.py \
    ${pkgdir}/usr/share/${pkgbase}/

  install -m755 pysmb.py \
    ${pkgdir}/usr/share/${pkgbase}/

  install -d ${pkgdir}/etc/dbus-1/system.d/
  install -m644 newprinternotification.conf \
    ${pkgdir}/etc/dbus-1/system.d/

  install -d ${pkgdir}/lib/udev/rules.d
  install -m755 udev/{udev-add-printer,udev-configure-printer} \
    ${pkgdir}/lib/udev/
  install -m644 udev/70-printers.rules \
    ${pkgdir}/lib/udev/rules.d

  for file in build/lib/cupshelpers/*.py; do
    install -Dm644 $file \
      ${pkgdir}/usr/lib/python2.7/site-packages/cupshelpers/$(basename $file)
  done
}

package_system-config-printer-gnome() {
  pkgdesc='A CUPS printer configuration tool and status applet - GTK frontend'
  depends=('system-config-printer-common' 'gnome-python' 'gnome-icon-theme' 'pygobject' 'pygtk' 'libxml2' 'python-notify')

  cd ${srcdir}/${pkgbase}-${pkgver}
  make DESTDIR="${pkgdir}" install

  # files provided by system-config-printer-common
  cd ${pkgdir}/usr/share/${pkgbase}
  rm config.py \
    debug.py \
    installpackage.py \
    monitor.py \
    PhysicalDevice.py \
    ppdippstr.py \
    probe_printer.py \
    SearchCriterion.py \
    smburi.py \
    statereason.py \
    XmlHelper.py \
    pysmb.py
  rm ${pkgdir}/etc/dbus-1/system.d/newprinternotification.conf
  rm -r ${pkgdir}/etc/udev/
  rm -r ${pkgdir}/usr/lib/
}
