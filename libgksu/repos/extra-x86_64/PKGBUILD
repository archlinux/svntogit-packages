# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libgksu
pkgver=2.0.12
pkgrel=3
pkgdesc="gksu authorization library"
arch=(i686 x86_64)
url="http://www.nongnu.org/gksu/index.html"
license=(GPL)
depends=('libgnome-keyring>=2.30.1' 'libgtop>=2.28.1' 'startup-notification>=0.10' 'gconf>=2.28.1')
makedepends=('gtk-doc' 'pkgconfig' 'intltool')
options=('!libtool' '!emptydirs')
install=libgksu.install
source=(http://people.debian.org/~kov/gksu/${pkgname}-${pkgver}.tar.gz
        libgksu-2.0.12-revert-forkpty.patch)
md5sums=('c7154c8806f791c10e7626ff123049d3'
         'aebbe57e5286c654e27cf714cf3b704a')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/libgksu-2.0.12-revert-forkpty.patch"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static
  make
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install
  rm -f ${pkgdir}/usr/lib/*.a
  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain libgksu ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
