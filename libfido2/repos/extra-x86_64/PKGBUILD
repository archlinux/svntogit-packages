# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Karol Babioch <karol@babioch.de>

pkgname=libfido2
pkgver=1.6.0
pkgrel=1
pkgdesc='Library functionality for FIDO 2.0, including communication with a device over USB'
url='https://developers.yubico.com/libfido2/'
arch=('x86_64')
license=('BSD')
depends=('glibc' 'openssl' 'libcbor' 'libcbor.so' 'hidapi' 'systemd-libs'  'libudev.so')
makedepends=('cmake' 'systemd')
provides=('libfido2.so')
source=(https://developers.yubico.com/libfido2/Releases/libfido2-${pkgver}.tar.gz{,.sig})
sha512sums=('c473732a2f7ef54156097d315e44457d89056446ab3112a7c7a6fd99d5c2c8ae0ca2451ff9cd45be6c32de1ab335d6dfdb2b0c56b40cae9eb41391d18d83be4a'
            'SKIP')
b2sums=('59444cc9e32b5d9f2a0f9138fe4f3ad644865e1f7f24f50bbace262308aa7c10b58aa0e890a3493e8fd102468e26947f7fc0864b5d7a1e185e40ad730064db8e'
        'SKIP')
validpgpkeys=(
  'EE90AE0D19774C8386628FAAB428949EF7914718' # pedro martelletto <pedro@yubico.com>
  '1D7308B0055F5AEF36944A8F27A9C24D9588EA0F' # Aveen Ismail <aveen.ismail@yubico.com>
  '7FBB6186957496D58C751AC20E777DD85755AA4A' # Konstantinos Georgantas <kostas@yubico.com>
)

build() {
  cd ${pkgname}-${pkgver}
  # do not use bundled udev rules superseded by systemd
  cmake -B build \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_C_FLAGS="${CFLAGS} ${CPPFLAGS}" \
    -Wno-dev \
    -S .
  make -C build VERBOSE=1
}

package() {
  cd ${pkgname}-${pkgver}
  make -C build DESTDIR="${pkgdir}" install
  install -Dm 644 NEWS README.adoc -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
