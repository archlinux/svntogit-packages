# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>

pkgname=bison
pkgver=2.5
pkgrel=3
pkgdesc="The GNU general-purpose parser generator"
arch=('i686' 'x86_64')
license=('GPL3')
url="http://www.gnu.org/software/bison/bison.html"
depends=('glibc' 'm4' 'sh')
groups=('base-devel')
install=bison.install
source=(ftp://ftp.gnu.org/gnu/bison/${pkgname}-${pkgver}.tar.bz2{,.sig}
        bison-2.5-undefined-reference.patch)
md5sums=('9dba20116b13fc61a0846b0058fbe004'
         '610b73db67bd4760209458efe7554ca3'
         '2015c5421b2ad99032154f27bf5c4772')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  
  # workaround for FS#26794
  patch -Np1 -i $srcdir/bison-2.5-undefined-reference.patch
  
  ./configure --prefix=/usr --datadir=/usr/share
  make
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
