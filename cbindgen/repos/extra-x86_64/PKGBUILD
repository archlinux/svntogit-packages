# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=cbindgen
pkgver=0.13.1
pkgrel=1
pkgdesc="A tool for generating C bindings to Rust code"
url="https://github.com/eqrion/cbindgen"
arch=(x86_64)
license=(MPL2)
depends=(gcc-libs)
makedepends=(cargo git)
_commit=3b97f4ff6b0558cab300c010222caee84245dd5d  # tags/v0.13.1^0
source=("git+https://github.com/eqrion/cbindgen#commit=$_commit")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $pkgname
  cargo fetch --locked
}

build() {
  cd $pkgname
  cargo build --release --frozen
}

check() {
  cd $pkgname
  # test_expand* fails; needs nightly rust
  cargo test --release --frozen || :
}

package() {
  cd $pkgname
  install -Dt "$pkgdir/usr/bin" target/release/cbindgen
  install -Dt "$pkgdir/usr/share/doc/$pkgname" -m644 README.md
}
