# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Bhushan Shah <bhush94 at gmail dot com>

pkgname=plasma-mediacenter
pkgver=5.6.5
pkgrel=1
pkgdesc="A mediacenter user interface based on KDE Plasma components"
arch=(i686 x86_64)
url="http://community.kde.org/Plasma/Plasma_Media_Center/"
license=(GPL3)
depends=(plasma-workspace baloo hicolor-icon-theme)
makedepends=(extra-cmake-modules python)
groups=(plasma)
source=("http://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('8620f70e66455fbc404719bfd6d94d5f662584bde00102466986cd0d31fedc14'
            'SKIP')
validpgpkeys=('13C16D03EDE728514473AA73A506E6D4DD4D5088') # Jonathan Riddell

prepare() {
  mkdir -p build
}    

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
