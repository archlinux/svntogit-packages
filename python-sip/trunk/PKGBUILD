# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=python-sip
pkgver=4.12.1
pkgrel=1
arch=('i686' 'x86_64')
url="http://www.riverbankcomputing.com/software/sip/"
license=('custom:"sip"')
pkgdesc="A tool that makes it easy to create Python 3 bindings for C and C++ libraries"
depends=('python2-sip' 'python')
source=("http://www.riverbankcomputing.com/static/Downloads/sip4/sip-${pkgver}.tar.gz")
md5sums=('0f8e8305b14c1812191de2e0ee22fea9')

build() {
  cd "${srcdir}/sip-${pkgver}"

  python configure.py CFLAGS="${CFLAGS}" LFLAGS="${LDFLAGS}"
  make
}

package() {
  cd "${srcdir}/sip-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  
  # Provided by python2-sip package
  rm "${pkgdir}/usr/bin/sip"
}
