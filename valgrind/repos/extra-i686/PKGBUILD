# $Id$
# Maintainer: Dan McGee <dan@archlinux.org>
# Maintainer: Allan McRae <allan@archlinux.org>

pkgname=valgrind
pkgver=3.5.0
pkgrel=6
pkgdesc="A tool to help find memory-management problems in programs"
arch=('i686' 'x86_64')
license=('GPL')
url="http://valgrind.org/"
depends=('glibc>=2.12' 'glibc<2.13' 'perl')
makedepends=('gdb')
options=('!emptydirs')
source=(http://valgrind.org/downloads/${pkgname}-${pkgver}.tar.bz2
        glibc-patch-version.patch
        glibc-2.12.patch
        valgrind-3.5.0-stat_h.patch
        valgrind-3.5.0-elf-indirect-functions.patch
        valgrind-3.5.0-initialise-isifunc.patch)
md5sums=('f03522a4687cf76c676c9494fcc0a517'
         'b657f0ebdde3d9aefc9fd16f9e653702'
         '0ac843aecfc539a53ae88c48b1a17047'
         'e435a0debedf207b279256eff50c68f6'
         '10af3a098fa1f62441b7e52dbce38969'
         'cbdee7745fabac7119a8897a6c7147a6')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # make sure our CFLAGS are respected
  sed -i -e 's:^CFLAGS="-Wno-long-long":CFLAGS="$CFLAGS -Wno-long-long":' configure.in

  patch -Np1 -i ${srcdir}/glibc-patch-version.patch || return 1
  patch -Np0 -i ${srcdir}/glibc-2.12.patch || return 1
  patch -Np1 -i ${srcdir}/valgrind-3.5.0-stat_h.patch || return 1
  patch -Np0 -i ${srcdir}/valgrind-3.5.0-elf-indirect-functions.patch || return 1
  patch -Np0 -i ${srcdir}/valgrind-3.5.0-initialise-isifunc.patch || return 1
  autoreconf

  if [ "${CARCH}" = "x86_64" ]; then
    ./configure --prefix=/usr --mandir=/usr/share/man --enable-only64bit || return 1
  else
    ./configure --prefix=/usr --mandir=/usr/share/man || return 1
  fi
  make || return 1
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install || return 1
}
