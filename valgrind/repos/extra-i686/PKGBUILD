# $Id$
# Maintainer: Dan McGee <dan@archlinux.org>
# Maintainer: Allan McRae <allan@archlinux.org>

pkgname=valgrind
pkgver=3.5.0
pkgrel=2
pkgdesc="A tool to help find memory-management problems in programs"
arch=('i686' 'x86_64')
license=('GPL')
url="http://valgrind.org/"
depends=('glibc>=2.10.1' 'glibc<2.11' 'perl')
makedepends=('gdb')
options=(!emptydirs)
source=(http://valgrind.org/downloads/${pkgname}-${pkgver}.tar.bz2)
md5sums=('f03522a4687cf76c676c9494fcc0a517')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # make sure our CFLAGS are respected
  sed -i -e 's:^CFLAGS="-Wno-long-long":CFLAGS="$CFLAGS -Wno-long-long":' configure.in

  if [ "${CARCH}" = "x86_64" ]; then
    ./configure --prefix=/usr --mandir=/usr/share/man --enable-only64bit || return 1
  else
    ./configure --prefix=/usr --mandir=/usr/share/man || return 1
  fi
  make || return 1
  make DESTDIR=${pkgdir} install || return 1
}
