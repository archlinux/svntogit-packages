# $Id$
# Maintainer: Dan McGee <dan@archlinux.org>
# Maintainer: Allan McRae <allan@archlinux.org>

pkgname=valgrind
pkgver=3.6.1
pkgrel=2
pkgdesc="A tool to help find memory-management problems in programs"
arch=('i686' 'x86_64')
license=('GPL')
url="http://valgrind.org/"
depends=('glibc>=2.14' 'glibc<2.15' 'perl')
makedepends=('gdb')
options=('!emptydirs')
source=(http://valgrind.org/downloads/${pkgname}-${pkgver}.tar.bz2
        valgrind-3.6.1-glibc-2.14.patch
        glibc-patch-version.patch)    
md5sums=('2c3aa122498baecc9d69194057ca88f5'
         '560032ce5d27ef0c7c1af32c3fd45833'
         'b657f0ebdde3d9aefc9fd16f9e653702')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # make sure our CFLAGS are respected
  sed -i -e 's:^CFLAGS="-Wno-long-long":CFLAGS="$CFLAGS -Wno-long-long":' configure.in

  # glibc-2.14 compatibility
  patch -Np1 -i ${srcdir}/valgrind-3.6.1-glibc-2.14.patch

  # prevent need to rebuild with glibc patch level version bumps
  patch -Np1 -i ${srcdir}/glibc-patch-version.patch

  if [ "${CARCH}" = "x86_64" ]; then
    ./configure --prefix=/usr --mandir=/usr/share/man --enable-only64bit
  else
    ./configure --prefix=/usr --mandir=/usr/share/man
  fi
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
