# $Id$
# Maintainer: Dan McGee <dan@archlinux.org>
# Maintainer: Allan McRae <allan@archlinux.org>

pkgname=valgrind
pkgver=3.9.0
pkgrel=5
pkgdesc='A tool to help find memory-management problems in programs'
arch=(i686 x86_64)
license=(GPL)
url='http://valgrind.org/'
depends=('glibc>=2.19' 'glibc<2.20' 'perl')
makedepends=('gdb' 'openmpi')
optdepends=('openmpi: MPI support')
options=('staticlibs' '!emptydirs')
source=(http://valgrind.org/downloads/$pkgname-$pkgver.tar.bz2
        valgrind-3.9.0-glibc-2.19.patch
        add_xabort_op.patch)
sha1sums=('9415e28933de9d6687f993c4bb797e6bd49583f1'
          '4653e994d2660490c51a97cc70e45a506e80bfab'
          '126e90494e0134a70649456cd1a1f1369024c345')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i "$srcdir"/valgrind-3.9.0-glibc-2.19.patch
  patch -p0 -i "$srcdir"/add_xabort_op.patch  # FS#41696
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --mandir=/usr/share/man --with-mpicc=mpicc
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
