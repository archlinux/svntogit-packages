# $Id$
# Maintainer: Dan McGee <dan@archlinux.org>
# Maintainer: Allan McRae <allan@archlinux.org>

pkgname=valgrind
pkgver=3.5.0
pkgrel=3
pkgdesc="A tool to help find memory-management problems in programs"
arch=('i686' 'x86_64')
license=('GPL')
url="http://valgrind.org/"
depends=('glibc>=2.11' 'glibc<2.12' 'perl')
makedepends=('gdb')
options=(!emptydirs)
source=(http://valgrind.org/downloads/${pkgname}-${pkgver}.tar.bz2 glibc211.diff)
md5sums=('f03522a4687cf76c676c9494fcc0a517'
         'bb137ce7378b6a2d1ad203418da8b68f')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -Np0 -i ${srcdir}/glibc211.diff || return 1
  autoreconf

  # make sure our CFLAGS are respected
  sed -i -e 's:^CFLAGS="-Wno-long-long":CFLAGS="$CFLAGS -Wno-long-long":' configure.in

  if [ "${CARCH}" = "x86_64" ]; then
    ./configure --prefix=/usr --mandir=/usr/share/man --enable-only64bit || return 1
  else
    ./configure --prefix=/usr --mandir=/usr/share/man || return 1
  fi
  make || return 1
  make DESTDIR=${pkgdir} install || return 1
}
