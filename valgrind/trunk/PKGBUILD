# $Id$
# Maintainer: Dan McGee <dan@archlinux.org>
# Maintainer: Allan McRae <allan@archlinux.org>

pkgname=valgrind
pkgver=3.9.0
pkgrel=2
pkgdesc='A tool to help find memory-management problems in programs'
arch=(i686 x86_64)
license=(GPL)
url='http://valgrind.org/'
depends=('glibc>=2.19' 'glibc<2.20' 'perl')
makedepends=('gdb')
options=('staticlibs' '!emptydirs')
source=(http://valgrind.org/downloads/$pkgname-$pkgver.tar.bz2
        valgrind-3.9.0-glibc-2.19.patch)
sha1sums=('9415e28933de9d6687f993c4bb797e6bd49583f1'
          '4653e994d2660490c51a97cc70e45a506e80bfab')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i "$srcdir"/valgrind-3.9.0-glibc-2.19.patch
}

build() {
  cd $pkgname-$pkgver

  if [[ $CARCH = "x86_64" ]]; then
    configure_flags='--enable-only64bit'
  fi

  ./configure --prefix=/usr --mandir=/usr/share/man $configure_flags

  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
