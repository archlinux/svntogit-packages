# $Id$
# Maintainer: Dan McGee <dan@archlinux.org>
# Maintainer: Allan McRae <allan@archlinux.org>

pkgname=valgrind
pkgver=3.6.0
pkgrel=3
pkgdesc="A tool to help find memory-management problems in programs"
arch=('i686' 'x86_64')
license=('GPL')
url="http://valgrind.org/"
depends=('glibc>=2.13' 'glibc<2.14' 'perl')
makedepends=('gdb')
options=('!emptydirs')
source=(http://valgrind.org/downloads/${pkgname}-${pkgver}.tar.bz2
        glibc-patch-version.patch
       	glibc-2.13.patch
       	glibc-2.13-strcasecmp.patch)    
md5sums=('b289c5f4ab8e39741602445f1dd09b34'
         'b657f0ebdde3d9aefc9fd16f9e653702'
         '3706469d3b712f420e161a5ba1143d02'
         'd593be202296116653c792556ab6db73')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # make sure our CFLAGS are respected
  sed -i -e 's:^CFLAGS="-Wno-long-long":CFLAGS="$CFLAGS -Wno-long-long":' configure.in

  # prevent need to rebuild with glibc patch level version bumps
  patch -Np1 -i ${srcdir}/glibc-patch-version.patch

  # glibc-2.13 "compatibility"
  patch -Np1 -i ${srcdir}/glibc-2.13.patch

  # http://bugs.kde.org/show_bug.cgi?id=256600
  patch -Np0 -i ${srcdir}/glibc-2.13-strcasecmp.patch

  if [ "${CARCH}" = "x86_64" ]; then
    ./configure --prefix=/usr --mandir=/usr/share/man --enable-only64bit
  else
    ./configure --prefix=/usr --mandir=/usr/share/man
  fi
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
