# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgbase=sof-bin
pkgname=(sof-firmware sof-tools)
pkgver=2.2.1
pkgrel=1
pkgdesc="Sound Open Firmware"
url="https://www.sofproject.org/"
arch=(x86_64)
license=(custom:BSD custom:ISC)
options=(debug)
_binver=v${pkgver%.*}
_tplgver=v${pkgver}
source=(https://github.com/thesofproject/sof-bin/releases/download/$_binver/sof-bin-$_binver.tar.gz 
        https://github.com/thesofproject/sof-bin/releases/download/$_tplgver/sof-tplg-$_tplgver.tar.gz)
sha256sums=('f0a22018aa02266e3713f1551b698847ca303859be4be8c711dd3685de55ad15'
            'ccdda55f89a63f421ae9a019cc9c8f8f0f8668a0e8c35e384da09ea6be411bb7')

package_sof-firmware() {
  options+=(!strip)

  local fwdir="$pkgdir/usr/lib/firmware/intel"
  mkdir -p "$fwdir"

  cp -at "$fwdir" sof-tplg-$_tplgver
  ln -sr "$fwdir"/{sof-tplg-$_tplgver,sof-tplg}

  cd sof-bin-$_binver

  cp -at "$fwdir" sof-$_binver
  ln -sr "$fwdir"/{sof-$_binver,sof}

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 LICENCE* Notice*
}

package_sof-tools() {
  pkgdesc+=" - tools"
  depends=(alsa-lib)

  cd sof-bin-$_binver

  install -Dt "$pkgdir/usr/bin" tools-$_binver/*
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 LICENCE* Notice*
}

# vim:set sw=2 et:
