# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgbase=sof-bin
pkgname=(sof-firmware sof-tools)
pkgver=2.1.1.a
pkgrel=1
pkgdesc="Sound Open Firmware"
url="https://www.sofproject.org/"
arch=(x86_64)
license=(custom:BSD custom:ISC)
makedepends=(rsync)
_binver=v${pkgver%.a}
_tplgver=v${pkgver/%.a/a}
source=(https://github.com/thesofproject/sof-bin/releases/download/$_binver/sof-bin-$_binver.tar.gz 
        https://github.com/thesofproject/sof-bin/releases/download/$_tplgver/sof-tplg-$_tplgver.tar.gz)
sha256sums=('adcd22ba2f2aad911fdc53c478187bc6ddb9174fa52fa43ba07f6f0cc8d12a19'
            '58839acdde407062eb388140a8500d4cfb04384aa8c4a8f2bea5a985c7ad748f')

prepare() {
  cp -at sof-bin-$_binver/sof-tplg-$_binver sof-tplg-$_tplgver/*
}

package_sof-firmware() {
  options=(!strip)

  cd sof-bin-$_binver

  (
    export FW_DEST="$pkgdir/usr/lib/firmware/intel"
    export TOOLS_DEST="$srcdir/tools/usr/bin"

    mkdir -p "$FW_DEST" "$TOOLS_DEST"
    ./install.sh $_binver
  )

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 LICENCE* Notice*
}

package_sof-tools() {
  pkgdesc+=" - tools"
  depends=(alsa-lib)

  cd sof-bin-$_binver

  mv "$srcdir"/tools/* "$pkgdir"

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 LICENCE* Notice*
}

# vim:set sw=2 et:
