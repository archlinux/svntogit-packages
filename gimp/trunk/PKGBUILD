# $Id$
# Maintainer: tobias <tobias@archlinux.org>

pkgname=gimp
pkgver=2.6.11
pkgrel=7
pkgdesc="GNU Image Manipulation Program"
arch=('i686' 'x86_64')
url="http://www.gimp.org/"
license=('GPL' 'LGPL')
depends=('pygtk' 'lcms' 'libxpm' 'libwmf' 'libxmu' 'librsvg' 'libmng' 'dbus-glib' \
         'libexif' 'gegl' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('intltool' 'libwebkit' 'poppler-glib' 'alsa-lib' 'iso-codes' 'curl')
optdepends=('gutenprint: for sophisticated printing only as gimp has built-in cups print support'
            'libwebkit: for the help browser'
            'poppler-glib: for pdf support'
            'alsa-lib: for MIDI event controller module'
            'curl: for URI support')
options=('!libtool' '!makeflags')
conflicts=('gimp-devel')
install=gimp.install
#source=(ftp://ftp.gimp.org/pub/gimp/v${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2 linux.gpl 
source=(http://mirror.umoss.org/gimp/gimp/v${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2 linux.gpl 
        uri-backend-libcurl.patch gimp-poppler-0.18.patch)
md5sums=('bb2939fe13e54fc7255cef5d097bb5dd'
         'bb27bc214261d36484093e857f015f38'
         'e894f4b2ffa92c71448fdd350e9b78c6'
         'bfc73f80e911ed7a7d500d80973469f1')
sha1sums=('2f9d596e727bdbf304fa78257c1731d9faf3934c'
          '110ce9798173b19a662d086ed7b882b4729f06cf'
          'a65b0ee6cd1b4345065b7b98c07f2fed15f844f4'
          '1479a6d80be7adf74f66a3b88057a5029fe892e8')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 < ../uri-backend-libcurl.patch
  patch -p1 < ../gimp-poppler-0.18.patch
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --sysconfdir=/etc \
    --enable-mp --enable-gimp-console --enable-gimp-remote \
    --enable-python --with-gif-compression=lzw --with-libcurl \
    --without-aa --without-hal --without-gvfs --without-gnomevfs
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|' "${pkgdir}"/usr/lib/gimp/2.0/plug-ins/*.py
  install -D -m644 "${srcdir}/linux.gpl" "${pkgdir}/usr/share/gimp/2.0/palettes/Linux.gpl"

  rm "${pkgdir}/usr/share/man/man1/gimp-console.1"
  ln -s gimp-console-${pkgver%.*}.1.gz "${pkgdir}/usr/share/man/man1/gimp-console.1.gz"
  ln -s gimptool-2.0 "${pkgdir}/usr/bin/gimptool"
  ln -sf gimptool-2.0.1.gz "${pkgdir}/usr/share/man/man1/gimptool.1.gz"
}
