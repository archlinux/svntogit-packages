# $Id$
# Maintainer: Daniel Isenmann <daniel@archlinux.org>

pkgname=gimp
pkgver=2.8.10
pkgrel=1
pkgdesc="GNU Image Manipulation Program"
arch=('i686' 'x86_64')
url="http://www.gimp.org/"
license=('GPL' 'LGPL')
depends=('pygtk' 'lcms' 'libxpm' 'libwmf' 'libxmu' 'librsvg' 'libmng' 'dbus-glib' \
         'libexif' 'gegl' 'jasper' 'desktop-file-utils' 'hicolor-icon-theme' 'babl' 'openexr')
makedepends=('intltool' 'webkitgtk2' 'poppler-glib' 'alsa-lib' 'iso-codes' 'curl' 'ghostscript')
optdepends=('gutenprint: for sophisticated printing only as gimp has built-in cups print support'
            'webkitgtk2: for the help browser'
            'poppler-glib: for pdf support'
            'alsa-lib: for MIDI event controller module'
            'curl: for URI support'
            'ghostscript: for postscript support')
options=('!makeflags')
conflicts=('gimp-devel')
install=gimp.install
source=(ftp://ftp.gimp.org/pub/gimp/v${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2 linux.gpl 
        uri-backend-libcurl.patch
	patch_CVE197.patch
	patch_CVE1913.patch
	freetype_location.patch)
md5sums=('84c964aab7044489af69f7319bb59b47'
         'bb27bc214261d36484093e857f015f38'
         'e894f4b2ffa92c71448fdd350e9b78c6'
         '901643efc82fae57af466d8a0a8991ff'
         'dcc2acc9f9962a7c83ffb4243c08c469'
         'ca0f32cb329ddad8079f8e2c0fc75b29')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  #patch -p1 < ../uri-backend-libcurl.patch
  patch -p1 < ../patch_CVE1913.patch
  patch -p1 < ../patch_CVE197.patch
  patch -p1 < ../freetype_location.patch
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --sysconfdir=/etc \
    --enable-mp --enable-gimp-console --enable-gimp-remote \
    --enable-python --with-gif-compression=lzw --with-libcurl \
    --without-aa --without-hal --without-gvfs --without-gnomevfs
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|' "${pkgdir}"/usr/lib/gimp/2.0/plug-ins/*.py
  install -D -m644 "${srcdir}/linux.gpl" "${pkgdir}/usr/share/gimp/2.0/palettes/Linux.gpl"

  rm "${pkgdir}/usr/share/man/man1/gimp-console.1"
  ln -s gimp-console-${pkgver%.*}.1.gz "${pkgdir}/usr/share/man/man1/gimp-console.1.gz"
  ln -s gimptool-2.0 "${pkgdir}/usr/bin/gimptool"
  ln -sf gimptool-2.0.1.gz "${pkgdir}/usr/share/man/man1/gimptool.1.gz"
}
