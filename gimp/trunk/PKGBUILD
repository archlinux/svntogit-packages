# $Id$
# Maintainer: tobias <tobias@archlinux.org>

pkgname=gimp
pkgver=2.4.7
pkgrel=1
pkgdesc="GNU Image Manipulation Program"
arch=('i686' 'x86_64')
license=('GPL2')
url="http://www.gimp.org/"
depends=('gtk2>=2.12.11' 'lcms>=1.17' 'libxpm>=3.5.7' 'libwmf>=0.2.8.4'
         'libxmu>=1.0.4' 'librsvg>=2.22.2' 'libmng>=1.0.10' 'dbus-glib>=0.7.6'
         'libexif>=0.6.16' 'libart-lgpl>=2.3.20' 'pygtk>=2.12.1')
makedepends=('gutenprint>=5.0.2' 'perlxml' 'poppler-glib>=0.8.4'
             'pkgconfig' 'alsa-lib>=1.0.16' 'libgnomeui>=2.22.1')
				 optdepends=('gutenprint: Advanced printing support' 'poppler-glib: Opening PDF files')
options=('!libtool' '!makeflags')
conflicts=('gimp-unstable')
install=${pkgname}.install
source=(ftp://ftp.gimp.org/pub/gimp/v2.4/${pkgname}-${pkgver}.tar.bz2
        linux.gpl)
md5sums=('8d0e059597d70fbdeb77e6447bae29c4' 'bb27bc214261d36484093e857f015f38')

build() {
   cd ${startdir}/src/${pkgname}-${pkgver}
   ./configure --prefix=/usr --sysconfdir=/etc \
     --enable-mp --enable-gimp-console --disable-devel-docs \
     --enable-python --with-gif-compression=lzw \
     --without-aa || return 1
   make || return 1
   make DESTDIR=${startdir}/pkg install-strip || return 1
   install -Dm644 ${startdir}/src/linux.gpl \
     ${startdir}/pkg/usr/share/gimp/2.0/palettes/Linux.gpl || return 1
}
