# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=xorg-xkbcomp
pkgver=1.4.2+7+g29fda8e
pkgrel=1
pkgdesc="X Keyboard description compiler"
arch=(x86_64)
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libxkbfile')
makedepends=('xorg-util-macros' git)
groups=('xorg-apps' 'xorg')
_commit=29fda8e50e4a4a127348e63fcf9f47600beab93c  # master
source=("git+https://gitlab.freedesktop.org/xorg/app/xkbcomp#commit=$_commit")
sha512sums=('SKIP')
validpgpkeys=('3C2C43D9447D5938EF4551EBE23B7E70B467F0BF') # Peter Hutterer (Who-T) <office@who-t.net>
validpgpkeys+=('A66D805F7C9329B4C5D82767CCC4F07FAC641EFF') # "Daniel Stone <daniels@collabora.com>"
validpgpkeys+=('DD38563A8A8224537D1F90E45B8A2D50A0ECD0D3') # "Adam Jackson <ajax@benzedrine.nwnk.net>"

pkgver() {
  cd xkbcomp
  git describe --tags | sed 's/^xkbcomp-//;s/-/+/g'
}

prepare() {
  cd xkbcomp
  NOCONFIGURE=1 ./autogen.sh
}

check() {
  make -C xkbcomp check
}

build() {
  cd xkbcomp
  ./configure --prefix=/usr
  make
}

package() {
  cd xkbcomp
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
