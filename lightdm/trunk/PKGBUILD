# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Morfeo <morfeo89@hotmail.it>

pkgbase=lightdm
pkgname=('lightdm' 'liblightdm-qt4' 'liblightdm-qt5')
pkgver=1.28.0
pkgrel=1
epoch=1
pkgdesc='A lightweight display manager'
arch=('x86_64')
url='https://www.freedesktop.org/wiki/Software/LightDM/'
license=('GPL3' 'LGPL3')
makedepends=('gcc-libs' 'glib2' 'git' 'gobject-introspection' 'gtk-doc'
             'intltool' 'itstool' 'libgcrypt' 'libx11' 'libxcb' 'libxdmcp'
             'libxklavier' 'pam' 'polkit' 'qt4' 'qt5-base' 'vala' 'yelp-tools')
source=("git+https://github.com/CanonicalLtd/lightdm.git#tag=${pkgver}"
        'lightdm.service'
        'lightdm.sysusers'
        'lightdm.tmpfiles'
        'lightdm.pam'
        'lightdm-autologin.pam'
        'lightdm.rules'
        'lightdm-default-config.patch'
        'Xsession')
sha256sums=('SKIP'
            '0db37a14521be729411a767f157fbd07adb738b14006277def53a1efe4dacfb8'
            '19acdbf64c342f77662c08aa53a5ab932ef7faad6e16452b0fbe7758fa935345'
            'dc01ed0bd1c2f271a4b13ed030485e4a9fa4015c69806d6be485237ff69dce41'
            'e8c4c5fd3b801a390d201166fd1fb9730e78a5c62928768103b870b6bd980ea0'
            '33e3c3d6e16c8d30756754ea3f31f5457c5be0343686aad287692be34958984a'
            'a89566307e1c81c24f037d854cbd472d2f94f8a4b759877a01563a332319f7d6'
            'cf8783a9c56b8460d557f14f4687346194eb72e6060e23d6c8d7a7e856b2d2a4'
            'd30321a1b490500483b8ed7825fcff2c24a7c760ac627789ff517693888ec3c5')

prepare() {
  cd lightdm

  patch -Np1 -i ../lightdm-default-config.patch

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd lightdm

  export MOC4='moc-qt4'
  export MOC5='moc-qt5'

  ./configure \
    --prefix='/usr' \
    --libexecdir='/usr/lib/lightdm' \
    --localstatedir='/var' \
    --sbindir='/usr/bin' \
    --sysconfdir='/etc' \
    --disable-static \
    --disable-tests \
    --enable-gtk-doc \
    --with-greeter-user='lightdm' \
    --with-greeter-session='lightdm-gtk-greeter'
  make
}

package_lightdm() {
  depends=('glib2' 'libgcrypt' 'libx11' 'libxcb' 'libxdmcp' 'libxklavier' 'pam'
           'polkit')
  optdepends=('accountsservice: Enhanced user accounts handling'
              'lightdm-gtk-greeter: GTK greeter'
              'xorg-server-xephyr: LightDM test mode')
  backup=('etc/apparmor.d/lightdm-guest-session'
          'etc/lightdm/keys.conf'
          'etc/lightdm/lightdm.conf'
          'etc/lightdm/users.conf'
          'etc/lightdm/Xsession'
          'etc/pam.d/lightdm'
          'etc/pam.d/lightdm-autologin'
          'etc/pam.d/lightdm-greeter')

  cd lightdm

  make DESTDIR="${pkgdir}" install
  make DESTDIR="${pkgdir}" -C liblightdm-qt uninstall
  install -m 755 ../Xsession "${pkgdir}"/etc/lightdm/Xsession
  rm -rf "${pkgdir}"/etc/init
  rm -rf "${pkgdir}"/usr/include/lightdm-qt{,5}-*

  # PAM
  install -m 644 ../lightdm.pam "${pkgdir}"/etc/pam.d/lightdm
  install -m 644 ../lightdm-autologin.pam "${pkgdir}"/etc/pam.d/lightdm-autologin

  # PolicyKit
  install -dm 750 -g 102 "${pkgdir}"/usr/share/polkit-1/rules.d
  install -m 644 ../lightdm.rules "${pkgdir}"/usr/share/polkit-1/rules.d/lightdm.rules

  # Systemd
  install -dm 755 "${pkgdir}"/usr/lib/{systemd/system,sysusers.d,tmpfiles.d}
  install -m 644 ../lightdm.service "${pkgdir}"/usr/lib/systemd/system/lightdm.service
  install -m 644 ../lightdm.sysusers "${pkgdir}"/usr/lib/sysusers.d/lightdm.conf
  install -m 644 ../lightdm.tmpfiles "${pkgdir}"/usr/lib/tmpfiles.d/lightdm.conf
}

package_liblightdm-qt4() {
  pkgdesc='LightDM Qt client library'
  depends=('gcc-libs' 'lightdm' 'qt4')
  options=('!emptydirs')

  cd lightdm

  make DESTDIR="${pkgdir}" -C liblightdm-gobject install
  make DESTDIR="${pkgdir}" -C liblightdm-qt install
  make DESTDIR="${pkgdir}" -C liblightdm-gobject uninstall
  find "${pkgdir}" -type d -name *qt5* -exec rm -rf {} +
  find "${pkgdir}" -type f  -name *qt5* -exec rm {} +
  find "${pkgdir}" -type l  -name *qt5* -exec rm {} +
}

package_liblightdm-qt5() {
  pkgdesc='LightDM Qt client library'
  depends=('gcc-libs' 'lightdm' 'qt5-base')
  options=('!emptydirs')

  cd lightdm

  make DESTDIR="${pkgdir}" -C liblightdm-gobject install
  make DESTDIR="${pkgdir}" -C liblightdm-qt install
  make DESTDIR="${pkgdir}" -C liblightdm-gobject uninstall
  find "${pkgdir}" -type d -name *qt[!5]* -exec rm -rf {} +
  find "${pkgdir}" -type f  -name *qt[!5]* -exec rm {} +
  find "${pkgdir}" -type l  -name *qt[!5]* -exec rm {} +
}

# vim: ts=2 sw=2 et:
