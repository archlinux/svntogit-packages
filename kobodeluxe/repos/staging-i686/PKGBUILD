# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=kobodeluxe
pkgver=0.5.1
pkgrel=5
pkgdesc="An enhanced version of Akira Higuchi's game XKobo, an addictive space shoot'em up"
arch=('i686' 'x86_64')
url="http://www.olofson.net/kobodl/"
license=('GPL' 'LGPL')
depends=('sdl_image' 'hicolor-icon-theme' 'gtk-update-icon-cache')
install=kobodeluxe.install
source=(http://www.olofson.net/kobodl/download/KoboDeluxe-${pkgver}.tar.gz
        kobodeluxe-gcc-4.3.patch kobodeluxe-0.5.1-glibc2.10.patch)
sha1sums=('12abe182ccde52050cd23077092d0424d25c3e75'
          '274dc8e7909bc6f9fab31306c85d992af8b62674'
          '9d1b3b8cef4a16b6a082fed9f93f1dc12b2a3be0')

prepare() {
  cd KoboDeluxe-${pkgver}
  tar -xf icons.tar.gz
  patch -p0 -i "${srcdir}/kobodeluxe-gcc-4.3.patch"
  patch -p0 -i "${srcdir}/kobodeluxe-0.5.1-glibc2.10.patch"
}

build() {
  cd KoboDeluxe-${pkgver}
  ./configure --prefix=/usr --localstatedir=/var --sharedstatedir=/var/games
  make
}

package() {
  cd KoboDeluxe-${pkgver}
  make DESTDIR="${pkgdir}" install
 
  install -D -m644 icons/KDE/kobo-deluxe.desktop "${pkgdir}/usr/share/applications/kobo-deluxe.desktop"
  for i in 16 22 32 48 64 128 ; do
    install -D -m644 icons/KDE/icons/${i}x${i}/kobodl.png "${pkgdir}/usr/share/icons/hicolor/${i}x${i}/apps/kobodl.png"
  done

  chown root:games "${pkgdir}/usr/bin/kobodl"
  chmod 2755 "${pkgdir}/usr/bin/kobodl"
  chown root:games "${pkgdir}/var/games"
  chmod 775 "${pkgdir}/var/games"
  chown root:games "${pkgdir}/var/games/kobo-deluxe"
  chmod 775 "${pkgdir}/var/games/kobo-deluxe"
  chown root:games "${pkgdir}/var/games/kobo-deluxe/scores"
  chmod 775 "${pkgdir}/var/games/kobo-deluxe/scores"
}
