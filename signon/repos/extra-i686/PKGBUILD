# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgbase=signon
pkgname=('signon-qt4' 'signon-qt5')
pkgver=8.57
pkgrel=1
arch=('i686' 'x86_64')
url="http://code.google.com/p/accounts-sso/"
license=('LGPL')
makedepends=('qt4' 'libproxy' 'doxygen' 'graphviz' 'qt5-base' 'ttf-font' 'qt5-tools')
options=('!emptydirs')
source=("https://sources.archlinux.org/other/packages/${pkgbase}/$pkgbase-$pkgver.tar.bz2"
        'use_keyring.patch')
sha1sums=('c9fa0567b68db434a7a954dcb303adf7aad1578b'
          'cfdd48b494ad267654ceba23f1b81bcb8f56b9a1')

prepare() {
  cd $pkgbase-$pkgver
  # Use gnome-keyring secrets storage if signon-keyring-extension is available
  patch -Np1 -i "$srcdir/use_keyring.patch"
  cd ..

  cp -r $pkgbase-$pkgver $pkgbase-$pkgver-qt4

  # qt4 fix
  cd $pkgbase-$pkgver-qt4
  sed -i 's/qdbusxml2cpp/qdbusxml2cpp-qt4/' src/signond/signond.pro
}

build() {
  cd $pkgbase-$pkgver
  qmake PREFIX=/usr LIBDIR=/usr/lib
  make
  cd ..

  cd $pkgbase-$pkgver-qt4
  qmake-qt4 PREFIX=/usr LIBDIR=/usr/lib
  make
}

package_signon-qt4() {
  pkgdesc="Qt4 Framework to provide credential storage and authentication service"
  depends=('qt4' 'libproxy')
  install=$pkgbase.install
  provides=('signon')
  replaces=('signon' 'signon-doc')
  conflicts=('signon' 'signon-doc' 'signon-qt5')

  cd $pkgbase-$pkgver-qt4
  make INSTALL_ROOT="$pkgdir" install

  # Do not ship docs
  rm -rf "${pkgdir}"/usr/share/doc
}

package_signon-qt5() {
  pkgdesc="Qt5 Framework to provide credential storage and authentication service"
  depends=('qt5-base' 'libproxy')
  install=$pkgbase.install
  conflicts=('signon' 'signon-qt4')

  cd $pkgbase-$pkgver
  make INSTALL_ROOT="$pkgdir" install

  # Do not ship docs
  rm -rf "${pkgdir}"/usr/share/doc
}
