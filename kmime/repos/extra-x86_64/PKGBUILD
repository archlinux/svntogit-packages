# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kmime
pkgver=20.04.0
pkgrel=2
pkgdesc="Library for handling mail messages and newsgroup articles"
arch=(x86_64)
url="https://kontact.kde.org"
license=(LGPL)
depends=(ki18n kcodecs)
makedepends=(extra-cmake-modules)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
         kdebug-420849.patch::"https://cgit.kde.org/kmime.git/patch/?id=dfaaec1d")
sha256sums=('4230ca0147d31fa688cf84ac8e7bb46e64deb9536b7a25544be73171615c8fac'
            'SKIP'
            '63e06a74e2dfbbd14c6d823a42b245f6e1e476298900460a6848e11e12f82a9d')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -Rp1 -i ../kdebug-420849.patch # Revert bogus fix that makes kmail fail to parse adresses with non-ascii characters
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
