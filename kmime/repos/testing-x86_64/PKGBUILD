# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kmime
pkgver=20.12.0
pkgrel=1
pkgdesc="Library for handling mail messages and newsgroup articles"
arch=(x86_64)
url="https://kontact.kde.org"
license=(LGPL)
depends=(ki18n kcodecs)
makedepends=(extra-cmake-modules)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('03375fd4963ad87c675b091c82e4d4ecfd5b17ed513964f7567a38e6b33caf64'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
