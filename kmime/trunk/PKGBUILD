# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kmime
pkgver=16.04.1
pkgrel=1
pkgdesc="Library for handling mail messages and newsgroup articles"
arch=(i686 x86_64)
url="https://community.kde.org/KDE_PIM"
license=(LGPL)
depends=(ki18n kcodecs)
makedepends=(extra-cmake-modules python)
source=("http://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
md5sums=('7ec824a6a56bc2dce75a5c61a6bb4d59')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
