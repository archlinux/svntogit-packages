# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-luit
pkgver=1.1.1
pkgrel=2
pkgdesc="Filter that can be run between an arbitrary application and a UTF-8 terminal emulator"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libfontenc')
groups=('xorg' 'xorg-apps')
makedepends=('xorg-util-macros')
source=(http://xorg.freedesktop.org/archive/individual/app/luit-${pkgver}.tar.bz2
        git_fixes.diff)
sha256sums=('30b0e787cb07a0f504b70f1d6123930522111ce9d4276f6683a69b322b49c636'
            'aa5d83b8cc772e135e57ae0f0d2b7662b51981f4a120a136a7b201c254b23327')

build() {
  cd luit-${pkgver}
  patch -Np1 -i $srcdir/git_fixes.diff
  autoreconf -vfi
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/luit-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
