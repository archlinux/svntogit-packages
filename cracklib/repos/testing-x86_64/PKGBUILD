# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Federico Quagliata (quaqo) <quaqo@despammed.com>

pkgname=cracklib
pkgver=2.9.0
pkgrel=2
pkgdesc="Password Checking Library"
arch=('i686' 'x86_64')
license=('GPL')
url="http://sourceforge.net/projects/cracklib"
depends=('glibc' 'zlib')
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz)
md5sums=('e0f94ac2138fd33c7e77b19c1e9a9390')

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure --prefix=/usr --sbindir=/usr/bin --without-python
  make 
}

check() {
  cd $srcdir/$pkgname-$pkgver
  make check
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
  # symlink cracklib-small #34778
  mkdir -p $pkgdir/usr/share/dict
  ln -sf /usr/share/cracklib/cracklib-small $pkgdir/usr/share/dict/cracklib-small
  sh ./util/cracklib-format dicts/cracklib-small \
    | sh ./util/cracklib-packer $pkgdir/usr/share/cracklib/pw_dict
}
