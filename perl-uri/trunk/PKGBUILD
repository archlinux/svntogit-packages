# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Manolis Tzanidakis
# Contributor: Firmicus <francois.archlinux.org> 

pkgname=perl-uri
_realname=URI
pkgver=1.73
pkgrel=1
pkgdesc="Uniform Resource Identifiers (absolute and relative)"
arch=('any')
url="http://search.cpan.org/dist/${_realname}/"
license=('PerlArtistic')
depends=('perl')
checkdepends=('perl-test-needs')
provides=('perl-uri-escape=3.30')
options=('!emptydirs')
source=("http://cpan.metacpan.org/authors/id/E/ET/ETHER/${_realname}-${pkgver}.tar.gz")
sha512sums=('b72d07e9e9e284f360ad4bf42b22ddec2d1930be40ec465ca28c68484e87b01c55ab664cdec63af387bc435e9428681c64aed39b46b668d7c7c5f36be5fbeb2e')

build() {
  cd "${srcdir}/${_realname}-$pkgver"
  # install module in vendor directories.
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make test
}

package() {
  cd "${srcdir}/${_realname}-$pkgver"
  make install DESTDIR="${pkgdir}"
}
# vim: ts=2 sw=2 et ft=sh
