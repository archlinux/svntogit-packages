# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Manolis Tzanidakis
# Contributor: Firmicus <francois.archlinux.org> 

pkgname=perl-uri
_realname=URI
pkgver=1.72
pkgrel=1
pkgdesc="Uniform Resource Identifiers (absolute and relative)"
arch=('any')
url="http://search.cpan.org/dist/${_realname}/"
license=('PerlArtistic')
depends=('perl')
checkdepends=('perl-test-needs')
provides=('perl-uri-escape=3.30')
options=('!emptydirs')
source=("http://cpan.metacpan.org/authors/id/E/ET/ETHER/${_realname}-${pkgver}.tar.gz")
sha512sums=('804285c956efcd728569a8ca9c619ddc59d87ca686d1783f697dfee7da4d25721a565515a3e4ad39ff1e4a7239565d0e899ee88db95705804882ce43105f6186')

build() {
  cd "${srcdir}/${_realname}-$pkgver"
  # install module in vendor directories.
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make test
}

package() {
  cd "${srcdir}/${_realname}-$pkgver"
  make install DESTDIR="${pkgdir}"
}
# vim: ts=2 sw=2 et ft=sh
