# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Manolis Tzanidakis
# Contributor: Firmicus <francois.archlinux.org> 

pkgname=perl-uri
pkgver=1.76
pkgrel=2
pkgdesc="Uniform Resource Identifiers (absolute and relative)"
arch=('any')
url="https://search.cpan.org/dist/URI/"
license=('PerlArtistic')
depends=('perl')
checkdepends=('perl-test-needs')
provides=('perl-uri-escape=3.30')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/O/OA/OALDERS/URI-$pkgver.tar.gz")
sha512sums=('1fcb02692bdf5f223c2878569615d153b3c4c5adc04c33aee23f3f015e08b4895b03536ef3a8c7cb7a334c2faa056e5e46c805d56d810704cba63d96136e0a83')

build() {
  cd URI-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd URI-$pkgver
  make test
}

package() {
  cd URI-$pkgver
  make install DESTDIR="$pkgdir"
}
# vim: ts=2 sw=2 et ft=sh
