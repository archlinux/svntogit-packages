# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Justin Davis <jrcd83@gmail.com>

pkgname=perl-guard
pkgver=1.023
pkgrel=14
pkgdesc='safe cleanup blocks'
arch=('x86_64')
url='https://metacpan.org/release/Guard'
license=('GPL' 'PerlArtistic')
makedepends=('perl')
options=('!emptydirs')
source=("https://search.cpan.org/CPAN/authors/id/M/ML/MLEHMANN/Guard-${pkgver}.tar.gz")
sha256sums=('34c4ddf91fc93d1090d86da14df706d175b1610c67372c01e12ce9555d4dd1dc')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor"     \
      PERL_MB_OPT="--installdirs vendor" \
      MODULEBUILDRC=/dev/null

    cd "${srcdir}/Guard-${pkgver}"
    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    cd "${srcdir}/Guard-${pkgver}"
    make test
  )
}

package() {
    cd "${srcdir}/Guard-${pkgver}"
    make DESTDIR="$pkgdir" install
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
