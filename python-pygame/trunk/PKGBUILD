# $Id$
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: john(?)
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=python-pygame
pkgver=1.9.1
pkgrel=3
pkgdesc="Python game library"
arch=('i686' 'x86_64')
url="http://www.pygame.org/"
license=('LGPL')
depends=('python2' 'sdl_mixer' 'libvorbis' 'sdl_ttf' 'sdl_image' 'smpeg')
replaces=('pygame')
provides=('pygame')
changelog=ChangeLog
source=(http://pygame.org/ftp/pygame-${pkgver}release.tar.gz 'config.patch')
md5sums=('1c4cdc708d17c8250a2d78ef997222fc' 'aaa668f3cd710d8604114501ea6a6f48')
sha1sums=('a45aeb0623e36ae7a1707b5f41ee6274f72ca4fa' '285815e28705d5a2aea53c9d952d35fddf10dd13')

build() {
	cd "${srcdir}/pygame-${pkgver}release"
	patch -Np0 -i "${srcdir}/config.patch"
	python2 config.py -auto
}

package() {
	cd "${srcdir}/pygame-${pkgver}release"
	python2 setup.py install --root="${pkgdir}" --prefix=/usr

	# Copying the examples and tests
	cp -R examples lib/* "${pkgdir}/usr/lib/python2.7/site-packages/pygame"
	cp -R test/* "${pkgdir}/usr/lib/python2.7/site-packages/pygame/tests"

	# Fixing permissions
	chmod 644 "${pkgdir}"/usr/include/python2.7/pygame/*
}
