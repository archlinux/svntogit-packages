# $Id$
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: john(?)
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=python-pygame
pkgver=1.9.1
pkgrel=2
pkgdesc="Python game library"
arch=('i686' 'x86_64')
url="http://www.pygame.org/"
license=('LGPL')
depends=('python' 'sdl_mixer' 'libvorbis' 'sdl_ttf' 'sdl_image' 'smpeg')
replaces=('pygame')
provides=('pygame')
source=(http://pygame.org/ftp/pygame-${pkgver}release.tar.gz
	'config.patch')
md5sums=('1c4cdc708d17c8250a2d78ef997222fc'
         'aaa668f3cd710d8604114501ea6a6f48')

build() {
	cd ${srcdir}/pygame-${pkgver}release
	patch -Np0 -i ${srcdir}/config.patch || return 1
	python config.py -auto
}

package() {
	cd ${srcdir}/pygame-${pkgver}release
	python setup.py install --root=${pkgdir} --prefix=/usr

	# Copying the examples and tests
	cp -R examples lib/* ${pkgdir}/usr/lib/python2.6/site-packages/pygame
	cp -R test/* ${pkgdir}/usr/lib/python2.6/site-packages/pygame/tests

	# Fixing permissions
	chmod 644 ${pkgdir}/usr/include/python2.6/pygame/*
}
