# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org> 
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kbreakout
pkgver=17.04.1
pkgrel=1
pkgdesc="A Breakout-like game"
url="https://kde.org/applications/games/kbreakout/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegames')
depends=('libkdegames' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'python' 'kdoctools')
conflicts=('kdegames-kbreakout')
replaces=('kdegames-kbreakout')
source=("https://download.kde.org/stable/applications/${pkgver}/src/kbreakout-${pkgver}.tar.xz"{,.sig})
sha256sums=('30f1a5016235f5d4068f93473be6e101ca3fbd66fcf94b0fb6eadb6a1e6d5b95'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
