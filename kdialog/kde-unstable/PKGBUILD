# Maintainer: Antonio Rojas <arojas@archlinux,org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=kdialog
pkgver=19.03.80
pkgrel=1
pkgdesc="A utility for displaying dialog boxes from shell scripts"
arch=(x86_64)
url="https://www.kde.org/"
license=(LGPL)
depends=(kio)
makedepends=(extra-cmake-modules)
conflicts=(kdebase-kdialog)
replaces=(kdebase-kdialog)
groups=(kde-applications kdebase)
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('c3648332edd91469b1514a2dd96f3da06aff35f054cc006d8e6bf20b3c78cfaf'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
