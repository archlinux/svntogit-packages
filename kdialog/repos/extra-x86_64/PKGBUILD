# Maintainer: Antonio Rojas <arojas@archlinux,org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=kdialog
pkgver=17.04.3
pkgrel=2
pkgdesc="A utility for displaying dialog boxes from shell scripts"
arch=(i686 x86_64)
url="https://www.kde.org/"
license=(LGPL)
depends=(kio)
makedepends=(extra-cmake-modules python)
conflicts=(kdebase-kdialog)
replaces=(kdebase-kdialog)
groups=(kde-applications kdebase)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('b5e02fa434b7ea4d0ec1c97ebec190077a3a72181c9a0ec5befca0e13bc46595'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
