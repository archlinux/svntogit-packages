# $Id$
# Maintainer: Juergen Hoetzel <juergen@archlinux.org>
# Contributor: <kleptophobiac@gmail.com>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=mythtv
pkgver=0.22
pkgrel=4
pkgdesc="A Homebrew PVR project"
arch=('i686' 'x86_64')
url="http://www.mythtv.org/"
license=('GPL2')
groups=('pvr')
depends=('mysql-clients>=5.1.41-2' 'qt' 'lame' 'lirc-utils' 'ffmpeg' 'libxvmc' 'fribidi')
backup=('etc/conf.d/mythbackend')
install=mythtv.install
source=(ftp://ftp.osuosl.org/pub/mythtv/${pkgname}-${pkgver}.tar.bz2
	mythbackend
	archlinux.conf.d.mythbackend)
md5sums=('e8f8b5b6a51cd7be700e215b2a1bf2c0' 'feadcc9ad064d93d6dceab1efc0bd9ed'\
         'bb8e4033d82428d827570fae9ba15e6a')

build() {
   cd $srcdir/${pkgname}-${pkgver} 
   
   ARCH=${CARCH/_/-}
  ./configure --prefix=/usr --cpu=${ARCH} \
    --enable-mmx \
    --enable-audio-oss \
    --enable-audio-alsa \
    --disable-audio-jack \
    --disable-audio-pulse \
    --disable-audio-arts \
    --disable-vdpau \
    --enable-dvb \
    --enable-lirc \
    --enable-joystick-menu \
    --enable-v4l \
    --enable-ivtv \
    --enable-dvb \
    --dvb-path=/usr/include/linux/dvb \
    --enable-xvmc \
    --enable-ffmpeg \
    --enable-xvmc-pro \
    --enable-xvmc-vld \
    --enable-opengl-vsync \
    --enable-firewire \
    --enable-fribidi 

   # build

   qmake mythtv.pro  
   make 

   # basic install
   make INSTALL_ROOT=$pkgdir install 
   
   install -D -m0755 $srcdir/mythbackend $pkgdir/etc/rc.d/mythbackend 

   # config file 
   install -D -m644 $srcdir/archlinux.conf.d.mythbackend $pkgdir/etc/conf.d/mythbackend
}
