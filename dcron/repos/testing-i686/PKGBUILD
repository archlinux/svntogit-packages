# Maintainer: Paul Mattal <paul.archlinux.org>

pkgname=dcron
pkgver=4.5
pkgrel=1
pkgdesc="dillon's lightweight cron daemon"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.jimpryor.net/linux/dcron"
backup=('var/spool/cron/root' 'etc/conf.d/crond')
depends=('bash')
provides=('cron')
conflicts=('cron')
source=("http://www.jimpryor.net/linux/releases/${pkgname}-${pkgver}.tar.gz")
md5sums=('078833f3281f96944fc30392b1888326')
optdepends=('smtp-server: sending cron job output via email')
install=$pkgname.install

build() {
	cd "$srcdir/${pkgname}-${pkgver}"

	# by default, any member of group "users" can edit their own crontab
	make \
		PREFIX=/usr \
		CRONTAB_GROUP=users \
		CRONTABS=/var/spool/cron \
		CRONSTAMPS=/var/spool/cronstamps
}

package() {
	cd "$srcdir/${pkgname}-${pkgver}"
	make DESTDIR="$pkgdir" install

	# install standard configuration and scripts
	install -d -m755 "$pkgdir/etc/cron."{hourly,daily,weekly,monthly} 

	install -D -m755 extra/run-cron "$pkgdir/usr/sbin/run-cron"
	install -D -m0600 extra/root.crontab "$pkgdir/var/spool/cron/root" 
	install -D -m755 extra/crond.rc "$pkgdir/etc/rc.d/crond"
	install -D -m0644 extra/crond.conf "$pkgdir/etc/conf.d/crond"
	install -D -m644 extra/crond.logrotate "$pkgdir/etc/logrotate.d/crond"
}
