# Maintainer: Paul Mattal <paul.archlinux.org>

pkgname=dcron
pkgver=4.4
pkgrel=1
pkgdesc="dillon's lightweight cron daemon"
arch=(i686 x86_64)
license=('GPL')
url="http://www.jimpryor.net/linux/dcron"
backup=(var/spool/cron/root etc/logrotate.d/crond)
depends=('glibc')
provides=('cron')
source=("http://www.jimpryor.net/linux/releases/${pkgname}-${pkgver}.tar.gz")
md5sums=('02d848ba043a9df5bf2102a9f4bc04bd')
optdepends=('smtp-server: sending cron job output via email')

build() {
	cd "$srcdir/${pkgname}-${pkgver}"


	# by default, any member of group "users" can edit their own crontab
	make PREFIX=/usr CRONTAB_GROUP=users CRONTABS=/var/spool/cron CRONSTAMPS=/var/spool/cronstamps || return 1
	make DESTDIR="$pkgdir" install || return 1


	install -d -m755 "$pkgdir/etc/cron."{hourly,daily,weekly,monthly} || return 1
	install -D -m755 extra/crond.rc "$pkgdir/etc/rc.d/crond" || return 1
	install -D -m755 extra/run-cron "$pkgdir/usr/sbin/run-cron" || return 1
	install -D -m0600 extra/root.crontab "$pkgdir/var/spool/cron/root" || return 1
	install -D -m644 extra/crond.logrotate "$pkgdir/etc/logrotate.d/crond" || return 1
	# install -D -m755 extra/prune-cronstamps "$pkgdir/etc/cron.monthly/prune-cronstamps" || return 1

}
