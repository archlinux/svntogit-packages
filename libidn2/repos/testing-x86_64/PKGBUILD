# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Daurnimator <quae@daurnimator.com>

pkgname=libidn2
pkgver=2.0.4
pkgrel=2
pkgdesc="Free software implementation of IDNA2008, Punycode and TR46"
arch=('i686' 'x86_64')
url="https://www.gnu.org/software/libidn/#libidn2"
license=('GPL2' 'LGPL3')
depends=('libunistring')
makedepends=('gtk-doc' 'ruby-ronn')
source=(https://ftp.gnu.org/gnu/libidn/$pkgname-$pkgver.tar.gz{,.sig})
sha256sums=('644b6b03b285fb0ace02d241d59483d98bc462729d8bb3608d5cad5532f3d2f0'
            'SKIP')
validpgpkeys=('1CB27DBC98614B2D5841646D08302DB6A2670428')

prepare() {
  cd $pkgname-$pkgver
  # https://gitlab.com/libidn/libidn2/issues/39
  autoreconf -fvi
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}
package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
