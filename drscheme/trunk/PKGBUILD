# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>

pkgname=drscheme
pkgver=4.2.5
pkgrel=1
pkgdesc="PLT Scheme Environment"
arch=('i686' 'x86_64')
url="http://www.drscheme.org/"
license=('LGPL')
depends=('gcc-libs' 'cairo' 'desktop-file-utils' 'libjpeg' 'mesa' 'openssl' 'libxaw' 'libxmu' 'libxft')
options=('!makeflags')
install=${pkgname}.install
source=(http://download.plt-scheme.org/bundles/${pkgver}/plt/plt-${pkgver}-src-unix.tgz
	drscheme.desktop libpng-1.4.patch)
md5sums=('70ec6e8ffdcb83bb34766685c13fa510'
	'41ba9c74aa541308aa6f67b262387c6c'
	'ec18faef466c1f18047d14a50be0413c')

build() {
  cd "${srcdir}/plt-${pkgver}/src"
  patch -p2 < ../../libpng-1.4.patch || return 1
  ./configure --prefix=/usr || return 1
  make || return 1
}

package() {
  cd "${srcdir}/plt-${pkgver}/src"
  make DESTDIR="${pkgdir}" install || return 1
  install -D -m644 ../collects/icons/plt.xpm "${pkgdir}/usr/share/pixmaps/drscheme.xpm" || return 1
  install -D -m644 "${srcdir}/drscheme.desktop" "${pkgdir}/usr/share/applications/drscheme.desktop" || return 1
}
