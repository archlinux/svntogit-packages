# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-kbreakout
pkgver=4.9.80
pkgrel=1
pkgdesc="A Breakout-like game"
url="http://kde.org/applications/games/kbreakout/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'libkdegames')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/unstable/${pkgver}/src/kbreakout-${pkgver}.tar.xz")
sha1sums=('5b3dac9a9352e2a246c42e36ee25125ddd195a06')

build() {
  mkdir build
  cd build
  cmake ../kbreakout-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
