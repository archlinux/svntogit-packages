# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Link Dupont <link@subpop.net>
#
pkgname=dbus-core
pkgver=1.2.3
pkgrel=1
pkgdesc="Freedesktop.org message bus system"
url="http://www.freedesktop.org/Software/dbus"
arch=(i686 x86_64)
license=('GPL' 'custom')
depends=('expat>=2.0')
options=(!libtool)
install=dbus.install
source=(http://dbus.freedesktop.org/releases/dbus/dbus-${pkgver}.tar.gz
	dbus)
md5sums=('f71641385768e99361bd298568207cee'
         'f3fd196cc278bd7b45a4ca01fb8f7894')

build() {
  cd "${srcdir}/dbus-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
  	      --libexecdir=/usr/lib/dbus-1.0 --with-dbus-user=81 \
              --with-system-pid-file=/var/run/dbus.pid \
	      --enable-inotify --disable-dnotify \
              --disable-verbose-mode --disable-static \
	      --disable-tests --disable-asserts --without-x || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
  chown 81:81 "${pkgdir}/var/run/dbus" || return 1

  install -m755 -d "${pkgdir}/etc/rc.d" || return 1
  install -m755 "${srcdir}/dbus" "${pkgdir}/etc/rc.d/" || return 1

  #Fix configuration file
  sed -i -e 's|<user>81</user>|<user>dbus</user>|' "${pkgdir}/etc/dbus-1/system.conf" || return 1

  #install .keep files so pacman doesn't delete empty dirs
  touch "${pkgdir}/usr/share/dbus-1/services/.keep" || return 1
  touch "${pkgdir}/usr/share/dbus-1/system-services/.keep" || return 1
  touch "${pkgdir}/etc/dbus-1/session.d/.keep" || return 1
  touch "${pkgdir}/etc/dbus-1/system.d/.keep" || return 1

  rmdir "${pkgdir}/usr/lib/dbus-1.0/dbus-1" || return 1

  install -d -m755 "${pkgdir}/usr/share/licenses/dbus"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/dbus/" || return 1
}
