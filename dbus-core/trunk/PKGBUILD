# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Link Dupont <link@subpop.net>
#
pkgname=dbus-core
pkgver=1.6.8
pkgrel=1
pkgdesc="Freedesktop.org message bus system"
url="http://www.freedesktop.org/Software/dbus"
arch=(i686 x86_64)
license=('GPL' 'custom')
depends=('expat' 'coreutils' 'filesystem' 'libsystemd' 'shadow') # shadow for install scriptlet FS#29341
makedepends=('libx11')
options=(!libtool)
install=dbus.install
source=(http://dbus.freedesktop.org/releases/dbus/dbus-$pkgver.tar.gz #{,.asc}
        dbus)
md5sums=('3bf059c7dd5eda5f539a1b7cfe7a14a2'
         'f0364f3f5dc5f653bb05d39aa36e3264')

build() {
  cd dbus-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/usr/lib/dbus-1.0 --with-dbus-user=81 \
      --with-system-pid-file=/run/dbus/pid \
      --with-system-socket=/run/dbus/system_bus_socket \
      --with-console-auth-dir=/run/console/ \
      --enable-inotify --disable-dnotify \
      --disable-verbose-mode --disable-static \
      --disable-tests --disable-asserts \
      --with-systemdsystemunitdir=/usr/lib/systemd/system \
      --enable-systemd
  make
}

package(){
  cd dbus-$pkgver
  make DESTDIR="$pkgdir" install

  rm -f "$pkgdir/usr/bin/dbus-launch"
  rm -f "$pkgdir/usr/share/man/man1/dbus-launch.1"
  rm -rf "$pkgdir/var/run"

  install -m755 -d "$pkgdir/etc/rc.d"
  install -m755 ../dbus "$pkgdir/etc/rc.d/"

  #Fix configuration file
  sed -i -e 's|<user>81</user>|<user>dbus</user>|' "$pkgdir/etc/dbus-1/system.conf"

  install -dm755 "$pkgdir/usr/share/licenses/dbus-core"
  install -m644 COPYING "$pkgdir/usr/share/licenses/dbus-core/"
}
