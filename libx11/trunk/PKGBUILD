# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=libx11
pkgver=1.8.3
pkgrel=3
pkgdesc="X11 client-side library"
arch=(x86_64)
url="https://xorg.freedesktop.org/"
# keep xorgproto runtime dependency
# https://lists.archlinux.org/pipermail/arch-dev-public/2019-December/029767.html
depends=('libxcb' 'xorgproto')
makedepends=('xorg-util-macros' 'xtrans')
license=('custom')
options=('debug')
source=(${url}/releases/individual/lib/libX11-${pkgver}.tar.xz{,.sig}
        176.diff
        177.diff)
sha512sums=('bc862338fed855986659e9ffa641db6b36c3ac9abced590d1b164e3cc24446671936e3688cdca18393129c4ea41777977eeb37e87d8edc14d6cc5d194a9c0325'
            'SKIP'
            '6f5a06e7191b355e5adf48c3391ba66dc2482673b1a485f4c53253c1b800847b9c20c006140b1775644dbd6c77668e588b7df9d6f23aee3e3c9d36b7135c3f8b'
            '6c6c1c98f36bffb4f492e83f3e76fa6fe07757dff52d72a891c389e94c53350ab7bd1e07fcaaa6e1eea69b6c1138e2bdc5e7466a58a3de5799bca705db478ab0')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # Alan Coopersmith <alanc@freedesktop.org>
#validpgpkeys=('C41C985FDCF1E5364576638B687393EE37D128F8') # Matthieu Herrb <matthieu.herrb@laas.fr>
#validpgpkeys=('3BB639E56F861FA2E86505690FDD682D974CA72A') # Matt Turner <mattst88@gmail.com>
#validpgpkeys=('995ED5C8A6138EB0961F18474C09DD83CAAA50B2') # Adam Jackson <ajax@nwnk.net>
#validpgpkeys=('C383B778255613DFDB409D91DB221A6900000011') # "Keith Packard <keithp@keithp.com>"

prepare() {
  cd libX11-${pkgver}
  # https://gitlab.freedesktop.org/xorg/lib/libx11/-/merge_requests/176
  # should fix FS#76669
  patch -Np1 -i ../176.diff
  # https://gitlab.freedesktop.org/xorg/lib/libx11/-/merge_requests/177
  # should fix FS#76860
  patch -Np1 -i ../177.diff
}

build() {
  cd libX11-${pkgver}
  ./configure --prefix=/usr \
    --disable-static \
    --disable-xf86bigfont
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd libX11-${pkgver}
  make check
}

package() {
  cd "${srcdir}/libX11-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
