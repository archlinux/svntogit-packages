# $Id$
# Maintainer: Daniel Isenmann <daniel@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>

pkgname=xsp
pkgver=2.10.2
pkgrel=4
pkgdesc="A simple webserver based on mono - provides ASP.NET support"
arch=(i686 x86_64)
license=('custom')
url="http://www.go-mono.com"
depends=('mono>=2.10.2' 'sqlite')
options=(!makeflags)
install=${pkgname}.install
source=(http://download.mono-project.com/sources/xsp/xsp-2.10.2.tar.bz2
        ${pkgname}.rc.d ${pkgname}.conf.d ${pkgname}.webapp ${pkgname}.service)

build() {
 # get rid of that .wapi errors; thanks to brice
  export MONO_SHARED_DIR=${srcdir}/src/weird
  mkdir -p "${MONO_SHARED_DIR}"
 # import pathes
 # build
  cd ${srcdir}/${pkgname}-${pkgver}
  #sed -i 's|/bin/sh|\0\nexport MONO_PATH=${MONO_PATH}:/opt/mono/lib/mono/1.0|' scripts/script.in
  ./configure --prefix=/usr --sysconfdir=/etc
  make || return 1
 # tweak the xsp shellscript to grab system dll's
  for script in scripts/*2; do
    sed -i 's|/usr/lib/mono/1.0|/usr/lib/mono/2.0|' $script
  done
   #destdir related bugfixes
  sed -i 's|mkdir \$(datadir)|mkdir $(DESTDIR)$(datadir)|' test/2.0/treeview/Makefile
  sed -i 's|gif \$(datadir)|gif $(DESTDIR)$(datadir)|' test/2.0/treeview/Makefile
}

package(){
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir}/ install
 # move test files from share to arch' default html home
  mkdir -p ${pkgdir}/srv/http/html
  mv ${pkgdir}/usr/lib/xsp/test ${pkgdir}/srv/http/html/xsp
  rm -rf ${pkgdir}/usr/share
  chown -R http:http ${pkgdir}/srv/http/html/${pkgname}

 # install a deamon
  install -D -m755 ${srcdir}/${pkgname}.rc.d \
    ${pkgdir}/etc/rc.d/${pkgname}
 # install a deamon configurationfile
  install -D -m644 ${srcdir}/$pkgname.conf.d \
    ${pkgdir}/etc/conf.d/${pkgname}
 # install a xsp configuration home
  install -D -m644 ${srcdir}/${pkgname}.webapp \
    ${pkgdir}/etc/${pkgname}/${pkgname}.webapp
  install -D -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
 # install systemd service file
  install -D -m644 ${srcdir}/${pkgname}.service \
    ${pkgdir}/usr/lib/systemd/system/xsp.service
}
md5sums=('4fe62fc95ad5dc136d8a7f3299d523b6'
         '9575bd7d6f64d51ba05bdd6370665858'
         '35d921df0fefc30f47a438c95d420efc'
         'c917c07f68b945691506c29750db482f'
         '9d83bd36d209f8d36a11dfbc4fa50819')
