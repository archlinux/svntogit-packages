# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=racket
pkgver=5.3.5
pkgrel=1
pkgdesc="A programming language environment (formerly known as PLT Scheme) suitable for tasks ranging from scripting to application development"
arch=('i686' 'x86_64')
url="http://racket-lang.org/"
license=('LGPL')
depends=('gtk2' 'desktop-file-utils')
makedepends=('gsfonts' 'sqlite')
options=('!libtool' '!strip')
install=racket.install
source=(http://download.racket-lang.org/installers/${pkgver}/racket/${pkgname}-${pkgver}-src-unix.tgz \
        drracket.desktop racket-libpng16.patch)
sha1sums=('8a895454a9e0fdf689368fa54ce63ba88d432c5e'
          'a20808f6b250225704856f82a544681a962a299d'
          '5b0260f92fc1ec4dc09d90a5264683bd3b5eed5e')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i "${srcdir}/racket-libpng16.patch"
}

build() {
  cd ${pkgname}-${pkgver}/src
  [ "$CARCH" == "x86_64" ] && export CFLAGS+=" -fPIC"
  ./configure --prefix=/usr --enable-shared
  make
}

package() {
  cd ${pkgname}-${pkgver}/src
  make DESTDIR="${pkgdir}" install

  install -D -m644 ../collects/icons/plt.xpm "${pkgdir}/usr/share/pixmaps/drracket.xpm"
  install -D -m644 "${srcdir}/drracket.desktop" "${pkgdir}/usr/share/applications/drracket.desktop"
}
