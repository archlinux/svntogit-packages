# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=racket
pkgver=5.2
pkgrel=1
pkgdesc="A programming language environment (formerly known as PLT Scheme) suitable for tasks ranging from scripting to application development"
arch=('i686' 'x86_64')
url="http://racket-lang.org/"
license=('LGPL')
depends=('gtk2' 'desktop-file-utils')
makedepends=('gsfonts')
options=('!strip')
install=racket.install
source=(http://download.racket-lang.org/installers/${pkgver}/racket/${pkgname}-${pkgver}-src-unix.tgz \
        drracket.desktop)
sha1sums=('f88007c12d7e5f2773b122d3652c8cd1d15da8c3'
          'a20808f6b250225704856f82a544681a962a299d')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}/src"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/src"
  make DESTDIR="${pkgdir}" install
  install -D -m644 ../collects/icons/plt.xpm "${pkgdir}/usr/share/pixmaps/drracket.xpm"
  install -D -m644 "${srcdir}/drracket.desktop" "${pkgdir}/usr/share/applications/drracket.desktop"
}
