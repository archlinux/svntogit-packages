# $Id$
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: tobias <tobias@archlinux.net>
# Contributor: Robert Emil Berge <robert@rebi.no>

pkgname=jack
_longname=jack-audio-connection-kit
pkgver=0.121.3
pkgrel=7
pkgdesc="A low-latency audio server"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL')
depends=('libsamplerate' 'readline')
makedepends=('doxygen' 'libffado' 'celt')
optdepends=('libffado: FireWire support'
            'celt: NetJACK driver')
url="http://jackaudio.org/"
backup=(etc/security/limits.d/99-audio.conf)
options=('!libtool')
provides=("$_longname=$pkgver")
conflicts=("$_longname")
replaces=("$_longname")
install=$pkgname.install
source=("http://jackaudio.org/downloads/$_longname-$pkgver.tar.gz"
        '99-audio.conf'
        '40-hpet-permissions.rules'
        'ffado_setbuffsize-jack1.patch')
md5sums=('35f470f7422c37b33eb965033f7a42e8'
         'ae65b7c9ebe0fff6c918ba9d97ae342d'
         '471aad533ff56c5d3cbbf65ce32cadef'
         'c1f78ee7847c6d5e471d90626623ffb4')

build() {
  cd "$srcdir/$_longname-$pkgver"

  # backport firewire stuff
  #   - needed for setbuffsize feature in latest stable ffado
  # from https://github.com/jackaudio/jack1/commit/025d3ad
  patch -Np1 -i "$srcdir/ffado_setbuffsize-jack1.patch"

  ./configure --prefix=/usr \
              --libdir=/usr/lib
  make
}

package() {
  cd "$srcdir/$_longname-$pkgver"

  make DESTDIR="$pkgdir" install

  # configure realtime access/scheduling
  # see https://bugs.archlinux.org/task/26343
  install -Dm644 "$srcdir/99-audio.conf" \
    "$pkgdir/etc/security/limits.d/99-audio.conf"

  install -Dm644 "$srcdir/40-hpet-permissions.rules" \
    "$pkgdir/usr/lib/udev/rules.d/40-hpet-permissions.rules"

  # install a missing header forgotten by upstream
  # see https://bugs.archlinux.org/task/26865
  install -Dm644 "$srcdir/$_longname-$pkgver/jack/jslist.h" \
    "$pkgdir/usr/include/jack/jslist.h"
}

# vim:set ts=2 sw=2 et:
