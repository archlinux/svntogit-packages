# $Id$
# Maintainer: tobias <tobias@archlinux.net>
# Contributor: Robert Emil Berge <robert@rebi.no>

pkgname=jack
pkgver=0.118.0
pkgrel=3
pkgdesc="A low-latency audio server"
arch=('i686' 'x86_64')
license=('GPL2' 'custom:LGPL2')
depends=('libsndfile' 'bash' 'libsamplerate')
makedepends=('doxygen')
url="http://jackit.sourceforge.net/"
backup=(etc/conf.d/jack-audio-connection-kit)
options=('!libtool')
provides=("jack-audio-connection-kit=${pkgver}")
replaces=('jack-audio-connection-kit')
source=(http://jackaudio.org/downloads/jack-audio-connection-kit-${pkgver}.tar.gz \
        ${pkgname}.rc.d ${pkgname}.conf.d)
md5sums=('d58e29a55f285d54e75134cec8e02a10' '15a3ecd76b3186eb0fa9c6d16ffbdfef' \
         '7366c406870666e10129456149804122')

build() {
        cd ${srcdir}/jack-audio-connection-kit-${pkgver}
        ./configure --prefix=/usr --libdir=/usr/lib
        make || return 1
}

package() {
        cd ${srcdir}/jack-audio-connection-kit-${pkgver}
        make DESTDIR=${pkgdir} install
        install -Dm755 ../${pkgname}.rc.d ${pkgdir}/etc/rc.d/${pkgname}
        install -Dm644 ../${pkgname}.conf.d ${pkgdir}/etc/conf.d/${pkgname}
        install -Dm644 COPYING.LGPL ${pkgdir}/usr/share/licenses/${pkgname}/COPYING.LGPL
}
