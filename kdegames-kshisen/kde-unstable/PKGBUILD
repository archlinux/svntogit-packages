# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-kshisen
pkgver=4.11.95
pkgrel=1
pkgdesc="A solitaire-like game played using the standard set of Mahjong tiles"
url="http://kde.org/applications/games/kshisen/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'libkdegames' 'libkmahjongg')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/unstable/${pkgver}/src/kshisen-${pkgver}.tar.xz")
sha1sums=('3d2c37c98ef0bdd66af260f4faecd0c268460d48')

build() {
  mkdir build
  cd build
  cmake ../kshisen-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
