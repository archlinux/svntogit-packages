# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=kscreen-frameworks
pkgver=5.2.1
pkgrel=1
pkgdesc="KDE's screen management software"
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/workspace/kscreen'
license=('LGPL')
depends=('kxmlgui' 'libkscreen-frameworks' 'qt5-declarative' 'qt5-graphicaleffects')
makedepends=('extra-cmake-modules' 'git' 'python')
conflicts=('kscreen')
groups=('plasma')
install=$pkgname.install
source=("http://download.kde.org/stable/plasma/${pkgver}/kscreen-$pkgver.tar.xz")
md5sums=('bee9ef8e00b2bbc24a72c4d5cb922fa7')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../kscreen-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
