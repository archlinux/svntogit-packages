# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdebindings-smokekde
pkgver=4.13.2
pkgrel=1
pkgdesc="Language independent library for KDE bindings"
url="https://projects.kde.org/projects/kde/kdebindings/smoke/smokekde"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kdebindings')
depends=('kdebindings-smokeqt')
makedepends=('cmake' 'automoc4' 'kdebindings-smokegen' 'boost'
             'kdepimlibs' 'kdegraphics-okular' 'kdesdk-kate'
             'mesa')
optdepends=('kdesdk-kate: Kate bindings'
            'kdegraphics-okular: Okular bindings'
            'kdepimlibs: akonadi bindings')
conflicts=('kdebindings-smoke')
source=("http://download.kde.org/stable/${pkgver}/src/smokekde-${pkgver}.tar.xz")
sha1sums=('8f1813c8ae808d94de8c1a7476ea38db8ec5931c')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../smokekde-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_Nepomuk=OFF \
    -DWITH_Soprano=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
