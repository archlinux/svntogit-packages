# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=cdparanoia
pkgver=10.2
pkgrel=7
pkgdesc="Compact Disc Digital Audio extraction tool"
arch=('x86_64')
url="http://www.xiph.org/paranoia/"
license=('GPL')
depends=('glibc')
options=('!makeflags' '!staticlibs')
source=(http://downloads.xiph.org/releases/cdparanoia/cdparanoia-III-$pkgver.src.tgz
        gcc.patch)
sha512sums=('4ab0a0f5ef44d56c1af72d1fc1035566a1a89c4eeddb9e8baea675fe51c06138d913342afc8bed167d9fa55672fa25a2763ce21f7e24c1232e4739aff20733a7'
            '4d86fccc0967dd8d568b97a5b5b92dbe830a7227bee3167371ffc8e7d7f0b99aa0ad4116539c3414f2cc4a600c9bc3fcf06b5d8e4d2fe531dd9d6781736bd1a8')

prepare() {
  cd "${srcdir}/${pkgname}-III-${pkgver}"
  patch -p0 -i ${srcdir}/gcc.patch
}

build() {
  cd "${srcdir}/${pkgname}-III-${pkgver}"
  ./configure --prefix=/usr --mandir=/usr/share/man
  make
}

package() {
  cd "${srcdir}/${pkgname}-III-${pkgver}"
  make prefix="${pkgdir}/usr" MANDIR="${pkgdir}/usr/share/man" install
}
