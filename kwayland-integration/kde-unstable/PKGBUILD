# $Id$
# Maintainer:Antonio Rojas <arojas@archlinux.org>

pkgname=kwayland-integration
pkgver=5.3.95
pkgrel=1
pkgdesc='Provides integration plugins for various KDE frameworks for the wayland windowing system'
arch=(i686 x86_64)
url='https://projects.kde.org/projects/kde/workspace/kwayland-integration'
license=(LGPL)
depends=(kwindowsystem kidletime kwayland)
makedepends=(extra-cmake-modules)
groups=(plasma)
source=("http://download.kde.org/unstable/plasma/$pkgver/$pkgname-$pkgver.tar.xz")
md5sums=('e8767792bd4e4541dbf04bd5a63ee4f5')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
