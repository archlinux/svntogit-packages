# $Id$
# Mainainer: SÃ©bastien Luttringer <seblu@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>

pkgname=file
pkgver=5.20
pkgrel=2
pkgdesc='File type identification utility'
arch=('i686' 'x86_64')
license=('custom')
groups=('base' 'base-devel')
url='http://www.darwinsys.com/file/'
depends=('glibc' 'zlib')
source=("ftp://ftp.astron.com/pub/$pkgname/$pkgname-$pkgver.tar.gz"
		'01-cve-2014-3710.patch')
md5sums=('5d5e13eb3e0e13839da869a31790faf2'
         'dde0002f513f44c4ef6c2152f0ece4ed')

prepare() {
  # https://bugs.archlinux.org/task/42759
  patch -p1 -d $pkgname-$pkgver < '01-cve-2014-3710.patch'
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --datadir=/usr/share/file
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
  rmdir "$pkgdir/usr/share/man/man5"
}

# vim:set ts=2 sw=2 et:
