# $Id$
# Maintainer: SÃ©bastien Luttringer <seblu@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>

pkgname=file
pkgver=5.21
pkgrel=2
pkgdesc='File type identification utility'
arch=('i686' 'x86_64')
license=('custom')
groups=('base' 'base-devel')
url='http://www.darwinsys.com/file/'
depends=('glibc' 'zlib')
source=("ftp://ftp.astron.com/pub/$pkgname/$pkgname-$pkgver.tar.gz"
        '01-fix-broken-xz.patch')
md5sums=('549fe96e09041eabece9de2bb28ef923'
         '62c8d81023e1705d8826d79d0a8fdaae')

prepare() {
  # https://bugs.archlinux.org/task/43078
  patch -p1 -d $pkgname-$pkgver < '01-fix-broken-xz.patch'
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --datadir=/usr/share/file
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
  rmdir "$pkgdir/usr/share/man/man5"
}

# vim:set ts=2 sw=2 et:
