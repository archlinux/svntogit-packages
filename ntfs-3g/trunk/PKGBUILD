# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Thomas Bächler <thomas.archlinux.org>

pkgname=ntfs-3g
pkgver=2010.10.2
pkgrel=1
pkgdesc="Stable read and write NTFS driver"
url="http://www.tuxera.com"
arch=('i686' 'x86_64')
license=('GPL2')
depends=('glibc' 'fuse')
makedepends=('pkgconfig')
options=('!libtool')
source=(http://www.tuxera.com/opensource/${pkgname}-${pkgver}.tgz
        25-ntfs-config-write-policy.fdi)
sha1sums=('97531dc0e556596e6bf2169863e9b8368c7d2bd0'
          '200029f2999a2c284fd30ae25734abf6459c3501')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ac_cv_path_LDCONFIG=/bin/true ./configure --prefix=/usr \
    --with-fuse=external --disable-static
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  ln -s /bin/ntfs-3g "${pkgdir}/sbin/mount.ntfs"
  install -m755 -d "${pkgdir}/usr/share/hal/fdi/policy/10osvendor"
  install -m644 "${srcdir}/25-ntfs-config-write-policy.fdi" "${pkgdir}/usr/share/hal/fdi/policy/10osvendor/"
}
