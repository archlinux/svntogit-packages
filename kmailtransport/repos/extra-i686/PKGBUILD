# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kmailtransport
pkgver=15.08.3
pkgrel=1
pkgdesc="Mail Transport Service"
arch=(i686 x86_64)
url="https://projects.kde.org/$pkgname"
license=(LGPL)
depends=(akonadi-mime kcmutils)
makedepends=(extra-cmake-modules python kdoctools boost)
source=("http://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
md5sums=('ad724803f0de08fac85db184cb9d706e')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
