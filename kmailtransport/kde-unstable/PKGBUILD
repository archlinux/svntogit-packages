# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kmailtransport
pkgver=18.11.80
pkgrel=1
pkgdesc="Mail Transport Service"
arch=(x86_64)
url="https://kontact.kde.org"
license=(LGPL)
depends=(akonadi-mime kcmutils ksmtp libkgapi)
makedepends=(extra-cmake-modules boost)
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('7fc453702390d54f0064f5dc5057e6fe858da2c140300086800434bc8f89c0a3'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
