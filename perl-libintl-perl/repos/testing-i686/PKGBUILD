# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: kevin <kevin@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>

pkgname=perl-libintl-perl
_realname=libintl-perl
pkgver=1.24
pkgrel=3
pkgdesc="Perl Module: Localization support"
arch=('i686' 'x86_64')
license=('LGPL')
url="http://search.cpan.org/dist/${_realname}/"
depends=('coreutils' 'gettext' 'perl')
options=('!emptydirs')
source=(http://www.cpan.org/authors/id/G/GU/GUIDO/${_realname}-$pkgver.tar.gz)
md5sums=('66f989c07541e32325b0557b89aead8f')

build() {
  cd "${srcdir}/${_realname}-$pkgver"
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "${srcdir}/${_realname}-$pkgver"
  make test
}

package() {
  cd "${srcdir}/${_realname}-$pkgver"
  make install DESTDIR="${pkgdir}"
# template start; name=perl-binary-module-dependency; version=1;
if [[ $(find "$pkgdir/usr/lib/perl5/" -name "*.so") ]]; then
	_perlver_min=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]);')
	_perlver_max=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]+1);')
	depends+=("perl>=$_perlver_min" "perl<$_perlver_max")
fi
# template end;
}
