# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: kevin <kevin@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>

pkgname=perl-libintl-perl
pkgver=1.33
pkgrel=1
pkgdesc="Perl Module: Localization support"
arch=('x86_64')
license=('LGPL')
url="https://search.cpan.org/dist/libintl-perl"
depends=('coreutils' 'gettext' 'perl')
options=('!emptydirs')
source=("https://www.cpan.org/authors/id/G/GU/GUIDO/libintl-perl-$pkgver.tar.gz")
sha512sums=('494486d2e2f07fa8ee4c4b9d7b2576973772330aab7736488a692c54a6b250b0b7c06892d8cffc164346b9ebd3549141378fc84da339eaa4b34dc16c4f1a59b6')

build() {
  cd libintl-perl-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd libintl-perl-$pkgver
  make test
}

package() {
  cd libintl-perl-$pkgver
  PERL_USE_UNSAFE_INC=1 \
  make install DESTDIR="$pkgdir"
}
