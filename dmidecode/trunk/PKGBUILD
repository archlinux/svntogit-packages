# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=dmidecode
pkgver=3.4
pkgrel=1
pkgdesc="Desktop Management Interface table related utilities"
arch=('x86_64')
url="https://www.nongnu.org/dmidecode"
license=('GPL')
depends=('glibc')
source=("https://download.savannah.nongnu.org/releases/${pkgname}/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha256sums=('43cba851d8467c9979ccdbeab192eb6638c7d3a697eba5ddb779da8837542212'
            'SKIP')
validpgpkeys=('7CA69F4460F1BDC41FD2C858A5526B9BB3CD4E6A') # Jean Delvare (kernel.org) <jdelvare@suse.de>

build() {
  cd ${pkgname}-${pkgver}

  sed -i "s:sbin:bin:g" Makefile
  make prefix=/usr CFLAGS="${CFLAGS}" LDFLAGS="${LDFLAGS}"
}

package() {
  cd ${pkgname}-${pkgver}

  make prefix=/usr DESTDIR="${pkgdir}" install
}
