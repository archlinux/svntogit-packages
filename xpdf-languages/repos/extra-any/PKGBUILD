# $Id$
# Contributor: Tobias Kieslich <tobias@justdreams.de>
# Contributor: Eric Belanger <eric@archlinux.org>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgbase=xpdf-languages
pkgver=20130611
pkgrel=1
pkgdesc='Encoding information to use specific character sets in Xpdf; does not include fonts'
url='http://foolabs.com/xpdf/'
license=('GPL2')
arch=('any')
depends=('xpdf')

install=install

pkgname=('xpdf-arabic'
         'xpdf-chinese-simplified'
         'xpdf-chinese-traditional'
         'xpdf-cyrillic'
         'xpdf-greek'
         'xpdf-hebrew'
         'xpdf-japanese'
         'xpdf-korean'
         'xpdf-latin2'
         'xpdf-thai'
         'xpdf-turkish')

_tars=(${pkgname[@]/%/.tar.gz})
source=(${_tars[@]/#/ftp://ftp.foolabs.com/pub/xpdf/}
        'xpdfrc-chinese-simplified.add'
        'xpdfrc-chinese-traditional.add'
        'xpdfrc-japanese.add'
        'xpdfrc-korean.add'
        'LICENSE.adobe')

sha1sums=('b3d1f6fa2299de0b6ee25ea73967502ea3a98228'
          'd7f0528ab1c21652906c7747e74a27c2f75308dd'
          '17f885acda9e39cedcc46328b9791c930acf8416'
          'c5c55919064e452e712a83c69063479a2de08e80'
          '0e968c80d21352a36fcded0916b8a623af02ec40'
          '2820c90b1f303849ed24aabd9afd8581daba095b'
          'd23a212a0a685d66ea1bf1748b1d7eee4907cc0c'
          '97b9aa4308c9417fd3ca2b79cf251d5280100fa1'
          '508f237d795bb26abf89fb5999998a87555af159'
          'd8bf58d960121508fb823c7bc7475bc0008b670e'
          'aabf97033e074593b3efccfdd92068e41e48c356'
          '6bb0b98d0bae449388395e8d211a5998de7de8d6'
          '87717bed8e5db3b0c6f1b23e9094aebfdffcec75'
          'd0a88b14d6cdf2ee9cfe12d12fedc22e23c1f435'
          '5f12a6fb5d45c9052c86b43e0422335be98c492f'
          '7fcb2e282f5513424178c1f647acc8994c7ee941')

for i in ${pkgname[@]}; do j=${i/xpdf-/}; eval 'package_'${i}'() {
	cd "${srcdir}"
	
	sed \
		-e "s:/usr/local/share/xpdf:/usr/share/xpdf:" \
		-e "/displayCIDFontX.*/d" \
		-i '${i}'/add-to-xpdfrc \

	[[ -e xpdfrc-'${j}'.add ]] && {
		cat xpdfrc-'${j}'.add >> '${i}'/add-to-xpdfrc
		# exactly those packages for which `grep -q Adobe *` matches...
		install -D LICENSE.adobe "${pkgdir}"/usr/share/licenses/'${i}'/LICENSE
		license=(GPL2 custom)
	}

	install -d "${pkgdir}"/usr/share/xpdf
	cp -r '${i}' "${pkgdir}"/usr/share/xpdf/'${j}'
}'; done
