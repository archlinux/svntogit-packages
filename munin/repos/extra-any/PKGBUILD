# $Id $
# Maintainer: Dan McGee <dan@archlinux.org>
# Contributor: Evan LeCompte <evanlec@gmail.com>
pkgname=('munin' 'munin-node')
pkgbase=munin
pkgver=1.4.3
pkgrel=1
pkgdesc="A distributed monitoring/graphing tool"
arch=('any')
url="http://munin.projects.linpro.no/"
license=("GPL")
depends=('perl>=5.10.1' 'perl<5.10.2')
source=(http://downloads.sourceforge.net/sourceforge/munin/munin-$pkgver.tar.gz
        Makefile.config
        munin-cron-entry
        munin-node.init
        logrotate.munin
        logrotate.munin-node
        08-munin-font-dir.conf
        iostat_partition)

build() { 
	cd $srcdir/munin-$pkgver
	cp $srcdir/Makefile.config $srcdir/$pkgname-$pkgver || exit 1
	# multithreading wrecks havoc on the build, should probably report this upstream
	make -j1 PREFIX='' || exit 1
}

package_munin() {
	depends=('perl>=5.10.1' 'perl<5.10.2' 'rrdtool' 'perl-html-template' 'perl-date-manip' 'perl-log-log4perl')
	backup=(etc/munin/munin.conf etc/logrotate.d/munin)
	install=munin.install
	cd $srcdir/munin-$pkgver
	make DESTDIR=$pkgdir install-master-prime || exit 1
	install -D -m644 $srcdir/munin-cron-entry $pkgdir/etc/munin/munin-cron-entry
	install -D -m644 $srcdir/logrotate.munin $pkgdir/etc/logrotate.d/munin
	install -D -m644 $srcdir/08-munin-font-dir.conf $pkgdir/etc/fonts/conf.d/08-munin-font-dir.conf
}

package_munin-node() {
	depends=('perl>=5.10.1' 'perl<5.10.2' 'perl-net-server')
	optdepends=('perl-net-snmp: for SNMP plugins'
	            'perl-net-ssleay: for SSL/TLS support')
	backup=(etc/munin/munin-node.conf etc/logrotate.d/munin-node)
	install=munin-node.install
	cd $srcdir/munin-$pkgver
	make DESTDIR=$pkgdir install-common-prime install-node-prime install-plugins-prime || exit 1
	install -m755 $srcdir/iostat_partition $pkgdir/usr/lib/munin/plugins/
	install -D -m755 $srcdir/munin-node.init $pkgdir/etc/rc.d/munin-node
	install -D -m644 $srcdir/logrotate.munin-node $pkgdir/etc/logrotate.d/munin-node
}

md5sums=('229646facc4e2bca6511b20b3e5eb73f'
         '6ebc31eaa09734394606f7a1e19e38a5'
         'dc9c83aa2a278466fb475364462f4119'
         '9551a2f7c0f0364fd1b7dd010675e47a'
         'db77b53150a906256a71a9f539c7fac2'
         'cdf139f2b6ae36852113f3411caa6e99'
         'e33a45c3b80a83eecabbe5a9920c1eb6'
         '12289ab5d062449eaa14376f927f5d26')
