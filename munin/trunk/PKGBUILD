# $Id $
# Maintainer: Dan McGee <dan@archlinux.org>
# Contributor: Evan LeCompte <evanlec@gmail.com>
pkgname=munin
pkgver=1.2.6
pkgrel=13
pkgdesc="A distributed monitoring/graphing tool"
arch=('i686' 'x86_64')
url="http://munin.projects.linpro.no/"
license=("GPL")
depends=('perl>=5.10.1' 'perl<5.10.2' 'rrdtool' 'perl-html-template' 'perl-date-manip' 'ttf-dejavu')
backup=(etc/munin/munin.conf etc/logrotate.d/munin)
install=munin.install
source=(http://downloads.sourceforge.net/sourceforge/munin/munin_$pkgver.tar.gz
        Makefile.config munin-cron-entry logrotate.munin
        rrdtool-utf8-only.diff)

build() { 
	cd $srcdir/$pkgname-$pkgver
	cp $srcdir/Makefile.config $srcdir/$pkgname-$pkgver || exit 1
	patch -Np3 < $srcdir/rrdtool-utf8-only.diff
	make PREFIX='' || exit 1
}

package() {
	cd $srcdir/$pkgname-$pkgver
	make DESTDIR=$pkgdir install-main install-doc install-man || exit 1
	install -D -m644 $srcdir/munin-cron-entry $pkgdir/etc/munin/munin-cron-entry
	install -D -m644 $srcdir/logrotate.munin $pkgdir/etc/logrotate.d/munin
}

md5sums=('45f84d58f80642ce914f147232f0d396'
         '5f9b83e0c876821cf2b20012f7cb63f0'
         'dc9c83aa2a278466fb475364462f4119'
         'db77b53150a906256a71a9f539c7fac2'
         '36f52b88d6465e198b5cf40e06d30b4d')
