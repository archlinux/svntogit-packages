# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: François Charette <firmicus ατ gmx δοτ net>

pkgname=perl-io-stringy
pkgver=2.111
pkgrel=2
pkgdesc="I/O on in-core objects like strings/arrays"
arch=('any')
url="http://search.cpan.org/dist/IO-stringy"
license=('GPL' 'PerlArtistic')
depends=('perl')
options=('!emptydirs')
source=("http://www.cpan.org/authors/id/D/DS/DSKOLL/IO-stringy-$pkgver.tar.gz") 
md5sums=('e91acf0a800b190d13585a47de775bdd')

build() {
  cd  "${srcdir}/IO-stringy-${pkgver}"
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd  "${srcdir}/IO-stringy-${pkgver}"
  make test
}

package() {
  cd  "${srcdir}/IO-stringy-${pkgver}"
  make install DESTDIR="${pkgdir}"

  find ${pkgdir} -name '.packlist' -delete
  find ${pkgdir} -name '*.pod' -delete
}
