# $Id$
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: tobias <tobias@archlinux.org>
# Contributor: Sarah Hay <sarahhay@mb.sympatico.ca>

pkgname=openbox
pkgver=3.4.11.2
pkgrel=1
pkgdesc="A window manager for the X11 windowing system"
arch=('i686' 'x86_64')
url="http://openbox.org"
license=('GPL')
depends=('startup-notification' 'libxml2' 'libxinerama' 'libxrandr'
	'libxcursor' 'pango')
optdepends=('pyxdg: for the xdg-autostart script')
groups=('lxde')
install=${pkgname}.install
options=('!libtool' 'force')
backup=('etc/xdg/openbox/menu.xml' 'etc/xdg/openbox/rc.xml')
source=(http://www.icculus.org/openbox/releases/${pkgname}-${pkgver}.tar.gz
	'which-2.20.patch')
md5sums=('30e669134fa81df810fe7d1dc59cd931'
         'ec5e3d4d74779b0ca576a053ae54124d')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}

	# FS#11455
	patch -Np1 -i ${srcdir}/which-2.20.patch || return 1

	./configure --prefix=/usr \
	    --with-x \
	    --enable-startup-notification \
	    --sysconfdir=/etc
	make || return 1
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}

	make DESTDIR=${pkgdir} install

	sed -i "s:startkde:/usr/bin/\0:" \
	    ${pkgdir}/usr/share/xsessions/openbox-kde.desktop
	sed -i "s:=gnome-session:=/usr/bin/gnome-session:" \
	    ${pkgdir}/usr/share/xsessions/openbox-gnome.desktop
}
