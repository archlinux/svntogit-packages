# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: tobias <tobias@archlinux.org>
# Contributor: Sarah Hay <sarahhay@mb.sympatico.ca>

pkgname=openbox
pkgver=3.4.7.2
pkgrel=4
pkgdesc="A window manager for the X11 windowing system"
arch=(i686 x86_64)
license=('GPL')
depends=('startup-notification' 'libxml2' 'libxinerama' 'libxrandr' \
         'libxcursor' 'pango')
optdepends=('pyxdg: for the xdg-autostart script')
url="http://icculus.org/openbox"
groups=('lxde')
install=openbox.install
options=('!libtool')
backup=('usr/etc/xdg/openbox/menu.xml' 'usr/etc/xdg/openbox/rc.xml')
source=(http://www.icculus.org/openbox/releases/${pkgname}-${pkgver}.tar.gz
	'which-2.20.patch'
	'fix-workspaces-number-in-gdm.patch')
md5sums=('9e7589e90519bc6ac2f4656ea6869439'
         'b72cac492e0ed83fd5ced9c1263cfa24'
	 '17f354e7565bac87697a8dad827abc0b')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  #FS#11455
  patch -Np1 -i ${srcdir}/which-2.20.patch || return 1
  #FS#16676
  patch -Np1 -i ${srcdir}/fix-workspaces-number-in-gdm.patch || return 1

  ./configure --prefix=/usr \
    --with-x \
    --enable-startup-notification \
    --sysconfdir=/etc
  make || return 1
  make DESTDIR=${pkgdir} install

  sed -i "s:startkde:/usr/bin/\0:" ${startdir}/pkg/usr/share/xsessions/openbox-kde.desktop
  sed -i "s:=gnome-session:=/usr/bin/gnome-session:" ${startdir}/pkg/usr/share/xsessions/openbox-gnome.desktop
}
