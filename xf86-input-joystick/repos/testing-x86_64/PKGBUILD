# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xf86-input-joystick
pkgver=1.6.1
pkgrel=2
pkgdesc="X.Org Joystick input driver"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('glibc')
makedepends=('xorg-server-devel' 'X-ABI-XINPUT_VERSION=18' 'resourceproto' 'scrnsaverproto')
conflicts=('xorg-server<1.13.0' 'X-ABI-XINPUT_VERSION<18' 'X-ABI-XINPUT_VERSION>=19')
groups=('xorg-drivers' 'xorg')
options=('!libtool')
source=(${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2
        #git-fixes.patch
        50-joystick.conf)
sha1sums=('0712652ec6cdb2fca6ad888548b232a4de9cf580'
          'e1ff3699a0470c6bb78a53f718df9d8521621e11')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  #patch -Np1 -i "${srcdir}/git-fixes.patch"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/etc/X11/xorg.conf.d"
  install -m644 "${srcdir}/50-joystick.conf" "${pkgdir}/etc/X11/xorg.conf.d/"

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
