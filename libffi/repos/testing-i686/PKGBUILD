# $Id$
# Maintainer:  Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Stéphane Gaudreault <stephane@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=libffi
pkgver=3.1
pkgrel=1
pkgdesc='Portable foreign function interface library'
arch=('i686' 'x86_64')
url='http://sourceware.org/libffi/'
license=('MIT')
depends=('glibc')
checkdepends=('dejagnu')
install=libffi.install
source=(ftp://sourceware.org/pub/libffi/libffi-$pkgver.tar.gz
        0001-Fix-paths-in-libffi.pc.in.patch)
sha1sums=('cb373ef2115ec7c57913b84ca72eee14b10ccdc3'
          '85b406c5208a7b8fdba9c8a4782ab524f5c5eec4')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../0001-Fix-paths-in-libffi.pc.in.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-static
  make
}

check() {
  make -C $pkgname-$pkgver check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
