# $Id$
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=libffi
pkgver=3.0.13
pkgrel=4
pkgdesc="Portable, high level programming interface to various calling conventions"
arch=('i686' 'x86_64')
url="http://sourceware.org/libffi/"
license=('MIT')
depends=('glibc')
checkdepends=('dejagnu')
options=('!libtool')
install=libffi.install
source=(ftp://sourceware.org/pub/libffi/libffi-${pkgver}.tar.gz)
sha1sums=('f5230890dc0be42fb5c58fbf793da253155de106')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-static
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
