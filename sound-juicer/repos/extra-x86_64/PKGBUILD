# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributer: Ben <contrasutra@myrealbox.com>

pkgname=sound-juicer
pkgver=2.32.1
pkgrel=1
pkgdesc="A cd ripper application"
arch=(i686 x86_64)
license=('GPL')
depends=('libmusicbrainz3' 'libgnome-media-profiles' 'gstreamer0.10-base-plugins' 'gstreamer0.10-good-plugins' 'brasero' 'hicolor-icon-theme')
makedepends=('intltool' 'gnome-doc-utils>=0.20.0' 'pkgconfig' 'gnome-common' 'gtk-doc')
options=(!emptydirs)
#source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.32/${pkgname}-${pkgver}.tar.bz2)
source=(ftp://ftp.archlinux.org/other/${pkgname}/${pkgname}-${pkgver}-20110501.tar.xz)
groups=('gnome-extra')
install=sound-juicer.install
url="http://www.burtonini.com/blog/computers/sound-juicer"
sha256sums=('e4312ebf75c8798dd3f7a9d2d9ee7291b593bd68c7b0559742b91ca7a52674fe')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var \
      --disable-scrollkeeper
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain sound-juicer ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
