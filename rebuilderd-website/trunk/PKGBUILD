# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=rebuilderd-website
pkgver=0.3.1
pkgrel=1
pkgdesc="Arch Linux Rebuilderd website reproducible.archlinux.org"
license=('MIT')
arch=(any)
depends=()
makedepends=('yarn' 'ruby-sass' 'make' 'git')
url="https://gitlab.archlinux.org/archlinux/rebuilderd-website"
validpgpkeys=('E499C79F53C96A54E572FEE1C06086337C50773E')
source=("git+https://gitlab.archlinux.org/archlinux/rebuilderd-website.git#tag=v${pkgver}?signed")
sha512sums=('SKIP')

build() {
  cd ${pkgname}
  make dist
}

package() {
  cd ${pkgname}
  install -d ${pkgdir}/usr/share/webapps/${pkgname}

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # Install web files
  install -Dm644 dist/${pkgname}-${pkgver}/index.html "${pkgdir}/usr/share/webapps/${pkgname}/index.html"
  install -Dm644 dist/${pkgname}-${pkgver}/favicon.ico "${pkgdir}/usr/share/webapps/${pkgname}/favicon.ico"
  install -Dm644 dist/${pkgname}-${pkgver}/bundle.css "${pkgdir}/usr/share/webapps/${pkgname}/bundle.css"
  install -Dm644 dist/${pkgname}-${pkgver}/bundle.css.map "${pkgdir}/usr/share/webapps/${pkgname}/bundle.css.map"
  install -Dm644 dist/${pkgname}-${pkgver}/bundle.js "${pkgdir}/usr/share/webapps/${pkgname}/bundle.js"
}
