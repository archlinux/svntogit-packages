# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gstreamer-vaapi
pkgver=0.5.10
pkgrel=1
pkgdesc="GStreamer Multimedia Framework VA Plugins"
arch=(i686 x86_64)
license=(LGPL)
url="http://www.freedesktop.org/software/vaapi/releases/gstreamer-vaapi/"
depends=(gst-plugins-base libva libxrandr libvpx)
provides=("gst-vaapi=$pkgver")
conflicts=(gst-vaapi)
replaces=(gst-vaapi)
source=($url/$pkgname-$pkgver.tar.bz2)
sha1sums=('456d8e8bc9f76e8f16318172d2b61c18e61c7da0')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-static --disable-builtin-libvpx
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd $pkgname-$pkgver
  make -k check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
