# $Id$
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgbase=pyatspi
pkgname=(python-atspi python2-atspi)
pkgver=2.26.0
pkgrel=2
arch=(any)
url="https://wiki.linuxfoundation.org/en/AT-SPI_on_D-Bus"
license=(GPL2)
makedepends=(python-gobject python2-gobject at-spi2-core gnome-common git)
_commit=813ba139a46c071103efd45ec65481213974f07f  # tags/PYATSPI_2_26_0^0
source=("git+https://git.gnome.org/browse/pyatspi2#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd pyatspi2
  git describe --tags | sed 's/^PYATSPI_//;s/_/./g;s/-/+/g'
}

prepare() {
  mkdir python2 python3
  cd pyatspi2
  NOCONFIGURE=1 ./autogen.sh
}

_build() (
  cd python$1
  ../pyatspi2/configure --prefix=/usr --with-python=/usr/bin/python$1
  make
)

build() {
  _build 2
  _build 3
}

package_python-atspi() {
  pkgdesc="Python 3 bindings for at-spi"
  depends=(python-gobject at-spi2-core)

  cd python3
  make DESTDIR="$pkgdir" install
}

package_python2-atspi() {
  pkgdesc="Python 2 bindings for at-spi"
  depends=(python2-gobject at-spi2-core)

  cd python2
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
