# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgbase=pyatspi
pkgname=(python-atspi)
pkgver=2.32.0
pkgrel=1
pkgdesc="Python bindings for D-Bus AT-SPI"
url="https://wiki.linuxfoundation.org/accessibility/atk/at-spi/at-spi_on_d-bus"
arch=(any)
license=(GPL2)
depends=(python-gobject at-spi2-core)
makedepends=(git)
_commit=1a4186acd5e03a40bc8e920de4e0f6cca92c8ec8  # tags/PYATSPI_2_32_0^0
source=("git+https://gitlab.gnome.org/GNOME/pyatspi2.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd pyatspi2
  git describe --tags | sed 's/^PYATSPI_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd pyatspi2
  NOCONFIGURE=1 ./autogen.sh
}

build() (
  cd pyatspi2
  ./configure --prefix=/usr --with-python=/usr/bin/python
  make
)

package_python-atspi() {
  cd pyatspi2
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
