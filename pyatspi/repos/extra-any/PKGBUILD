# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
pkgname=pyatspi
pkgver=2.2.1
pkgrel=1
pkgdesc="Python bindings for at-spi"
arch=(any)
url="http://www.linuxfoundation.org/en/AT-SPI_on_D-Bus"
license=('GPL2')
depends=('python2-gobject' 'at-spi2-core')
source=(http://download.gnome.org/sources/${pkgname}/2.2/${pkgname}-${pkgver}.tar.xz)
sha256sums=('814afe09407d2197871ba5c202362421ed5bae80d11b2d110ca91510f69db37a')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed '/AC_PATH_XTRA/d' -i configure.ac
  autoreconf
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
