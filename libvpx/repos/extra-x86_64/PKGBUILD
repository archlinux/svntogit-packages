# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=libvpx
pkgver=1.3.0
pkgrel=1
pkgdesc='The VP8 Codec SDK'
arch=('i686' 'x86_64')
url='http://www.webmproject.org/'
license=('BSD')
depends=('glibc')
makedepends=('yasm' 'git')
source=($pkgname::git+http://code.google.com/p/webm.libvpx#tag=v$pkgver)
md5sums=('SKIP')

build() {
    cd $pkgname
    ./configure --prefix=/usr \
        --enable-vp8 \
        --enable-vp9 \
        --enable-runtime-cpu-detect \
        --enable-shared \
        --enable-postproc \
        --enable-pic \
        --disable-install-docs \
        --disable-install-srcs
    make
}

package() {
    cd $pkgname
    make DIST_DIR="$pkgdir/usr" install
    install -Dm0644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
