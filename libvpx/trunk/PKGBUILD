# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

pkgname=libvpx
pkgver=1.5.0
pkgrel=4
pkgdesc='VP8 and VP9 codec'
arch=('i686' 'x86_64')
url='http://www.webmproject.org/'
license=('BSD')
depends=('glibc' 'gcc-libs')
makedepends=('yasm' 'git')
provides=('libvpx.so')
source=($pkgname-$pkgver.tar.gz::https://github.com/webmproject/libvpx/archive/v$pkgver.tar.gz
        0001-Fix-for-issue-1114-compile-error.patch)
md5sums=('0c662bc7525afe281badb3175140d35c'
         'a03e318d5d92270ed826b55d6d9dc828')

prepare() {
  cd $pkgname-$pkgver

  # https://bugs.chromium.org/p/webm/issues/detail?id=1114
  patch -Np1 -i ../0001-Fix-for-issue-1114-compile-error.patch
}

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr \
    --enable-vp8 \
    --enable-vp9 \
    --enable-vp9-highbitdepth \
    --enable-runtime-cpu-detect \
    --enable-shared \
    --enable-postproc \
    --enable-pic \
    --enable-experimental --enable-spatial-svc \
    --disable-install-docs \
    --disable-install-srcs
  make
}

package() {
  cd $pkgname-$pkgver
  make DIST_DIR="$pkgdir/usr" install
  install -Dm0644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
