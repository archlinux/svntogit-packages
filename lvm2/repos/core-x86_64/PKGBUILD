# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>

pkgbase=lvm2
pkgname=('lvm2' 'device-mapper')
pkgver=2.02.66
_pkgverdm=1.02.45
_pkgverlvm=${pkgver}
pkgrel=1
arch=('i686' 'x86_64')
url="http://sourceware.org/lvm2/"
license=('GPL2' 'LGPL2.1')
groups=('base')
conflicts=('mkinitcpio<0.5.99')
source=(ftp://sources.redhat.com/pub/lvm2/LVM2.${_pkgverlvm}.tgz
        lvm2_install
        lvm2_hook)
md5sums=('59766571610a4298f8bedf9f73839050'
         'f781c9f5dd4f9934952c687b73d26a18'
         '95bed25bdbc2c2d8500e7a07aeff7f65')
sha1sums=('f1c99d93813e124b0653bb3866fb6494257b3fea'
          '583424bb0a6f89ffe4e4ea446b912f0fedac7050'
          '0dc0c0d620f634c4d078afdb78e3bae15ccf957b')
                                          
build() {
  cd "${srcdir}/LVM2.${_pkgverlvm}"
  unset LDFLAGS
  ./configure --prefix= --sysconfdir=/etc --localstatedir=/var --datarootdir=/usr/share \
    --includedir=/usr/include --with-usrlibdir=/usr/lib \
    --enable-pkgconfig --enable-readline --enable-dmeventd --enable-cmdlib --enable-applib \
    --with-udevdir=/lib/udev/rules.d/ --enable-udev_sync --enable-udev_rules || return 1
  make || return 1
}

package_device-mapper() {
  pkgdesc="Device mapper userspace library and tools"
  url="http://sourceware.org/dm/"
  depends=('glibc' 'udev')

  cd "${srcdir}/LVM2.${_pkgverlvm}"
  make DESTDIR="${pkgdir}" install_device-mapper || return 1
}

package_lvm2() {
  pkgdesc="Logical Volume Manager 2 utilities"
  depends=('bash' "device-mapper>=${pkgver}" 'udev' 'readline')
  conflicts=('lvm' 'mkinitcpio<0.5.99')
  backup=('etc/lvm/lvm.conf')
  options=('!makeflags')

  cd "${srcdir}/LVM2.${_pkgverlvm}"
  make DESTDIR="${pkgdir}" install_lvm2 || return 1
  # install applib
  cd liblvm
  make DESTDIR="${pkgdir}" install || return 1
  cd ..
  # /etc directories
  install -d "${pkgdir}"/etc/lvm/{archive,backup} || return 1
  # mkinitcpio hook
  install -D -m644 "${srcdir}/lvm2_hook" "${pkgdir}/lib/initcpio/hooks/lvm2" || return 1
  install -D -m644 "${srcdir}/lvm2_install" "${pkgdir}/lib/initcpio/install/lvm2" || return 1
}
