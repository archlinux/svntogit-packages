# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>

pkgname=perl-netaddr-ip
pkgver=4.073
pkgrel=2
pkgdesc="Perl module to manage IP addresses and subnets"
arch=('i686' 'x86_64')
url="http://search.cpan.org/dist/NetAddr-IP/"
license=('PerlArtistic' 'GPL')
depends=('perl')
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/M/MI/MIKER/NetAddr-IP-${pkgver}.tar.gz)
md5sums=('4dc78e95809450100c1037cc10942fdf')

build() {
  cd NetAddr-IP-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd NetAddr-IP-${pkgver}
  make test
}

package() {
  cd NetAddr-IP-${pkgver}
  make DESTDIR="${pkgdir}" install
}
