# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=tomboy
pkgver=1.6.1
pkgrel=1
pkgdesc="Desktop note-taking application for Linux and Unix"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnome.org/projects/tomboy/"
depends=('gtk2' 'gtkspell' 'ndesk-dbus-glib' 'mono-addins' 'gnome-sharp')
makedepends=('intltool' 'pkgconfig'  'gnome-doc-utils')
options=('!libtool' '!emptydirs' '!makeflags')
groups=('gnome-extra')
install=tomboy.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/1.6/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('4c0b5e0103682711e1cd620c9c617716a33b3520d62c22a2f6ab7bab2a54e61c')

build() {
  export MONO_SHARED_DIR="${srcdir}/.wabi"
  mkdir -p "${MONO_SHARED_DIR}"

  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-scrollkeeper \
      --disable-update-mimedb
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 install

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain tomboy ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
