# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=vsftpd
pkgver=2.3.5
pkgrel=1
pkgdesc="Very Secure FTP daemon"
arch=('i686' 'x86_64')
url="https://security.appspot.com/vsftpd.html"
license=('GPL2')
depends=('openssl')
backup=('etc/vsftpd.conf' 'etc/xinetd.d/vsftpd')
install=vsftpd.install
source=(https://security.appspot.com/downloads/${pkgname}-${pkgver}.tar.gz{,.asc}
        'vsftpd.xinetd' 'vsftpd.d')
sha1sums=('f15b39ba6d68c953ab3c3e613e6ddc2a26493755'
          '2ddf92782f19dafc72439a2917acb1d7e124d149'
          'c87b4ce56dac15223694a6e86c01ea813b877596'
          '24f268956c24e78be0c234c6d31f41487922eafe')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # build-time config
  sed \
    -e 's|^#undef VSF_BUILD_SSL$|#define VSF_BUILD_SSL|' \
    -i builddefs.h
  make CFLAGS="${CFLAGS}"
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  install -D -m755 vsftpd "${pkgdir}/usr/sbin/vsftpd"
  install -D -m644 vsftpd.conf "${pkgdir}/etc/vsftpd.conf"
  install -D -m644 vsftpd.8 "${pkgdir}/usr/share/man/man8/vsftpd.8"
  install -D -m644 vsftpd.conf.5 "${pkgdir}/usr/share/man/man5/vsftpd.conf.5"
  install -D -m644 "${srcdir}/vsftpd.xinetd" "${pkgdir}/etc/xinetd.d/vsftpd"
  install -D -m755 "${srcdir}/vsftpd.d" "${pkgdir}/etc/rc.d/vsftpd"

  install -d -m755 "${pkgdir}/usr/share/empty"
}
