# Maintainer: Florian Pritz <bluewind@xinu.at>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=perl-crypt-openssl-rsa
pkgver=0.32
pkgrel=1
pkgdesc='RSA encoding and decoding, using the openSSL libraries'
arch=(x86_64)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=('perl>=5.6' perl-crypt-openssl-random)
makedepends=('perl-crypt-openssl-guess>=0.11')
url='https://metacpan.org/release/Crypt-OpenSSL-RSA'
source=("https://search.cpan.org/CPAN/authors/id/T/TO/TODDR/Crypt-OpenSSL-RSA-$pkgver.tar.gz")
md5sums=('41ee1df9ce505ffb85ce26a18c494841')
sha512sums=('2972611a8fa4e9c44fd09c9e7fa32f0abd377230f14dcdfa99edab87bba2199c6a0cab4d1e08bdf3057959c0efd3343eb989e46f4befd572b15540e59f1e3c64')
_ddir="Crypt-OpenSSL-RSA-$pkgver"

build()
(
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  /usr/bin/perl Makefile.PL
  make
)

check()
(
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
)

package()
(
  cd "$srcdir/$_ddir"
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
)

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
