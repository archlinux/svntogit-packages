# $Id: PKGBUILD 62098 2010-01-02 09:13:44Z tpowa $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=libindi
pkgver=0.8
pkgrel=1
pkgdesc="A distributed control protocol designed to operate astronomical instrumentation"
url="http://www.indilib.org/index.php?title=Main_Page"
license=('GPL2')
arch=('i686' 'x86_64')
depends=('libnova' 'cfitsio' 'boost-libs')
makedepends=('pkgconfig' 'cmake' 'boost')
provides=('indilib')
replaces=('indilib')
conflicts=('indilib')
options=('!libtool')
source=("http://downloads.sourceforge.net/indi/${pkgname}_${pkgver}.tar.gz")
md5sums=('ca2b7c56431eb5e08218929e5eb72150')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
