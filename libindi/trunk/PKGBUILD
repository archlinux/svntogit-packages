# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=libindi
pkgver=0.9.7
pkgrel=1
pkgdesc="A distributed control protocol designed to operate astronomical instrumentation"
url="http://www.indilib.org/index.php?title=Main_Page"
license=('GPL2')
arch=('i686' 'x86_64')
depends=('libnova' 'cfitsio' 'boost-libs' 'libusb-compat')
makedepends=('cmake' 'boost')
options=('staticlibs') # libindiclient builds as static only
source=("http://downloads.sourceforge.net/indi/${pkgname}_${pkgver}.tar.gz")
md5sums=('3e457c4226d7a445a0d89c044cced6b7')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install

  install -d "${pkgdir}"/usr/lib/udev/rules.d
  mv "${pkgdir}"/lib/udev/rules.d/99-gpusb.rules \
    "${pkgdir}"/usr/lib/udev/rules.d/
  rmdir "${pkgdir}"/lib/udev/rules.d "${pkgdir}"/lib/udev "${pkgdir}"/lib
}
