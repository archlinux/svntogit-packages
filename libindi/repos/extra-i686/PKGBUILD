# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=libindi
pkgver=1.4.1
pkgrel=4
pkgdesc="A distributed control protocol designed to operate astronomical instrumentation"
url="http://www.indilib.org/index.php?title=Main_Page"
license=(GPL2)
arch=(i686 x86_64)
depends=(libnova cfitsio libusb libjpeg gsl curl)
makedepends=(cmake boost qt5-base)
source=("https://github.com/indilib/indi/releases/download/v$pkgver/libindi_$pkgver.tar.gz")
sha256sums=('f393faa01d98ead9a4205b567402d37fc48a1e53896ed0caeb0e0567bd3ba2e6')

prepare() {
  mkdir -p build

  sed -e 's|GROUP="plugdev", MODE="0666"|TAG+="uaccess"|' -i libindi/drivers/*/*.rules
}

build() {
  cd build
  cmake ../libindi \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DUDEVRULES_INSTALL_DIR=/usr/lib/udev/rules.d
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
