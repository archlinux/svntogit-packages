# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=libindi
pkgver=1.1.0
pkgrel=2
pkgdesc="A distributed control protocol designed to operate astronomical instrumentation"
url="http://www.indilib.org/index.php?title=Main_Page"
license=(GPL2)
arch=(i686 x86_64)
depends=(libnova cfitsio boost-libs libusb libjpeg gsl curl)
makedepends=(cmake boost)
source=("http://www.indilib.org/jdownloads/Source/${pkgname}_$pkgver.tar.gz")
md5sums=('0002e231177e382e1aacd6369044db22')

prepare() {
  mkdir -p build

  sed -e 's|GROUP="plugdev", MODE="0666"|TAG+="uaccess"|' -i ${pkgname}_$pkgver/drivers/focuser/99-perfectstar.rules
}

build() {
  cd build
  cmake ../${pkgname}_$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DUDEVRULES_INSTALL_DIR=/usr/lib/udev/rules.d
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
