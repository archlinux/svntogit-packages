# $Id$
# Maintainer: juergen <juergen@archlinux.org>

pkgname=xemacs
pkgver=21.5.28
pkgrel=5
pkgdesc="highly customizable open source text editor and application development system forked from GNU Emacs"
arch=(i686 x86_64)
makedepends=('libungif' 'xbitmaps')
depends=('db>=4.7' 'libpng' 'libtiff' 'gpm>=1.20.4' 'bash' 'xaw3d')
source=(http://ftp.xemacs.org/pub/xemacs/beta/$pkgname-$pkgver.tar.gz xemacs.desktop)
md5sums=('12e35715c5239c63651a8189973527ab' 'a4d3d5c0aa2c7ce7bec491f809ca3694')
url="http://www.xemacs.org/"
license=('GPL')
        
build() {
  cd $startdir/src/${pkgname}-${pkgver}
  infodir=/usr/lib/xemacs-21.5-b28/info
  mandir=/usr/share/man/man1
  ./configure --build=$CHOST --prefix=/usr --with-dynamic \
      --with-x11 --without-postgresql --with-athena=3d \
      --enable-database=berkdb --without-ldap \
      --enable-menubars=lucid --enable-scrollbars=lucid \
      --enable-widgets=athena --enable-dialogs=athena \
      --enable-external-widget \
      --with-jpeg --with-png --with-tiff \
      --with-ncurses --with-pop --with-xfs --disable-sound \
      --infodir=$infodir \
      --mandir=$mandir || return 1

  make || return 1
#  make autoloads
  
  make prefix="$startdir/pkg/usr" mandir="$startdir/pkg$mandir" infodir="$startdir/pkg$infodir" install gzip-el

  rm $startdir/pkg/usr/bin/{b2m,ctags,etags,rcs-checkin}
  rm $startdir/pkg/usr/share/man/man1/{ctags.1,etags.1}

  # fix  FS#7927
  mkdir -p $startdir/pkg/usr/share/pixmaps ${startdir}/pkg/usr/share/applications
  install -D -m644 ${startdir}/src/${pkgname}.desktop   ${startdir}/pkg/usr/share/applications
  ln -s   ../../lib/xemacs-21.5-b28/etc/xemacs-icon.xpm $startdir/pkg/usr/share/pixmaps/

  # correct permissions
  chown -R root.root $startdir/pkg
}
# vim: ts=2 sw=2 et ft=sh
