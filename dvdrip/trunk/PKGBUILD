# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Fredrik Hammar <Horney_C86@Hotmail.com>

pkgname=dvdrip
pkgver=0.98.11
pkgrel=2
pkgdesc="A Gtk frontend for transcode writen in Perl"
arch=('i686' 'x86_64')
license=('custom')
url="http://www.exit1.org/dvdrip/"
install=${pkgname}.install
depends=('perl-gtk2-ex-formfactory' 'transcode' 'imagemagick'
         'perl-libintl-perl' 'desktop-file-utils' 'perl-event-execflow')
optdepends=('ogmtools: for ogm/odd video creation')
source=(http://www.exit1.org/dvdrip/dist/${pkgname}-${pkgver}.tar.gz \
        ${pkgname}.desktop)
options=(!emptydirs)
md5sums=('6dfa4199d451757a37eea233a07da4c0'
         '9c5b347c2e0ae847d0e7abb3cf54bad3')

build() {
  export MAKEFLAGS="-j1"
  cd "${srcdir}/${pkgname}-${pkgver}"
  perl Makefile.PL INSTALLSITESCRIPT=/usr/bin INSTALLSITELIB=/usr/lib/perl5/site_perl/current 
  make 
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make install DESTDIR="${pkgdir}" 

  # remove perllocal.pod and .packlist
  find ${pkgdir} -name perllocal.pod -delete
  find ${pkgdir} -name .packlist -delete

  install -Dm644 ${srcdir}/${pkgname}.desktop "${pkgdir}/usr/share/applications/${pkgname}.desktop" 
  install -Dm 644 COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE" 
}
