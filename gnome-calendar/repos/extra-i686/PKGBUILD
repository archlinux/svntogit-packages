# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-calendar
pkgver=3.20.2
pkgrel=2
pkgdesc="Simple and beautiful calendar application designed to perfectly fit the GNOME desktop"
url="https://wiki.gnome.org/Apps/Calendar"
arch=(i686 x86_64)
license=(GPL)
depends=(evolution-data-server 'gsettings-desktop-schemas>=3.21.2')
makedepends=(intltool python gnome-common)
groups=(gnome-extra)
source=(http://download.gnome.org/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz
        year-view-use-gsettings-desktop-schemas.patch)
sha256sums=('f132cff56310b83cf086628e949685b04cdaf872e989d67dbb8a3e4e9943deee'
            '668a698fc5e6902f3796b70a418c6c88ef49ab1e1c11bb9e2f4f16d344ee8f77')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../year-view-use-gsettings-desktop-schemas.patch
}

build() {
  cd $pkgname-$pkgver
  autoreconf -fi
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
