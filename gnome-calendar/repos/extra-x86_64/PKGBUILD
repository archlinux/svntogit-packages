# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-calendar
pkgver=3.26.3
pkgrel=1
pkgdesc="Simple and beautiful calendar application designed to perfectly fit the GNOME desktop"
url="https://wiki.gnome.org/Apps/Calendar"
arch=(x86_64)
license=(GPL)
depends=(evolution-data-server gsettings-desktop-schemas evolution gnome-control-center)
makedepends=(python appstream-glib git meson gtk-doc)
groups=(gnome)
_commit=7e88d7c14493d2656edf626fef7ab6162806d607  # tags/3.26.3^0
source=("git+https://git.gnome.org/browse/gnome-calendar#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build -Denable-gtk-doc=true
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}

# vim:set ts=2 sw=2 et:
