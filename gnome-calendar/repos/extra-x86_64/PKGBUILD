# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-calendar
pkgver=3.24.2
pkgrel=1
pkgdesc="Simple and beautiful calendar application designed to perfectly fit the GNOME desktop"
url="https://wiki.gnome.org/Apps/Calendar"
arch=(i686 x86_64)
license=(GPL)
depends=(evolution-data-server 'gsettings-desktop-schemas>=3.21.2')
makedepends=(intltool python gnome-common appstream-glib git)
groups=(gnome-extra)
_commit=d42ce8c5e7c92d0d715663177cb3e97faf24e143  # tags/3.24.2^0
source=("git+https://git.gnome.org/browse/gnome-calendar#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
