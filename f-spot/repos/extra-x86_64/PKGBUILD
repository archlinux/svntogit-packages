# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Kritoke <kritoke@nospam.gmail.com>
# Contributor: György Balló <ballogy@freestart.hu>

pkgname=f-spot
pkgver=0.8.2
pkgrel=4
pkgdesc="A personal photo management program for GNOME"
url="http://www.gnome.org/projects/f-spot/"
arch=(i686 x86_64)
license=('GPL')
depends=('mono-addins' 'gnome-sharp' 'ndesk-dbus-glib' 'lcms' 'gnome-keyring-sharp' 'flickrnet' 'libgphoto2' 'desktop-file-utils' 'hicolor-icon-theme' 'xdg-utils' 'libunique')
makedepends=('intltool' 'gnome-doc-utils')
install=$pkgname.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:3}/${pkgname}-${pkgver}.tar.bz2
        mono-2.8.patch
        flickrnet-3.0.patch
        0004-Fix-build-error-with-recent_gtk-sharp.patch)
sha256sums=('24448d3431a95a9faa0aa975554414eb638f918811507177e9d69d11d417873a'
            '4e7d2f1909870b842bbbe087f4381f325d5281d2deae0d32ba5713ee66d52b65'
            'e8e4470c7cd29c000a9505bcd85cb25641890f66e568edb33d495e8e38fc92a5'
            'a7f46b3fad59b8d81a329e51ca722373c82bcf664976b8bcb9618084307c81ec')

prepare() {
  mkdir .wabi

  cd $pkgname-$pkgver
  patch -Np1 -i ../mono-2.8.patch

  # https://bugzilla.gnome.org/show_bug.cgi?id=665063
  patch -Np1 -i ../flickrnet-3.0.patch
  
  # Patch from Debian
  patch -Np1 -i ../0004-Fix-build-error-with-recent_gtk-sharp.patch
}

build() {
  export MONO_SHARED_DIR="${srcdir}/.wabi"

  cd $pkgname-$pkgver
  MCS=/usr/bin/dmcs ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static --disable-scrollkeeper --disable-schemas-install --enable-release \
              --with-gnome-screensaver=/usr --with-gnome-screensaver-privlibexecdir=/usr/lib/gnome-screensaver \
              --with-vendor-build-id=ArchLinux
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR=${pkgdir} install

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain f-spot ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}

