# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=attica-qt5
pkgver=5.10.0
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc='Qt5 library that implements the Open Collaboration Services API'
url='https://projects.kde.org/projects/frameworks/attica'
license=('LGPL')
depends=('qt5-base')
makedepends=('extra-cmake-modules')
groups=('kf5')
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/attica-${pkgver}.tar.xz")
md5sums=('dd25019323f63ac9ee13fd2a92a30a3e')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../attica-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
