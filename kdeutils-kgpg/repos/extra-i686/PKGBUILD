# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeutils-kgpg
pkgver=15.08.3
pkgrel=1
pkgdesc='A GnuPG frontend'
url='http://kde.org/applications/utilities/kgpg/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdebase-runtime' 'kdepimlibs4')
makedepends=('cmake' 'automoc4' 'boost')
groups=('kde-applications' 'kdeutils')
install=${pkgname}.install
source=("http://download.kde.org/stable/applications/${pkgver}/src/kgpg-${pkgver}.tar.xz")
sha1sums=('804bdb7bd3c9ad51c2f7e7f288665e4a93faa7b5')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kgpg-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
