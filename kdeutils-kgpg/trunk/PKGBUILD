# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeutils-kgpg
pkgver=16.04.2
pkgrel=1
pkgdesc='A GnuPG frontend'
url='http://kde.org/applications/utilities/kgpg/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4' 'boost')
groups=('kde-applications' 'kdeutils')
source=("http://download.kde.org/stable/applications/${pkgver}/src/kgpg-${pkgver}.tar.xz")
sha1sums=('faae8d6eee34a25aba0f717372fcc79ceae7527b')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kgpg-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
