# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: boromil@gmail.com

pkgname=ffmpegthumbnailer
pkgver=2.0.10
pkgrel=3
pkgdesc="Lightweight video thumbnailer that can be used by file managers."
url="https://github.com/dirkvdb/ffmpegthumbnailer"
license=('GPL2')
arch=('i686' 'x86_64')
depends=('ffmpeg' 'libjpeg' 'libpng')
optdepends=('gvfs: support for gio uris')
source=("https://github.com/dirkvdb/$pkgname/archive/$pkgver.tar.gz"
        "locale.patch")
sha1sums=('c947cb175dfc6b414046b2ffc7008246fa101f4f'
          '569d262435cd5ebc4515e2389df048e3630af034')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/locale.patch"
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./autogen.sh
  ./configure --prefix=/usr --enable-gio --enable-thumbnailer
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
