# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: boromil@gmail.com

pkgname=ffmpegthumbnailer
pkgver=2.1.1
pkgrel=1
pkgdesc="Lightweight video thumbnailer that can be used by file managers."
url="https://github.com/dirkvdb/ffmpegthumbnailer"
license=('GPL2')
arch=('i686' 'x86_64')
depends=('ffmpeg' 'libjpeg' 'libpng')
makedepends=('cmake')
optdepends=('gvfs: support for gio uris')
source=("https://github.com/dirkvdb/$pkgname/archive/$pkgver.tar.gz")
sha1sums=('a5e7f15a95990349d6b54a13d723d209e19eab35')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  mkdir -p build
  cd build
  cmake .. \
	-DCMAKE_BUILD_TYPE=Release \
	-DCMAKE_INSTALL_PREFIX=/usr \
	-DCMAKE_INSTALL_LIBDIR=lib \
	-DENABLE_GIO=ON \
	-DENABLE_THUMBNAILER=ON
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"/build
  make DESTDIR="${pkgdir}" install
}
