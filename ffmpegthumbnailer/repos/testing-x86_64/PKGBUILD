# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: boromil@gmail.com

pkgname=ffmpegthumbnailer
pkgver=2.1.0
pkgrel=2
pkgdesc="Lightweight video thumbnailer that can be used by file managers."
url="https://github.com/dirkvdb/ffmpegthumbnailer"
license=('GPL2')
arch=('i686' 'x86_64')
depends=('ffmpeg' 'libjpeg' 'libpng')
makedepends=('cmake')
optdepends=('gvfs: support for gio uris')
source=("https://github.com/dirkvdb/$pkgname/archive/$pkgver.tar.gz" ffmpegthumbnailer-ffmpeg3.patch)
sha1sums=('5fe86d26b1d5baddc5b75c5015b8d1a1a3719285'
          '634273e4373ec826ebc26daaadd01458a2a17df6')

prepare() {
  cd $pkgname-$pkgver
# Fix build with ffmpeg 3.0 (Gentoo)
  patch -lp1 -i ../ffmpegthumbnailer-ffmpeg3.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  mkdir -p build
  cd build
  cmake .. \
	-DCMAKE_BUILD_TYPE=Release \
	-DCMAKE_INSTALL_PREFIX=/usr \
	-DCMAKE_INSTALL_LIBDIR=lib \
	-DENABLE_GIO=ON \
	-DENABLE_THUMBNAILER=ON
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"/build
  make DESTDIR="${pkgdir}" install
}
