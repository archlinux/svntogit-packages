# $Id: PKGBUILD 23110 2010-08-06 11:01:51Z rvanharen $
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: boromil@gmail.com

pkgname=ffmpegthumbnailer
pkgver=2.0.7
pkgrel=5
pkgdesc="Lightweight video thumbnailer that can be used by file managers."
url="http://code.google.com/p/ffmpegthumbnailer/"
license=('GPL2')
arch=('i686' 'x86_64')
depends=('ffmpeg' 'libjpeg' 'libpng')
optdepends=('gvfs: support for gio uris')
source=("http://ffmpegthumbnailer.googlecode.com/files/$pkgname-$pkgver.tar.gz"
        'ffmpegthumbnailer.desktop' 'ffmpegapi_fix_r241.patch')
options=('!libtool')
sha1sums=('b8f5371aa995fefd1fb75e306e8cd76e8c9f3a73'
          'bdd3ae35a5c6f0e1f4b0c7926f72b3429b2eaa53'
          '4e1a6a0f06ae3ae322ea44046599ebf3ab693cb6')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Don't use deprecated (removed?) ffmpeg api
  patch -Np0 -i "${srcdir}/ffmpegapi_fix_r241.patch"

  ./configure --prefix=/usr --enable-gio
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # FS#24105: Generate thumbnails in nautilus
  # FS#26540: Rename to .thumbnailer for nautilus
  install -Dm644 "${srcdir}/ffmpegthumbnailer.desktop" \
    "${pkgdir}/usr/share/thumbnailers/ffmpegthumbnailer.thumbnailer"
}
