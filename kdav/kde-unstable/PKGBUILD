# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kdav
pkgver=19.03.80
pkgrel=1
pkgdesc="A DAV protocol implemention with KJobs"
url='https://pim.kde.org/'
arch=(x86_64)
license=(GPL LGPL FDL)
depends=(kio qt5-xmlpatterns)
makedepends=(extra-cmake-modules)
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('b964d41350c5189cfad6931ab4125a1b3dbe2c4488f1b33fabd5dc71a9c69984'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
