# $Id: PKGBUILD 145371 2011-12-22 07:58:59Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=libkipi
pkgver=4.8.1
pkgrel=1
pkgdesc="An interface to use kipi-plugins from a KDE application"
url="http://kde.org/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdelibs')
makedepends=('cmake' 'automoc4')
replaces=('kdegraphics-libs')
conflicts=('kdegraphics-libs')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/xz/${pkgname}-${pkgver}.tar.xz")
sha1sums=('bc290e3354c206d7852433aac704fc96b3a55700')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
