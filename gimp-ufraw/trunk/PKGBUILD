# $Id$
# Maintainer: Tobias Kieslich <tobias@archlinux.org>

pkgname=gimp-ufraw
pkgver=0.18
pkgrel=1
pkgdesc="Standalone or gimp plugin converter for raw files"
url="http://ufraw.sourceforge.net/"
arch=('i686' 'x86_64')
license=('GPL2')
depends=('gtkimageview' 'exiv2')
makedepends=('gimp' 'cinepaint' 'pkg-config')
optdepends=('gimp: to use the gimp import plugin for raw images'
	'cinepaint: to use the cinepaint import plugin for raw images')
source=(http://downloads.sourceforge.net/ufraw/ufraw-${pkgver}.tar.gz)
md5sums=('454f40a402928998a82e2645d9265d96')
sha1sums=('41c9ad7aa7f1cbb63a6b0b330b3599b18a7e8cd2')

build() {
  cd "${srcdir}/ufraw-${pkgver}"

  ./configure --prefix=/usr \
              --enable-extras \
              --enable-mime \
              --enable-openmp
  sed -i "s/-ffast-math -fomit-frame-pointer -W -Wall -O3/${CFLAGS}/" Makefile
  make
}

package() {
  cd "${srcdir}/ufraw-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -f "${pkgdir}/usr/bin/dcraw"
}
