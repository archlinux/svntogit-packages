# $Id$
# Maintainer: Tobias Kieslich <tobias@archlinux.org>

pkgname=gimp-ufraw
pkgver=0.18
pkgrel=5
pkgdesc="Standalone or gimp plugin converter for raw files"
url="http://ufraw.sourceforge.net/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gtkimageview' 'exiv2' 'lcms' 'bzip2' 'desktop-file-utils')
makedepends=('gimp' 'cinepaint')
optdepends=('gimp: to use the gimp import plugin for raw images'
            'cinepaint: to use the cinepaint import plugin for raw images')
install=gimp-ufraw.install
source=(http://downloads.sourceforge.net/ufraw/ufraw-${pkgver}.tar.gz)
sha1sums=('41c9ad7aa7f1cbb63a6b0b330b3599b18a7e8cd2')

build() {
  cd "${srcdir}/ufraw-${pkgver}"

  ./configure --prefix=/usr \
              --enable-extras \
              --enable-mime \
              --enable-openmp
  sed -i "s/-ffast-math -fomit-frame-pointer -W -Wall -O3/${CFLAGS}/" Makefile
  make
}

package() {
  cd "${srcdir}/ufraw-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -f "${pkgdir}/usr/bin/dcraw"
}
