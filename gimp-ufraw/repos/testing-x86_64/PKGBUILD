# $Id$
# Maintainer: Tobias Kieslich <tobias@archlinux.org>

pkgname=gimp-ufraw
_srcname=ufraw
pkgver=0.16
pkgrel=3
pkgdesc="Standalone or gimp plugin converter for raw files"
url="http://ufraw.sourceforge.net/"
arch=('i686' 'x86_64')
license=('GPL2')
depends=('gtkimageview' 'exiv2')
makedepends=('gimp' 'cinepaint' 'pkgconfig')
optdepends=('gimp: to use the gimp import plugin for raw images'
	'cinepaint: to use the cinepaint import plugin for raw images')
source=(http://downloads.sourceforge.net/${_srcname}/${_srcname}-${pkgver}.tar.gz)
md5sums=('61e100e42f17e3a7fcfae64506eebd14')

build() {
  cd "${srcdir}/${_srcname}-${pkgver}"

  ./configure --prefix=/usr \
              --enable-extras \
              --enable-mime \
              --enable-openmp \
              --with-exiv2 || return 1
  sed -i "s/-ffast-math -fomit-frame-pointer -W -Wall -O3/${CFLAGS}/" Makefile || return 1
  make || return 1
}

package() {
  cd ${srcdir}/${_srcname}-${pkgver}
  make DESTDIR="${pkgdir}" install || return 1
  rm -f ${pkgdir}/usr/bin/dcraw
}
