# Maintainer: Firmicus <firmicus āt gmx dōt net>

pkgname=texlive-langextra
pkgver=2008.12666
pkgrel=2
pkgdesc="TeX Live - Bundle of all extra language support"
license=('GPL')
arch=('i686' 'x86_64')
conflicts=('texlive-langafrican' 'texlive-langarab' 'texlive-langarmenian' 'texlive-langcroatian' 'texlive-langhebrew' 'texlive-langindic' 'texlive-langmongolian' 'texlive-langtibetan' 'texlive-langvietnamese') 
provides=('texlive-langafrican' 'texlive-langarab' 'texlive-langarmenian' 'texlive-langcroatian' 'texlive-langhebrew' 'texlive-langindic' 'texlive-langmongolian' 'texlive-langtibetan' 'texlive-langvietnamese') 
depends=('texlive-latexextra')
makedepends=('lzma-utils')
groups=('texlive-lang')
url='http://tug.org/texlive/'
source=("ftp://ftp.archlinux.org/other/texlive/$pkgname-$pkgver-src.zip")
install=texlive.install
md5sums=('80bd4fdbb0010672a927fc0df26aa635')

build() {
   cd $srcdir
   install -m755 -d $pkgdir/usr/share/texmf-var/arch/installedpkgs || return 1
   sed -i '/^#/d' CONTENTS || return 1
   _revnr=`echo $pkgver | sed 's/2008\.//'`
   install -m644 CONTENTS $pkgdir/usr/share/texmf-var/arch/installedpkgs/${pkgname}_${_revnr}.pkgs || return 1
   for p in *.tar.lzma; do 
	   lzma --force -dc $p | tar xf - || return 1
   done
   install -m755 -d $pkgdir/usr/share || return 1
   find texmf* -type d -exec install -d -m755 $pkgdir/usr/share/'{}' \; || exit 1
   find texmf* -type f -exec install -m644 '{}' $pkgdir/usr/share/'{}' \; || exit 1
   chmod a+x $pkgdir/usr/share/texmf-dist/scripts/bengali/ebong.py
}
