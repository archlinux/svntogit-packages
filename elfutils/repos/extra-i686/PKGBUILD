# $Id$
# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Andrej Gelenberg <andrej.gelenberg@udo.edu>

pkgname=elfutils
pkgver=0.158
pkgrel=3
pkgdesc="Libraries and utilities to handle ELF object files and DWARF debugging information"
arch=('i686' 'x86_64')
url="https://fedorahosted.org/elfutils/"
license=('LGPL3' 'GPL' 'GPL3')
depends=('gcc-libs' 'zlib' 'bzip2' 'xz')
provides=('libelf')
replaces=('libelf')
conflicts=('libelf')
source=(https://fedorahosted.org/releases/e/l/elfutils/${pkgver}/elfutils-${pkgver}.tar.bz2{,.sig}
        fix-run-backtrace-native-core-test.patch
	CVE-2014-0172.patch)
options=('staticlibs')
sha1sums=('09adbbf0f3a35bb1bcb77c2eaa40de8d3443af4d'
          'SKIP'
          '8ecef640f3d1229cdf45ffda016a69848c18e61b'
          '3e776c07d6ca2c7604a384d266f79c3ece1fb179')

prepare() {
  cd ${pkgname}-${pkgver}

  patch -Np1 -i ../fix-run-backtrace-native-core-test.patch
  # merged upstream
  patch -Np1 -i ../CVE-2014-0172.patch
}

build() {
  cd ${pkgname}-${pkgver}

  CFLAGS+=" -g"  # required for test-suite success
  ./configure --prefix=/usr --program-prefix="eu-"

  make
}

check() {
  cd ${pkgname}-${pkgver}

  make check
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install
}
