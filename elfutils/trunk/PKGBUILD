# $Id$
# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Andrej Gelenberg <andrej.gelenberg@udo.edu>

pkgbase=elfutils
pkgname=(elfutils libelf)
pkgver=0.164
pkgrel=1
pkgdesc="Utilities to handle ELF object files and DWARF debugging information"
arch=('i686' 'x86_64')
url="https://fedorahosted.org/elfutils/"
license=('LGPL3' 'GPL' 'GPL3')
depends=('gcc-libs' 'zlib' 'bzip2' 'xz')
options=('staticlibs')
source=(https://fedorahosted.org/releases/e/l/elfutils/${pkgver}/elfutils-${pkgver}.tar.bz2{,.sig})
sha1sums=('55a951f909267db76d506f61752508a86589aa37'
          'SKIP')
validpgpkeys=('47CC0331081B8BC6D0FD4DA08370665B57816A6A')  # Mark J. Wielaard <mark@klomp.org>

build() {
  cd ${pkgbase}-${pkgver}

  CFLAGS+=" -g"  # required for test-suite success
  ./configure --prefix=/usr --program-prefix="eu-" --enable-deterministic-archives

  make
}

check() {
  cd ${pkgbase}-${pkgver}

  make check
}

package_elfutils() {
  depends=("libelf=$pkgver-$pkgrel")

  cd ${pkgbase}-${pkgver}

  make DESTDIR="${pkgdir}" install

  mkdir "${srcdir}"/libelf
  mv "${pkgdir}"/usr/{lib,include} "${srcdir}/libelf"
}

package_libelf() {
  pkgdesc="Libraries to handle ELF object files and DWARF debugging information"
  mv libelf "${pkgdir}/usr"
}
