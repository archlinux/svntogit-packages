# $Id$
# Maintainer: Dan McGee <dan@archlinux.org>

pkgname=pacman-mirrorlist
pkgver=20130626
pkgrel=1
pkgdesc="Arch Linux mirror list for use by pacman"
arch=('any')
url="http://www.archlinux.org/mirrorlist/"
license=('GPL')
backup=(etc/pacman.d/mirrorlist)
source=(mirrorlist)

# NOTE on building this package:
# * Go to the trunk/ directory
# * Run bash -c ". PKGBUILD; updatelist"
# * Update the checksums, update pkgver
# * Build the package

updatelist() {
  rm -f mirrorlist
  wget -O mirrorlist https://www.archlinux.org/mirrorlist/all/smart/
}

package() {
  mkdir -p $pkgdir/etc/pacman.d
  install -m644 $srcdir/mirrorlist $pkgdir/etc/pacman.d/
}

md5sums=('a6149e2cd43bc25ac25bdb19078ba0ac')
sha256sums=('f8c16642b34000499994e8745fb89808c9e8ebffe62a3b93ac478de054f9c14d')
