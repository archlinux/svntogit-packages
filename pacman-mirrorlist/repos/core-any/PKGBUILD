# $Id$
# Maintainer: Dan McGee <dan@archlinux.org>

pkgname=pacman-mirrorlist
pkgver=20140405
pkgrel=1
pkgdesc="Arch Linux mirror list for use by pacman"
arch=('any')
url="http://www.archlinux.org/mirrorlist/"
license=('GPL')
backup=(etc/pacman.d/mirrorlist)
source=(mirrorlist)

# NOTE on building this package:
# * Go to the trunk/ directory
# * Run bash -c ". PKGBUILD; updatelist"
# * Update the checksums, update pkgver
# * Build the package

updatelist() {
  rm -f mirrorlist
  wget -O mirrorlist https://www.archlinux.org/mirrorlist/all/http/
}

package() {
  mkdir -p $pkgdir/etc/pacman.d
  install -m644 $srcdir/mirrorlist $pkgdir/etc/pacman.d/
}

md5sums=('e838c91ab98a5c76f09abe8ade4cc431')
sha256sums=('33bc5e22994f5cf7e307e7f7b94da38a7f1a030337759612d410f1e14574c65e')
