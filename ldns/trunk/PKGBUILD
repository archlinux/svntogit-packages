# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: mathieui <mathieui@mathieui.net>
# Contributor: jiribb <jiribb@gmail.com>

pkgname=ldns
pkgver=1.8.2
pkgrel=2
pkgdesc='Fast DNS library supporting recent RFCs'
url='https://www.nlnetlabs.nl/projects/ldns/'
license=('custom:BSD')
arch=('x86_64')
depends=('openssl' 'dnssec-anchors')
optdepends=('libpcap: ldns-dpa tool')
makedepends=('libpcap')
source=("https://www.nlnetlabs.nl/downloads/${pkgname}/${pkgname}-${pkgver}.tar.gz"{,.asc}
		https://github.com/NLnetLabs/ldns/commit/1acee0c5d35f4a04df07e5d5f4490e6318513997.patch)
sha256sums=('b92b001cdd382de653620a05445e42e17a827eec93d64ee587ad291a533cc1e9'
            'SKIP'
            '03084dcee80fc1b6de47117bc9e0106e541fdda3600dbe0f6d9a8fb44d21d14e')
validpgpkeys=('DC34EE5DB2417BCC151E5100E5F8F8212F77A498') # Willem Toorop

prepare() {
	cd $pkgname-$pkgver
	# https://github.com/NLnetLabs/ldns/issues/183
	patch -p1 -i ../1acee0c5d35f4a04df07e5d5f4490e6318513997.patch
}

build() {
	cd $pkgname-$pkgver
	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--disable-rpath \
		--with-drill \
		--with-examples \
		--with-trust-anchor=/etc/trusted-key.key \

	make
}

package() {
	cd $pkgname-$pkgver
	make DESTDIR="${pkgdir}" install
	install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
