# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-knavalbattle
pkgver=4.11.3
pkgrel=1
pkgdesc="A ship sinking game"
url="http://kde.org/applications/games/knavalbattle/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'libkdegames')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/knavalbattle-${pkgver}.tar.xz")
sha1sums=('025c480ecea840efecbd20b41921ce56fe52d470')

build() {
  mkdir build
  cd build
  cmake ../knavalbattle-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
