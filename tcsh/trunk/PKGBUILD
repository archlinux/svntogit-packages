# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=tcsh
pkgver=6.15.01
pkgrel=4
pkgdesc="Enhanced version of the Berkeley C shell"
arch=('i686' 'x86_64')
url="http://www.tcsh.org/Welcome"
license=('BSD')
depends=('ncurses')
install=tcsh.install
source=(ftp://ftp.astron.com/pri/${pkgname}-${pkgver}.tar.gz csh.cshrc \
        csh.login tcsh.config.skel usr_csh.sh usr_tcsh.sh)
md5sums=('0563ce3d604b84b564dea7fd7bd87832' '299cd79eb3cbc2a3caeac1c3a0309556'\
         '4869b9da87c79854e2cc97241f125853' '6bd69d3c815412ada4579ac8f8cc9c45'\
         '2b672c67b5e08ab81fbb03d8588d25e3' '8758b20326f00a1057bd95a300c28355')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/ --sysconfdir=/etc --mandir=/usr/share/man || return 1
  make || return 1
  make DESTDIR=${pkgdir} install install.man || return 1

  install -D -m644 ${srcdir}/csh.cshrc ${pkgdir}/etc/csh.cshrc || return 1
  install -D -m644 ${srcdir}/csh.login ${pkgdir}/etc/csh.login || return 1
  install -D -m644 ${srcdir}/tcsh.config.skel ${pkgdir}/etc/skel/.tcsh.config || return 1
  ln -s tcsh ${pkgdir}/bin/csh || return 1
  install -D -m 755 ${srcdir}/usr_tcsh.sh ${pkgdir}/usr/bin/tcsh || return 1
  install -D -m 755 ${srcdir}/usr_csh.sh ${pkgdir}/usr/bin/csh || return 1
  install -D -m644 Copyright ${pkgdir}/usr/share/licenses/${pkgname}/Copyright || return 1
}
