# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-desktop-sharp
pkgver=2.26.0
pkgrel=5
pkgdesc="GNOME desktop bindings for C#"
arch=('i686' 'x86_64')
license=(LGPL)
url="http://gtk-sharp.sourceforge.net"
depends=('gnome-sharp>=2.24.1' 'vte>=0.23.5' 'librsvg>=2.26.0' 'gnome-panel>=2.29.91' 'libwnck>=2.29.91' 'gtkhtml>=3.29.91' 'gtksourceview2>=2.9.2')
makedepends=('pkgconfig' 'monodoc' 'libgnomeprintui>=2.18.4')
optdepends=('libgnomeprintui')
options=('!libtool')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.26/${pkgname}-${pkgver}.tar.bz2
	gnome-desktop-sharp-lib-target.patch)
md5sums=('4bc990900bb318b2ba0b0e7998bb47d1'
         '980cd3adf5e745f4caee0a172a51dcc3')

build() {
 # get rid of that .wapi errors; thanks to brice
  export MONO_SHARED_DIR="${srcdir}/weird"
  mkdir -p "${MONO_SHARED_DIR}"

  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i ${srcdir}/gnome-desktop-sharp-lib-target.patch || return 1
  ./configure --prefix=/usr --sysconfdir=/etc || return 1
  make || return 1
  make GACUTIL="/usr/bin/gacutil /root ${pkgdir}/usr/lib" \
      DESTDIR="${pkgdir}" install || return 1
}
