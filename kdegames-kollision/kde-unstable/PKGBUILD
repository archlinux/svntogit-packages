# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-kollision
pkgver=4.10.90
pkgrel=1
pkgdesc="A simple ball dodging game"
url="http://kde.org/applications/games/kollision/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'libkdegames')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/unstable/${pkgver}/src/kollision-${pkgver}.tar.xz")
sha1sums=('78708e2026bf3d195d75bcfd3892ae23832c7801')

build() {
  mkdir build
  cd build
  cmake ../kollision-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
