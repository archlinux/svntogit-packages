# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-kollision
pkgver=4.13.2
pkgrel=1
pkgdesc="A simple ball dodging game"
url="http://kde.org/applications/games/kollision/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'libkdegames')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/kollision-${pkgver}.tar.xz")
sha1sums=('b39d7282f4a98db0d99c181b04860109909b21a5')

build() {
  mkdir build
  cd build
  cmake ../kollision-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
