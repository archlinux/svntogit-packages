# $Id$
# Maintainer:
# Contributor: Simo Leone <simo@archlinux.org>
# Contributor: jlvsimoes <jlvsimoes@oninet.pt>

pkgname=aspell-pt
_pkgverPT=20070510
_pkgverBR=20090702
pkgver=$_pkgverBR
pkgrel=3
pkgdesc="Portuguese and Brazilian Portuguese dictionary for aspell"
arch=('i686' 'x86_64')
url="http://aspell.net/"
license=('GPL2')
depends=('aspell')
conflicts=('aspell-pt_br')
replaces=('aspell-pt_br')
source=(ftp://ftp.gnu.org/gnu/aspell/dict/pt_BR/aspell6-pt_BR-$_pkgverBR-0.tar.bz2
        ftp://ftp.gnu.org/gnu/aspell/dict/pt_PT/aspell6-pt_PT-$_pkgverPT-0.tar.bz2)
md5sums=('e082a8956882eb94a67c12e1b8c4a324' 'a54267ce8f91de6e6a1baf1e8048cba0')
sha1sums=('add1db9a6a908dccaad13a7fd85c3b202299ff26' 'e136c2f411b582897437b06b9068c98ee333be41')

build() {
  cd "$srcdir/aspell6-pt_BR-$_pkgverBR-0"
  ./configure
  make

  cd "$srcdir/aspell6-pt_PT-$_pkgverPT-0"
  ./configure
  make
}

package() {
  cd "$srcdir/aspell6-pt_BR-$_pkgverBR-0"
  make DESTDIR="$pkgdir" install

  cd "$srcdir/aspell6-pt_PT-$_pkgverPT-0"
  make DESTDIR="$pkgdir" install
}
