# $Id$
# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Contributer: Allan McRae <allan@archlinux.org>
# Contributer: Jason Chu <jason@archlinux.org>

pkgname=python2
pkgver=2.7.2
pkgrel=4
_pybasever=2.7
pkgdesc="A high-level scripting language"
arch=('i686' 'x86_64')
license=('PSF')
url="http://www.python.org/"
depends=('bzip2' 'gdbm' 'openssl' 'zlib' 'expat' 'sqlite3' 'libffi')
makedepends=('tk')
optdepends=('tk: for IDLE')
conflicts=('python<3')
options=('!makeflags')
source=(http://www.python.org/ftp/python/${pkgver%rc?}/Python-${pkgver}.tar.xz
        linux2.patch
        gdbm-magic-values.patch)
sha1sums=('56700044141402dc35e7a0a24aa7ffda1a8c1a53'
          '937c06190899a491d662c7918658313e7640070e'
          '0f4cd9508c9ace620a829a688a9285e0480b5fb5')

build() {
  cd "${srcdir}/Python-${pkgver}"

  # Temporary workaround for FS#22322
  # See http://bugs.python.org/issue10835 for upstream report
  sed -i "/progname =/s/python/python${_pybasever}/" Python/pythonrun.c

  # Enable built-in SQLite3 module to load extensions (fix FS#22122)
  sed -i "/SQLITE_OMIT_LOAD_EXTENSION/d" setup.py

  # FS#23997
  sed -i -e "s|^#.* /usr/local/bin/python|#!/usr/bin/python2|" Lib/cgi.py

  # Make sure sys.platform is set to linux2, even on kernel 3.X
  # cf http://bugs.python.org/issue12326
  patch -Np1 -i ../linux2.patch

  # gdbm has new magic that whichdb does not recognize
  # http://bugs.python.org/issue13007
  patch -Np1 -i ../gdbm-magic-values.patch

  # Ensure that we are using the system copy of various libraries (expat, zlib and libffi),
  # rather than copies shipped in the tarball
  rm -r Modules/expat
  rm -r Modules/zlib
  rm -r Modules/_ctypes/{darwin,libffi}*

  export OPT="${CFLAGS}"
  ./configure --prefix=/usr --enable-shared --with-threads --enable-ipv6 \
              --enable-unicode=ucs4 --with-system-expat --with-system-ffi \
              --with-dbmliborder=gdbm:ndbm

  make MACHDEP=linux2
}

package() {
  cd "${srcdir}/Python-${pkgver}"
  make DESTDIR="${pkgdir}" altinstall maninstall

  ln -sf python${_pybasever}        "${pkgdir}/usr/bin/python2"
  ln -sf python${_pybasever}-config "${pkgdir}/usr/bin/python2-config"
  ln -sf python${_pybasever}.1      "${pkgdir}/usr/share/man/man1/python2.1"

  ln -sf ../../libpython${_pybasever}.so \
    "${pkgdir}/usr/lib/python${_pybasever}/config/libpython${_pybasever}.so"

  mv "${pkgdir}/usr/bin/smtpd.py" "${pkgdir}/usr/lib/python${_pybasever}/"

  # some useful "stuff"
  install -dm755 "${pkgdir}"/usr/lib/python${_pybasever}/Tools/{i18n,scripts}
  install -m755 Tools/i18n/{msgfmt,pygettext}.py \
    "${pkgdir}/usr/lib/python${_pybasever}/Tools/i18n/"
  install -m755 Tools/scripts/{README,*py} \
    "${pkgdir}/usr/lib/python${_pybasever}/Tools/scripts/"

  # fix conflicts with python
  mv "${pkgdir}"/usr/bin/idle{,2}
  mv "${pkgdir}"/usr/bin/pydoc{,2}
  mv "${pkgdir}"/usr/bin/2to3{,-2.7}

  # clean up #!s
  find "${pkgdir}/usr/lib/python${_pybasever}/" -name '*.py' | \
    xargs sed -i "s|#[ ]*![ ]*/usr/bin/env python$|#!/usr/bin/env python2|"

  # clean-up reference to build directory
  sed -i "s#${srcdir}/Python-${pkgver}:##" \
    "${pkgdir}/usr/lib/python${_pybasever}/config/Makefile"

  # license
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
