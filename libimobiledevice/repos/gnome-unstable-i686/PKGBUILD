# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Gabriel Martinez < reitaka at gmail dot com >

pkgname=libimobiledevice
pkgver=1.1.0
pkgrel=1
pkgdesc="Is a software library that talks the protocols to support iPhone and iPod Touch devices on Linux"
url="http://libimobiledevice.org/"
arch=('i686' 'x86_64')
license=('GPL2' 'LGPL2.1')
depends=('gnutls'  'glib2' 'libplist' 'usbmuxd')
makedepends=('swig' 'python2')
options=('!libtool')
source=(http://libimobiledevice.org/downloads/${pkgname}-${pkgver}.tar.bz2
        swig-version-check.patch)
md5sums=('b38bb70a41538a2ad593b0779651a636'
         '8bb215e36fe0e08a72332c108c7842cc')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # SWIG version check is wrong (still not fixed upstream)
  patch -p1 -i "$srcdir"/swig-version-check.patch
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
