# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Gabriel Martinez < reitaka at gmail dot com >

pkgname=libimobiledevice
pkgver=1.0.4
pkgrel=1
pkgdesc="Is a software library that talks the protocols to support iPhone and iPod Touch devices on Linux"
url="http://libimobiledevice.org/"
arch=('i686' 'x86_64')
license=('GPL2' 'LGPL2.1')
depends=('gnutls'  'glib2' 'libplist>=1.3' 'usbmuxd>=1.0.6')
makedepends=('swig' 'python2')
options=('!libtool')
source=(http://libimobiledevice.org/downloads/${pkgname}-${pkgver}.tar.bz2
        swig-version-check.patch)
md5sums=('dbf62f2fddaa87558b6f1ebc76abb549'
         '8bb215e36fe0e08a72332c108c7842cc')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # SWIG version check is wrong (still not fixed upstream)
  patch -p1 -i "$srcdir"/swig-version-check.patch
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
