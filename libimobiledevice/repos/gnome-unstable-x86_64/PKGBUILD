# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Gabriel Martinez < reitaka at gmail dot com >

pkgname=libimobiledevice
pkgver=1.1.1
pkgrel=3
pkgdesc="Is a software library that talks the protocols to support iPhone and iPod Touch devices on Linux"
url="http://libimobiledevice.org/"
arch=('i686' 'x86_64')
license=('GPL2' 'LGPL2.1')
depends=('usbmuxd' 'libplist' 'gnutls' 'libgcrypt')
makedepends=('python2' 'swig')
options=('!libtool')
source=(http://libimobiledevice.org/downloads/$pkgname-$pkgver.tar.bz2
        swig-version-check.patch)
md5sums=('cdc13037e822d9ac2e109536701d153a'
         '8bb215e36fe0e08a72332c108c7842cc')

build() {
  cd "$pkgname-$pkgver"

  # SWIG version check is wrong (still not fixed upstream)
  patch -p1 -i ../swig-version-check.patch

  PYTHON=/usr/bin/python2 ./configure --prefix=/usr
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
