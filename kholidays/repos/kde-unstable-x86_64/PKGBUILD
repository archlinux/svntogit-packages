# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kholidays
pkgver=16.07.90
pkgrel=1
pkgdesc="KDE library for regional holiday information"
arch=(i686 x86_64)
url="https://community.kde.org/KDE_PIM"
license=(LGPL)
depends=(qt5-base)
makedepends=(extra-cmake-modules qt5-declarative)
optdepends=('qt5-declarative: QML bindings')
source=("http://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
md5sums=('df4b602ce7b28ce3193dc56da5f703c7')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
