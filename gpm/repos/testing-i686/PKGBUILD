# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=gpm
pkgver=1.20.6
pkgrel=1
pkgdesc="A mouse server for the console and xterm"
arch=('i686' 'x86_64')
url="http://unix.schottelius.org/gpm/"
license=('GPL')
depends=('ncurses' 'texinfo')
backup=('etc/conf.d/gpm')
options=('!makeflags')
install=gpm.install
source=(http://unix.schottelius.org/gpm/archives/${pkgname}-${pkgver}.tar.bz2 \
        gpm gpm.conf.d)
md5sums=('6b534da16dc1b28ba828dea89e520f6f' 'c4d42a4725503a5524cae060a959a3d3'\
         '5f109341a7d1bb18c59d7c3a395d848a')
sha1sums=('f3bb9272878b7934968381c8b25edab359114150' 'c13ad1422d24af320e2abb9786c7eacef39b293c'\
          '3bcb67027f0ad7dc393388fb02c261126671f9f5')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc || return 1
  make || return 1
  make DESTDIR=${pkgdir} install || return 1
  install -D -m755 ../gpm ${pkgdir}/etc/rc.d/gpm || return 1
  install -D -m644 ../gpm.conf.d ${pkgdir}/etc/conf.d/gpm || return 1
  gzip ${pkgdir}/usr/share/info/gpm.info

# library fixes
  cd ${pkgdir}/usr/lib/
  ln -s libgpm.so.2.* libgpm.so || return 1
  chmod 755 ${pkgdir}/usr/lib/libgpm.so.* || return 1
}
