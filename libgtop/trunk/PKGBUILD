# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libgtop
pkgver=2.38.0+13+gcab8791f
pkgrel=1
pkgdesc="A library for collecting system monitoring data"
url="https://gitlab.gnome.org/GNOME/libgtop"
arch=(x86_64)
license=(LGPL)
depends=(glib2 libxau)
makedepends=(gobject-introspection gnome-common git gtk-doc)
_commit=cab8791fdd6a174b1aea06410214800a774a729b  # master
source=("git+https://gitlab.gnome.org/GNOME/libgtop.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static --enable-gtk-doc
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
