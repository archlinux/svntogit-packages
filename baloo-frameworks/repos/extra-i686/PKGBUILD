# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=baloo-frameworks
pkgver=5.1.2
pkgrel=1
pkgdesc="A framework for searching and managing metadata"
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kdelibs/baloo'
license=('LGPL')
depends=('xapian-core' 'kfilemetadata5' 'kidletime' 'kcmutils' 'kdelibs4support')
makedepends=('extra-cmake-modules' 'kdoctools' 'python')
conflicts=('baloo4')
provides=('baloo')
install="${pkgname}.install"
source=("http://download.kde.org/stable/plasma/${pkgver}/baloo-$pkgver.tar.xz")
md5sums=('a8ddea44b89176530b6598231ff89c43')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../baloo-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
