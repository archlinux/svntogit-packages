# Maintainer: Florian Pritz <bluewind@xinu.at>

pkgname=perl-crypt-openssl-random
pkgver=0.11
pkgrel=2
pkgdesc='Routines for accessing the OpenSSL pseudo-random number generator'
arch=(i686 x86_64)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=(perl openssl)
url=https://metacpan.org/release/Crypt-OpenSSL-Random
source=("http://search.cpan.org/CPAN/authors/id/R/RU/RURBAN/Crypt-OpenSSL-Random-$pkgver.tar.gz")
md5sums=('5d71337503e0356ce1ce1481504e5885')
sha512sums=('c579cb2b586dbabd697a9c7c989a360ed4bd8e9b28d7c93ad99a86bd9bd680a89c3fa77f6315a1105fd41bfdfa18087ca4cd51fb320faad7a6be9b90e3e69b95')
_ddir="Crypt-OpenSSL-Random-$pkgver"

build() {
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  /usr/bin/perl Makefile.PL
  make
}

check() {
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
}

package() {
  cd "$srcdir/$_ddir"
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
# template start; name=perl-binary-module-dependency; version=1;
if [[ $(find "$pkgdir/usr/lib/perl5/" -name "*.so") ]]; then
	_perlver_min=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]);')
	_perlver_max=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]+1);')
	depends+=("perl>=$_perlver_min" "perl<$_perlver_max")
fi
# template end;
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
