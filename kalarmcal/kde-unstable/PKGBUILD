# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kalarmcal
pkgver=19.03.90
pkgrel=1
pkgdesc="The KAlarm client library"
arch=(x86_64)
url="https://kontact.kde.org"
license=(LGPL)
depends=(kholidays akonadi kcalutils)
makedepends=(extra-cmake-modules boost)
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('ffe05b141802afaa831c0923a1f086051dc9e366339c639cc7f3077d8b0845ad'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
