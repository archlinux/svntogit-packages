# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kalarmcal
pkgver=21.07.80
pkgrel=1
pkgdesc='The KAlarm client library'
arch=(x86_64)
url='https://kontact.kde.org'
license=(LGPL)
depends=(kholidays libakonadi kcalutils)
makedepends=(extra-cmake-modules boost)
source=(https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('bcef86f4215b6d73b520c368c315751eda5e84136ed3e5961f9d191cff68a241'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
