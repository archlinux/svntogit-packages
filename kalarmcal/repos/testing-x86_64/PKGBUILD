# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kalarmcal
pkgver=21.12.3
pkgrel=1
pkgdesc='The KAlarm client library'
arch=(x86_64)
url='https://kontact.kde.org'
license=(LGPL)
depends=(kholidays libakonadi kcalutils)
makedepends=(extra-cmake-modules boost qt5-tools doxygen)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('194f97ee30c8382ac7fd80432e72f04f5db3f3fe4a009ab487c82c014eae1798'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
options=(debug)

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
