# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=gnuplot 
pkgver=4.6.1
pkgrel=2
pkgdesc="Plotting package which outputs to X11, PostScript, PNG, GIF, and others" 
arch=('i686' 'x86_64') 
url="http://www.gnuplot.info" 
license=('custom') 
depends=('readline' 'gd' 'wxgtk' 'cairo' 'libjpeg' 'lua') 
makedepends=('texinfo' 'emacs' 'texlive-core' 'texlive-latexextra')
install=gnuplot.install 
source=("http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz")
sha1sums=('1ea21a628223159b0297ae65fe8293afd5aab3c0')

build() {
  cd $srcdir/$pkgname-$pkgver

  # fix default source location; use the GDFONTPATH variable to modify at runtime 
  sed -i 's|/usr/X11R6/lib/X11/fonts/truetype|/usr/share/fonts/TTF|' src/variable.c

  sed -i -e 's|/usr/X11R6/lib/X11/fonts/Type1|/usr/share/fonts/Type1|' \
  -e 's|$(X11ROOT)/X11R6/lib/X11/fonts/Type1|$(X11ROOT)/usr/share/fonts/Type1|' \
  src/variable.c

  ./configure --prefix=/usr --mandir=/usr/share/man \
              --infodir=/usr/share/info \
              --libexecdir=/usr/bin \
              --with-gihdir=/usr/share/gnuplot \
              --datadir=/usr/share \
              --with-readline=gnu \
              --with-texdir=$pkgdir/usr/share/texmf/tex/latex/gnuplot
  make pkglibexecdir=/usr/bin
}


package() {
  cd $srcdir/$pkgname-$pkgver
  make prefix=$pkgdir/usr libexecdir=$pkgdir/usr/bin \
         mandir=$pkgdir/usr/share/man \
        infodir=$pkgdir/usr/share/info \
        pkglibexecdir=$pkgdir/usr/bin \
        GIHDIR=$pkgdir/usr/share/gnuplot \
        datadir=$pkgdir/usr/share install

  install -Dm644 lisp/dotemacs $pkgdir/usr/share/emacs/site-lisp/dotemacs
  install -Dm644 Copyright $pkgdir/usr/share/licenses/$pkgname/Copyright

  rm -f $pkgdir/usr/share/texmf-dist/ls-R
}
