# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=atk
pkgver=2.26.0
pkgrel=1
pkgdesc="The interface definitions of accessibility infrastructure"
url="https://git.gnome.org/browse/atk"
arch=(i686 x86_64)
license=(LGPL)
depends=(glib2)
makedepends=(gobject-introspection git gtk-doc meson)
_commit=d2fbf661260626939cc3186c58177c1890c0e2e2  # tags/ATK_2_26_0^0
source=("git+https://git.gnome.org/browse/atk#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^ATK_//;s/_/./g;s/-/+/g'
}

prepare() {
  mkdir build
  cd $pkgname
}

check() {
  cd build
  meson test
}

build() {
  cd build
  meson setup --prefix=/usr --buildtype=release ../$pkgname \
    -Denable_docs=true
  ninja
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}
