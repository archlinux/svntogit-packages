# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
pkgname=atk
pkgver=2.0.1
pkgrel=1
pkgdesc="A library providing a set of interfaces for accessibility"
arch=(i686 x86_64)
license=('LGPL')
depends=('glib2')
makedepends=('pkgconfig' 'gobject-introspection')
options=('!libtool')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz)
url='http://www.gtk.org/'
sha256sums=('4588867bb9eb788b2cf3399d3a2d5e4898193a9027436f6cffc9c18a627452a1')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
