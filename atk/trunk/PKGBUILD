# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=atk
pkgver=2.38.0
pkgrel=1
pkgdesc="Interface definitions of accessibility infrastructure"
url="https://gitlab.gnome.org/GNOME/atk"
arch=(x86_64)
license=(LGPL)
depends=(glib2)
makedepends=(gobject-introspection git gtk-doc meson)
provides=(libatk-1.0.so)
_commit=f1051ba57a2110c46a136a48f4d60a405bc2c3f5  # tags/2.38.0^0
source=("git+https://gitlab.gnome.org/GNOME/atk.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd atk
  git describe --tags | sed 's/^ATK_//;s/_/./g;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd atk
}

build() {
  arch-meson atk build -D docs=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
