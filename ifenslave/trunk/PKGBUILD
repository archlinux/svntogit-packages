# $Id$
# Maintainer: 
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=ifenslave
pkgver=1.1.0
pkgrel=7
pkgdesc='Utility for bonding ethernet interfaces'
arch=('i686' 'x86_64')
url='http://www.linuxfoundation.org/en/Net:Bonding'
license=('GPL2')
depends=('glibc')
makedepends=('linux-api-headers')
backup=('etc/conf.d/bonding')
source=(ifenslave.c::'http://git.kernel.org/?p=linux/kernel/git/torvalds/linux-2.6.git;a=blob_plain;f=Documentation/networking/ifenslave.c;h=65968fbf'
        'bonding.conf.d')
sha1sums=('c657d9e6b8acb983bb8ef856f8f7f25ee6bf4f73'
          '6843971c0254768868b556af33289d7d32d6208d')

build() {
	cd "${srcdir}"
	gcc -Wall -Wstrict-prototypes ${CFLAGS} ${LDFLAGS} ifenslave.c -o ifenslave
}

package() {
	cd "${srcdir}"
	install -Dm755 ifenslave "${pkgdir}"/sbin/ifenslave
	install -Dm644 bonding.conf.d "${pkgdir}"/etc/conf.d/bonding
}
