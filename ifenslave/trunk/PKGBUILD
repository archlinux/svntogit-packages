# $Id$
# Maintainer: 
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=ifenslave
pkgver=1.1.0
pkgrel=6
pkgdesc="Utility for bonding ethernet interfaces"
arch=('i686' 'x86_64')
url="http://www.linuxfoundation.org/en/Net:Bonding"
license=('GPL2')
depends=('glibc')
makedepends=('linux-api-headers')
backup=('etc/conf.d/bonding')
source=(ifenslave.c::'http://git.kernel.org/?p=linux/kernel/git/torvalds/linux-2.6.git;a=blob_plain;f=Documentation/networking/ifenslave.c;h=2bac96'
        bonding.conf.d)
md5sums=('41a23dc9ac3522ac2af23396ba70bcc4'
         '397fccbf574d5082c7a3c8d3793dc000')

build() {
  cd $srcdir
  gcc -Wall -Wstrict-prototypes $CFLAGS $LDFLAGS ifenslave.c -o ifenslave
}

package() {
  cd $srcdir
  install -Dm755 ifenslave $pkgdir/sbin/ifenslave
  install -Dm644 bonding.conf.d $pkgdir/etc/conf.d/bonding
}
