# Maintainer: SÃ©bastien "Seblu" Luttringer <seblu@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=less
pkgver=580
pkgrel=1
pkgdesc='A terminal based program for viewing text files'
license=('GPL3')
arch=('x86_64')
url='http://www.greenwoodsoftware.com/less'
depends=('glibc' 'ncurses' 'pcre2')
validpgpkeys=('AE27252BD6846E7D6EAE1DD6F153A7C833235259') # Mark Nudelman
source=("http://www.greenwoodsoftware.com/$pkgname/$pkgname-$pkgver.tar.gz"
        "$pkgname-$pkgver.tar.gz.sig::http://www.greenwoodsoftware.com/$pkgname/$pkgname-$pkgver.sig")
sha256sums=('5b5c606e3de2e8d93929342c89e71861a4a6766210f65e1c05a75f8cf62fa287'
            'SKIP')

build() {
  cd $pkgname-$pkgver
  sh configure --prefix=/usr --sysconfdir=/etc --with-regex=pcre2
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
