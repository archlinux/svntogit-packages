# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-klettres
pkgver=4.14.1
pkgrel=1
pkgdesc="Learn The Alphabet"
url="http://kde.org/applications/education/klettres/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/klettres-${pkgver}.tar.xz")
sha1sums=('73f06f57f858190bffe36635bf691a14ac40e84a')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../klettres-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
