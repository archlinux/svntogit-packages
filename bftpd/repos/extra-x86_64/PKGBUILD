# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Thomas BÃ¤chler <thomas@archlinux.org>

pkgname=bftpd
pkgver=5.0
pkgrel=1
pkgdesc='Small, easy-to-configure FTP server'
url='http://bftpd.sourceforge.net/'
arch=('x86_64')
license=('GPL')
depends=('pam' 'zlib' 'glibc')
backup=(etc/bftpd.conf
        etc/logrotate.d/bftpd
        etc/pam.d/bftpd
        etc/xinetd.d/bftpd)
source=(https://downloads.sourceforge.net/sourceforge/bftpd/bftpd-${pkgver}.tar.gz
        bftpd.logrotate
        bftpd.pam
        bftpd.xinetd
        bftpd.service
        bftpd.socket
        bftpd_AT.service
        bftpd.tmpfiles
        bftpd-4.9-Makefile.patch)
sha512sums=('99daa36647e98cc78bc2fe79843af73f0862fd21eedf8656d5c3f28cc298ce84c6937fca50b8b40f287bf702cf3f720c7858ea68029d6ba2bc8e281347f6da89'
            '50faec3cf93a4702945bb8aee5b2e9b227183ec098fe93c729ca14d02c9432ccf4b25bc86f9b4b1ae40c9a9820b6b5a9e83a1dde78085a36a2c84b69690262b6'
            '1e6183ab0eb812b3ef687ac2c26ce78f7cb30540f606d20023669ac00ba04075487fb72e4dc89cc05dab0269ff6aca98fc1167cc75669c225b88b592482fbf67'
            '98d7679faffd497f0cc2faa6129b010f0a47092e1caebc82582e362e3eb107c09cf17496b10bbc4fc406ebfea7f75a70d119c071721099d169765f53ce89b51a'
            '1433b8b1a2eea07b095e41eb83e8819fcb22c63b632fe73ec87e1f0490a5f84f836cc65ea1ca2f8fa9ab901122350a45f781593748df04fa211168b5f08546b8'
            'd37b9e6f69d781ecf8ddf63072a0e00c1cb27959ac622b5ba19fc7db0021f2da83fb427ee13f310c52565e5cbc8a5016bf28e63e9fb95e2fd0a568f5028f0056'
            'b1a9f01f974b72a40d072c75a3962f5354eeb23f7383643f20cf3809ffc2d6217c450d0564be223b99552ebc55e26c0e4f1362a2ede24280e865a3f63ceca4a5'
            '92650582747a34a29631b1755d2eb14d124a9632888c39cf2dbe5c2834f5b4c5f6506588ef0106cc643397587ffbe9a2240787030452bf29b17b7aacf1632cc1'
            'c5b35f43b44cb60ee05329f45092f714ed542a5ce07c2c08d63761ca2d040bbd2dfd9a5b3f70e1826e3e6b48d8d35e8d66c222d9f5697f102154e4ea65e3d4af')
prepare() {
  cd ${pkgname}
  patch -Np1 < ../bftpd-4.9-Makefile.patch
  # Fix default in config file
  sed 's|#ROOTDIR="/path/for/anonymous/user"|# bftpd interprets ROOTDIR="%h" (the default), as ROOTDIR="/" for the anonymous user, override it\n  ROOTDIR="/srv/ftp"|' -i bftpd.conf
  rm -f config.{cache,status}
  autoreconf -fiv
}

build() {
  cd ${pkgname}
    ./configure \
    --prefix=/usr \
    --enable-pam \
    --enable-libz \
    --mandir=/usr/share/man \
    --sysconfdir=/etc \
    --sbindir=/usr/bin
  make
}

package() {
  cd ${pkgname}
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}/usr/var"
  install -Dm 644 ../bftpd.service "${pkgdir}/usr/lib/systemd/system/bftpd.service"
  install -Dm 644 ../bftpd_AT.service "${pkgdir}/usr/lib/systemd/system/bftpd@.service"
  install -Dm 644 ../bftpd.socket "${pkgdir}/usr/lib/systemd/system/bftpd.socket"
  install -Dm 644 ../bftpd.tmpfiles "${pkgdir}/usr/lib/tmpfiles.d/bftpd.conf"
  install -Dm 644 ../bftpd.logrotate "${pkgdir}/etc/logrotate.d/bftpd"
  install -Dm 644 ../bftpd.pam "${pkgdir}/etc/pam.d/bftpd"
  install -Dm 644 ../bftpd.xinetd "${pkgdir}/etc/xinetd.d/bftpd"
}

# vim: ts=2 sw=2 et:
