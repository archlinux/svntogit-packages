# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=links
pkgver=2.19
pkgrel=2
pkgdesc="A text WWW browser, similar to Lynx"
arch=('x86_64')
url="http://links.twibright.com/"
license=('GPL')
depends=('bzip2' 'gpm' 'libevent' 'pcre' 'xz')
makedepends=('libtiff' 'libpng' 'libxt' 'librsvg')
optdepends=('libx11: for using xlinks'
	    'libtiff: for using xlinks'
	    'libpng: for using xlinks'
	    'librsvg: for using xlinks')
source=("http://links.twibright.com/download/${pkgname}-${pkgver}.tar.bz2"
        'links.desktop')
sha256sums=('70758c7dd9bb70f045407900e0a90f1114947fce832c2f9bdefd5c0158089a0a'
            '484571172aa19cf91c4ce9d9528a4ac52e9252d6e741897e7339880aa30dbc88')

configure_options=(
  --prefix=/usr
  --mandir=/usr/share/man
  --disable-javascript
)

prepare() {
  cd ${pkgname}-${pkgver}
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" configure
}

build() {
  cd ${pkgname}-${pkgver}
  (cd intl; ./gen-intl; ./synclang)
  ./configure \
    "${configure_options[@]}" \
    --enable-graphics \
    --with-x \
    --with-fb
  make
  mv links xlinks

  ./configure \
    "${configure_options[@]}" \
    --disable-graphics \
    --without-x \
    --without-fb
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -D -m0755 xlinks "${pkgdir}/usr/bin/xlinks"
  ln -s links.1.gz "${pkgdir}/usr/share/man/man1/xlinks.1.gz"

  install -D -m0644 "${srcdir}/links.desktop" "${pkgdir}/usr/share/applications/links.desktop"
  install -d "${pkgdir}/usr/share/pixmaps"
  install -m0644 links_16x16_1.xpm links_16x16_2.xpm links_32x32.xpm "${pkgdir}/usr/share/pixmaps/"

  install -d "${pkgdir}/usr/share/doc/links/calibration"
  install -m0644 doc/links_cal/* "${pkgdir}/usr/share/doc/links/calibration/"
}
