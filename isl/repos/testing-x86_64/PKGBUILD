# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>

pkgname=isl
pkgver=0.09
pkgrel=1
pkgdesc="Library for manipulating sets and relations of integer points bounded by linear constraints"
arch=('i686' 'x86_64')
url="http://www.kotnet.org/~skimo/isl/"
license=('LGPL2.1')
options=('!libtool')
source=(http://www.kotnet.org/~skimo/isl/$pkgname-$pkgver.tar.bz2)
md5sums=('6873f0b3c9f4d1fb8efae4aea53f2ab2')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make -j1 DESTDIR="$pkgdir/" install
  
  # this seems a better place for this file...
  install -dm755 $pkgdir/usr/share/gdb/auto-load/
  mv $pkgdir/usr/{lib,share/gdb/auto-load}/libisl.so.9.0.0-gdb.py
}
