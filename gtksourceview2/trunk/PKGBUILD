# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gtksourceview2
pkgver=2.10.5
pkgrel=4
pkgdesc="A text widget adding syntax highlighting and more to GNOME"
arch=('x86_64')
license=('GPL')
depends=('gtk2' 'libxml2')
makedepends=('intltool' 'pkg-config' 'python')
url="http://www.gnome.org"
source=(https://download.gnome.org/sources/gtksourceview/2.10/gtksourceview-${pkgver}.tar.bz2)
sha256sums=('c585773743b1df8a04b1be7f7d90eecdf22681490d6810be54c81a7ae152191e')

prepare() {
  cd "${srcdir}/gtksourceview-${pkgver}"
  sed -i 's#python#python2#' gtksourceview/language-specs/convert.py
}
 
build() {
  cd "${srcdir}/gtksourceview-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd "${srcdir}/gtksourceview-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
