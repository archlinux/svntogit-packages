# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-pep517
pkgname=('python-pep517' 'python2-pep517')
pkgver=0.5.0
pkgrel=1
pkgdesc="Wrappers to build Python packages using PEP 517 hooks"
arch=('any')
license=('MIT')
url="https://github.com/takluyver/pep517"
makedepends=('python-setuptools' 'python2-setuptools' 'python-pytoml' 'python2-pytoml')
checkdepends=('python-pytest' 'python2-pytest' 'python-pytest-flake8' 'python2-pytest-flake8'
              'python-testpath')
source=("https://pypi.io/packages/source/p/pep517/pep517-$pkgver.tar.gz")
sha512sums=('5bae6bae4cea9557a2113ddc29257ca47b0670a676dc495e9b4d28e0f685ef2c08cf6715dc473df99591b05a9c3bd12cd59094c575f852552d3bde73558c5031')

prepare() {
  cp -a pep517-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/pep517-$pkgver
  python setup.py build
 
  cd "$srcdir"/pep517-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/pep517-$pkgver
  # https://github.com/pypa/pep517/issues/35
  pytest -k 'not setup.py'

  cd "$srcdir"/pep517-$pkgver-py2
  # No python2-testpath
  # pytest2
}
 
package_python-pep517() {
  depends=('python-pytoml')
 
  cd pep517-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
 
package_python2-pep517() {
  depends=('python2-pytoml')
 
  cd pep517-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
