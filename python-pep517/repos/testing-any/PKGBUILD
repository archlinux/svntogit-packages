# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-pep517
pkgname=('python-pep517' 'python2-pep517')
pkgver=0.10.0
pkgrel=1
pkgdesc="Wrappers to build Python packages using PEP 517 hooks"
arch=('any')
license=('MIT')
url="https://github.com/takluyver/pep517"
makedepends=('python-setuptools' 'python2-setuptools' 'python-toml' 'python2-toml'
             'python2-importlib-metadata' 'python2-zipp')
checkdepends=('python-mock' 'python-pytest-flake8' 'python-testpath' 'python-pip')
source=("https://pypi.io/packages/source/p/pep517/pep517-$pkgver.tar.gz")
sha512sums=('4f2b2a6d4bfb46883b769d3a11aac7ed136a835eaff025e9201b5c592f6aaa236bcc6532f94654bf6962f3df1062677daa6444dce6f95caec4e306d9fe21d426')

prepare() {
  cp -a pep517-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/pep517-$pkgver
  python setup.py build

  cd "$srcdir"/pep517-$pkgver-py2
  python2 setup.py build
}

check() {
  cd pep517-$pkgver
  # setup.py: https://github.com/pypa/pep517/issues/35
  # tests/test_meta.py::test_classic_package: error in pip install command
  pytest --deselect setup.py --deselect tests/test_meta.py::test_classic_package
}

package_python-pep517() {
  depends=('python-toml')

  cd pep517-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-pep517() {
  depends=('python2-toml' 'python2-importlib-metadata' 'python2-zipp')

  cd pep517-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
