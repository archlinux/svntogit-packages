# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-pep517
pkgname=('python-pep517' 'python2-pep517')
pkgver=0.6.0
pkgrel=1
pkgdesc="Wrappers to build Python packages using PEP 517 hooks"
arch=('any')
license=('MIT')
url="https://github.com/takluyver/pep517"
makedepends=('python-setuptools' 'python2-setuptools' 'python-pytoml' 'python2-pytoml'
             'python-importlib-metadata' 'python2-importlib-metadata')
checkdepends=('python-pytest' 'python-pytest-flake8' 'python-testpath' 'python-pip')
source=("https://pypi.io/packages/source/p/pep517/pep517-$pkgver.tar.gz")
sha512sums=('dd93b4a9646b87a851dd8dc2e26ab07b5268762ac1fe216d3ed9e65fe713d664eb39ffa2a9a121a3755e01774342db46c6584c8ac75501d706dd992e99b3d5d4')

prepare() {
  cp -a pep517-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/pep517-$pkgver
  python setup.py build
 
  cd "$srcdir"/pep517-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/pep517-$pkgver
  # https://github.com/pypa/pep517/issues/35
  pytest -k 'not setup.py'

  cd "$srcdir"/pep517-$pkgver-py2
  # No python2-testpath
  # pytest2
}
 
package_python-pep517() {
  depends=('python-pytoml' 'python-importlib-metadata')
 
  cd pep517-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
 
package_python2-pep517() {
  depends=('python2-pytoml' 'python2-importlib-metadata')
 
  cd pep517-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
