# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-pep517
pkgname=('python-pep517' 'python2-pep517')
pkgver=0.11.0
pkgrel=1
pkgdesc="Wrappers to build Python packages using PEP 517 hooks"
arch=('any')
license=('MIT')
url="https://github.com/takluyver/pep517"
makedepends=('python-setuptools' 'python2-setuptools' 'python-tomli' 'python2-toml'
             'python2-importlib-metadata' 'python2-zipp')
checkdepends=('python-mock' 'python-pytest-flake8' 'python-testpath' 'python-pip')
source=("https://pypi.io/packages/source/p/pep517/pep517-$pkgver.tar.gz")
sha512sums=('4981aacef2b7bf86205a93753699c1af557a1af6e33214d8ecc99922ba48988a0cedd0e3194839f790c37b751d6caf3c85fd7538f0e33d15a7c97cf20174e4fd')

prepare() {
  # Copied from openSUSE: 
  # Remove what appears to be overly cautious flag
  # that causes tests to require internet, both here
  # and the test suites of any dependencies. Tracking at:
  # https://github.com/pypa/pep517/issues/101
  sed -i "s/ '--ignore-installed',//" pep517-$pkgver/pep517/envbuild.py
  cp -a pep517-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/pep517-$pkgver
  python setup.py build

  cd "$srcdir"/pep517-$pkgver-py2
  python2 setup.py build
}

check() {
  cd pep517-$pkgver
  # setup.py: https://github.com/pypa/pep517/issues/35
  # tests/test_meta.py::test_classic_package: error in pip install command
  pytest --deselect setup.py --deselect tests/test_meta.py::test_classic_package
}

package_python-pep517() {
  depends=('python-tomli')

  cd pep517-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-pep517() {
  depends=('python2-toml' 'python2-importlib-metadata' 'python2-zipp')

  cd pep517-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
