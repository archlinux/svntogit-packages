# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

# NOTE: requires rebuilt with each new gcc version

pkgname=libtool
pkgver=2.4.6+40+g6ca5e224
pkgrel=2
_gccver=7.2.1
pkgdesc='A generic library support script'
arch=(x86_64)
url='http://www.gnu.org/software/libtool"'
license=(GPL)
groups=(base-devel)
depends=(sh tar glibc)
makedepends=("gcc=$_gccver" git help2man)
provides=("libltdl=$pkgver" "libtool-multilib=$pkgver")
conflicts=('libltdl' 'libtool-multilib')
replaces=('libltdl' 'libtool-multilib')
_commit=6ca5e224bc7bcc114a9ba2cf5dcf0fbf0ec40c9f
source=(git+https://git.savannah.gnu.org/git/libtool.git#commit=$_commit)
md5sums=('SKIP')


pkgver() {
  cd libtool
  git describe --tags | sed 's/-/+/g;s/^v//'
}

prepare() {
  cd libtool
  ./bootstrap
}

build() {
  cd libtool
  ./configure --prefix=/usr lt_cv_sys_lib_dlsearch_path_spec="/usr/lib /usr/lib32"
  make
}

check() {
  cd libtool
  make check || :
}

package() {
  cd libtool
  make DESTDIR="$pkgdir" install
}
