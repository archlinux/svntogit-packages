# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=bash-completion
pkgver=2.1
pkgrel=4
pkgdesc="Programmable completion for the bash shell"
arch=('any')
url="http://bash-completion.alioth.debian.org/"
license=('GPL')
depends=('bash')
options=('!emptydirs' '!makeflags')
source=('http://anonscm.debian.org/gitweb/?p=bash-completion/bash-completion.git;a=snapshot;h=867282a7341ccfff9a0e8a8ef6bba6e781b66afb;sf=tgz'
#http://bash-completion.alioth.debian.org/files/${pkgname}-${pkgver}.tar.bz2
        bash-4.3.patch bash-completion-lz4.patch)
md5sums=('3b0ea5f328c228c570ee391b4faed06a'
         'f964553680d88a89dbe8a7275e94bd3b'
         '54a801a7a2b7aaae6ad7b3b6419003fe')

prepare() {
  rm -rf ${pkgname}-${pkgver}
  mv ${pkgname}-867282a ${pkgname}-${pkgver}
  cd ${pkgname}-${pkgver}
  patch -p1 -i ../bash-4.3.patch
  patch -p0 -i ../bash-completion-lz4.patch
  autoreconf --install
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

# bash-completion is sourced in /etc/bash.bashrc so that non-bash shell don't source it
  rm "${pkgdir}/etc/profile.d/bash_completion.sh"

# remove Slackware's makepkg completion
  rm "${pkgdir}/usr/share/bash-completion/completions/makepkg"
}
