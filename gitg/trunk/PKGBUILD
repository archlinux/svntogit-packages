# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=gitg
pkgver=3.14.0
pkgrel=1
pkgdesc='A GIT repository viewer based on GTK+'
arch=('i686' 'x86_64')
url='https://git.gnome.org/browse/gitg/'
license=('GPL')
depends=('gtksourceview3' 'git' 'desktop-file-utils' 'libgit2-glib' 'libgee' 'webkitgtk' 'libpeas' 'gtkspell3')
makedepends=('intltool' 'vala' 'gobject-introspection' 'python2')
groups=('gnome-extra')
install=gitg.install
source=(ftp://ftp.gnome.org/pub/GNOME/sources/gitg/${pkgver:0:4}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('8e45a7198896eedd829a20ff8de437a08869d30005638114ca87abd42ffea11b')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make WARN_CFLAGS="-Wall"
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
