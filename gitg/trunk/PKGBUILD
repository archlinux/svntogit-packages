# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=gitg
pkgver=3.15.1
pkgrel=1
pkgdesc='A GIT repository viewer based on GTK+'
arch=('i686' 'x86_64')
url='https://git.gnome.org/browse/gitg/'
license=('GPL')
depends=('gtksourceview3' 'git' 'desktop-file-utils' 'libgit2-glib' 'libgee' 'webkit2gtk' 'libpeas' 'gtkspell3')
makedepends=('intltool' 'vala' 'gobject-introspection' 'python2' 'gnome-common')
groups=('gnome-extra')
install=gitg.install
source=(ftp://ftp.gnome.org/pub/GNOME/sources/gitg/${pkgver:0:4}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('feec5c8ef4f7134900305539ad4f087927497015191f26fd9f0cacfc7379cff3')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr

  # Don't overlink
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' -e 's/    if test "$export_dynamic" = yes && test -n "$export_dynamic_flag_spec"; then/      func_append compile_command " -Wl,-O1,--as-needed"\n      func_append finalize_command " -Wl,-O1,--as-needed"\n\0/' libtool

  make WARN_CFLAGS="-Wall"
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
