# $Id$
# Maintainer: Alexander Baldeck <alexander@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-xfs
pkgver=1.1.1
pkgrel=1
pkgdesc="X.Org X11 xfs font server"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
backup=('etc/X11/fs/config')
depends=('xorg-font-utils' 'libxfont>=1.4.0' 'libfs>=1.0.2' 'libx11>=1.2.2')
makedepends=('pkgconfig' 'xtrans')
source=(${url}/releases/individual/app/xfs-${pkgver}.tar.bz2
        ${url}/releases/individual/app/xfsinfo-1.0.3.tar.bz2
        ${url}/releases/individual/app/fslsfonts-1.0.3.tar.bz2
        ${url}/releases/individual/app/fstobdf-1.0.4.tar.bz2
        ${url}/releases/individual/app/showfont-1.0.3.tar.bz2
        xfs.rc)
md5sums=('e02c6cae689c082b8c98a421df8f8670'
         'aae6fa48b5c2ce9186a5ff4ccc14a1d4'
         '9b50d967ac6d4bae9bffb62a5e527a50'
         'fc1f42aac16d6b76c79a1a3a0fb6a53b'
         '50b466ab6ed4a5b8df5abf1ef844309a'
         '251f5447f64151e9bb096ebc9f999c3f')

build() {
  cd "${srcdir}"
  pushd xfs-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc
  make configdir=/etc/X11/fs
  popd  
    
  for i in xfsinfo-* fslsfonts-* fstobdf-* showfont-*; do
    if [ -d "${i}" ]; then
      pushd "${i}"
      ./configure --prefix=/usr
      make
      popd
    fi
  done
}

package() {
  cd "${srcdir}"
  pushd xfs-${pkgver}
  make configdir=/etc/X11/fs DESTDIR="${pkgdir}" install
  sed -i -e 's|/lib/X11/|/share/|g' "${pkgdir}/etc/X11/fs/config"
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.xfs"
  popd
  
  for i in xfsinfo-* fslsfonts-* fstobdf-* showfont-*; do
    if [ -d "${i}" ]; then
      pushd "${i}"
      make DESTDIR="${pkgdir}" install
      install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.${i%-*}"
      popd
    fi
  done
  install -m755 -d "${pkgdir}/etc/rc.d"
  install -m755 "${srcdir}/xfs.rc" "${pkgdir}/etc/rc.d/xfs"
}
