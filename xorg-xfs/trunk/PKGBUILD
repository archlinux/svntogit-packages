# $Id$
# Maintainer: Alexander Baldeck <alexander@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-xfs
pkgver=1.1.2
pkgrel=1
pkgdesc="X.Org X11 xfs font server"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
backup=('etc/X11/fs/config')
depends=('xorg-font-utils' 'libxfont>=1.4.5' 'libfs>=1.0.2' 'libx11>=1.2.2')
makedepends=('pkgconfig' 'xtrans' 'xorg-util-macros')
source=(${url}/releases/individual/app/xfs-${pkgver}.tar.bz2
        ${url}/releases/individual/app/xfsinfo-1.0.3.tar.bz2
        ${url}/releases/individual/app/fslsfonts-1.0.3.tar.bz2
        ${url}/releases/individual/app/fstobdf-1.0.4.tar.bz2
        ${url}/releases/individual/app/showfont-1.0.3.tar.bz2
        xfs.rc)
sha1sums=('d4a64e12f8a773bd0120977191586ca12baf1ace'
          '21c5f544f34d9164797575da85727558f5e09d33'
          '00177564597ac629207ca597f663b689902817df'
          '5310493f53277fd196f93bbb7f2a204cad8997b7'
          '161bf5f037db70dd75cb327648fbba559e821e76'
          '21277375814e358ec16e8f8330f420e3d4df63e5')

build() {
  cd "${srcdir}"
  pushd xfs-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc
  make configdir=/etc/X11/fs
  popd  
    
  for i in xfsinfo-* fslsfonts-* fstobdf-* showfont-*; do
    if [ -d "${i}" ]; then
      pushd "${i}"
      ./configure --prefix=/usr
      make
      popd
    fi
  done
}

package() {
  cd "${srcdir}"
  pushd xfs-${pkgver}
  make configdir=/etc/X11/fs DESTDIR="${pkgdir}" install
  sed -i -e 's|/lib/X11/|/share/|g' "${pkgdir}/etc/X11/fs/config"
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.xfs"
  popd
  
  for i in xfsinfo-* fslsfonts-* fstobdf-* showfont-*; do
    if [ -d "${i}" ]; then
      pushd "${i}"
      make DESTDIR="${pkgdir}" install
      install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.${i%-*}"
      popd
    fi
  done
  install -m755 -d "${pkgdir}/etc/rc.d"
  install -m755 "${srcdir}/xfs.rc" "${pkgdir}/etc/rc.d/xfs"
}
