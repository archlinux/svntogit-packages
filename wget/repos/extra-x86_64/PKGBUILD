# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=wget
pkgver=1.16
pkgrel=1
pkgdesc="A network utility to retrieve files from the Web"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/wget/wget.html"
license=('GPL3')
depends=('openssl' 'libidn' 'util-linux')
checkdepends=('perl-http-daemon' 'perl-io-socket-ssl' 'python3')
optdepends=('ca-certificates: HTTPS downloads')
backup=('etc/wgetrc')
install=wget.install
source=(ftp://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.xz{,.sig}
        wget-1.16-fix-test-proxied-https-auth.patch)
sha1sums=('08d991acc80726abe57043a278f9da469c454503'
          'SKIP'
          'a324d1910a6440552a3745c873da9e3dff7d7ec9')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i "${srcdir}/wget-1.16-fix-test-proxied-https-auth.patch"
  sed -i 's/--no-check-certificate/& --no-http-keep-alive/' \
    tests/Test-proxied-https-auth.px
  cat >> doc/sample.wgetrc <<EOF

# default root certs location
ca_certificate=/etc/ssl/certs/ca-certificates.crt
EOF
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc --enable-nls --with-ssl=openssl
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
