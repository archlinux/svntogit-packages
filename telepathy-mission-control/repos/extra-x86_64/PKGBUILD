# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Ju Liu <liuju86 at gmail dot com>
# Contributor: Bjorn Lindeijer <bjorn lindeijer nl>
# Contributor: Andreas Zwinkau <beza1e1@web.de>

pkgname=telepathy-mission-control
pkgver=5.16.5
pkgrel=2
pkgdesc="A Telepathy component providing abstraction of some of the details of connection managers."
arch=(x86_64)
url="http://telepathy.freedesktop.org/wiki/Mission Control"
license=(LGPL2.1)
depends=(telepathy-glib dconf libnm)
makedepends=(libxslt python gtk-doc)
source=(https://telepathy.freedesktop.org/releases/$pkgname/$pkgname-$pkgver.tar.gz{,.asc}
        tmc-glib-2.64.patch::"https://gitlab.freedesktop.org/telepathy/telepathy-mission-control/commit/d8dab08f.patch")
sha256sums=('4c6b433b8b9079fd7df3e29aefcad6755a2081a9a634ffb6b33936c7d0d8bd03'
            'SKIP'
            'd28f42c32467c341b2b7fb5ab48e816aa000aeca3d9131edb799740fef8439bd')
validpgpkeys=(38FDAD7D73CE1AE7A16200291F3845DD1432A9FB  # Georgios Kiagiadakis <gkiagia@tolabaki.gr>
              AA33B0D27868E36C151780F0FE0B6D736B1195ED) # Alexandr Akulich <akulichalexander@gmail.com>

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../tmc-glib-2.64.patch # Fix spaces in property name
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --libexecdir=/usr/lib/telepathy \
      --with-connectivity=nm \
      --enable-gtk-doc \
      --disable-upower \
      --disable-static \
      --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
