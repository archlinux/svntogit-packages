# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kgeography
pkgver=20.12.1
pkgrel=1
pkgdesc="Geography Trainer"
url="https://kde.org/applications/education/kgeography/"
arch=(x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kde-education)
depends=(kxmlgui hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools python)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('10dd7c91dbd3bf478b0f7b355cc24cc122eab4c0289e9753e4f4d313b6cb5a8e'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
