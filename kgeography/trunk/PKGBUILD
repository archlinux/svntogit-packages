# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kgeography
pkgver=16.04.2
pkgrel=1
pkgdesc="Geography Trainer"
url="http://kde.org/applications/education/kgeography/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdeedu')
depends=('kxmlgui' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'python' 'kdoctools')
replaces=('kdeedu-kgeography')
conflicts=('kdeedu-kgeography')
source=("http://download.kde.org/stable/applications/${pkgver}/src/kgeography-${pkgver}.tar.xz")
sha1sums=('7af0f6f2816a930c960ee548c1b9811889e16747')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
