# $Id$
# Maintainer: Laurent Carlier <lordheavym@gmail.com>

pkgname=libxfont2
pkgver=2.0.1
pkgrel=1
pkgdesc="X11 font rasterisation library"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('freetype2' 'libfontenc' 'xproto' 'fontsproto')
makedepends=('xorg-util-macros' 'xtrans')
source=(${url}/archive/individual/lib/libXfont2-${pkgver}.tar.bz2{,.sig})
sha256sums=('e9fbbb475ddd171b3a6a54b989cbade1f6f874fc35d505ebc5be426bc6e4db7e'
            'SKIP')
validpgpkeys=('C383B778255613DFDB409D91DB221A6900000011') # Keith Packard <keithp@keithp.com>

build() {
  cd "${srcdir}/libXfont2-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static
  make
}

package() {
  cd "${srcdir}/libXfont2-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
