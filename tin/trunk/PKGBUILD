# $Id$
# Maintainer: dorphell <dorphell@archlinux.org>

pkgname=tin
pkgver=1.8.3
pkgrel=7
pkgdesc="Curses based threaded NNTP and spool based UseNet newsreader"
arch=('i686' 'x86_64')
url="http://www.tin.org"
license=('BSD')
depends=('ncurses' 'libidn' 'icu' 'perl')
source=("ftp://ftp.tin.org/pub/news/clients/tin/stable/$pkgname-$pkgver.tar.bz2"
        'LICENSE')
md5sums=('e6ed45205020665c097556822b0d575b'
         '22de35b1bdc6f0df87c93ae794198b21')

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure --prefix=/usr --mandir=/usr/share/man
  make build || return 1
  make || return 1
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
  rm -f $pkgdir/usr/share/man/man5/mbox.5
  rm -f $pkgdir/usr/share/man/man5/mmdf.5
  mv $pkgdir/usr/bin/url_handler.sh $pkgdir/usr/bin/tin_url_handler.sh
  install -D -m644 $srcdir/LICENSE \
    $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
