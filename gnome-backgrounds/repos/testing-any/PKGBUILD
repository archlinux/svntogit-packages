# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-backgrounds
pkgver=42.0
pkgrel=1
pkgdesc="Background images and data for GNOME"
url="https://gitlab.gnome.org/GNOME/gnome-backgrounds"
arch=(any)
makedepends=(glib2 meson)
license=(CCPL:by-sa)
groups=(gnome)
source=(https://download.gnome.org/sources/$pkgname/${pkgver%%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('4c7fe1a09f459c5f77189a5982524fce05d888944955d679910f234606c1295d')

build() {
  arch-meson $pkgname-$pkgver build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 et:
