# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-backgrounds
pkgver=3.26.2
pkgrel=1
pkgdesc="Background images and data for GNOME"
url="https://git.gnome.org/browse/gnome-backgrounds"
arch=(any)
makedepends=(git glib2 meson)
license=(GPL)
groups=(gnome)
_commit=ebb69030864690831b431a95dcea02dcd143815f  # tags/3.26.2^0
source=("git+https://git.gnome.org/browse/gnome-backgrounds#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  mkdir build
}

build() {
  cd build
  meson --prefix=/usr --buildtype=release ../$pkgname
  ninja
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}
