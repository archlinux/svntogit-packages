# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: AndyRTR <andyrtr@archlinux.org>
# Contributor: Tobias Kieslich <tobias (at) archlinux.org>

pkgname=xfce4-timer-plugin
pkgver=1.6.0
pkgrel=1
pkgdesc="Plugin to track time for the Xfce4 panel"
arch=('i686' 'x86_64')
url="http://goodies.xfce.org/projects/panel-plugins/xfce4-timer-plugin"
license=('GPL2')
groups=('xfce4-goodies')
depends=('xfce4-panel' 'libxfcegui4')
makedepends=('intltool')
install=xfce4-timer-plugin.install
source=(http://archive.xfce.org/src/panel-plugins/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=('39d7d21f099bc4219f6a6156142f0bbb8374986ee1970a9c0c8dc138b87f867c')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/ \
    --localstatedir=/var
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
