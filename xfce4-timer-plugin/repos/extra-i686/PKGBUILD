# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Tobias Kieslich <tobias (at) archlinux.org>

pkgname=xfce4-timer-plugin
pkgver=1.6.0
pkgrel=1
pkgdesc="Plugin to track time for the Xfce4 panel"
arch=('i686' 'x86_64')
url="http://goodies.xfce.org/projects/panel-plugins/xfce4-timer-plugin"
license=('GPL2')
depends=('xfce4-panel' 'libxfcegui4')
makedepends=('intltool')
groups=('xfce4-goodies')
install=xfce4-timer-plugin.install
source=(http://archive.xfce.org/src/panel-plugins/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2)
sha1sums=('aa37ccf7dad36000f413fd7dd29068da4858c8fe')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--libexecdir=/usr/lib/ \
	--localstatedir=/var
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
