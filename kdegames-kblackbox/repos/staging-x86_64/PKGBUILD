# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-kblackbox
pkgver=4.10.4
pkgrel=1
pkgdesc="A game of hide and seek played on a grid of boxes"
url="http://kde.org/applications/games/kblackbox/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'libkdegames')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/kblackbox-${pkgver}.tar.xz")
sha1sums=('dd19f0c54740e5e0add1271ebd6595f237a670d5')

build() {
  mkdir build
  cd build
  cmake ../kblackbox-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
