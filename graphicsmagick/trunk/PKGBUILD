# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>

pkgname=graphicsmagick
pkgver=1.3.7
pkgrel=2
pkgdesc="Image processing system"
arch=('i686' 'x86_64')
url="http://www.graphicsmagick.org/"
license=('MIT')
depends=('bzip2' 'freetype2' 'ghostscript' 'jasper' 'lcms' 'libsm' 
	'libtiff' 'libwmf' 'libxml2' 'libtool')
options=('!libtool' 'force')
#source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/GraphicsMagick-${pkgver}.tar.bz2)
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-history/1.3/GraphicsMagick-${pkgver}.tar.gz)
md5sums=('ac019a9ece8c45f5213bf31981588c63')

build() {
  cd "${srcdir}/GraphicsMagick-${pkgver}"

  ./configure --prefix=/usr --enable-shared --with-gs-font-dir=/usr/share/fonts/Type1 --with-quantum-depth=16
  make || return 1
  make DESTDIR="${pkgdir}" install

  # Install MIT license
  install -Dm644 "Copyright.txt" "${pkgdir}/usr/share/licenses/${pkgname}/Copyright.txt" || return 1

  # Remove perllocal.pod and .packlist
  find "${pkgdir}" -name perllocal.pod -delete
  find "${pkgdir}" -name .packlist -delete
}
