# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-kmahjongg
pkgver=15.11.80
pkgrel=1
pkgdesc="A tile matching game for one or two players"
url="http://kde.org/applications/games/kmahjongg/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegames')
depends=('kdebase-runtime' 'libkmahjongg4' 'libkdegames4')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/unstable/applications/${pkgver}/src/kmahjongg-${pkgver}.tar.xz")
sha1sums=('d872cfe9841d839fc21e054b48877fd4253e478e')

build() {
  mkdir -p build
  cd build
  cmake ../kmahjongg-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
