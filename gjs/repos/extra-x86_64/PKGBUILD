# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=gjs
pkgver=1.68.1
pkgrel=1
epoch=2
pkgdesc="Javascript Bindings for GNOME"
url="https://wiki.gnome.org/Projects/Gjs"
arch=(x86_64)
license=(GPL)
depends=(cairo gobject-introspection-runtime js78 dconf readline)
makedepends=(gobject-introspection git meson dbus)
checkdepends=(valgrind xorg-server-xvfb gtk3 gtk4)
provides=(libgjs.so)
_commit=1639d90f350aa271cb84478c8ae69c1aa2abb419  # tags/1.68.1^0
source=("git+https://gitlab.gnome.org/GNOME/gjs.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname

  # Fix _LocalFilePrototype
  git cherry-pick -n ec9385b89cb2bce0615093c3c957cbbb5ea6b769
}

build() {
  arch-meson $pkgname build \
    --buildtype release \
    -D installed_tests=false \
    -D profiler=disabled
  meson compile -C build
}

check() {
  xvfb-run -s '-screen 0 1920x1080x24 -nolisten local' \
    meson test -C build --print-errorlogs
}

package() {
  depends+=(libreadline.so)
  DESTDIR="$pkgdir" meson install -C build
}
