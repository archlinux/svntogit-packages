# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=gjs
pkgver=1.45.4
pkgrel=1
pkgdesc="Javascript Bindings for GNOME"
arch=(i686 x86_64)
url="http://live.gnome.org/Gjs"
license=(GPL)
depends=(cairo gobject-introspection-runtime 'js>=24.2.0' 'gtk3')
makedepends=('gobject-introspection')
source=(https://download.gnome.org/sources/gjs/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('db51bc2aae51700a2a8ca8ba597c488505d106bc70b9605e77c6ab5183212228')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-static --libexecdir=/usr/lib
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd $pkgname-$pkgver
  # Needs a display
  make -k check || :
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
