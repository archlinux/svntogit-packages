# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Justin "juster" Davis <jrcd83@gmail.com>

pkgname=perl-net-http
pkgver=6.22
pkgrel=2
pkgdesc="Low-level HTTP connection (client)"
arch=('any')
url=https://search.cpan.org/dist/Net-HTTP
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl')
checkdepends=('perl-uri')
conflicts=('perl-libwww<6')
source=("https://cpan.metacpan.org/authors/id/O/OA/OALDERS/Net-HTTP-$pkgver.tar.gz")
sha512sums=('8d0af85baee8a3b257995c6dd277c2eb578d1feeec89f3e4be0548959d2cf07934764d9d16a542a50f72a4611e3f4b6335ff7e969a28cd74fbb0ec3961588131')

build() {
    export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor"     \
      PERL_MB_OPT="--installdirs vendor" \
      MODULEBUILDRC=/dev/null

    cd Net-HTTP-${pkgver}
    /usr/bin/perl Makefile.PL
    make
}

check() {
    export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    cd Net-HTTP-${pkgver}
    make test
}

package() {
    cd Net-HTTP-${pkgver}
    make DESTDIR="$pkgdir" install
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
