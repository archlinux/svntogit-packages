# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Justin "juster" Davis <jrcd83@gmail.com>

pkgname=perl-net-http
pkgver=6.17
pkgrel=1
pkgdesc="Low-level HTTP connection (client)"
arch=('any')
url=http://search.cpan.org/dist/Net-HTTP
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl')
checkdepends=('perl-uri')
conflicts=('perl-libwww<6')
source=("http://cpan.metacpan.org/authors/id/O/OA/OALDERS/Net-HTTP-$pkgver.tar.gz")
sha512sums=('ce4453896fea1194342ed5279173fe3289ae7a2f489145930bd74eb680a347d974675b65d387aa7735d2c5404d7c5a34049b10a1ba87f36df0ad6e3d45d4f2f8')

build() {
    export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor"     \
      PERL_MB_OPT="--installdirs vendor" \
      MODULEBUILDRC=/dev/null

    cd Net-HTTP-${pkgver}
    /usr/bin/perl Makefile.PL
    make
}

check() {
    export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    cd Net-HTTP-${pkgver}
    make test
}

package() {
    cd Net-HTTP-${pkgver}
    make DESTDIR="$pkgdir" install
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
