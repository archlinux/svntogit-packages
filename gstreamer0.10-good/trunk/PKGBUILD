# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=gstreamer0.10-good
pkgname=('gstreamer0.10-good' 'gstreamer0.10-good-plugins')
pkgver=0.10.23
pkgrel=1
arch=('i686' 'x86_64')
license=('LGPL')
makedepends=('intltool' 'pkgconfig' 'gstreamer0.10-base>=0.10.29' 'libavc1394>=0.5.3-3' 'libiec61883>=1.2.0' 'aalib>=1.4rc5-6' 'libshout>=2.2.2' 'libdv>=1.0.0' 'esound>=0.2.41' 'flac>=1.2.1' 'gconf>=2.28.1' 'wavpack>=4.60.1' 'taglib>=1.6.3' 'libsoup-gnome>=2.30.1' 'libv4l>=0.6.4' 'libcaca>=0.99.beta17' 'bzip2>=1.0.5' 'gtk2>=2.20.1')
url="http://gstreamer.freedesktop.org/"
options=(!libtool !emptydirs)
source=(${url}/src/gst-plugins-good/gst-plugins-good-${pkgver}.tar.bz2)
sha256sums=('2e7771a611cedb1e8208601cb26744fbf33109598e222afee1be8811ba4babcf')

build() {
  cd "${srcdir}/gst-plugins-good-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --enable-experimental \
    --disable-schemas-install \
    --disable-hal \
    --with-package-name="GStreamer Good Plugins (Archlinux)" \
    --with-package-origin="http://www.archlinux.org/"

  make
  sed -e 's/gst sys ext/gst/' -i Makefile
}

package_gstreamer0.10-good() {
  depends=('gstreamer0.10-base>=0.10.29' 'bzip2>=1.0.5')
  pkgdesc="GStreamer Multimedia Framework Good plugin libraries"
  conflicts=('gstreamer0.10-bad<=0.10.17')

  cd "${srcdir}/gst-plugins-good-${pkgver}"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1
  rm -rf "${pkgdir}/etc/gconf"
}

package_gstreamer0.10-good-plugins() {
  depends=("gstreamer0.10-good=${pkgver}" 'libavc1394>=0.5.3-3' 'libiec61883>=1.2.0' 'aalib>=1.4rc5-6' 'libshout>=2.2.2' 'libdv>=1.0.0' 'esound>=0.2.41' 'flac>=1.2.1' 'gconf>=2.28.1' 'wavpack>=4.60.1' 'taglib>=1.6.3' 'libsoup-gnome>=2.30.1' 'libv4l>=0.6.3' 'libcaca>=0.99.beta17' 'libpng>=1.4.1' 'libjpeg>=8.0.1')
  pkgdesc="GStreamer Multimedia Framework Good Plugins (gst-plugins-good)"
  groups=('gstreamer0.10-plugins')
  replaces=('gstreamer0.10-aalib' 'gstreamer0.10-wavpack' 'gstreamer0.10-shout2' 'gstreamer0.10-taglib' 'gstreamer0.10-libcaca' 'gstreamer0.10-libpng' 'gstreamer0.10-jpeg' 'gstreamer0.10-cairo' 'gstreamer0.10-flac' 'gstreamer0.10-speex' 'gstreamer0.10-gdkpixbuf' 'gstreamer0.10-dv1394' 'gstreamer0.10-annodex' 'gstreamer0.10-gconf' 'gstreamer0.10-esd' 'gstreamer0.10-cdio' 'gstreamer0.10-dv' 'gstreamer0.10-soup')
  conflicts=('gstreamer0.10-aalib' 'gstreamer0.10-wavpack' 'gstreamer0.10-shout2' 'gstreamer0.10-taglib' 'gstreamer0.10-libcaca' 'gstreamer0.10-libpng' 'gstreamer0.10-jpeg' 'gstreamer0.10-cairo' 'gstreamer0.10-flac' 'gstreamer0.10-speex' 'gstreamer0.10-gdkpixbuf' 'gstreamer0.10-dv1394' 'gstreamer0.10-annodex' 'gstreamer0.10-gconf' 'gstreamer0.10-esd' 'gstreamer0.10-cdio' 'gstreamer0.10-dv' 'gstreamer0.10-bad-plugins<0.10.7' 'gstreamer0.10-soup')
  install=gstreamer0.10-good-plugins.install

  cd "${srcdir}/gst-plugins-good-${pkgver}"
  make -C sys DESTDIR="${pkgdir}" install || return 1
  make -C ext GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1
  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  install -m644 gconf/gstreamer-0.10.schemas "${pkgdir}/usr/share/gconf/schemas/gstreamer0.10-good-plugins.schemas" || return 1
}
