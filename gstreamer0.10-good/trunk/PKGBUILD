# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=gstreamer0.10-good
pkgname=('gstreamer0.10-good' 'gstreamer0.10-good-plugins')
pkgver=0.10.21
pkgrel=1
arch=('i686' 'x86_64')
license=('LGPL')
makedepends=('intltool' 'pkgconfig' 'gstreamer0.10-base>=0.10.28' 'libavc1394>=0.5.3-3' 'libiec61883>=1.2.0' 'aalib>=1.4rc5-5' 'libshout>=2.2.2' 'libdv>=1.0.0' 'esound>=0.2.41' 'flac>=1.2.1' 'gconf>=2.28.0' 'wavpack>=4.50.1' 'taglib>=1.5' 'libsoup-gnome>=2.28.2' 'libv4l>=0.6.0' 'libcaca>=0.99.beta16' 'bzip2' 'gtk2>=2.18.6')
url="http://gstreamer.freedesktop.org/"
options=(!libtool !emptydirs)
source=(${url}/src/gst-plugins-good/gst-plugins-good-${pkgver}.tar.bz2
        gst-png14.patch)
sha256sums=('24445a1cf56302ab4ad5f56055d5c3d81c704b8f9f6875fe78a25d4f72716d22'
            '6528ce748ef57ef1cce084a53785c843504607ee3cd055b6b7752bde8eec171f')

build() {
  cd "${srcdir}/gst-plugins-good-${pkgver}"
  patch -Np1 -i "${srcdir}/gst-png14.patch" || return 1
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --enable-experimental \
    --disable-schemas-install \
    --disable-hal \
    --with-package-name="GStreamer Good Plugins (Archlinux)" \
    --with-package-origin="http://www.archlinux.org/" || return 1

  make || return 1
  sed -e 's/gst sys ext/gst/' -i Makefile || return 1
}

package_gstreamer0.10-good() {
  depends=('gstreamer0.10-base>=0.10.28' 'bzip2')
  pkgdesc="GStreamer Multimedia Framework Good plugin libraries"
  conflicts=('gstreamer0.10-bad<=0.10.17')

  cd "${srcdir}/gst-plugins-good-${pkgver}"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1
  rm -rf "${pkgdir}/etc/gconf"
}

package_gstreamer0.10-good-plugins() {
  depends=("gstreamer0.10-good=${pkgver}" 'libavc1394>=0.5.3-3' 'libiec61883>=1.2.0' 'aalib>=1.4rc5-5' 'libshout>=2.2.2' 'libdv>=1.0.0' 'esound>=0.2.41' 'flac>=1.2.1' 'gconf>=2.28.0' 'wavpack>=4.50.1' 'taglib>=1.5' 'libsoup-gnome>=2.28.0' 'libv4l>=0.6.0' 'libcaca>=0.99.beta16' 'libpng>=1.4.0' 'libjpeg>=8')
  pkgdesc="GStreamer Multimedia Framework Good Plugins (gst-plugins-good)"
  groups=('gstreamer0.10-plugins')
  replaces=('gstreamer0.10-aalib' 'gstreamer0.10-wavpack' 'gstreamer0.10-shout2' 'gstreamer0.10-taglib' 'gstreamer0.10-libcaca' 'gstreamer0.10-libpng' 'gstreamer0.10-jpeg' 'gstreamer0.10-cairo' 'gstreamer0.10-flac' 'gstreamer0.10-speex' 'gstreamer0.10-gdkpixbuf' 'gstreamer0.10-dv1394' 'gstreamer0.10-annodex' 'gstreamer0.10-gconf' 'gstreamer0.10-esd' 'gstreamer0.10-cdio' 'gstreamer0.10-dv' 'gstreamer0.10-soup')
  conflicts=('gstreamer0.10-aalib' 'gstreamer0.10-wavpack' 'gstreamer0.10-shout2' 'gstreamer0.10-taglib' 'gstreamer0.10-libcaca' 'gstreamer0.10-libpng' 'gstreamer0.10-jpeg' 'gstreamer0.10-cairo' 'gstreamer0.10-flac' 'gstreamer0.10-speex' 'gstreamer0.10-gdkpixbuf' 'gstreamer0.10-dv1394' 'gstreamer0.10-annodex' 'gstreamer0.10-gconf' 'gstreamer0.10-esd' 'gstreamer0.10-cdio' 'gstreamer0.10-dv' 'gstreamer0.10-bad-plugins<0.10.7' 'gstreamer0.10-soup')
  install=gstreamer0.10-good-plugins.install

  cd "${srcdir}/gst-plugins-good-${pkgver}"
  make -C sys DESTDIR="${pkgdir}" install || return 1
  make -C ext GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1
  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  install -m644 gconf/gstreamer-0.10.schemas "${pkgdir}/usr/share/gconf/schemas/gstreamer0.10-good-plugins.schemas" || return 1
}
