# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Caleb Cushing <xenoterracide@gmail.com>

pkgname=perl-file-sharedir
_realname=File-ShareDir
pkgver=1.102
pkgrel=4
pkgdesc="Locate per-dist and per-module shared files"
arch=(any)
license=(PerlArtistic GPL)
depends=(perl-class-inspector)
url="http://search.cpan.org/dist/File-ShareDir"
options=('!emptydirs')
_author=REHSACK
source=("http://search.cpan.org/CPAN/authors/id/${_author::1}/${_author::2}/${_author}/$_realname-$pkgver.tar.gz")
md5sums=('553157f0e81cd2dabfe140ceb06cd03f')

build() {
  cd "$srcdir/$_realname-$pkgver"
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd "$srcdir/$_realname-$pkgver"
  make DESTDIR="$pkgdir" install
}
