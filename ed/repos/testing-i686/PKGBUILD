# $Id$
# Maintainer: Dave Reisner <dreisner@archlinux.org>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=ed
pkgver=1.8
pkgrel=2
pkgdesc="A POSIX-compliant line-oriented text editor"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnu.org/software/ed/ed.html"
depends=('glibc' 'sh')
options=('!emptydirs')
source=("ftp://ftp.gnu.org/gnu/ed/$pkgname-$pkgver.tar.gz"{,.sig})
install='ed.install'
md5sums=('2268d2344b3c52d23730acb2e3c942fe'
         'SKIP')

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/ \
              --bindir=/usr/bin \
              --mandir=/usr/share/man \
              --infodir=/usr/share/info \
              "CFLAGS=$CFLAGS" \
              "LDFLAGS=$LDFLAGS"

  make
}

check() {
  make -C "$pkgname-$pkgver" check
}

package() {
  make -C "$pkgname-$pkgver" "DESTDIR=$pkgdir" install
}
