# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kwebkitpart
pkgver=1.2.0.20120705
pkgrel=1
pkgdesc="A WebKit browser component for KDE"
url="http://opendesktop.org/content/show.php?content=127960"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("ftp://ftp.archlinux.org/other/${pkgname}/${pkgname}-${pkgver}.tar.gz")
md5sums=('659f9737bcc79f0bfbd08ff37ad0e232')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
