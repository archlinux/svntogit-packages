# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kwebkitpart
pkgver=1.3.4
pkgrel=1
pkgdesc="A WebKit browser component for KDE"
url="https://projects.kde.org/projects/extragear/base/kwebkitpart/"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('kdelibs')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
# Generate using kde releaseme scripts
source=("ftp://ftp.archlinux.org/other/packages/${pkgname}/${pkgname}-${pkgver}.tar.xz")
md5sums=('0a3e176a7c24f00e22c92a7b105585f8')

build() {
  sed -i '/add_subdirectory(kdelauncher)/d' ${pkgname}-${pkgver}/CMakeLists.txt

  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
