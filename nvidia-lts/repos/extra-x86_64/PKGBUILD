# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=nvidia-lts
pkgver=455.45.01
pkgrel=9
epoch=1
pkgdesc="NVIDIA drivers for linux-lts"
arch=('x86_64')
url="https://www.nvidia.com/"
makedepends=('linux-lts-headers' "nvidia-dkms=$pkgver")
provides=('NVIDIA-MODULE')
license=('custom')
options=('!strip')

build() {
    _kernver=$(</usr/src/linux-lts/version)

    fakeroot dkms build --dkmstree "${srcdir}" -m nvidia/${pkgver} -k ${_kernver}
}

package() {
    depends=('linux-lts' "nvidia-utils=${pkgver}" 'libglvnd')

    _kernver="$(</usr/src/linux-lts/version)"

    install -Dt "${pkgdir}/usr/lib/modules/${_kernver}/extramodules" -m644 \
        nvidia/${pkgver}/${_kernver}/${CARCH}/module/*

    # compress each module individually
    find "$pkgdir" -name '*.ko' -exec xz -T1 {} +

    echo "blacklist nouveau" |
        install -Dm644 /dev/stdin "${pkgdir}/usr/lib/modprobe.d/${pkgname}.conf"

    install -Dt "${pkgdir}/usr/share/licenses/${pkgname}" -m644 \
        /usr/share/licenses/nvidia-dkms/LICENSE
}












