# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>

pkgname=nvidia-lts
pkgver=260.19.36
_kernver='2.6.32-lts'
pkgrel=2
pkgdesc="NVIDIA drivers for kernel26-lts"
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=('kernel26-lts>=2.6.32' 'kernel26-lts<2.6.33' "nvidia-utils=$pkgver")
makedepends=('kernel26-lts-headers>=2.6.32' 'kernel26-lts-headers<2.6.33')
provides=('nvidia')
license=('custom')
install=nvidia-lts.install
if [ "$CARCH" = "i686" ]; then
  _arch='x86'
  _pkg="NVIDIA-Linux-${_arch}-${pkgver}"
  source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
  md5sums=('2826484d87827351774449382e45c4a9')
elif [ "$CARCH" = "x86_64" ]; then
  _arch='x86_64'
  _pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
  source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
  md5sums=('6ebc5db9066e920b0b0e48377ce5eeb5')
fi

build() {
  cd "${srcdir}"
  sh ${_pkg}.run --extract-only
  cd ${_pkg}/kernel
  make SYSSRC=/lib/modules/${_kernver}/build module
}

package() {
  install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" \
    "${pkgdir}/lib/modules/${_kernver}/kernel/drivers/video/nvidia.ko"
  install -d -m755 "${pkgdir}/etc/modprobe.d"
  echo "blacklist nouveau" >> "${pkgdir}/etc/modprobe.d/nouveau_blacklist-lts.conf"
  install -D -m644 "${srcdir}/${_pkg}/LICENSE" "${pkgdir}/usr/share/licenses/nvidia-lts/LICENSE"
}
