# $Id$
# Maintainer:
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=cfitsio
pkgver=3.390
pkgrel=1
pkgdesc="A library of C and Fortran subroutines for reading and writing data files in FITS (Flexible Image Transport System) data format"
arch=(i686 x86_64)
url="http://heasarc.gsfc.nasa.gov/fitsio/"
license=(custom)
depends=(glibc)
source=("ftp://heasarc.gsfc.nasa.gov/software/fitsio/c/${pkgname}${pkgver/./}.tar.gz")
sha512sums=('9e72fe58e1b2e45dfad23c5a60f9056a4e958b3b567751c5284d5d89992f0a2721aa44ede8655f0e1595eaea9d6ba4644b001c54274764f9d05f890d471e4534')

build() {
  cd $pkgname
  ./configure --prefix=/usr \
    --enable-reentrant
  make shared

  # Build extra utils
  make fpack
  make funpack
}

check() {
  cd $pkgname
  make testprog
  LD_LIBRARY_PATH=. ./testprog > testprog.lis
  [[ -z $(diff testprog.lis testprog.out) ]] || return 1
  [[ -z $(cmp testprog.fit testprog.std) ]] || return 1
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install

  install -d "$pkgdir"/usr/bin
  install -m755 {fpack,funpack} "$pkgdir"/usr/bin/

  install -D -m644 License.txt \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
