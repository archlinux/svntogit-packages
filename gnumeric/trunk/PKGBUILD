# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Kritoke <typeolinux@yahoo.com>

pkgname=gnumeric
pkgver=1.12.43
pkgrel=1
pkgdesc="A GNOME Spreadsheet Program"
arch=('x86_64')
url="http://www.gnome.org/projects/gnumeric/"
license=('GPL')
depends=('goffice')
makedepends=('intltool' 'psiconv' 'pygobject-devel' docbook-xml
             'gobject-introspection' 'python2-gobject' gtk-doc autoconf-archive git yelp-tools)
optdepends=('python2-gobject: for python2 plugin support'
            'perl: for perl plugin support'
            'psiconv: for Psion 5 file support'
            'yelp: for viewing the help manual')
_commit=4c4674d02c00da73b1896b0f9513d86357ccf927  # tags/GNUMERIC_1_12_43^0
source=("git+https://gitlab.gnome.org/GNOME/gnumeric.git#commit=$_commit"
        revert-warnings.patch)
sha256sums=('SKIP'
            'bcafca016b809000c2a5bf911e2e3dfa4de28f9e541d9964574cac5c7ce09e53')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^GNUMERIC_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd $pkgname
  git cherry-pick -n fdb43346579e9db9122609b1b390d59a94ecd782  
  sed 's/@GIOVERRIDESDIR@/$(DESTDIR)@GIOVERRIDESDIR@/g' -i introspection/Makefile.am
  sed 's/python -c/$PYTHON -c/' -i configure.ac
  patch -Np0 -i ../revert-warnings.patch
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-schemas-compile --enable-introspection --disable-silent-rules

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname
  make DESTDIR="${pkgdir}" install
}
