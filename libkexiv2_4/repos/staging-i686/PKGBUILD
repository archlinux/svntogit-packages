# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=libkexiv2_4
_pkgname=libkexiv2
pkgver=15.08.3
pkgrel=1
pkgdesc="A library to manipulate pictures metadata"
url="https://projects.kde.org/projects/kde/kdegraphics/libs/libkexiv2"
arch=(i686 x86_64)
license=(GPL LGPL FDL)
depends=(kdelibs)
makedepends=(cmake automoc4)
replaces=('libkexiv2<15.11')
conflicts=('libkexiv2<15.11')
source=("http://download.kde.org/stable/applications/$pkgver/src/$_pkgname-$pkgver.tar.xz")
sha1sums=('09adf7887dc644431a6930ba645162707c16bb3d')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$_pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
