# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
pkgname=libusb
pkgver=1.0.18
pkgrel=1
depends=('glibc' 'systemd')
pkgdesc="Library that provides generic access to USB device"
arch=(i686 x86_64)
url="http://libusb.info/"
license=('LGPL')
source=(http://downloads.sourceforge.net/${pkgname}/releases/${pkgver}/${pkgname}-${pkgver}.tar.bz2)
replaces=('libusb1' 'libusbx')
provides=("libusbx=$pkgver")
conflicts=("libusbx")
md5sums=('4a6d049923efc6496f1d383054dac1a6')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --disable-static
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package () {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
