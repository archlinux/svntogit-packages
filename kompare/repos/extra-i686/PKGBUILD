# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kompare
pkgver=15.08.0
pkgrel=2
pkgdesc='Diff/Patch Frontend'
url='http://kde.org/applications/development/kompare/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdesdk')
depends=('libkomparediff2' 'ktexteditor' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'python' 'kdoctools')
install=${pkgname}.install
conflicts=('kdesdk-kompare')
replaces=('kdesdk-kompare')
source=("http://download.kde.org/stable/applications/${pkgver}/src/kompare-${pkgver}.tar.xz" 'allow-selecting-files.patch')
sha1sums=('0d8d981090a28d99ed2241b6d49b8ebe102fbde6'
          '78b2635618d9e3f54485dc5a456f8ce1cb453b4f')

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
# Allow selecting files https://bugs.kde.org/show_bug.cgi?id=351782
  patch -p1 -i ../allow-selecting-files.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
