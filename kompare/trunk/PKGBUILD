# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kompare
pkgver=16.04.2
pkgrel=1
pkgdesc='Diff/Patch Frontend'
url='http://kde.org/applications/development/kompare/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdesdk')
depends=('libkomparediff2' 'ktexteditor' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'python' 'kdoctools')
conflicts=('kdesdk-kompare')
replaces=('kdesdk-kompare')
source=("http://download.kde.org/stable/applications/${pkgver}/src/kompare-${pkgver}.tar.xz")
sha1sums=('3eb36e00e5db9858cf2055a48f8c7b3e857062fd')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
