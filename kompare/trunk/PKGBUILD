# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kompare
pkgver=15.04.0
pkgrel=2
pkgdesc='Diff/Patch Frontend'
url='http://kde.org/applications/development/kompare/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdesdk')
depends=('libkomparediff2' 'ktexteditor' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'python' 'kdoctools')
install=${pkgname}.install
conflicts=('kdesdk-kompare')
replaces=('kdesdk-kompare')
source=("http://download.kde.org/stable/applications/${pkgver}/src/kompare-${pkgver}.tar.xz" 'desktop.patch')
sha1sums=('c35f3ee00012ac48d3042ab05dc31c610bea76ba'
          'e7301d8ee9a487a7e8e9fd6c46857890e640be4e')

prepare() {
  mkdir build

  cd $pkgname-$pkgver
# Fix launching from the .desktop file
  patch -p1 -i "$srcdir"/desktop.patch
}

build() {
  cd build
  cmake ../kompare-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
