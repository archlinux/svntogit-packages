# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Camille Moncelier <pix@devlife.org>

pkgname=m17n-db
pkgver=1.6.5
pkgrel=2
pkgdesc='Multilingual text processing library (database)'
url='http://www.nongnu.org/m17n/'
arch=('any')
license=('GPL')
depends=('glibc') # not because of compiling, but because of /usr/share/i18n/charmaps/ files that glibc provides
source=("http://download.savannah.gnu.org/releases/m17n/${pkgname}-${pkgver}.tar.gz")
sha1sums=('39948b2b93d352d6b3a6760bfde40e1482baf864')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	# See FS#41265
	cd "${pkgdir}/usr/share/m17n"
	install -d icons
	mv *.png icons
}
