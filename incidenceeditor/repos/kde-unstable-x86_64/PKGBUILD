# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=incidenceeditor
pkgver=19.03.80
pkgrel=1
pkgdesc="KDE PIM incidence editor"
arch=(x86_64)
url="https://kontact.kde.org"
license=(LGPL)
depends=(eventviews)
makedepends=(extra-cmake-modules boost)
conflicts=('libkdepim<16')
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('86d83ff1b7489075f99dd2abfd08c6d4dece046b5a9a009125ce1d6048f2b2c0'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
