# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: György Balló <ballogy@freestart.hu>

pkgname=zeitgeist
pkgver=0.9.15
pkgrel=4
pkgdesc="Service logging user activities and events"
arch=(i686 x86_64)
url="http://zeitgeist-project.com/"
license=(GPL2 LGPL2.1)
depends=(json-glib telepathy-glib gtk3 xapian-core)
makedepends=(intltool gobject-introspection vala raptor python2-rdflib gnome-common git)
provides=(zeitgeist-datahub)
conflicts=(zeitgeist-datahub)
replaces=(zeitgeist-datahub)
source=("$pkgname::git://anongit.freedesktop.org/$pkgname/$pkgname#commit=63dba18c")
sha256sums=('SKIP')

prepare() {
  cd $pkgname

  sed -i 's/python -/$PYTHON -/' configure.ac
  sed -i '1s/python$/python2/' data/ontology2code

  mkdir m4
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  export PYTHON=/usr/bin/python2
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib/$pkgname --enable-fts
  make
}

check() {
  cd $pkgname
  # Log test fails in chroot, as we have no X and no proper dbus
  make -k check || :
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
