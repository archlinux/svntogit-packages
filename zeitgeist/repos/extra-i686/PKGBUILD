# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: György Balló <ballogy@freestart.hu>

pkgname=zeitgeist
pkgver=0.9.16
pkgrel=2
pkgdesc="Service logging user activities and events"
arch=(i686 x86_64)
url="http://zeitgeist-project.com/"
license=(GPL2 LGPL2.1)
depends=(json-glib telepathy-glib gtk3 xapian-core)
makedepends=(intltool gobject-introspection vala raptor python2-rdflib gnome-common)
provides=(zeitgeist-datahub)
conflicts=(zeitgeist-datahub)
replaces=(zeitgeist-datahub)
source=(https://launchpad.net/zeitgeist/${pkgver:0:3}/${pkgver}/+download/zeitgeist-${pkgver}.tar.xz{,.asc})
md5sums=('f56b58a19d5fd5a7365ddcbe26ee6b15'
         'SKIP')
validpgpkeys=('E4884AEEDE4CC02043C3D8045DECDBA89270E723') #Rico Tzschichholz <ricotz@ubuntu.com>

prepare() {
  cd $pkgname-$pkgver

  sed -i 's/python -/$PYTHON -/' configure.ac
  sed -i '1s/python$/python2/' data/ontology2code

  autoreconf -fi
}

build() {
  cd $pkgname-$pkgver
  export PYTHON=/usr/bin/python2
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib/$pkgname --enable-fts
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' -e 's/    if test "$export_dynamic" = yes && test -n "$export_dynamic_flag_spec"; then/      func_append compile_command " -Wl,-O1,--as-needed"\n      func_append finalize_command " -Wl,-O1,--as-needed"\n\0/' libtool
  make
}

check() {
  cd $pkgname-$pkgver
  # Log test fails in chroot, as we have no X and no proper dbus
  make -k check || :
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
