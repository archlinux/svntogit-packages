# $Id$
# Contributor: Rainy <rainylau(at)gmail(dot)com>
# Contributor: Lee.MaRS <leemars at gmail dot com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Maintainer: Brad Fanella <bradfanella@archlinux.us>

pkgname=ibus
pkgver=1.5.3
pkgrel=1
pkgdesc="Next Generation Input Bus for Linux"
arch=('i686' 'x86_64')
url="http://ibus.googlecode.com"
license=('LGPL')
depends=('hicolor-icon-theme' 'gtk2' 'gtk3' 'python2-gobject' 'libnotify')
makedepends=('gobject-introspection' 'vala' 'dconf' 'iso-codes' 'intltool')
options=('!libtool')
install=ibus.install
source=(http://ibus.googlecode.com/files/${pkgname}-${pkgver}.tar.gz)
sha1sums=('51ba186cbf5e1e194ba0cd22b81f00d9d40ec521')

build() {
  cd ${pkgname}-${pkgver}
  export PYTHON=python2
  ./configure \
    --prefix=/usr \
    --libexecdir=/usr/lib/ibus \
    --sysconfdir=/etc \
    --disable-gconf \
    --enable-dconf \
    --disable-memconf \
    --enable-ui \
    --enable-python-library
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
