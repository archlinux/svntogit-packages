# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>

pkgbase=ibus
pkgname=(ibus libibus)
pkgver=1.5.4
pkgrel=4
pkgdesc="Next Generation Input Bus for Linux"
arch=('i686' 'x86_64')
url="http://ibus.googlecode.com"
license=('LGPL')
depends=('dconf' 'gtk2' 'gtk3' 'hicolor-icon-theme' 'libnotify' 'pygtk' 'python2-dbus')
makedepends=('gobject-introspection' 'vala' 'gconf' 'iso-codes' 'intltool' 'python2-gobject')
options=('!emptydirs')
source=(http://ibus.googlecode.com/files/${pkgbase}-${pkgver}.tar.gz)
sha1sums=('2ce4b4d728a4685fd77360d0097b0e568f920c7b')

build() {
  cd ${pkgbase}-${pkgver}
  export PYTHON=python2
  ./configure \
    --prefix=/usr \
    --libexecdir=/usr/lib/ibus \
    --sysconfdir=/etc \
    --disable-gconf \
    --enable-dconf \
    --disable-memconf \
    --enable-ui \
    --enable-python-library
  make
}

package_ibus() {
  depends+=("libibus=$pkgver" 'python2-gobject' 'iso-codes')
  install=ibus.install

  cd ${pkgbase}-${pkgver}
  make DESTDIR="${pkgdir}" install
  make -C src DESTDIR="${pkgdir}" uninstall
  make -C bindings DESTDIR="${pkgdir}" uninstall
  make DESTDIR="${pkgdir}" uninstall-pkgconfigDATA
}

package_libibus() {
  pkgdesc="IBus support library"
  depends=('glib2')

  cd ${pkgbase}-${pkgver}
  make -C src DESTDIR="${pkgdir}" install
  make -C bindings DESTDIR="${pkgdir}" install
  make DESTDIR="${pkgdir}" install-pkgconfigDATA
}
