# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>

pkgbase=ibus
pkgname=(ibus libibus)
pkgver=1.5.5
pkgrel=1
pkgdesc="Next Generation Input Bus for Linux"
arch=('i686' 'x86_64')
url="http://ibus.googlecode.com"
license=('LGPL')
depends=('dconf' 'gtk2' 'gtk3' 'hicolor-icon-theme' 'libnotify' 'pygtk' 'python2-dbus' 'python2-gobject' 'iso-codes')
makedepends=('gobject-introspection' 'vala' 'gconf' 'intltool')
options=('!emptydirs')
source=(http://ibus.googlecode.com/files/${pkgbase}-${pkgver}.tar.gz)

build() {
  cd ${pkgbase}-${pkgver}
  export PYTHON=python2
  ./configure \
    --prefix=/usr \
    --libexecdir=/usr/lib/ibus \
    --sysconfdir=/etc \
    --disable-gconf \
    --enable-dconf \
    --disable-memconf \
    --enable-ui \
    --enable-python-library
  make
}

package_ibus() {
  depends+=("libibus=$pkgver")
  install=ibus.install

  cd ${pkgbase}-${pkgver}
  make DESTDIR="${pkgdir}" install
  make -C src DESTDIR="${pkgdir}" uninstall
  make -C bindings DESTDIR="${pkgdir}" uninstall
  make DESTDIR="${pkgdir}" uninstall-pkgconfigDATA
}

package_libibus() {
  pkgdesc="IBus support library"
  depends=('glib2')

  cd ${pkgbase}-${pkgver}
  make -C src DESTDIR="${pkgdir}" install
  make -C bindings DESTDIR="${pkgdir}" install
  make DESTDIR="${pkgdir}" install-pkgconfigDATA
}

sha512sums=('8249d6ee4918b0e0fe030af736eed2260e1376b2fdaf0d819f289e0bd880a5e3874f1fb3d097fc74b9f359822ce16f4dae52a263aa28c2e2ce7bdf671cb2fea9')
