# $Id$
# Contributor: John Proctor <jproctor@prium.net>
# Maintainer: juergen <juergen@archlinux.org>

pkgname=ecl
pkgver=16.0.0
pkgrel=1
pkgdesc="Embeddable Common Lisp"
arch=('i686' 'x86_64')
url="https://common-lisp.net/project/ecl/"
license=('LGPL')
depends=('bash' 'gmp')
makedepends=('texinfo')
provides=('common-lisp' 'cl-asdf')
options=('!makeflags')
source=(https://common-lisp.net/project/ecl/files/ecl-${pkgver}.tgz)
sha1sums=('3309b5f4126d1a024f7eacaec6aca0e391dd3c75')

build() {
  cd $srcdir/$pkgname-$pkgver
  sed -i 's|-Wl,--rpath,~A|-Wl,--rpath,/usr/lib/ecl|' src/configure 
  ./configure \
    --build=$CHOST \
    --prefix=/usr \
    --with-tcp \
    --with-clos-streams \
    --enable-shared \
    --enable-boehm=included \
    --with-system-gmp \
    --without-x \
    --enable-threads \
    --without-clx

  make
}

package() {
  make -C $srcdir/$pkgname-$pkgver DESTDIR=$pkgdir install
}
