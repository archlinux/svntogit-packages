# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Sylvain HENRY <hsyl20@yahoo.fr>

pkgname=opencl-headers
pkgver=2022.05.18
pkgrel=1
epoch=2
pkgdesc='OpenCL (Open Computing Language) header files'
arch=('any')
url='https://www.khronos.org/registry/cl/'
license=('custom')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/KhronosGroup/OpenCL-Headers/archive/v${pkgver}.tar.gz")
sha256sums=('88a1177853b279eaf574e2aafad26a84be1a6f615ab1b00c20d5af2ace95c42e')
optdepends=('opencl-clhpp: C++ support')

package() {
  cd "${srcdir}"/OpenCL-Headers*/CL

  install -dm755 "${pkgdir}"/usr/include/CL

  for h in *.h; do
    install -m 644 ${h} "${pkgdir}"/usr/include/CL/
  done

  # remove useless headers
  rm "${pkgdir}"/usr/include/CL/{cl_d3d,cl_dx9}*.h

  install -D -m644 ../LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
