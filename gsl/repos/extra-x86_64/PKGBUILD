# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Juergen Hoetzel <juergen.archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=gsl
pkgver=2.6
pkgrel=1
pkgdesc="The GNU Scientific Library (GSL) is a modern numerical library for C and C++ programmers"
url="https://www.gnu.org/software/gsl/gsl.html"
source=("https://ftp.gnu.org/gnu/gsl/$pkgname-$pkgver.tar.gz"{,.sig})
license=('GPL')
arch=('x86_64')
depends=('glibc')
sha256sums=('b782339fc7a38fe17689cb39966c4d821236c28018b6593ddb6fd59ee40786a8'
            'SKIP')
validpgpkeys=(DD61B7FC02790F978360F399245FB74BAE05B3E9) # Patrick Alken <alken@colorado.edu>

build() {
    cd ${pkgname}-${pkgver}
    ./configure --prefix=/usr
    make
}

check() {
    cd ${pkgname}-${pkgver}
    make check
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install
}
