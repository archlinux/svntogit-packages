# $Id$
# Maintainer: Ronald van Haren <ronald@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgbase=digikam
pkgname=('digikam' 'kipi-plugins')
_pkgver=5.0.0-beta7
pkgver=${_pkgver//-/_}
pkgrel=1
pkgdesc="Digital photo management application for KDE"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.digikam.org/"
makedepends=('extra-cmake-modules' 'libkipi' 'libksane' 'liblqr' 'opencv' 'boost' 'grantlee-qt5'
             'libgpod' 'hugin' 'opencv' 'doxygen' 'lensfun' 'imagemagick' 'eigen'
             'libusb' 'kio' 'kdoctools' 'marble' 'kfilemetadata' 'akonadi-contact' 'qt5-multimedia' 'kdesignerplugin'
             'threadweaver' 'kcalcore' 'knotifyconfig' 'kdelibs4support' 'qt5-xmlpatterns') # 'libmediawiki' 'libkvkontakte'
source=("http://download.kde.org/unstable/${pkgname}/${pkgname}-${_pkgver}.tar.xz")
sha1sums=('133ed06a3768c1f4d22967f9c28dc6d4488cd2c3')

prepare() {
  mkdir -p build
}

build() {
  cd build

  cmake ../${pkgname}-${_pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DBUILD_TESTING=OFF \
    -DENABLE_KFILEMETADATASUPPORT=ON \
    -DENABLE_MEDIAPLAYER=ON \
    -DENABLE_AKONADICONTACTSUPPORT=ON \
    -DENABLE_MYSQLSUPPORT=ON \
    -DENABLE_OPENCV3=ON
  make
}

package_digikam() {
  pkgdesc="Digital photo management application for KDE"
  depends=('liblqr' 'libkipi' 'lensfun' 'opencv' 'akonadi-contact'
	'knotifyconfig' 'libksane' 'kfilemetadata' 'qt5-multimedia' 'marble' 'threadweaver' 'kcalcore')
  optdepends=('kipi-plugins: more tools and plugins')

  cd build/core
  make DESTDIR="${pkgdir}" install
#  cd ../../build/doc/digikam/digikam
#  make DESTDIR="${pkgdir}" install
#  cd ../../../../build/po
#  make DESTDIR="${pkgdir}" install
}

package_kipi-plugins() {
  pkgdesc="A collection of plugins extending the KDE graphics and image applications as digiKam"
  depends=('kio' 'libkipi')
  optdepends=(# 'libmediawiki: MediaWiki Export plugin'
      #       'libkvkontakte: VKontakte.ru Exporter plugin'
              'libgpod: iPodExport plugin'
              'opencv: Remove Red Eyes plugin'
              'imagemagick: Video SlideShow plugin'
              'qt5-gstreamer: Video SlideShow plugin'
              'threadweaver: panorama plugin'
	      'hugin: panorama plugin')

  cd build/extra
  make DESTDIR="${pkgdir}" install

#  cd ../../../build/po
#  make DESTDIR="${pkgdir}" install

#  cd ../../../build/doc/kipi-plugins
#  make DESTDIR="${pkgdir}" install
}
