# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=libwacom
pkgver=0.28
pkgrel=2
pkgdesc="Library to identify Wacom tablets and their features"
arch=('x86_64')
url="http://linuxwacom.sourceforge.net/wiki/index.php/Libwacom"
license=('MIT')
depends=('glib2' 'systemd' 'libgudev')
validpgpkeys=('3C2C43D9447D5938EF4551EBE23B7E70B467F0BF')
source=(https://sourceforge.net/projects/linuxwacom/files/libwacom/$pkgname-$pkgver.tar.bz2{,.sig}
        fix-pairedid-entry.patch)
sha1sums=('6a6b19a587087412753ab69a2179f5f408f49ba2'
          'SKIP'
          'e9dbc6f8d6922ca02fd7efb003c785307dff2d6e')
sha256sums=('e7d632301288b221cb5af69b4c5e57fd062bafd9a9acd6f9ce271570103267ef'
            'SKIP'
            'ed9a5500359c84d7f8784025185c7e66df7726d52b74170e15c17d64d871d51b')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../fix-pairedid-entry.patch
}
build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -m755 -d ${pkgdir}/usr/lib/udev/rules.d
  cd tools
  ./generate-udev-rules > ${pkgdir}/usr/lib/udev/rules.d/65-libwacom.rules
  
}
