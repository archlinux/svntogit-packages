# $Id$
# Maintainer: 
# Contributor: Alexander Baldeck <alexander@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=zvbi
pkgver=0.2.33
pkgrel=6
pkgdesc="VBI capture and decoding library"
url="http://zapping.sourceforge.net/cgi-bin/view/ZVBI/WebHome"
arch=('i686' 'x86_64')
depends=('libpng' 'libx11')
license=('GPL')
options=('!libtool')
source=("http://downloads.sourceforge.net/sourceforge/zapping/${pkgname}-${pkgver}.tar.bz2"
        'fix-includes.patch'
        'zvbi-0.2.33-libpng15.patch')
md5sums=('1741a6045c3eedfb611d645f2da69ac8'
         '38766bc59e1571133d0399f0102da653'
         '4434de7addc438be8c5a33274146bd11')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -Np1 -i "${srcdir}/fix-includes.patch"
  patch -Np0 -i "${srcdir}/zvbi-0.2.33-libpng15.patch"

  ./configure --prefix=/usr \
              --disable-static \
              --mandir=/usr/share/man
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

        # usrmove
        cd "$pkgdir"
        mv usr/sbin/* usr/bin/
        rmdir usr/sbin
}
