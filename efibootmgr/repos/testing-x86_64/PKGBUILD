# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Murtuza Akhtari <inxsible at gmail dot com>
# Contributor: Keshav Amburay <(the ddoott ridikulus ddoott rat) (aatt) (gemmaeiil) (ddoott) (ccoomm)>

pkgname=efibootmgr
pkgdesc="Linux user-space application to modify the EFI Boot Manager"
pkgver=17
_commit=e067160ecef8208e1944002e5d50b275733211fb # 17
pkgrel=1
arch=('x86_64')
url="https://github.com/rhinstaller/efibootmgr"
license=('GPL2')
depends=('glibc' 'efivar')
makedepends=('git')
source=("${pkgname}::git+https://github.com/rhboot/efibootmgr#tag=${pkgver}?signed")
sha512sums=('SKIP')
validpgpkeys=('B00B48BC731AA8840FED9FB0EED266B70F4FEF10') # Peter Jones <pjones@redhat.com>

prepare() {
  mv -v "${pkgname}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  # removing hotfix function declaration:
  # https://github.com/rhboot/efibootmgr/issues/128
  sed -e '/extern int efi_set_verbose/d' -i "src/${pkgname}.c"
}

build() {
  cd "${pkgname}-${pkgver}"
  make libdir='/usr/lib' sbindir='/usr/bin' EFIDIR='/boot/EFI'
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" \
       libdir='/usr/lib' \
       sbindir='/usr/bin' \
       EFIDIR='/boot/EFI' \
       install
  install -vDm 644 {AUTHORS,README,README.md,TODO} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
