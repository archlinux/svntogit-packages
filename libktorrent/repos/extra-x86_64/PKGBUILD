# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=libktorrent
pkgver=1.3.1
pkgrel=4
pkgdesc="A BitTorrent protocol implementation"
arch=('i686' 'x86_64')
url="http://ktorrent.pwsp.net/"
license=('GPL2')
depends=('kdelibs')
makedepends=('automoc4' 'cmake' 'boost' 'doxygen')
source=("http://ktorrent.pwsp.net/downloads/4.3.1/${pkgname}-${pkgver}.tar.bz2")
sha1sums=('71b377c0cad01dca6061f1fe92f91c4cf05476b3')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
