# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=libktorrent
pkgver=2.0.1
_ktorrentver=5.0
pkgrel=1
pkgdesc="A BitTorrent protocol implementation"
arch=(i686 x86_64)
url="http://ktorrent.pwsp.net/"
license=(GPL2)
depends=(kio qca-qt5)
makedepends=(extra-cmake-modules python boost doxygen)
source=("http://download.kde.org/stable/ktorrent/$_ktorrentver/$pkgname-$pkgver.tar.xz")
sha1sums=('ebf4aedd97741ad534ee272576a4649788209574')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
