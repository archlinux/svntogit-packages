# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=konsole
pkgver=21.08.0
pkgrel=2
arch=(x86_64)
url='https://apps.kde.org/konsole/'
pkgdesc='KDE terminal emulator'
license=(GPL LGPL FDL)
groups=(kde-applications kde-utilities)
depends=(knotifyconfig kpty kparts knewstuff)
makedepends=(extra-cmake-modules kdoctools)
optdepends=('keditbookmarks: to manage bookmarks')
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        https://invent.kde.org/utilities/konsole/-/commit/fb7f838fd3138a39aea3bcb2e91f923741587137.patch
        https://invent.kde.org/utilities/konsole/-/commit/c78edfbac49852cec40efd5cbe73c341bc06c5ab.patch)
sha256sums=('5bdd16aadfe3e45a353dfac31c66cc769232f31dea8d4735a6de61f1384abebf'
            'SKIP'
            '22a50cda9d74f3f2a6d47077b80e7a3e8547e0512200220d7efc5555425ef702'
            '90408f4e67401eadb4b57bf5a4a02b73da6d227c110c57cc75647b311c5d9f7e')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

prepare() {
  # Upstream suggested backports for toolbars and window size
  patch -d $pkgname-$pkgver -p1 < fb7f838fd3138a39aea3bcb2e91f923741587137.patch
  patch -d $pkgname-$pkgver -p1 < c78edfbac49852cec40efd5cbe73c341bc06c5ab.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
