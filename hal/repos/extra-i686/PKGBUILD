# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Link Dupont <link@subpop.net>

pkgname=hal
pkgver=0.5.14
pkgrel=4
pkgdesc="Hardware Abstraction Layer"
arch=(i686 x86_64)
license=('GPL' 'custom')
url="http://www.freedesktop.org/wiki/Software/hal"
depends=('dbus-glib>=0.82' 'libusb>=0.1.12' 'udev>=146' 'filesystem>=0.7.1-5' 'hal-info>=0.20090716' 'eject' 'dmidecode' 'pciutils>=3.0.2' 'usbutils>=0.73-5' 'pm-utils>=1.2.5' 'consolekit>=0.4.1' 'util-linux-ng>=2.16')
makedepends=('pkgconfig' 'gperf')
options=('!libtool' '!makeflags')
install=hal.install
source=(http://hal.freedesktop.org/releases/${pkgname}-${pkgver}.tar.gz
        hal
        hal-0.5.9-hide-diagnostic.patch
        hal-remove-dell-killswitch.patch
        hal-KVM-evdev.patch
        hal-HDAPS-blacklist.patch
        hal-xen-unignore-axes.patch
        hal-use-at-console.patch
        fix-libusb-detection.patch
        dbus-error-init.patch
        path-max.patch
        handle-input-touchpad.patch
        macbook-fix-ioperm.patch)
md5sums=('e9163df591a6f38f59fdbfe33e73bf20'
         '277e96ac130d7bfce0b30f0b80db8782'
         '4d4b6801a1cedca22b8bdd9db73b16fb'
         '6d87c3e63184ae3a69caafc846f538a3'
         '6507e5091ee2d11a87ae738a8e2caecb'
         '1171c2d83b76059f2da7a3538e08fa4e'
         '52bd305299aa22ae07f1a862c22d30fa'
         '3f11234fb5e5044fbfc5199ec65b182c'
         'da088f12cfc2d190bbf95b747e19ab9e'
         '634f4ec2203eff7de8fa2ed2c6b9cbe0'
         'f8c9b3a40d03907b498feef571d42466'
         '0575677614db0632b17b1a719798c7e0'
         '46a5db3ff896ee37762aa8d7e70942c4')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/hal-0.5.9-hide-diagnostic.patch" || return 1
  patch -Np1 -i "${srcdir}/hal-remove-dell-killswitch.patch" || return 1
  patch -Np1 -i "${srcdir}/hal-KVM-evdev.patch" || return 1
  patch -Np1 -i "${srcdir}/hal-HDAPS-blacklist.patch" || return 1
  patch -Np1 -i "${srcdir}/hal-xen-unignore-axes.patch" || return 1
  patch -Np1 -i "${srcdir}/hal-use-at-console.patch" || return 1
  patch -Np1 -i "${srcdir}/fix-libusb-detection.patch" || return 1
  patch -Np1 -i "${srcdir}/dbus-error-init.patch" || return 1
  patch -Np1 -i "${srcdir}/path-max.patch" || return 1
  patch -Np1 -i "${srcdir}/handle-input-touchpad.patch" || return 1
  patch -Np1 -i "${srcdir}/macbook-fix-ioperm.patch" || return 1
  
  libtoolize --force || return 1
  aclocal || return 1
  autoconf || return 1
  automake || return 1

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/usr/lib/hal --with-udev-prefix=/etc \
      --enable-static=no --disable-acpi-ibm \
      --disable-docbook-docs --disable-console-kit \
      --disable-policy-kit --disable-acl-management \
      --enable-umount-helper --disable-smbios \
      --with-hal-user=hal --with-hal-group=hal \
      --with-pid-file=/var/run/hald.pid \
      --disable-gtk-doc || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
  install -m755 -d "${pkgdir}/etc/rc.d"
  install -m755 -d "${pkgdir}/media" || return 1
  install -m755 "${srcdir}/hal" "${pkgdir}/etc/rc.d/" || return 1

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/" || return 1
  
  # Fix udev rule location
  install -d -m755 "${pkgdir}/lib"
  mv "${pkgdir}/etc/udev" "${pkgdir}/lib/"
}
