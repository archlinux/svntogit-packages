# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=akonadi-search
pkgver=17.12.3
pkgrel=1
pkgdesc="Libraries and daemons to implement searching in Akonadi"
url='https://community.kde.org/KDE_PIM'
arch=(x86_64)
license=(GPL LGPL FDL)
depends=(xapian-core akonadi-mime kcontacts kcalcore krunner kcmutils)
makedepends=(extra-cmake-modules boost python)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('3d55f80c7071d16026181e267fa2e527da76adb762831732d19d1eadb25df65a'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
