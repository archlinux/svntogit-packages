# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=libkgeomap
pkgver=15.03.97
pkgrel=1
pkgdesc="A wrapper around world map components for browsing and arranging photos on a map"
url="https://projects.kde.org/projects/kde/kdegraphics/libs/libkgeomap"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdeedu-marble')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/unstable/applications/${pkgver}/src/${pkgname}-${pkgver}.tar.xz")
sha1sums=('7d6724cd21c0a7de4c519aa9db23f0d3ddfb17cc')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
