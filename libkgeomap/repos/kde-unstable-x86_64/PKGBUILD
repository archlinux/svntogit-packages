# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=libkgeomap
pkgver=15.07.80
pkgrel=1
pkgdesc="A wrapper around world map components for browsing and arranging photos on a map"
url="https://projects.kde.org/projects/kde/kdegraphics/libs/libkgeomap"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('libmarble4')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/unstable/applications/${pkgver}/src/${pkgname}-${pkgver}.tar.xz")
sha1sums=('0b9ccd2f009af8ab0a3a894601e913eb7ff9f960')

prepare() {
  mkdir build

# Fix build with libmarble4
  sed -e 's|#include <marble|#include <marble4|' -i $pkgname-$pkgver/libkgeomap/*.cpp -i $pkgname-$pkgver/libkgeomap/*.h
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
