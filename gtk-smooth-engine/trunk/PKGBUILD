# $Id$
# Contributor: Tobias Kieslich <tobias@justdreams.de>
# InStagingBy: Damir Perisa <damir.perisa@bluewin.ch>
# Maintainer: damir <damir@archlinux.org>

pkgname=gtk-smooth-engine
pkgver=0.6.0.1
_spkgver=0.5.8
pkgrel=5
pkgdesc="The smooth-engine for gtk1 - including smooth-themes for gtk1 and gtk2"
arch=(i686 x86_64)
url="http://sourceforge.net/projects/smooth-engine"
license=('LGPL')
depends=('gtk-engines' 'gdk-pixbuf')
options=('!libtool')
source=(http://downloads.sourceforge.net/smooth-engine/${pkgname}-${pkgver}.tar.gz \
        http://downloads.sourceforge.net/smooth-engine/smooth-themes-$_spkgver.tar.gz
	deprecated_macros.patch free_border.patch)
md5sums=('a2231118c8187649d1e634fdfe6f36de' 'a46c592261c4d371498d644eece6493e'
	 'f2fcb7c088735ff0f07915987097755b' 'd018dc9be612514fc921bb12fd7d7070')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  patch -p1 < ${srcdir}/deprecated_macros.patch || return 1
  patch -p1 < ${srcdir}/free_border.patch || return 1

  ./configure --prefix=/usr --enable-gtk-1 --disable-gtk-2 || return 1
  make || return 1
  make DESTDIR=${pkgdir} install || return 1

  cd ${srcdir}/smooth-themes-$_spkgver
  ./configure --prefix=/usr --enable-gtk-1 --enable-gtk-2 || return 1
  make || return 1
  make DESTDIR=${pkgdir} install || return 1
}
