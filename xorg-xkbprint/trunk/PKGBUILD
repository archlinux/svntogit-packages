# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Brian Bidulock <bidulock@openss7.org>

pkgname=xorg-xkbprint
pkgver=1.0.5
pkgrel=2
pkgdesc="Generates a PostScript description of an XKB keyboard description"
arch=(x86_64)
url="https://gitlab.freedesktop.org/xorg/app/xkbprint"
license=('custom')
depends=('libxkbfile')
makedepends=('xorg-util-macros')
groups=('xorg-apps')
options=('debug')
# key has been revoked
source=(https://xorg.freedesktop.org/archive/individual/app/xkbprint-${pkgver}.tar.bz2) #{,.sig})
sha512sums=('ac3870352f3a595b5b8a8cb64f39e9bea81b17572c73cfbf72482477a2f576d0ffcdcf735cb3c66b8a88a7fe0f7aece6738cbac87df5641e44fae1ab9b97111f')
#            'SKIP')

build() {
  cd xkbprint-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd xkbprint-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING -t "$pkgdir"/usr/share/licenses/$pkgname/
}
