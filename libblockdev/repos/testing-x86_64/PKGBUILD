# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=libblockdev
pkgver=2.15
pkgrel=1
pkgdesc="A library for manipulating block devices"
arch=('x86_64')
url="https://github.com/rhinstaller/libblockdev"
license=('LGPL')
depends=('btrfs-progs' 'dosfstools' 'dmraid' 'gptfdisk' 'libbytesize' 'lvm2' 'mdadm' 'parted'
         'volume_key' 'xfsprogs' 'kmod')
makedepends=('gobject-introspection' 'systemd' 'python')
source=("$pkgname-$pkgver.tar.gz::https://github.com/rhinstaller/libblockdev/archive/$pkgver-1.tar.gz")
sha512sums=('a58ee6ca75c0ee8706796afb6bf26334cc1716bb3ba9230f35c485643faf4921e70ff3a86ebb2be41b2d0177e68fdc0f8011e198e46d66aca9e9712a228c4ea3')

prepare() {
  sed -i 's/-ldmraid //' $pkgname-$pkgver-1/src/lib/Makefile.am
}

build() {
  cd "$srcdir"/$pkgname-$pkgver-1
  ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc --without-lvm_dbus
  make
}

check() {
  cd "$srcdir"/$pkgname-$pkgver-1
  make check
}

package() {
  cd "$srcdir"/$pkgname-$pkgver-1
  make DESTDIR="$pkgdir" install
}
