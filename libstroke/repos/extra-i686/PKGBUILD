# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=libstroke
pkgver=0.5.1
pkgrel=5
pkgdesc="A stroke (mouse gesture) translation library"
arch=('i686' 'x86_64')
url="http://etla.net/libstroke/"
license=('GPL2')
depends=('glibc')
makedepends=('libx11' 'gtk')
options=('!libtool')
source=(http://etla.net/libstroke/${pkgname}-${pkgver}.tar.gz \
        libgstroke-Makefile.am.patch libstroke-Makefile.am.patch \
        libstroke-0.5.1-m4_syntax.patch)
md5sums=('51b9a4e309ac15cfcab96191eed03cb2'
         '60bbaf355355c3c66c8015b0f7feca85'
         '96413a77e1d39d1a3223e8eb6be8ef01'
         '93c47eef46a0cc4011a246485c5a6620')
sha1sums=('48bd2b98faa8681ccd97eda7a09442845dbf1b7b'
          'a0c3bd9e894650475ba3049b0ef3d7f9580c203d'
          'ee5a3635c31dba00b070a942c512cc0d53056188'
          'c9240937776b0572e3927e1c56d78309413c5209')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 < ../libgstroke-Makefile.am.patch
  patch -p1 < ../libstroke-Makefile.am.patch
  patch -p1 < ../libstroke-0.5.1-m4_syntax.patch

  libtoolize --force --copy
  aclocal
  autoconf
  automake --add-missing --force --copy

  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
