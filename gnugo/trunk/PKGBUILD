# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Jason Chu <jason@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: damir <damir@archlinux.org>

pkgname=gnugo
pkgver=3.8
pkgrel=5
pkgdesc='Program that plays the game of Go'
url='https://www.gnu.org/software/gnugo/'
license=('GPL3' 'custom')
arch=('i686' 'x86_64')
depends=('ncurses')
validpgpkeys=('67CCE2F5220D3E03024A175B68E7E09135C3EC3E')
source=("https://ftp.gnu.org/gnu/gnugo/${pkgname}-${pkgver}.tar.gz"{,.sig}
        'LICENSE')
sha256sums=('da68d7a65f44dcf6ce6e4e630b6f6dd9897249d34425920bfdd4e07ff1866a72'
            'SKIP'
            '8dac18a30d6d33a2ee85673e64bd745732f7e9292ef65ca11a21c17a85a7fb6d')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	rm -r "${pkgdir}"/usr/share/info
	install -Dm644 ../LICENSE "${pkgdir}/usr/share/licenses/${pkgname}"/LICENSE
}
