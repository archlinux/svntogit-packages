# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Jason Chu <jason@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: damir <damir@archlinux.org>

pkgname=gnugo
pkgver=3.8
pkgrel=5
pkgdesc='Program that plays the game of Go'
url='http://www.gnu.org/software/gnugo/'
license=('GPL3' 'custom')
arch=('i686' 'x86_64')
depends=('ncurses')
source=("http://ftp.gnu.org/gnu/gnugo/${pkgname}-${pkgver}.tar.gz"
        'LICENSE')
sha1sums=('a8ce3c7512634f789bc0c964fe23a5a6209f25db'
          '0221b29e03fe7356c131429631ad0ac3c8da04d6')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	rm -r "${pkgdir}"/usr/share/info
	install -Dm644 ../LICENSE "${pkgdir}/usr/share/licenses/${pkgname}"/LICENSE
}
