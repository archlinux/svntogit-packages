# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kopeninghours
pkgver=22.03.90
pkgrel=1
pkgdesc='Library for parsing and evaluating OSM opening hours expressions'
arch=(x86_64)
url='https://www.kde.org'
license=(LGPL)
depends=(kholidays ki18n)
makedepends=(extra-cmake-modules python qt5-tools doxygen boost)
optdepends=('boost-libs: Python bindings' 'python: Python bindings')
source=(https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('3e4806e56d3c9a71d0173ccd26b55bbfeef2a514db6573840e48b3f60e212865'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
options=(debug)

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
