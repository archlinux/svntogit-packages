# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=mtools
pkgver=4.0.25
pkgrel=1
pkgdesc="A collection of utilities to access MS-DOS disks"
arch=('x86_64')
url="https://www.gnu.org/software/mtools/"
license=('GPL3')
depends=('bash' 'glibc')
makedepends=('libx11' 'texinfo')
optdepends=('libx11: required by floppyd'
            'libxau: required by floppyd')
backup=("etc/${pkgname}.conf")
source=("https://ftp.gnu.org/gnu/mtools/${pkgname}-${pkgver}.tar.bz2"{,.sig})
sha512sums=('91b879de6b41bf9fd6d9282d846e21fe58dbcd3461d97d2d6cae752a309a5129091a0e6ffda5b28ef062c33970a1c0a43ff1cf19106232866bdbdb762c64a143'
            'SKIP')
b2sums=('54b0de421d5de003c6dff9a524af2a37d7fe0fb88361e195b000b8c8637ed894ec01ca295ea71ed0df781e9503d609f5e21b987a79c7e9d2c2e0c7176d5eb825'
        'SKIP')
# contacted upstream about switch to new key (as 1024bit DSA)
validpgpkeys=('E151F8F54AE4F4E9019F037BC80631B26F431961') # ALAIN KNAFF <alain@knaff.lu>

prepare() {
  cd "${pkgname}-${pkgver}"
  sed -e '/^SAMPLE FILE$/s:^:# :' -i mtools.conf
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --infodir=/usr/share/info \
              --sysconfdir=/etc
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -vDm 644 mtools.conf -t "${pkgdir}/etc/"
  install -vDm 644 {README,NEWS} -t "${pkgdir}/usr/share/doc/${pkgname}"
}
