# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=mtools
pkgver=4.0.40
pkgrel=1
epoch=1
pkgdesc="A collection of utilities to access MS-DOS disks"
arch=(x86_64)
url="https://www.gnu.org/software/mtools/"
license=(GPL3)
depends=(bash glibc)
makedepends=(libx11 texinfo)
optdepends=(
  'libx11: required by floppyd'
  'libxau: required by floppyd'
)
backup=(etc/$pkgname.conf)
options=(debug)
source=(https://ftp.gnu.org/gnu/mtools/$pkgname-$pkgver.tar.bz2{,.sig})
sha512sums=('c3acf8723ae207d9eccdca3c2b55aed262175328417031397c14c7120a1e02f0c52c7fefc0145dfb09fb91f1d3e63254e7a45bc7edf50b1ecc87b7cc95d2f390'
            'SKIP')
b2sums=('6ae2dbd2129d48528f6a36f81f44979b2dd977729e3f3f0ab5ff4aa8d29b0841cd770e2ed12f0f772676f6bcfeb3949d4fb79e240cf1d88524bbaa6cdfb4d26e'
        'SKIP')
validpgpkeys=('51A23D9D7C4DF41AA48FA893E99CF5537790B839') # Alain KNAFF <alain@knaff.lu>

prepare() {
  # comment entry in default config
  sed -e '/^SAMPLE FILE$/s:^:# :' -i $pkgname-$pkgver/mtools.conf
}

build() {
  cd $pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man \
    --infodir=/usr/share/info \
    --sysconfdir=/etc
  make
}

check() {
  make -k check -C $pkgname-$pkgver
}

package() {
  make DESTDIR="$pkgdir" install -C $pkgname-$pkgver
  install -vDm 644 $pkgname-$pkgver/mtools.conf -t "$pkgdir/etc/"
  install -vDm 644 $pkgname-$pkgver/{README,NEWS} -t "$pkgdir/usr/share/doc/$pkgname"
}
