# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=mtools
pkgver=4.0.34
pkgrel=1
epoch=1
pkgdesc="A collection of utilities to access MS-DOS disks"
arch=('x86_64')
url="https://www.gnu.org/software/mtools/"
license=('GPL3')
depends=('bash' 'glibc')
makedepends=('libx11' 'texinfo')
optdepends=('libx11: required by floppyd'
            'libxau: required by floppyd')
backup=("etc/${pkgname}.conf")
source=("https://ftp.gnu.org/gnu/mtools/${pkgname}-${pkgver}.tar.bz2"{,.sig})
sha512sums=('5a401ac17b9021e5e957bbd911810958f3d665d2ada8b60e723d72709e687c804bbecdb09b50fc23a9876540abead3894213e5f668f23356c629261b40980535'
            'SKIP')
b2sums=('9e4874bd724e2a62e23d7b7def601e41ba6676d2b9c37d198f0b521938297664dcd1d2fc75531c79acc93e9c642ea4555b174007ca6d2fd27cbad1b9a3d61f1c'
        'SKIP')
validpgpkeys=('51A23D9D7C4DF41AA48FA893E99CF5537790B839') # Alain KNAFF <alain@knaff.lu>

prepare() {
  cd "${pkgname}-${pkgver}"
  # comment entry in default config
  sed -e '/^SAMPLE FILE$/s:^:# :' -i mtools.conf
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --infodir=/usr/share/info \
              --sysconfdir=/etc
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -vDm 644 mtools.conf -t "${pkgdir}/etc/"
  install -vDm 644 {README,NEWS} -t "${pkgdir}/usr/share/doc/${pkgname}"
}
