# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Antonio Rojas <nqn1976 @ gmail.com>

pkgname=libkpeople
pkgver=0.1.0
pkgrel=2
pkgdesc="A contact aggregation library for KDE"
arch=('i686' 'x86_64')
url='http://community.kde.org/KTp/libkpeople'
license=('LGPL')
depends=('nepomuk-core')
makedepends=('cmake' 'automoc4')
source=("http://download.kde.org/unstable/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.bz2")
sha1sums=('6ab54b3ba203ff06d17f101060ed277696dfbaad')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}/" install
}
