# $Id: PKGBUILD 225717 2014-11-07 02:03:06Z foutrelis $
# Maintainer: Tom Gundersen <teg@jklm.no>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: James Rayner <iphitus@gmail.com>
# Contributor: Sergio Jovani Guzman <moret@paretux.org>

pkgname=konversation
pkgver=1.6beta1
_pkgver=1.6-beta1
pkgrel=1
pkgdesc="A user friendly IRC client for KDE"
arch=('i686' 'x86_64')
url="http://konversation.kde.org"
depends=('knotifyconfig' 'kemoticons' 'kparts' 'kidletime' 'qca-qt5' 'hicolor-icon-theme')
optdepends=('python: python scripting support')
makedepends=('extra-cmake-modules' 'kdoctools' 'python')
license=('GPL2' 'FDL')
install=${pkgname}.install
source=("http://download.kde.org/unstable/${pkgname}/${_pkgver}/src/${pkgname}-${_pkgver}.tar.xz" 'qca-qt5.patch')
sha1sums=('564bdc092cbdce1c0a9cf1e5362d4956c059c6f3'
          '500739fc7d66aa812909e3fc805b363f50c971dd')

prepare() {
  mkdir -p build

# fix linking to Qca
  cd $pkgname-$_pkgver
  patch -p1 -i "$srcdir"/qca-qt5.patch
}

build() {
  cd build
  cmake ../${pkgname}-${_pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
