# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: James Rayner <iphitus@gmail.com>
# Contributor: Sergio Jovani Guzman <moret@paretux.org>

pkgname=konversation
pkgver=22.03.90
pkgrel=1
pkgdesc='A user-friendly and fully-featured IRC client'
arch=(x86_64)
url='https://apps.kde.org/konversation/'
depends=(knotifyconfig kparts kidletime knewstuff qca-qt5 hicolor-icon-theme perl)
optdepends=('python: python scripting support' 'keditbookmarks: for managing bookmarks')
makedepends=(extra-cmake-modules kdoctools)
license=(GPL2 FDL)
groups=(kde-applications kde-network)
source=(https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('93fcb31bccc4e2dd9e7cc085d1655e77668f95033d9410bbdbc4799e11bcbd39'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
options=(debug)

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
