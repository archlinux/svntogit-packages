# $Id$
# Maintainer: Thomas Baechler <thomas@archlinux.org>
pkgname=davfs2
pkgver=1.4.7
pkgrel=1
pkgdesc="File system driver that allows you to mount a WebDAV folder"
arch=(i686 x86_64)
url="http://savannah.nongnu.org/projects/davfs2"
license=('GPL')
depends=('neon')
backup=(etc/davfs2/davfs2.conf etc/davfs2/secrets)
source=(http://mirrors.zerg.biz/nongnu/davfs2/${pkgname}-${pkgver}.tar.gz)
sha256sums=('50c0ee2a12e8d09fc6a852d32bdfebb1f784939433a85f05254bc7ef28cff044')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  dav_user=nobody dav_group=network ./configure --prefix=/usr --sysconfdir=/etc --disable-debug
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # Create some dirs
  install -d -m755 "${pkgdir}/sbin"
  make DESTDIR="${pkgdir}" install

  rm -f ${pkgdir}/sbin/{u,}mount.davfs
  ln -sf /usr/sbin/mount.davfs "${pkgdir}/sbin/mount.davfs"
  ln -sf /usr/sbin/umount.davfs "${pkgdir}/sbin/umount.davfs"

  rm -f ${pkgdir}/usr/share/davfs2/{BUGS,COPYING,FAQ,NEWS,TODO,ChangeLog,GPL,README,THANKS}
}
