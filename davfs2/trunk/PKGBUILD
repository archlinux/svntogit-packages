# $Id$
# Maintainer: Thomas Baechler <thomas@archlinux.org>
pkgname=davfs2
pkgver=1.5.0
pkgrel=1
pkgdesc="File system driver that allows you to mount a WebDAV folder"
arch=(i686 x86_64)
url="http://savannah.nongnu.org/projects/davfs2"
license=('GPL')
depends=('neon')
backup=(etc/davfs2/davfs2.conf etc/davfs2/secrets)
source=(http://mirrors.zerg.biz/nongnu/davfs2/${pkgname}-${pkgver}.tar.gz{,.sig})
sha256sums=('2229aa85195e787f7ae006b813bac95e6a593183bbfec0d561f255567e58d861'
            'SKIP')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  dav_user=nobody dav_group=network ./configure --prefix=/usr --sbindir=/usr/bin --sysconfdir=/etc --disable-debug
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # This directory contains worthless symlinks
  rm -r ${pkgdir}/sbin
  rm -f ${pkgdir}/usr/share/davfs2/{BUGS,COPYING,FAQ,NEWS,TODO,ChangeLog,GPL,README,THANKS}
}
