# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Jan de Groot <jgc.archlinux.org>
# Contributor: Eric Johnson <eric.archlinux.org>

pkgname=libidn
pkgver=1.34
pkgrel=2
pkgdesc="Implementation of the Stringprep, Punycode and IDNA specifications"
url="https://www.gnu.org/software/libidn/"
arch=('x86_64')
license=('GPL3' 'LGPL')
depends=('glibc')
source=(https://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz{,.sig})
# signed by either of the two keys
validpgpkeys=('9AA9BDB11BB1B99A21285A330664A76954265E8C'
              '1CB27DBC98614B2D5841646D08302DB6A2670428')
# '9AA9BDB11BB1B99A21285A330664A76954265E8C' => "Simon Josefsson <simon@josefsson.org>"
# '1CB27DBC98614B2D5841646D08302DB6A2670428' => "Tim RÃ¼hsen <tim.ruehsen@gmx.de>"
sha256sums=('3719e2975f2fb28605df3479c380af2cf4ab4e919e1506527e4c7670afff6e3c'
            'SKIP')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  # doc html installation - gtk-doc isn't supported anymore - FS#55151
  mkdir -p ${pkgdir}/usr/share/gtk-doc/html/${pkgname}
  cp -r -v doc/reference/html/* ${pkgdir}/usr/share/gtk-doc/html/${pkgname}
}
