# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=psi
pkgver=0.15
pkgrel=3
pkgdesc='Instant messaging application designed for the Jabber IM'
url='http://psi-im.org/'
license=('GPL')
arch=('i686' 'x86_64')
depends=('qca-qt4' 'enchant' 'libxss' 'xdg-utils')
makedepends=('icu')
install='psi.install'
source=("http://downloads.sourceforge.net/psi/psi-${pkgver}.tar.bz2"
        'archlinux.patch')
md5sums=('aea15f17cf93e7cea52f305523f761be'
         '2ed44ef84007b5b7f9a3f0a4f5f6a86d')

prepare() {
	cd ${srcdir}/psi-${pkgver}
	patch -p0 -i ${srcdir}/archlinux.patch
}

build() {
	cd ${srcdir}/psi-${pkgver}
	./configure --prefix=/usr --release --no-separate-debug-info
	make
}

package() {
	cd ${srcdir}/psi-${pkgver}
	make INSTALL_ROOT=${pkgdir} install
	rm -rf ${pkgdir}/usr/share/psi/certs/
}
