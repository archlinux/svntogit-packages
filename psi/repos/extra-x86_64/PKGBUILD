# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=psi
pkgver=0.14
pkgrel=6
pkgdesc='Instant messaging application designed for the Jabber IM'
url='http://psi-im.org/'
license=('GPL')
arch=('i686' 'x86_64')
depends=('qca-ossl' 'qca-gnupg' 'enchant' 'libxss' 'xdg-utils')
install='psi.install'
source=("http://downloads.sourceforge.net/psi/psi-${pkgver}.tar.bz2"
        'archlinux.patch' 'affiliationlistconfigmuc.patch'
        'configureroomcrash.patch' 'gcc-47.patch' 'input-validation.patch')
md5sums=('9d7dcce3bcab53b741a712199bd986be'
         'a965c6a4ad1028f14a5897c3f5088ae8'
         '28258c884a0a6f2cbb297435a144ae04'
         '8291eee7110a8aa8163c2e2e38c6d491'
         'd7acbb7b81671339d490622c74082d11'
         '259cf255ca2d2395840a25799056a58b')

build() {
	cd ${srcdir}/psi-${pkgver}
	patch -p1 -i ${srcdir}/archlinux.patch
	patch -p1 -i ${srcdir}/affiliationlistconfigmuc.patch
	patch -p1 -i ${srcdir}/configureroomcrash.patch
	patch -p0 -i ${srcdir}/gcc-47.patch
	patch -p0 -i ${srcdir}/input-validation.patch
	./configure --prefix=/usr --disable-bundled-qca --release --no-separate-debug-info
	make
}

package() {
	cd ${srcdir}/psi-${pkgver}
	make INSTALL_ROOT=${pkgdir} install
}
