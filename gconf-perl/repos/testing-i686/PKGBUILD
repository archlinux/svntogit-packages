# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gconf-perl
pkgver=1.044
pkgrel=11
pkgdesc="Gnome2-GConf perl bindings for gconf"
arch=('i686' 'x86_64')
license=('LGPL')
url="http://gtk2-perl.sourceforge.net/"
makedepends=('perl-extutils-pkgconfig' 'perl-extutils-depends')
checkdepends=('libx11')
depends=('glib-perl' 'gconf' 'perl')
options=(!emptydirs)
source=(http://downloads.sourceforge.net/sourceforge/gtk2-perl/Gnome2-GConf-${pkgver}.tar.gz
        disable-orbit.patch)
md5sums=('ea386003b18f067524833b0eeb271330'
         '14852fc936ce7b552eb204913d55fd1c')

prepare() {
  cd Gnome2-GConf-$pkgver
  patch -Np1 -i ../disable-orbit.patch
}

build() {
  cd Gnome2-GConf-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Gnome2-GConf-$pkgver
  dbus-launch --exit-with-session make test
}

package() {
  cd Gnome2-GConf-$pkgver
  make DESTDIR="${pkgdir}" install

# template start; name=perl-binary-module-dependency; version=1;
if [[ $(find "$pkgdir/usr/lib/perl5/" -name "*.so") ]]; then
	_perlver_min=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]);')
	_perlver_max=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]+1);')
	depends+=("perl>=$_perlver_min" "perl<$_perlver_max")
fi
# template end;
}
