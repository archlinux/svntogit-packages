# $Id: PKGBUILD 73980 2012-07-18 00:34:26Z speps $
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=suil
pkgver=0.8.0
pkgrel=1
pkgdesc="A lightweight C library for loading and wrapping LV2 plugin UIs"
arch=('i686' 'x86_64')
url="http://drobilla.net/software/suil/"
license=('custom:ISC')
depends=('lv2>=1.8.0')
makedepends=('python2' 'qt4' 'gtk2')
optdepends=('qt4: Qt 4.x UI wrapping support'
            'gtk2: GTK+ 2.x UI wrapping support')
source=("http://download.drobilla.net/$pkgname-$pkgver.tar.bz2")
md5sums=('8b6039593b2b8d6838b3b29e36874c1c')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # remove ldconfig --speps
  sed -i "/ldconfig/d" wscript

  python2 waf configure --prefix=/usr
  python2 waf build $MAKEFLAGS
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  python2 waf install --destdir="$pkgdir"

  # license
  install -Dm644 COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
