# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=gstreamer0.10-bad
pkgname=('gstreamer0.10-bad' 'gstreamer0.10-bad-plugins')
pkgver=0.10.16
pkgrel=1
arch=(i686 x86_64)
license=('LGPL' 'GPL')
makedepends=('pkgconfig' 'gstreamer0.10-base>=0.10.25' 'xvidcore>=1.2.1' 'libdca>=0.0.5' 'bzip2' 'libdc1394>=2.1.0' 'exempi>=2.1.0' 'jack-audio-connection-kit>=0.116.2' 'neon>=0.28.4' 'faac>=1.28' 'libexif>=0.6.17' 'musicbrainz>=2.1.5' 'faad2>=2.7' 'libmms>=0.4' 'libcdaudio>=0.99.12' 'libmpcdec>=1.2.6' 'mjpegtools>=1.9.0' 'libdvdnav>=4.1.3' 'libmodplug>=0.8.7' 'jasper>=1.900.1' 'liblrdf>=0.4.0' 'libofa>=0.9.3' 'soundtouch>=1.4.0')
url="http://gstreamer.freedesktop.org/"
options=(!libtool !emptydirs)
source=(${url}/src/gst-plugins-bad/gst-plugins-bad-${pkgver}.tar.bz2)
md5sums=('2288f7093a54891622ec1016bc939204')

build() {
  cd "${srcdir}/gst-plugins-bad-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --enable-experimental \
    --with-package-name="GStreamer Bad Plugins (Archlinux)" \
    --with-package-origin="http://www.archlinux.org/" || return 1

  make || return 1
  sed -e 's/gst sys ext/gst/' -i Makefile || return 1
}

package_gstreamer0.10-bad() {
  pkgdesc="GStreamer Multimedia Framework Bad Plugin libraries (gst-plugins-bad)"
  depends=('gstreamer0.10-base>=0.10.25')
  groups=('gstreamer0.10-plugins')

  cd "${srcdir}/gst-plugins-bad-${pkgver}"
  make DESTDIR="${pkgdir}" install || return 1
}

package_gstreamer0.10-bad-plugins() {
  pkgdesc="GStreamer Multimedia Framework Bad Plugins (gst-plugins-bad)"
  depends=("gstreamer0.10-bad=${pkgver}" 'xvidcore>=1.2.1' 'libdca>=0.0.5' 'bzip2' 'libdc1394>=2.1.0' 'exempi>=2.1.0' 'jack-audio-connection-kit>=0.116.2' 'neon>=0.28.4' 'faac>=1.28' 'libexif>=0.6.17' 'musicbrainz>=2.1.5' 'faad2>=2.7' 'libmms>=0.4' 'libcdaudio>=0.99.12' 'libmpcdec>=1.2.6' 'mjpegtools>=1.9.0' 'libdvdnav>=4.1.3' 'libmodplug>=0.8.7' 'jasper>=1.900.1' 'liblrdf>=0.4.0' 'libofa>=0.9.3')
  groups=('gstreamer0.10-plugins')

  cd "${srcdir}/gst-plugins-bad-${pkgver}"
  make -C gst-libs DESTDIR="${pkgdir}" install || return 1
  make -C ext DESTDIR="${pkgdir}" install || return 1
  make -C sys DESTDIR="${pkgdir}" install || return 1
  make -C gst-libs DESTDIR="${pkgdir}" uninstall || return 1
}
