# $Id$
# Maintainer: damir <damir@archlinux.org>
# Packager: Maksim Sipos (maxsipos at gmail dot com)

pkgname=plotutils
pkgver=2.6
pkgrel=3
arch=('x86_64' 'i686')
pkgdesc="Set of utilities and libraries for plotting."
url="http://directory.fsf.org/graphics/plotutils.html"
license=("GPL")
depends=("libpng" "gcc-libs" "libxaw>=1.0.5")
options=('!libtool')
install=plotutils.install
source=(http://ftp.gnu.org/pub/gnu/plotutils/$pkgname-$pkgver.tar.gz{,.sig} 
        plotutils-2.6-libpng-1.5.patch)
sha1sums=('7921301d9dfe8991e3df2829bd733df6b2a70838'
          'ab78e2d06402aaac367da7a0e5810439d5b9853f'
          '492f0e04f8265ab50d9ba0905a0f5adf3ec06ab8')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p0 -i ../plotutils-2.6-libpng-1.5.patch
  ./configure --prefix=/usr \
	--with-gnu-ld \
	--with-x \
	--enable-libplotter
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
