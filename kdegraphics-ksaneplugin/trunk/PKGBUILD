# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegraphics-ksaneplugin
pkgver=16.04.2
pkgrel=1
pkgdesc="A scan plugin that implements the scanning"
url="https://www.kde.org/applications/graphics/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('libksane4')
makedepends=('cmake' 'automoc4')
replaces=('kdegraphics-libs')
conflicts=('kdegraphics-libs')
source=("http://download.kde.org/stable/applications/${pkgver}/src/ksaneplugin-${pkgver}.tar.xz")
sha1sums=('4c47793c311ea7337ed4e5bbf10d5fdca74f3b2d')

build() {
  cd "${srcdir}"
  mkdir -p build
  cd build
  cmake ../ksaneplugin-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
