# $Id: PKGBUILD 59825 2009-11-28 01:40:17Z andrea $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kdebindings
pkgname=('kdebindings-csharp'
         'kdebindings-python'
         'kdebindings-ruby'
         'kdebindings-smoke')
groups=('kdebindings')
pkgver=4.3.90
pkgrel=3
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'kdepimlibs' 'pyqt>=4.6.0'
             'qimageblitz' 'qscintilla' 'ruby' 'mono' 'kdegraphics-okular')
source=("http://download.kde.org/unstable/${pkgver}/src/${pkgbase}-${pkgver}.tar.bz2")
md5sums=('80687fa18608e081c814fdaf467f3eb5')

build() {
	export MONO_SHARED_DIR="${srcdir}/build/.mono"
	mkdir -p "$MONO_SHARED_DIR"
	cd $srcdir/build

	# disable broken examples
	sed -e 's#add_subdirectory( examples )##' -i ../${pkgbase}-${pkgver}/csharp/plasma/CMakeLists.txt || return 1

	# fix build with ruby 1.9
	# see http://bugs.archlinux.org/task/16175
	find ../$pkgbase-$pkgver/ruby -name CMakeLists.txt \
		-exec sed -i "s|\${RUBY_INCLUDE_PATH}|\${RUBY_INCLUDE_PATH} /usr/include/ruby-1.9.1/${CARCH}-linux|" {} \;

	cmake ../${pkgbase}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DBUILD_java=OFF \
		-DBUILD_php=OFF \
		-DBUILD_falcon=OFF \
		-DENABLE_KROSSRUBY=OFF
	make
}

package_kdebindings-csharp() {
	pkgdesc='KDE bindings for mono'
	depends=('kdebindings-smoke' 'mono')
	export MONO_SHARED_DIR="${srcdir}/build/.mono"
	mkdir -p "$MONO_SHARED_DIR"
	cd $srcdir/build/csharp
	make DESTDIR=$pkgdir install
}

package_kdebindings-python() {
	pkgdesc='KDE bindings for python'
	depends=('kdepimlibs' 'pyqt')
	cd $srcdir/build/python
	make DESTDIR=$pkgdir install
}

package_kdebindings-ruby() {
	pkgdesc='KDE bindings for ruby'
	depends=('kdebindings-smoke' 'ruby')
	cd $srcdir/build/ruby
	make DESTDIR=$pkgdir install
}

package_kdebindings-smoke() {
	pkgdesc='Language independent library for Qt and KDE bindings'
	depends=('kdepimlibs' 'qscintilla' 'kdegraphics-okular')
	groups=()
	cd $srcdir/build/smoke
	make DESTDIR=$pkgdir install
}
