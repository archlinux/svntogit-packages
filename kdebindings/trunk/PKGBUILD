# $Id: PKGBUILD 59825 2009-11-28 01:40:17Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kdebindings
pkgname=('kdebindings-csharp'
         'kdebindings-python'
         'kdebindings-smoke')
pkgver=4.5.2
pkgrel=6
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'pyqt' 'kdepim-runtime' 'mono'
	'kdegraphics-okular' 'qwt' 'docbook-xsl' 'boost' 'qscintilla')
source=("http://download.kde.org/stable/${pkgver}/src/${pkgbase}-${pkgver}.tar.bz2"
	'fix-pyqt4-build.patch'
	'fix-generator-segfaults.patch')
sha1sums=('108fd6e3dbb341adc0e90487d1b681534968f1d2'
	'36b52d147fee24a65c756baaad00e3d1ac081d3d'
	'c0045f5b57fb62e6b0a9443548938f93af501b9d')

build() {
	cd ${srcdir}/${pkgbase}-${pkgver}

	patch -Np0 -i ${srcdir}/fix-pyqt4-build.patch
	
	# Already fixed in 4.5.3
	patch -Np4 -i ${srcdir}/fix-generator-segfaults.patch

	export MONO_SHARED_DIR="${srcdir}/build/.mono"
	mkdir -p "$MONO_SHARED_DIR"
	cd ${srcdir}/build

	cmake ../${pkgbase}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DBUILD_falcon=OFF \
		-DBUILD_java=OFF \
		-DBUILD_ruby=OFF \
		-DBUILD_php=OFF \
		-DWITH_PolkitQt=OFF
	make
}

package_kdebindings-csharp() {
	pkgdesc='KDE bindings for mono'
	depends=('kdebindings-smoke' 'mono')
	export MONO_SHARED_DIR="${srcdir}/build/.mono"
	mkdir -p "$MONO_SHARED_DIR"
	cd $srcdir/build/csharp
	make DESTDIR=$pkgdir install
}

package_kdebindings-python() {
	pkgdesc='KDE bindings for python'
	depends=('kdepim-runtime' 'pyqt' 'qscintilla')
	cd $srcdir/build/python
	make DESTDIR=$pkgdir install

	# Use the python2 executable
	find "${pkgdir}" -name '*.py' | xargs sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|'
}

package_kdebindings-smoke() {
	pkgdesc='Language independent library for Qt and KDE bindings'
	depends=('qscintilla' 'kdegraphics-okular' 'kdepim-runtime')
	cd $srcdir/build/smoke
	make DESTDIR=$pkgdir install
}
