# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Alexander Fehr <pizzapunk gmail com>

pkgbase=tracker
pkgname=(tracker libtracker-sparql)
pkgver=0.16.0
_tver=${pkgver%.*}
pkgrel=1
pkgdesc="All-in-one indexer, search tool and metadata database"
arch=(i686 x86_64)
license=(GPL)
makedepends=(libgee libsecret upower libexif exempi
             poppler-glib libgsf icu enca networkmanager gtk3
             desktop-file-utils hicolor-icon-theme gobject-introspection
             intltool giflib gst-plugins-base-libs totem-plparser
             taglib libvorbis flac vala libgxps)
url="http://www.gnome.org"
options=('!libtool' '!emptydirs')
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgbase/$_tver/$pkgbase-$pkgver.tar.xz)
sha256sums=('299c6a180370522d1d794c184aaef8d8222d3f737d564b8e5ca25b8751470c4f')

build() {
  cd $pkgbase-$pkgver

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/tracker \
    --disable-unit-tests \
    --disable-nautilus-extension \
    --enable-libflac \
    --enable-libvorbis

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' -e 's/    if test "$export_dynamic" = yes && test -n "$export_dynamic_flag_spec"; then/      func_append compile_command " -Wl,-O1,--as-needed"\n      func_append finalize_command " -Wl,-O1,--as-needed"\n\0/' libtool

  make
}

package_tracker() {
  depends=("libtracker-sparql=$pkgver-$pkgrel" libgee libsecret
           upower libexif exempi poppler-glib libgsf enca
           networkmanager gtk3 desktop-file-utils hicolor-icon-theme)
  optdepends=('giflib: extractor for GIF data'
              'gst-plugins-base-libs: video extractor'
              'totem-plparser: playlist support'
              'libvorbis: Vorbis metadata extractor'
              'flac: FLAC metadata extractor'
              'taglib: writeback for audio files'
              'libgxps: XPS extractor')
  groups=('gnome-extra')
  install=tracker.install

  cd $pkgbase-$pkgver
  make DESTDIR="$pkgdir" install

### Split libtracker-sparql

  mkdir -p "$srcdir"/sparql/usr/{include,lib}/tracker-$_tver
  mkdir -p "$srcdir"/sparql/usr/lib/{girepository-1.0,pkgconfig}
  mkdir -p "$srcdir"/sparql/usr/share/{gir-1.0,vala/vapi}

  mv "$pkgdir"/usr/lib/libtracker-sparql-* "$srcdir/sparql/usr/lib"

  mv "$pkgdir"/usr/lib/tracker-$_tver/*.so* \
    "$srcdir/sparql/usr/lib/tracker-$_tver"

  mv "$pkgdir"/usr/share/vala/vapi/tracker-sparql-* \
    "$srcdir/sparql/usr/share/vala/vapi"

  mv {"$pkgdir","$srcdir/sparql"}/usr/include/tracker-$_tver/libtracker-sparql
  mv {"$pkgdir","$srcdir/sparql"}/usr/lib/girepository-1.0/Tracker-$_tver.typelib
  mv {"$pkgdir","$srcdir/sparql"}/usr/lib/pkgconfig/tracker-sparql-$_tver.pc
  mv {"$pkgdir","$srcdir/sparql"}/usr/share/gir-1.0/Tracker-$_tver.gir
}

package_libtracker-sparql() {
  pkgdesc="$pkgdesc (SPARQL library)"
  depends=(sqlite icu glib2 libffi pcre util-linux)

  mv "$srcdir"/sparql/* "$pkgdir"
}
