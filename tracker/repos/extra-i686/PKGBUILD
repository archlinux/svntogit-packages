# $Id$
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Alexander Fehr <pizzapunk gmail com>

pkgbase=tracker
pkgname=(tracker libtracker-sparql)
pkgver=0.12.7
_tver=${pkgver%.*}
pkgrel=1
pkgdesc="All-in-one indexer, search tool and metadata database"
arch=('i686' 'x86_64')
license=('GPL')
makedepends=('libgee' 'libgnome-keyring' 'upower' 'libexif' 'exempi'
             'poppler-glib' 'libgsf' 'icu' 'enca' 'networkmanager' 'gtk3'
             'desktop-file-utils' 'hicolor-icon-theme' 'gobject-introspection'
             'intltool' 'giflib' 'gstreamer0.10-base' 'totem-plparser'
             'taglib' 'nautilus' 'gnome-panel' 'libvorbis' 'flac' 'vala')
url="http://www.gnome.org"
options=('!libtool' '!emptydirs')
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgbase/$_tver/$pkgbase-$pkgver.tar.xz)
sha256sums=('e7a4d016326bc0a1b6aea07e48c3de73ffc29119812149e2dee5eac35104f1b4')

build() {
  cd "$srcdir/$pkgbase-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/tracker \
    --disable-unit-tests \
    --enable-libflac \
    --enable-libvorbis

  make
}

package_tracker() {
  depends=("libtracker-sparql=$pkgver-$pkgrel" 'libgee' 'libgnome-keyring'
           'upower' 'libexif' 'exempi' 'poppler-glib' 'libgsf' 'enca'
           'networkmanager' 'gtk3' 'desktop-file-utils' 'hicolor-icon-theme')
  optdepends=('giflib: extractor for GIF data'
              'gstreamer0.10-base: video extractor'
              'totem-plparser: playlist support'
              'nautilus: nautilus-extension'
              'libvorbis: Vorbis metadata extractor'
              'flac: FLAC metadata extractor'
              'taglib: writeback for audio files'
              'gnome-panel: tracker-search-bar')
  groups=('gnome-extra')
  install=tracker.install

  cd "$srcdir/$pkgbase-$pkgver"
  make DESTDIR="$pkgdir" install

### Split libtracker-sparql

  mkdir -p "$srcdir"/sparql/usr/{include,lib}/tracker-$_tver
  mkdir -p "$srcdir"/sparql/usr/lib/{girepository-1.0,pkgconfig}
  mkdir -p "$srcdir"/sparql/usr/share/{gir-1.0,vala/vapi}

  mv "$pkgdir"/usr/lib/libtracker-sparql-* "$srcdir/sparql/usr/lib"

  mv "$pkgdir"/usr/lib/tracker-$_tver/*.so* \
    "$srcdir/sparql/usr/lib/tracker-$_tver"

  mv "$pkgdir"/usr/share/vala/vapi/tracker-sparql-* \
    "$srcdir/sparql/usr/share/vala/vapi"

  mv {"$pkgdir","$srcdir/sparql"}/usr/include/tracker-$_tver/libtracker-sparql
  mv {"$pkgdir","$srcdir/sparql"}/usr/lib/girepository-1.0/Tracker-$_tver.typelib
  mv {"$pkgdir","$srcdir/sparql"}/usr/lib/pkgconfig/tracker-sparql-$_tver.pc
  mv {"$pkgdir","$srcdir/sparql"}/usr/share/gir-1.0/Tracker-$_tver.gir
}

package_libtracker-sparql() {
  pkgdesc="$pkgdesc (SPARQL library)"
  depends=('sqlite3' 'icu' 'glib2' 'libffi' 'pcre' 'util-linux')

  mv "$srcdir"/sparql/* "$pkgdir"
}
