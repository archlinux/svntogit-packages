# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-service-identity
pkgname=('python-service-identity' 'python2-service-identity')
pkgver=16.0.0
pkgrel=2
pkgdesc="Service identity verification for pyOpenSSL"
arch=('any')
license=('MIT')
url="http://pypi.python.org/pypi/service_identity"
makedepends=('python-pyopenssl' 'python2-pyopenssl' 'python-pyasn1' 'python2-pyasn1'
             'python-setuptools' 'python2-setuptools' 'python-pyasn1-modules' 'python2-pyasn1-modules'
             'python-attrs' 'python2-attrs' 'python-idna' 'python2-idna')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
source=("http://pypi.python.org/packages/source/s/service_identity/service_identity-$pkgver.tar.gz")
md5sums=('d52392597b9c44a740abf322bfdb21e6')

prepare() {
  cp -a service_identity-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/service_identity-$pkgver
  python setup.py build
 
  cd "$srcdir"/service_identity-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/service_identity-$pkgver
  python setup.py ptr

  cd "$srcdir"/service_identity-$pkgver-py2
  python2 setup.py ptr
}
 
package_python-service-identity() {
  depends=('python-pyopenssl' 'python-pyasn1' 'python-pyasn1-modules' 'python-attrs')
  optdepends=('python-idna: for Internationalized Domain Names support')
 
  cd service_identity-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
 
package_python2-service-identity() {
  depends=('python2-pyopenssl' 'python2-pyasn1' 'python2-pyasn1-modules' 'python2-attrs')
  optdepends=('python2-idna: for Internationalized Domain Names support')
 
  cd service_identity-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
