# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=telepathy-kde-common-internals
_pkgname=ktp-common-internals
pkgver=20.11.90
pkgrel=1
pkgdesc='Common components for KDE-Telepathy'
arch=(x86_64)
url='https://community.kde.org/Real-Time_Communication_and_Collaboration'
license=(GPL)
groups=(kde-applications kde-network telepathy-kde)
depends=(knotifyconfig ktexteditor kpeople telepathy-logger-qt kaccounts-integration libotr telepathy-accounts-signon)
makedepends=(extra-cmake-modules kdoctools doxygen)
source=("https://download.kde.org/unstable/release-service/$pkgver/src/$_pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('048084510535d3fb0865dc6924eac7c943ed358f0a34048b25a704963d278f0a'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $_pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
