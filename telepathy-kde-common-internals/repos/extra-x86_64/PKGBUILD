# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=telepathy-kde-common-internals
_pkgname=ktp-common-internals
pkgver=22.04.2
pkgrel=2
pkgdesc='Common components for KDE-Telepathy'
arch=(x86_64)
url='https://community.kde.org/KTp'
license=(GPL)
groups=(kde-applications kde-network telepathy-kde)
depends=(knotifyconfig ktexteditor kpeople telepathy-logger-qt kaccounts-integration libotr telepathy-accounts-signon telepathy-kde-integration-module)
makedepends=(extra-cmake-modules kdoctools doxygen)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$_pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('e258518ecf71acfd9cc2675fed1e975a0293d7bbe9f0fca6150a4b5332564d5b'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
options=(debug)

build() {
  cmake -B build -S $_pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
