# $Id$
# Maintainer: Alexander Baldeck <alexander@archlinux.org>
# Contributor: Dale Blount <dale@archlinux.org>
# Contributor: Anders Bostrom <anders.bostrom@home.se>
pkgname=thunderbird
pkgver=3.0
pkgrel=4
pkgdesc="Standalone Mail/News reader"
arch=('i686' 'x86_64')
license=('MPL' 'GPL')
url="http://www.mozilla.org/projects/thunderbird"
depends=('gtk2>=2.18.5' 'gcc-libs>=4.4' 'libidl2>=0.8.13' 'mozilla-common' 'nss>=3.12.3' 'libxt' 'shared-mime-info'
	 'alsa-lib>=1.0.21.a' 'dbus-glib>=0.82')
makedepends=('zip' 'pkgconfig' 'libgnomeui')
options=('!makeflags')
source=(ftp://ftp.mozilla.org/pub/mozilla.org/thunderbird/releases/${pkgver}/source/thunderbird-${pkgver}.source.tar.bz2
        mozconfig
        thunderbird.desktop
        thunderbird-3.0-lang.patch
        thunderbird-appversion.patch
	thunderbird-shared-error.patch
	libpng14.patch)
md5sums=('9a564ac2489b1b0f9348dc298b913f6c'
         'aacdcab7732af31a16cd4dc86665f4a3'
         '8f56c6c1c2e4b8e50db816642ca47f94'
         '25b6fe16ac24cd5c852213e5c1adb272'
         '2af84c5fa1095373e0b2b450fec0d655'
         '3dca714ee8054634e6dfdb059464dc42'
         '8868360dc354baa129c5a244cb840339')

build() {
  cd "${srcdir}/comm-1.9.1"
  patch -Np1 -i "${srcdir}/thunderbird-3.0-lang.patch" || return 1
  patch -Np1 -i "${srcdir}/thunderbird-appversion.patch" || return 1
  patch -Np1 -i "${srcdir}/thunderbird-shared-error.patch" || return 1
  patch -Np1 -i "${srcdir}/libpng14.patch" || return 1

  cp "${srcdir}/mozconfig" .mozconfig
  unset CXXFLAGS
  unset CFLAGS

  export LDFLAGS="-Wl,-rpath,/usr/lib/thunderbird-3.0"

  make -f client.mk build MOZ_MAKE_FLAGS="${MAKEFLAGS}" || return 1
  make DESTDIR="${pkgdir}" install || return 1

  rm -rf "${pkgdir}/usr/bin/defaults"

  install -m755 -d "${pkgdir}/usr/share/applications"
  install -m755 -d "${pkgdir}/usr/share/pixmaps"

  install -m644 "${srcdir}/comm-1.9.1/mail/branding/nightly/mailicon48.png" \
  	"${pkgdir}/usr/share/pixmaps/thunderbird.png" || return 1

  install -m644 "${srcdir}/thunderbird.desktop" \
      "${pkgdir}/usr/share/applications/" || return 1

  rm -f ${pkgdir}/usr/lib/pkgconfig/thunderbird-ns{s,pr}.pc
}
