# $Id$
# Maintainer: Aaron Griffin <aaron@archlinux.org>
# Contributor: Mark Schneider <queueRAM@gmail.com>

pkgname=gnucash
pkgver=2.2.9
pkgrel=9
pkgdesc="A personal and small-business financial-accounting application"
arch=('i686' 'x86_64')
url="http://www.gnucash.org"
license=("GPL")
depends=('gtkhtml>=3.30.1' 'slib' 'goffice>=0.8.2' 'guile' 'libofx' 'libgnomeui>=2.24.3' 'hicolor-icon-theme')
makedepends=('intltool')
optdepends=('evince: for print preview'
            'gnucash-docs: (AUR) for user documentation'
            'perl-finance-quote: for stock information lookups'
            'perl-date-manip: for stock information lookups')
options=('!libtool' '!makeflags')
install=gnucash.install
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.gz \
        ${pkgname}-${pkgver}-goffice.patch)
md5sums=('570eee09a1ae83c9953ec60a7fc146ed' 'cec6aab50fb139a12f9a0cf9d31e16b0')
sha1sums=('8f63c67fcceaae93ceb8827143cc6a470e3a096a' '823d5b8ceaeda25793620d0ef3e676316924f7b1')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}
	patch -p0 < ${srcdir}/${pkgname}-${pkgver}-goffice.patch || return 1
	./configure --prefix=/usr --mandir=/usr/share/man \
	    --sysconfdir=/etc --libexecdir=/usr/lib --enable-ofx || return 1
	make || return 1
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}
	make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR=${pkgdir} install || return 1

	install -dm755 ${pkgdir}/usr/share/gconf/schemas
	gconf-merge-schema ${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas --domain gnucash ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
	rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
