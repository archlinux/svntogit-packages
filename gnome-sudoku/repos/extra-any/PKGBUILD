# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-sudoku
pkgver=3.12.1
pkgrel=1
pkgdesc="Test your logic skills in this number grid puzzle"
arch=('any')
license=('GPL')
depends=('hicolor-icon-theme' 'python-gobject' 'dconf' 'gtk3')
makedepends=('intltool' 'itstool')
conflicts=('gnome-games')
replaces=('gnome-games')
options=('!emptydirs')
install=gnome-sudoku.install
url="https://live.gnome.org/GnomeSudoku"
groups=('gnome-extra')
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('1592aab91c929a08e7b73c38cfddf07d654eed45f4e43a3d87f19bb0be17a543')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  sed -i 's_#! /usr/bin/python_#!/usr/bin/python2_' "$pkgdir/usr/bin/gnome-sudoku"
}
