# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=dconf-editor
pkgver=3.38.3
pkgrel=2
pkgdesc="GSettings editor for GNOME"
url="https://wiki.gnome.org/Apps/DconfEditor"
arch=(x86_64)
license=(GPL3)
groups=(gnome-extra)
depends=(gtk3 dconf)
makedepends=(vala meson git)
options=(debug)
_commit=886181423ba8e93e766087425a0f2066f450cab1  # tags/3.38.3^0
source=("git+https://gitlab.gnome.org/GNOME/dconf-editor.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname

  # https://bugs.archlinux.org/task/75459
  git cherry-pick -n 56474378568e6ff4af8aa912810323e808c1d977
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
