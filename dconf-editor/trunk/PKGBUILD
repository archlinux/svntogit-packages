# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=dconf-editor
pkgver=3.26.2
pkgrel=1
pkgdesc="dconf Editor"
arch=(x86_64)
url="https://wiki.gnome.org/Projects/dconf"
license=(GPL3)
groups=(gnome-extra)
depends=(gtk3 dconf)
makedepends=(vala intltool appstream-glib git)
_commit=04fb074e05091c4fcb083efc4cdc970ceb5e449e  # tags/3.26.2^0
source=("git+https://git.gnome.org/browse/dconf-editor#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
