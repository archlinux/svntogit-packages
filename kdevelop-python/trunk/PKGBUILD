# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdevelop-python
pkgver=1.5.2
pkgrel=1
pkgdesc="Python language and documentation plugin for KDevelop"
arch=('i686' 'x86_64')
url="http://www.kdevelop.org/"
license=('GPL')
depends=('kdevelop')
makedepends=('cmake' 'automoc4' 'kdevelop-pg-qt')
source=("http://download.kde.org/stable/kdevelop/kdev-python/${pkgver}/src/kdev-python-v${pkgver}.tar.xz")
sha1sums=('aa08a4f164fabde363b1bc5141e1f673405949f3')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../kdev-python-v${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install

  sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|' \
      "${pkgdir}"/usr/share/apps/kdevpythonsupport/encodings/encodings/rot_13.py
}
