# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdevelop-python
pkgver=1.7.0
_pkgver=4.7.0
pkgrel=3
pkgdesc="Python language and documentation plugin for KDevelop"
arch=('i686' 'x86_64')
url="http://www.kdevelop.org/"
license=('GPL')
depends=('kdevelop' 'python')
makedepends=('cmake' 'automoc4' 'kdevelop-pg-qt')
source=("http://download.kde.org/stable/kdevelop/${_pkgver}/src/kdev-python-${pkgver}-py3.tar.xz")
sha1sums=('69ec55b920cfa1559561d367d435c15d63e920ac')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../kdev-python-${pkgver}-py3 \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
