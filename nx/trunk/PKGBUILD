# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgbase=nx
pkgname=('libxcomp' 'nxproxy' 'nx-x11' 'nx-xcompext' 'nxagent' 'nx-headers')
pkgver=3.5.0.13
pkgrel=1
arch=('i686' 'x86_64')
url="http://wiki.x2go.org/"
license=('GPL')
options=('!makeflags')
makedepends=('libjpeg-turbo' 'libpng' 'bash' 'perl' # runtime dependencies from subpackages
             'xproto' 'freetype2' 'libxaw' 'libxrender' 'libxp' 'libxpm' 'libxdamage' 'libxrandr' 'libxcomposite' 'libxtst' 'xorg-sessreg' # makedepends
             )
source=(http://code.x2go.org/releases/source/nx-libs/nx-libs_$pkgver-full.tar.gz
        nx-x11.ld.so.conf.d)
md5sums=('2c80e570d35a25989f8606e1f3620026'
         'f2ec60c7e2d81bef2f7292d2b33681a6')

build() {
  cd "${srcdir}/nx-libs_$pkgver"
  make CONFIGURE="./configure --prefix=/usr --libdir=/usr/lib --libexecdir=/usr/lib --includedir=/usr/include"
  # fake install
  mkdir $srcdir/fakeinstall
  make DESTDIR="$srcdir/fakeinstall" install
}

package_libxcomp() {
  
  pkgdesc="NX X compression library"
  depends=('libjpeg-turbo' 'libpng' 'gcc-libs')
	
  install -dm755 ${pkgdir}/usr/lib/nx/lib
  cp -a ${srcdir}/fakeinstall/usr/lib/nx/libXcomp.so* ${pkgdir}/usr/lib/nx/lib
  cd ${pkgdir}/usr/lib/
  ln -sv /usr/lib/nx/lib/libXcomp.so{,.3,.3.5.0} .
}

package_nxproxy() {

  pkgdesc="NX proxy"
  depends=('libxcomp')
  
  install -dm755 ${pkgdir}/usr/{bin,share/man/man1,lib/nx/bin}
  cp -a ${srcdir}/fakeinstall/usr/bin/nxproxy ${pkgdir}/usr/lib/nx/bin
  cd ${pkgdir}/usr/lib/nx
  ln -sv /usr/lib/nx/bin/nxproxy .
  
  # the wrapper
  cp -a ${srcdir}/nx-libs_$pkgver/bin/nxproxy ${pkgdir}/usr/bin
  chmod +x ${pkgdir}/usr/bin/nxproxy
  cp -a ${srcdir}/fakeinstall/usr/share/man/man1/nxproxy.1 ${pkgdir}/usr/share/man/man1
}

package_nx-xcompext() {
  
  pkgdesc="Xcompext/Xcompshad library for NX"
  depends=('libxcomp' 'nx-x11')
	
  install -dm755 ${pkgdir}/usr/lib/nx
  cp -a ${srcdir}/fakeinstall/usr/lib/nx/libXcompext.so.3.5.0 ${pkgdir}/usr/lib/nx
  cd ${pkgdir}/usr/lib/nx
  ln -sv libXcompext.so.3.5.0 libXcompext.so.3
  ln -sv libXcompext.so.3.5.0 libXcompext.so
  cd ${pkgdir}/usr/lib/
  ln -sv /usr/lib/nx/libXcompext.so{,.3,.3.5.0} .
}

package_nx-x11() {
 
  pkgdesc="NX-X11 lib for the NX framework"
  depends=('libxcomp')
	
  install -dm755 ${pkgdir}/{etc/ld.so.conf.d,usr/bin,usr/lib/nx/bin}
  cp -aR ${srcdir}/fakeinstall/usr/lib/NX3/lib*/nx/lib*.so* ${pkgdir}/usr/lib/nx

  cp -a ${srcdir}/fakeinstall/usr/lib/NX3/bin/nxauth ${pkgdir}/usr/lib/nx/bin
  cd ${pkgdir}/usr/lib/nx
  ln -sv /usr/lib/nx/bin/nxauth .
  
  # the wrapper
  cp -a ${srcdir}/nx-libs_$pkgver/bin/nxauth ${pkgdir}/usr/bin
  chmod +x ${pkgdir}/usr/bin/nxauth
  
  install -m 644 "$srcdir/nx-x11.ld.so.conf.d" "$pkgdir/etc/ld.so.conf.d/nx.conf"
}

package_nxagent() {
  
  pkgdesc="NX X server based on Xnest"
  depends=('nx-xcompext')
	
  install -dm755 ${pkgdir}/usr/{bin,lib/nx/bin}
  cp -aR ${srcdir}/fakeinstall/usr/lib/NX3/bin/nxagent ${pkgdir}/usr/lib/nx/bin
  cd ${pkgdir}/usr/lib/nx
  ln -sv /usr/lib/nx/bin/nxagent .
  
  # the wrapper
  cp -a ${srcdir}/nx-libs_$pkgver/bin/nxagent ${pkgdir}/usr/bin
  chmod +x ${pkgdir}/usr/bin/nxagent
  # required nxcompshad libs
  cp -a ${srcdir}/nx-libs_$pkgver/nxcompshad/libXcompshad.so.3.5.0 ${pkgdir}/usr/lib/nx
  cd ${pkgdir}/usr/lib/nx
  ln -sv libXcompshad.so.3.5.0 libXcompshad.so.3
  ln -sv libXcompshad.so.3.5.0 libXcompshad.so
  cd ${pkgdir}/usr/lib/
  ln -sv /usr/lib/nx/libXcompshad.so{,.3,.3.5.0} .
}

package_nx-headers() {

  pkgdesc="NX headers"
  
  install -dm755 ${pkgdir}/usr/include/nx
  cp -aR ${srcdir}/fakeinstall/usr/include/nx/* ${pkgdir}/usr/include/nx
}  
