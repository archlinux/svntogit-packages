# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-ffi-checklib
pkgver=0.25
pkgrel=1
pkgdesc="Check that a library is available for FFI"
arch=('any')
license=('PerlArtistic')
url="https://metacpan.org/release/FFI-CheckLib"
depends=('perl')
checkdepends=('perl-test-exit' 'perl-test2-suite')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/P/PL/PLICEASE/FFI-CheckLib-$pkgver.tar.gz")
sha512sums=('f6fd790bd6b5e64972f2692abdd81dcd617a4b9ca377bdced655939e1b6005d2d84dc49df67d4945ff5ae0e7ff955aeccc019bc522fc959f1831c0586f40d313')

build() {
  cd FFI-CheckLib-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd FFI-CheckLib-$pkgver
  make test
}

package() {
  cd FFI-CheckLib-$pkgver
  make DESTDIR="$pkgdir" install
}
