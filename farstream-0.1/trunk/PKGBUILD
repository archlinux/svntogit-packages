# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=farstream-0.1
_pkgname=farstream
pkgver=0.1.2
pkgrel=1
pkgdesc="Audio/Video Communications Framework (legacy version)"
arch=('i686' 'x86_64')
url="http://www.freedesktop.org/wiki/Software/Farstream"
license=('LGPL2.1')
depends=('libnice' 'gstreamer0.10-base' 'gstreamer0.10-bad-plugins'
         'gstreamer0.10-good' 'gstreamer0.10-ffmpeg')
makedepends=('gobject-introspection' 'gstreamer0.10-python' 'python2-gobject2')
options=('!libtool')
source=(http://freedesktop.org/software/$_pkgname/releases/$_pkgname/$_pkgname-$pkgver.tar.gz
        farstream-0.1.2-prefer-vp8.patch)
sha256sums=('fa5a40597b47b59d124f7fd57f66704b030d576cdc0d90e107c7a592f05e73d9'
            '5f8f2876bde0f21383b3d059c94914a2fcd63d45a1bb1b5cd2e2208cd2536da5')

build() {
  cd "$_pkgname-$pkgver"

  patch -Np1 -i "$srcdir/farstream-0.1.2-prefer-vp8.patch"

  ./configure \
    --prefix=/usr \
    --with-package-name='Arch Linux Farstream 0.1 Package' \
    --with-package-origin='httpw://www.archlinux.org/' \
    --disable-static
  make
}

package() {
  cd "$_pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
