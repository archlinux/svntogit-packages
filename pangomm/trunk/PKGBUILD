# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=pangomm
pkgname=('pangomm' 'pangomm-docs')
pkgver=2.40.0
pkgrel=1
arch=('i686' 'x86_64')
license=('LGPL')
makedepends=('pango' 'glibmm' 'glibmm-docs' 'cairomm' 'pkgconfig')
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/${pkgver::4}/${pkgname}-${pkgver}.tar.xz)
url="http://gtkmm.sourceforge.net/"
sha256sums=('7dd0afa9dcce57cdb0aad77da9ea46823ee8515d5f3ffd895b9ede7365c3d70d')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}
package_pangomm() {
  pkgdesc="C++ bindings for pango"
  depends=('pango' 'glibmm' 'cairomm')
  cd "${srcdir}/${pkgbase}-${pkgver}"
  sed -i -e 's/^doc_subdirs/#doc_subdirs/' Makefile
  make DESTDIR="${pkgdir}" install
}

package_pangomm-docs() {
  pkgdesc="Developer documentation for pangomm"
  cd "${srcdir}/${pkgbase}-${pkgver}/docs"
  make DESTDIR="${pkgdir}" install
}
