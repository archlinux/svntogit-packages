# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Mildred <mildred593 at online dot fr>

pkgname=claws-mail-extra-plugins
pkgver=3.7.8
pkgrel=2
pkgdesc="Extra plugins for claws-mail"
url="http://www.claws-mail.org/plugins.php?branch=EXT"
license=('GPL3')
arch=('i686' 'x86_64')
depends=('claws-mail>=3.7.8')
makedepends=('make' 'bc' 'perl>=5.10.1-5' 'libgtkhtml>=2.11.1-2' 'libnotify>=0.7'
             'libxml2>=2.7.6' 'curl>=7.19.7' 'libytnef>=1.5-2' 'libwebkit>=1.1.15.4-2'
             'pygtk>=2.16.0' 'libsoup-gnome' 'libcanberra')
optdepends=('libgtkhtml: for gtkhtml2_viewer plugin'
            'perl: for perl plugin'
            'libnotify: for notification plugin'
            'libcanberra: for notification plugin'
            'dbus-core: for notification plugin'
            'libxml2: for gtkhtml2_viewer and rssyl plugins'
            'curl: for gtkhtml2_viewer, vcalendar, rssyl and spamreport plugins'
            'libarchive: for archive plugin'
            'libytnef: for tnef_parse plugin'
            'libwebkit: for the fancy webkit html plugin'
            'libsoup-gnome: for the fancy webkit html plugin'
            'python2: for python plugin')
conflicts=('claws-gtkhtml2_viewer' 'claws-mail-acpinotifier-plugin'
           'sylpheed-claws-gtkhtml2-plugin' 'sylpheed-claws-rssyl-plugin'
           'sylpheed-claws-extra-plugins' 'claws-tnef-plugin'
           'claws-webkit-plugin-svn' 'claws-mayflower-plugin-svn')
replaces=('sylpheed-claws-extra-plugins')
options=('!libtool')
source=(http://downloads.sourceforge.net/project/sylpheed-claws/extra%20plugins/$pkgver/claws-mail-extra-plugins-$pkgver.tar.bz2
        claws-mail-notification-0.26-libnotify-0.7.patch)
md5sums=('8a67a00598262f8ca07a4af5cd43361d'
         'b76e1cdbff7af46d6594c34646c06fd0')

build() {
  cd "$srcdir/claws-mail-extra-plugins-$pkgver"

  # fix for python2
  export PYTHON="/usr/bin/python2"

  for dir in */; do
    [[ $dir != geolocation_plugin-* ]] && (
      cd $dir

      [[ $dir == notification_plugin-* ]] &&
        patch -Np0 -i "$srcdir/claws-mail-notification-0.26-libnotify-0.7.patch"

      ./configure --prefix=/usr --disable-static
      make
    )
  done
}

package() {
  cd "$srcdir/claws-mail-extra-plugins-$pkgver"
  for dir in */; do
    [[ $dir != geolocation_plugin-* ]] && 
      make -C $dir DESTDIR="$pkgdir" install
  done

  # fix fileconflict with libical, #11485
  rm -f "$pkgdir/usr/include/ical.h"
}
