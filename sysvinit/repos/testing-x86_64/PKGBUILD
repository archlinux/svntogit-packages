# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=sysvinit
pkgver=2.86
pkgrel=5
pkgdesc="Linux System V Init"
arch=('i686' 'x86_64')
url="http://freshmeat.net/projects/sysvinit/"
license=('GPL')
groups=('base')
depends=('shadow' 'util-linux' 'coreutils' 'glibc' 'awk')
source=(ftp://ftp.cistron.nl/pub/people/miquels/${pkgname}/${pkgname}-${pkgver}.tar.gz \
        80_killall_pidof.patch)
md5sums=('7d5d61c026122ab791ac04c8a84db967' '3d65ca1e0c47648d1d58cd2af29a1ea4')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -p1 < ../80_killall_pidof.patch || return 1
  cp src/init.c src/init.c.backup || return 1
  sed -i 's/\(.*\)\(Sending processes\)\(.*\)/\1\2 started by init\3/' src/init.c || return 1
  install -d ${pkgdir}/{bin,sbin}
  install -d ${pkgdir}/usr/{bin,include}
  install -d ${pkgdir}/usr/share/man/man{1,5,8}
  make -C src || return 1
  make -C src ROOT=${pkgdir} install || return 1
}
