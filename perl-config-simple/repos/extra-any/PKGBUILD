# Maintainer: Florian Pritz <bluewind@xinu.at>

pkgname=perl-config-simple
pkgver=4.59
pkgrel=15
pkgdesc="Simple configuration file class to create and read config files"
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=('perl')
url="https://search.cpan.org/~sherzodr/Config-Simple"
source=("https://search.cpan.org/CPAN/authors/id/S/SH/SHERZODR/Config-Simple-${pkgver}.tar.gz")
sha256sums=('cb78a975ad8463f992f35f392227aaf188d533c9092373742b3c2bb592781054')

build() {
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
    PERL_AUTOINSTALL=--skipdeps                            \
    PERL_MM_OPT="INSTALLDIRS=vendor"     \
    PERL_MB_OPT="--installdirs vendor" \
    MODULEBUILDRC=/dev/null

  cd "${srcdir}/Config-Simple-${pkgver}"
  /usr/bin/perl Makefile.PL
  make
}

check() {
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
  cd "${srcdir}/Config-Simple-${pkgver}"
  make test
}

package() {
  cd "$_distdir"
  cd "${srcdir}/Config-Simple-${pkgver}"
  make DESTDIR="$pkgdir" install
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}
