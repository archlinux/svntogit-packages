# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Guillaume ALAUX <guillaume@archlinux.org>

pkgname=ca-certificates-java
pkgver=20140324
pkgrel=1
pkgdesc='Common CA certificates (JKS keystore)'
arch=('any')
url='http://packages.qa.debian.org/c/ca-certificates-java.html'
license=('GPL')
depends=('ca-certificates')
makedepends=('maven')
checkdepends=('junit')
install='ca-certificates-java.install'
backup=('etc/default/cacerts')
source=("http://ftp.debian.org/debian/pool/main/c/${pkgname}/${pkgname}_${pkgver}.tar.xz"
        'init-jks-keystore' 'jks-keystore.hook')
md5sums=('18d945321d2cc391124d1c5f18617814'
         '2daf767a756d74696afae926da539ddb'
         '0beeef887fbab98e7d1d40ec0cd455dd')

prepare() {
	mkdir "${srcdir}/m2_repo"
}

build() {
	cd ${srcdir}/${pkgname}-${pkgver}
	mvn package -Dmaven.repo.local="${srcdir}/m2_repo" -Dmaven.test.skip=true
}

check() {
	cd ${srcdir}/${pkgname}-${pkgver}
	mvn -Dmaven.repo.local="${srcdir}/m2_repo" test
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}

	install -d -m755 ${pkgdir}/etc/ssl/certs/java
	install -D -m600 debian/default ${pkgdir}/etc/default/cacerts
	install -D -m644 target/ca-certificates-java-${pkgver}.jar ${pkgdir}/usr/share/ca-certificates-java/ca-certificates-java.jar
	install -D -m755 ${srcdir}/init-jks-keystore ${pkgdir}/usr/bin/init-jks-keystore
	install -D -m755 ${srcdir}/jks-keystore.hook ${pkgdir}/etc/ca-certificates/update.d/jks-keystore
}
