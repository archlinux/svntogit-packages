# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=ca-certificates-java
pkgver=20120524
pkgrel=1
pkgdesc='Common CA certificates (JKS keystore)'
arch=('any')
url='http://packages.qa.debian.org/c/ca-certificates-java.html'
license=('GPL')
depends=('ca-certificates')
makedepends=('jdk7-openjdk')
install='ca-certificates-java.install'
backup=('etc/default/cacerts')
source=("http://ftp.debian.org/debian/pool/main/c/${pkgname}/${pkgname}_${pkgver}.tar.gz"
        'init-jks-keystore' 'jks-keystore.hook')
md5sums=('ae043a6c77629546007575d7e152a679'
         '0fd9b0831a8499a4a246e2ecb998c438'
         '84fe0a111e9fe5afadaad8573430a9d1')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}
	javac -target 1.6 -source 1.6 UpdateCertificates.java
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}

	install -d -m755 ${pkgdir}/etc/ssl/certs/java
	install -D -m600 debian/default ${pkgdir}/etc/default/cacerts
	install -D -m755 UpdateCertificates.class ${pkgdir}/usr/share/ca-certificates-java/UpdateCertificates.class
	install -D -m755 ${srcdir}/init-jks-keystore ${pkgdir}/usr/sbin/init-jks-keystore
	install -D -m755 ${srcdir}/jks-keystore.hook ${pkgdir}/etc/ca-certificates/update.d/jks-keystore
}
