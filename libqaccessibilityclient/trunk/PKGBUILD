# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=libqaccessibilityclient
pkgver=0.4.0
pkgrel=1
arch=(x86_64)
pkgdesc="Helper library to make writing accessibility tools easier"
url='https://www.kde.org'
license=(LGPL2.1)
depends=(qt5-base)
makedepends=(extra-cmake-modules)
conflicts=(libkdeaccessibilityclient)
source=("https://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('6630f107eec6084cafbee29dee6a810d7174b09f7aae2bf80c31b2bc6a14deec'
            'SKIP')
validpgpkeys=(FB25861861063C64C97B89AE125C53EB96EC0C92  # Frederik Gladhorn <frederik@gladhorn.de>
              2D1D5B0588357787DE9EE225EC94D18F7F05997E) # Jonathan Riddell <jr@jriddell.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
