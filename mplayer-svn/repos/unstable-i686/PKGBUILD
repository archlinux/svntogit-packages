# $Id$
# Maintainer: Tom Killian <tom@archlinux.org>
# Contributor: lucke <lucke at o2 dot pl>

pkgname=mplayer-svn
pkgver=24986
pkgrel=1
pkgdesc="A movie player for linux"
depends=('libmad' 'libungif' 'cdparanoia' 'gtk2' 'codecs' 'sdl' 'xvidcore' \
         'libjpeg' 'lame' 'libtheora' 'esd' 'faad2' 'faac' 'ttf-bitstream-vera' \
         'libxxf86dga' 'fontconfig' 'libxinerama' 'libxv' 'libpng' \
	 'libxxf86vm' 'libgl')
makedepends=('subversion' 'pkgconfig')
source=(ftp://ftp.mplayerhq.hu/MPlayer/Skin/Blue-1.7.tar.bz2) 
conflicts=('mplayer')
provides=('mplayer')
url="http://www.mplayerhq.hu/"
license="GPL"
arch=('i686')

_svntrunk=svn://svn.mplayerhq.hu/mplayer/trunk
_svnmod=mplayer

build() {
  cd $startdir/src

  svn co $_svntrunk $_svnmod --config-dir ./ -r $pkgver
    
  cp -r $_svnmod $_svnmod-build
  cd $_svnmod-build
  
  unset CFLAGS

  ./configure --prefix=/usr --confdir=/etc/mplayer --win32codecsdir=/usr/lib/win32 \
  --enable-gui --enable-runtime-cpudetection --enable-largefiles --enable-menu \
  --disable-libdv --disable-aa --disable-liblzo --disable-speex --disable-fribidi \
  --disable-arts --disable-smb --disable-dvdnav --disable-openal --disable-faad-internal \
  --disable-x264 --disable-jack --disable-musepack --disable-libamr_nb --disable-libamr_wb \
  --disable-lirc --disable-lircc

  make || return 1
  make DESTDIR=$startdir/pkg install || return 1
  install -m644 etc/{codecs.conf,input.conf,example.conf} $startdir/pkg/etc/mplayer
  ln -s /usr/share/fonts/TTF/Vera.ttf $startdir/pkg/usr/share/mplayer/subfont.ttf
  rm -rf $startdir/pkg/usr/share/mplayer/font
  mv $startdir/src/Blue $startdir/pkg/usr/share/mplayer/skins/default
  
  rm -rf ../$_svnmod-build
}
md5sums=('e4e2020d11b681aac898103b3ba723c4')
