# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Laurent Carlier <lordheavym@gmail.com>

pkgname=telepathy-kde-text-ui
_pkgname=ktp-text-ui
pkgver=18.11.90
pkgrel=1
pkgdesc='Telepathy handler for Text Chats'
arch=(x86_64)
url='https://community.kde.org/Real-Time_Communication_and_Collaboration'
license=(GPL)
groups=(kde-applications kdenetwork telepathy-kde)
depends=(telepathy-kde-common-internals qt5-webengine kemoticons)
makedepends=(extra-cmake-modules kdoctools)
source=("https://download.kde.org/unstable/applications/$pkgver/src/$_pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('eb39804da7ceb626681faeeec1b8d282e3a1ce7c3fe4ab99419e6f663cc30a9b'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$_pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
