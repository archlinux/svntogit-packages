# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=python-evdev
pkgver=1.2.0
pkgrel=1
pkgdesc="Python bindings for the Linux input subsystem"
url="https://python-evdev.readthedocs.io/en/latest/"
arch=(x86_64)
license=(BSD)
depends=(python)
makedepends=(git)
_commit=91710cd7b86573f9bdb139ff9e9cfce212c94879  # tags/v1.2.0^0
source=("git+https://github.com/gvalkov/python-evdev#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

build() {
  cd $pkgname
  python setup.py build
}

package() {
  cd $pkgname
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 LICENSE
}
