# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-xfontsel
pkgver=1.1.0
pkgrel=1
pkgdesc='Point and click selection of X11 font names'
url='https://xorg.freedesktop.org/'
arch=('x86_64')
license=('custom')
makedepends=('xorg-util-macros')
depends=('libxaw' 'libxmu' 'libxt' 'libx11')
source=(https://xorg.freedesktop.org/archive/individual/app/xfontsel-${pkgver}.tar.xz{,.sig})
sha512sums=('be67d223668431c9b5dd2ef74032d2f087be5f8f0ccc427bc7ee43e7ce42214edddb07d8c3d6d6de93ea35314392d384f1ab1ae5c48b2cfbce418610e3945a52'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # "Alan Coopersmith <alan.coopersmith@oracle.com>"

build() {
  cd xfontsel-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd xfontsel-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
