# $Id$
# Maintainer: Tobias Kieslich <tobias@justdreams.de>
# Contributor: Tobias Kieslich <tobias@justdreams.de>

pkgname=gimp-refocus
pkgver=0.9.0
pkgrel=3
pkgdesc="A sharpen plugin for gimp using FIR Wiener filtering"
arch=('i686' 'x86_64')
url="http://refocus.sourceforge.net/"
license=('GPL2')
depends=('gimp')
source=(http://downloads.sourceforge.net/sourceforge/refocus/refocus-${pkgver}.tar.gz \
        refocus-gimp-2.0.patch refocus-mirror-fix.patch refocus-gimp-preview.patch)
md5sums=('8d4eac4ef45c904fb5e73021696bec94'
         '8ef9dfe697cd20be2be14c1ee53a240a'
         '532593cba030feab8ffa7800fc9cd782'
         '6b55dbdc656646c765064cf21e1a3c57')

build() {
  cd "${srcdir}/refocus-${pkgver}"
  patch -Np0 -i ../refocus-gimp-2.0.patch
  patch -Np0 -i ../refocus-mirror-fix.patch
  patch -Np0 -i ../refocus-gimp-preview.patch
  aclocal
  autoconf
  automake --add-missing
  ./configure --prefix=/usr --bindir=/usr/lib/gimp/2.0/plug-ins
  make
}

package() {
  cd "${srcdir}/refocus-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
