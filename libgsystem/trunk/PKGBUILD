# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=libgsystem
pkgver=2014.2
pkgrel=1
pkgdesc='"Copylib" for system service modules using GLib with GCC'
url="https://wiki.gnome.org/Projects/LibGSystem"
license=(GPL2)
arch=(i686 x86_64)
depends=(glib2 libsystemd)
makedepends=(gobject-introspection gtk-doc systemd)
source=(https://git.gnome.org/browse/$pkgname/snapshot/$pkgname-$pkgver.tar.xz)
sha256sums=('cbe8984b4f68bc0ba1fcecd5b33a763a60cc874a050e49ab62c414e8d79b21d1')

build() {
  cd $pkgname-$pkgver
  ./autogen.sh --prefix=/usr --disable-static
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
