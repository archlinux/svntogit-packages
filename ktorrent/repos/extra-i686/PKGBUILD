# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Travis Willard <travisw@wmpub.ca>
# Contributor: Georg Grabler <ggrabler@gmail.com>

pkgname=ktorrent
pkgver=4.3.0
pkgrel=2
pkgdesc="A powerful BitTorrent client for KDE"
arch=('i686' 'x86_64')
url='http://ktorrent.org/'
license=('GPL2')
depends=('libktorrent' 'kdebase-workspace' 'taglib')
makedepends=('automoc4' 'cmake' 'boost')
source=("http://ktorrent.org/downloads/${pkgver}/${pkgname}-${pkgver}.tar.bz2"
        'httpserver.patch' 'groupmiewmodel.diff')
install=${pkgname}.install
sha1sums=('60400c6286f58934dde7ab471f56188b68d69f7b'
          'cef22b24af540e1bfcf13da60e4846ade106c303'
	  '5b91b40e38afb15452bcb0644cd7ba57bc9db6eb' )

options=(strip)


build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	# build time patch
	patch -p0 -i "${srcdir}/httpserver.patch"
	patch -p0 -i "${srcdir}/groupmiewmodel.diff"

	cd "${srcdir}"
	mkdir build
	cd build
	cmake ../${pkgname}-${pkgver} \
	  -DCMAKE_BUILD_TYPE=Release \
	  -DCMAKE_INSTALL_PREFIX=/usr
	make
}

package() {
	cd "${srcdir}/build"
	make DESTDIR="${pkgdir}" install
}
