# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=baloo-widgets
pkgver=22.08.0
pkgrel=2
pkgdesc='Widgets for Baloo'
arch=(x86_64)
url='https://www.kde.org/'
license=(LGPL)
depends=(baloo)
makedepends=(extra-cmake-modules kdoctools)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        https://invent.kde.org/libraries/baloo-widgets/-/commit/7ff54ae9b68ca0edf3d6cd57c3eefb6cf072b6f8.patch)
sha256sums=('38f799fde70b1d3fad21593caee97338d226c2ccfe0602ef7d96ea5a0c9689fa'
            'SKIP'
            'b5d0335958637104a10dc9d1d1b96812e7516c098b2dc513689f1bf714e7ea71')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
options=(debug)

prepare() {
  # backport for KIO 5.98
  patch -d $pkgname-$pkgver -p1 < 7ff54ae9b68ca0edf3d6cd57c3eefb6cf072b6f8.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}

