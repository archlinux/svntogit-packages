# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=vice
pkgver=2.3
pkgrel=3
pkgdesc="A Versatile Commodore Emulator"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.viceteam.org/"
depends=('alsa-lib' 'gcc-libs' 'giflib' 'lame' 'libxrandr' 'libxv' 'libjpeg' 'libpng' 'libxaw' 
'libxxf86vm')
makedepends=('xorg-font-utils')
options=('!makeflags')
source=(http://downloads.sourceforge.net/project/vice-emu/releases/${pkgname}-${pkgver}.tar.gz
        vice-2.3-x11video.patch)
md5sums=('b48d137874daad50c087a0686cbdde34'
         '088292ce9efe785edfb2b0c7c670785e')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -Np1 -i ${srcdir}/vice-2.3-x11video.patch
  ./configure --prefix=/usr \
              --with-alsa \
              --enable-fullscreen
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
}
