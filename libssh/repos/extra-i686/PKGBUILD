# $Id$
# Maintainer: Tom Gundersen <teg@jklm.no>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: ice-man <icemanf@gmail.com>
# Contributor: sergeantspoon <sergeantspoon@archlinux.us>

pkgbase=libssh
pkgname=(libssh libssh-docs)
pkgver=0.7.1
pkgrel=2
pkgdesc="Library for accessing ssh client services through C libraries"
url="http://www.libssh.org/"
license=('LGPL')
arch=('i686' 'x86_64')
depends=('openssl')
makedepends=('cmake' 'cmocka' 'doxygen')
checkdepends=('openssh')
source=(https://red.libssh.org/attachments/download/154/$pkgname-$pkgver.tar.xz
        https://red.libssh.org/attachments/download/153/$pkgname-$pkgver.tar.asc)
md5sums=('bffc9dc548c3bae3a3afc5ac1654b272'
         'SKIP')
validpgpkeys=('8DFF53E18F2ABC8D8F3C92237EE0FC4DCC014E3D') # Andreas Schneider <asn@cryptomilk.org>

prepare() {
  # disable the test. It is confused by our clean container setup.
  # 'extra-x86-build' uses user 'nobody' that has a record in /etc/passwd file
  # but $HOME envvar is set to '/build'. The test expects that $HOME corresponds to passwd file.
  sed 's/unit_test(torture_path_expand_tilde_unix),//' -i libssh-${pkgver}/tests/unittests/torture_misc.c
}

build() {
  mkdir -p build
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DWITH_GSSAPI=OFF \
    -DWITH_TESTING=ON
  make
  make doc
}

check() {
  cd build
  make test
}

package_libssh() {
  cd build
  make DESTDIR="$pkgdir" install
}

package_libssh-docs() {
  pkgdesc="Documentation for libssh"
  depends=()

  mkdir -p "$pkgdir"/usr/share/doc/libssh
  cp -r build/doc/html "$pkgdir"/usr/share/doc/libssh
  cp -r build/doc/man "$pkgdir"/usr/share
}
