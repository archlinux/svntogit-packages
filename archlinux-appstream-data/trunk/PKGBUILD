# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=archlinux-appstream-data
pkgver=20191027
pkgrel=1
pkgdesc="Arch Linux application database for AppStream-based software centers"
arch=(any)
url="https://www.archlinux.org"
license=(GPL)
depends=()
makedepends=()
source=()
noextract=()
for _repo in core extra community; do
 source+=($_repo-$pkgver.xml.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/Components-x86_64.xml.gz
          $_repo-icons-64x64-$pkgver.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-64x64.tar.gz
          $_repo-icons-128x128-$pkgver.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-128x128.tar.gz)
 noextract+=($_repo.xml.gz-$pkgver $_repo-icons-{64x64,128x128}-$pkgver.tar.gz)
done
sha256sums=('a410bc76f84ad7ae9a38d00757921705ac240d6392d710d26021bf5c44e9e5f7'
            '7989bb311baa38ef545250282aa065d23281c46dfb8faabe4c653487bdbded5c'
            '7989bb311baa38ef545250282aa065d23281c46dfb8faabe4c653487bdbded5c'
            'ec010f1ff068c70b3f6002481b96c9b292d176a3e353da376c2094ab9ca0f051'
            '6e4f64501aa9a8fc1747b9a8f500679b59df028aee066640665d2eb42e02dedb'
            'dcb911b549f36c5c8d583399a9674ae27644a5861bf6e39d9a986197b8db97c5'
            '8e20aa838a08991170d4fae29a77687a0dfb96c0a27f31720db482bf086146fd'
            'fef501aeb81c00d7236b5afabcb56d3c152ef191a247020eaf7a94831d013294'
            'e18a48194193a346f8434a4aaf1e70abfad87a6dba31d1c2daa032e6d612a827')

package() {
  mkdir -p "$pkgdir"/usr/share/app-info/{icons/archlinux-arch-{core,extra,community}/{64x64,128x128},xmls}
  for _repo in core extra community; do
   tar -xzf $_repo-icons-64x64-$pkgver.tar.gz -C "$pkgdir"/usr/share/app-info/icons/archlinux-arch-$_repo/64x64
   tar -xzf $_repo-icons-128x128-$pkgver.tar.gz -C "$pkgdir"/usr/share/app-info/icons/archlinux-arch-$_repo/128x128
   install -m644 $_repo-$pkgver.xml.gz "$pkgdir"/usr/share/app-info/xmls/$_repo.xml.gz
  done
}
