# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=archlinux-appstream-data
pkgver=20230115
pkgrel=1
pkgdesc='Arch Linux application database for AppStream-based software centers'
arch=(any)
url='https://www.archlinux.org'
license=(GPL)
depends=()
makedepends=()
source=()
noextract=()
for _repo in core extra community multilib; do
 source+=($_repo-$pkgver.xml.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/Components-x86_64.xml.gz
          $_repo-icons-48x48-$pkgver.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-48x48.tar.gz
          $_repo-icons-64x64-$pkgver.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-64x64.tar.gz
          $_repo-icons-128x128-$pkgver.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-128x128.tar.gz)
 noextract+=($_repo.xml.gz-$pkgver $_repo-icons-{48x48,64x64,128x128}-$pkgver.tar.gz)
done
sha256sums=('aefec07bfec572f9fb835ea0e6e5f9d443e378af34d959e7967b48fd11258b44'
            '7989bb311baa38ef545250282aa065d23281c46dfb8faabe4c653487bdbded5c'
            '06e728d9a11807645a97790bb212dbdba51b54bfb69e0a9e9c5862c4e0c78d74'
            '7989bb311baa38ef545250282aa065d23281c46dfb8faabe4c653487bdbded5c'
            '4751a74450c8f679783de675047ef39d27c638f21c8239383f39caf23c998c02'
            'fd3119e195f2a660eb3e75c2ab6ef48afe5014e1b601cbfb9eae762e25f92a40'
            'fe991c734fc59452a04ff34c648d4ee8c2688b234623c750485ad92afe10a405'
            '5c03ab08b0689cab8bbfa9e55de3b4448e8775e3f0bb8a39e20e0967b4cc954f'
            '93fad1bcf4096828ebb28ae8a146a650b63ddbbd1ad052636dce9cf8411e5696'
            '19fb8b5f22d27ff2eca660e0f56f40ace45429cd6383c75c0a9ad86c13cc28b5'
            '1af7fde2ce2ebecb1323ec2336bd955497d0e9dff89f5640f8da10ed95e7c7a8'
            '703abd682abb827d6355301fe7c7058fb8cad85e20f81390f1dc518e5c16c0b2'
            'e8cdaf76cbca778f01d440cb242fdf9a3893ea073a74474cbaafd0f8e4c063af'
            'aa331e65dbdc8c8e271c3a1bacf58f9c09b6c3fb24bab023df8cc642d100c36a'
            '0a5823f58d58fb92a0da4b15949c5123cb16a9b345e5e7bfc1affd8fb9c274de'
            'f4c2f848f7a4eaeceb989fda1dabf2a0670f525beb7efa6c5dbb2b853a1bd652')

package() {
  mkdir -p "$pkgdir"/usr/share/app-info/{icons/archlinux-arch-{core,extra,community,multilib}/{48x48,64x64,128x128},xmls}
  for _repo in core extra community multilib; do
   tar -xzf $_repo-icons-48x48-$pkgver.tar.gz -C "$pkgdir"/usr/share/app-info/icons/archlinux-arch-$_repo/48x48
   tar -xzf $_repo-icons-64x64-$pkgver.tar.gz -C "$pkgdir"/usr/share/app-info/icons/archlinux-arch-$_repo/64x64
   tar -xzf $_repo-icons-128x128-$pkgver.tar.gz -C "$pkgdir"/usr/share/app-info/icons/archlinux-arch-$_repo/128x128
   install -m644 $_repo-$pkgver.xml.gz "$pkgdir"/usr/share/app-info/xmls/$_repo.xml.gz
  done
}
