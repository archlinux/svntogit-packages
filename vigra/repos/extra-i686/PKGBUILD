# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>

pkgname=vigra
pkgver=1.7.0
pkgrel=1
pkgdesc="Computer vision library"
arch=('i686' 'x86_64')
url="http://hci.iwr.uni-heidelberg.de/vigra/"
license=('custom:MIT')
depends=('libpng' 'libtiff' 'gcc-libs' 'sh' 'hdf5' 'fftw')
makedepends=('cmake' 'python-nose' 'doxygen' 'python-sphinx' 'boost')
optdepends=('python:	for python bindings'
           'boost:	for python bindings')
options=('!libtool')
source=(http://kogs-www.informatik.uni-hamburg.de/~koethe/vigra/${pkgname}-${pkgver}-src.tar.gz libpng-1.4.patch)
md5sums=('a8e5326433ee7bf77f6daa3dd00d6c61'
         'db610e48558eb323490ad21d636bac76')

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}-src
  patch -p1 < ../libpng-1.4.patch || return 1
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
	-DWITH_VIGRANUMPY=1 \
	-DDOCINSTALL=share/doc 
  make || return 1
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}-src
  make DESTDIR=$pkgdir install || return 1
  # license
  install -D -m644 LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
