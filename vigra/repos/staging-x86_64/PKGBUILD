# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>

pkgname=vigra
pkgver=1.7.0
pkgrel=5
pkgdesc="Computer vision library"
arch=('i686' 'x86_64')
url="http://hci.iwr.uni-heidelberg.de/vigra/"
license=('custom:MIT')
depends=('libpng' 'libtiff' 'gcc-libs' 'sh' 'hdf5' 'fftw')
makedepends=('cmake' 'python-nose' 'doxygen' 'python-sphinx' 'boost' 'python-numpy')
optdepends=('python2:  for python bindings'
            'boost:    for python bindings')
options=('!libtool')
source=(http://kogs-www.informatik.uni-hamburg.de/~koethe/vigra/${pkgname}-${pkgver}-src.tar.gz libpng-1.4.patch)
md5sums=('a40b3b013653f75dc84ba55b2df1fae2'
         'db610e48558eb323490ad21d636bac76')

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}-src
  patch -p1 < ../libpng-1.4.patch
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
      -DWITH_VIGRANUMPY=1 \
      -DDOCINSTALL=share/doc 
  make
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}-src
  make DESTDIR=$pkgdir install
  # license
  install -D -m644 LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
