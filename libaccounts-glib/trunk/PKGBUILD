# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=libaccounts-glib
pkgver=1.24
pkgrel=1
pkgdesc="Glib-based client library for accessing the online accounts database"
arch=(x86_64)
url="https://gitlab.com/accounts-sso/libaccounts-glib"
license=(LGPL)
depends=(dbus-glib libxml2 sqlite)
makedepends=(python-gobject gobject-introspection vala meson check)
source=($pkgname-$pkgver.tar.gz::"https://gitlab.com/accounts-sso/libaccounts-glib/repository/archive.tar.gz?ref=VERSION_$pkgver")
md5sums=('e8bd50db8ccb7c6c795373c45c3e9dbf')

prepare() {
  mkdir -p build
  mv $pkgname-*/ $pkgname
}

build() {
  cd build
  arch-meson ../$pkgname -Dtests=false
  ninja
}

check() {
  cd build
  ninja test || true
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}
