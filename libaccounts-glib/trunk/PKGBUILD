# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=libaccounts-glib
pkgver=1.23
pkgrel=3
pkgdesc="Glib-based client library for accessing the online accounts database"
arch=(x86_64)
url="https://gitlab.com/accounts-sso/libaccounts-glib"
license=(LGPL)
depends=(dbus-glib libxml2 sqlite)
makedepends=(python-gobject docbook-xsl gtk-doc)
source=($pkgname-$pkgver.tar.gz::"https://gitlab.com/accounts-sso/libaccounts-glib/repository/archive.tar.gz?ref=VERSION_$pkgver")
md5sums=('af872137184b38e39db415d67a32a510')

build() {
  cd $pkgname-VERSION*
  ./autogen.sh
  ./configure \
    --prefix=/usr \
    --disable-static \
    --disable-gtk-doc
  make
}

package() {
  cd $pkgname-VERSION*
  make DESTDIR="$pkgdir" install
}
