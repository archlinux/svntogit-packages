# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Emmanuel Gil Peyrot <linkmauve@linkmauve.fr>

pkgname=libtiger
pkgver=0.3.4
pkgrel=5
pkgdesc="A rendering library for Kate streams using Pango and Cairo"
url="http://git.xiph.org/?p=users/oggk/tiger.git;a=summary"
license=('LGPL')
arch=('x86_64')
depends=('pango' 'libkate')
makedepends=('pkg-config' 'git')
source=("$pkgname::git+git://git.xiph.org/users/oggk/tiger.git#tag=tiger-${pkgver}"
        "0001-Fix-automake.patch")
md5sums=('SKIP'
         '1e7306e6b26e6915308b9a01c6dfda9c')

prepare() {
  cd "${srcdir}/${pkgname}"
  patch -Np1 -i "${srcdir}/0001-Fix-automake.patch"
  ./autogen.sh
}

build() {
  cd "${srcdir}/${pkgname}"
  ./configure --prefix=/usr \
              --enable-static=no \
              --disable-doc

  make
}

package() {
  cd "${srcdir}/${pkgname}"

  make DESTDIR="${pkgdir}" install
}
