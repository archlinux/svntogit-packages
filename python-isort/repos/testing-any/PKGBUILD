# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Yaron de Leeuw <me@jarondl.net>

pkgname=python-isort
pkgver=4.3.20
pkgrel=1
pkgdesc="A Python utility / library to sort Python imports."
arch=('any')
url="https://github.com/timothycrosley/isort"
license=('MIT')
depends=('python-setuptools')
checkdepends=('python-pytest' 'python-mock' 'python-pip' 'python-pip-api' 'pylama' 'python-pipreqs'
              'python-requirementslib')
source=("$pkgname-$pkgver.tar.gz::https://github.com/timothycrosley/isort/archive/$pkgver.tar.gz")
sha512sums=('03cca362a5c70b27f0893c00abdd65b39d35dfbd034b24a60703d222f0f21f8de5f650f2fb85adf6f37b1b33bfe02bf1b93d486000af7c6f45302a0d3c355cfb')

build() {
  cd isort-$pkgver
  python setup.py build
}

check() {
  cd isort-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.7/site-packages" PATH="$PWD/tmp_install/usr/bin:$PATH" pytest
}

package() {
  cd isort-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
