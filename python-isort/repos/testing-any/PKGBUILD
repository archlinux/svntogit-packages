# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Yaron de Leeuw <me@jarondl.net>

pkgname=python-isort
pkgver=5.0.4
pkgrel=1
pkgdesc="A Python utility / library to sort Python imports."
arch=('any')
url="https://github.com/timothycrosley/isort"
license=('MIT')
depends=('python')
makedepends=('python-dephell')
checkdepends=('pylama' 'python-hypothesis-auto' 'python-pip' 'python-pip-api' 'python-pipreqs'
              'python-pytest' 'python-requirementslib')
source=("https://github.com/timothycrosley/isort/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('99c8d6b6322829cdb1d1a0df935b2857edef96b166eb342f1fc3cee6d109e1a774e2b84df925dac81fc9d3d4532be008e6581230e527fda4362ba1809b237c8e')

prepare() {
  cd isort-$pkgver
  # poetry-generated setup.py are fatally broken, see:
  # https://github.com/sdispater/poetry/issues/866
  dephell deps convert --from pyproject.toml --to setup.py
}

build() {
  cd isort-$pkgver
  python setup.py build
}

check() {
  cd isort-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.8/site-packages" PATH="$PWD/tmp_install/usr/bin:$PATH" pytest --deselect tests/test_importable.py::test_importable
}

package() {
  cd isort-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
