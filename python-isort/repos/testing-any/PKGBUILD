# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Yaron de Leeuw <me@jarondl.net>

pkgbase=python-isort
pkgname=('python-isort' 'python2-isort')
pkgver=4.2.15
pkgrel=1
pkgdesc="A Python utility / library to sort Python imports."
arch=('any')
url="https://github.com/timothycrosley/isort"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest' 'python2-pytest' 'python-mock' 'python2-mock')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/timothycrosley/isort/archive/$pkgver.tar.gz")
sha512sums=('9defe4e5404a5b66766bca7afb6c3a2c1b3161202dacbac0f54f2935720df6ae1fbd256dfe37c21e773aa29345c32dfdbdceb887f003dc0bdd59e5e8a19ff073')

prepare() {
  cp -a isort-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/isort-$pkgver
  python setup.py build

  cd "$srcdir"/isort-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/isort-$pkgver
  python setup.py test

  cd "$srcdir"/isort-$pkgver-py2
  python2 setup.py test
}

package_python-isort() {
  depends=('python-setuptools')

  cd isort-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-isort() {
  depends=('python2-setuptools')

  cd isort-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  mv "$pkgdir"/usr/bin/isort{,2}
}
