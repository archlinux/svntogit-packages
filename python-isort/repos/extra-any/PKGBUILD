# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Yaron de Leeuw <me@jarondl.net>

pkgbase=python-isort
pkgname=('python-isort' 'python2-isort')
pkgver=4.2.5
pkgrel=2
pkgdesc="A Python utility / library to sort Python imports."
arch=('any')
url="https://github.com/timothycrosley/isort"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'git')
checkdepends=('python-pytest' 'python2-pytest' 'python-mock' 'python2-mock')
source=("git+https://github.com/timothycrosley/isort.git#tag=$pkgver")
sha512sums=('SKIP')

prepare() {
  cp -a isort{,-py2}
}

build() {
  cd "$srcdir"/isort
  python setup.py build

  cd "$srcdir"/isort-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/isort
  python setup.py test

  cd "$srcdir"/isort-py2
  python2 setup.py test
}

package_python-isort() {
  depends=('python-setuptools')

  cd isort
  python setup.py install --root="$pkgdir/" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-isort() {
  depends=('python2-setuptools')

  cd isort-py2
  python2 setup.py install --root="$pkgdir/" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  mv "$pkgdir"/usr/bin/isort{,2}
}
