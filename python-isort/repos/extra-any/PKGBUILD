# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Yaron de Leeuw <me@jarondl.net>

pkgname=python-isort
pkgver=4.3.21.2
_tag=4.3.21-2
pkgrel=1
pkgdesc="A Python utility / library to sort Python imports."
arch=('any')
url="https://github.com/timothycrosley/isort"
license=('MIT')
depends=('python-setuptools')
checkdepends=('python-pytest' 'python-mock' 'python-pip' 'python-pip-api' 'pylama' 'python-pipreqs'
              'python-requirementslib')
source=("$pkgname-$_tag.tar.gz::https://github.com/timothycrosley/isort/archive/$_tag.tar.gz")
sha512sums=('fc2aa00c87196864864d2516b8be7acdedcf40d71d1931510e4b878dbd2b6da0b579661b50221f03f4aa5560c8bf23d5e1e10a2694d2b741ca09a92e40a4c687')

build() {
  cd isort-$_tag
  python setup.py build
}

check() {
  cd isort-$_tag
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.7/site-packages" PATH="$PWD/tmp_install/usr/bin:$PATH" pytest || warning "Tests failed"
}

package() {
  cd isort-$_tag
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
