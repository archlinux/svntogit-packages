# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Yaron de Leeuw <me@jarondl.net>

pkgname=python-isort
pkgver=5.5.5
pkgrel=1
pkgdesc="A Python utility / library to sort Python imports."
arch=('any')
url="https://github.com/timothycrosley/isort"
license=('MIT')
depends=('python-toml')
makedepends=('python-dephell')
checkdepends=('git' 'pylama' 'python-black' 'python-hypothesis-auto' 'python-hypothesmith'
              'python-libcst' 'python-pip' 'python-pip-api' 'python-pipreqs' 'python-pytest'
              'python-pytest-mock' 'python-requirementslib')
source=("https://github.com/timothycrosley/isort/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('7d5918b98f324ee13fecef54fdac662bf4d692cabe49d7b4b4f0646d372e148e13f24add9afbaabbb0b65ccb631d5f15b3005fbd0936955c46583c1fc1f13614')

prepare() {
  cd isort-$pkgver

  # Devendor toml
  rm -r isort/_vendored
  sed -i 's/from ._vendored //' isort/settings.py

  # poetry-generated setup.py are fatally broken, see:
  # https://github.com/sdispater/poetry/issues/866
  dephell deps convert --from pyproject.toml --to setup.py
  for _plugin in example_isort_formatting_plugin example_shared_isort_profile; do
    pushd $_plugin
    dephell deps convert --from pyproject.toml --to setup.py
    popd
  done
}

build() {
  cd isort-$pkgver
  python setup.py build
}

check() {
  cd isort-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  for _plugin in example_isort_formatting_plugin example_shared_isort_profile; do
    pushd $_plugin
    python setup.py install --root="$srcdir/isort-$pkgver/tmp_install" --optimize=1
    popd
  done
  # This is just weird
  PYTHONPATH="$PWD/example_isort_formatting_plugin:$PWD/example_shared_isort_profile:$PWD/tmp_install/usr/lib/python3.8/site-packages" PATH="$PWD/tmp_install/usr/bin:$PATH" \
    pytest --deselect tests/test_importable.py::test_importable
}

package() {
  cd isort-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
