# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
pkgname=qemu-kvm
pkgver=1.1.1
pkgrel=1
pkgdesc="Latest KVM QEMU is a generic and open source processor emulator which achieves a good emulation speed by using dynamic translation."
arch=(i686 x86_64)
license=('GPL2' 'LGPL2.1')
url="http://www.linux-kvm.org"
depends=('libjpeg' 'libpng' 'libsasl' 'curl' 'sdl' 'alsa-lib' 'nss' 'glib2' 'gnutls>=2.4.1' 'bluez' 'vde2' 'util-linux' 'libpulse' 'seabios' 'libcap-ng')
makedepends=('texi2html' 'perl' 'python2')
backup=('etc/qemu/target-x86_64.conf')
install=qemu-kvm.install
conflicts=('qemu')
provides=('qemu')
replaces=('kvm')
source=(http://downloads.sourceforge.net/kvm/${pkgname}-${pkgver}.tar.gz
        65-kvm.rules)
options=(!strip)

build()
{
    cd "${srcdir}/${pkgname}-${pkgver}"
    # fix esound building
    ./configure --prefix=/usr \
                --python=/usr/bin/python2 \
                --sysconfdir=/etc \
                --audio-drv-list=alsa,sdl,oss,pa \
                --audio-card-list=ac97,sb16,es1370,hda \
                --enable-docs
    make
}

package()
{
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" libexecdir=/usr/lib/qemu install
    rm "${pkgdir}/usr/share/qemu/bios.bin"

    # symbolic link for backwards compatibility
    ln -s qemu-system-x86_64 "${pkgdir}/usr/bin/qemu-kvm"
    # symbolic link for to qemu binary for emulator apps
    ln -s qemu-system-x86_64 "${pkgdir}/usr/bin/qemu"
    # symbolic link for to qemu binary for emulator apps
    ln -s qemu-system-x86_64 "${pkgdir}/usr/bin/kvm"
    # fix man page
    mv "${pkgdir}/usr/share/man/man1/qemu.1" \
                     "${pkgdir}/usr/share/man/man1/qemu-kvm.1"
    # install udev rules
    install -D -m644 "${srcdir}/65-kvm.rules" \
                     "${pkgdir}/usr/lib/udev/rules.d/65-kvm.rules"
    # strip scripts directory
    find "${pkgdir}/usr/bin"  -type f -perm -u+w 2>/dev/null | while read binary ; do
      case "$(file -bi "$binary")" in
        *application/x-executable*) # Binaries
        /usr/bin/strip $STRIP_BINARIES "$binary";;
      esac
    done
}
md5sums=('f8f5629b00f48192cc0467e1d31c4dd2'
         'b316a066d2f1bb57d8f5b7ea1d0d1caf')
