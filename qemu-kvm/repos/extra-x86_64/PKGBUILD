# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
pkgname=qemu-kvm
pkgver=0.14.1
pkgrel=2
pkgdesc="Latest KVM QEMU is a generic and open source processor emulator which achieves a good emulation speed by using dynamic translation."
arch=(i686 x86_64)
license=('GPL2' 'LGPL2.1')
url="http://www.linux-kvm.org"
depends=('libjpeg' 'libpng' 'libsasl' 'curl' 'sdl' 'alsa-lib' 'esound' 'gnutls>=2.4.1' 'bluez' 'vde2' 'util-linux-ng')
makedepends=('texi2html' 'perl')
backup=('etc/qemu/target-x86_64.conf')
install=qemu-kvm.install
conflicts=('qemu')
provides=('qemu')
replaces=('kvm')
source=(http://downloads.sourceforge.net/kvm/${pkgname}-${pkgver}.tar.gz
        65-kvm.rules)
options=(!strip)

build()
{
    cd ${srcdir}/${pkgname}-${pkgver}
    # fix esound building
    ./configure --prefix=/usr \
                --sysconfdir=/etc \
                --audio-drv-list=alsa,sdl,oss,esd \
                --audio-card-list=ac97,sb16,es1370,hda \
                --enable-docs
    make
}
package()
{
    cd ${srcdir}/${pkgname}-${pkgver}
    make DESTDIR=${pkgdir} install
    # symbolic link for backwards compatibility
    ln -s qemu-system-x86_64 ${pkgdir}/usr/bin/qemu-kvm
    # symbolic link for to qemu binary for emulator apps
    ln -s qemu-system-x86_64 ${pkgdir}/usr/bin/qemu
    # symbolic link for to qemu binary for emulator apps
    ln -s qemu-system-x86_64 ${pkgdir}/usr/bin/kvm
    # fix man page
    mv ${pkgdir}/usr/share/man/man1/qemu.1 \
                     ${pkgdir}/usr/share/man/man1/qemu-kvm.1
    # install udev rules
    install -D -m644 ${srcdir}/65-kvm.rules \
                     ${pkgdir}/lib/udev/rules.d/65-kvm.rules
    # strip scripts directory
    find ${pkgdir}/usr/bin  -type f -perm -u+w 2>/dev/null | while read binary ; do
      case "$(file -bi "$binary")" in
        *application/x-executable*) # Binaries
        /usr/bin/strip $STRIP_BINARIES "$binary";;
      esac
    done
}
md5sums=('ed37a2c0b1981220b842920140574c63'
         'b316a066d2f1bb57d8f5b7ea1d0d1caf')
