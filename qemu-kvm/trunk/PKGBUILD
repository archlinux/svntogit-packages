# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
pkgname=qemu-kvm
pkgver=0.12.3
pkgrel=2
pkgdesc="Latest KVM QEMU is a generic and open source processor emulator which achieves a good emulation speed by using dynamic translation."
arch=(i686 x86_64)
license=('GPL2' 'LGPL2.1')
url="http://www.linux-kvm.org"
depends=('libsasl' 'curl' 'sdl' 'alsa-lib' 'esound' 'gnutls>=2.4.1' 'bluez' 'vde2' 'util-linux-ng')
makedepends=('texi2html' 'perl')
install=qemu-kvm.install
conflicts=('qemu')
provides=('qemu')
replaces=('kvm')
source=(http://downloads.sourceforge.net/kvm/${pkgname}-${pkgver}.tar.gz
        65-kvm.rules
        block-avoid-creating-too-large-iovecs-in-multiwrite_merge.diff)

build()
{
    cd ${srcdir}/${pkgname}-${pkgver}
    patch -p1 -i ${srcdir}/block-avoid-creating-too-large-iovecs-in-multiwrite_merge.diff || return 1
    # fix esound building
    ./configure --prefix=/usr \
                --audio-drv-list=alsa,sdl,oss,esd \
                --audio-card-list=ac97,sb16,es1370,adlib \
                --enable-docs || return 1
    make || return 1
    make DESTDIR=${pkgdir} install || return 1
    # symbolic link for backwards compatibility
    ln -s qemu-system-x86_64 ${pkgdir}/usr/bin/qemu-kvm
    # symbolic link for to qemu binary for emulator apps
    ln -s qemu-system-x86_64 ${pkgdir}/usr/bin/qemu
    # fix man page
    mv ${pkgdir}/usr/share/man/man1/qemu.1 \
                     ${pkgdir}/usr/share/man/man1/qemu-kvm.1
    # install udev rules
    install -D -m644 ${srcdir}/65-kvm.rules \
                     ${pkgdir}/lib/udev/rules.d/65-kvm.rules
}
md5sums=('ab484975004f66fb48cb5589bd9b9dcb'
         'b316a066d2f1bb57d8f5b7ea1d0d1caf'
         '3d71cb1c3d7c24e4ee1a6264b6f60dca')
