# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=libgames-support
pkgver=1.0.2
pkgrel=1
pkgdesc="Code shared between GNOME games"
url="https://git.gnome.org/browse/libgames-support/"
license=(LGPL)
arch=(i686 x86_64)
depends=(gtk3 libgee)
makedepends=(gobject-introspection vala intltool)
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver:0:3}/$pkgname-$pkgver.tar.xz)
sha256sums=('b096e8d8ddc57ff247edc5eae842d405402b043e289f3a0949499e1df514ba66')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-static
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
