# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=kmplayer
pkgver=0.11.2b
pkgrel=1
pkgdesc="Video player plugin for Konqueror and basic MPlayer/Xine/ffmpeg/ffserver/VDR frontend for KDE"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
url="http://kmplayer.kde.org"
depends=('kdebase-runtime' 'gtk2')
makedepends=('pkgconfig' 'cmake' 'automoc4')
install=${pkgname}.install
source=("http://kmplayer.kde.org/pkgs/$pkgname-$pkgver.tar.bz2")
md5sums=('4dcaf4dc7fa2b2e9a2792ae7cd525bd0')

build() {
  cd ${srcdir}
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make || return 1
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install || return 1

  # icons are included in oxygen-icons
  rm -rf ${pkgdir}/usr/share/icons/oxygen/
}
