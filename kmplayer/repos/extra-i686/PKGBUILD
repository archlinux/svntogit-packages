# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=kmplayer
pkgver=0.11.2c
pkgrel=1
pkgdesc="Video player plugin for Konqueror and basic MPlayer/Xine/ffmpeg/ffserver/VDR frontend for KDE"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
url="http://kmplayer.kde.org"
depends=('kdebase-runtime' 'pango')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'docbook-xsl')
install=${pkgname}.install
source=("http://kmplayer.kde.org/pkgs/$pkgname-$pkgver.tar.bz2")
md5sums=('7a1363dfaad0c7b563ecbd58f301e704')

build() {
  cd ${srcdir}
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install
}
