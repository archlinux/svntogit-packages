# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=qtwebkit
pkgver=2.3.0
pkgrel=1
arch=('i686' 'x86_64')
url='http://trac.webkit.org/wiki/QtWebKit'
pkgdesc='An open source web browser engine (Qt port)'
license=('LGPL2.1' 'GPL3')
depends=('qt4' 'systemd' 'gstreamer0.10-base')
makedepends=('gperf' 'python2' 'ruby' 'git' 'mesa')
conflicts=('qt<4.8')
_qtver=4.8.4
source=("${pkgname}-${pkgver}.tar.gz"::"http://gitorious.org/webkit/qtwebkit-23/archive-tarball/${pkgname}-${pkgver}"
        "http://releases.qt-project.org/qt4/source/qt-everywhere-opensource-src-${_qtver}.tar.gz"
        'use-python2.patch'
        'qwebview.patch')
sha1sums=('8e0aa2f895376a0a88b55548bfadb270da3708ba'
          'f5880f11c139d7d8d01ecb8d874535f7d9553198'
          '315b6ff603f35e5492a036f7082f6aa075dfb607'
          'ef467fcfc9e74aa88356f27acc21792706ed1e4d')

build() {
  cd webkit-qtwebkit-23

  patch -p1 -i "${srcdir}"/use-python2.patch

  OPTS="--no-webkit2"
  if [ "${CARCH}" = "i686" ]; then
      # FS#33418
      OPTS="${OPTS} --no-force-sse2"
  fi

  export QTDIR=/usr
  export PATH="/usr/lib/qt4/bin:$PATH"
  Tools/Scripts/build-webkit --qt \
    --makeargs="${MAKEFLAGS}" \
    --prefix=/usr \
    ${OPTS}

  # Build the QWebView plugin (FS#27914)
  cd ../qt-everywhere-opensource-src-${_qtver}
  patch -p1 -i "${srcdir}"/qwebview.patch
  cd tools/designer/src/plugins/qwebview
  qmake-qt4
  make
}

package() {
  cd webkit-qtwebkit-23
  make INSTALL_ROOT="${pkgdir}" -C WebKitBuild/Release install

  cd ../qt-everywhere-opensource-src-${_qtver}/tools/designer/src/plugins/qwebview
  make INSTALL_ROOT="${pkgdir}" install
}
