# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=flex
pkgver=2.5.35
pkgrel=3
pkgdesc="A tool for generating text-scanning programs"
arch=('i686' 'x86_64')
url="http://flex.sourceforge.net"
license=('custom')
groups=('base-devel')
depends=('glibc' 'm4' 'sh')
install=flex.install
source=(http://downloads.sourceforge.net/sourceforge/flex/flex-$pkgver.tar.bz2 
        flex-2.5.35-gcc44.patch
	lex.sh)
md5sums=('10714e50cea54dc7a227e3eddcd44d57'
         'e4444ef5c07db71a43280be74139bdea'
         'f725259ec23a9e87ee29e2ef82eda9a5')

build() {
  cd $srcdir/$pkgname-$pkgver || return 1
  patch -Np1 -i $srcdir/flex-2.5.35-gcc44.patch || return 1
  
  ./configure --prefix=/usr \
    --mandir=/usr/share/man --infodir=/usr/share/info || return 1
  make || return 1
  make prefix=$pkgdir/usr \
    mandir=$pkgdir/usr/share/man \
    infodir=$pkgdir/usr/share/info \
    install || return 1
  install -Dm755 $srcdir/lex.sh $pkgdir/usr/bin/lex || return 1

  install -Dm644 COPYING \
  	$pkgdir/usr/share/licenses/$pkgname/license.txt || return 1
  	
  rm -f $pkgdir/usr/share/info/dir
  gzip -9 $pkgdir/usr/share/info/*
}
