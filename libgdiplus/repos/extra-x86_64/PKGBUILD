# $Id$
# Maintainer: Daniel Isenmann <daniel@archlinux.org>

pkgname=libgdiplus
pkgver=2.10.9
pkgrel=5
pkgdesc="An Open Source Implementation of the GDI+ API"
arch=(i686 x86_64)
license=('MPL' 'LGPL')
url="http://www.mono-project.com"
depends=('libtiff' 'cairo' 'giflib' 'glib2' 'libexif')
source=(http://download.mono-project.com/sources/${pkgname}/${pkgname}-${pkgver}.tar.bz2
        libpng15.patch
        libgdiplus0-giflib5.patch
        libgdiplus-2.10.9-gold.patch
        libgdiplus-2.10.9-freetype25.patch
        libgdiplus-2.10.9-giflib-5.1.0.patch)
md5sums=('b4615c14584b5d73cbb9757c28887654'
         'a2d143676bbaceeb88b4c34069e93986'
         'ad97558c721106eea03c7808b501814b'
         '95fb92750c131ce9287419c3ac5fcb76'
         '6ee484dde02ea89aded1287c0a8d4bad'
         '8c0a975b12a011b21e4216c44be1416e')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -Np0 -i ../libpng15.patch
  patch -Np1 -i ../libgdiplus0-giflib5.patch
  patch -Np1 -i ../libgdiplus-2.10.9-gold.patch
  patch -Np1 -i ../libgdiplus-2.10.9-freetype25.patch
  patch -Np1 -i ../libgdiplus-2.10.9-giflib-5.1.0.patch

  sed -i -e 's/-L\${libjpeg_prefix}\/lib -ljpeg/-ljpeg/' configure
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --with-cairo=system

  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
