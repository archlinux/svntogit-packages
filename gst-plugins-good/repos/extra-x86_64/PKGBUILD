# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgbase=gst-plugins-good
pkgname=(gst-plugins-good gst-plugin-gtk gst-plugin-qmlgl)
pkgver=1.18.2
pkgrel=1
pkgdesc="Multimedia graph framework - good plugins"
url="https://gstreamer.freedesktop.org/"
arch=(x86_64)
license=(LGPL)
depends=(libpulse libsoup gst-plugins-base-libs wavpack aalib taglib libdv libshout libvpx
         gdk-pixbuf2 libcaca libavc1394 libiec61883 libxdamage v4l-utils cairo libgudev speex flac
         libraw1394 lame mpg123 twolame)
makedepends=(python git meson jack2 gtk3 qt5-{tools,declarative,x11extras,wayland} nasm)
checkdepends=(gst-plugins-base xorg-server-xvfb)
options=(!emptydirs)
_commit=cc896a7e9031de420ab915ab94451ffa9c1bf26d  # tags/1.18.2^0
source=("git+https://gitlab.freedesktop.org/gstreamer/gst-plugins-good.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build \
    -D doc=disabled \
    -D rpicamsrc=disabled \
    -D gobject-cast-checks=disabled \
    -D package-name="GStreamer Good Plugins (Arch Linux)" \
    -D package-origin="https://www.archlinux.org/"
  meson compile -C build
}

check() (
  mkdir -p -m 700 "${XDG_RUNTIME_DIR:=$PWD/runtime-dir}"
  export XDG_RUNTIME_DIR

  xvfb-run -s '-screen 0 1920x1080x24 -nolisten local +iglx -noreset' \
    meson test -C build --print-errorlogs
)

package_gst-plugins-good() {
  depends+=(libjack.so)

  DESTDIR="$pkgdir" meson install -C build

  for _x in gtk qmlgl; do
    mkdir -p "$_x/lib/gstreamer-1.0"
    _f="lib/gstreamer-1.0/libgst${_x}.so"
    mv "$pkgdir/usr/$_f" "$_x/$_f"
  done
}

package_gst-plugin-gtk() {
  pkgdesc="${pkgdesc%-*}- gtk plugin"
  depends=(gst-plugins-base-libs gtk3)

  mv gtk "$pkgdir/usr"
}

package_gst-plugin-qmlgl() {
  pkgdesc="${pkgdesc%-*}- qmlgl plugin"
  depends=(gst-plugins-base-libs qt5-{declarative,x11extras,wayland})

  mv qmlgl "$pkgdir/usr"
}
