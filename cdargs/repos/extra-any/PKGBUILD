# $Id$
# Maintainer: Aaron Griffin <aaron@archlinux.org>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>

pkgname=cdargs
pkgver=1.35
pkgrel=3
pkgdesc="A replacement for 'cd' that includes bookmarks/browsing for faster navigation"
arch=('any')
license=('GPL')
url="http://www.skamphausen.de/cgi-bin/ska/CDargs"
depends=('ncurses' 'gcc-libs')
source=("http://www.skamphausen.de/downloads/${pkgname}/${pkgname}-${pkgver}.tar.gz")
install=${pkgname}.install
md5sums=('50be618d67f0b9f2439526193c69c567')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr --mandir=/usr/share/man
  make
  
  cd contrib
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  
  # install helper scripts
  cd contrib
  make DESTDIR=${pkgdir} install
  
  install -d ${pkgdir}/usr/share/cdargs
  install $pkgname-{tcsh.csh,bash.sh} ${pkgdir}/usr/share/cdargs
}
