# Maintainer: kevin <kevin@archlinux.org>
# Contributor: Eric Johnson <eric@coding-zone.com>

pkgname=perl-mime-lite
pkgver=3.030
pkgrel=4
pkgdesc="Perl module that provides lightweight MIME generator"
arch=('any')
url="http://search.cpan.org/dist/MIME-Lite/"
license=('GPL' 'PerlArtistic')
depends=('perl-email-date-format' 'perl-mailtools' 'perl-mime-types' 'perl')
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/R/RJ/RJBS/MIME-Lite-${pkgver}.tar.gz")
md5sums=('5a6d90329e049eee77248d667343acc7')

build() {
  cd MIME-Lite-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd MIME-Lite-${pkgver}
  make test
}

package() {
  cd MIME-Lite-${pkgver}
  make DESTDIR="${pkgdir}" install
}
