# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=dleyna-core
pkgver=0.7.0
pkgrel=2
pkgdesc="Library of utility functions that are used by the higher level dLeyna"
url="https://github.com/phako/dleyna-core"
arch=(x86_64)
license=(LGPL2.1)
depends=(gupnp)
makedepends=(git meson)
_commit=6fb250b19f774a7fb908e85926ec1c3a011e81d7  # tags/v0.7.0^0
source=("git+https://github.com/phako/dleyna-core#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
