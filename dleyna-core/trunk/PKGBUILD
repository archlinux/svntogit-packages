# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=dleyna-core
pkgver=0.5.0
pkgrel=1
pkgdesc="Library of utility functions that are used by the higher level dLeyna"
arch=(i686 x86_64)
url="http://01.org/dleyna"
license=(LGPL2.1)
depends=(gupnp)
source=(https://01.org/sites/default/files/downloads/dleyna/$pkgname-$pkgver.tar.gz
        crash.patch)
sha256sums=('de3027acf5718eae837b5f126bd49b8111189fb075e040d43b480d2b9b114aae'
            '28f8fa43476e4c71b8b1f16b7ab47df045db3c8490a97c19c7354ce56094bffe')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../crash.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
