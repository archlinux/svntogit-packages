# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=sdl_ttf
pkgver=2.0.11
pkgrel=3
pkgdesc="A library that allows you to use TrueType fonts in your SDL applications"
arch=('i686' 'x86_64')
license=('custom')
url="http://www.libsdl.org/projects/SDL_ttf/"
depends=('sdl>=1.2.12' 'freetype2')
source=(http://www.libsdl.org/projects/SDL_ttf/release/SDL_ttf-$pkgver.tar.gz
        bug1433.patch)
md5sums=('61e29bd9da8d245bc2471d1b2ce591aa'
         'f847697e18eec539aaf3ffb96996abbf')

build() {
  cd "$srcdir/SDL_ttf-$pkgver"
  
  # Fix FS#28674
  patch -Ni "$srcdir/bug1433.patch"
 
  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd "$srcdir/SDL_ttf-$pkgver"
  make DESTDIR="$pkgdir" install

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
