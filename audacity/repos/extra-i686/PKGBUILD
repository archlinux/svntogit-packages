# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=audacity
pkgver=2.0.6
pkgrel=1
pkgdesc="A program that lets you manipulate digital audio waveforms"
arch=('i686' 'x86_64')
url="http://audacity.sourceforge.net/"
license=('GPL')
depends=('libmad' 'libid3tag' 'wxgtk2.8' 'lame' 'jack' 'soundtouch'
	 'ffmpeg' 'desktop-file-utils')
makedepends=('cmake' 'python2')
options=('!makeflags')
install=audacity.install
source=(http://downloads.sourceforge.net/project/audacity/audacity/${pkgver}/${pkgname}-minsrc-${pkgver}.tar.xz
        audacity-ffmpeg.patch)
sha1sums=('9fd664a71684e7e07472a48fa716f89c87b1929d'
          '3fa0a06501ae525515f3e46ef9d20aa1a9ea8921')

prepare() {
  cd ${pkgname}-src-${pkgver}
  patch -p1 -i "${srcdir}/audacity-ffmpeg.patch"
}

build() {
  cd ${pkgname}-src-${pkgver}
  WX_CONFIG=/usr/bin/wx-config-2.8 ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-src-${pkgver}
  make DESTDIR="${pkgdir}" install
}
