# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=audacity
pkgver=1.3.8
pkgrel=1
pkgdesc="A program that lets you manipulate digital audio waveforms"
arch=('i686' 'x86_64')
url="http://audacity.sourceforge.net/"
license=('GPL')
depends=('libvorbis' 'libmad' 'libid3tag' 'wxgtk' 'libsamplerate' \
         'lame' 'shared-mime-info' 'desktop-file-utils' 'redland' 'jack-audio-connection-kit')
makedepends=('zip' 'python' 'pkgconfig' 'soundtouch')
options=('!makeflags')
install=audacity.install
source=(http://audacity.googlecode.com/files/${pkgname}-minsrc-${pkgver}.tar.bz2 \
        audacity.xpm audacity16.xpm)
md5sums=('88b3c68663a06f146fe822b91608e8f1' 'c8f7f7851638eedf84d05a1afde797b2'\
         '5e94ccbfb665e8356dc4172a3673b4d4')
sha1sums=('8b8e5dbbf38546c08eb77005f77648d28836dfd4'
          'a2b6626cd1cdcd9facb262116804d9349e1e6144'
          'a208a4ed129c73f97347bab929c7758a345e8e9a')

build() {
  cd "${srcdir}/${pkgname}-src-${pkgver}"

  WX_CONFIG=/usr/bin/wx-config \
  ./configure --prefix=/usr \
     --with-portaudio=v19 --with-libsamplerate=system \
     --without-libresample --with-libmad=system \
     --with-id3tag=system --with-flac=system \
     --with-vorbis=system --with-libexpat=system \
     --with-libsndfile=system --with-soundtouch \
     --enable-unicode --without-taglib || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1

  install -m644 ../audacity.xpm "${pkgdir}/usr/share/${pkgname}/audacity.xpm" || return 1
  install -m644 ../audacity16.xpm "${pkgdir}/usr/share/${pkgname}/audacity16.xpm" || return 1
}
