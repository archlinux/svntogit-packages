# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=audacity
pkgver=2.1.2
pkgrel=3
pkgdesc="A program that lets you manipulate digital audio waveforms"
arch=('i686' 'x86_64')
url="http://audacityteam.org"
license=('GPL')
depends=('libmad' 'libid3tag' 'wxgtk' 'lame' 'lilv' 'soundtouch'
         'ffmpeg' 'vamp-plugin-sdk' 'sbsms' 'portsmf' 'desktop-file-utils')
makedepends=('cmake' 'python2')
options=('!makeflags')
source=(https://github.com/audacity/audacity/archive/Audacity-${pkgver}.zip
        audacity-ffmpeg.patch)
sha1sums=('ced07f7401bef12d7ec0bc033409c9baa956c5c5'
          '5f1733a3802bcec7d9b54cb3ec8d7d81fc38fc61')

prepare() {
  cd audacity-Audacity-${pkgver}
  patch -p1 -i "${srcdir}/audacity-ffmpeg.patch"
}

build() {
  cd audacity-Audacity-${pkgver}
  ./configure --prefix=/usr --with-libsamplerate
  make
}

package() {
  cd audacity-Audacity-${pkgver}
  make DESTDIR="${pkgdir}" install
}
