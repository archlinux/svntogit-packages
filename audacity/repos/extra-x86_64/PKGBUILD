# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=audacity
pkgver=1.3.12
pkgrel=2
pkgdesc="A program that lets you manipulate digital audio waveforms"
arch=('i686' 'x86_64')
url="http://audacity.sourceforge.net/"
license=('GPL')
depends=('libvorbis' 'libmad' 'libid3tag' 'wxgtk' 'libsamplerate' 'lame'
	'shared-mime-info' 'desktop-file-utils' 'redland' 'jack' 'soundtouch')
makedepends=('zip' 'python' 'pkgconfig')
options=('!makeflags')
install=${pkgname}.install
source=(http://audacity.googlecode.com/files/${pkgname}-minsrc-${pkgver}-beta.tar.bz2)
md5sums=('76996fec67181ca82ba191e012518b57')

build() {
  cd "${srcdir}/${pkgname}-src-${pkgver}-beta"

  WX_CONFIG=/usr/bin/wx-config \
  ./configure --prefix=/usr \
     --with-portaudio=v19 --with-libsamplerate=system \
     --without-libresample --with-libmad=system \
     --with-id3tag=system --with-flac=system \
     --with-vorbis=system --with-libexpat=system \
     --with-libsndfile=system --with-soundtouch \
     --enable-unicode --without-taglib || return 1
  make || return 1
}

package() {
  cd "${srcdir}/${pkgname}-src-${pkgver}-beta"
  make DESTDIR="${pkgdir}" install || return 1
}
