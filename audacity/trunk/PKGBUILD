# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=audacity
pkgver=2.0.1
pkgrel=2
pkgdesc="A program that lets you manipulate digital audio waveforms"
arch=('i686' 'x86_64')
url="http://audacity.sourceforge.net/"
license=('GPL')
depends=('libmad' 'libid3tag' 'wxgtk' 'lame' 'hicolor-icon-theme'
	 'desktop-file-utils' 'jack' 'soundtouch' 'ffmpeg-compat')
options=('!makeflags')
install=audacity.install
source=(http://audacity.googlecode.com/files/${pkgname}-minsrc-${pkgver}.tar.bz2)
sha1sums=('366a10f3bd71c4d84c29437fff1e27a8560f691b')

build() {
  cd "${srcdir}/${pkgname}-src-${pkgver}"
  WX_CONFIG=/usr/bin/wx-config PKG_CONFIG_PATH+="/usr/lib/ffmpeg-compat/pkgconfig" \
    ./configure --prefix=/usr \
    --with-portaudio --with-libsamplerate \
    --without-libresample --with-libmad \
    --with-ffmpeg --with-id3tag --with-libflac \
    --with-vorbis --with-libexpat \
    --with-libsndfile --with-soundtouch \
    --enable-unicode --without-taglib
  make
}

package() {
  cd "${srcdir}/${pkgname}-src-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
