# $Id$
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: dorphell <dorphell@archlinux.org>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=audacity
pkgver=1.3.12
pkgrel=3
pkgdesc="A program that lets you manipulate digital audio waveforms"
arch=('i686' 'x86_64')
url="http://audacity.sourceforge.net/"
license=('GPL')
depends=('libvorbis' 'libmad' 'libid3tag' 'wxgtk' 'libsamplerate' 'lame'
	'shared-mime-info' 'desktop-file-utils' 'redland' 'jack' 'soundtouch')
makedepends=('zip' 'python' 'pkgconfig')
options=('!makeflags')
install=${pkgname}.install
source=(http://audacity.googlecode.com/files/${pkgname}-minsrc-${pkgver}-beta.tar.bz2
	audacity-ffmpeg.patch)
md5sums=('76996fec67181ca82ba191e012518b57'
	 '52cd58c39078d78a96e972df4c3c41f6')

build() {
	cd ${srcdir}/${pkgname}-src-${pkgver}-beta

	patch -p0 < ${srcdir}/audacity-ffmpeg.patch || return 1
	
	WX_CONFIG=/usr/bin/wx-config \
	./configure --prefix=/usr \
	     --with-portaudio=v19 --with-libsamplerate=system \
	     --without-libresample --with-libmad=system \
	     --with-id3tag=system --with-flac=system \
	     --with-vorbis=system --with-libexpat=system \
	     --with-libsndfile=system --with-soundtouch \
	     --enable-unicode --without-taglib || return 1
	make || return 1
}

package() {
	cd ${srcdir}/${pkgname}-src-${pkgver}-beta

	make DESTDIR=${pkgdir} install || return 1
}
