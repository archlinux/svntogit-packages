# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=knewstuff
pkgver=
pkgrel=1
pkgdesc='KNewStuff'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/knewstuff'
license=('LGPL')
depends=('attica-qt5')
makedepends=('extra-cmake-modules' 'git')
conflicts=('knewstuff')
provides=('knewstuff')
source=('git://anongit.kde.org/knewstuff.git')
md5sums=('SKIP')

pkgver() {
  cd knewstuff
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../knewstuff \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
