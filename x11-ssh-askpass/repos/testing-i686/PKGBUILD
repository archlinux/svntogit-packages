# $Id$

# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Mike Sampson <mike at sambodata dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Charles Mauch <cmauch@gmail.com>

pkgname=x11-ssh-askpass
pkgver=1.2.4.1
pkgrel=3
pkgdesc='Lightweight passphrase dialog for SSH'
url='http://www.jmknoble.net/software/x11-ssh-askpass/'
license=('custom')
arch=('i686' 'x86_64')
depends=('libxt')
makedepends=('imake')
#source=("${url}${pkgname}-${pkgver}.tar.gz" # httpd only sends half the bits
source=("http://pkgs.fedoraproject.org/repo/pkgs/openssh/x11-ssh-askpass-1.2.4.1.tar.gz/8f2e41f3f7eaa8543a2440454637f3c3/x11-ssh-askpass-1.2.4.1.tar.gz"
        'profile.d')
sha1sums=('78c992951685d4dbffb77536f37b83ae2a6eafc7'
          '28be56290d07e2625bc8e81ae8c7ea741d329c51')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	./configure \
		--mandir=/usr/share/man \
		--libexecdir=/usr/lib/ssh \
		--with-app-defaults-dir=/usr/share/X11/app-defaults \

	xmkmf
	make includes
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make DESTDIR="${pkgdir}" install install.man
	install -Dm755 ../profile.d "${pkgdir}/etc/profile.d/${pkgname}.sh"

	install -d "${pkgdir}/usr/share/licenses/${pkgname}"
	sed '109,174p;d' README > "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
