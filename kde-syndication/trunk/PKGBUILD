# Maintainer: Antonio Rojas <arojas@archlinux.org>

_pkgname=syndication
pkgname=kde-$_pkgname
pkgver=16.04.2
pkgrel=1
pkgdesc="RSS/Atom parser library"
arch=(i686 x86_64)
url="https://community.kde.org/KDE_PIM"
license=(LGPL)
depends=(kio)
makedepends=(extra-cmake-modules python)
source=("http://download.kde.org/stable/applications/$pkgver/src/$_pkgname-$pkgver.tar.xz")
md5sums=('a12ae8b9e267848644afa2451760a08d')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$_pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
