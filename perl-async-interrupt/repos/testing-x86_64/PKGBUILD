# Packager: Justin Davis (juster) <jrcd83@gmail.com>
# $Id$

pkgname=perl-async-interrupt
_ver=1.2
pkgver=1.20
pkgrel=2
pkgdesc='allow C/XS libraries to interrupt perl asynchronously'
arch=(i686 x86_64)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=(perl-common-sense)
url=https://metacpan.org/release/Async-Interrupt
source=("http://search.cpan.org/CPAN/authors/id/M/ML/MLEHMANN/Async-Interrupt-$_ver.tar.gz")
md5sums=('3ec1355c266dfd77245d1c3a1de6fa8d')
sha512sums=('41091c445247597567c9470fd926318e42d08b1f71e4bee2ed9bad327f66ef679b9bfd89b3e0c66d820a3a82e423eac5a56dca97675a11566e917257f77a2a90')

build()
(
  cd "$srcdir/Async-Interrupt-$_ver"
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  /usr/bin/perl Makefile.PL
  make
)

check()
(
  cd "$srcdir/Async-Interrupt-$_ver"
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
)

package()
(
  cd "$srcdir/Async-Interrupt-$_ver"
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
)

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
