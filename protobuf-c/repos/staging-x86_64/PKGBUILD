# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Aurélien Wailly <aurelien.wailly@gmail.com>

pkgname=protobuf-c
pkgver=1.4.0
pkgrel=3
pkgdesc='Protocol Buffers implementation in C'
arch=('x86_64')
url='https://github.com/protobuf-c/protobuf-c'
license=('BSD')
depends=('protobuf')
provides=('libprotobuf-c.so')
options=('!lto') # https://github.com/protobuf-c/protobuf-c/issues/488
source=("$url/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('26d98ee9bf18a6eba0d3f855ddec31dbe857667d269bc0b6017335572f85bbcb')

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr --disable-static
  make
}

check() {
  make -C "$pkgname-$pkgver" check
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
