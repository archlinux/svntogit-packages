# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Thomas Arnhold <thomas at arnhold dot org>

pkgbase=libmwaw
pkgname=('libmwaw' 'libmwaw-docs')
pkgver=0.3.21
pkgrel=3
pkgdesc="Import library for some old mac text documents."
arch=('x86_64')
url="https://sourceforge.net/projects/libmwaw/"
license=('LGPL2.1' 'MPL')
makedepends=('librevenge' 'doxygen')
options=('debug')
source=(https://sourceforge.net/projects/${pkgname}/files/${pkgname}/${pkgname}-${pkgver}/${pkgname}-${pkgver}.tar.xz)
sha1sums=('198d281b63ddfe0a6d3c9678ffe62b458fbbdabf')
sha256sums=('e8750123a78d61b943cef78b7736c8a7f20bb0a649aa112402124fba794fc21c')

build() {
  cd "${pkgname}"-${pkgver}
  ./configure --prefix=/usr
  make
}

check() {
  cd "${pkgname}"-${pkgver}
  make check
}

package_libmwaw() {

  depends=('librevenge')

  cd "${pkgname}"-${pkgver}
  make DESTDIR="$pkgdir" install

  # Split -docs
  mkdir -p ../docs/usr/share
  mv -t ../docs/usr/share "$pkgdir"/usr/share/doc
  rmdir "$pkgdir"/usr/share
}

package_libmwaw-docs() {

  pkgdesc+=" (documentation)"

  mv -t "$pkgdir" docs/*
}
