# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=xorg-xclock
pkgver=1.1.1
pkgrel=1
pkgdesc="X clock"
arch=(x86_64)
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libx11' 'libxmu' 'libxaw' 'libxrender' 'libxft' 'libxkbfile')
makedepends=('xorg-util-macros')
source=(https://xorg.freedesktop.org/archive/individual/app/xclock-${pkgver}.tar.xz{,.sig})
sha512sums=('059ea986bc7537c2796e8855676e8357bd8a7852fbac0839b3f8a9e3ffa088de41bc4e4961973353f0a7b7293366b81bc764d74f3dc4e90e361da06185d976e7'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # "Alan Coopersmith <alan.coopersmith@oracle.com>"

build() {
  cd "xclock-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "xclock-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
