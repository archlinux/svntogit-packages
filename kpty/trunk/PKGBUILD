# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kpty
pkgver=r5.022a5fb
pkgrel=1
pkgdesc='KPty'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kpty'
license=('LGPL')
depends=('kcoreaddons' 'ki18n')
makedepends=('extra-cmake-modules' 'git')
conflicts=('kpty')
provides=('kpty')
source=('git://anongit.kde.org/kpty.git')
md5sums=('SKIP')

pkgver() {
  cd kpty
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kpty \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
