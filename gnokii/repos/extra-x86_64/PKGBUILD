# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=gnokii
pkgver=0.6.27
pkgrel=6
pkgdesc="Tools and user space driver for use with mobile phones"
arch=('i686' 'x86_64')
url="http://www.gnokii.org/"
license=('GPL')
depends=('libusb' 'libxpm' 'bluez' 'libical')
makedepends=('gtk2' 'libmysqlclient>=5.1' 'postgresql-libs>=8.3.3' 'perlxml')
optdepends=('gtk2: xgnokii GUI'
            'libmysqlclient: smsd mysql backend'
	    'postgresql-libs: smsd postgresql backend')
backup=('etc/gnokiirc')
options=('!libtool' '!makeflags')
source=(http://www.gnokii.org/download/$pkgname/$pkgname-${pkgver}.tar.bz2
	'gnokii.patch' 'smsd-mysql-5.1.35.patch')
md5sums=('fd0ae7996a2e2a2c29e9f3c625df8f3d'
         '4d764727686ee34bc73489c8b1321c82'
         'fb7da45d778c1f45031f79ce531a11f9')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # FS#13249
  sed -i 's|cellphone|phone|' xgnokii/xgnokii.desktop.in || return 1

  patch -Np1 -i "${srcdir}/gnokii.patch" || return 1
  patch -Np1 -i ${srcdir}/smsd-mysql-5.1.35.patch

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --enable-security \
    --disable-unix98test || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1

  install -Dm644 Docs/sample/gnokiirc "${pkgdir}/etc/gnokiirc" || return 1
}
