# $Id$
# Maintainer:
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=gnokii
pkgver=0.6.31
pkgrel=3
pkgdesc="Tools and user space driver for use with mobile phones"
arch=('i686' 'x86_64')
url="http://www.gnokii.org/"
license=('GPL')
depends=('libusb-compat' 'libxpm' 'bluez' 'libical')
makedepends=('gtk2' 'libmariadbclient' 'postgresql-libs' 'intltool' 'mariadb')
optdepends=('gtk2: xgnokii GUI'
            'libmariadbclient: smsd mysql backend'
            'postgresql-libs: smsd postgresql backend')
backup=('etc/gnokiirc')
options=('!libtool' '!makeflags')
source=("http://www.gnokii.org/download/$pkgname/$pkgname-${pkgver}.tar.bz2"
        'gnokii.patch')
md5sums=('d9627f4a1152d3ea7806df4532850d5f'
         'fd1c448114c2e0de8e05981030f0e69f')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # FS#13249
  sed -i 's|cellphone|phone|' xgnokii/xgnokii.desktop.in

  patch -Np1 -i "${srcdir}/gnokii.patch"

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --enable-security \
    --disable-unix98test
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -Dm644 Docs/sample/gnokiirc "${pkgdir}/etc/gnokiirc"
}
