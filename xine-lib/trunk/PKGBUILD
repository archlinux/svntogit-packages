# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=xine-lib
pkgver=1.1.18
pkgrel=1
pkgdesc="A multimedia playback engine"
arch=('i686' 'x86_64')
url="http://www.xine-project.org"
license=('LGPL' 'GPL')
depends=('libgl' 'libxvmc' 'esound' 'flac' 'libvorbis' 'sdl' 'libmng' 'libtheora'
         'libxcb' 'wavpack' 'ffmpeg' 'heimdal' 'faad2')
makedepends=('pkgconfig' 'libtool' 'imagemagick' 'smbclient' 'mesa' 'alsa-lib' 
             'vcdimager' 'jack-audio-connection-kit')
optdepends=('imagemagick: for using the imagemagick plugin' 'smbclient: for using the smb plugin' 'jack-audio-connection-kit: for using the jack plugin')
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/xine/${pkgname}-${pkgver}.tar.bz2
        http://downloads.sourceforge.net/sourceforge/xine/compat.c
        xine-lib-1.1.1-configure-no-mcpu-march.patch)
md5sums=('2313c279bc99303cd12d4ee0ac826e20' 'c4537f3b0445e15a031e1f69fc57bc28'\
         '9776df4eb54d2f1f68d8268adbc3b5c2')
sha1sums=('4b80610939930569715c51a315f2d919a7e42a13' 'fe0646f11a6930631a64fe1b7c517662e79b58d2'\
         '121a8358d7919b2e51067412373f52848290338a')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  cp "${srcdir}/compat.c" src/dxr3/compat.c || return 1
  patch -p0 < ../xine-lib-1.1.1-configure-no-mcpu-march.patch || return 1

  libtoolize --force --copy || return 1
  aclocal -I m4 || return 1
  autoconf || return 1
  automake --add-missing || return 1

  ./configure --prefix=/usr --with-w32-path=/usr/lib/codecs \
    --with-xv-path=/usr/lib --with-xxmc-path=/usr/lib --with-xvmc-path=/usr/lib \
    --with-libflac --with-wavpack --with-xcb \
    --without-arts --with-jack --without-speex \
    --disable-gnomevfs --without-pulseaudio --disable-aalib \
    --disable-modplug --with-external-ffmpeg || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
}
