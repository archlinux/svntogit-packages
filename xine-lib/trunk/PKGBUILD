# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=xine-lib
pkgver=1.1.15
pkgrel=2
pkgdesc="A free video player for Unix"
arch=('i686' 'x86_64')
url="http://xinehq.de/"
license=('LGPL' 'GPL')
depends=('libgl' 'libxvmc' 'esd' 'flac>=1.1.4' 'libvorbis' 'sdl' 'libmng' 'libtheora'
         'libxcb' 'wavpack' 'ffmpeg>=20080715')
makedepends=('pkgconfig' 'libtool' 'automake' 'autoconf' 'imagemagick' 
             'smbclient' 'mesa' 'alsa-lib' 'vcdimager')
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/xine/${pkgname}-${pkgver}.tar.bz2
        xine-lib-1.1.1-configure-no-mcpu-march.patch xine-header.patch)
md5sums=('42a2b4893b7f892eb334de2fc36d49c8' '9776df4eb54d2f1f68d8268adbc3b5c2'
         'b139ee72700f8d118f9051e9140473f5')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -Np0 -i ../xine-lib-1.1.1-configure-no-mcpu-march.patch || return 1
  patch -p0 -i ../xine-header.patch || return 1
  libtoolize --force --copy || return 1
  aclocal -I m4 || return 1
  autoconf || return 1
  automake --add-missing || return 1

  ./configure --prefix=/usr --with-w32-path=/usr/lib/codecs \
    --with-xv-path=/usr/lib --with-libflac --with-wavpack \
    --without-arts --without-jack --without-speex \
    --disable-gnomevfs --without-pulseaudio --disable-aalib \
    --disable-modplug --with-external-ffmpeg || return 1
  make || return 1
  make DESTDIR=${pkgdir} install || return 1
}
