# $Id$
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>

pkgname=clutter-gst2
pkgver=2.0.14
pkgrel=1
pkgdesc="GStreamer bindings for clutter"
arch=(i686 x86_64)
url="http://www.clutter-project.org/"
license=(LGPL)
depends=(clutter gst-plugins-base-libs libxdamage)
makedepends=(gobject-introspection)
conflicts=('clutter-gst<=2.0.14')
source=(http://download.gnome.org/sources/clutter-gst/${pkgver:0:3}/clutter-gst-$pkgver.tar.xz)
sha256sums=('5d7d8c1a31748e9039b7ba9dae9f412ec065715008adffdfde332dd34e7d0535')

build() {
  cd clutter-gst-$pkgver
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

check() {
  cd clutter-gst-$pkgver
  make check
}

package() {
  cd clutter-gst-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
