# $Id$
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>

pkgname=libpcap
pkgver=1.6.1
pkgrel=1
pkgdesc="A system-independent interface for user-level packet capture"
arch=('i686' 'x86_64')
url="http://www.tcpdump.org/"
license=('BSD')
depends=('glibc' 'libnl' 'sh' 'libusbx')
makedepends=('flex' 'bluez-libs')
source=(http://www.tcpdump.org/release/libpcap-${pkgver}.tar.gz{,.sig}
        mgmt.h)
sha256sums=('116cbb3ac9e96d5dd7b39638a2f894a67fa3dcf06d794e6dae2b9a942ad13476'
            'SKIP'
            '7c85da5330ce7ecb6934795c02c652b9e344461302cf74804a4692c3e2e1e7e2')

prepare() {
  mkdir -p ${srcdir}/include/bluetooth
  cp ${srcdir}/mgmt.h ${srcdir}/include/bluetooth/
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  CFLAGS="$CFLAGS -I${srcdir}/include" \
  ./configure --prefix=/usr \
    --enable-ipv6 \
    --enable-bluetooth \
    --with-libnl
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  install -d -m755 ${pkgdir}/usr/bin
  make DESTDIR=${pkgdir} install
  
  # backwards compatibility, programs often look for net/bpf.h
  mkdir -p ${pkgdir}/usr/include/net
  cd ${pkgdir}/usr/include/net
  ln -s ../pcap-bpf.h bpf.h

  # install the license
  install -D -m644 ${srcdir}/$pkgname-$pkgver/LICENSE ${pkgdir}/usr/share/licenses/$pkgname/LICENSE
}
