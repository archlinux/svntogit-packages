# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Adam Hani Schakaki <krzd@krzd.net>

pkgname=caribou
pkgver=0.4.11
pkgrel=1
pkgdesc="A text entry and UI navigation application being developed as an alternative to the Gnome On-screen Keyboard."
arch=('i686' 'x86_64')
url="http://live.gnome.org/Caribou"
license=(LGPL)
depends=(at-spi2-atk python2-atspi python2-gobject gtk3 libxklavier libgee clutter dconf)
makedepends=(intltool docbook-xsl gtk2)
install=caribou.install
options=(!libtool !emptydirs)
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/0.4/$pkgname-$pkgver.tar.xz)
sha256sums=('e4e81387a1a6875652b8ec9dede8573844fac274c8de9ad0416cf586a318f4f9')

build() {
  cd $pkgname-$pkgver
  sed -i s'|#!/usr/bin/python|#!/usr/bin/python2|'g tools/{fix_gir,make_schema}.py
  export PYTHON=/usr/bin/python2
  ./configure --prefix=/usr --sysconfdir=/etc \
    --libexecdir=/usr/lib/caribou \
    --disable-gtk2-module \
    --disable-static \
    --disable-schemas-compile

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
