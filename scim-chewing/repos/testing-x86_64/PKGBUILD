# $Id$
# Contributor: damir <damir@archlinux.org>
# Contributor: whisky <archlinux.cle(at)gmail.com>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=scim-chewing
pkgver=0.3.4
pkgrel=2
pkgdesc='Traditional Chinese input method module for SCIM'
arch=('i686' 'x86_64')
url='http://chewing.csie.net/'
license=('GPL')
depends=('scim' 'libchewing')
makedepends=('intltool')
options=('!libtool')
source=("http://chewing.csie.net/download/scim/${pkgname}-${pkgver}.tar.bz2")
sha1sums=('f6c8d16f19f17449ee169f1ca787350353e93b05')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr --disable-static
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
