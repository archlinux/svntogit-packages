# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: whisky <archlinux.cle(at)gmail.com>
# Contributor: damir <damir@archlinux.org>

pkgname=scim-chewing
pkgver=0.3.5
pkgrel=3
pkgdesc='Traditional Chinese input method module for SCIM'
url='http://chewing.csie.net/'
license=('GPL')
arch=('i686' 'x86_64')
makedepends=('intltool')
depends=('scim' 'libchewing')
source=("http://chewing.googlecode.com/files/${pkgname}-${pkgver}.tar.bz2")
sha1sums=('f26ced990f0c9e5d43e271cd6e77583de81a008d')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	sed '/CHEWING_VERSION=/s/0.3.4/0.4.0/' -i configure
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
