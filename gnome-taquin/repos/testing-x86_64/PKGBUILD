# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-taquin
pkgver=3.26.1
pkgrel=1
pkgdesc="Move tiles so that they reach their places"
url="https://wiki.gnome.org/Apps/Taquin"
arch=(x86_64 i686)
license=(GPL3)
depends=(gtk3 librsvg libcanberra)
makedepends=(intltool vala itstool yelp-tools appstream-glib git)
groups=(gnome-extra)
_commit=00bbeb6abc8ee8b3fdfd1a8e88fd344f5f401c8d  # tags/3.26.1^0
source=("git+https://git.gnome.org/browse/gnome-taquin#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
