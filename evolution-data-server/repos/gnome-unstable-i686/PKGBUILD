# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=evolution-data-server
pkgver=2.30.0
pkgrel=1
pkgdesc="Evolution Data Server provides a central location for addressbook and calendar in the GNOME Desktop"
arch=('i686' 'x86_64')
depends=('libsoup>=2.30.0' 'nss>=3.12.4' 'libgnome-keyring>=2.29.4' 'heimdal>=1.3.1' 'libgweather>=2.29.92' 'libical>=0.44' 'db>=4.8.26')
makedepends=('intltool' 'pkgconfig' 'gtk-doc' 'gperf')
options=('!libtool')
url="http://www.gnome.org"
license=('GPL')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.30/${pkgname}-${pkgver}.tar.bz2
        patch-camel_camel-sasl-gssapi.c)
sha256sums=('89330c0ff21904d95de4d78ed84621dbbfa776091d705737ae8d829c7706cd04'
            '7a16a8e040a9623a56d05754b5a23d969d12e1c2098eb7baec48e935a43832b4')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np0 -i "${srcdir}/patch-camel_camel-sasl-gssapi.c" || return 1
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --with-openldap=yes \
      --libexecdir=/usr/lib/evolution-data-server-1.2 \
      --with-krb5=/usr --enable-gnome-keyring \
      --with-libdb=/usr || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
}
