# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=evolution-data-server
pkgver=3.44.3
pkgrel=1
pkgdesc="Centralized access to appointments and contacts"
url="https://wiki.gnome.org/Apps/Evolution"
arch=(x86_64)
depends=(gnome-online-accounts nss krb5 libgweather-4 libical db libgdata libphonenumber
         libcanberra)
makedepends=(intltool gperf gobject-introspection vala gtk-doc boost git cmake ninja)
provides=(libcamel-1.2.so libebackend-1.2.so libebook-1.2.so libebook-contacts-1.2.so
          libecal-2.0.so libedata-book-1.2.so libedata-cal-2.0.so libedataserver-1.2.so
          libedataserverui-1.2.so)
license=(GPL)
options=(debug)
_commit=68265e2723489a1854a393800dbfbc563a9f9054  # tags/3.44.3^0
source=("git+https://gitlab.gnome.org/GNOME/evolution-data-server.git#commit=$_commit"
        fix-tests.diff)
sha256sums=('SKIP'
            '435ee829d1f821c5adc3163024057d9fdea6184d35eae87c4bd502b661f10148')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^EVOLUTION_DATA_SERVER_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd $pkgname

  # Disable tests broken by libphonenumber update. Same reason
  # as the already-disabled tests in the same files.
  git apply -3 ../fix-tests.diff
}

build() {
  cmake -S $pkgname -B build -G Ninja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIBEXEC_INSTALL_DIR=/usr/lib \
    -DSYSCONF_INSTALL_DIR=/etc \
    -DENABLE_INTROSPECTION=ON \
    -DENABLE_VALA_BINDINGS=ON \
    -DENABLE_GTK_DOC=ON \
    -DWITH_PHONENUMBER=ON \
    -DWITH_GWEATHER4=ON
  cmake --build build
}

check() {
  cd build
  ctest --output-on-failure --stop-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
