# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=evolution-data-server
pkgver=3.44.0
pkgrel=2
pkgdesc="Centralized access to appointments and contacts"
url="https://wiki.gnome.org/Apps/Evolution"
arch=(x86_64)
depends=(gnome-online-accounts nss krb5 libgweather-4 libical db libgdata libphonenumber
         libcanberra)
makedepends=(intltool gperf gobject-introspection vala gtk-doc boost git cmake ninja)
provides=(libcamel-1.2.so libebackend-1.2.so libebook-1.2.so libebook-contacts-1.2.so
          libecal-2.0.so libedata-book-1.2.so libedata-cal-2.0.so libedataserver-1.2.so
          libedataserverui-1.2.so)
license=(GPL)
options=(debug)
_commit=1c7202b0a17d97426a5dc1391ccf8da37b5eb0ec  # tags/3.44.0^0
source=("git+https://gitlab.gnome.org/GNOME/evolution-data-server.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^EVOLUTION_DATA_SERVER_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  cmake -S $pkgname -B build -G Ninja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIBEXEC_INSTALL_DIR=/usr/lib \
    -DSYSCONF_INSTALL_DIR=/etc \
    -DENABLE_INTROSPECTION=ON \
    -DENABLE_VALA_BINDINGS=ON \
    -DENABLE_GTK_DOC=ON \
    -DWITH_PHONENUMBER=ON \
    -DWITH_GWEATHER4=ON
  cmake --build build
}

check() {
  cd build
  ctest --output-on-failure --stop-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
