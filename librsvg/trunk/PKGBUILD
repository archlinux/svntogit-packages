# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=librsvg
pkgver=2.26.0
pkgrel=2
pkgdesc="SAX-based renderer for SVG files into a GdkPixbuf"
arch=(i686 x86_64)
license=('LGPL')
depends=('gtk2>=2.18.6' 'libcroco>=0.6.1' 'libgsf>=1.14.11')
makedepends=('intltool' 'pkgconfig' 'gtk-doc')
optdepends=('python: rsvg commandline utility')
options=('!libtool')
url="http://librsvg.sourceforge.net/"
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.26/${pkgname}-${pkgver}.tar.bz2)
install=librsvg.install
md5sums=('65dbd726a514fe8b797d26254b8efc1e')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --libexecdir=/usr/lib/${pkgname} \
              --with-croco --disable-static \
	      --disable-mozilla-plugin --with-svgz || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
  rm -rf "${pkgdir}/usr/lib/mozilla"
}
