# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=librsvg
pkgver=2.40.9
pkgrel=2
epoch=1
pkgdesc="A SVG viewing library"
arch=(i686 x86_64)
license=('LGPL')
depends=('gdk-pixbuf2' 'pango' 'libcroco')
makedepends=('intltool' 'gobject-introspection' 'vala' 'python2')
options=('!emptydirs')
url="https://live.gnome.org/LibRsvg"
install=librsvg.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('13964c5d35357552b47d365c34215eee0a63bf0e6059b689f048648c6bf5f43a')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-static --enable-vala
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
