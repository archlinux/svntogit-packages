# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=cairo-perl
pkgver=1.108
pkgrel=1
pkgdesc="Perl wrappers for cairo"
arch=(x86_64)
license=('LGPL')
url="http://gtk2-perl.sourceforge.net/"
options=('!emptydirs')
depends=('cairo' 'perl')
makedepends=('perl-extutils-depends' 'perl-extutils-pkgconfig')
source=(https://downloads.sourceforge.net/sourceforge/gtk2-perl/Cairo-${pkgver}.tar.gz)
sha512sums=('e5714906e5f8058d55db9e8c784d87b0c3293bc19524f036af9c2c8b7bd136689dcff2e6d8bae4c38bda807a13ea64331eccd20e17592def7fd4a18705389a20')

build() {
  cd Cairo-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Cairo-$pkgver
  make test
}

package() {
  cd Cairo-$pkgver
  make DESTDIR="${pkgdir}" install
}
