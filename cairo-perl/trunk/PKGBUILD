# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=cairo-perl
pkgver=1.105
pkgrel=2
pkgdesc="Perl wrappers for cairo"
arch=(i686 x86_64)
license=('LGPL')
url="http://gtk2-perl.sourceforge.net/"
options=('!emptydirs')
depends=('cairo' 'perl')
makedepends=('perl-extutils-depends' 'perl-extutils-pkgconfig')
source=(http://downloads.sourceforge.net/sourceforge/gtk2-perl/Cairo-${pkgver}.tar.gz)
md5sums=('a9ace46142f5bab645d3d1b99d05d9d5')

build() {
  cd Cairo-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Cairo-$pkgver
  make test
}

package() {
  cd Cairo-$pkgver
  make DESTDIR="${pkgdir}" install
# template start; name=perl-binary-module-dependency; version=1;
if [[ $(find "$pkgdir/usr/lib/perl5/" -name "*.so") ]]; then
	_perlver_min=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]);')
	_perlver_max=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]+1);')
	depends+=("perl>=$_perlver_min" "perl<$_perlver_max")
fi
# template end;
}
