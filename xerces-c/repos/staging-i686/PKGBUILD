# $Id$
# Maintainer:
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Geoffroy Carrier <geoffroy@archlinux.org>
# Contributor: Roman Kyrylych <Roman.Kyrylych@gmail.com>

pkgname=xerces-c
pkgver=3.1.2
pkgrel=2
pkgdesc="A validating XML parser written in a portable subset of C++"
arch=('i686' 'x86_64')
url="http://xerces.apache.org/xerces-c/"
license=('APACHE')
depends=('gcc-libs' 'curl')
source=("http://apache.osuosl.org/xerces/c/3/sources/${pkgname}-${pkgver}.tar.gz")
md5sums=('9eb1048939e88d6a7232c67569b23985')

build() {
  cd ${pkgname}-${pkgver}

  [[ "${CARCH}" = "i686" ]] && SSE2="--disable-sse2"

  ./configure --prefix=/usr \
    --sysconfdir=/etc ${SSE2}
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}/" install
}
