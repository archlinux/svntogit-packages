# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-killbots
pkgver=4.13.2
pkgrel=1
pkgdesc="A simple game of evading killer robots"
url="http://kde.org/applications/games/killbots/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'libkdegames')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/killbots-${pkgver}.tar.xz")
sha1sums=('7f6674b3c3a637e9c8e289b0c41ca3b90d931e1c')

build() {
  mkdir build
  cd build
  cmake ../killbots-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
