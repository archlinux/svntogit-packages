# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>

# This is a transitional package which will hopefully go away in a few months;
# we're waiting for the following software to be ported to the vte-2.91 API:
#
# roxterm  http://sourceforge.net/p/roxterm/bugs/107/
# sakura   https://bugs.launchpad.net/sakura/+bug/1337962
# tilda    https://github.com/lanoxx/tilda/issues/94

pkgname=vte290
pkgver=0.36.3
pkgrel=2
pkgdesc="Virtual Terminal Emulator widget for use with GTK3 (vte-2.90 API)"
arch=('i686' 'x86_64')
url="http://www.gnome.org"
license=('LGPL')
depends=('gtk3' 'vte-common')
makedepends=('intltool' 'gobject-introspection' 'python2')
options=('!emptydirs')
source=(http://download.gnome.org/sources/vte/${pkgver::4}/vte-$pkgver.tar.xz)
sha256sums=('54e5b07be3c0f7b158302f54ee79d4de1cb002f4259b6642b79b1e0e314a959c')

build() {
  cd "$srcdir/vte-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/vte \
    --localstatedir=/var \
    --disable-static \
    --enable-introspection
  make
}

package(){
  cd "$srcdir/vte-$pkgver"

  make DESTDIR="$pkgdir" install

  # Files below are provided by vte-common
  rm "$pkgdir/usr/lib/vte/gnome-pty-helper"
  rm "$pkgdir/etc/profile.d/vte.sh"
}

# vim:set ts=2 sw=2 et:
