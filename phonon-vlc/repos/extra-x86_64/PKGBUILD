# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgbase=phonon-vlc
pkgname=(phonon-qt5-vlc)
pkgdesc="Phonon VLC backend for Qt5"
pkgver=0.10.3
pkgrel=1
arch=(x86_64)
url='http://phonon.kde.org/'
license=(LGPL)
depends=(vlc phonon-qt5)
provides=(phonon-qt5-backend)
makedepends=(extra-cmake-modules qt5-tools)
source=("http://download.kde.org/stable/phonon/phonon-backend-vlc/$pkgver/phonon-backend-vlc-$pkgver.tar.xz"{,.sig})
sha256sums=('fd503932902633c26333f1f78d243bb201c7855a35f8cf935204dfcb550c39d2'
            'SKIP')
validpgpkeys=(CB9387521E1EE0127DA804843FDBB55084CC5D84) # Harald Sitter <sitter@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../phonon-vlc-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DPHONON_BUILD_PHONON4QT5=ON
  make
}

package_phonon-qt5-vlc() {
  cd build
  make DESTDIR="$pkgdir" install
}
