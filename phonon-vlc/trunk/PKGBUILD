# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgbase=phonon-vlc
pkgname=(phonon-qt5-vlc)
pkgdesc="Phonon VLC backend for Qt5"
pkgver=0.11.2
pkgrel=1
arch=(x86_64)
url='https://community.kde.org/Phonon'
license=(LGPL)
depends=(vlc phonon-qt5)
provides=(phonon-qt5-backend)
makedepends=(extra-cmake-modules qt5-tools)
source=("https://download.kde.org/stable/phonon/phonon-backend-vlc/$pkgver/phonon-backend-vlc-$pkgver.tar.xz"{,.sig})
sha256sums=('c6c33bfc68d137f0e57a029e4b798cbb90f54af6f731af49677857791ccd53a5'
            'SKIP')
validpgpkeys=(CB9387521E1EE0127DA804843FDBB55084CC5D84) # Harald Sitter <sitter@kde.org>

build() {
  cmake -B build -S phonon-backend-vlc-$pkgver
  cmake --build build
}

package_phonon-qt5-vlc() {
  DESTDIR="$pkgdir" cmake --install build
}
