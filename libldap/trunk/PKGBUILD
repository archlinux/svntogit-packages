# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=libldap
pkgver=2.4.21
pkgrel=2
pkgdesc="Lightweight Directory Access Protocol (LDAP) client libraries"
arch=('i686' 'x86_64')
license=('custom')
url="http://www.openldap.org/"
backup=(etc/openldap/ldap.conf)
depends=('libsasl' 'openssl')
makedepends=('tcp_wrappers')
options=('!libtool')
source=(ftp://ftp.openldap.org/pub/OpenLDAP/openldap-release/openldap-${pkgver}.tgz
        ntlm.patch)
md5sums=('e7128c57b2bacd940e8906057c94ff26'
         'dfa9eb6f2fd9d0a3dab0e1860923489e')

build() {
  cd ${srcdir}/openldap-${pkgver}
  patch -Np1 -i ${srcdir}/ntlm.patch || return 1

  ./configure --prefix=/usr \
              --libexecdir=/usr/sbin \
              --sysconfdir=/etc \
	      --mandir=/usr/share/man \
              --localstatedir=/var/lib/openldap \
              --enable-crypt --enable-dynamic \
              --with-threads --enable-wrappers \
	      --enable-spasswd --with-cyrus-sasl \
	      --disable-bdb --disable-hdb
  cd include
  make || return 1
  make DESTDIR=${pkgdir} install || return 1

  cd ../libraries
  make depend || return 1
  make || return 1

  make DESTDIR=${pkgdir} install || return 1

  cd ../doc/man/man3
  make || return 1
  make DESTDIR=${pkgdir} install || return 1

  cd ../man5
  make || return 1
  install -Dm644 ldap.conf.5.tmp \
    ${pkgdir}/usr/share/man/man5/ldap.conf.5 || return 1
  
  # get rid of duplicate default conf files
  rm ${pkgdir}/etc/openldap/*.default

  ln -sf liblber.so ${pkgdir}/usr/lib/liblber.so.2 || return 1
  ln -sf libldap.so ${pkgdir}/usr/lib/libldap.so.2 || return 1

  install -Dm644 ${srcdir}/openldap-${pkgver}/LICENSE \
    ${pkgdir}/usr/share/licenses/$pkgname/LICENSE
}
