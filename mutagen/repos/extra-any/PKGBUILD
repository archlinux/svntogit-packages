# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgbase=mutagen
pkgname=('mutagen' 'python-mutagen')
pkgver=1.29
pkgrel=1
arch=('any')
url="https://bitbucket.org/lazka/mutagen"
license=('GPL2')
makedepends=('python' 'python2')
source=(https://bitbucket.org/lazka/mutagen/downloads/${pkgbase}-${pkgver}.tar.gz{,.sig})
sha1sums=('cd2fb0a4375e4fd572905ba93e5467dfafcf74be'
          'SKIP')
validpgpkeys=('0EBF782C5D53F7E5FB02A66746BD761F7A49B0EC')

prepare() {
  cp -r ${pkgbase}-${pkgver} python-${pkgbase}-${pkgver}
}

check() {
  cd ${pkgbase}-${pkgver}
  LANG=en_US.UTF8 python2 setup.py test
  cd ../python-${pkgbase}-${pkgver}
  LANG=en_US.UTF8 python setup.py test
}

package_mutagen() {
  pkgdesc="An audio metadata tag reader and writer"
  depends=('python2')
 
  cd ${pkgbase}-${pkgver}
  python2 setup.py install --root="${pkgdir}"
}

package_python-mutagen() {
  pkgdesc="An audio metadata tag reader and writer library for Python 3"
  depends=('python')

  cd python-${pkgbase}-${pkgver}
  python setup.py install --root="${pkgdir}"
  rm -r "${pkgdir}"/usr/{bin,share}
}
