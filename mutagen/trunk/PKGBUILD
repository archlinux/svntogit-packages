# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgbase=mutagen
pkgname=('mutagen' 'python-mutagen')
pkgver=1.40.0
pkgrel=2
arch=('any')
url="https://github.com/quodlibet/mutagen"
license=('GPL2')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest' 'python2-pytest' 'python-hypothesis' 'python2-hypothesis' 'python-pyflakes' 'python2-pyflakes'
              'python-pycodestyle' 'python2-pycodestyle')
source=($pkgbase-$pkgver.tar.gz::"https://github.com/quodlibet/mutagen/releases/download/release-$pkgver/$pkgbase-$pkgver.tar.gz"
        $pkgbase-$pkgver.tar.gz.sig::"https://github.com/quodlibet/mutagen/releases/download/release-$pkgver/$pkgbase-$pkgver.tar.gz.sig"
        pyflakes-fixes.patch
        disable-pycodestyle-warning-w504.patch)
sha256sums=('b2a2c2ce87863af12ed7896f341419cd051a3c72c3c6733db9e83060dcadee5e'
            'SKIP'
            '201f81d44c497544c6f86a186964abeae27f254db5c026802245217b359516f6'
            'da9f8adf4fa1567957732e9f2399d4397579f990cd6046eaac40b177e8578f89')
validpgpkeys=('0EBF782C5D53F7E5FB02A66746BD761F7A49B0EC')

prepare() {
  patch -d ${pkgbase}-${pkgver} -Np1 < pyflakes-fixes.patch
  patch -d ${pkgbase}-${pkgver} -Np1 < disable-pycodestyle-warning-w504.patch
  cp -r ${pkgbase}-${pkgver} python-${pkgbase}-${pkgver}
}

check() {
  cd ${pkgbase}-${pkgver}
  LANG=en_US.UTF8 python2 setup.py test
  cd ../python-${pkgbase}-${pkgver}
  LANG=en_US.UTF8 python setup.py test
}

package_mutagen() {
  pkgdesc="An audio metadata tag reader and writer"
  depends=('python2')
 
  cd ${pkgbase}-${pkgver}
  python2 setup.py install --root="${pkgdir}"
}

package_python-mutagen() {
  pkgdesc="An audio metadata tag reader and writer library for Python 3"
  depends=('python')

  cd python-${pkgbase}-${pkgver}
  python setup.py install --root="${pkgdir}"
  rm -r "${pkgdir}"/usr/{bin,share}
}
