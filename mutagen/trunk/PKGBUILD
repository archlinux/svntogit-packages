# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgbase=mutagen
pkgname=('mutagen' 'python-mutagen')
pkgver=1.37
pkgrel=1
arch=('any')
url="https://github.com/quodlibet/mutagen"
license=('GPL2')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest' 'python2-pytest')
source=($pkgname-$pkgver::"https://github.com/quodlibet/mutagen/archive/release-$pkgver.tar.gz")
sha1sums=('4c9725f0b613d54e8ae522dac3d716c36ef10095')
validpgpkeys=('0EBF782C5D53F7E5FB02A66746BD761F7A49B0EC')

prepare() {
  cp -r ${pkgbase}-release-${pkgver} python-${pkgbase}-${pkgver}
}

check() {
  cd ${pkgbase}-release-${pkgver}
  LANG=en_US.UTF8 python2 setup.py test
  cd ../python-${pkgbase}-${pkgver}
  LANG=en_US.UTF8 python setup.py test
}

package_mutagen() {
  pkgdesc="An audio metadata tag reader and writer"
  depends=('python2')
 
  cd ${pkgbase}-release-${pkgver}
  python2 setup.py install --root="${pkgdir}"
}

package_python-mutagen() {
  pkgdesc="An audio metadata tag reader and writer library for Python 3"
  depends=('python')

  cd python-${pkgbase}-${pkgver}
  python setup.py install --root="${pkgdir}"
  rm -r "${pkgdir}"/usr/{bin,share}
}
