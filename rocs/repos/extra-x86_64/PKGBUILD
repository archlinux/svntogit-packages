# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=rocs
pkgver=15.04.1
pkgrel=1
pkgdesc="Rocs Graph Theory"
url="http://kde.org/applications/education/rocs/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdeedu')
depends=('grantlee-qt5' 'ktexteditor' 'kdeclarative' 'qt5-webkit')
makedepends=('extra-cmake-modules' 'python' 'kdoctools' 'boost')
install=${pkgname}.install
conflicts=('kdeedu-rocs')
replaces=('kdeedu-rocs')
source=("http://download.kde.org/stable/applications/${pkgver}/src/rocs-${pkgver}.tar.xz")
sha1sums=('05b5f5f2c372674ba5636414071055e0c6e1e4dd')

build() {
  mkdir build
  cd build
  cmake ../rocs-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
