# $Id$
#Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=font-bh-ttf
pkgver=1.0.3
pkgrel=1
pkgdesc="X.org Luxi Truetype fonts"
arch=('any')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('xorg-fonts-encodings' 'xorg-fonts-alias' 'xorg-font-utils' 'fontconfig')
install=xfonts.install
source=(${url}/releases/individual/font/${pkgname}-${pkgver}.tar.bz2)
md5sums=('e8ca58ea0d3726b94fe9f2c17344be60')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
      --with-fontdir=/usr/share/fonts/TTF
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm "${pkgdir}/usr/share/fonts/"*/fonts.*

  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
