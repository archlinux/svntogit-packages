# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Maintainer: Guillaume ALAUX <guillaume@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=xerces2-java
pkgver=2.12.0
pkgrel=1
pkgdesc="High performance fully compliant Java XML parser"
arch=('any')
url="https://xml.apache.org/xerces2-j"
license=('APACHE')
depends=('java-runtime' 'java-resolver')
provides=("java-xerces2=${pkgver}")
conflicts=('java-xerces2')
replaces=('java-xerces2')
source=(http://archive.apache.org/dist/xerces/j/Xerces-J-bin.${pkgver}.tar.gz{,.asc})
sha512sums=('1a65af6b48429c2967a393e6ff2d6d4bed602fbfc95d85ab4da3d7442eed4373134455dc4a2488ea80fe1ea142126a4774ded7a115e20b61552769acdcfb6261'
            'SKIP')
validpgpkeys=('4D8FB572FB6ADCFD69CBFE0D7B2586A6B5E25C3D') # Mukul Gandhi (CODE SIGNING KEY) <mukulg@apache.org>

package() {
  cd xerces-${pkgver//./_}
  install -dm755 "${pkgdir}"/usr/share/java
  install -m644 {serializer,xercesImpl,xml-apis}.jar "${pkgdir}"/usr/share/java/
}
