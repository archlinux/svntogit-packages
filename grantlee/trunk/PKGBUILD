# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=grantlee
pkgver=5.1.0
pkgrel=2
pkgdesc="A string template engine based on the Django template system and written in Qt5"
arch=('x86_64')
url="https://www.gitorious.org/grantlee/pages/Home"
license=('LGPL3')
depends=('qt5-script')
makedepends=('cmake' 'doxygen')
conflicts=('grantlee-qt5')
provides=('grantlee-qt5')
replaces=('grantlee-qt5')
source=("http://downloads.grantlee.org/grantlee-$pkgver.tar.gz"{,.asc}) # Stephen Kelly <steveire@gmail.com>
md5sums=('57989ae9f7c113e682ef1713a6f1e92a'
         'SKIP')
validpgpkeys=(FCA530E51EE4331C2DF16637D264C7B1D02D6509)

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTS=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
