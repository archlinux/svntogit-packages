# $Id$
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>

pkgname=linux-firmware
pkgver=20130808
pkgrel=1
pkgdesc="Firmware files for Linux"
makedepends=('git')
arch=('any')
url="http://git.kernel.org/?p=linux/kernel/git/firmware/linux-firmware.git;a=summary"
license=('GPL2' 'GPL3' 'custom')
conflicts=('linux-firmware-git'
           'kernel26-firmware'
           'ar9170-fw'
           'iwlwifi-1000-ucode'
           'iwlwifi-3945-ucode'
           'iwlwifi-4965-ucode'
           'iwlwifi-5000-ucode'
           'iwlwifi-5150-ucode'
           'iwlwifi-6000-ucode'
           'rt2870usb-fw'
           'rt2x00-rt61-fw'
           'rt2x00-rt71w-fw'
           'amd-ucode')
replaces=('kernel26-firmware'
          'ar9170-fw'
          'iwlwifi-1000-ucode'
          'iwlwifi-3945-ucode'
          'iwlwifi-4965-ucode'
          'iwlwifi-5000-ucode'
          'iwlwifi-5150-ucode'
          'iwlwifi-6000-ucode'
          'rt2870usb-fw'
          'rt2x00-rt61-fw'
          'rt2x00-rt71w-fw'
          'amd-ucode')
options=(!strip)
source=('git://git.kernel.org/pub/scm/linux/kernel/git/firmware/linux-firmware.git#branch=master')
md5sums=('SKIP')

pkgver() {
  date +%Y%m%d
}

package() {
  cd "${srcdir}/${pkgname}"
  # remove not needed Makefile #35236
  rm Makefile
  install -d -m755 "${pkgdir}/usr/lib/firmware"
  cp -a * "${pkgdir}/usr/lib/firmware/"
  install -d -m755 "${pkgdir}/usr/share/licenses/linux-firmware/"
  rm -f "${pkgdir}/usr/lib/firmware/"{README*,configure,GPL*}
  mv "${pkgdir}/usr/lib/firmware"/{LICEN*,WHENCE} "${pkgdir}/usr/share/licenses/linux-firmware/"
}
# vim:set ts=2 sw=2 et:
