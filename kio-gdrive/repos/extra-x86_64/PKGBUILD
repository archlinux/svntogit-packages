# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kio-gdrive
pkgver=20.12.1
pkgrel=1
arch=(x86_64)
pkgdesc="KIO Slave to access Google Drive"
url="https://community.kde.org/KIO_GDrive"
license=(GPL)
depends=(libkgapi kaccounts-providers)
makedepends=(extra-cmake-modules kdoctools intltool)
groups=(kde-applications kde-network)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('e07ef1431b2d9b7e12d9c09483c31f1f321351fc88177d1450d7b18c4640b02b'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package(){
  DESTDIR="$pkgdir" cmake --install build
}
