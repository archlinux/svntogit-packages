# Maintainer:

pkgname=python-fastbencode
pkgver=0.0.7
pkgrel=1
pkgdesc='Implementation of bencode with optional fast C extensions'
arch=(x86_64)
url='https://github.com/breezy-team/fastbencode'
license=(GPL2)
depends=(python)
makedepends=(python-build python-installer python-setuptools python-wheel)
checkdepends=(python-pytest)
source=(https://pypi.io/packages/source/f/fastbencode/fastbencode-$pkgver.tar.gz)
sha256sums=('b6bc9abe542d0663793529576f49ba8891508554f85d09b5d6d5ed7af7e0c6e4')

build() {
  cd fastbencode-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd fastbencode-$pkgver
  python setup.py test
}

package() {
  cd fastbencode-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
}
