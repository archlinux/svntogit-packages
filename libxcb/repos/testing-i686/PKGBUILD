# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

pkgname=libxcb
pkgver=1.9.3
pkgrel=1
pkgdesc="X11 client-side library"
arch=('i686' 'x86_64')
url="http://xcb.freedesktop.org/"
depends=('xcb-proto>=1.9' 'libxdmcp' 'libxau')
makedepends=('pkgconfig' 'libxslt' 'python2')
license=('custom')
source=($url/dist/$pkgname-$pkgver.tar.bz2
        libxcb-1.1-no-pthread-stubs.patch
        reorder-configure.patch)
sha256sums=('aad09d223fcb5bd345ce4d1737f178a557b6f2e201128e1ee3c83af46028018b'
            'e071e9ef4ded0229d19e40ffee229ae6c3171bbebcc3322a2378a84cfb6a8678'
            'a82e789d8b18134ce23ea1f7fd2e71a5386be6e63b1a7f8b774b61e8d032c336')

prepare() {
  cd "$pkgname-$pkgver"
  patch -Np1 -i ../libxcb-1.1-no-pthread-stubs.patch
  # https://bugs.freedesktop.org/show_bug.cgi?id=66413
  #patch -Np1 -i ../reorder-configure.patch
  #NOCONFIGURE=1 ./autogen.sh
  autoreconf -vfi
}

build() {
  export PYTHON=/usr/bin/python2 
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr \
    --enable-xinput \
    --enable-xkb \
    --disable-static
  make
}

check() {
  cd $pkgname-$pkgver
  make -k check
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -m755 -d "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname"
}
