# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-xprop
pkgver=1.2.2
pkgrel=1
pkgdesc="Property displayer for X"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libx11')
makedepends=('xorg-util-macros')
groups=('xorg-apps' 'xorg')
source=(http://xorg.freedesktop.org/archive/individual/app/xprop-${pkgver}.tar.bz2)
sha256sums=('9bee88b1025865ad121f72d32576dd3027af1446774aa8300cce3c261d869bc6')

build() {
  cd "${srcdir}/xprop-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/xprop-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
