# $Id$
# Contributor: Camille Moncelier <pix@devlife.org>, simo <simo@archlinux.org>
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgname=kexec-tools
pkgver=2.0.2
pkgrel=4
pkgdesc="Load another kernel from the currently executing Linux kernel"
arch=('i686' 'x86_64')
url="http://kernel.org/pub/linux/utils/kernel/kexec/"
source=("http://kernel.org/pub/linux/utils/kernel/kexec/kexec-tools-${pkgver}.tar.bz2"
        'kexec-tools-2.0.0-purgatory-makefile.patch'
        'kexec' 
        'kexec.conf.d')
depends=('gawk')
makedepends=('zlib' 'xz')
backup=('etc/conf.d/kexec')
license=('GPL2')
md5sums=('35d05fed1c97008ac34c5bfb929295eb'
         'b96f47c29757fcadfbc50ea6ea22f839'
         'be059a103e5989519d22fca99f860a64'
         '9ca7e31d2c5c66c436a9a2baf20ad9b2')

build() {
  cd $srcdir/$pkgname-$pkgver
  export LDFLAGS+=" -static"
  patch -Np1 -i ../kexec-tools-2.0.0-purgatory-makefile.patch
  ./configure --prefix=/usr
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install

  mv $pkgdir/usr/sbin $pkgdir/sbin

  install -D -m644 kexec/kexec.8 $pkgdir/usr/share/man/man8/kexec.8
  install -D -m644 $srcdir/kexec.conf.d $pkgdir/etc/conf.d/kexec
  install -D -m755 $srcdir/kexec $pkgdir/etc/rc.d/kexec
}
