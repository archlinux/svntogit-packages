# $Id$
# Contributor: Camille Moncelier <pix@devlife.org>, simo <simo@archlinux.org>
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgname=kexec-tools
pkgver=2.0.8
pkgrel=1
pkgdesc="Load another kernel from the currently executing Linux kernel"
arch=('i686' 'x86_64')
url="http://kernel.org/pub/linux/utils/kernel/kexec/"
source=("https://sources.archlinux.org/other/kexec-tools/kexec-tools-${pkgver}.tar.bz2"
        # upstream tarballs broken with bsdtar!
        #"http://kernel.org/pub/linux/utils/kernel/kexec/kexec-tools-${pkgver}.tar.xz"
        #"http://kernel.org/pub/linux/utils/kernel/kexec/kexec-tools-${pkgver}.tar.sign"
        )
# validpgpkeys=('E27CD9A1F5ACC2FF4BFE7285D7CF64696A374FBE')# Simon Horman
depends=('gawk' 'zlib' 'xz')
license=('GPL2')

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure --prefix=/usr --sbindir=/usr/bin --without-lzma
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
  install -D -m644 kexec/kexec.8 $pkgdir/usr/share/man/man8/kexec.8
}
md5sums=('f6277721247b87b05ecc9a98d3eace1a')
