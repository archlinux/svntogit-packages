# $Id$
# Contributor: Camille Moncelier <pix@devlife.org>, simo <simo@archlinux.org>
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgname=kexec-tools
pkgver=2.0.3
pkgrel=2
pkgdesc="Load another kernel from the currently executing Linux kernel"
arch=('i686' 'x86_64')
url="http://kernel.org/pub/linux/utils/kernel/kexec/"
source=("ftp://ftp.archlinux.org/other/kexec-tools/kexec-tools-${pkgver}.tar.bz2"
        #"http://kernel.org/pub/linux/utils/kernel/kexec/kexec-tools-${pkgver}.tar.bz2"
        'kexec' 
        'kexec.conf.d')
depends=('gawk' 'zlib' 'xz')
backup=('etc/conf.d/kexec')
license=('GPL2')

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure --prefix=/usr --without-lzma
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install

  mv $pkgdir/usr/sbin $pkgdir/sbin

  install -D -m644 kexec/kexec.8 $pkgdir/usr/share/man/man8/kexec.8
  install -D -m644 $srcdir/kexec.conf.d $pkgdir/etc/conf.d/kexec
  install -D -m755 $srcdir/kexec $pkgdir/etc/rc.d/kexec
}
md5sums=('3d9ff82e8e16165b9e79ded271c08348'
         'be059a103e5989519d22fca99f860a64'
         '9ca7e31d2c5c66c436a9a2baf20ad9b2')
