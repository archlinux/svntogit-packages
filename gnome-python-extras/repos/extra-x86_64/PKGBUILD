# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=gnome-python-extras
pkgname=('gnome-python-extras' 'python2-egg' 'python2-gtkspell')
pkgver=2.25.3
pkgrel=13
arch=('i686' 'x86_64')
url="http://www.daa.com.au/~james/pygtk/"
license=('GPL' 'LGPL')
makedepends=('gtkspell' 'libgnomeui' 'python2' 'pygtk' 'gnome-python')
options=('!libtool')
source=(http://ftp.gnome.org/pub/gnome/sources/gnome-python-extras/2.25/gnome-python-extras-${pkgver}.tar.bz2
	gnome-python-extras-2.25.3-update-for-2.27.2.patch)
md5sums=('9f3b7ec5c57130b96061cb486b79c076' '0b0a4d93016d14f2306682301de3bda3')
sha1sums=('e69e5288466c960ed6f985bb10c9969b21ed85a4' 'b065efb7f7f5baa7b95b63bcadb6a4623d1bc9e5')

prepare() {
  cd $pkgbase-$pkgver
  patch -Np1 -i ../gnome-python-extras-2.25.3-update-for-2.27.2.patch
}
    
build() {
  cd $pkgbase-$pkgver
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --disable-gksu --disable-gtkhtml2 --disable-gksu2 --disable-gksuui
  make
}

package_gnome-python-extras() {
  pkgdesc="Gnome Python interfaces for libraries not part of the core platform"
  depends=('python2-egg' 'python2-gtkspell')

  cd $pkgbase-$pkgver
  make install-pkgconfigDATA DESTDIR="${pkgdir}"
}

package_python2-egg(){
  pkgdesc="Python bindings for interacting with egg"
  depends=('libgnomeui' 'pygtk')

  cd $pkgbase-$pkgver
  make -C egg install DESTDIR="${pkgdir}"
}

package_python2-gtkspell() {
  pkgdesc="Python bindings for the gtkspell"
  depends=('gtkspell' 'pygtk')

  cd $pkgbase-$pkgver
  make -C gtkspell install DESTDIR="${pkgdir}"
}
