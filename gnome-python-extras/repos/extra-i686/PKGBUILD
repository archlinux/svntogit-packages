# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-python-extras
pkgver=2.25.3
pkgrel=3
pkgdesc="Gnome Python interfaces for libraries not part of the core platform"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL')
depends=('gnome-python>=2.22.1')
makedepends=('libgtkhtml' 'gtkspell' 'gdl>=2.28.1' 'xulrunner>=1.9.1' 'libgksu' 'pkgconfig' 'libgnomeui' 'libgda' 'docbook-xsl')
optdepends=('libgtkhtml' 'gtkspell' 'gdl' 'xulrunner' 'libgksu' 'libgnomeui' 'libgda')
options=('!libtool')
source=(http://ftp.gnome.org/pub/gnome/sources/gnome-python-extras/2.25/gnome-python-extras-${pkgver}.tar.bz2
	gnome-python-extras-2.25.3-update-for-2.27.2.patch)
url="http://www.daa.com.au/~james/pygtk/"
md5sums=('9f3b7ec5c57130b96061cb486b79c076'
         'ccb4a45519b950e74f1bb5d884a67549')

build() {
  cd "${srcdir}/gnome-python-extras-${pkgver}"
  patch -Np1 -i "${srcdir}"/gnome-python-extras-2.25.3-update-for-2.27.2.patch || return 1

  ./configure --prefix=/usr --enable-docs || return 1
  make MOZILLA_HOME=/usr/lib/xulrunner-1.9.1 || return 1
  make DESTDIR="${pkgdir}" install || return 1
}
