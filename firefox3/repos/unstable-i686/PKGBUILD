# $Id$
# Maintainer: Dan McGee <dan@archlinux.org>
# Contributor: Jakub Schmidtke <sjakub@gmail.com>

pkgname=firefox3
pkgver=3.0rc2
pkgrel=1
pkgdesc="Standalone web browser from mozilla.org"
arch=(i686 x86_64)
license=('MPL' 'GPL' 'LGPL')
depends=('gtk2>=2.12.0' 'pango>=1.18.0' 'gcc-libs' 'libxt' 'libidl2' 'mozilla-common' 'desktop-file-utils' 'cairo' 'curl' 'nss>=3.11.7' 'dbus-glib' 'libpng>=1.2.24-3')
makedepends=('zip' 'imagemagick' 'pkgconfig' 'python')
install=firefox.install
url="http://www.mozilla.org/projects/firefox"
source=(http://releases.mozilla.org/pub/mozilla.org/firefox/releases/${pkgver}/source/firefox-${pkgver}-source.tar.bz2
        mozconfig
        firefox3.desktop
        firefox3-safe.desktop
        mozilla-firefox-1.0-lang.patch)
options=('!makeflags')

build() {
  cd ${startdir}/src/mozilla
  patch -Np1 -i $startdir/src/mozilla-firefox-1.0-lang.patch || return 1

  export MOZ_PROJECT=browser

  sed "s/#CFLAGS#/${CFLAGS}/g" ${startdir}/src/mozconfig >.mozconfig
  make -f client.mk build || return 1
  make DESTDIR=${startdir}/pkg install || return 1

  cd ${startdir}/pkg/opt/mozilla/lib/firefox-3.0
  export MOZ_DISABLE_GNOME=1
  export MOZTMP=`mktemp -d -p ${startdir}/src`
  LD_LIBRARY_PATH=`pwd` HOME=${MOZTMP} ./firefox-bin -register
  rm -rf ${MOZTMP}
  cd chrome
  find . -maxdepth 1 -type d -exec rm -rf {} \;

  #Remove mozilla devel stuff, this is in XULRunner now
  rm -rf ${startdir}/pkg/opt/mozilla/share
  rm -rf ${startdir}/pkg/opt/mozilla/include
  rm -rf ${startdir}/pkg/opt/mozilla/lib/pkgconfig
  rm -rf ${startdir}/pkg/opt/mozilla/lib/firefox-devel-3.0

  rm -rf ${startdir}/pkg/opt/mozilla/bin/defaults

  mkdir -p ${startdir}/pkg/usr/share/applications
  mkdir -p ${startdir}/pkg/usr/share/pixmaps
  install -m644 ${startdir}/src/mozilla/browser/app/default48.png ${startdir}/pkg/usr/share/pixmaps/firefox3.png
  install -m644 ${startdir}/src/firefox3.desktop ${startdir}/pkg/usr/share/applications/
  install -m644 ${startdir}/src/firefox3-safe.desktop ${startdir}/pkg/usr/share/applications/

  # ensure icon shows up correctly in window manager stuff
  mkdir -p ${startdir}/pkg/opt/mozilla/lib/firefox-3.0/chrome/icons/default
  install -m644 ${startdir}/src/mozilla/browser/app/mozicon50.xpm \
    ${startdir}/pkg/opt/mozilla/lib/firefox-3.0/chrome/icons/default/default.xpm
  install -m644 ${startdir}/src/mozilla/browser/app/mozicon50.xpm \
    ${startdir}/pkg/opt/mozilla/lib/firefox-3.0/icons/default.xpm

  cd ${startdir}/pkg/opt/mozilla/bin && mv firefox firefox3
}

md5sums=('17fc2acfffa0da2df2457d39747a17b0'
         'bda73d7398c628bb9bf257e55e8d0627'
         '1edbe1a5b584613bde6c21f4ee247b6e'
         '370aa36551a70150c1c6f07672ca0f32'
         'bd5db57c23c72a02a489592644f18995')
