# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Thijs Vermeir <thijsvermeir@gmail.com>
# Contributor: Denis Zawada <deno@rootnode.net>

pkgname=gupnp-av
pkgver=0.12.10
pkgrel=2
pkgdesc="Library to ease handling and implementation of UPnP A/V profiles"
arch=(x86_64)
url="http://gupnp.org/"
license=(LGPL)
depends=(gupnp)
makedepends=(gobject-introspection gtk-doc vala autoconf-archive git)
_commit=d61e6a16d1b5e7df70277f410b2853e4b6e7d877  # tags/gupnp-av-0.12.10^0
source=("git+https://gitlab.gnome.org/GNOME/gupnp-av.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^gupnp-av-//;s/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static --enable-gtk-doc --enable-compile-warnings=minimum
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
