# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgname=kipi-plugins
pkgver=1.6.0
pkgrel=2
pkgdesc="libkipi plugins for digikam and kde apps"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.kipi-plugins.org"
makedepends=('pkgconfig' 'cmake' 'automoc4')
depends=('kdegraphics-libs' 'kdepimlibs' 'opencv' 'libgpod')
source=("http://downloads.sourceforge.net/kipi/$pkgname-$pkgver.tar.bz2")
optdepends=('imagemagick: for picture manipulation'
            'qca: for shwup webservice support')
install=${pkgname}.install

build() {
  cd $srcdir
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON
  make
}

package() {
  cd ${srcdir}/build
  make DESTDIR=$pkgdir install
}
md5sums=('5c9b68818afab24a7fe31e4c9580b81a')
