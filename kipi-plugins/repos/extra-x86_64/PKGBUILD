# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kipi-plugins
pkgver=19.08.3
pkgrel=1
pkgdesc="A collection of plugins extending the KDE graphics and image applications"
arch=(x86_64)
license=(GPL)
url="https://userbase.kde.org/KIPI"
depends=(kio libkipi)
optdepends=('libmediawiki: MediaWiki Export plugin'
            'libkvkontakte: VKontakte.ru Exporter plugin'
            'qt5-xmlpatterns: rajce.net plugin')
makedepends=(extra-cmake-modules qt5-xmlpatterns libmediawiki libkvkontakte)
groups=(kdegraphics kde-applications)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('f540e517421503b54e906633c71c2f688bb920c5aefe7d05a13d0eeaf0356a9b'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build

  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
