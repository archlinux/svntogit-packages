# $Id$
# Maintainer: Dale Blount <dale@archlinux.org>
# Contributor: Gregor Ibic <gregor.ibic@intelicom.si>

pkgname=clamav
pkgver=0.96.3
pkgrel=1
pkgdesc='Anti-virus toolkit for Unix'
arch=('i686' 'x86_64')
depends=('bzip2' 'zlib' 'libtool')
options=(!libtool)
install="$pkgname.install"
license=('GPL')
backup=('etc/clamav/clamd.conf' 'etc/clamav/freshclam.conf' 'etc/conf.d/clamav')
url='http://www.clamav.net/'
source=("http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz"
        'clamav'
	'clamav.confd'
	'clamav.logrotate')

sha1sums=('b4566c83bc7ca8bdc3783a9487cfe5eaf6084c65'
          '7f15f0b13a1c11235bc99ef0add01efd8a442f07'
          'cb116cdab49a810381a515cbcfb6a6c148547f07'
          'be3310d2b41a68ce06e33c84ab68ffe59fdce104')

build() {
	cd "$srcdir/$pkgname-$pkgver"

	./configure --prefix=/usr --sysconfdir=/etc/clamav \
		--with-dbdir=/var/lib/clamav --disable-clamav
	# will add clamav user with clamav.install
	make
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install

	# make sure conf files get installed, cause make install
	# doesn't do that if clamav is already installed upon building.
	for i in clamd freshclam; do
		install -D -m644 etc/$i.conf "$pkgdir/etc/clamav/$i.conf"
	done

	install -D -m644 "$srcdir/clamav.confd" "$pkgdir/etc/conf.d/clamav"
	install -D -m755 "$srcdir/clamav" "$pkgdir/etc/rc.d/clamav"
	install -D -m644 "$srcdir/clamav.logrotate" "$pkgdir/etc/logrotate.d/clamav"

	# create log dirs/files & fix conf files.
	mkdir -p "$pkgdir/var/log/clamav/"
	mkdir -p "$pkgdir/var/run/clamav/"
	chown 64.root "$pkgdir/var/log/clamav/"
	chown 64.root "$pkgdir/var/run/clamav/"

	sed -i -e "s:\#LogFile /tmp/clamd.log:LogFile /var/log/clamav/clamd.log:" \
		-e "s:\#PidFile /var/run/clamd.pid:PidFile /var/run/clamav/clamd.pid:" \
		-e "s:\#User clamav:User clamav:" \
		-e "s:\#LogTime:LogTime:" \
		-e "s:\#TemporaryDirectory /var/tmp:TemporaryDirectory /tmp:" \
		-e "s:\#LocalSocket /tmp/clamd.socket:LocalSocket /var/lib/clamav/clamd.sock:" \
		"$pkgdir/etc/clamav/clamd.conf"

	sed -i -e "s:\#UpdateLogFile /var/log/freshclam.log:UpdateLogFile /var/log/clamav/freshclam.log:" \
		-e "s:\#NotifyClamd /path/to/clamd.conf$:NotifyClamd /etc/clamav/clamd.conf:" \
		"$pkgdir/etc/clamav/freshclam.conf"

	# fix perms on virus database directory and un-distribute databases to require freshclam
	rm "$pkgdir"/var/lib/clamav/*.cvd
	chown 64:64 -R "$pkgdir/var/lib/clamav/"
}
