# $Id$
# Contributor: Dale Blount <dale@archlinux.org>
# Contributor: Gregor Ibic <gregor.ibic@intelicom.si>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=clamav
pkgver=0.98.3
pkgrel=1
pkgdesc='Anti-virus toolkit for Unix'
url='http://www.clamav.net/'
license=('GPL')
arch=('i686' 'x86_64')
depends=('bzip2' 'libltdl' 'gcc-libs')
source=("http://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz"{,.sig}
        'logrotate'
        'tmpfiles.d'
        'clamd.conf'
        'freshclam.conf'
        'clamd.service'
        'freshclamd.service')
sha1sums=('32f0a0675f8023b20e1d19fe8592e330674d0551' 'SKIP'
          'bb488a56b0f6a0760446cde89c1e3321e2717b78'
          'a224ea9b4d0f4f196827347d54bed51e11c197ea'
          '887f624eb305f2446f55d8339e2972ad0cfe2b79'
          'b767837d0279ad30b92c314cb762b73e5ad0415e'
          'b6ac0c5de1e5b41232b10d4f84e86201c370eaeb'
          'cda9a087e5593992150cb456e34c5f6f589aca82')

backup=('etc/clamav/clamd.conf'
        'etc/clamav/freshclam.conf'
        'etc/logrotate.d/clamav')

install=install

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure \
		--prefix=/usr \
		--sbindir=/usr/bin \
		--sysconfdir=/etc/clamav \
		--with-dbdir=/var/lib/clamav \
		--disable-clamav \

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	install -Dm644 ../clamd.conf "${pkgdir}"/etc/clamav/clamd.conf
	install -Dm644 ../freshclam.conf "${pkgdir}"/etc/clamav/freshclam.conf
	install -Dm644 ../freshclamd.service "${pkgdir}"/usr/lib/systemd/system/freshclamd.service
	install -Dm644 ../clamd.service "${pkgdir}"/usr/lib/systemd/system/clamd.service
	install -Dm644 ../tmpfiles.d "${pkgdir}"/usr/lib/tmpfiles.d/clamav.conf
	install -Dm644 ../logrotate "${pkgdir}"/etc/logrotate.d/clamav

	install -d -o 64 -g 64 "${pkgdir}"/var/log/clamav
	install -d -o 64 -g 64 "${pkgdir}"/var/lib/clamav
}
