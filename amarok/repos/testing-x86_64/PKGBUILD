# $Id$
# Maintainer: Ronald van Haren <ronald@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=amarok
replaces=('amarok-base' 'amarok-engine-xine' 'amarok-base-mysqlfree')
pkgver=2.5.0
pkgrel=5
pkgdesc="The powerful music player for KDE"
arch=("i686" "x86_64")
url="http://amarok.kde.org"
license=('GPL2' 'LGPL2.1' 'FDL')
depends=('kdebase-runtime' 'mysql' 'qtscriptgenerator' 'taglib-extras'
        'liblastfm' 'ffmpeg' 'libofa' 'qjson')
makedepends=('pkgconfig' 'automoc4' 'cmake' 'libgpod' 'libmtp' 'loudmouth'
             'libmygpo-qt')
optdepends=("libgpod: support for Apple iPod audio devices"
	        "libmtp: support for portable media devices"
        	"loudmouth: backend needed by mp3tunes for syncing"
            "ifuse: support for Apple iPod Touch and iPhone"
            "libmygpo-qt: gpodder.net Internet Service")
install="${pkgname}.install"
source=("http://download.kde.org/stable/${pkgname}/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2"
	"toolbarfix.patch"
	"amarok-2.5.0-ffmpeg-fixes.patch"
	"contextviewfix.patch")
sha1sums=('9849900d20225e703c43d242650a8fa211cf15f2'
          'd22bc6a36a33ea12035ebbb8959d1fdfbb39275f'
          '61193ccc48e9f99b4856eef27fe7a4aaf465feec'
          'a95e3bfb177adacc268552ae479dd88b70f5f9e1')

build() {
  cd "${srcdir}"

  patch -Np0 -i "${srcdir}"/toolbarfix.patch
  patch -Np0 -i "${srcdir}"/amarok-2.5.0-ffmpeg-fixes.patch

  pushd ${pkgname}-${pkgver}
  patch -Np1 -i "${srcdir}"/contextviewfix.patch # FS#30448
  popd

  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package(){
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
