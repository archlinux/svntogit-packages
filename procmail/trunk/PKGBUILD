# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=procmail
pkgver=3.22
pkgrel=7
pkgdesc="Highly configurable auto mail processing."
arch=('i686' 'x86_64')
url="http://www.procmail.org"
license=('GPL' 'custom:Artistic')
depends=('glibc' 'sh')
makedepends=('smtp-server')
source=(#"http://www.procmail.org/${pkgname}-${pkgver}.tar.gz" # currently down
        "ftp://ftp.informatik.rwth-aachen.de/pub/packages/procmail/${pkgname}-${pkgver}.tar.gz"
        'procmail-3.22-getline.patch')
md5sums=('1678ea99b973eb77eda4ecf6acae53f1'
         'cf8e5d4854050ab0c836e07406a3dfa9')

build() {
  cd $srcdir/$pkgname-$pkgver
  patch -p1 -i ${srcdir}/procmail-3.22-getline.patch
  yes n | make CFLAGS0="${CFLAGS} ${LDFLAGS}" LOCKINGTEST="/tmp ."
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make BASENAME=${pkgdir}/usr MANDIR=${pkgdir}/usr/share/man install
  install -D -m644 Artistic ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
  install -d -m755 ${pkgdir}/usr/share/doc/${pkgname}/examples
  install -m644 examples/* ${pkgdir}/usr/share/doc/${pkgname}/examples/
}
