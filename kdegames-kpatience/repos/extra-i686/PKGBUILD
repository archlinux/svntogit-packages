# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-kpatience
pkgver=4.12.3
pkgrel=1
pkgdesc="Offers a selection of solitaire card games"
url="http://kde.org/applications/games/kpat/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'libkdegames')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/kpat-${pkgver}.tar.xz")
sha1sums=('07d1bf56d5d4daf152d58ba6d0d14bb3ea9d2abc')

build() {
  mkdir build
  cd build
  cmake ../kpat-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
