# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdebindings-perlkde
pkgver=4.7.3
pkgrel=1
pkgdesc="Perl bindings for the KDE libraries"
url="http://kde.org/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kdebindings')
depends=('kdebindings-perlqt' 'kdebindings-smokekde')
makedepends=('cmake' 'automoc4' 'kdebindings-smokegen' 'kdepimlibs'
            'kdegraphics-okular' 'kdesdk-kate')
source=("http://download.kde.org/stable/${pkgver}/src/perlkde-${pkgver}.tar.bz2")
sha1sums=('e0cc0d644084f1c71b6afc30d5467c49ba9a9fab')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../perlkde-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
