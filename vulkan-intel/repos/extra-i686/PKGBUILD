# $Id$
# Maintainer: Laurent Carlier <lordheavym@gmail.com>

pkgname=vulkan-intel
pkgver=0.0.3+3713+ga998e49
#pkgver=11.2+branchpoint+3713+ga998e49
pkgrel=1
pkgdesc="Intel's Vulkan mesa driver"
arch=(x86_64 i686)
url="http://www.mesa3d.org"
license=('custom')
depends=('vulkan-icd-loader' 'libgcrypt' 'wayland' 'libxcb' 'libxshmfence')
makedepends=('libdrm' 'dri3proto' 'glproto' 'libxxf86vm' 'libxdamage' 'expat>=2.0.1' 'libxmu'
             'talloc' 'wayland' 'pkgconfig' 'imake' 'xorg-server-devel' 'python2-mako' 'python' 'git')
source=('anvil::git://anongit.freedesktop.org/mesa/mesa#commit=a998e492594df333834131dbe705b4023ef7e929'
        LICENSE
        fixes.patch) 
sha256sums=('SKIP'
            '7fdc119cf53c8ca65396ea73f6d10af641ba41ea1dd2bd44a824726e01c8b3f2'
            'f2b490dacd654d7c810481a0025f200fc864cdd4d8d647ece9815cc83903265e')

# pkgver() {
#   cd $pkgname
#   git describe --long | sed 's/-/+/g'
# }

prepare() {
  cd "${srcdir}/anvil"

  patch -Np1 -i ../fixes.patch  
}

build() {
  cd "${srcdir}/anvil"

 ./autogen.sh --prefix=/usr \
    --sysconfdir=/etc \
    --with-sha1=libgcrypt \
    --with-dri-drivers=i965 \
    --with-egl-platforms=x11,drm,wayland \
    --with-vulkan-drivers=intel \
    --with-gallium-drivers=
  make

  # fake installation
  mkdir $srcdir/fakeinstall
  make DESTDIR=${srcdir}/fakeinstall install
}

package() {
  cd "${srcdir}/anvil"
  
  install -m755 -d ${pkgdir}/usr/share
  mv -v ${srcdir}/fakeinstall/etc/vulkan ${pkgdir}/usr/share

  install -m755 -d ${pkgdir}/usr/{include/vulkan,lib}
  mv -v ${srcdir}/fakeinstall/usr/lib/libvulkan_intel.so ${pkgdir}/usr/lib/
  mv -v ${srcdir}/fakeinstall/usr/include/vulkan/vulkan_intel.h ${pkgdir}/usr/include/vulkan

  install -m755 -d "${pkgdir}"/usr/share/licenses/${pkgname}
  install -m644 "${srcdir}/LICENSE" "${pkgdir}"/usr/share/licenses/${pkgname}/
}
