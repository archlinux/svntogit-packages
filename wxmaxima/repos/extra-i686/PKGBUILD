# $Id: PKGBUILD,v 1.10 2008/05/13 19:03:39 ronald Exp $
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Angelo Theodorou <encelo@users.sourceforge.net>
# Contributor: Vinay S Shastry <vinayshastry@gmail.com>

pkgname=wxmaxima
pkgver=12.09.0
pkgrel=1
pkgdesc="A wxWidgets GUI for the computer algebra system Maxima"
arch=('i686' 'x86_64')
url="http://wxmaxima.sourceforge.net/"
license=('GPL2')
depends=('maxima' 'libxml2' 'wxgtk' 'shared-mime-info' 'desktop-file-utils')
source=(http://downloads.sourceforge.net/$pkgname/wxMaxima-$pkgver.tar.gz)
install=wxmaxima.install
sha1sums=('9b56f674392eabb75183b228757df8834b45b2a6')

build() {
  cd "${srcdir}/wxMaxima-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/wxMaxima-${pkgver}"

  make DESTDIR="${pkgdir}" install 

  # Fix category in .desktop file
  sed -i -e 's/Utility;X-Red-Hat-Base;X-Red-Hat-Base-Only;/Science;Math;/' wxmaxima.desktop

  # Install desktop file and icon
  install -m755 -d "${pkgdir}/usr/share/applications"
  install -m755 -d "${pkgdir}/usr/share/pixmaps"
  install -m644 wxmaxima.desktop "${pkgdir}/usr/share/applications/" 
  install -m644 data/wxmaxima.png "${pkgdir}/usr/share/pixmaps/" 
}
