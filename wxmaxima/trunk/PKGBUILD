# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Angelo Theodorou <encelo@users.sourceforge.net>
# Contributor: Vinay S Shastry <vinayshastry@gmail.com>

pkgname=wxmaxima
pkgver=21.05.2
pkgrel=1
pkgdesc="A wxWidgets GUI for the computer algebra system Maxima"
arch=('x86_64')
url="https://wxmaxima-developers.github.io/wxmaxima/"
license=('GPL2')
depends=('maxima' 'wxgtk3')
makedepends=('cmake')
source=($pkgname-$pkgver.tar.gz::"https://github.com/wxMaxima-developers/wxmaxima/archive/Version-${pkgver}.tar.gz")
sha256sums=('4d2d486a24090ace2f64ceccb026210e2e6299a32cb348d43134ef80440bcf01')

build() {
  cmake -B build -S ${pkgname}-Version-${pkgver} \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DwxWidgets_CONFIG_EXECUTABLE=/usr/bin/wx-config-gtk3
  cmake --build build
}

#check() {
#  cd ${pkgname}-Version-${pkgver}
#  ./src/wxmaxima test/testbench_simple.wxmx
#}

package() {
  DESTDIR="${pkgdir}" cmake --install build
}
