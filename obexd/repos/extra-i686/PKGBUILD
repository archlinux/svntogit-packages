# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Geoffroy Carrier <geoffroy@archlinux.org>

pkgbase=obexd
pkgname=('obexd-client' 'obexd-server')
pkgver=0.47
pkgrel=1
url="http://www.bluez.org/"
arch=('i686' 'x86_64')
license=('GPL2')
makedepends=('bluez' 'libical')
source=("http://www.kernel.org/pub/linux/bluetooth/${pkgbase}-${pkgver}.tar.bz2")
md5sums=('af721152055c40a226e374c2dd989901')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  ./configure --prefix=/usr \
    --libexecdir=/usr/lib/obexd
  make
}

package_obexd-client() {
  pkgdesc="D-Bus service providing high-level OBEX client side functionality"
  depends=('bluez')
  provides=("obexd=${pkgver}")
  replaces=('obexd')
  conflicts=('obexd')

  cd "${srcdir}/${pkgbase}-${pkgver}/client"
  install -m755 -d "${pkgdir}/usr/lib/obexd"
  install -m755 -d "${pkgdir}/usr/share/dbus-1/services"
  install -m755 obex-client "${pkgdir}/usr/lib/obexd/"
  install -m644 obex-client.service "${pkgdir}/usr/share/dbus-1/services/"
}

package_obexd-server() {
  pkgdesc="D-Bus service providing high-level OBEX server side functionality"
  depends=('bluez' 'libical')
  conflicts=('obex-data-server')

  cd "${srcdir}/${pkgbase}-${pkgver}/src"
  install -m755 -d "${pkgdir}/usr/lib/obexd"
  install -m755 -d "${pkgdir}/usr/share/dbus-1/services"
  install -m755 obexd "${pkgdir}/usr/lib/obexd/"
  install -m644 obexd.service "${pkgdir}/usr/share/dbus-1/services/"
}
