# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: AndyRTR <andyrtr@archlinux.org>
# Contributor: Ben <ben@benmazer.net>

pkgname=xfce4-fsguard-plugin
pkgver=1.1.0
pkgrel=2
pkgdesc="File system usage monitor plugin for the Xfce4 panel"
arch=('x86_64')
license=('GPL2')
url="http://goodies.xfce.org/projects/panel-plugins/xfce4-fsguard-plugin"
groups=('xfce4-goodies')
depends=('xfce4-panel')
makedepends=('intltool')
source=(https://archive.xfce.org/src/panel-plugins/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=('6b038322b49d29625219e6a54497085b8f834b9c1fd15282ce286a6529d7c445')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
