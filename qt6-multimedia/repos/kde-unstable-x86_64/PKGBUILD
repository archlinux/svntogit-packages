# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=qt6-multimedia
pkgname=(qt6-multimedia qt6-multimedia-gstreamer qt6-multimedia-ffmpeg)
_qtver=6.4.0-beta3
pkgver=${_qtver/-/}
pkgrel=1
arch=(x86_64)
url='https://www.qt.io'
license=(GPL3 LGPL3 FDL custom)
pkgdesc='Classes for audio, video, radio and camera functionality'
makedepends=(cmake ninja qt6-declarative qt6-shadertools gst-plugins-base libpulse ffmpeg qt6-quick3d)
groups=(qt6)
options=(debug)
_pkgfn=${pkgname/6-/}-everywhere-src-$_qtver
source=(https://download.qt.io/development_releases/qt/${pkgver%.*}/$_qtver/submodules/$_pkgfn.tar.xz
        qt6-multimedia-ffmpeg-5.1.patch::https://code.qt.io/cgit/qt/qtmultimedia.git/patch/?id=ddf562ea)
sha256sums=('512c01c96bdc3ab25a1a981b7e8af3d949b99d575f981afb0a1418405a42fae1'
            '20d7e0bc6a59c2f6bfccd980c9b5b713e864b744bea54f231c0057d4c06f2d4d')

prepare() {
  patch -d $_pkgfn -p1 < qt6-multimedia-ffmpeg-5.1.patch # Fix build with FFmpeg 5.1
}

build() {
  cmake -B build -S $_pkgfn -G Ninja \
    -DCMAKE_MESSAGE_LOG_LEVEL=STATUS
  cmake --build build
}

package_qt6-multimedia() {
  depends=(qt6-base qt6-multimedia-backend)
  optdepends=('qt6-declarative: QML bindings'
              'qt6-quick3d: for QtQuick3DAudio')

  DESTDIR="$pkgdir" cmake --install build
  rm -r "$pkgdir"/usr/lib/qt6/plugins/ # Split plugins

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt6-base "$pkgdir"/usr/share/licenses/$pkgname
}

package_qt6-multimedia-gstreamer() {
  pkgdesc='Gstreamer backend for qt6-multimedia'
  depends=(qt6-multimedia gst-plugins-base)
  provides=(qt6-multimedia-backend)
  optdepends=('gst-plugins-good: camera support, additional plugins'
              'gst-plugins-bad: camera support, additional plugins'
              'gst-plugins-ugly: additional plugins'
              'gst-libav: additional plugins')
  
  install -Dm755 build/lib/qt6/plugins/multimedia/libgstreamermediaplugin.so -t "$pkgdir"/usr/lib/qt6/plugins/multimedia
}

package_qt6-multimedia-ffmpeg() {
  pkgdesc='FFMpeg backend for qt6-multimedia'
  depends=(qt6-multimedia ffmpeg libva)
  provides=(qt6-multimedia-backend)

  install -Dm755 build/lib/qt6/plugins/multimedia/libffmpegmediaplugin.so -t "$pkgdir"/usr/lib/qt6/plugins/multimedia 
}
