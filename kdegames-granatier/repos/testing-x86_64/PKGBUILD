# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-granatier
pkgver=14.12.3
pkgrel=1
pkgdesc="A clone of the classic Bomberman game"
url="http://kde.org/applications/games/granatier/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'libkdegames')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/applications/${pkgver}/src/granatier-${pkgver}.tar.xz")
sha1sums=('441d82d25ec1aa61aaf23e15dd682f209c1bfe80')

build() {
  mkdir build
  cd build
  cmake ../granatier-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
