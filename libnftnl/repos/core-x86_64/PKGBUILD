# $Id$
# Maintainer: SÃ©bastien Luttringer

pkgname=libnftnl
pkgver=1.0.8
pkgrel=1
pkgdesc='Netfilter library providing interface to the nf_tables subsystem'
arch=('x86_64')
url='https://netfilter.org/projects/libnftnl/'
license=('GPL2')
depends=('libmnl')
validpgpkeys=('C09DB2063F1D7034BA6152ADAB4655A126D292E4') # Netfilter Core Team
# 2016-11-03: https sources download is broken with curl
source=("http://netfilter.org/projects/libnftnl/files/libnftnl-$pkgver.tar.bz2"{,.sig})
sha1sums=('8f9cb4983b54092478ade39f78b2850062729f4b'
          'SKIP')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
