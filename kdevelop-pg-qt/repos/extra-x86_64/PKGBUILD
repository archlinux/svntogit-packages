# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdevelop-pg-qt
pkgver=2.0
pkgrel=1
pkgdesc="KDevelop Parser Generator, a LL(1) parser generator used by KDevelop language plugins"
arch=(i686 x86_64)
url="http://www.kdevelop.org/"
license=(GPL)
depends=(qt5-base)
makedepends=(extra-cmake-modules)
source=("http://download.kde.org/stable/$pkgname/${pkgver}.0/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha1sums=('021cbe910468f4ae5169b8ff8933562c5e7dbe5f'
          'SKIP')
validpgpkeys=('329FD02C5AA48FCC77A4BBF0AC44AC6DB29779E6') # Sven Brauch <mail@svenbrauch.de>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
