# $Id: PKGBUILD 206624 2014-03-03 18:01:46Z andrea $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdevelop-pg-qt
pkgver=1.9.90git20151023
pkgrel=1
pkgdesc="KDevelop Parser Generator, a LL(1) parser generator used by KDevelop language plugins"
arch=(i686 x86_64)
url="http://www.kdevelop.org/"
license=(GPL)
depends=(qt5-base)
makedepends=(extra-cmake-modules)
source=("http://quickgit.kde.org/?p=kdevelop-pg-qt.git&a=snapshot&h=3776ab94101968656a4b15cc3faec1208fa054db&fmt=tgz")
sha1sums=('b186c2349c0d4cad3bc4acc6517f529f4ff55210')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
