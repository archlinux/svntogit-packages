# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdevelop-pg-qt
pkgver=2.2.2
pkgrel=1
pkgdesc='KDevelop Parser Generator, a LL(1) parser generator used by KDevelop language plugins'
arch=(x86_64)
url='https://www.kdevelop.org/'
license=(GPL)
depends=(qt5-base)
makedepends=(extra-cmake-modules)
source=(https://download.kde.org/stable/$pkgname/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('3d56604c479f8f04ae32a523ee91f3078c717117d0deb39e973e22494cbb65f1'
            'SKIP')
validpgpkeys=('329FD02C5AA48FCC77A4BBF0AC44AC6DB29779E6'  # Sven Brauch <mail@svenbrauch.de>
              '364EFA5F639572907D392999C64CF56B13CACE5D'  # Kevin Funk <kfunk@kde.org>
              '0A48BC961075B4BA8523E3790A345FB086E797D9'  # https://share.kde.org/s/8dKaJTw8pR8iiwJ
              'E6EBD6EB0518FE5BCCA5F6A76AACDD263C2CEFD4') # Friedrich W. H. Kossebau <kossebau@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
