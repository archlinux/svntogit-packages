# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Sarah Hay <sarah@archlinux.org>
# Contributor: Arjan Timmerman <arjan@soulfly.nl>

pkgname=perl-extutils-pkgconfig
pkgver=1.16
pkgrel=4
pkgdesc="The Perl Pkgconfig module"
arch=('any')
url="http://gtk2-perl.sourceforge.net/"
license=('LGPL')
depends=('perl')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/X/XA/XAOC/ExtUtils-PkgConfig-$pkgver.tar.gz")
md5sums=('b86318f2b6ac6af3ee985299e1e38fe5')

build() {
  cd ExtUtils-PkgConfig-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd ExtUtils-PkgConfig-${pkgver}
  make test
}

package() {
  cd ExtUtils-PkgConfig-${pkgver}
  make DESTDIR="${pkgdir}" install
}
