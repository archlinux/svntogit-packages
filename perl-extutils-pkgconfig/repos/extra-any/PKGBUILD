# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Sarah Hay <sarah@archlinux.org>
# Contributor: Arjan Timmerman <arjan@soulfly.nl>

pkgname=perl-extutils-pkgconfig
_realname=ExtUtils-PkgConfig
pkgver=1.14
pkgrel=1
pkgdesc="The Perl Pkgconfig module"
arch=(any)
license=('LGPL')
url="http://gtk2-perl.sourceforge.net/"
depends=('perl>=5.10.0')
replaces=('extutils-pkgconfig')
source=(http://downloads.sourceforge.net/sourceforge/gtk2-perl/${_realname}-${pkgver}.tar.gz)
md5sums=('80003c7a6a214e7712fbd9f9d9abe93a')
options=('!emptydirs')

build() {
  cd ${_realname}-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd ${_realname}-${pkgver}
  make test
}

package() {
  cd ${_realname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
