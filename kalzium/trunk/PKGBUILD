# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=kalzium
pkgver=19.12.2
pkgrel=1
pkgdesc="Periodic Table of Elements"
url="https://kde.org/applications/education/kalzium/"
arch=(x86_64)
license=(GPL LGPL FDL)
depends=(kparts knewstuff kplotting kunitconversion qt5-script avogadrolibs openbabel hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools eigen ocaml facile python)
groups=(kde-applications kdeedu)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
        kalzium-openbabel3.patch)
sha256sums=('ce40143bdf33a7896b9e17fd3172287b33f6f2486bb0ecce2392d16bf88f9590'
            'SKIP'
            '820550c6443cec572b28338899d2be871acc42fc02654a0df48a306b7cce09d5')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../kalzium-openbabel3.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DOPENBABEL2_INCLUDE_DIR=/usr/include/openbabel3 \
    -DOPENBABEL2_LIBRARIES=/usr/lib/libopenbabel.so \
    -DOPENBABEL2_VERSION_MET=TRUE
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
