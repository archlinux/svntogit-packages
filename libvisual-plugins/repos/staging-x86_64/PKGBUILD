# $Id$
# Maintainer: damir <damir@archlinux.org>

pkgname=libvisual-plugins
pkgver=0.4.0
pkgrel=9
pkgdesc="plugins for libvisual"
arch=("i686" "x86_64")
license=('GPL')
url="http://www.localhost.nl/~synap/libvisual/"
depends=('libvisual' 'gtk2' 'glu' 'alsa-lib' 'jack')
makedepends=('mesa')
source=(http://downloads.sourceforge.net/sourceforge/libvisual/libvisual-plugins-${pkgver}.tar.gz
        02_64-bit_JESS_fix.patch
        03_build_against_gl_fixes.patch
        04_lv_analyzer_build_fix.patch
        05_fix_po.patch
        050_all_automagic.patch
        60_no-const-vispluginfo-in-nastyfft.patch)
md5sums=('4330e9287f9d6fae02f482f428a1e77b'
         'f9cfb607bfcbfef60830fae4e7dc6963'
         '116701408747dbb87dc134434478ebe2'
         '01678a8f1584c76a44e59d81003a1109'
         '6189b7427c4e11c8b8d6c6266d6a1629'
         'b50ae94c424a5f0af235deffa8451eb6'
         '7012518727cb072a7e90a826a5577cc6')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/02_64-bit_JESS_fix.patch"
  patch -Np1 -i "${srcdir}/03_build_against_gl_fixes.patch"
  patch -Np1 -i "${srcdir}/04_lv_analyzer_build_fix.patch"
  patch -Np0 -i "${srcdir}/050_all_automagic.patch"
  patch -Np1 -i "${srcdir}/60_no-const-vispluginfo-in-nastyfft.patch"

  autoreconf -fi
  # Apply later as autoreconf overwrites po/Makefile.in.in
  patch -Np1 -i "${srcdir}/05_fix_po.patch"

  ./configure --prefix=/usr \
      --disable-gstreamer-plugin \
      --disable-gforce \
      --disable-esd
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
