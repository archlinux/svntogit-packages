# $Id$
# Contributor: György Balló <ballogy@freestart.hu>

pkgname=banshee
pkgver=2.2.1
pkgrel=1
pkgdesc="Music management and playback for GNOME"
arch=('i686' 'x86_64')
url="http://banshee.fm/"
license=('MIT')
depends=(libxxf86vm gstreamer0.10-base-plugins mono-addins notify-sharp-svn dbus-sharp-glib boo libwebkit libsoup-gnome gdata-sharp taglib-sharp gtk-sharp-beans 'gudev-sharp' 'gkeyfile-sharp' gconf-sharp libmtp libgpod mono-zeroconf desktop-file-utils hicolor-icon-theme xdg-utils media-player-info)
makedepends=('intltool' 'gnome-doc-utils')
optdepends=('gstreamer0.10-good-plugins: Extra media codecs'
            'gstreamer0.10-ugly-plugins: Extra media codecs'
            'gstreamer0.10-bad-plugins: Extra media codecs'
            'gstreamer0.10-ffmpeg: Extra media codecs'
            'brasero: CD burning')
options=('!libtool')
install=$pkgname.install
source=(http://download.gnome.org/sources/$pkgname/2.2/$pkgname-$pkgver.tar.xz
        banshee-recent-gpod.patch)
sha256sums=('55c03ddf9732199b9f83d4ff375af99ae5a4eea46b8a198695427e5c48f1b9a5'
            '018eeb00d1b4e625d13f1184863e499be5b5dde050a7513e012d5898125073d9')

build() {
  export MONO_SHARED_DIR="$srcdir/.wabi"
  mkdir -p "$MONO_SHARED_DIR"

  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -i "$srcdir/banshee-recent-gpod.patch"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-docs \
              --disable-static \
              --disable-scrollkeeper \
              --disable-schemas-install \
              --with-vendor-build-id=ArchLinux
  make
}

package() {
  export MONO_SHARED_DIR="$srcdir/.wabi"
  mkdir -p "$MONO_SHARED_DIR"

  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  install -D -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
