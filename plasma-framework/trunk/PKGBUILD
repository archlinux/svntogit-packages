# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=plasma-framework
pkgver=5.16.0
pkgrel=2
pkgdesc='Plasma library and runtime components based upon KF5 and Qt5'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/plasma-framework'
license=('LGPL')
depends=('qt5-quickcontrols' 'kactivities-frameworks')
makedepends=('extra-cmake-modules' 'qt5-tools' 'kdoctools')
groups=('kf5')
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"
	"bug355577.patch::https://quickgit.kde.org/?p=plasma-framework.git&a=commitdiff&h=9ccc62&o=plain")
md5sums=('bf64d612d33c73c51841d74540cc119f'
         '0494f166e36aec137472057120abcf3c')

prepare() {
  mkdir -p build

# Fix crash in SDDM https://bugs.kde.org/show_bug.cgi?id=355577
  cd $pkgname-$pkgver
  patch -p1 -i ../bug355577.patch
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DQML_INSTALL_DIR=lib/qt/qml \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
