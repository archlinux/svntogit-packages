# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Robert McCathie <archaur at rmcc dot com dot au>

pkgname=libwps
pkgver=0.4.13
pkgrel=1
pkgdesc="a Microsoft Works file word processor format import filter library"
arch=('x86_64')
url="https://sourceforge.net/projects/libwps/"
license=('LGPL')
depends=('libwpd' 'librevenge')
makedepends=('doxygen')
options=('debug')
source=("https://downloads.sourceforge.net/project/libwps/$pkgname/$pkgname-$pkgver/$pkgname-$pkgver.tar.xz")
sha1sums=('2221acf8e25a3487d3601230339508c8d5c59e59')
sha256sums=('ce95afe6c030689779a2543a4834827666eee27c10d8a74860d8d172a956c40f')

build() {
	cd $pkgname-$pkgver
	./configure  --prefix=/usr \
	  --disable-static
	make
}

check() {
	cd ${pkgname}-${pkgver}
	make check
}

package() {
	cd $pkgname-$pkgver
	make DESTDIR="$pkgdir" install
}
