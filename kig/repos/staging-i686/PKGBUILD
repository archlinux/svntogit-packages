# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kig
pkgver=17.04.1
pkgrel=2
pkgdesc="Interactive Geometry"
url="https://kde.org/applications/education/kig/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdeedu')
depends=('ktexteditor' 'boost-libs' 'python2' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'kdoctools' 'boost' 'qt5-xmlpatterns')
optdepends=('qt5-xmlpatterns: support for GeoGebra files')
replaces=('kdeedu-kig')
conflicts=('kdeedu-kig')
source=("https://download.kde.org/stable/applications/${pkgver}/src/kig-${pkgver}.tar.xz"{,.sig})
sha256sums=('129e67f3b203814502870549e87796c8576999a99062e6cf90808ee45fc3ade7'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install

  # Use the python2 executable
  sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|' "${pkgdir}"/usr/bin/pykig.py
}
