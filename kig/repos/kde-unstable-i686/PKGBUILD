# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kig
pkgver=15.11.80
pkgrel=2
pkgdesc="Interactive Geometry"
url="http://kde.org/applications/education/kig/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdeedu')
depends=('ktexteditor' 'boost-libs' 'python2' 'hicolor-icon-theme') # kemoticons, kitemmodels are only required for tests
makedepends=('extra-cmake-modules' 'kdoctools' 'boost' 'qt5-xmlpatterns')
optdepends=('qt5-xmlpatterns: support for GeoGebra files')
replaces=('kdeedu-kig')
conflicts=('kdeedu-kig')
install=${pkgname}.install
source=("http://download.kde.org/unstable/applications/${pkgver}/src/kig-${pkgver}.tar.xz")
sha1sums=('0f72f8b0fabbed65af6e03c0f0016a25944f6d8f')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kig-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBoostPython_INCLUDE_DIRS=/usr/include/python2.7 \
    -DBoostPython_LIBRARIES='-lboost_python -lpython2.7'
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install

  # Use the python2 executable
  sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|' "${pkgdir}"/usr/bin/pykig.py
}
