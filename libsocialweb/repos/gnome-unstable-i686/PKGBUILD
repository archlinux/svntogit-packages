# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=libsocialweb
pkgver=0.25.20
pkgrel=1
pkgdesc="A personal social data server"
arch=('i686' 'x86_64')
url="http://www.gtk.org/"
license=('GPL2')
depends=('glib2' 'gconf' 'libsoup-gnome' 'rest' 'dbus-glib' 'libgnome-keyring'
         'json-glib' 'networkmanager')
makedepends=('intltool' 'gtk-doc' 'vala' 'gobject-introspection')
options=('!libtool')
source=(http://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-${pkgver}.tar.xz)
sha256sums=('eb0078f44eb5fc4570fcee26c5670a433ca1bb83be0cead421a4a03e7d838ff5')

build() {
  cd $pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/libsocialweb \
    --enable-all-services=yes \
    --with-online=networkmanager \
    --enable-vala-bindings
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
