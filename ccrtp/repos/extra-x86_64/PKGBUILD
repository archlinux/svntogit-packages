# $Id$
# Maintainer:
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jeff Mickey <jeff@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=ccrtp
pkgver=2.0.3
pkgrel=2
pkgdesc="An implementation of RTP, the real-time transport protocol from the IETF"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/ccrtp/"
license=('GPL' 'custom')
depends=('ucommon' 'libgcrypt')
options=('!libtool')
install=$pkgname.install
source=("ftp://ftp.gnu.org/pub/gnu/$pkgname/$pkgname-$pkgver.tar.gz"{,.sig})
md5sums=('0d57da1b8e212fb634b8951067fbc1e0'
         '127fa3b293f7a7c3be1d2891c4544453')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -Dm644 COPYING.addendum "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
