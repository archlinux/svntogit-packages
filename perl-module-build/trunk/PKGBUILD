# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor : Alan Young <harleypig@gmail.com>

pkgname=perl-module-build
pkgver=0.4205
pkgrel=1
pkgdesc="Build, test, and install Perl modules"
arch=('any')
url="http://search.cpan.org/dist/Module-Build"
license=('PerlArtistic' 'GPL')
depends=('perl')
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/L/LE/LEONT/Module-Build-$pkgver.tar.gz)
md5sums=('bfc8504e845131625308bd4389c69fbe')

build() {
  cd Module-Build-$pkgver
  perl Build.PL installdirs=vendor
  perl Build
}

check() {
  cd Module-Build-$pkgver
  perl Build test
}

package() {
  cd Module-Build-$pkgver
  perl Build install destdir="$pkgdir"
}
