# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgbase=kdebindings-kross
pkgname=('kdebindings-kross-python'
#         'kdebindings-kross-ruby'
         'kdebindings-kross-java')
pkgver=4.6.90
pkgrel=1
url="http://kde.org/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdebindings')
makedepends=('kdelibs' 'cmake' 'automoc4' 'python2' 'openjdk6')
source=("http://download.kde.org/unstable/${pkgver}/src/kross-interpreters-${pkgver}.tar.bz2")
sha1sums=('db15d0957951a56bdb8c3727dc68cbc207b0e4b4')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../kross-interpreters-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_ruby=OFF
  make
}

package_kdebindings-kross-python() {
  pkgdesc="Python2 language interpreters to enable in-process scripting with Kross"
  depends=('kdelibs' 'python2')

  cd "${srcdir}"/build/python
  make DESTDIR="${pkgdir}" install
}

package_kdebindings-kross-java() {
  pkgdesc="Java language interpreters to enable in-process scripting with Kross"
  depends=('kdelibs' 'openjdk6')

  cd "${srcdir}"/build/java
  make DESTDIR="${pkgdir}" install
}

package_kdebindings-kross-ruby() {
  pkgdesc="Ruby language interpreters to enable in-process scripting with Kross"
  depends=('kdelibs' 'ruby')

  cd "${srcdir}"/build/ruby
  make DESTDIR="${pkgdir}" install
}
