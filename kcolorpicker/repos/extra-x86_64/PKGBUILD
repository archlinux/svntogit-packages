# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: dracorp aka Piotr Rogoza <piotr.r.public at gmail.com>

pkgname=kcolorpicker
pkgver=0.1.6
pkgrel=1
pkgdesc='Qt based Color Picker with popup menu'
arch=(x86_64)
url='https://github.com/DamirPorobic/kColorPicker'
license=(GPL)
makedepends=(cmake)
depends=(qt5-base)
source=(https://github.com/ksnip/kColorPicker/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('a167b081d444c54057f587782b81bf0f057b50e34e8a2d074e88007387007e91')

build() {
  cmake -B build -S kColorPicker-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_SHARED_LIBS=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
