# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeutils-kremotecontrol
pkgver=15.04.0
pkgrel=2
pkgdesc='Configure your remote controls for use with applications'
url='http://kde.org/applications/utilities/kremotecontrol/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
groups=('kde-applications' 'kdeutils')
replaces=('kdeutils-kdelirc')
conflicts=('kdeutils-kdelirc')
install=${pkgname}.install
source=("http://download.kde.org/stable/applications/${pkgver}/src/kremotecontrol-${pkgver}.tar.xz")
sha1sums=('2ec946acb6b2f5845031595e848e758ddc40ffee')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../kremotecontrol-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $srcdir/build
  make DESTDIR=$pkgdir install
}
