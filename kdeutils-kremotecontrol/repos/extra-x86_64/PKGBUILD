# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeutils-kremotecontrol
pkgver=4.8.0
pkgrel=1
pkgdesc='Configure your remote controls for use with applications'
url='http://kde.org/applications/utilities/kremotecontrol/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdebase-workspace')
makedepends=('cmake' 'automoc4')
groups=('kde' 'kdeutils')
replaces=('kdeutils-kdelirc')
conflicts=('kdeutils-kdelirc')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/xz/kremotecontrol-${pkgver}.tar.xz")
sha1sums=('6a4d7eede9736aa14213db082ceec5a2132c89c5')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../kremotecontrol-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $srcdir/build
  make DESTDIR=$pkgdir install
  cd $srcdir/build/doc
  make DESTDIR=$pkgdir install
}
