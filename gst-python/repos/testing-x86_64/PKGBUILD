# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgbase=gst-python
pkgname=('gst-python' 'gst-python2')
pkgver=1.8.2
pkgrel=1
pkgdesc="GStreamer Python binding overrides"
arch=('i686' 'x86_64')
license=('LGPL')
makedepends=('pkg-config' 'python-gobject' 'python2-gobject' 'gst-plugins-base')
url="http://gstreamer.freedesktop.org/"
source=(${url}/src/$pkgbase/$pkgbase-${pkgver}.tar.xz)
sha256sums=('4f5015239482014c16f435559a45e6c87930340b65ee04e2e6fa164c8d724d97')

prepare() {
  mkdir py2 py3
}

_build() (
  cd $1
  # Out-of-tree build fails to check
  cp -a ../$pkgbase-$pkgver/* .
  ./configure --prefix=/usr
  make
)

build() {
  _build py3
  PYTHON=python2 _build py2
}

check() {
  make -C py3 check
  make -C py2 check
}

package_gst-python() {
  pkgdesc="${pkgdesc//Python/Python 3}"
  depends=('python-gobject' 'gst-plugins-base')

  make -C py3 DESTDIR="$pkgdir" install
}

package_gst-python2() {
  pkgdesc="${pkgdesc//Python/Python 2}"
  depends=('python2-gobject' 'gst-plugins-base')

  make -C py2 DESTDIR="$pkgdir" install
}
