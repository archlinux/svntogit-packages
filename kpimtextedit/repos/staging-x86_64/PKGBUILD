# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kpimtextedit
pkgver=16.03.80
pkgrel=1
pkgdesc="A textedit with PIM-specific features"
arch=(i686 x86_64)
url="https://projects.kde.org/projects/kde/pim/$_gitname"
license=(LGPL)
depends=(kio kemoticons grantlee-qt5)
makedepends=(extra-cmake-modules python kdoctools kdesignerplugin)
source=("http://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
md5sums=('e993fe4c5350dc87bc95ca5ede3415b9')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
