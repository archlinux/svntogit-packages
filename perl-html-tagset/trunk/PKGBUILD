# Maintainer: Felix Yan <felixonmars@gmail.com>
# Conributor: kevin <kevin@archlinux.org>
# Contributor: Manolis Tzanidakis

pkgname=perl-html-tagset
_realname=HTML-Tagset
pkgver=3.20
pkgrel=7
pkgdesc="Data tables useful in parsing HTML"
arch=('any')
license=('PerlArtistic')
url="http://search.cpan.org/dist/${_realname}/"
depends=('perl>=5.10.0')
replaces=('html-tagset')
provides=('html-tagset')
options=(!emptydirs)
source=(http://www.cpan.org/authors/id/P/PE/PETDANCE/${_realname}-${pkgver}.tar.gz)
md5sums=('d2bfa18fe1904df7f683e96611e87437')

build() {
  cd ${srcdir}/${_realname}-${pkgver}
  # install module in vendor directories.
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd ${srcdir}/${_realname}-${pkgver}
  make test
}

package() {
  cd ${srcdir}/${_realname}-${pkgver}
  make install DESTDIR=${pkgdir}
}

# vim: ts=2 sw=2 et ft=sh
