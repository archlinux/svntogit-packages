# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org> 

pkgname=kaccounts-integration
pkgver=15.08.3
pkgrel=3
pkgdesc='Small system to administer web accounts for the sites and services across the KDE desktop, including: Google, Facebook, Owncloud, IMAP, 
Jabber and others'
arch=(i686 x86_64)
url='https://projects.kde.org/projects/kdereview/kaccounts-integration'
license=(GPL)
groups=(kde-applications kdenetwork)
depends=(kcmutils signon-kwallet-extension signon-plugin-oauth2 signon-ui)
makedepends=(extra-cmake-modules kdoctools)
source=("http://download.kde.org/stable/applications/${pkgver}/src/$pkgname-${pkgver}.tar.xz"
	bug354949.patch::"https://quickgit.kde.org/?p=kaccounts-integration.git&a=commitdiff&h=fe6b9c&o=plain")
md5sums=('ffefdb1009cfc113b38f860baf9f2f29'
         '7298b99ffab3231ba048390ef9614c72')

prepare() {
  mkdir -p build

# Fix accounts creation http://bugs.kde.org/show_bug.cgi?id=354949
  cd $pkgname-$pkgver
  patch -p1 -i ../bug354949.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
