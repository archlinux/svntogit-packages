# $Id$
# Maintainer: aurelien <aurelien@archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=terminal
pkgver=0.2.8
pkgrel=2
pkgdesc="A modern terminal emulator primarly for the Xfce desktop environment"
arch=(i686 x86_64)
license=('GPL2')
url="http://www.xfce.org/projects/terminal/"
groups=('xfce4')
depends=('exo>=0.3.4' 'vte>=0.16.14' 'dbus-glib>=0.76'
	 'startup-notification>=0.9' 'hicolor-icon-theme')
makedepends=('pkgconfig')
options=('!libtool')
install=terminal.install
source=(http://www.xfce.org/archive/xfce-4.4.2/src/Terminal-${pkgver}.tar.bz2
	terminal-dont-die-on-dbus-kill.patch)
md5sums=('cfe660ecf50e9d3b073576bbc4af6ab2' '7cff5c513739737a0ed2109354538241')

build() {
  cd ${srcdir}/Terminal-${pkgver}
  patch -Np0 -i ${srcdir}/terminal-dont-die-on-dbus-kill.patch || return 1
  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib/xfce4 \
    --localstatedir=/var --disable-static || return 1
  make || return 1
  make DESTDIR=${pkgdir} install || return 1
}
