# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Antonio Rojas

pkgbase=breeze
pkgname=('breeze' 'breeze-icons' 'breeze-kde4')
pkgver=5.4.2
pkgrel=2
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/workspace/breeze'
license=('LGPL')
makedepends=('extra-cmake-modules' 'frameworkintegration' 'kdelibs'
             'automoc4' 'kdecoration' 'kcmutils')
source=("http://download.kde.org/stable/plasma/${pkgver}/$pkgbase-$pkgver.tar.xz"
	missing-dark-icons.patch::"https://quickgit.kde.org/?p=breeze.git&a=commitdiff&h=12d8b9a9&o=plain")
md5sums=('0e1d48eba3d0261023954b46fe7af36e'
         '7e0b852fb238a59c411f29eff66c014b')

prepare() {
  mkdir build{,-kde4}

  cd $pkgbase-$pkgver
# Fix missing breeze-dark mimetype icons
  patch -p1 -i ../missing-dark-icons.patch
}

build() {
  cd build
  cmake ../${pkgbase}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make

  cd ../build-kde4
  cmake ../${pkgbase}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DUSE_KDE4=ON \
    -DBUILD_TESTING=OFF
  make
}

package_breeze() {
  depends=('frameworkintegration' 'kdecoration' 'breeze-icons')
  pkgdesc='Artwork, styles and assets for the Breeze visual style for the Plasma Desktop'
  optdepends=('breeze-kde4: Breeze widget style for KDE4 applications'
		'gtk-theme-orion: GTK theme matching Breeze'
		'kcmutils: for breeze-settings')
  groups=('plasma')

  cd build
  make DESTDIR="$pkgdir" install

  # split off icons
  rm -r "$pkgdir"/usr/share/icons/{breeze,breeze-dark}

  # make QtCurve preset and color scheme available for KDE4 too
  install -d "$pkgdir"/usr/share/apps/{QtCurve,color-schemes}
  ln -s /usr/share/QtCurve/Breeze.qtcurve "$pkgdir"/usr/share/apps/QtCurve/
  ln -s /usr/share/color-schemes/Breeze{,Dark}.colors "$pkgdir"/usr/share/apps/color-schemes/
}

package_breeze-icons() {
  pkgdesc='Breeze icon themes for KDE Plasma'
  groups=('plasma')
  
  cd build/icons
  make DESTDIR="$pkgdir" install

  cd ../icons-dark
  make DESTDIR="$pkgdir" install
}

package_breeze-kde4() {
  pkgdesc='Breeze widget style for KDE4 applications'
  depends=('kdelibs')

  cd build-kde4
  make DESTDIR="$pkgdir" install

  # needed for pure Qt4 apps
  install -d "$pkgdir"/usr/lib/qt4/plugins/styles
  ln -s /usr/lib/kde4/plugins/styles/breeze.so "$pkgdir"/usr/lib/qt4/plugins/styles
}
