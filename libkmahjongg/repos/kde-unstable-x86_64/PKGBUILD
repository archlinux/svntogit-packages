# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=libkmahjongg
pkgver=20.11.90
pkgrel=1
pkgdesc="Common code, backgrounds and tile sets for games using Mahjongg tiles"
url="https://www.kde.org/applications/games/"
arch=(x86_64)
license=(GPL LGPL FDL)
depends=(kconfigwidgets kcompletion qt5-svg)
makedepends=(extra-cmake-modules)
source=("https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('f34ec8647e0b207cd60f0fdbd195258e57d640446748de6d07f0f01c24e3f27d'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
