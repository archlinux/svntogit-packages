# $Id$
# Maintainer: Judd Vinet <jvinet@zeroflux.org>

pkgname=mod_python
pkgver=3.3.1
pkgrel=6
pkgdesc="An Apache module that embeds the Python interpreter within the server"
arch=('i686' 'x86_64')
url="http://www.modpython.org/"
license=('APACHE')
depends=('apache' 'python2')
install=mod_python.install
source=(http://www.apache.org/dist/httpd/modpython/mod_python-${pkgver}.tgz
	compile.patch)
md5sums=('a3b0150176b726bd2833dac3a7837dc5' '4786c158223c2d3a94f3b6e69826b522')
sha1sums=('e538170fd78e09408b6d8593da980b126a0fef93' 'ac69bb68a1c4bf804e9ad716e458774d404f8d78')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np0 -i ../compile.patch
  autoreconf
  ./configure --prefix=/usr --with-python=/usr/bin/python2
  sed -i -e 's/ -Wl,--hash-style  $//' src/Makefile
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
