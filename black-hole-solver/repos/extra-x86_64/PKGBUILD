# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=black-hole-solver
pkgver=1.2.0
pkgrel=1
pkgdesc="Solvers and statistics for 'Golf' solitaire, 'Black Hole' solitaire, 'All in a Row' solitaire and related card patience games."
arch=(x86_64)
url="https://www.shlomifish.org/open-source/projects/black-hole-solitaire-solver/"
license=(MIT)
depends=(glibc)
makedepends=(cmake perl-path-tiny)
source=(http://fc-solve.shlomifish.org/downloads/fc-solve/black-hole-solver-$pkgver.tar.xz)
sha256sums=('dad2801cf7da0c75e29fbb186a3540f3b9863af85e1d6ab2d2d33e51f6266d17')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_STATIC_LIBRARY=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir"/$pkgname-$pkgver/COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

