# $Id$
# Maintaier: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=libkolab
pkgver=1.0.1
pkgrel=1
pkgdesc="Advanced Kolab Object Handling Library"
url='http://git.kolab.org/libkolab/'
arch=(i686 x86_64)
license=(GPL)
depends=(libkolabxml akonadi-notes akonadi-client kcontacts kcalutils)
makedepends=(extra-cmake-modules kdoctools boost git)
#source=("http://mirror.kolabsys.com/pub/releases/$pkgname-$pkgver.tar.gz"{,.gpg} 'fix-build.patch')
source=("git+https://git.kolab.org/diffusion/LK/libkolab.git#tag=libkolab-1.0.1" 'fix-build.patch')
md5sums=('SKIP'
         '46198b207b39d9f067812ce10114ab19')

prepare() {
  mkdir -p build

# Fix build against kcontacts 15.08
  cd $pkgname
  patch -p1 -i "$srcdir"/fix-build.patch
}

build() {
  cd build
  cmake ../$pkgname \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
