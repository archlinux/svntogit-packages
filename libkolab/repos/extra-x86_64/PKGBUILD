# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=libkolab
pkgver=0.5.3
pkgrel=1
pkgdesc="Advanced Kolab Object Handling Library"
url='http://git.kolab.org/libkolab/'
arch=('i686' 'x86_64')
license=('GPL')
depends=('libkolabxml' 'kdepimlibs')
makedepends=('cmake' 'automoc4' 'boost')
source=("http://mirror.kolabsys.com/pub/releases/${pkgname}-${pkgver}.tar.gz"{,.gpg})
md5sums=('c42b2c8cbd6de81137c3cf5b5e5a4584'
         'b061e4dd0e94880044025a2ef244b16f')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
