# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=telepathy-qt4
pkgver=0.7.1
pkgrel=1
pkgdesc="A library for Qt-based Telepathy clients"
arch=('i686' 'x86_64')
url="http://telepathy.freedesktop.org/wiki/"
license=('LGPL')
options=('!libtool')
depends=('qt' 'telepathy-farsight')
makedepends=('libxslt' 'python2' 'cmake')
source=("http://telepathy.freedesktop.org/releases/${pkgname}/${pkgname}-${pkgver}.tar.gz")
md5sums=('a609c6e9638e8cf24134b2165fec1c8a')

build() {
    cd "${srcdir}"
    mkdir build
    cd build
    cmake ../${pkgname}-${pkgver} \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr
    make
}

package() {
    cd "${srcdir}"/build
    make DESTDIR="${pkgdir}" install
}
