# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
pkgname=telepathy-qt4
pkgver=0.4.2
pkgrel=1
pkgdesc="A library for Qt-based Telepathy clients."
arch=('i686' 'x86_64')
url="http://telepathy.freedesktop.org"
groups=('telepathy')
license=('LGPL')
options=('!libtool')
depends=('qt' 'telepathy-farsight')
makedepends=('libxslt' 'python2' 'cmake')
source=(http://telepathy.freedesktop.org/releases/$pkgname/$pkgname-$pkgver.tar.gz)
md5sums=('d014e49b11fcbb4dc87aa1757d209a3a')

build() {
    cd "${srcdir}"

    mkdir build
    cd build
    cmake ../${pkgname}-${pkgver} \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DHAVE_QDBUSVARIANT_OPERATOR_EQUAL=1
    make
}

package() {
    cd "${srcdir}"/build
    make DESTDIR="$pkgdir" install
}
