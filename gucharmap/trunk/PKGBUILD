# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan De Groot <jgc@archlinux.org>

pkgname=gucharmap
pkgver=10.0.0
pkgrel=1
pkgdesc="Gnome Unicode Charmap"
url="https://wiki.gnome.org/Apps/Gucharmap"
arch=(i686 x86_64)
license=(GPL)
depends=(dconf gtk3)
makedepends=(gtk-doc intltool gobject-introspection itstool docbook-xsl
             python2 appdata-tools unicode-character-database unzip gnome-common git)
groups=(gnome)
_commit=c2f8d745654f59d9f2712dbaef5439d096f52364  # tags/10.0.0^0
source=("git+https://git.gnome.org/browse/gucharmap#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --disable-static \
    --disable-schemas-compile --enable-introspection --with-unicode-data=/usr/share/unicode
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
