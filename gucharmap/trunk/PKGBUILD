# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan De Groot <jgc@archlinux.org>

pkgname=gucharmap
pkgver=3.9.92
pkgrel=1
pkgdesc="Gnome Unicode Charmap"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnome.org"
depends=('dconf' 'gtk3' 'hicolor-icon-theme')
makedepends=('gtk-doc' 'intltool' 'gobject-introspection' 'itstool' 'docbook-xsl' 'desktop-file-utils')
options=('!libtool' '!emptydirs')
groups=('gnome')
install=gucharmap.install
source=(http://download.gnome.org/sources/$pkgname/${pkgver:0:3}/$pkgname-$pkgver.tar.xz)
sha256sums=('2ff6e6b33e447d977fd4b00b1a76047d300d657c934ce3f86680f2c8912cccbf')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --disable-schemas-compile  --enable-introspection
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
