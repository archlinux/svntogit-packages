# $Id$
# Maintainer: Jan De Groot <jgc@archlinux.org>

pkgname=gucharmap
pkgver=2.28.1
pkgrel=1
pkgdesc="Gnome Unicode Charmap"
arch=(i686 x86_64)
license=('GPL')
url="http://www.gnome.org"
depends=('gconf>=2.28.0' 'hicolor-icon-theme')
makedepends=('gnome-doc-utils>=0.18.0' 'pkgconfig' 'intltool')
groups=('gnome-extra')
options=('!libtool' '!emptydirs')
install=gucharmap.install
source=(http://ftp.gnome.org/pub/gnome/sources/gucharmap/2.28/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('91e909a1223b60a466ca2001b41de43ca7e26104d7f4da6422ecbdc07970eee3')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --disable-scrollkeeper || return 1
  make || return 1
  make -j1 GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1
 
  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gucharmap ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
