# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan De Groot <jgc@archlinux.org>

pkgname=gucharmap
pkgver=3.12.0
pkgrel=1
pkgdesc="Gnome Unicode Charmap"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnome.org"
depends=('dconf' 'gtk3' 'hicolor-icon-theme')
makedepends=('gtk-doc' 'intltool' 'gobject-introspection' 'itstool' 'docbook-xsl' 'desktop-file-utils')
options=('!emptydirs')
groups=(gnome)
install=gucharmap.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('902f02e741d7eebf919e8c6bb8936f7eafe88b4c556874c9121346c40dc55c0f')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --disable-schemas-compile  --enable-introspection
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
