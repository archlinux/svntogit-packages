# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=plasma-firewall
pkgver=5.25.2
pkgrel=1
pkgdesc='Control Panel for your system firewall'
arch=(x86_64)
url='https://kde.org/plasma-desktop/'
license=(GPL LGPL)
source=(https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz{,.sig})
depends=(systemsettings)
makedepends=(extra-cmake-modules)
optdepends=('iproute2: netstat backend'
            'firewalld: firewalld backend'
            'ufw: ufw backend')
groups=(plasma)
sha256sums=('cc3092f783f76d31a70df967c741d2cfefa7d71f7d1ecff8838fb5432df1913d'
            'SKIP')
validpgpkeys=('E0A3EB202F8E57528E13E72FD7574483BB57B18D'  # Jonathan Esk-Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>
options=(debug)

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DLIBEXEC_INSTALL_DIR=lib
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
