# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=plasma-firewall
pkgver=5.21.1
pkgrel=1
pkgdesc="Control Panel for your system firewall"
arch=(x86_64)
url='https://kde.org/plasma-desktop/'
license=(GPL LGPL)
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig})
depends=(systemsettings plasma-framework)
makedepends=(extra-cmake-modules)
optdepends=('iproute2: netstat backend'
            'firewalld: firewalld backend'
            'ufw: ufw backend')
groups=(plasma)
sha256sums=('0b06c722d040031c6587f2d2ee24f7c0470078642ae73b5ad1efc81d6ae207a7'
            'SKIP')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DLIBEXEC_INSTALL_DIR=lib
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
