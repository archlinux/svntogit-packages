# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=swell-foop
pkgver=41.1
pkgrel=1
pkgdesc="Clear the screen by removing groups of colored and shaped tiles"
url="https://wiki.gnome.org/Apps/Swell%20Foop"
arch=(x86_64)
license=(GPL)
depends=(clutter-gtk librsvg libgnome-games-support libgee)
makedepends=(yelp-tools appstream-glib vala git meson)
groups=(gnome-extra)
options=(debug)
_commit=f89d9cfe7c4b73ae37cf5ed0a6bdc9060365e55d  # tags/41.1^0
source=("git+https://gitlab.gnome.org/GNOME/swell-foop.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
