# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=swell-foop
pkgver=3.26.0+8+g4ff76c8
pkgrel=1
pkgdesc="Clear the screen by removing groups of colored and shaped tiles"
url="https://wiki.gnome.org/Apps/Swell%20Foop"
arch=(x86_64)
license=(GPL)
depends=(clutter-gtk librsvg)
makedepends=(intltool itstool gobject-introspection yelp-tools appstream-glib vala git)
groups=(gnome-extra)
_commit=4ff76c8035c0dfc16d465a4ec46834163d501a3b  # master
source=("git://git.gnome.org/swell-foop#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --disable-schemas-compile
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
