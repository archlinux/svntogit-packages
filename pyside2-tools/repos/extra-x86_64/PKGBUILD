# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=pyside2-tools
_qtver=5.13.1
pkgver=${_qtver/-/}
pkgrel=1
arch=(x86_64)
url='https://www.qt.io'
license=(LGPL)
pkgdesc='lupdate, rcc and uic tools for PySide2'
depends=(pyside2)
makedepends=(cmake shiboken2)
_pkgfqn=pyside-setup-everywhere-src-${_qtver}
source=("https://download.qt.io/official_releases/QtForPython/pyside2/PySide2-$pkgver-src/${_pkgfqn}.tar.xz")
sha256sums=('c57c71ddece90b29d0934cf5fac1e1634719484637b56dcbe49eb277c1c01536')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${_pkgfqn}/sources/pyside2-tools \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTS=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
