# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=pyside2-tools
_qtver=5.11.0
pkgver=${_qtver/-/}
pkgrel=3
arch=(x86_64)
url='http://qt-project.org/'
license=(LGPL)
pkgdesc='lupdate, rcc and uic tools for PySide2'
depends=(python-pyside2)
makedepends=(cmake shiboken2 pyside2)
_pkgfqn=pyside-setup-everywhere-src-${_qtver}
source=("http://download.qt.io/official_releases/QtForPython/pyside2/PySide2-$pkgver-src/${_pkgfqn}.tar.xz")
sha256sums=('fbc412c4544bca308291a08a5173a949ca530d801f00b8337902a5067e490922')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${_pkgfqn}/sources/pyside2-tools \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTS=OFF \
    -DUSE_PYTHON_VERSION=3
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
