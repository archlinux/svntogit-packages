# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=pyside2-tools
_qtver=5.12.2
pkgver=${_qtver/-/}
pkgrel=2
arch=(x86_64)
url='https://www.qt.io'
license=(LGPL)
pkgdesc='lupdate, rcc and uic tools for PySide2'
depends=(pyside2)
makedepends=(cmake shiboken2)
_pkgfqn=pyside-setup-everywhere-src-${_qtver}
source=("https://download.qt.io/official_releases/QtForPython/pyside2/PySide2-$pkgver-src/${_pkgfqn}.tar.xz")
sha256sums=('ed974c0592019cbbcd4e4db3b18cf4f2af2c399cc1650e5c526be3efd7562bc1')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${_pkgfqn}/sources/pyside2-tools \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTS=OFF \
    -DCMAKE_BUILD_TYPE=None # https://bugreports.qt.io/browse/PYSIDE-980
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
