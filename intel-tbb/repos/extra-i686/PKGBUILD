# $Id$
# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Denis Martinez <deuns.martinez AT gmail.com>

pkgname=intel-tbb
pkgver=4.4_20160526
_tag=tbb${pkgver/\./}oss
_file=${_tag}_src_0.tgz
pkgrel=2
pkgdesc='High level abstract threading library'
arch=(i686 x86_64)
url='http://www.threadingbuildingblocks.org/'
license=(GPL)
depends=(gcc-libs)
source=(http://threadingbuildingblocks.org/sites/default/files/software_releases/source/$_file)
sha1sums=('d6737ce2aa16e1d6729bb15ae067b139d0842533')

build() {
  cd $_tag
  export CXXFLAGS+=" -fno-lifetime-dse" # FS#49898
  make
}

package() {
  cd $_tag
  install -d "$pkgdir"/usr/lib
  install -m755 build/linux_*/*.so* "$pkgdir"/usr/lib
  install -d "$pkgdir"/usr/include
  cp -a include/tbb "$pkgdir"/usr/include
}
