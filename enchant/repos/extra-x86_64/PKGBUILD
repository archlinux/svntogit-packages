# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=enchant
pkgver=2.1.2
pkgrel=3
pkgdesc="A wrapper library for generic spell checking"
arch=('x86_64')
url="https://abiword.github.io/enchant/"
license=('LGPL')
depends=('aspell' 'hunspell' 'hspell' 'libvoikko' 'glib2')
makedepends=('git')
_commit=9889e6350eb645feca2b0c061f719ca4188984d5  # tags/v2.1.2
source=("git+https://github.com/AbiWord/enchant.git#commit=$_commit"
        0001-voikko-Avoid-crashing-when-there-are-no-languages.patch
        hspell-fix-nullpointer-dereference.patch)
sha256sums=('SKIP'
            'b30f9ff749a050515a4eb39686b2e3e5e7f2bc84eff26b3611fe3885785166ec'
            '5bdd03739ce609bf718b158cd90a5f373a7d12db44f70e5bfa6f4becb5ee056e')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/\./g'
}

prepare() {
  cd $pkgname

  # https://github.com/AbiWord/enchant/pull/161
  patch -Np1 -i ../0001-voikko-Avoid-crashing-when-there-are-no-languages.patch

  # https://github.com/AbiWord/enchant/issues/170
  patch -Np1 -i ../hspell-fix-nullpointer-dereference.patch

  ./bootstrap
}

build() {
  cd $pkgname
  ./configure --prefix=/usr \
    --disable-static \
    --disable-ispell \
    --with-myspell-dir=/usr/share/myspell
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname
  make DESTDIR="${pkgdir}" install
}
