# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Tobias Kieslich <tobias (at) archlinux.org>

pkgname=thunar-archive-plugin
pkgver=0.4.0
pkgrel=1
pkgdesc="Create and extract archives in Thunar"
arch=('x86_64')
url="http://goodies.xfce.org/projects/thunar-plugins/thunar-archive-plugin"
license=('GPL2')
groups=('xfce4-goodies')
depends=('thunar' 'hicolor-icon-theme')
makedepends=('intltool' 'xfce4-dev-tools')
optdepends=('file-roller'
            'ark'
            'xarchiver')
source=(https://archive.xfce.org/src/thunar-plugins/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=('bf82fa86a388124eb3c4854249c30712b2922e61789607268ee14548549b3115')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/xfce4 \
    --localstatedir=/var \
    --disable-static
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
