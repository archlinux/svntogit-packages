# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
# Contributor: György Balló <ballogy@freestart.hu>
pkgname=flickrnet
_pkgname=FlickrNet
pkgver=2.2
_pkgver=48055
pkgrel=1
pkgdesc="Flickr.Net API Library"
arch=('any')
url="http://www.codeplex.com/FlickrNet"
license=('LGPL')
depends=('mono')
source=($pkgname-$pkgver.zip::"http://download.codeplex.com/Project/Download/SourceControlFileDownload.ashx?ProjectName=${_pkgname}&changeSetId=$_pkgver&ext=.zip"
        "$pkgname.pc")
md5sums=('9ad3fde83ca2a74a4cade2f1b11a77e1'
         '131dd6e01f99c0533e9367b5cfda3a13')

build() {
  cd $srcdir/$_pkgname
  sed -i 's/AssemblyKeyFile("..\\\\..\\\\FlickrNet.snk")/AssemblyKeyFile("FlickrNet.snk")/' ./AssemblyInfo.cs
  sed -i "s/@VERSION@/$pkgver/" $srcdir/$pkgname.pc

  gmcs -target:library -out:FlickrNet.dll -r:System.Web.dll -r:System.Drawing.dll *.cs
}

package() {
  cd $srcdir/$_pkgname

  install -Dm644 $_pkgname.dll $pkgdir/usr/lib/mono/$pkgname-$pkgver/$_pkgname.dll
  install -Dm644 $srcdir/$pkgname.pc $pkgdir/usr/lib/pkgconfig/$pkgname.pc
}
