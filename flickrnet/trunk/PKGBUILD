# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
# Contributor: György Balló <ballogy@freestart.hu>
pkgname=flickrnet
pkgver=3.7.0.0
_pkgver=103861
pkgrel=1
pkgdesc="Flickr.Net API Library"
arch=('any')
url="http://www.codeplex.com/FlickrNet"
license=('LGPL')
depends=('mono')
makedepends=('unzip')
source=($pkgname-$pkgver.zip::"http://download.codeplex.com/Project/Download/SourceControlFileDownload.ashx?ProjectName=FlickrNet&changeSetId=$_pkgver"
        "$pkgname.pc")
md5sums=('b2002b0764725ffbd33f879e1d475d4e'
         '131dd6e01f99c0533e9367b5cfda3a13')

prepare() {
  cd "$srcdir/FlickrNet"

  sed -i "s/@VERSION@/$pkgver/" "$srcdir/$pkgname.pc"
}

build() {
  cd "$srcdir/FlickrNet"

  xbuild  /property:Configuration=Release FlickrNet.csproj
}

package() {
  cd "$srcdir/FlickrNet"

  gacutil -i bin/Release/FlickrNet.dll -package $pkgname-$pkgver -root "$pkgdir/usr/lib/"
  install -Dm644 "$srcdir/$pkgname.pc" "$pkgdir/usr/lib/pkgconfig/$pkgname.pc"
}
