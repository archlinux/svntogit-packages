# $Id$
# Maintainer: Tobias Kieslich <tobias funnychar archlinux.org>
# Contributor: Alois Nespor alois.nespor@gmail.com

pkgname=xfburn
pkgver=0.4.3
pkgrel=6
arch=('i686' 'x86_64')
pkgdesc="A simple CD/DVD burning tool based on libburnia libraries"
url="http://goodies.xfce.org/projects/applications/xfburn"
license=('GPL2')
groups=('xfce4-goodies')
install=${pkgname}.install
depends=('libburn' 'libisofs' 'libxfcegui4' 'exo' 'gstreamer0.10-base'
         'hicolor-icon-theme' 'desktop-file-utils' 'librsvg')
makedepends=('intltool')
source=(http://archive.xfce.org/src/apps/xfburn/0.4/$pkgname-$pkgver.tar.bz2
        fix_empty_dir_segfault.diff)
sha1sums=('52d33e56b7ce9774fba680f50723eaf1a7215091'
          'e374dd4942a49b52166fa9f2aa40e9aefb3aa3ec')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # fix https://bugs.archlinux.org/task/26121
  patch -Np0 -i "$srcdir/fix_empty_dir_segfault.diff"

  # fix build issue
  sed -i '1 i\
    #include <glib-object.h>' \
    xfburn/xfburn-settings.h

  ./configure \
    --prefix=/usr \
    --enable-gstreamer \
    --enable-dbus \
    --disable-hal
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
