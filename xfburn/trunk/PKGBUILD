# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Tobias Kieslich <tobias funnychar archlinux.org>
# Contributor: Alois Nespor alois.nespor@gmail.com

pkgname=xfburn
pkgver=0.6.2
pkgrel=1
pkgdesc="A simple CD/DVD burning tool based on libburnia libraries"
arch=('x86_64')
url="https://docs.xfce.org/apps/xfburn/start"
license=('GPL')
groups=('xfce4-goodies')
depends=('libburn' 'libisofs' 'gtk3' 'libxfce4ui' 'exo' 'gst-plugins-base-libs'
         'libgudev' 'desktop-file-utils')
makedepends=('intltool')
source=(https://archive.xfce.org/src/apps/xfburn/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=('83416e132e9bb31cd52fdc41460f96efa9ed57a290ec65f09714b83bb3d00327')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --enable-gstreamer \
    --disable-debug
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
