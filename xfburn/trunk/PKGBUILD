# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Tobias Kieslich <tobias funnychar archlinux.org>
# Contributor: Alois Nespor alois.nespor@gmail.com

pkgname=xfburn
pkgver=0.5.2
pkgrel=1
pkgdesc="A simple CD/DVD burning tool based on libburnia libraries"
arch=('i686' 'x86_64')
url="http://goodies.xfce.org/projects/applications/xfburn"
license=('GPL')
depends=('libburn' 'libisofs' 'exo' 'gstreamer0.10-base' 'desktop-file-utils')
makedepends=('intltool')
groups=('xfce4-goodies')
install=xfburn.install
source=(http://archive.xfce.org/src/apps/xfburn/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=('63682aab3c30c77e464b2b3b84606d1e1dcefc98e7042072fa09b729d6f84412')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --enable-gstreamer
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
