# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=font-bitstream-speedo
pkgver=1.0.2
pkgrel=3
pkgdesc="Bitstream Speedo fonts"
arch=('any')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('xorg-mkfontdir' 'xorg-mkfontscale' 'fontconfig')
source=(http://xorg.freedesktop.org/releases/individual/font/${pkgname}-${pkgver}.tar.bz2)
md5sums=('13f6f107be164cfbf6be40d35ecf0c0f')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
      --with-fontdir=/usr/share/fonts/speedo 
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -f "${pkgdir}/usr/share/fonts/speedo"/fonts.*
  
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
