# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-shell-extensions
pkgver=3.28.0+1+g496ae16
pkgrel=1
pkgdesc="Extensions for GNOME shell, including classic mode"
url="https://wiki.gnome.org/Projects/GnomeShell/Extensions"
arch=(any)
license=(GPL)
makedepends=(meson git sassc js52)
optdepends=('nautilus: gnome-classic session')
groups=(gnome)
_commit=496ae16cf18b1a7580d953b095542031178428ca  # master
source=("git+https://gitlab.gnome.org/GNOME/gnome-shell-extensions.git#commit=$_commit"
        "git+https://git.gnome.org/browse/gnome-shell-sass")
sha256sums=('SKIP'
            'SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname

  git submodule init
  git config --local submodule.data/gnome-shell-sass.url "$srcdir/gnome-shell-sass"
  git submodule update
}

build() {
  arch-meson $pkgname build -D extension_set=all -D classic_mode=true
  ninja -C build
}

package() {
  depends=(gnome-shell)

  DESTDIR="$pkgdir" ninja -C build install
}
