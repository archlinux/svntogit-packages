# $Id$
# Contributor: Eric Johnson <eric@coding-zone.com>
# Maintainer: Thayer Williams <thayer@archlinux.org>

pkgname=i8kmon
pkgver=1.33
pkgrel=1
pkgdesc="Tk-based daemon for i8kutils and Dell Inspiron/Latitude notebooks"
url="http://people.debian.org/~dz/i8k/"
license=('GPL2')
arch=('any')
depends=('i8kutils' 'tk')
backup=(etc/i8kutils/i8kmon.conf)
source=(ftp://ftp.archlinux.org/other/i8kutils/i8kutils_$pkgver.tar.gz i8kmon.daemon)
md5sums=('1ab077b301dbb0f4be8ef52eb3b97628'
         '6a1680603c9a9e52479372d549308995')

build() {
  cd $srcdir/i8kutils-$pkgver

  # conform to Arch Linux guidelines
  sed -i 's|/etc/i8kmon|/etc/i8kutils/i8kmon.conf|g' i8kmon.1 || return 1
  sed -i 's|/etc/i8kmon|/etc/i8kutils/i8kmon.conf|g' i8kmon || return 1

  # no configure script, and Makefile has /usr hardcoded
  install -D -m755 i8kmon $pkgdir/usr/bin/i8kmon || return 1
  install -D -m644 i8kmon.conf $pkgdir/etc/i8kutils/i8kmon.conf || return 1
  install -D -m644 i8kmon.1 $pkgdir/usr/share/man/man1/i8kmon.1 || return 1

  # install daemon script.
  install -D -m755 ../i8kmon.daemon $pkgdir/etc/rc.d/i8kmon || return 1
}
# vim: ts=2 sw=2 et ft=sh
