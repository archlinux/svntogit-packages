# $Id$
# Maintainer:
# Contributor: eric <eric@archlinux.org>
# Contributor: Markus Meissner <markus@meissna.de>

pkgname=tidyhtml
# obtain pkgver from http://tidy.cvs.sourceforge.net/tidy/tidy/src/version.h?view=markup
pkgver=1.46
pkgrel=3
pkgdesc="A tool to tidy down your HTML code to a clean style"
arch=('i686' 'x86_64')
url="http://tidy.sourceforge.net/"
license=('custom')
depends=('glibc')
source=("https://sources.archlinux.org/other/${pkgname}/${pkgname}-${pkgver}.tar.bz2")
md5sums=('3d970d54be83c5ec985687e9976a2832')

mksource() {
  mkdir ${pkgname}-${pkgver}
  cd ${pkgname}-${pkgver}
  cvs -d:pserver:anonymous@tidy.cvs.sourceforge.net:/cvsroot/tidy login   #hit enter for password
  cvs -z3 -d:pserver:anonymous@tidy.cvs.sourceforge.net:/cvsroot/tidy co -P tidy
  cd ..
  tar -cvjf ${pkgname}-${pkgver}.tar.bz2 ${pkgname}-${pkgver}/*
}

build(){
  cd ${pkgname}-${pkgver}/tidy
  source build/gnuauto/setup.sh

  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}/tidy
  make DESTDIR=$pkgdir install

  install -Dm644 htmldoc/license.html \
    "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
