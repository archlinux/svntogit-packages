# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>

pkgname=python-prometheus_client
pkgver=0.7.1
pkgrel=3

pkgdesc="Prometheus instrumentation library for Python applications"
url="https://github.com/prometheus/client_python"
arch=('any')
license=('APACHE')

depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-twisted' 'python-pytest')

source=(client_python-$pkgver.tar.gz::https://github.com/prometheus/client_python/archive/v${pkgver}.tar.gz)

sha512sums=('84b145ed191c9903d390b152c72de4f2afcb4c500b8debba59ded081f00cc76109b336ed2d7783d59e9922ea2927632f7ef730f336974294b20048c51e9d58a4')

build() {
  cd client_python-$pkgver
  python setup.py build
}

check() {
  cd client_python-$pkgver
  python setup.py test
}

package() {
  cd client_python-$pkgver
  python setup.py install --root="$pkgdir" --skip-build --optimize=1
}
