# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdebindings-perlqt
pkgver=4.9.0
pkgrel=1
pkgdesc="Perl bindings for the Qt libraries"
url="https://projects.kde.org/projects/kde/kdebindings/perl/perlqt"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kdebindings')
depends=('kdebindings-smokeqt' 'perl-list-moreutils')
makedepends=('cmake' 'automoc4' 'kdebindings-smokegen')
source=("http://download.kde.org/stable/${pkgver}/src/perlqt-${pkgver}.tar.xz")
sha1sums=('c51a788cee16f6222e70c96361083581fad1fb51')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../perlqt-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
