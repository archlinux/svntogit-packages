# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kwalletmanager
pkgver=15.08.3
pkgrel=1
pkgdesc='Wallet management tool'
arch=(i686 x86_64)
url='http://www.kde.org/applications/system/kwalletmanager/'
license=(LGPL)
depends=(kdelibs4support kcmutils hicolor-icon-theme)
makedepends=(extra-cmake-modules python kdoctools)
conflicts=(kdeutils-kwalletmanager)
replaces=(kdeutils-kwalletmanager)
groups=(kde-applications kdeutils)
source=("http://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
install=$pkgname.install
md5sums=('7359076a3f81aa5c345ba4982636fa5e')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}

