# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Baptiste Daroussin <baptiste.daroussin@gmail.com>
# Contributor: Grigorios Bouzakis <grbzks[at]gmail[dot]com>
# Contributor: mightyjaym <jm.ambrosino@free.fr>

pkgname=giflib
pkgver=4.2.1
pkgrel=2
pkgdesc="A library for reading and writing gif images"
url="http://sourceforge.net/projects/giflib/"
arch=('i686' 'x86_64')
license=('MIT')
depends=('libx11')
makedepends=('xmlto' 'docbook-xml' 'docbook-xsl')
provides=("libungif=${pkgver}")
conflicts=('libungif')
replaces=('libungif')
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/giflib/${pkgname}-${pkgver}.tar.bz2
        giflib-4.2.1-quantize.patch)
md5sums=('bbd3324af24156d5d7e66ab1cee8508d'
         '67e46b344340ac10f4f597d772679f23')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -i -e '/AC_PATH_XTRA/d' -e 's/AM_CONFIG_HEADER/AC_CONFIG_HEADER/' configure.ac
  patch -Np1 -i "$srcdir"/giflib-4.2.1-quantize.patch
  autoreconf -fi
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --disable-static
  make

  # libungif compatibility - instructions taken from Redhat specfile
  MAJOR=`echo ${pkgver} | sed 's/\([0-9]\+\)\..*/\1/'`
  gcc -shared -Wl,-soname,libungif.so.${MAJOR} -Llib/.libs -lgif -o libungif.so.${pkgver}
}

package() {
  cd ${pkgname}-${pkgver} 
  make DESTDIR="${pkgdir}" install

  install -m755 libungif.so.${pkgver} "${pkgdir}/usr/lib/"
  ln -sf libungif.so.${pkgver} "${pkgdir}/usr/lib/libungif.so.4"
  ln -sf libungif.so.4 "${pkgdir}/usr/lib/libungif.so"

  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
