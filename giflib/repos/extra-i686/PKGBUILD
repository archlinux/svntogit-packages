# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Baptiste Daroussin <baptiste.daroussin@gmail.com>
# Contributor: Grigorios Bouzakis <grbzks[at]gmail[dot]com>
# Contributor: mightyjaym <jm.ambrosino@free.fr>

pkgname=giflib
pkgver=5.0.4
pkgrel=2
pkgdesc="A library for reading and writing gif images"
url="http://sourceforge.net/projects/giflib/"
arch=('i686' 'x86_64')
license=('MIT')
depends=('libx11')
makedepends=('xmlto' 'docbook-xml' 'docbook-xsl')
provides=("libungif=${pkgver}")
conflicts=('libungif')
replaces=('libungif')
source=(http://downloads.sourceforge.net/sourceforge/giflib/${pkgname}-${pkgver}.tar.bz2
        giflib-5.0.4-include-stddef.patch)
md5sums=('da60935d07374de00a78c2f6f720c8f5'
         'e39b98822334f2bd2009469e5dea65c5')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -Np1 -i ../giflib-5.0.4-include-stddef.patch
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd ${pkgname}-${pkgver} 
  make DESTDIR="${pkgdir}" install

  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
