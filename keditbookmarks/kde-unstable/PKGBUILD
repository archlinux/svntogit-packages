# Maintainer: Antonio Rojas <arojas@archlinux,org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=keditbookmarks
pkgver=19.03.90
pkgrel=1
pkgdesc="Bookmark Organizer and Editor"
arch=(x86_64)
url="https://www.kde.org/"
license=(LGPL)
depends=(kparts)
makedepends=(extra-cmake-modules kdoctools)
conflicts=(kdebase-keditbookmarks)
replaces=(kdebase-keditbookmarks)
groups=(kde-applications kdebase)
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('9e055b32c30b7382b29673ac5314d35c73a40a102bf64165da838ae3d0f7692e'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
