# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdewebdev-klinkstatus
pkgver=17.04.3
pkgrel=1
arch=(i686 x86_64)
pkgdesc="Link Checker"
url='https://www.kde.org'
license=(GPL LGPL FDL)
groups=(kde-applications kdewebdev)
depends=(kdebase-runtime kdepimlibs4 tidy)
makedepends=(cmake automoc4 boost)
source=("https://download.kde.org/stable/applications/${pkgver}/src/klinkstatus-$pkgver.tar.xz"{,.sig})
sha256sums=('d392df9d61ec7963fb9a7f1a02a7e11161e3ca647799743202806a2bd070a953'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../klinkstatus-${pkgver} \
  	-DCMAKE_BUILD_TYPE=Release \
  	-DKDE4_BUILD_TESTS=OFF \
	-DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
