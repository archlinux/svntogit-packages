# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdewebdev-klinkstatus
pkgver=16.11.80
pkgrel=1
arch=(i686 x86_64)
pkgdesc="Link Checker"
url='http://www.kde.org'
license=(GPL LGPL FDL)
groups=(kde-applications kdewebdev)
depends=(kdebase-runtime kdepimlibs4 tidy)
makedepends=(cmake automoc4 boost)
source=("http://download.kde.org/stable/applications/${pkgver}/src/klinkstatus-$pkgver.tar.xz"{,.sig})
sha1sums=('25fa40d83cbcdd34588f7b4442e27b72ec785e7b'
          'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../klinkstatus-${pkgver} \
  	-DCMAKE_BUILD_TYPE=Release \
  	-DKDE4_BUILD_TESTS=OFF \
	-DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
