# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>

pkgname=gthumb
pkgver=3.2.2
pkgrel=1
pkgdesc="Image browser and viewer for the GNOME Desktop"
arch=(i686 x86_64)
license=(GPL)
url="http://live.gnome.org/gthumb"
depends=(desktop-file-utils hicolor-icon-theme dconf librsvg clutter-gtk
         gst-plugins-base-libs gsettings-desktop-schemas libwebp libsecret
         libsoup webkitgtk3 json-glib)
makedepends=(intltool libchamplain libopenraw exiv2 itstool brasero librsvg libopenraw liboauth docbook-xsl)
optdepends=('libopenraw: read RAW files'
            'exiv2: metadata support'
            'libchamplain: map viewer'
            'brasero: burn discs'
            'liboauth: web albums')
options=('!libtool' '!emptydirs')
install=gthumb.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('e201b9065d401144a17c2a33e4a85f0dcf457dc5cba6932dbac0197e141cdee8')

build() {
  cd $pkgname-$pkgver
  
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --enable-libchamplain --enable-libopenraw
  
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' -e 's/    if test "$export_dynamic" = yes && test -n "$export_dynamic_flag_spec"; then/      func_append compile_command " -Wl,-O1,--as-needed"\n      func_append finalize_command " -Wl,-O1,--as-needed"\n\0/' libtool

  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
