# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-knetwalk
pkgver=4.14.0
pkgrel=1
pkgdesc="Connect all the terminals to the server, in as few turns as possible"
url="http://kde.org/applications/games/knetwalk/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'libkdegames')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/knetwalk-${pkgver}.tar.xz")
sha1sums=('a8c12218af4f966252fff1542f4633929c23388a')

build() {
  mkdir build
  cd build
  cmake ../knetwalk-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
