# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=gnome-screenshot
pkgver=3.15.90
pkgrel=1
pkgdesc="Take pictures of your screen"
arch=(i686 x86_64)
url="http://gnome.org"
license=('GPL2')
depends=('dconf' 'gtk3' 'libcanberra')
makedepends=('intltool')
groups=('gnome')
install=$pkgname.install
source=(http://download.gnome.org/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('cc24e3d08cbd449ac709a540d368c72339dcd74a6c2c21b7d2ca2da1f2fdf35e')

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
