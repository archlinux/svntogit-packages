# Maintainer: RÃ©my Oudompheng <remy@archlinux.org>

pkgname=perl-file-slurper
_dist=File-Slurper
pkgver=0.010
pkgrel=2
pkgdesc="A simple, sane and efficient module to slurp a file"
arch=('any')
url="https://metacpan.org/release/${_dist}"
license=('PerlArtistic' 'GPL')
depends=('perl')
checkdepends=('perl-test-warnings')
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/L/LE/LEONT/${_dist}-${pkgver}.tar.gz)
md5sums=('e3d593d0c5b689076b07f2ca93f3c5c9')
sha256sums=('a393364648c9d6be938e8a68c3094c5f8a37b19d3159141ec81ba49559343c16')

build() {
    cd "${srcdir}/${_dist}-${pkgver}"
    unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
    export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
    perl Makefile.PL
    make
}

check() {
    cd "${srcdir}/${_dist}-${pkgver}"
    unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
    export PERL_MM_USE_DEFAULT=1
    make test
}

package() {
    cd "${srcdir}/${_dist}-${pkgver}"
    unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
    make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
}
