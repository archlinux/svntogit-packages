# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Fran√ßois Charette <firmicus@gmx.net>

pkgname=xz
pkgver=5.0.1
pkgrel=1
pkgdesc='Library and command line tools for XZ and LZMA compressed files'
arch=('i686' 'x86_64')
url='http://tukaani.org/xz/'
license=('GPL' 'LGPL' 'custom')
depends=('sh')
provides=('lzma' 'lzma-utils' 'xz-utils')
replaces=('lzma' 'lzma-utils' 'xz-utils')
conflicts=('lzma' 'lzma-utils' 'xz-utils')
options=('!libtool')
source=("http://tukaani.org/${pkgname}/${pkgname}-${pkgver}.tar.xz")
md5sums=('85d15b01a405616a888d09e19fb40f31')

# keep an upgrade path for older installations
PKGEXT='.pkg.tar.gz'

build() {
	cd ${srcdir}/${pkgname}-${pkgver}

	./configure --prefix=/usr \
		--disable-rpath \
		--enable-werror
	sed -i 's|^hardcode_libdir_flag_spec=.*|hardcode_libdir_flag_spec=""|g' libtool
	sed -i 's|^runpath_var=LD_RUN_PATH|runpath_var=DIE_RPATH_DIE|g' libtool
	make
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}
	make DESTDIR=${pkgdir} install
	install -d -m755 ${pkgdir}/usr/share/licenses/xz/
	ln -sf /usr/share/doc/xz/COPYING ${pkgdir}/usr/share/licenses/xz/
	ln -sf /usr/share/licenses/common/GPL2/license.txt ${pkgdir}/usr/share/doc/xz/COPYING.GPLv2
}
