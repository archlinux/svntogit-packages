# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
#Contributor: Urs Wolfer <uwolfer @ fwo.ch>

pkgname=dmraid
pkgver=1.0.0.rc16
pkgrel=3
pkgdesc="Device mapper RAID interface"
url="http://people.redhat.com/~heinzm/sw/dmraid/"
depends=('device-mapper>=2.0.54')
arch=('i686' 'x86_64')
license=('GPL')
source=(http://people.redhat.com/~heinzm/sw/dmraid/src/$pkgname-$pkgver.tar.bz2
        dmraid_install
        dmraid_hook)
install=dmraid.install
md5sums=('32832c1dfd7e72cd4355490322fca68a'
         'c414d781b82b5f1c8555a760b0fd71cc'
         'b1fcca57a711be68b57c5ec8614d87cb')

build() {
  cd $srcdir/$pkgname/$pkgver

  # create static bin
  ./configure --enable-static_link --prefix=/usr --enable-led --enable-intel_led
  make || return 1
  make DESTDIR=$pkgdir exec_prefix=$pkgdir/usr install
  mv $pkgdir/usr/sbin/dmraid $pkgdir/usr/sbin/dmraid.static

  make clean
  ./configure --enable-led --enable-intel_led
  make || return 1
  make DESTDIR=$pkgdir exec_prefix=$pkgdir/ mandir=$pkgdir/usr/share/man includedir=$pkgdir/usr/include install || return 1
  mkdir -p $pkgdir/var/lock/dmraid
  chmod 1777 $pkgdir/var/lock/
  install -D -m644 $srcdir/dmraid_install $pkgdir/lib/initcpio/install/dmraid
  install -D -m644 $srcdir/dmraid_hook $pkgdir/lib/initcpio/hooks/dmraid
  # fix .a file
  chmod 644 $pkgdir/lib/libdmraid.a
}
