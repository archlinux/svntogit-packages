# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=pyside2
pkgname=(pyside2 python-pyside2 python2-pyside2)
_qtver=5.11.0
pkgver=${_qtver/-/}
pkgrel=2
arch=(x86_64)
url='http://qt-project.org/'
license=(LGPL)
pkgdesc='Enables the use of Qt5 APIs in Python applications'
makedepends=(shiboken2 python-shiboken2 python2-shiboken2 cmake
             qt5-multimedia qt5-tools qt5-sensors qt5-charts qt5-webengine qt5-datavis3d
             qt5-websockets qt5-speech qt5-3d qt5-svg qt5-script qt5-scxml qt5-x11extras)
groups=(qt qt5)
_pkgfqn=pyside-setup-everywhere-src-${_qtver}
source=("http://download.qt.io/official_releases/QtForPython/pyside2/PySide2-$pkgver-src/${_pkgfqn}.tar.xz")
sha256sums=('fbc412c4544bca308291a08a5173a949ca530d801f00b8337902a5067e490922')

prepare() {
  mkdir -p build{,2}
}

build() {
  cd build
  cmake ../${_pkgfqn}/sources/pyside2 \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTS=OFF \
    -DUSE_PYTHON_VERSION=3
  make

  cd ../build2
  cmake ../${_pkgfqn}/sources/pyside2 \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTS=OFF \
    -DUSE_PYTHON_VERSION=2
  make
}

package_pyside2() {
  depends=()

  cd build
  make DESTDIR="$pkgdir" install
# Provided in python-pyside2
  rm -r "$pkgdir"/usr/lib/{python*,lib*,cmake/*/*python*}
}

package_python-pyside2() {
  depends=(python-shiboken2 qt5-base)
  optdepends=('qt5-svg: QtSvg bindings'
              'qt5-script: QtScript bindings'
              'qt5-speech: QtTextToSpeech bindings'
              'qt5-websockets: QtWebSockets bindings'
              'qt5-webengine: QtWebEngine bindings'
              'qt5-datavis3d: QtDataVisualization bindings'
              'qt5-scxml: QtScxml bindings'
              'qt5-sensors: QtSensors bindings'
              'qt5-3d: Qt3D bindings'
              'qt5-x11extras: QtX11Extras bindings'
              'qt5-charts: QtCharts bindings'
              'qt5-tools: QtHelp bindings')
  cd build
  make DESTDIR="$pkgdir" install
# Provided in pyside2
  rm -r "$pkgdir"/usr/{include,share,lib/{pkgconfig,cmake/*/PySide2Config{.cmake,Version.cmake}}}
}

package_python2-pyside2() {
  depends=(python2-shiboken2 qt5-base)
  optdepends=('qt5-svg: QtSvg bindings'
              'qt5-script: QtScript bindings'
              'qt5-speech: QtTextToSpeech bindings'
              'qt5-websockets: QtWebSockets bindings'
              'qt5-webengine: QtWebEngine bindings'
              'qt5-datavis3d: QtDataVisualization bindings'
              'qt5-scxml: QtScxml bindings'
              'qt5-sensors: QtSensors bindings'
              'qt5-3d: Qt3D bindings'
              'qt5-x11extras: QtX11Extras bindings'
              'qt5-charts: QtCharts bindings'
              'qt5-tools: QtHelp bindings')
  cd build2
  make DESTDIR="$pkgdir" install
# Provided in pyside2
  rm -r "$pkgdir"/usr/{include,share,lib/{pkgconfig,cmake/*/PySide2Config{.cmake,Version.cmake}}}
}
