# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Committer: dorphell <dorphell@gmx.net>

pkgname=screen
pkgver=4.0.3
pkgrel=15
_ptygroup=5 #the UID of our PTY/TTY group
pkgdesc="Full-screen window manager that multiplexes a physical terminal"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/screen/"
license=('GPL')
depends=('ncurses' 'pam')
backup=('etc/screenrc' 'etc/pam.d/screen')
options=('!makeflags')
install=screen.install
source=(ftp://ftp.uni-erlangen.de/pub/utilities/screen/screen-${pkgver}.tar.gz
        screen-4.0.3-long-term.patch
        screen-4.0.3-caption-colors.patch
        screen.pam)
md5sums=('8506fd205028a96c741e4037de6e3c42'
         '2723c3e71d5a2e5a74b6ddbc5215ef0f'
         '191b6ced695ce2ae3a65bf02c2d4220f'
         '74f7919d7b65436cc2ea5341fa8ca374')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -Np1 -i $srcdir/screen-4.0.3-long-term.patch
  patch -Np2 -i $srcdir/screen-4.0.3-caption-colors.patch
  
  ./configure --prefix=/usr --enable-pam \
        --with-sys-screenrc=/etc/screenrc --enable-colors256 \
       --enable-rxvt_osc --enable-telnet \
       --with-pty-group=$_ptygroup \
       --mandir=/usr/share/man --infodir=/usr/share/info \
       --with-socket-dir=/run/screens
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  install -Dm644 ${srcdir}/screen.pam ${pkgdir}/etc/pam.d/screen

  install -Dm644 etc/completer.zsh ${pkgdir}/usr/share/zsh/site-functions/screen
  install -Dm644 etc/etcscreenrc ${pkgdir}/etc/screenrc
  install -Dm644 etc/screenrc ${pkgdir}/etc/skel/.screenrc
}
