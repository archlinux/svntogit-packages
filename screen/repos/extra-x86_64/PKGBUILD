# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Committer: dorphell <dorphell@gmx.net>

pkgname=screen
pkgver=4.3.1
pkgrel=2
pkgdesc='Full-screen window manager that multiplexes a physical terminal'
url='http://www.gnu.org/software/screen/'
arch=('i686' 'x86_64')
license=('GPL')
depends=('ncurses' 'pam')
validpgpkeys=('2EE59A5D0C50167B5535BBF1B708A383C53EF3A4')
source=("http://ftp.gnu.org/gnu/screen/screen-${pkgver}.tar.gz"{,.sig}
        'pam.d')
sha1sums=('a524761504e28480517e338b20c852f2ab100c93' 'SKIP'
          '1ab4b512d2ac840d16db6986d7c98d7ce2f6383f')

backup=('etc/screenrc' 'etc/pam.d/screen')
options=('!makeflags')
install=install

_ptygroup=5 #the UID of our PTY/TTY group

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	./configure \
		--prefix=/usr \
		--mandir=/usr/share/man \
		--infodir=/usr/share/info \
		--enable-colors256 \
		--enable-pam \
		--enable-rxvt_osc \
		--enable-telnet \
		--with-pty-group=$_ptygroup \
		--with-socket-dir=/run/screens \
		--with-sys-screenrc=/etc/screenrc \

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	install -Dm644 ../pam.d "${pkgdir}"/etc/pam.d/screen

	install -Dm644 etc/completer.zsh "${pkgdir}"/usr/share/zsh/site-functions/_screen
	install -Dm644 etc/etcscreenrc "${pkgdir}"/etc/screenrc
	install -Dm644 etc/screenrc "${pkgdir}"/etc/skel/.screenrc
}
