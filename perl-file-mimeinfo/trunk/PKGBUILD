# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-file-mimeinfo
pkgver=0.32
pkgrel=1
pkgdesc='Determine file type, includes mimeopen and mimetype'
arch=('any')
url="https://metacpan.org/release/File-MimeInfo"
license=('GPL' 'PerlArtistic')
depends=('perl-encode-locale' 'perl-file-basedir' 'perl-file-desktopentry' 'shared-mime-info')
checkdepends=('perl-test-pod' 'perl-test-pod-coverage')
options=('!emptydirs')
source=(https://cpan.metacpan.org/authors/id/M/MI/MICHIELB/File-MimeInfo-$pkgver.tar.gz)
sha512sums=('c8cdad74d732321471188c96a5d03725bc51c10655fee7c0e400da6a7215fee8925be95cb9114e748747fb03c3dd33f5b4d1ca0641bdf050339537a222ea6009')

build() {
  cd File-MimeInfo-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd File-MimeInfo-$pkgver
  make test
}

package() {
  cd File-MimeInfo-$pkgver
  make DESTDIR="${pkgdir}" install
}
