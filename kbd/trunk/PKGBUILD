# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgname=kbd
pkgver=2.2.0
pkgrel=1
pkgdesc="Keytable files and keyboard utilities"
arch=('x86_64')
url="http://www.kbd-project.org"
license=('GPL')
depends=('glibc' 'pam')
makedepends=('check')
source=(https://www.kernel.org/pub/linux/utils/${pkgname}/${pkgname}-${pkgver}.tar.gz
https://www.kernel.org/pub/linux/utils/kbd/${pkgname}-${pkgver}.tar.sign
        'fix-euro2.patch')
provides=('vlock')
conflicts=('vlock')
replaces=('vlock')
validpgpkeys=(
              '7F2A3D07298149A0793C9A4EA45ABA544CFFD434' #Alexey Gladkov 
             )

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}
  # rename keymap files with the same names
  # this is needed because when only name of keymap is specified
  # loadkeys loads the first keymap it can find, which is bad (see FS#13837)
  # this should be removed when upstream adopts the change
  mv data/keymaps/i386/qwertz/cz{,-qwertz}.map
  mv data/keymaps/i386/olpc/es{,-olpc}.map
  mv data/keymaps/i386/olpc/pt{,-olpc}.map
  mv data/keymaps/i386/fgGIod/trf{,-fgGIod}.map
  mv data/keymaps/i386/colemak/{en-latin9,colemak}.map
  # fix euro2 #28213
  patch -Np1 -i ../fix-euro2.patch
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr --datadir=/usr/share/kbd --mandir=/usr/share/man
  make KEYCODES_PROGS=yes RESIZECONS_PROGS=yes
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make KEYCODES_PROGS=yes RESIZECONS_PROGS=yes DESTDIR=${pkgdir} install
}
md5sums=('5b07c5cf73f2a2bdff5a489368969f70'
         'SKIP'
         'd869200acbc0aab6a9cafa43cb140d4e')
