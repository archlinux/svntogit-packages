# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgname=kbd
pkgver=1.15.3
pkgrel=2
pkgdesc="Keytable files and keyboard utilities"
arch=('i686' 'x86_64')
url="ftp://ftp.altlinux.org/pub/people/legion/kbd/"
license=('GPL')
depends=('glibc')
source=(ftp://ftp.altlinux.org/pub/people/legion/kbd/${pkgname}-${pkgver}.tar.gz
        fix-es.po.patch)
md5sums=('8143e179a0f3c25646ce5085e8777200'
         '4ded3edb50fb7a3277bae6a870cee812')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -Np1 -i ../fix-es.po.patch
  # rename keymap files with the same names
  # this is needed because when only name of keymap is specified
  # loadkeys loads the first keymap it can find, which is bad (see FS#13837)
  # this should be removed when upstream adopts the change
  mv data/keymaps/i386/qwertz/cz{,-qwertz}.map
  mv data/keymaps/i386/olpc/es{,-olpc}.map
  mv data/keymaps/i386/olpc/pt{,-olpc}.map
  mv data/keymaps/i386/dvorak/no{,-dvorak}.map
  mv data/keymaps/i386/fgGIod/trf{,-fgGIod}.map
  mv data/keymaps/i386/colemak/{en-latin9,colemak}.map

  ./configure --prefix=/usr --datadir=/usr/share/kbd --mandir=/usr/share/man
  make KEYCODES_PROGS=yes RESIZECONS_PROGS=yes
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make KEYCODES_PROGS=yes RESIZECONS_PROGS=yes DESTDIR=${pkgdir} install

  # this is needed because initscripts call /bin/loadkeys
  # remove this when next versions of kbd
  # and initscripts with /usr/bin/loadkeys usage will be released
  mkdir ${pkgdir}/bin
  ln -s /usr/bin/loadkeys ${pkgdir}/bin/loadkeys
}
