# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Antonio Rojas < nqn1976 @ gmail.com >

pkgname=kdeplasma-applets-plasma-nm
pkgver=0.9.3.5
pkgrel=2
pkgdesc="Plasma applet written in QML for managing network connections"
arch=('i686' 'x86_64')
url="https://projects.kde.org/projects/kde/workspace/plasma-nm"
license=('GPL')
depends=('kdebase-workspace' 'libnm-qt')
makedepends=('cmake' 'automoc4' 'openconnect' 'mobile-broadband-provider-info')
optdepends=('mobile-broadband-provider-info: Database of mobile broadband service providers'
            'openconnect: Cisco AnyConnect VPN plugin')
conflicts=('kdeplasma-applets-networkmanagement')
source=("http://download.kde.org/stable/plasma-nm/plasma-nm-${pkgver}.tar.xz"
        'openconnect7.patch')
md5sums=('16f5e711de503fa8e5adeef9922c7c98'
         '669e568324f059acc8777e82403b1deb')

prepare() {
  mkdir build

  cd plasma-nm-${pkgver}
  patch -p1 -i "${srcdir}"/openconnect7.patch
}

build() {
  cd build
  cmake ../plasma-nm-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
