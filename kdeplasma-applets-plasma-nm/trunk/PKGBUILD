# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Antonio Rojas < nqn1976 @ gmail.com >

pkgname=kdeplasma-applets-plasma-nm
pkgver=0.9.3.1
pkgrel=3
pkgdesc="Plasma applet written in QML for managing network connections"
arch=('i686' 'x86_64')
url="https://projects.kde.org/projects/playground/network/plasma-nm"
license=('GPL')
depends=('kdebase-workspace' 'libnm-qt')
makedepends=('cmake' 'automoc4' 'openconnect')
optdepends=('mobile-broadband-provider-info: Database of mobile broadband service providers'
            'openconnect: Cisco AnyConnect VPN plugin')
conflicts=('kdeplasma-applets-networkmanagement')
source=("http://download.kde.org/unstable/plasma-nm/plasma-nm-${pkgver}.tar.xz"
        'plasma-nm-fixes.patch')
md5sums=('c6ce9cd8523ec0f99fe87dea8b76a02f'
         'e5825709f49ba6c4386e909b80f75066')

prepare() {
  mkdir build

  cd plasma-nm-${pkgver}
  patch -p1 -i "${srcdir}"/plasma-nm-fixes.patch
}

build() {
  cd build
  cmake ../plasma-nm-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
