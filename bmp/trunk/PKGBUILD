# $Id$
# Maintainer: Kevin Piche <kevin@archlinux.org>
# Contributor: Ben <contrasutra@myrealbox.com>

pkgname=bmp
pkgver=0.9.7.1
pkgrel=8
pkgdesc="gtk2 port of xmms"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/beepmp/"
license=('GPL')
depends=('id3lib' 'libglade' 'libsm' 'libvorbis' 'alsa-lib' 'unzip' 'desktop-file-utils')
makedepends=('libxt')
options=('!libtool')
install=bmp.install
source=(http://downloads.sourceforge.net/sourceforge/beepmp/${pkgname}-${pkgver}.tar.gz \
        ${pkgver}-visualization.patch bmp-${pkgver}-crossfade-0.3.9.patch)
md5sums=('c25d5a8d49cc5851d13d525a20023c4c'
         'fa1bdf5a8d03e8539a6bed5caa471176'
         '42ad4109ddb66024d4c6d1f3e572ab86')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p0 -i ../${pkgver}-visualization.patch 
  patch -p1 -i ../${pkgname}-${pkgver}-crossfade-0.3.9.patch 
  ./configure --prefix=/usr --mandir=/usr/share/man 
  make 
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install 
}
# vim: ts=2 sw=2 et ft=sh
