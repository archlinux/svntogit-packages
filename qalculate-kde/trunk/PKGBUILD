# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=qalculate-kde
pkgver=0.9.7
pkgrel=4
pkgdesc="KDE frontend for libqalculate"
arch=('i686' 'x86_64')
url="http://qalculate.sourceforge.net/"
license=('GPL')
depends=('libqalculate' 'kdelibs3' 'cln')
options=('!makeflags')
source=(http://downloads.sourceforge.net/sourceforge/qalculate/${pkgname}-${pkgver}.tar.gz
        qalculate-kde-0.9.7-gcc47.patch)
sha1sums=('fa0db8a3fb80264328770c4bbcbc0892a4d4ce91'
          '8f23a12dd37e2fd3d316b78ee0831700310c0d36')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i "${srcdir}/qalculate-kde-0.9.7-gcc47.patch"
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/opt/kde --without-arts
  make
}

package() {
 cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
