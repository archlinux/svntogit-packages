# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=composer
pkgver=2.0.7
pkgrel=1
pkgdesc="Dependency Manager for PHP"
arch=('any')
license=('MIT')
url='https://getcomposer.org/'
depends=('php' 'unzip')
makedepends=('git')
checkdepends=('php-intl')
source=("${pkgname}::git+https://github.com/composer/composer.git#tag=${pkgver}"
        "${pkgname}-${pkgver}.phar::https://getcomposer.org/download/${pkgver}/composer.phar")
sha256sums=('SKIP'
            '0a060e8caf1d5cde55c4562fd68369b08cf231b8fd30da9c8f994b111557ebe9')
COMPOSER_FLAGS="--ansi --no-interaction --no-progress --prefer-dist"

prepare() {
	cd "${srcdir}/${pkgname}"

	php "${srcdir}/${pkgname}-${pkgver}.phar" install --no-dev ${COMPOSER_FLAGS}
}

build() {
	cd "${srcdir}/${pkgname}"

	php -d phar.readonly=Off bin/compile
}

check() {
	cd "${srcdir}/${pkgname}"

	# check if the upstream build and ours differ
	diff -q ${srcdir}/${pkgname}-${pkgver}.phar composer.phar > /dev/null || msg2 "Upstream composer differs from ours"

	# Install the dev dependencies to run the unit tests
	bin/composer install ${COMPOSER_FLAGS}

	bin/composer validate

	git config --global user.email composer@example.com
	git config --global user.name composer

	php -d extension=intl vendor/bin/simple-phpunit
}

package() {
	cd "${srcdir}/${pkgname}"

	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm755 composer.phar "${pkgdir}/usr/bin/composer"
}
