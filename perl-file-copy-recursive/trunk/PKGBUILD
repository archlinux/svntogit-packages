# $Id$
# Maintainer: Dan McGee <dan@archlinux.org>
# Contributor: AUR Perl <aurperl@juster.info>

pkgname=perl-file-copy-recursive
pkgver=0.38
pkgrel=2
pkgdesc="extension for recursively copying files and directories"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl')
url='http://search.cpan.org/dist/File-Copy-Recursive'
source=("http://search.cpan.org/CPAN/authors/id/D/DM/DMUEY/File-Copy-Recursive-$pkgver.tar.gz")
md5sums=('e76dc75ab456510d67c6c3a95183f72f')

build() {
  cd "$srcdir/File-Copy-Recursive-$pkgver"
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "$srcdir/File-Copy-Recursive-$pkgver"
  make test
}

package() {
  cd "$srcdir/File-Copy-Recursive-$pkgver"
  make DESTDIR="$pkgdir" install
}
