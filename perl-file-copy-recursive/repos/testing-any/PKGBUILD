# Maintainer: Felix Yan <felixonmars@gmail.com>

pkgname=perl-file-copy-recursive
pkgver=0.45
pkgrel=2
pkgdesc="Perl extension for recursively copying files and directories"
arch=('any')
url="https://metacpan.org/release/File-Copy-Recursive"
license=('GPL' 'PerlArtistic')
depends=('perl')
checkdepends=('perl-path-tiny' 'perl-test-deep' 'perl-test-fatal' 'perl-test-file'
              'perl-test-warnings')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/D/DM/DMUEY/File-Copy-Recursive-$pkgver.tar.gz")
sha512sums=('6d8a6c55bc54bfe5ce3834527776c6b1466d5672f5f8901c27053188bbc78e87dd62bed8d330c04079b02d0967be0a8d0cd779e67973cf79a2c6ff87d5921c60')

build() {
  cd File-Copy-Recursive-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check () {
  cd File-Copy-Recursive-$pkgver
  make test
}

package () {
  cd File-Copy-Recursive-$pkgver
  make DESTDIR="$pkgdir" install
}
