# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kruler
pkgver=15.11.90
pkgrel=1
pkgdesc="Screen Ruler"
url="http://kde.org/applications/graphics/kruler/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegraphics')
depends=('knotifications' 'kxmlgui' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'python' 'kdoctools')
install=${pkgname}.install
conflicts=('kdegraphics-kruler')
replaces=('kdegraphics-kruler')
source=("http://download.kde.org/unstable/applications/${pkgver}/src/kruler-${pkgver}.tar.xz")
sha1sums=('1efccc69744eec670188ff5c37ce7faf1fe75a82')

build() {
  cd "${srcdir}"
  mkdir -p build
  cd build
  cmake ../kruler-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
