# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Marti Raudsepp <marti@juffo.org>

pkgname=media-player-info
pkgver=19
pkgrel=1
pkgdesc="Data files describing media player capabilities, for post-HAL systems"
arch=('any')
license=('BSD')
url="http://cgit.freedesktop.org/media-player-info/"
depends=('systemd' 'python')
source=(http://www.freedesktop.org/software/media-player-info/$pkgname-$pkgver.tar.gz)
sha256sums=('d5cfb54609d59fe18d63253842c2c56c23b8d44e361d01b34e4a9a3e5fd0265a')
install=media-player-info.install

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr \
      --with-udevdir=/usr/lib/udev
  LANG="en_US.UTF-8" make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  install -d "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname"
}
