# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kwayland
pkgver=5.93.0
pkgrel=2
pkgdesc='Qt-style Client and Server library wrapper for the Wayland libraries'
arch=(x86_64)
url='https://www.kde.org'
license=(LGPL)
depends=(qt5-wayland)
makedepends=(extra-cmake-modules doxygen qt5-tools qt5-doc wayland-protocols plasma-wayland-protocols)
groups=(kf5)
source=(https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz{,.sig}
        https://invent.kde.org/frameworks/kwayland/-/commit/9e70125d.patch)
sha256sums=('1648a273ad1c0c1895a0edff9a3e97eb151e1e3533362e0f01d72706ac0a7abe'
            'SKIP'
            'a4099ee6a6b4cdb9b2303f087460c90816bc4b26b66b67896bfb786d04af743f')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>
options=(debug)

prepare() {
  patch -d $pkgname-$pkgver -p1 < 9e70125d.patch # Fix pkgconfig file
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
