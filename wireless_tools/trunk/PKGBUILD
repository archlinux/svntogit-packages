# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Jason Chu <jchu@xentac.net>

pkgname=wireless_tools
pkgver=29
pkgrel=8
pkgdesc='Tools allowing to manipulate the Wireless Extensions'
url='http://www.hpl.hp.com/personal/Jean_Tourrilhes/Linux/Tools.html'
license=('GPL')
arch=('i686' 'x86_64')
source=("http://www.hpl.hp.com/personal/Jean_Tourrilhes/Linux/${pkgname}.${pkgver}.tar.gz"
        'dense.patch')
sha1sums=('22040ac1497f4c5f8ddeca365591b01ae3475710'
          '5c297c7dff1ec1d2a3d918c20622ab3c6daf5b72')

prepare() {
	cd "${srcdir}/${pkgname}.${pkgver}"
	patch -p1 -i ../dense.patch # FS#15363
}

build() {
	cd "${srcdir}/${pkgname}.${pkgver}"
	make CFLAGS="${CFLAGS} -I." LDFLAGS="${LDFLAGS}"
}

package() {
	cd "${srcdir}/${pkgname}.${pkgver}"
	make \
		INSTALL_DIR="${pkgdir}/usr/bin" \
		INSTALL_LIB="${pkgdir}/usr/lib" \
		INSTALL_INC="${pkgdir}/usr/include" \
		INSTALL_MAN="${pkgdir}/usr/share/man" \
		install
}
