# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributer: Jason Chu <jchu@xentac.net>

pkgname=wireless_tools
pkgver=29
pkgrel=5
pkgdesc="Wireless Tools"
arch=('i686' 'x86_64')
url="http://www.hpl.hp.com/personal/Jean_Tourrilhes/Linux/Tools.html"
license=('GPL')
depends=('glibc')
source=("http://www.hpl.hp.com/personal/Jean_Tourrilhes/Linux/wireless_tools.${pkgver}.tar.gz")
md5sums=('e06c222e186f7cc013fd272d023710cb')

build() {
	cd ${srcdir}/wireless_tools.${pkgver}
	make
}

package() {
	cd ${srcdir}/wireless_tools.${pkgver}
	make INSTALL_DIR="${pkgdir}/usr/sbin" \
		INSTALL_LIB="${pkgdir}/usr/lib" \
		INSTALL_INC="${pkgdir}/usr/include" \
		INSTALL_MAN="${pkgdir}/usr/share/man" install
}
