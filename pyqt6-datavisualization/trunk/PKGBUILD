# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgbase=pyqt6-datavisualization
pkgname=(python-pyqt6-datavisualization)
pkgver=6.2.0
pkgrel=3
pkgdesc='Python bindings for QtDataVisualization'
arch=(x86_64)
url='https://www.riverbankcomputing.com/software/pyqtdatavisualization/intro'
license=(GPL3)
groups=(pyqt6)
depends=(qt6-datavis3d python-pyqt6)
makedepends=(qt6-declarative sip pyqt-builder)
source=(https://pypi.python.org/packages/source/P/PyQt6-DataVisualization/PyQt6_DataVisualization-$pkgver.tar.gz)
sha256sums=('7526bfd9433acb8eabdb354ba9e027d1bb34b8fa9d14f299d4b3b4c81a21e37a')
options=(debug)

build() {
  cd PyQt6_DataVisualization-$pkgver
  sip-build \
    --no-make \
    --qmake=/usr/bin/qmake6
  cd build
  make
}

package_python-pyqt6-datavisualization(){
  cd PyQt6_DataVisualization-$pkgver/build
  make INSTALL_ROOT="$pkgdir" install
}
