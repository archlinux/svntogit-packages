# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: lydgate

pkgname=mlocate
pkgver=0.22
pkgrel=2
pkgdesc="Faster merging drop-in for slocate"
arch=('i686' 'x86_64')
url="http://carolina.mff.cuni.cz/~trmac/blog/mlocate"
license=('GPL')
groups=('base')
conflicts=('slocate')
provides=('slocate')
replaces=('slocate')
depends=('glibc' 'sh')
backup=('etc/updatedb.conf'
        'etc/cron.daily/updatedb')
install=mlocate.install
source=(https://fedorahosted.org/releases/m/l/mlocate/mlocate-$pkgver.tar.bz2
	updatedb.conf
	updatedb.cron.daily)
md5sums=('ffd78b6ff967a8ae9f4780f6a9a95e68'
         '66898efb290eff4efd44a0b94fe3927d'
         'eee7f9db8c6145724313af6fd5fe9d66')

build() {
  cd $srcdir/$pkgname-$pkgver

  # Build!
  sed -i '/^groupname /s/mlocate/locate/' Makefile.in
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var/lib
  make || return 1

  # Set up a default updatedb.conf and a daily cronjob
  install -Dm644 ../updatedb.conf $pkgdir/etc/updatedb.conf
  install -Dm744 ../updatedb.cron.daily $pkgdir/etc/cron.daily/updatedb

  # Install Mlocate
  make DESTDIR=$pkgdir install

  ln -sv locate $pkgdir/usr/bin/slocate
  chgrp -v 21 $pkgdir/usr/bin/locate
  chmod -v 2755 $pkgdir/usr/bin/locate

  install -dm755 $pkgdir/var/lib
  install -d -m750 -g21 $pkgdir/var/lib/locate
}
