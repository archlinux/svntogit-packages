# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Sarah Hay <sarahhay@mb.sympatico.ca>

# This package must not by "any" arch.
# Requires rebuild with every change to sdl-config

pkgname=perl-alien-sdl
_realname=Alien-SDL
pkgver=1.438
pkgrel=1
pkgdesc="Building, finding and using SDL binaries"
arch=(i686 x86_64)
license=(PerlArtistic GPL)
url="http://search.cpan.org/dist/Alien-SDL/"
depends=(sdl=1.2.15 perl-file-sharedir perl-capture-tiny)
makedepends=(perl-file-which perl-text-patch)
options=(!emptydirs)
_author=JTPALMER
source=(http://search.cpan.org/CPAN/authors/id/${_author::1}/${_author::2}/$_author/$_realname-$pkgver.tar.gz)
md5sums=('1936ba9e300677151124b62883cedf20')

package() {
  cd "$srcdir/$_realname-$pkgver"

  # install module in vendor directories
  perl Build.PL installdirs=vendor destdir="$pkgdir" --with-sdl-config

  perl Build
  perl Build install
}
