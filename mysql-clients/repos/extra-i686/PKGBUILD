# $Id$
# Maintainer: Douglas Soares de Andrade <douglas@archlinux.org>

pkgname=mysql-clients
pkgver=5.1.36
pkgrel=1
pkgdesc="MySQL client tools"
arch=(i686 x86_64)
depends=("libmysqlclient>=${pkgver}")
makedepends=('tcp_wrappers' 'libtool' 'gcc' 'readline>=6.0')
url=('http://www.mysql.com/')
options=('!libtool')
license=('GPL')
source=(ftp://mirror.switch.ch/mirror/mysql/Downloads/MySQL-5.1/mysql-$pkgver.tar.gz
        mysql-fix-libs.patch)

build() {
  cd $startdir/src/mysql-${pkgver}
  patch -Np1 -i ${srcdir}/mysql-fix-libs.patch || return 1
  
  ./configure --prefix=/usr --libexecdir=/usr/sbin \
  --localstatedir=/var --sysconfdir=/etc \
  --without-debug --without-docs --without-readline \
  --enable-local-infile --with-ssl \
  --with-charset=latin1 --with-collation=latin1_general_ci \
  --with-extra-charsets=complex --enable-thread-safe-client \
  --with-libwrap
 
  pushd include || return
  make || return 1
  popd

  pushd libmysql
  make link_sources get_password.lo || return
  popd 
  
  for dir in strings regex mysys dbug extra; do
    pushd ${dir} || return 1
    make || return 1
    popd
  done

  cd client

  sed -i -e 's|\$(top_builddir)/libmysql/libmysqlclient.la|/usr/lib/mysql/libmysqlclient.so|g' Makefile
  make link_sources
  make || return 1
  make DESTDIR=${startdir}/pkg install

  # Removing libmysql stuff
  rm -rf $pkgdir/usr/lib/
}
md5sums=('18e694c4ecbe851fe8e21e1668116c46'
         'd1921e85821a66196c24ef6e5346b572')
