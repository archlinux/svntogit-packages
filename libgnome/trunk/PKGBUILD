# $Id$ 
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libgnome
pkgver=2.30.0
pkgrel=1
pkgdesc="Common libraries for GNOME"
arch=(i686 x86_64)
license=('LGPL')
depends=('gnome-vfs>=2.24.3' 'libbonobo>=2.24.3' 'gconf>=2.28.1' 'gvfs')
makedepends=('pkgconfig' 'intltool')
options=('!libtool' '!emptydirs')
url="http://www.gnome.org"
install=libgnome.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.30/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('3ed932b94f16614cb03c7661f39b574845970c6ce3ca46e46338409283856f1f')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static --disable-esd || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain libgnome-2.0 ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
