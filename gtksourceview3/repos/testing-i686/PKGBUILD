# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=gtksourceview3
_pkgbasename=gtksourceview
pkgver=3.6.0
pkgrel=1
pkgdesc="A text widget adding syntax highlighting and more to GNOME"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gtk3' 'libxml2')
makedepends=('intltool' 'gobject-introspection' 'glade')
options=('!libtool')
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/$_pkgbasename/${pkgver%.*}/$_pkgbasename-$pkgver.tar.xz)
sha256sums=('7d268618fbe8c2960300a305d38255294c9f497fcbc0e53a1da1bf9bda0d1c7a')

build() {
  cd "$_pkgbasename-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --enable-glade-catalog
  make
}

package() {
  cd "$_pkgbasename-$pkgver"
  make DESTDIR="$pkgdir" install
}
