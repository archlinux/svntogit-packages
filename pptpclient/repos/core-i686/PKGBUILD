# $Id$
# Maintainer: Daniel Isenmann <daniel @ archlinux.org>
# Contributor: Mark Nikkel <mnikkel@gmail.com>

pkgname=pptpclient
pkgver=1.8.0
pkgrel=2
pkgdesc="Client for the proprietary Microsoft Point-to-Point Tunneling Protocol, PPTP."
arch=('i686' 'x86_64')
url="http://pptpclient.sourceforge.net/"
license=('GPL')
depends=('ppp' 'iproute2')
backup=('etc/ppp/options.pptp')
source=("http://downloads.sourceforge.net/sourceforge/pptpclient/pptp-$pkgver.tar.gz")
md5sums=('4efce9f263e2c3f38d79d9df222476de')

prepare() {
  cd $srcdir/pptp-$pkgver

  # use our CFLAGS and LDFLAGS
  sed -i -e "/CFLAGS  =/ c\CFLAGS = ${CFLAGS}" Makefile
  sed -i -e "/LDFLAGS =/ c\LDFLAGS = ${LDFLAGS}" Makefile

  # adjust ip path
  sed -i 's#/bin/ip#/usr/bin/ip#' routing.c

  # put binaries in /usr/bin
  sed -i "/BINDIR/s#/sbin#/bin#" Makefile
}

build() {
  cd $srcdir/pptp-$pkgver
  make 
}

package() {
  cd $srcdir/pptp-$pkgver
  make DESTDIR=$pkgdir install
}
