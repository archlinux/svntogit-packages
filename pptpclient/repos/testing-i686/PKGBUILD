# $Id$
# Maintainer: Daniel Isenmann <daniel @ archlinux.org>
# Contributor: Mark Nikkel <mnikkel@gmail.com>

pkgname=pptpclient
pkgver=1.7.2
pkgrel=4
pkgdesc="Client for the proprietary Microsoft Point-to-Point Tunneling Protocol, PPTP."
arch=('i686' 'x86_64')
url="http://pptpclient.sourceforge.net/"
license=('GPL')
depends=('ppp' 'iproute2')
backup=('etc/ppp/options.pptp')
source=("http://downloads.sourceforge.net/sourceforge/pptpclient/pptp-$pkgver.tar.gz")
md5sums=('4c3d19286a37459a632c7128c92a9857')

build() {
  cd $srcdir/pptp-$pkgver

  # use our CFLAGS and LDFLAGS
  sed -i -e "/CFLAGS  =/ c\CFLAGS = ${CFLAGS}" Makefile
  sed -i -e "/LDFLAGS =/ c\LDFLAGS = ${LDFLAGS}" Makefile

  # adjust ip path
  sed -i 's#/bin/ip#/usr/sbin/ip#' routing.c

  make 
}

package() {
  cd $srcdir/pptp-$pkgver
  make DESTDIR=$pkgdir install
}
