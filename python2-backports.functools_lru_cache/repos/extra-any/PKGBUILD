# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Crass00 <crass00 @ hotmail . com>

pkgname=python2-backports.functools_lru_cache
pkgver=1.5
pkgrel=1
pkgdesc="Backport of functools.lru_cache from Python 3.3"
arch=('any')
url="https://github.com/jaraco/backports.functools_lru_cache"
license=('MIT')
depends=('python2-backports')
makedepends=('python2-setuptools-scm')
checkdepends=('python2-pytest-runner')
optdepends=('python2-ipaddress: for IPAddress ServerAltnames support')
source=("https://pypi.io/packages/source/b/backports.functools_lru_cache/backports.functools_lru_cache-$pkgver.tar.gz")
sha512sums=('083a69beb050a6de0f838231ceae05cd1825f42310b2cd4f0e53f7ca043af4c699ad7042c32831ee365bb6b374d33189136e45689c77815b99773d145f0ef4f1')

build() {
  cd backports.functools_lru_cache-$pkgver
  python2 setup.py build
}

check() {
  cd backports.functools_lru_cache-$pkgver
  python2 setup.py pytest
}

package() {
  cd backports.functools_lru_cache-$pkgver
  python2 setup.py install --root="$pkgdir" --optimize=1

  rm "$pkgdir"/usr/lib/python2.7/site-packages/backports/__init__.py*
}
