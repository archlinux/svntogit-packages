# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Armin Luntzer
# Original opennx PKGBUILD: Tomas Groth tomasgroth.at.yahoo.dk

pkgname=opennx
pkgver=0.16.0.719
pkgrel=1
pkgdesc="A GPL replacement for the NoMachine client, patched to always show the session chooser"
url="http://opennx.sf.net/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('wxgtk' 'libcups' 'libxext' 'libxft' 'xorg-xauth' 'curl')
makedepends=('zip' 'opensc' 'libpulse' 'smbclient' 'libusb-compat')
#optdepends=('cups: for full local printing support')
source=(http://downloads.sourceforge.net/project/opennx/opennx/CI-source/opennx-$pkgver.tar.gz)
md5sums=('bb0ca1df36f4d9246c6f88d734be937b')

build() {
  cd $srcdir/opennx*
  ./configure --prefix=/usr \
    --enable-usbip
  make
}

package() {
  cd $srcdir/opennx*
  make DESTDIR=${pkgdir} install
  make DESTDIR=${pkgdir} install-man
  
  # fix some file locations
  install -dm755 ${pkgdir}/usr/share/applications
  cp -aR ${pkgdir}/usr/share/applnk/xdg/* ${pkgdir}/usr/share/applications
  rm -rf ${pkgdir}/usr/share/applnk
  
  sed -i -e "s:Exec=/usr/NX/bin/opennx:Exec=/usr/bin/opennx:" ${pkgdir}/usr/share/applications/*.desktop
}
