# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org> 

pkgname=discover
pkgver=5.11.5
pkgrel=2
pkgdesc='KDE and Plasma resources management GUI'
arch=(x86_64)
url='https://projects.kde.org/projects/kde/workspace/discover'
license=(LGPL)
depends=(knewstuff kdeclarative kitemmodels qt5-graphicaleffects appstream-qt archlinux-appstream-data
         hicolor-icon-theme kirigami2 qt5-quickcontrols)
makedepends=(extra-cmake-modules python plasma-framework packagekit-qt5 flatpak)
optdepends=('packagekit-qt5: to manage packages from Arch Linux repositories' 'flatpak: Flatpak packages support')
groups=(plasma)
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig} discover-no-deb-rpm.patch
        discover-pk-qt-1.0a.patch::"https://cgit.kde.org/discover.git/patch/?id=d1070951"
        discover-pk-qt-1.0b.patch::"https://cgit.kde.org/discover.git/patch/?id=243ad65f")
sha256sums=('b4cec7b23ee43b8c6caa2cb69ebbab903ce64351d846288eeb3afb1a25b9ba7b'
            'SKIP'
            '1910a7a95c8537cdf59fea87bdb4a4ac750d073c63182536358ef5c75508521d'
            '4a8a2e802b9b290082caacb2b8fa7439e3932dd056bb9f3939c51d8eee3799d7'
            'e8e0b153b2644747f692d056b1189857ee91657444fdb34d29d1d59bcc086b70')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell
              '348C8651206633FD983A8FC4DEACEA00075E1D76'  # KDE Neon
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
# Don't handle .rpm or .deb files
  patch -p1 -i ../discover-no-deb-rpm.patch
# Fix build with packagekit-qt 1.0.0
  patch -p1 -i ../discover-pk-qt-1.0a.patch
  patch -p1 -i ../discover-pk-qt-1.0b.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
