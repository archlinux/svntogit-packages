# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: damir <damir.archlinux.org>

pkgname=muparser
pkgver=2.2.4
pkgrel=1
pkgdesc="A fast math parser library"
arch=('i686' 'x86_64')
url="http://muparser.sourceforge.net/"
depends=('gcc-libs')
license=('custom')
options=('!makeflags')
source=("${pkgname}-${pkgver}.zip::https://drive.google.com/uc?export=download&id=0BzuB-ydOOoduejdwdTQwcF9JLTA")
sha1sums=('78db09680cafd5db4fd08a5d472afd2fa65f9274')

build() {
  cd $srcdir/${pkgname}_v${pkgver//./_}
  sh ./configure --prefix=/usr
  make
}

package() {
  cd $srcdir/${pkgname}_v${pkgver//./_}
  make DESTDIR=$pkgdir install

  # license
  install -D -m644 License.txt \
	$pkgdir/usr/share/licenses/${pkgname}/LICENSE
}
