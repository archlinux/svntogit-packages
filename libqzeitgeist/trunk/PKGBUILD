# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=libqzeitgeist
pkgver=0.8.0
pkgrel=5
pkgdesc="A Qt interface to the Zeitgeist event tracking system"
url='https://projects.kde.org/projects/kdesupport/libqzeitgeist'
arch=('i686' 'x86_64')
license=('GPL')
depends=('qt4')
makedepends=('cmake' 'automoc4' 'python2' 'zeitgeist')
source=("http://download.kde.org/stable/${pkgname}/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2"
        'set-declarative-installation-dir.patch')
md5sums=('97bdea6a1865db7d5f29c93e3a492f24'
         '712013e582acb73296e41ad842d0da01')

prepare() {
  mkdir build

  cd ${pkgname}-${pkgver}
  patch -p1 -i "${srcdir}"/set-declarative-installation-dir.patch

  # Fix python2 path
  sed -i 's|/usr/bin/env python|/usr/bin/env python2|' \
    scripts/onto2cpp.py
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DDECLARATIVE_IMPORT_PREFIX=/usr/lib/qt4/imports
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
