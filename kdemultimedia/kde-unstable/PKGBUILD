# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kdemultimedia
pkgname=('kdemultimedia-dragonplayer'
         'kdemultimedia-ffmpegthumbs'
         'kdemultimedia-juk'
         'kdemultimedia-kioslave'
         'kdemultimedia-kmix'
         'kdemultimedia-kscd'
         'kdemultimedia-mplayerthumbs')
pkgver=4.4.90
pkgrel=1
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdemultimedia')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'kdebase-runtime' 'tunepimp' 'ffmpeg'
	'mplayer' 'docbook-xsl')
source=("http://download.kde.org/unstable/${pkgver}/src/${pkgbase}-${pkgver}.tar.bz2"
        'mplayerthumbs.config')
sha1sums=('9622c0a7aeef17f411afb5b8470c165fb5350bab'
          'ba016fa2563c14ffcba852c62506b66bfc6ee683')

build() {
	cd $srcdir
	mkdir build
	cd build
	cmake ../${pkgbase}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DWITH_Xine=OFF \
		-DWITH_PulseAudio=OFF
	make
}

package_kdemultimedia-dragonplayer() {
	pkgdesc='Video Player'
	depends=('kdebase-runtime')
	url="http://kde.org/applications/multimedia/dragonplayer/"
	install='kdemultimedia.install'
	cd $srcdir/build/dragonplayer
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/dragonplayer
	make DESTDIR=$pkgdir install
}

package_kdemultimedia-ffmpegthumbs() {
	pkgdesc='Video Files (ffmpegthumbs)'
	depends=('kdebase-runtime' 'ffmpeg')
	install='kdemultimedia.install'
	cd $srcdir/build/ffmpegthumbs
	make DESTDIR=$pkgdir install
}

package_kdemultimedia-juk() {
	pkgdesc='Music Player'
	depends=('kdebase-runtime' 'tunepimp')
	url="http://kde.org/applications/multimedia/juk"
	install='kdemultimedia.install'
	cd $srcdir/build/juk
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/juk
	make DESTDIR=$pkgdir install
}

package_kdemultimedia-kioslave() {
	pkgdesc='Audio CD Browser'
	depends=('kdelibs' 'musicbrainz' 'cdparanoia')
	conflicts=('kdemultimedia-libkcddb' 'kdemultimedia-libkcompactdisc')
	replaces=('kdemultimedia-libkcddb' 'kdemultimedia-libkcompactdisc')
	for i in kioslave doc/kioslave libkcddb libkcompactdisc; do
		cd $srcdir/build/${i}
		make DESTDIR=$pkgdir install
	done
}

package_kdemultimedia-kmix() {
	pkgdesc='Sound Mixer'
	depends=('kdebase-runtime')
	url="http://kde.org/applications/multimedia/kmix"
	install='kdemultimedia.install'
	cd $srcdir/build/kmix
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kmix
	make DESTDIR=$pkgdir install
}

package_kdemultimedia-kscd() {
	pkgdesc='CD Player'
	depends=('kdebase-runtime' 'musicbrainz')
	url="http://kde.org/applications/multimedia/kscd"
	install='kdemultimedia.install'
	cd $srcdir/build/kscd
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kcontrol/cddbretrieval
	make DESTDIR=$pkgdir install
}

package_kdemultimedia-mplayerthumbs() {
	pkgdesc='Video Files (MPlayerThumbs)'
	depends=('kdebase-runtime' 'mplayer')
	replaces=('mplayerthumbs')
	conflicts=('mplayerthumbs')
	cd $srcdir/build/mplayerthumbs
	make DESTDIR=$pkgdir install
	install -D -m644 $srcdir/mplayerthumbs.config $pkgdir/usr/share/config/mplayerthumbs
}
