# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=soprano
pkgver=2.9.4
pkgrel=1
pkgdesc='A library which provides a highly usable object-oriented C++/Qt4 framework for RDF data'
arch=('i686' 'x86_64')
url='http://soprano.sourceforge.net/'
license=('GPL' 'LGPL')
depends=('qt4' 'libiodbc' 'virtuoso-base' 'redland')
makedepends=('cmake' 'java-environment' 'doxygen')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.bz2")
md5sums=('91413e5783efeee0beb747ea4c5c9ef0')

prepare() {
  mkdir build
}

build() {
  cd build
  # we need the rpath
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DSOPRANO_DISABLE_CLUCENE_INDEX=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
