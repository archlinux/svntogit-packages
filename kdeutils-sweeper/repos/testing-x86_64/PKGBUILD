# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeutils-sweeper
pkgver=14.12.2
pkgrel=1
pkgdesc='System Cleaner'
url='http://kde.org/applications/utilities/sweeper'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
groups=('kde' 'kdeutils')
source=("http://download.kde.org/stable/applications/${pkgver}/src/sweeper-${pkgver}.tar.xz")
sha1sums=('68f4b28a5f25c643aa228e1e3107495841d0ee59')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../sweeper-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $srcdir/build
  make DESTDIR=$pkgdir install
}
