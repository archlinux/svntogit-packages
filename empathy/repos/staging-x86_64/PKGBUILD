# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=empathy
pkgver=3.12.10
pkgrel=2
pkgdesc="A GNOME instant messaging client using the Telepathy framework."
arch=(i686 x86_64)
url="http://live.gnome.org/Empathy"
license=(GPL2)
depends=(clutter-gst2 clutter-gtk folks gcr iso-codes libcanberra libpulse webkitgtk
         telepathy-farstream telepathy-glib telepathy-logger telepathy-mission-control geoclue2
         geocode-glib cheese libchamplain libgudev)
makedepends=(intltool itstool docbook-xsl python2)
optdepends=('telepathy-gabble: XMPP/Jabber support'
            'telepathy-idle: IRC support'
            'telepathy-salut: Link-local XMPP support'
            'telepathy-rakia: SIP support'
            'telepathy-haze: libpurple support')
options=('!makeflags')
groups=(gnome)
install=empathy.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('794140c3743aa83739ef7efa9eff1950e2f718fde9dc29064fa9d616478e97cb')

build() {
  cd $pkgname-$pkgver
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr \
      --sysconfdir=/etc  \
      --libexecdir=/usr/lib/empathy \
      --disable-static
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
