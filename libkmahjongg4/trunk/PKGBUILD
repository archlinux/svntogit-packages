# $Id: PKGBUILD 231086 2015-02-07 09:42:34Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=libkmahjongg4
pkgver=14.12.3
pkgrel=2
pkgdesc="Common code, backgrounds and tile sets for games using Mahjongg tiles"
url="https://projects.kde.org/projects/kde/kdegames/libmahjongg"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdelibs')
makedepends=('cmake' 'automoc4')
conflicts=('libkmahjongg<15')
source=("http://download.kde.org/Attic/applications/${pkgver}/src/libkmahjongg-${pkgver}.tar.xz")
sha1sums=('511983c1b92e391e24909b04d3ae2817f720e661')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../libkmahjongg-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
