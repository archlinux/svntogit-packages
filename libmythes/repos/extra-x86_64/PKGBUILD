# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>

pkgname=libmythes
pkgver=1.2.1
pkgrel=1
pkgdesc="a simple thesaurus, contains static library and header file needed to develop applications using mythes + perl script to create index files"
arch=('i686' 'x86_64')
url="http://hunspell.sourceforge.net/ "
license=('custom')
depends=('glibc' 'perl')
options=('force' '!libtool')
provides=('mythes')
source=(http://downloads.sourceforge.net/hunspell/${pkgname/lib/}-${pkgver}.tar.gz)
md5sums=('54b310488dda6929cf31ae859928c945')

build() {
  cd ${srcdir}/${pkgname/lib/}-$pkgver
  ./configure --prefix=/usr --disable-static
  make
  # run the example program:
  ./example th_en_US_new.idx th_en_US_new.dat checkme.lst
  # run the example program with stemming and morphological generation:
  # e.g. to check mouse, mice, rodents, eats, eaten, ate, eating etc. words
  ./example morph.idx morph.dat morph.lst morph.aff morph.dic
}

package() {
  cd ${srcdir}/${pkgname/lib/}-$pkgver
  make DESTDIR=$pkgdir install
  # license
  install -Dm644 ${srcdir}/${pkgname/lib/}-$pkgver/COPYING $pkgdir/usr/share/licenses/$pkgname/COPYING
}
