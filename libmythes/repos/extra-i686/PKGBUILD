# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>

pkgname=libmythes
pkgver=1.2.4
pkgrel=2
epoch=1
pkgdesc="a simple thesaurus"
arch=('i686' 'x86_64')
url="http://hunspell.sourceforge.net/ "
license=('custom')
depends=('glibc' 'perl')
makedepends=('hunspell')
provides=('mythes')
source=(http://downloads.sourceforge.net/hunspell/${pkgname/lib/}-${pkgver}.tar.gz)
sha1sums=('c038831d84882cdf639e038e44decb1e40527591')

build() {
  cd ${pkgname/lib/}-$pkgver
  ./configure --prefix=/usr --disable-static
  make
}

check() {
  cd ${pkgname/lib/}-$pkgver
  # run the example program:
  ./example th_en_US_new.idx th_en_US_new.dat checkme.lst
  # run the example program with stemming and morphological generation:
  # e.g. to check mouse, mice, rodents, eats, eaten, ate, eating etc. words
  ./example morph.idx morph.dat morph.lst morph.aff morph.dic
}

package() {
  cd ${pkgname/lib/}-$pkgver
  make DESTDIR=$pkgdir install
  # license
  install -Dm644 ${srcdir}/${pkgname/lib/}-$pkgver/COPYING $pkgdir/usr/share/licenses/$pkgname/COPYING
}
