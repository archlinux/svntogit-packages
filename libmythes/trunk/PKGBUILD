# Maintainer: AndyRTR <andyrtr@archlinux.org>

pkgname=libmythes
pkgver=1.2.4
pkgrel=3
epoch=1
pkgdesc="a simple thesaurus"
arch=('x86_64')
url="http://hunspell.sourceforge.net/ "
license=('custom')
depends=('glibc' 'perl')
makedepends=('hunspell')
provides=('mythes')
source=(http://downloads.sourceforge.net/hunspell/${pkgname/lib/}-${pkgver}.tar.gz)
sha512sums=('a04da39812bcfb1391a2cba7de73e955eafe141679ec03ed6657d03bebf360b432480d0037dff9ed72a1dfda5a70d77d44ac2bb14cdb109fd8e2a38376feee21')

build() {
  cd ${pkgname/lib/}-$pkgver
  ./configure --prefix=/usr --disable-static
  make
}

check() {
  cd ${pkgname/lib/}-$pkgver
  # run the example program:
  ./example th_en_US_new.idx th_en_US_new.dat checkme.lst
  # run the example program with stemming and morphological generation:
  # e.g. to check mouse, mice, rodents, eats, eaten, ate, eating etc. words
  ./example morph.idx morph.dat morph.lst morph.aff morph.dic
}

package() {
  cd ${pkgname/lib/}-$pkgver
  make DESTDIR=$pkgdir install
  # license
  install -Dm644 ${srcdir}/${pkgname/lib/}-$pkgver/COPYING $pkgdir/usr/share/licenses/$pkgname/COPYING
}
