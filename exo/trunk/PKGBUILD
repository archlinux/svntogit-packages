# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=exo
pkgver=0.10.2
pkgrel=3
pkgdesc="Extensions to Xfce by os-cillation"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('GPL2' 'LGPL')
groups=('xfce4')
depends=('libxfce4ui' 'gtk2' 'hicolor-icon-theme' )
makedepends=('perl-uri' 'intltool' 'gtk-doc' 'xorg-iceauth')
optdepends=('perl-uri: for mail-compose helper script')
install=$pkgname.install
source=(http://archive.xfce.org/src/xfce/$pkgname/0.10/$pkgname-$pkgver.tar.bz2
        0001-Fix-exo_str_looks_like_an_uri-bug-10098.patch
        0001-Wrap-URIs-with-quotes-in-exo-helpers-to-better-handl.patch
        0001-Drop-use-of-the-remote-option-in-Firefox-helpers.patch)
sha256sums=('b385828bf8a38204da3254b57fdfa25a72694495aa189fabd5040f707eec76ce'
            'f3a92ea399c5786845d3c3a9d74a427299ecd5ed7e5ef2ac1cf636492050e247'
            '7d73621d9a94609490b207d2229609997443a9850d7db56394125f1fed533cac'
            '9ef87f55e7d3cb1e0bc38797683063f571021d32319db8b69237a5105d8206c3')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # https://bugzilla.xfce.org/show_bug.cgi?id=10098
  patch -Np1 -i "$srcdir/0001-Fix-exo_str_looks_like_an_uri-bug-10098.patch"

  # https://bugzilla.xfce.org/show_bug.cgi?id=11601
  patch -Np1 -i "$srcdir/0001-Wrap-URIs-with-quotes-in-exo-helpers-to-better-handl.patch"
  patch -Np1 -i "$srcdir/0001-Drop-use-of-the-remote-option-in-Firefox-helpers.patch"
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Python 2 fix
  export PYTHON=python2

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/xfce4 \
    --localstatedir=/var  \
    --disable-static \
    --enable-gtk-doc \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
