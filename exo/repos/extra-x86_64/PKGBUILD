# $Id$
# Maintainer: tobias <tobias funnychar archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=exo
pkgver=0.3.105
pkgrel=2
pkgdesc="Extensions to Xfce by os-cillation"
arch=('i686' 'x86_64')
license=('GPL2' 'LGPL')
url="http://www.xfce.org/projects/exo"
groups=('xfce4')
depends=("libxfce4util>=4.6.1" "hal>=0.5.13-2" 'hicolor-icon-theme' 'sh')
makedepends=("pygtk>=2.14.1" 'pkgconfig' 'libnotify' 'perl-uri' 'intltool')
optdepends=('libnotify		notification support for exo-mount-notify' \
            'perl-uri		for mail-compose helper script')
options=('!libtool')
install=${pkgname}.install
source=(http://archive.xfce.org/src/xfce/${pkgname}/0.3/${pkgname}-${pkgver}.tar.bz2
	fix_usb_throughput.patch)
md5sums=('ba87f84894498bf23ae39d849c55f98c'
         '0be6fb1242d687ae587880b4a10218fe')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  # fix http://bugs.archlinux.org/task/16750
  patch -Np1 -i $srcdir/fix_usb_throughput.patch || return 1
  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib/xfce4 \
    --localstatedir=/var --disable-static --enable-notifications \
    --enable-python || return 1
  make || return 1
  make DESTDIR=${pkgdir} install || return 1
}
