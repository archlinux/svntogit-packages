# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: tobias <tobias funnychar archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=exo
pkgver=0.5.6
pkgrel=1
pkgdesc="Extensions to Xfce by os-cillation"
arch=('i686' 'x86_64')
license=('GPL2' 'LGPL')
url="http://www.xfce.org/projects/exo"
depends=("libxfce4util>=4.7.3" 'gtk2' 'hicolor-icon-theme' )
makedepends=('pygtk' 'perl-uri' 'intltool' 'gtk-doc' 'xorg-iceauth')
optdepends=('perl-uri: for mail-compose helper script')
options=('!libtool')
install=${pkgname}.install
source=(http://archive.xfce.org/src/xfce/${pkgname}/0.5/${pkgname}-${pkgver}.tar.bz2)
md5sums=('f739b307e656b908fa0d26d568979d7e')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # python2 fix
  export PYTHON=python2
  ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--libexecdir=/usr/lib/xfce4 \
	--localstatedir=/var  \
	--disable-static \
	--enable-gio-unix \
	--enable-python \
	--enable-gtk-doc \
	--disable-debug
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
