# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=dleyna-connector-dbus
pkgver=0.3.0
pkgrel=2
pkgdesc="Provides a D-Bus API for the dLeyna services"
arch=(x86_64)
url="http://01.org/dleyna"
license=(LGPL2.1)
depends=(dleyna-core dbus)
makedepends=(git)
_commit=1f0932aaacdd67700912a0c9c6e610f16f346ced  # tags/0.3.0
source=("git+https://github.com/01org/dleyna-connector-dbus#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
