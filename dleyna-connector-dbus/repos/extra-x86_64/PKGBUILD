# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=dleyna-connector-dbus
pkgver=0.2.0
pkgrel=2
pkgdesc="Provides a D-Bus API for the dLeyna services"
arch=(i686 x86_64)
url="http://01.org/dleyna"
license=(LGPL2.1)
depends=(dleyna-core dbus)
source=(https://01.org/sites/default/files/downloads/dleyna/$pkgname-$pkgver.tar.gz
        crash.patch)
sha256sums=('e791511206761282a658bb8253de076dcb1f8c8fbdc81219efe5ffe80cb2bea5'
            'cf44774ba06160dbd6c8aa5f1a3caae0f054f44c3ae4613a8d17aa8464ee9195')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../crash.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
