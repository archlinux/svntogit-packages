# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=dleyna-connector-dbus
pkgver=0.4.0
pkgrel=1
pkgdesc="Provides a D-Bus API for the dLeyna services"
url="https://github.com/phako/dleyna-connector-dbus"
arch=(x86_64)
license=(LGPL2.1)
depends=(dleyna-core dbus)
makedepends=(git meson)
_commit=a7b7e1fdacc4a43efbbce2189ce0d65788380ee1  # tags/v0.4.0^0
source=("git+https://github.com/phako/dleyna-connector-dbus#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
