# $Id$
# Maintainer:
# Contributor: xduugu
# Contributor: Manolis Tzanidakis
# Contributor: Jonathan Schmidt <j.schmidt@archlinux.us

pkgname=acpid
pkgver=2.0.13
pkgrel=1
pkgdesc="A daemon for delivering ACPI power management events with netlink support"
arch=('i686' 'x86_64')
url="http://tedfelix.com/linux/acpid-netlink.html"
license=('GPL')
depends=('bash')
replaces=('acpid2')
backup=('etc/acpi/handler.sh' 'etc/acpi/events/anything' 'etc/conf.d/acpid')
source=("http://www.tedfelix.com/linux/$pkgname-$pkgver.tar.gz"
        'acpid'
        'anything'
        'handler.sh'
        'acpid.conf.d')
md5sums=('5bbbb9a320192df1cdd8d0e9a0196379'
         'd69203a032c4583f9abaafcf21a7ed84'
         '2d37b98d6e74bab815604b8b48c6cfd4'
         'd411b758e8531adee191b66bcbc2892d'
         '929c6d2e91295c22ed9ec6212d7eabef')

build() {
  cd "${srcdir}"/$pkgname-$pkgver
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver
  make DESTDIR="${pkgdir}" install

  install -Dm755 "$srcdir/acpid" "$pkgdir/etc/rc.d/acpid"
  install -Dm644 "$srcdir/anything" "$pkgdir/etc/acpi/events/anything"
  install -Dm755 "$srcdir/handler.sh" "$pkgdir/etc/acpi/handler.sh"
  install -Dm644 "$srcdir/acpid.conf.d" "$pkgdir/etc/conf.d/acpid"

  chmod 755 "${pkgdir}"/usr/sbin/acpid
}
