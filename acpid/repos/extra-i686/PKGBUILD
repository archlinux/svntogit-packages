# $Id$
# Maintainer: Thayer Williams <thayer@archlinux.org>
# Contributor: Manolis Tzanidakis
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=acpid
pkgver=1.0.10
pkgrel=3
pkgdesc="A daemon for delivering ACPI power management events"
arch=('i686' 'x86_64')
url="http://acpid.sourceforge.net"
license=('GPL')
depends=(glibc)
source=(http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz
    acpid
    acpid.conf.d
    anything
    handler.sh)
backup=('etc/acpi/handler.sh' 'etc/acpi/events/anything' 'etc/conf.d/acpid')

build() {
    cd $srcdir/$pkgname-$pkgver
    /usr/bin/make || return 1
    /bin/install -d -m0755 $pkgdir/usr/bin || return 1
    /usr/bin/make INSTPREFIX=$pkgdir \
        MAN8DIR=$pkgdir/usr/share/man/man8 install || return 1

    # install our supplementary scripts
    /bin/install -D -m0755 $srcdir/acpid $pkgdir/etc/rc.d/acpid || return 1
    /bin/install -D -m0644 $srcdir/acpid.conf.d $pkgdir/etc/conf.d/acpid || return 1
    /bin/install -D -m0644 $srcdir/anything $pkgdir/etc/acpi/events/anything || return 1
    /bin/install -D -m0755 $srcdir/handler.sh $pkgdir/etc/acpi/handler.sh || return 1

    # set world readable bit on the acpid binary
    /bin/chmod 755 $pkgdir/usr/sbin/acpid || return 1
}

md5sums=('61156ef32015c56dc0f2e3317f4ae09e'
         'd9ca7f71f520238a0448fab105a23fe9'
         '91fdb3709c878eed757d192a420251a1'
         '2d37b98d6e74bab815604b8b48c6cfd4'
         '0e8dd13793b1baa79a745f4034888367')
