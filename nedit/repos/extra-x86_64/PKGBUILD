# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=nedit
pkgver=5.6
pkgrel=1
pkgdesc="A Unix text editor for programmers and general users"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/nedit/"
license=('GPL')
depends=('lesstif')
options=('!makeflags')
source=(http://sourceforge.net/projects/nedit/files/nedit-source/${pkgname}-${pkgver}-src.tar.gz)
sha1sums=('133641224b0969e31e3404b874c71c35341f028b')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -i 's|fgets|//fgets|' util/check_lin_tif.c
  sed -i 's|"/bin/csh"|"/bin/sh"|' source/preferences.c
}

build() {
  cd ${pkgname}-${pkgver}
  make CFLAGS+="${CFLAGS} -DBUILD_UNTESTED_NEDIT" linux docs
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm755 source/nedit "${pkgdir}/usr/bin/nedit"
  install -Dm755 source/nc "${pkgdir}/usr/bin/nedit-client"
  install -Dm644 doc/nedit.man "${pkgdir}/usr/share/man/man1/nedit.1"
  install -Dm644 doc/nc.man "${pkgdir}/usr/share/man/man1/nedit-client.1"
  install -Dm644 doc/nedit.html "${pkgdir}/usr/share/doc/nedit/nedit.html"
}
