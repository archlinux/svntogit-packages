# $Id$
# Contributor: Kevin Piche <kevin@archlinux.org>
# Contributor: Manolis Tzanidakis

pkgname=stunnel
pkgver=4.31
pkgrel=1
pkgdesc="A program that allows you to encrypt arbitrary TCP connections inside SSL."
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.stunnel.org"
depends=('openssl' 'tcp_wrappers')
source=(http://www.stunnel.org/download/stunnel/src/${pkgname}-${pkgver}.tar.gz
        'stunnel-nopem.patch'
        'stunnel')
options=('!libtool')
install="$pkgname.install"
md5sums=('2fc31bc0c940fbe545a88d896b13cacf'
         'aec994e209405420eed197fd15ca09b0'
         'cb647c71ff4cb1e035b6e515d5f13ebf')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # don't create a certificate...
  patch -p1 -i ${srcdir}/stunnel-nopem.patch || return 1

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --with-tcp-wrappers \
    --localstatedir=/var
  make || return 1
  make DESTDIR=${pkgdir} install

  for i in ca.pl importCA.sh; do
    install -D -m 755 tools/$i ${pkgdir}/usr/share/stunnel/$i
  done
  install -D -m 755 ${srcdir}/stunnel ${pkgdir}/etc/rc.d/stunnel
  sed -e "s:/usr/var/lib/stunnel/:/var/run/stunnel:g" \
           -e "s:/usr/etc/stunnel/:/etc/stunnel/:g" \
           -e "s:nobody:stunnel:g" -e "s:nogroup:stunnel:g" \
           -i ${pkgdir}/etc/stunnel/stunnel.conf-sample
  install -D -m 644 tools/stunnel.cnf \
    ${pkgdir}/etc/stunnel/stunnel.cnf || return 1
}
# vim: ts=2 sw=2 et ft=sh
