# $Id: PKGBUILD 13455 2008-09-27 11:03:58Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=libcanberra
pkgname=(libcanberra libcanberra-pulse)
pkgver=0.26
pkgrel=3
pkgdesc="A small and lightweight implementation of the XDG Sound Theme Specification"
arch=(i686 x86_64)
license=('LGPL')
depends=('libvorbis>=1.3.1' 'gstreamer0.10>=0.10.30' 'libtool>=2.2.10' 'gtk2>=2.21.8' 'alsa-lib>=1.0.23' 'tdb>=1.2.1')
makedepends=('gtk-doc' 'libpulse')
options=(!emptydirs)
url=http://0pointer.de/lennart/projects/libcanberra
source=(http://0pointer.de/lennart/projects/${pkgbase}/${pkgbase}-${pkgver}.tar.gz
        libcanberra-gtk-module.sh)
md5sums=('ee2c66ada7c851a4e7b6eb1682285a24'
         'a54799e624aac814b9343ab05f25c38b')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  ./configure --sysconfdir=/etc --prefix=/usr --localstatedir=/var \
      --disable-static --with-builtin=dso --enable-null --disable-oss \
      --enable-alsa --enable-gstreamer --enable-pulse
  make
}

package_libcanberra() {
  install=libcanberra.install
  optdepends=("$pkgbase-pulse: PulseAudio driver")

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make -j1 DESTDIR="${pkgdir}" install
  rm -f "${pkgdir}/usr/lib/libcanberra-gtk.la"
  rm -f "${pkgdir}/usr/lib/gtk-2.0/modules/"*.la
  
  install -m755 -d "${pkgdir}/usr/share/gconf" 
  mv "${pkgdir}/etc/gconf/schemas" "${pkgdir}/usr/share/gconf/"

  install -m755 -d "${pkgdir}/etc/X11/xinit/xinitrc.d"
  install -m755 "${srcdir}/libcanberra-gtk-module.sh" "${pkgdir}/etc/X11/xinit/xinitrc.d/40-libcanberra-gtk-module"

  # Split libcanberra-pulse
  mkdir pulse-plugin
  mv "${pkgdir}"/usr/lib/${pkgbase}-${pkgver}/${pkgbase}-pulse.* pulse-plugin
}

package_libcanberra-pulse() {
  pkgdesc="PulseAudio plugin for libcanberra"
  depends=("$pkgbase=$pkgver-$pkgrel" 'libpulse')
  groups=('pulseaudio-gnome')

  cd "${srcdir}/${pkgbase}-${pkgver}"

  mkdir -p "${pkgdir}/usr/lib/${pkgbase}-${pkgver}"
  mv pulse-plugin/* "${pkgdir}/usr/lib/${pkgbase}-${pkgver}" 
}

