# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>

pkgname=libxshmfence
pkgver=1.2
pkgrel=1
pkgdesc="a library that exposes a event API on top of Linux futexes"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org/"
license=('GPL')
#groups=()
depends=('glibc')
makedepends=('xorg-util-macros' 'xproto')
source=(${url}/releases/individual/lib/${pkgname}-${pkgver}.tar.bz2{,.sig})
sha256sums=('d21b2d1fd78c1efbe1f2c16dae1cb23f8fd231dcf891465b8debe636a9054b0c'
            'SKIP')
validpgpkeys=('C383B778255613DFDB409D91DB221A6900000011') # Keith Packard <keithp@keithp.com>

build() {
	cd $pkgname-$pkgver
	
	# fix some libtools errors
	#autoreconf -vfi
	
	./configure --prefix=/usr
	make
}

check() {
	cd $pkgname-$pkgver
	make -k check
}

package() {
	cd $pkgname-$pkgver
	make DESTDIR="$pkgdir/" install
	install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
	install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
