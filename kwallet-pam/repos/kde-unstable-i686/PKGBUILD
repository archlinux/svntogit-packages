# $Id$
# Maintainer:Antonio Rojas <arojas@archlinux.org>

pkgname=kwallet-pam
pkgver=5.5.0
pkgrel=1
pkgdesc='KWallet PAM integration'
arch=(i686 x86_64)
url='https://projects.kde.org/kwallet-pam'
license=(LGPL)
depends=(pam libgcrypt socat)
makedepends=(extra-cmake-modules)
groups=(plasma)
source=("http://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz")
md5sums=('d2107eb680d01256fd1938a97331fc62')

prepare() {
  mkdir -p build{,4}
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  make

  cd ../build4
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKWALLET4=1
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
  cd ../build4
  make DESTDIR="$pkgdir" install
}
