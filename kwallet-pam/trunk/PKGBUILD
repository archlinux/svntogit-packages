# $Id$
# Maintainer:Antonio Rojas <arojas@archlinux.org>

pkgname=kwallet-pam
pkgver=5.6.5
pkgrel=1
pkgdesc='KWallet PAM integration'
arch=(i686 x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
depends=(pam libgcrypt socat)
makedepends=(extra-cmake-modules qt5-base)
groups=(plasma)
source=("http://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('f58dbd266ac166164a137878c5e69c0cf8a8693ce17d45a7d94b29cb67c2a229'
            'SKIP')
validpgpkeys=('13C16D03EDE728514473AA73A506E6D4DD4D5088') # Jonathan Riddell

prepare() {
  mkdir -p build{,4}
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_LIBEXECDIR=lib
  make

  cd ../build4
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_LIBEXECDIR=lib \
    -DKWALLET4=1
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
  cd ../build4
  make DESTDIR="$pkgdir" install
}
