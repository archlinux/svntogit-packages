# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-netstatus
pkgver=2.28.2
pkgrel=1
pkgdesc="A GNOME applet that shows network status"
arch=(i686 x86_64)
license=('GPL')
url="http://www.gnome.org"
depends=('gnome-panel-bonobo>=2.32.0' 'hicolor-icon-theme')
makedepends=('intltool' 'pkgconfig' 'gnome-doc-utils>=0.20.2')
options=(!emptydirs)
groups=('gnome-extra')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.28/${pkgname}-${pkgver}.tar.bz2)
install=gnome-netstatus.install
sha256sums=('d1ad0a668a1344898d4f47b5591adddc7791edb6c2e92136ebcf8d278d959119')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/gnome-netstatus \
      --localstatedir=/var --disable-static \
      --disable-scrollkeeper
  make
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gnome-netstatus ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
