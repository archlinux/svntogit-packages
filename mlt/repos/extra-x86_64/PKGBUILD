# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Fabian Schoelzel <myfirstname.mylastname@googlemail.com>
# Contributor: funkyou <spamopfer@nickname.berlin.de>
# Contributor: tardo <tardo@nagi-fanboi.net>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgbase=mlt
pkgname=(mlt mlt-python-bindings)
pkgver=6.6.0
pkgrel=1
pkgdesc="An open source multimedia framework"
arch=(x86_64)
url="http://www.mltframework.org"
license=(GPL)
makedepends=(ladspa frei0r-plugins libdv sdl_image libsamplerate sox ffmpeg vid.stab qt5-svg
             jack libexif python2 swig movit eigen opencv libebur128)
source=($pkgname-$pkgver.tar.gz::"https://github.com/mltframework/mlt/archive/v$pkgver.tar.gz")
sha256sums=('11515546516b3c54719b6b402cacf46e8d5172450d6e9fe2655b237582490315')

build() {
  # mlt
  cd mlt-$pkgver

  ./configure --prefix=/usr \
	--avformat-swscale \
	--enable-gpl --enable-gpl3 \
	--qt-libdir=/usr/lib --qt-includedir=/usr/include/qt
  make

  # mlt python bindings
  cd src/swig/python
  sed -i 's_path=`which python_path=`which python2_' build
  sed -i 's_`python -c_`python2 -c_' build
  sed -i 's#python-config#python2-config#' build
  ./build
}

package_mlt() {
  optdepends=('sdl_image: for SDL plugin'
	'libsamplerate: for libavresample plugin'
	'sox: for SOX (Audio Swiss Army Knife) plugin'
	'ffmpeg: for ffmpeg plugin'
	'vid.stab: for video stabilize plugin'
	'gtk2: Gtk plugin'
	'qt5-svg: for Qt5 plugins'
	'jack: for JACK sound output plugin'
        'ladspa: for LADSPA plugins'
	'libexif: for auto rotate plugin'
	'frei0r-plugins: for additional effects'
	'movit: opengl plugin'
        'opencv: openCV plugin'
        'libebur128: plus module')

  cd mlt-$pkgver
  make DESTDIR="$pkgdir" install
}

package_mlt-python-bindings() {
  depends=(python2 mlt)

  cd "$srcdir/mlt-$pkgver/src/swig/python"
  mkdir -p "$pkgdir/usr/lib/python2.7/"
  install -m755 mlt.py "$pkgdir/usr/lib/python2.7/"
  install -m755 _mlt.so "$pkgdir/usr/lib/python2.7/"
  install -m755 mlt_wrap.o "$pkgdir/usr/lib/python2.7/"
}
