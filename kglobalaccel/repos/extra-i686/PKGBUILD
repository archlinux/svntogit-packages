# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kglobalaccel
pkgver=5.5.0
pkgrel=1
pkgdesc='Add support for global workspace shortcuts'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kglobalaccel'
license=('LGPL')
depends=('qt5-x11extras')
makedepends=('extra-cmake-modules' 'qt5-tools')
groups=('kf5')
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
md5sums=('6324dff31269befafe0617eb62ca2ea8')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
