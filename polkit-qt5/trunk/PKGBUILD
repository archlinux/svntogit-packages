# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Antonio Rojas <nqn1976 @ gmail.com>

pkgname=polkit-qt5
pkgver=0.103.0.bac771e
pkgrel=1
pkgdesc='Qt wrapper around polkit-1 client libraries'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kdesupport/polkit-qt-1'
license=('LGPL')
depends=('polkit' 'qt5-base')
makedepends=('extra-cmake-modules' 'git')
source=('git://anongit.kde.org/polkit-qt-1#commit=bac771e69887c9253f2b0973f6310810db0061f8')
md5sums=('SKIP')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../polkit-qt-1 \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
