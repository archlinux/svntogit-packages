# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>
# Contributor: K. Piche <kpiche@rogers.com>

pkgname=r
pkgver=2.13.2
pkgrel=1
pkgdesc="Language and environment for statistical computing and graphics"
arch=('i686' 'x86_64')
license=('GPL')
url=('http://www.r-project.org/')
depends=('blas' 'lapack' 'bzip2'  'libpng' 'libjpeg' 'libtiff'
         'ncurses' 'pcre' 'readline' 'zlib' 'perl' 'gcc-libs'
         'tk' 'libxt' 'libxmu' 'pango' 'xz')
makedepends=('openjdk6' 'gcc-fortran')
backup=('etc/R/Makeconf' 'etc/R/Renviron' 'etc/R/ldpaths' 'etc/R/repositories')
options=('!makeflags')
source=("http://cran.r-project.org/src/base/R-2/R-${pkgver}.tar.gz"
	'r.desktop'
	'r.png')
install=r.install
sha1sums=('3c8e3055b2d76cf79627325af8846848ca9a7e90'
          '13aa29219bcaa102e575de8c1c8e0833d233e836'
          'a69a07ec363440efc18ce0a7f2af103375dea978')
build() {
   cd ${srcdir}/R-${pkgver}
   sed -i 's|#define NeedFunctionPrototypes 0|#define NeedFunctionPrototypes 1|g' src/modules/X11/dataentry.c
   ./configure  --prefix=/usr \
		--libdir=/usr/lib \
		--sysconfdir=/etc/R \
		--datarootdir=/usr/share \
		  rsharedir=/usr/share/R/ \
		  rincludedir=/usr/include/R/ \
		  rdocdir=/usr/share/doc/R/ \
                --with-x \
		--enable-R-shlib \
                --with-lapack \
		--with-blas \
                F77=gfortran \
		LIBnn=lib
   make
}

package() {
   cd ${srcdir}/R-${pkgver}
   make -j1 DESTDIR=${pkgdir} install

   #  Fixup R wrapper scripts.
   sed -i "s|${pkgdir} ||" ${pkgdir}/usr/bin/R
   rm ${pkgdir}/usr/lib/R/bin/R
   cd ${pkgdir}/usr/lib/R/bin
   ln -s ../../../bin/R

  # install some freedesktop.org compatibility
  install -Dm644 ${srcdir}/r.desktop \
	${pkgdir}/usr/share/applications/r.desktop
  install -Dm644 ${srcdir}/r.png \
	${pkgdir}/usr/share/pixmaps/r.png

  # move the config directory to /etc and create symlinks
  install -d ${pkgdir}/etc/R
  cd ${pkgdir}/usr/lib/R/etc
  for i in *; do
    mv -f ${i} ${pkgdir}/etc/R
    ln -s /etc/R/${i} ${i}
  done
}
