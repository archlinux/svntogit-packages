# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=totem-plparser
pkgver=3.10.3
pkgrel=1
url="http://www.gnome.org"
pkgdesc="Totem playlist parser library"
license=('LGPL')
arch=(i686 x86_64)
depends=('gmime' 'libsoup' 'libarchive')
makedepends=('intltool' 'gobject-introspection' 'libquvi' 'python2')
optdepends=('libquvi: video site handling (Youtube etc.)')
source=(http://ftp.gnome.org/pub/gnome/sources/totem-pl-parser/${pkgver:0:4}/totem-pl-parser-$pkgver.tar.xz)
sha256sums=('14512c76c7f375d8bb9e9a220afbac20be2d2eb82abee9a8986d264079a0c72f')

build() {
  cd totem-pl-parser-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/usr/lib/$pkgname --disable-static
  make
}

package(){
  cd totem-pl-parser-$pkgver
  make DESTDIR="$pkgdir" install
}
