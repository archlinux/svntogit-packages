# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=totem-plparser
pkgver=3.10.4
pkgrel=1
url="http://www.gnome.org"
pkgdesc="Totem playlist parser library"
license=('LGPL')
arch=(i686 x86_64)
depends=('gmime' 'libsoup' 'libarchive' 'libquvi')
makedepends=('intltool' 'gobject-introspection' 'python2')
source=(http://ftp.gnome.org/pub/gnome/sources/totem-pl-parser/${pkgver:0:4}/totem-pl-parser-$pkgver.tar.xz)
sha256sums=('482353f8466b1cab06d99c0a112a4adc414bf43b9c8f3bd636570d20d1f4e265')

build() {
  cd totem-pl-parser-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/usr/lib/$pkgname --disable-static
  make
}

package(){
  cd totem-pl-parser-$pkgver
  make DESTDIR="$pkgdir" install
}
