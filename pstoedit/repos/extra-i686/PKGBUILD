# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Tobias Powalowski <t.powa@gmx.de>

pkgname=pstoedit
pkgver=3.50
pkgrel=4
pkgdesc="Translates PostScript and PDF graphics into other vector formats"
arch=("i686" "x86_64")
url="http://www.pstoedit.net/"
license=('GPL')
depends=('gcc-libs' 'plotutils' 'gd' 'imagemagick')
makedepends=('ghostscript')
options=('!libtool' '!makeflags')
source=("http://downloads.sourceforge.net/sourceforge/pstoedit/pstoedit-${pkgver}.tar.gz"
	'pstoedit-3.50-parallel.patch'	'pstoedit-3.50-plugin-close.patch')
sha1sums=('5685bef29fa00aae7cc314d3a1dd21612a98eb2b'
          '6e278b164d3e59fbb11e7fd5ae8ffb038932d69a'
          '54bbe019de66eb4cb8525371462595d21fbd074e')


build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # fix FS#19221 - [pstoedit] plugin close problem 
  patch -Np1 -i ../pstoedit-3.50-parallel.patch
  patch -Np1 -i ../pstoedit-3.50-plugin-close.patch

  sed -i 's/-pedantic//' configure

  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
