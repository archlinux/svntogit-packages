# $Id$
# Maintainer: judd <jvinet@zeroflux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=ypserv
pkgver=2.24
pkgrel=1
pkgdesc='Linux NIS Server'
arch=('i686' 'x86_64')
url='http://www.linux-nis.org/nis/ypserv/'
license=('GPL2')
depends=('gdbm' 'openslp')
backup=('etc/ypserv.conf' 'etc/netgroup' 'var/yp/securenets')
source=("ftp://ftp.kernel.org/pub/linux/utils/net/NIS/$pkgname-$pkgver.tar.gz" \
        'yppasswd'
        'ypserv')
sha1sums=('3ae53fe5e047b3bf6a1e407424311ec6e806a95d'
          '96192b628afe36709496e4801d016c4bff343f0e'
          'b625381bfa6cf62345377a7df30b8f45935206c5')

build() {
	cd "$srcdir/$pkgname-$pkgver"
	./configure --prefix=/usr
	make
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install

	install -D -m644 etc/netgroup "$pkgdir"/etc/netgroup
	install -D -m644 etc/ypserv.conf "$pkgdir"/etc/ypserv.conf
	install -D -m644 etc/securenets "$pkgdir"/var/yp/securenets
	install -D -m755 ../ypserv "$pkgdir"/etc/rc.d/ypserv
	install -D -m755 ../yppasswd "$pkgdir"/etc/rc.d/yppasswd
}
