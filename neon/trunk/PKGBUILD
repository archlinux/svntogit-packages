# $Id$
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Maintainer: Juergen Hoetzel <juergen@archlinux.org>

# KEEP LIBTOOL FILES!
pkgname=neon
pkgver=0.29.3
pkgrel=2
pkgdesc="HTTP and WebDAV client library with a C interface"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL')
depends=('heimdal' 'expat' 'ca-certificates')
url="http://www.webdav.org/neon/"
source=("http://www.webdav.org/neon/${pkgname}-${pkgver}.tar.gz")
md5sums=('ba1015b59c112d44d7797b62fe7bee51')
options=('libtool')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr \
    --with-expat --enable-shared --disable-static \
    --with-ssl=openssl --with-ca-bundle=/etc/ssl/certs/ca-certificates.crt
  make || return 1
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
