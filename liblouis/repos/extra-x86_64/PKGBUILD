# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=liblouis
pkgver=3.14.0
pkgrel=1
pkgdesc="Open-source braille translator and back-translator"
arch=(x86_64)
url="http://liblouis.org/"
license=('GPL3')
depends=(glibc)
makedepends=(help2man python)
source=(https://github.com/liblouis/liblouis/releases/download/v$pkgver/liblouis-$pkgver.tar.gz)
sha256sums=('f5b25f8059dd76595aeb419b1522dda78f281a75a7c56dceaaa443f8c437306a')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-static --enable-ucs4
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  cd python
  LD_PRELOAD+=":$srcdir/$pkgname-$pkgver/liblouis/.libs/liblouis.so"
  python setup.py install --root="$pkgdir" --prefix="/usr" --optimize=1
}
# vim:set ts=2 sw=2 et:
