# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=liblouis
pkgver=2.3.0
pkgrel=1
pkgdesc="Open-source braille translator and back-translator"
arch=(i686 x86_64)
url="http://code.google.com/p/liblouis/"
license=('GPL3')
depends=(glibc)
makedepends=(help2man python2)
optdepends=('python2: for python bindings')
options=(!libtool)
install=liblouis.install
source=(http://liblouis.googlecode.com/files/$pkgname-$pkgver.tar.gz)
sha1sums=('f670c4fd845b3310ec02140fb26d912bdb1df356')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  cd python
  LD_PRELOAD+=":$srcdir/$pkgname-$pkgver/liblouis/.libs/liblouis.so"
  python2 setup.py install --root="$pkgdir" --prefix="/usr" --optimize=1
}
# vim:set ts=2 sw=2 et:
