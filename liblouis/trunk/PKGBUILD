# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=liblouis
pkgver=2.6.5
pkgrel=1
pkgdesc="Open-source braille translator and back-translator"
arch=(i686 x86_64)
url="http://liblouis.org/"
license=('GPL3')
depends=(glibc)
makedepends=(help2man python2 python)
install=liblouis.install
source=(https://github.com/liblouis/liblouis/archive/v$pkgver.tar.gz)
sha1sums=('2e6dd4b3afe61861f197a8166f1f8e23a7c4f49f')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./autogen.sh
  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  cd python
  LD_PRELOAD+=":$srcdir/$pkgname-$pkgver/liblouis/.libs/liblouis.so"
  python2 setup.py install --root="$pkgdir" --prefix="/usr" --optimize=1
  python setup.py install --root="$pkgdir" --prefix="/usr" --optimize=1
}
# vim:set ts=2 sw=2 et:
