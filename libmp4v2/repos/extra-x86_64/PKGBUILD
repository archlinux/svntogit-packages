# Maintainer: Tom Gundersen <teg@jklm.no>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=libmp4v2
pkgver=4.1.4
pkgrel=1
pkgdesc="An API to create and modify mp4 files as defined by ISO-IEC:14496-1:2001 MPEG-4 Systems"
arch=('x86_64')
license=('MPL')
url="https://github.com/TechSmith/mp4v2"
depends=('gcc-libs')
source=("https://github.com/TechSmith/mp4v2/archive/Release-ThirdParty-MP4v2-$pkgver.tar.gz"
        libmp4v2-c++11.patch)
sha512sums=('1f25ddb702dfc4095200f9f1812bca3e2c6da88358f4f01c5e1e83426a31373b510079d5b1a863efbaee98ddba4b2c8d762d6e56cfa11521fc50c0f46feadab3'
            '1b1bd1aceef9294f028227adbe387001b7282bd35e8a952deed5a5fc00646285c49c6d871274e264e9b8ba112bba955dbe9d12031a9282e09e1924e1042642df')

prepare() {
  cd mp4v2-Release-ThirdParty-MP4v2-$pkgver
  patch -p0 -i ../libmp4v2-c++11.patch # Fix build with C++11
}

build() {
  cd mp4v2-Release-ThirdParty-MP4v2-$pkgver

  # https://github.com/TechSmith/mp4v2/issues/48
  CXXFLAGS="$CXXFLAGS -Wno-narrowing" ./configure --prefix=/usr
  make
}

package() {
  cd mp4v2-Release-ThirdParty-MP4v2-$pkgver
  make DESTDIR="$pkgdir" install
  make DESTDIR="$pkgdir" install-man
}
