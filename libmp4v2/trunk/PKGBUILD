# Maintainer: Tom Gundersen <teg@jklm.no>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=libmp4v2
pkgver=2.0.0
pkgrel=5
pkgdesc="MPEG-4 library"
arch=('x86_64')
license=('MPL')
url="http://code.google.com/p/mp4v2/"
depends=('gcc-libs')
source=("https://sources.archlinux.org/other/packages/$pkgname/mp4v2-${pkgver}.tar.bz2" libmp4v2-c++11.patch)
sha256sums=('0319b9a60b667cf10ee0ec7505eb7bdc0a2e21ca7a93db96ec5bd758e3428338'
            '946a1c9c8a927d2739248aa1164591de08507152c856f88e8c5d759c213871ee')

prepare() {
  cd mp4v2-$pkgver
  patch -p0 -i ../libmp4v2-c++11.patch # Fix build with C++11
}

build() {
  cd mp4v2-$pkgver

  ./configure --prefix=/usr
  make
}

package() {
  cd mp4v2-$pkgver
  make DESTDIR="$pkgdir" install
  make DESTDIR="$pkgdir" install-man
}
