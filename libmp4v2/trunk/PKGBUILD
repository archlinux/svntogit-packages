# Maintainer: Tom Gundersen <teg@jklm.no>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=libmp4v2
pkgver=4.1.6
pkgrel=1
pkgdesc="An API to create and modify mp4 files as defined by ISO-IEC:14496-1:2001 MPEG-4 Systems"
arch=('x86_64')
license=('MPL')
url="https://github.com/TechSmith/mp4v2"
depends=('gcc-libs')
source=("https://github.com/TechSmith/mp4v2/archive/Release-ThirdParty-MP4v2-$pkgver.tar.gz")
sha512sums=('91f5183d38c9d7fd754ba4b5e149b00600d5b6cb8da063667e5c3a6c8edb4e5efc919d6c4381acd6e011ef56dce3f722a78c51b984066f5c0cc54aae735f3611')

build() {
  cd mp4v2-Release-ThirdParty-MP4v2-$pkgver

  # https://github.com/TechSmith/mp4v2/issues/48
  CXXFLAGS="$CXXFLAGS -Wno-narrowing" ./configure --prefix=/usr
  make
}

package() {
  cd mp4v2-Release-ThirdParty-MP4v2-$pkgver
  make DESTDIR="$pkgdir" install
  make DESTDIR="$pkgdir" install-man
}
