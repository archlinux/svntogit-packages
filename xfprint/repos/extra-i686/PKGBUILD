# $Id$
# Maintainer: Tobias Kieslich <tobias funnychar archlinux.org>

pkgname=xfprint
pkgver=4.6.1
pkgrel=5
pkgdesc="A print dialog and a printer manager for Xfce"
arch=(i686 x86_64)
license=('GPL2')
url="http://www.xfce.org/"
groups=('xfce4')
depends=("libxfcegui4>=$pkgver" "xfconf>=$pkgver" 'libcups>=1.3.10' 'a2ps' 'krb5'
         'gnutls>=2.6.5' 'hicolor-icon-theme')
makedepends=('pkgconfig' 'intltool')
options=('!libtool')
install=${pkgname}.install
source=(http://archive.xfce.org/xfce/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2
        xfprint-manager-fix.diff)
md5sums=('d92fca97a42816085080baf07a99a62e'
         '69b3619a285e94d602a1d0ac08f4ca06')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  # fix xfprint-manager not showing cups printers
  # see http://bugs.archlinux.org/task/19965, http://bugs.gentoo.org/278047, http://bugzilla.xfce.org/show_bug.cgi?id=6089
  patch -Np0 -i ${srcdir}/xfprint-manager-fix.diff
  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib \
    --localstatedir=/var --disable-static
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
