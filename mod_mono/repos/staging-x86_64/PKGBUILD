# $Id$
# Maintainer: Daniel Isenmann <daniel@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>

pkgname=mod_mono
pkgver=2.10
pkgrel=4
pkgdesc="The mono module to make ASP.NET running on top of apache"
arch=('i686' 'x86_64')
url="http://www.go-mono.com"
license=('APACHE')
depends=('apache' 'xsp')
_sha1=6b73e850920865b8f6a16f232e555c71ec1cd26a
# There is some unreleased support for Apache2.4 in official github repo
source=(https://github.com/mono/mod_mono/archive/$_sha1.zip)
md5sums=('2ff278fb418a8eecb275846a05fbfcf1')

build() {
  cd ${pkgname}-${_sha1}
  ./autogen.sh 
  CFLAGS="${CFLAGS} `apr-1-config --cppflags --includes --cflags`" ./configure \
    --prefix=/usr --sysconfdir=/etc --with-apxs=/usr/bin/apxs \
    --with-mono-prefix=/usr --with-apr-config=/usr/bin/apr-1-config
  make
}

package() {
  cd ${pkgname}-${_sha1}
  make DESTDIR="${pkgdir}" install
}
