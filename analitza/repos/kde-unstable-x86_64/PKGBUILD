# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=analitza
pkgver=20.11.90
pkgrel=1
pkgdesc="A library to add mathematical features to your program"
url="https://edu.kde.org/"
arch=(x86_64)
license=(GPL LGPL FDL)
depends=(qt5-svg qt5-declarative)
makedepends=(extra-cmake-modules kdoctools eigen qt5-tools)
source=("https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('8fcab021e6b58f24141236f7481efe9671644c76c166dce3e7967caa3023cc8c'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
