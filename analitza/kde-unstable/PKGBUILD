# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=analitza
pkgver=19.11.80
pkgrel=1
pkgdesc="A library to add mathematical features to your program"
url="https://edu.kde.org/"
arch=(x86_64)
license=(GPL LGPL FDL)
depends=(qt5-svg qt5-declarative)
makedepends=(extra-cmake-modules kdoctools eigen qt5-tools)
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('21d8eb0708058a24ba6e7dee41922c410e578917844274c9a9057a880594c027'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
