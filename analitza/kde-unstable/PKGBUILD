# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=analitza
pkgver=15.08.0
pkgrel=1
pkgdesc="A library to add mathematical features to your program"
url="https://projects.kde.org/projects/kde/kdeedu/analitza"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('qt5-svg' 'qt5-declarative')
makedepends=('extra-cmake-modules' 'kdoctools' 'python' 'eigen')
replaces=('kdebase-analitza')
conflicts=('kdebase-analitza')
source=("http://download.kde.org/stable/applications/${pkgver}/src/analitza-${pkgver}.tar.xz")
sha1sums=('ab54ee2f8c5c0c3214dfe5c6522efc91d6cecd26')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../analitza-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
