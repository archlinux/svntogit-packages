# $Id$
# Contributor: Eric Belanger <eric@archlinux.org>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=proftpd
pkgver=1.3.3d
pkgrel=2
pkgdesc="A high-performance, scalable FTP server"
arch=('i686' 'x86_64')
url="http://www.proftpd.org"
license=('GPL')
depends=('glibc' 'pam' 'ncurses' 'libcap' 'libldap' 
	 'libmysqlclient' 'postgresql-libs')
backup=('etc/proftpd.conf' 'etc/conf.d/proftpd')
source=(ftp://ftp.proftpd.org/distrib/source/${pkgname}-${pkgver}.tar.bz2
	'proftpd'
        'proftpd.logrotate'
	'proftpd.conf.d')
md5sums=('69650e91e05b3a10fa3ac54ee261679b' '99f6f9a989e70e3fa50809fc2bbbbb0a'\
         'ddb09eb13131becdf0e081eef413116b' '71d5932b0461c318ed68c2c0c2660736')
sha1sums=('b2ee3ffdaa971c24327ad66412f2d71b628eddab' 'b7819d725817e55b69c73e2572c21a05db48cc86'\
         '83c38ec40efb7cc09d9824b98e65cd948a195cc6' 'f34f60cb4fb1f4af7be7aca427cbad3cad22bbb9')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	./configure --prefix=/usr --mandir=/usr/share/man --disable-pam \
	    --with-modules=mod_quotatab:mod_quotatab_file:mod_tls:mod_ldap:mod_sql:mod_sql_mysql:mod_sql_postgres \
	    --sysconfdir=/etc --localstatedir=/var/run --enable-ctrls --enable-ipv6 \
	    --with-includes=/usr/include/mysql:/usr/include/postgresql \
	    --with-libraries=/usr/lib/mysql:/usr/lib/postgresql --enable-nls
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make DESTDIR="${pkgdir}" install
	install -Dm644 ../proftpd.logrotate "${pkgdir}/etc/logrotate.d/proftpd"
	install -Dm644 ../proftpd.conf.d "${pkgdir}/etc/conf.d/proftpd"
	install -Dm755 ../proftpd "${pkgdir}/etc/rc.d/proftpd"
	install -Dm755 contrib/xferstats.holger-preiss \
	   "${pkgdir}/usr/bin/ftpstats"
	cd "${pkgdir}/etc"
	sed -i 's|nogroup|nobody|g' proftpd.conf
	rmdir "${pkgdir}/usr/libexec"
}
