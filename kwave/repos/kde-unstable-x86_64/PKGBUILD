# Maintainer: Antonio Rojas <arojas@archlinux,org>
# Contributor: Elvis Angelaccio <elvis.angelaccio@kdemail.net>
# Contributor: Gustavo alvarez <sl1pkn07@gmail.com>

pkgname=kwave
pkgver=19.03.80
pkgrel=1
pkgdesc="A sound editor for KDE"
arch=(x86_64)
url="https://kwave.sourceforge.net"
license=(GPL)
depends=(kio audiofile libmad libsamplerate hicolor-icon-theme id3lib fftw)
makedepends=(extra-cmake-modules kdoctools doxygen librsvg opus)
optdepends=('opus: OGG plugin')
groups=(kde-applications kdemultimedia)
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('33d5c7af93a1f40e53609efb52386b464f582f4b544e5ab8b3fb516bf83e2ceb'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
