# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-dbd-sqlite
pkgver=1.44
pkgrel=1
pkgdesc="Self-contained RDBMS in a DBI driver"
arch=('i686' 'x86_64')
url="http://search.cpan.org/dist/DBD-SQLite"
license=('GPL' 'PerlArtistic')
depends=('perl-dbi' 'sqlite')
options=('!emptydirs')
source=(http://www.cpan.org/authors/id/I/IS/ISHIGAKI/DBD-SQLite-${pkgver}.tar.gz)
md5sums=('354b826b912f8b5972179a13b1b8c4e1')

build() {
  cd DBD-SQLite-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd DBD-SQLite-${pkgver}
  make test
}

package() {
  cd DBD-SQLite-${pkgver}
  make install DESTDIR="${pkgdir}"
}
