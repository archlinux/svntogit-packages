# Maintainer: RÃ©my Oudompheng <remy at archlinux.org>

pkgname=texlive-humanities
pkgver=2018.48491
_revnr=${pkgver#2018.}
pkgrel=1
pkgdesc="TeX Live -  LaTeX packages for law, linguistics, social sciences, and humanities"
license=('GPL')
arch=(any)
depends=('texlive-core' 'texlive-latexextra')
optdepends=('texlive-pictures: for package qtree')
groups=('texlive-most')
url='http://tug.org/texlive/'
source=("https://sources.archlinux.org/other/texlive/$pkgname-$pkgver-src.zip")
options=('!emptydirs')
sha256sums=('c7153a006c8e6eab2d2a4bb39c18169d19f8c45c8a88b5ba7589171c36f15df1')

build() {
   cd "$srcdir"
   for p in *.tar.xz; do
	   bsdtar -xf $p
   done
   rm -rf {tlpkg,doc,source} || true
}

package() {
   install -m755 -d $pkgdir/var/lib/texmf/arch/installedpkgs
   sed -i '/^#/d' CONTENTS
   install -m644 CONTENTS $pkgdir/var/lib/texmf/arch/installedpkgs/${pkgname}_${_revnr}.pkgs
   install -m755 -d $pkgdir/usr/share
   wanteddirs=$(for d in *; do test -d $d && [[ $d != texmf* ]] && echo $d; done) || true
   for dir in $wanteddirs; do
     find $dir -type d -exec install -d -m755 $pkgdir/usr/share/texmf-dist/'{}' \;
     find $dir -type f -exec install -m644 '{}' $pkgdir/usr/share/texmf-dist/'{}' \;
   done
   if [[ -d texmf-dist ]]; then
     find texmf-dist -type d -exec install -d -m755 $pkgdir/usr/share/'{}' \;
     find texmf-dist -type f -exec install -m644 '{}' $pkgdir/usr/share/'{}' \;
   fi
   if [[ -d $pkgdir/usr/share/texmf-dist/scripts ]]; then
     find $pkgdir/usr/share/texmf-dist/scripts -type f -exec chmod a+x '{}' \;
   fi

    install -m755 -d $pkgdir/usr/bin
    ln -s /usr/share/texmf-dist/scripts/diadia/diadia.lua ${pkgdir}/usr/bin/diadia
}
