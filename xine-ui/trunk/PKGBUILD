# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>

pkgname=xine-ui
pkgver=0.99.6
pkgrel=1
pkgdesc="A free video player for Unix"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.xine-project.org"
depends=('xine-lib' 'curl' 'libxtst' 'libxinerama' 'libxv' 'libpng' 'libxft'
	'libxxf86vm' 'readline' 'xdg-utils' 'shared-mime-info' 'hicolor-icon-theme'
	'lirc-utils')
makedepends=('libxt')
options=('!emptydirs' '!strip')
install=${pkgname}.install
source=(http://downloads.sourceforge.net/xine/${pkgname}-${pkgver}.tar.xz
	lirc-check-a89347673097.patch lirc-makefile-a68dd15ff7ae.patch fix_lirc.diff)
md5sums=('d468b4e7fe39ff19888261e1da9be914'
         '4f06f63f5fd55b08a648e5d63f881898'
         '9f5acaf14f3945aa2ce428631d09cd47'
         '0174bd096756b9c6f47e9b8b5ff6e145')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 < "${srcdir}/lirc-check-a89347673097.patch" || return 1
  patch -p1 < "${srcdir}/lirc-makefile-a68dd15ff7ae.patch" || return 1
  patch -p1 < "${srcdir}/fix_lirc.diff" || return 1
  autoconf || return 1

  ./configure --prefix=/usr --mandir=/usr/share/man \
    --with-x --enable-lirc --without-aalib --enable-debug || return 1
  make || return 1
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install || return 1
  install -d "${pkgdir}/usr/share/applications"
  mv "${pkgdir}/usr/share/xine/desktop/xine.desktop" "${pkgdir}/usr/share/applications"
}
