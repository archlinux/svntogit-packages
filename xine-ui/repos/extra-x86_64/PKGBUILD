# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=xine-ui
pkgver=0.99.6
pkgrel=5
pkgdesc="A free video player for Unix"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.xine-project.org"
depends=('xine-lib' 'curl' 'libpng' 'libxft' 'libxxf86vm'
	 'xdg-utils' 'shared-mime-info' 'hicolor-icon-theme'
	 'desktop-file-utils' 'lirc-utils')
makedepends=('libxt')
options=('!strip')
install=xine-ui.install
source=(http://downloads.sourceforge.net/xine/${pkgname}-${pkgver}.tar.xz
	lirc-check-a89347673097.patch lirc-makefile-a68dd15ff7ae.patch 
        fix_lirc.diff xine-ui-xdg.diff xine-ui-0.99.6-libpng15.patch)
sha1sums=('abbdb331898c14025ee6b132f63ab7d3c1b5b31e'
          'b2df43ede3a9d1a34b80139704a4cbbc7295c584'
          '8bbd39f61de7b6991385457a4944b8ea104da88b'
          '2361df9ddad59c426b0f863414c6d527b463a38a'
          '9c70c6543dae6815710b239b4638ffe3843bac86'
          'bc7ec3153e61553dee8c0b6e04d1ca7cfe4a6f14')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 < "${srcdir}/lirc-check-a89347673097.patch"
  patch -p1 < "${srcdir}/lirc-makefile-a68dd15ff7ae.patch"
  patch -p1 < "${srcdir}/fix_lirc.diff"
  patch -p1 < "${srcdir}/xine-ui-xdg.diff"
  patch -p1 < "${srcdir}/xine-ui-0.99.6-libpng15.patch"
  sed -i '/\#include <curl\/types.h>/d' src/xitk/download.c
  autoconf
  ./configure --prefix=/usr --mandir=/usr/share/man \
    --with-x --enable-lirc --without-aalib --enable-debug
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" desktopdir=/usr/share/applications install
}
