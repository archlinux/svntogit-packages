# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=xine-ui
pkgver=0.99.8
pkgrel=1
pkgdesc="A free video player for Unix"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.xine-project.org"
depends=('xine-lib' 'curl' 'libxft' 'libxxf86vm' 'lirc-utils' 'xdg-utils'
         'shared-mime-info' 'hicolor-icon-theme' 'desktop-file-utils')
options=('!strip')
install=xine-ui.install
source=(http://downloads.sourceforge.net/xine/${pkgname}-${pkgver}.tar.xz xine-ui-readline.patch)
sha1sums=('ffe8e6507a95f92f1e29aa4e3751f4b505e8da1a'
          'e325c322a365a57ddbd62256a540202d092d4d1e')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i "${srcdir}/xine-ui-readline.patch"
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --mandir=/usr/share/man \
    --with-x --enable-lirc --without-aalib --enable-debug
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" desktopdir=/usr/share/applications install
}
