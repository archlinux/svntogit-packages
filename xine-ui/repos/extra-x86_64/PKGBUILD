# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=xine-ui
pkgver=0.99.6
pkgrel=2
pkgdesc="A free video player for Unix"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.xine-project.org"
depends=('xine-lib' 'curl' 'libxtst' 'libxinerama' 'libxv' 'libpng' 'libxft'
	'libxxf86vm' 'readline' 'xdg-utils' 'shared-mime-info' 'hicolor-icon-theme'
	'desktop-file-utils' 'lirc-utils')
makedepends=('libxt')
options=('!emptydirs' '!strip')
install=xine-ui.install
source=(http://downloads.sourceforge.net/xine/${pkgname}-${pkgver}.tar.xz
	lirc-check-a89347673097.patch lirc-makefile-a68dd15ff7ae.patch 
        fix_lirc.diff xine-ui-xdg.diff)
md5sums=('d468b4e7fe39ff19888261e1da9be914'
         '4f06f63f5fd55b08a648e5d63f881898'
         '9f5acaf14f3945aa2ce428631d09cd47'
         '0174bd096756b9c6f47e9b8b5ff6e145'
         '5972b4061acf3b553760de9ae2178d38')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 < "${srcdir}/lirc-check-a89347673097.patch"
  patch -p1 < "${srcdir}/lirc-makefile-a68dd15ff7ae.patch"
  patch -p1 < "${srcdir}/fix_lirc.diff"
  patch -p1 < "${srcdir}/xine-ui-xdg.diff"
  autoconf
  ./configure --prefix=/usr --mandir=/usr/share/man \
    --with-x --enable-lirc --without-aalib --enable-debug
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -d "${pkgdir}/usr/share/applications"
  mv "${pkgdir}/usr/share/xine/desktop/xine.desktop" "${pkgdir}/usr/share/applications"
}
