# $Id$
# Maintainer: dorphell <dorphell@archlinux.org>
pkgname=ntp
pkgver=4.2.4p4
pkgrel=5
pkgdesc="NTP (Network Time Protocol) tries to keep servers in sync"
arch=(i686 x86_64)
license=('custom')
url="http://www.ntp.org/"
depends=('openssl>=0.9.8h' 'readline')
backup=('etc/ntp.conf' 'conf.d/ntp-client.conf')
source=(http://www.eecis.udel.edu/~ntp/ntp_spool/ntp4/ntp-${pkgver}.tar.gz \
        ftp://ftp.archlinux.org/other/ntp//ntp-4.2.0a-manpages.tar.bz2 ntp.conf ntp-client.conf ntpd ntpdate)
md5sums=('fcb32a01e1ae3f9ed5bac86b12c7a1cc'
         'd3a5a6185ef5ed71d2fa8e06598fc28f'
         '5bd3924f5720b97837969ec4ec4d5e09'
         'c7f50632b69bd4f32cf052d0b1848463'
         '46118a8ec2c4f5bbfafd730af21b7c03'
         '140855352baeb89da0c6c1c475c18b57')
options=('!emptydirs')

build() {
  cd $startdir/src/$pkgname-$pkgver || return 1

  # configure
  ac_cv_header_dns_sd_h=0 ./configure --prefix=/usr  || return 1

  # build and install
  make || return 1
  make DESTDIR=$startdir/pkg install || return 1

  # install conf files
  mkdir -p $startdir/pkg/usr/share/ntp || return 1
  install -D -m644 conf/* $startdir/pkg/usr/share/ntp/ || return 1

  # install launch scripts 
  mkdir -p $startdir/pkg/etc/rc.d || return 1
  install -D -m755 $startdir/{ntpd,ntpdate} $startdir/pkg/etc/rc.d/ || return 1

  # install man pages
  install -D -m644 $startdir/src/man/{ntpdate,ntptime,ntptrace,tickadj}.1 \
  	$startdir/pkg/usr/man/man1/ || return 1

  # install sample configs
  install -D -m644 $startdir/ntp.conf $startdir/pkg/etc/ntp.conf || return 1
  install -D -m644 $startdir/ntp-client.conf \
  	$startdir/pkg/etc/conf.d/ntp-client.conf || return 1

  # create /var/lib/ntp
  mkdir -p $startdir/pkg/var/lib/ntp || return 1
  touch $startdir/pkg/var/lib/ntp/.placeholder || return 1

  install -Dm644 $startdir/src/$pkgname-$pkgver/COPYRIGHT $startdir/pkg/usr/share/licenses/$pkgname/COPYRIGHT
}
