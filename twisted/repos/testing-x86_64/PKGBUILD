# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>
# Contributor:  Douglas Soares de Andrade <douglas@archlinux.org>

pkgbase=twisted
pkgname=(python-twisted python2-twisted)
pkgver=18.4.0
pkgrel=1
pkgdesc="Asynchronous networking framework written in Python"
arch=('x86_64')
url="http://twistedmatrix.com/"
license=('MIT')
makedepends=('python-zope-interface' 'python2-zope-interface' 'python-setuptools'
             'python2-setuptools' 'python-service-identity' 'python2-service-identity'
             'python-incremental' 'python2-incremental' 'python-constantly' 'python2-constantly'
             'python-automat' 'python2-automat' 'python-hyperlink' 'python2-hyperlink')
checkdepends=('subversion' 'python2-sphinx' 'pygtk' 'xorg-server-xvfb' 'tk' 'openssh' 'gtk3'
              'python-gobject' 'python2-gobject' 'python-gobject2' 'python2-gobject2'
              'python2-soappy' 'python-subunit' 'python2-subunit' 'python-h2' 'python2-h2'
              'python-priority' 'python2-priority' 'python-cryptography' 'python2-cryptography'
              'python-idna' 'python2-idna' 'python-pyserial' 'python2-pyserial')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/twisted/twisted/archive/twisted-$pkgver.tar.gz")
sha512sums=('229b4d12f8c2d6592644690cfec140e6a82a662433af8eb00c7c276e3bf5ec8894e722974b0e5c4d0d122d6de17e984c4cc511d67714ce74e5e18187b92cfe7b')

prepare() {
  cp -a twisted-twisted-$pkgver{,-py2}

  sed -i 's:^#!.*bin.*python:#!/usr/bin/python2:' \
    twisted-twisted-$pkgver-py2/src/twisted/mail/test/pop3testserver.py \
    twisted-twisted-$pkgver-py2/src/twisted/trial/test/scripttest.py
}

build() {
  cd "$srcdir"/twisted-twisted-$pkgver
  python setup.py build

  cd "$srcdir"/twisted-twisted-$pkgver-py2
  python2 setup.py build
}

check() {
  export LC_CTYPE=en_US.UTF-8

  cd "$srcdir"/twisted-twisted-$pkgver
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-3.6:$PYTHONPATH" xvfb-run python -m twisted.trial twisted

  cd "$srcdir"/twisted-twisted-$pkgver-py2
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-2.7:$PYTHONPATH" xvfb-run python2 -m twisted.trial twisted || warning "Tests failed"
}

package_python-twisted() {
  depends=('python-zope-interface' 'python-incremental' 'python-constantly' 'python-automat'
           'python-hyperlink')
  optdepends=('python-pyopenssl: for TLS client hostname verification'
              'python-service-identity: for TLS client hostname verification'
              'python-idna: for TLS client hostname verification'
              'python-cryptography: for using conch'
              'python-pyasn1: for using conch'
              'python-appdirs: for using conch'
              'python-h2: for http2 support'
              'python-priority: for http2 support'
              'python-pyserial: for serial support'
              'tk: for using tkconch')

  cd twisted-twisted-$pkgver
  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1

  # twisted in python 3 supports only few command line tools for now.
  # Let's rename the tools the other way round after the python 3 version supports all.
  for name in trial twistd twist ckeygen cftp conch pyhtmlizer tkconch mailmail; do
    mv "$pkgdir"/usr/bin/${name}{,3}
  done

  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-twisted() {
  depends=('python2-zope-interface' 'python2-incremental' 'python2-constantly' 'python2-automat'
           'python2-hyperlink')
  optdepends=('python2-pyopenssl: for TLS client hostname verification'
              'python2-service-identity: for TLS client hostname verification'
              'python2-idna: for TLS client hostname verification'
              'python2-cryptography: for using conch'
              'python2-pyasn1: for using conch'
              'python2-appdirs: for using conch'
              'python2-h2: for http2 support'
              'python2-priority: for http2 support'
              'python2-pyserial: for serial support'
              'python2-soappy: for twisted.web.soap'
              'pygtk: for using manhole'
              'tk: for using tkconch')

  cd twisted-twisted-$pkgver-py2
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
