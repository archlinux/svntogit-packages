# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>
# Contributor:  Douglas Soares de Andrade <douglas@archlinux.org>

pkgbase=twisted
pkgname=(python-twisted python2-twisted)
pkgver=16.6.0
pkgrel=2
pkgdesc="Asynchronous networking framework written in Python"
arch=('i686' 'x86_64')
url="http://twistedmatrix.com/"
license=('MIT')
makedepends=('python-zope-interface' 'python2-zope-interface' 'git'
             'python-setuptools' 'python2-setuptools' 'python-service-identity'
             'python2-service-identity' 'python-incremental' 'python2-incremental'
             'python-constantly' 'python2-constantly')
checkdepends=('subversion' 'python2-sphinx' 'pygtk' 'xorg-server-xvfb' 'tk' 'openssh'
              'python-gobject' 'python2-gobject' 'python-gobject2' 'python2-gobject2'
              'python2-soappy' 'python-subunit' 'python2-subunit' 'python-h2' 'python2-h2'
              'python-service-identity' 'python2-service-identity' 'python-crypto' 'python2-crypto')
source=("git+https://github.com/twisted/twisted.git#tag=twisted-$pkgver")
sha1sums=('SKIP')

prepare() {
  cp -a twisted{,-py2}

  sed -i 's:^#!.*bin.*python:#!/usr/bin/python2:' \
    twisted-py2/src/twisted/mail/test/pop3testserver.py \
    twisted-py2/src/twisted/trial/test/scripttest.py
}

build() {
  cd "$srcdir"/twisted
  python setup.py build

  cd "$srcdir"/twisted-py2
  python2 setup.py build
}

check() {
  export LC_CTYPE=en_US.UTF-8

  cd "$srcdir"/twisted
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-3.6:$PYTHONPATH" xvfb-run python -m twisted.trial twisted || warning "Tests failed"

  cd "$srcdir"/twisted-py2
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-2.7:$PYTHONPATH" xvfb-run python2 -m twisted.trial twisted || warning "Tests failed"
}

package_python-twisted() {
  depends=('python-zope-interface' 'python-incremental' 'python-constantly')
  optdepends=('python-pyopenssl: for TLS client hostname verification'
              'python-service-identity: for TLS client hostname verification'
              'python-idna: for TLS client hostname verification'
              'python-cryptography: for using conch'
              'python-h2: for http2 support'
              'tk: for using tkconch')

  cd twisted
  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1

  # twisted in python 3 supports only few command line tools for now.
  # Let's rename the tools the other way round after the python 3 version supports all.
  for name in trial twistd twist ckeygen; do
    mv "$pkgdir"/usr/bin/${name}{,3}
  done

  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-twisted() {
  depends=('python2-zope-interface' 'python2-incremental' 'python2-constantly')
  optdepends=('python2-pyopenssl: for TLS client hostname verification'
              'python2-service-identity: for TLS client hostname verification'
              'python2-idna: for TLS client hostname verification'
              'python2-cryptography: for using conch'
              'python2-h2: for http2 support'
              'python2-soappy: for twisted.web.soap'
              'pygtk: for using manhole'
              'tk: for using tkconch')
  provides=("twisted=$pkgver")
  conflicts=('twisted<14.0.0')
  replaces=('twisted<14.0.0')

  cd twisted-py2
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
