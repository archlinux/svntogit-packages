# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>
# Contributor:  Douglas Soares de Andrade <douglas@archlinux.org>

pkgname=twisted
pkgver=11.0.0
pkgrel=1
pkgdesc="Asynchronous networking framework written in Python."
arch=('i686' 'x86_64')
url="http://twistedmatrix.com/"
license=('MIT')
depends=('python2' 'pycrypto' 'zope-interface')
optdepends=('python2-pyopenssl'
            'python-soappy:for twisted.web.soap')
install=twisted.install
source=(http://twistedmatrix.com/Releases/Twisted/${pkgver%.*}/Twisted-${pkgver}.tar.bz2)
md5sums=('d7f94a1609a1b8f3b8c8d0146d4cfe54')

build() {
    cd "${srcdir}/Twisted-${pkgver}"
    python2 setup.py build
}

package() {
    cd "${srcdir}/Twisted-${pkgver}"
    python2 setup.py install --prefix=/usr --root=${pkgdir} --optimize=1
    install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

    sed -e 's|#!/usr/bin/env python|#!/usr/bin/env python2|' \
        -i ${pkgdir}/usr/lib/python2.7/site-packages/twisted/trial/test/scripttest.py
    sed -e 's|#!/usr/bin/env python|#!/usr/bin/env python2|' \
        -i ${pkgdir}/usr/lib/python2.7/site-packages/twisted/mail/test/pop3testserver.py
}
