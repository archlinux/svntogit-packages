# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>
# Contributor:  Douglas Soares de Andrade <douglas@archlinux.org>

pkgname=twisted
pkgver=12.1.0
pkgrel=1
pkgdesc="Asynchronous networking framework written in Python."
arch=('i686' 'x86_64')
url="http://twistedmatrix.com/"
license=('MIT')
depends=('pycrypto' 'python2-zope-interface')
optdepends=('python2-pyopenssl'
            'python-soappy: for twisted.web.soap'
	    'python2-pyasn1: for using conch'
	    'pygtk: for using manhole'
	    'tk: for using tkconch')
install=twisted.install
source=(http://twistedmatrix.com/Releases/Twisted/${pkgver%.*}/Twisted-$pkgver.tar.bz2)
md5sums=('f396f1d6f5321e869c2f89b2196a9eb5')

build() {
    cd "$srcdir/Twisted-$pkgver"
    python2 setup.py build
}

package() {
    cd "$srcdir/Twisted-$pkgver"
    python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
    install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

    sed -i 's:^#!.*bin.*python:#!/usr/bin/python2:' \
      "$pkgdir"/usr/lib/python2.7/site-packages/twisted/trial/test/scripttest.py \
      "$pkgdir"/usr/lib/python2.7/site-packages/twisted/mail/test/pop3testserver.py \
      "$pkgdir"/usr/lib/python2.7/site-packages/twisted/python/test/pullpipe.py
}
