# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>
# Contributor:  Douglas Soares de Andrade <douglas@archlinux.org>

pkgname=twisted
pkgver=10.0.0
pkgrel=1
pkgdesc="Asynchronous networking framework written in Python."
arch=('i686' 'x86_64')
url="http://twistedmatrix.com/"
license=('MIT')
depends=('python' 'pycrypto' 'zope-interface')
optdepends=('pyopenssl'
            'python-soappy:for twisted.web.soap')
install=twisted.install
source=(http://tmrc.mit.edu/mirror/twisted/Twisted/${pkgver%.*}/Twisted-${pkgver}.tar.bz2)
md5sums=('3b226af1a19b25e3b3e93cc6edf5e284')

build() {
  cd "${srcdir}/Twisted-${pkgver}"

  python setup.py install --prefix=/usr --root=$pkgdir --optimize=1 || return 1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/$pkgname/LICENSE" || return 1
}
