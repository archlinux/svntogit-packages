# $Id$
# Mantainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Harley Laue <losinggeneration@yahoo.com>

pkgname=anjuta
pkgver=2.30.0.0
pkgrel=3
pkgdesc="Anjuta Integrated Development Environment (IDE)"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libunique>=1.1.6' 'gconf>=2.28.0' 'vte>=0.23.5' 'gdl>=2.30.0' 'autogen>=5.9.9' 'devhelp>=2.28.1' 'gtksourceview2>=2.10.0' 'glade>=3.6.7' 'libgda>=4.0.8' 'subversion>=1.6.9' 'gnome-icon-theme>=2.30' 'perl-locale-gettext')
makedepends=('gnome-doc-utils>=0.18.0' 'intltool')
conflicts=('gnome-build')
replaces=('gnome-build')
url="http://anjuta.sourceforge.net/"
install=anjuta.install
source=(ftp://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.30/${pkgname}-${pkgver}.tar.bz2)
options=('!libtool' '!emptydirs')
sha256sums=('09cf7e008c9f85dcafc8d13ce691c821c90ca4bf80f4d1c8af0bd83c6914c0cc')

build() {
  cd "${srcdir}/anjuta-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --disable-scrollkeeper || return 1
  make || return 1
  make -j1 GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1
   
  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain anjuta ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
