# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Harley Laue <losinggeneration@yahoo.com>

pkgname=anjuta
pkgver=3.4.0
pkgrel=3
pkgdesc="GNOME Integrated Development Environment (IDE)"
arch=('i686' 'x86_64')
license=('GPL')
depends=('vte3' 'gdl' 'autogen' 'devhelp' 'glade' 'libgda' 'subversion' 'gnome-icon-theme' 'vala' 'dconf')
makedepends=('gnome-doc-utils' 'intltool' 'gobject-introspection')
url="http://anjuta.sourceforge.net/"
install=anjuta.install
source=(ftp://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
options=('!libtool' '!emptydirs')
sha256sums=('c8175e04e797dc0e2aaede4ae73ee9cd7f37d3e59b158961703db546d4660abf')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-scrollkeeper
  make
}

package(){
  cd "$srcdir/$pkgname-$pkgver"

  # j1: Race during linking
  make -j1 DESTDIR="$pkgdir" install

  sed -i "1s|#!/usr/bin/python$|&2|" \
    $pkgdir/usr/share/anjuta/project/{pygtk,python}/src/main.py
}
