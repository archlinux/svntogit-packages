# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Harley Laue <losinggeneration@yahoo.com>

pkgbase=anjuta
pkgname=('libanjuta' 'anjuta')
pkgver=3.15.90
pkgrel=1
pkgdesc="GNOME Integrated Development Environment (IDE)"
arch=(i686 x86_64)
license=(GPL)
makedepends=(gdl vte3 autogen devhelp glade libgda subversion neon vala dconf gtk-doc 
             yelp-tools gnome-common intltool gobject-introspection itstool gjs)
url="http://www.anjuta.org/"
options=('!emptydirs')
source=(ftp://ftp.gnome.org/pub/gnome/sources/$pkgbase/${pkgver:0:4}/$pkgbase-$pkgver.tar.xz
        vala-0.28.patch)
sha256sums=('d7de0bd391a7e9706f7eaa7b07686ae77fd88a3045eec30b0bc55fa52a0824cb'
            '75a28a8ee7eb793fddc35b111a12dba22f3cf4ff9bd06a3132a14c9f2bcdc510')

prepare() {
  cd anjuta-$pkgver
  patch -Np1 -i ../vala-0.28.patch
  autoreconf -fi
}

build() {
  cd "$pkgbase-$pkgver"

  ./autogen.sh --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --enable-glade-catalog --enable-compile-warnings=minimum \
      PYTHON=/usr/bin/python2
  make
}

package_libanjuta(){
  pkgdesc="Anjuta runtime library"
  depends=(gdl)
  conflicts=('anjuta<3.4.4')

  cd "$pkgbase-$pkgver/libanjuta"

  # j1: Race during linking
  make -j1 DESTDIR="$pkgdir" install
}

package_anjuta(){
  pkgdesc="GNOME Integrated Development Environment (IDE)"
  depends=(libanjuta vte3 autogen devhelp glade libgda subversion neon vala dconf)
  groups=('gnome-extra')

  install=anjuta.install

  cd "$pkgbase-$pkgver"
  make -j1 DESTDIR="$pkgdir" install

  cd libanjuta
  make -j1 DESTDIR="$pkgdir" uninstall
}
