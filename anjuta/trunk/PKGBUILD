# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Harley Laue <losinggeneration@yahoo.com>

pkgname=anjuta
pkgver=3.4.1
pkgrel=1
pkgdesc="GNOME Integrated Development Environment (IDE)"
arch=('i686' 'x86_64')
license=('GPL')
depends=('vte3' 'gdl' 'autogen' 'devhelp' 'glade' 'libgda' 'subversion' 'gnome-icon-theme' 'vala' 'dconf')
makedepends=('gnome-doc-utils' 'intltool' 'gobject-introspection')
url="http://www.anjuta.org/"
install=anjuta.install
source=(ftp://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
options=('!libtool' '!emptydirs')
sha256sums=('92edd96d7b7df178405cd229ff72d284c5499ad4496702f4efac7178e2d84369')

build() {
  cd "$pkgname-$pkgver"
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-scrollkeeper
  make
}

package(){
  cd "$pkgname-$pkgver"

  # j1: Race during linking
  make -j1 DESTDIR="$pkgdir" install

  sed -i "1s|#!/usr/bin/python$|&2|" \
    $pkgdir/usr/share/anjuta/project/{pygtk,python}/src/main.py
}
