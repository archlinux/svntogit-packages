# $Id$
# Mantainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Harley Laue <losinggeneration@yahoo.com>

pkgname=anjuta
pkgver=2.4.2
pkgrel=1
pkgdesc="Anjuta Integrated Development Environment (IDE)"
arch=('i686' 'x86_64')
license=('GPL')
depends=('autogen>=5.9.5' 'devhelp>=0.19.1' 'gnome-build>=0.2.4' 'vte>=0.16.13' 'graphviz>=2.18' 'subversion>=1.4.5-4' 'libgnomeprintui>=2.18.2' 'gtksourceview2>=2.2.1' 'glade>=3.4.5' 'gconf>=2.22.0' 'libxslt>=1.1.23')
makedepends=('gnome-doc-utils>=0.12.0-1')
url="http://anjuta.sourceforge.net/"
install=anjuta.install
source=(http://downloads.sourceforge.net/sourceforge/anjuta/anjuta-$pkgver.tar.bz2)  
options=('!libtool' '!emptydirs')
md5sums=('66dc5a9aeb051846f82715b1bed55e37')

build() {
  cd ${startdir}/src/anjuta-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --enable-plugin-subversion --enable-plugin-glade \
		--enable-plugin-class-inheritance --enable-plugin-valgrind \
		--enable-plugin-sourceview --disable-gtk-doc \
		--enable-debug --disable-scrollkeeper || return 1
  make || return 1
  make -j1 GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR=${startdir}/pkg install || return 1
   
  install -m755 -d ${startdir}/pkg/usr/share/gconf/schemas
  gconf-merge-schema ${startdir}/pkg/usr/share/gconf/schemas/${pkgname}.schemas ${startdir}/pkg/etc/gconf/schemas/*.schemas || return 1
  rm -f ${startdir}/pkg/etc/gconf/schemas/*.schemas
}
