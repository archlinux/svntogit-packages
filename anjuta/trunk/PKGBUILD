# $Id$
# Mantainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Harley Laue <losinggeneration@yahoo.com>

pkgname=anjuta
pkgver=2.28.1.0
pkgrel=1
pkgdesc="Anjuta Integrated Development Environment (IDE)"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libunique>=1.1.2' 'gconf>=2.28.0' 'vte>=0.22.2' 'gdl>=2.28.1' 'autogen>=5.9.9' 'devhelp>=2.28.1' 'gtksourceview2>=2.8.0' 'glade>=3.6.7' 'libgda>=4.0.5' 'subversion>=1.6.5' 'gnome-icon-theme>=2.28.0' 'perl-locale-gettext')
makedepends=('gnome-doc-utils>=0.18.0' 'intltool')
conflicts=('gnome-build')
replaces=('gnome-build')
url="http://anjuta.sourceforge.net/"
install=anjuta.install
source=(ftp://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.28/${pkgname}-${pkgver}.tar.bz2)
options=('!libtool' '!emptydirs')
sha256sums=('31c888c2a51e8e103c71b6563324d2fc5ffad2ad08df7a42c82d1e1a55931b02')

build() {
  cd "${srcdir}/anjuta-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --disable-scrollkeeper || return 1
  make || return 1
  make -j1 GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1
   
  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain anjuta ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
