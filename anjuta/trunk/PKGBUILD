# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Harley Laue <losinggeneration@yahoo.com>

pkgbase=anjuta
pkgname=('libanjuta' 'anjuta')
pkgver=3.12.0
pkgrel=1
pkgdesc="GNOME Integrated Development Environment (IDE)"
arch=(i686 x86_64)
license=(GPL)
makedepends=(gdl vte3 autogen devhelp glade libgda subversion neon gnome-icon-theme vala dconf
             gtk-doc yelp-tools gnome-common intltool gobject-introspection itstool gjs
             python2)
url="http://www.anjuta.org/"
options=('!emptydirs')
source=(ftp://ftp.gnome.org/pub/gnome/sources/$pkgbase/${pkgver%.*}/$pkgbase-$pkgver.tar.xz)
sha256sums=('880efe7274d015a07aec2c5bdfebe27e313f76f1f524bd9309b5e3ecd29bda1f')

build() {
  cd "$pkgbase-$pkgver"

  ./autogen.sh --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --enable-glade-catalog --enable-compile-warnings=minimum \
      PYTHON=/usr/bin/python2
  make
}

package_libanjuta(){
  pkgdesc="Anjuta runtime library"
  depends=(gdl)
  conflicts=('anjuta<3.4.4')

  cd "$pkgbase-$pkgver/libanjuta"

  # j1: Race during linking
  make -j1 DESTDIR="$pkgdir" install
}

package_anjuta(){
  pkgdesc="GNOME Integrated Development Environment (IDE)"
  depends=(libanjuta vte3 autogen devhelp glade libgda subversion neon gnome-icon-theme vala dconf)
  groups=('gnome-extra')

  install=anjuta.install

  cd "$pkgbase-$pkgver"
  make -j1 DESTDIR="$pkgdir" install

  cd libanjuta
  make -j1 DESTDIR="$pkgdir" uninstall
}
