# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Harley Laue <losinggeneration@yahoo.com>

pkgbase=anjuta
pkgname=('libanjuta' 'anjuta')
pkgver=3.16.0
pkgrel=2
pkgdesc="GNOME Integrated Development Environment (IDE)"
arch=(i686 x86_64)
license=(GPL)
makedepends=(gdl vte3 autogen devhelp glade libgda subversion neon vala dconf gtk-doc 
             yelp-tools gnome-common intltool gobject-introspection itstool gjs)
url="http://www.anjuta.org/"
options=('!emptydirs')
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgbase/${pkgver:0:4}/$pkgbase-$pkgver.tar.xz)
sha256sums=('77432a01b37b46f5558bf4cc435f9a023abf17c5273d472e17dff1f9b3d9943c')

build() {
  cd "$pkgbase-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --enable-glade-catalog --enable-compile-warnings=minimum \
      PYTHON=/usr/bin/python2
  make
}

package_libanjuta(){
  pkgdesc="Anjuta runtime library"
  depends=(gdl)
  conflicts=('anjuta<3.4.4')

  cd "$pkgbase-$pkgver/libanjuta"

  # j1: Race during linking
  make -j1 DESTDIR="$pkgdir" install
}

package_anjuta(){
  pkgdesc="GNOME Integrated Development Environment (IDE)"
  depends=(libanjuta vte3 autogen devhelp glade libgda subversion neon vala dconf)
  groups=('gnome-extra')

  install=anjuta.install

  cd "$pkgbase-$pkgver"
  make -j1 DESTDIR="$pkgdir" install

  cd libanjuta
  make -j1 DESTDIR="$pkgdir" uninstall
}
