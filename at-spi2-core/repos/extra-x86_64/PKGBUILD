# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=at-spi2-core
pkgver=2.10.1
pkgrel=1
pkgdesc="Protocol definitions and daemon for D-Bus at-spi"
arch=('i686' 'x86_64')
url="http://www.gnome.org"
license=('GPL2')
depends=('dbus' 'glib2' 'libxtst')
makedepends=('intltool' 'gobject-introspection')
source=(http://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('349b7ad5e6a8eebbf88aaefcb67a0f49b025dbcdf6ad646978faf2ca84650119')

build() {
  cd $pkgname-$pkgver

  sed -i -e '/AC_PATH_XTRA/d' configure.ac
  autoreconf --force --install

  ./configure --prefix=/usr --sysconfdir=/etc \
    --libexecdir=/usr/lib/at-spi2-core --disable-xevie
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
