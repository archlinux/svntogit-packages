# $Id$
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=at-spi2-core
pkgver=2.28.0
pkgrel=1
pkgdesc="Protocol definitions and daemon for D-Bus at-spi"
url="https://git.gnome.org/browse/at-spi2-core"
arch=(x86_64)
license=(GPL2)
depends=(dbus glib2 libxtst)
makedepends=(intltool gobject-introspection git gtk-doc meson)
_commit=c210da7e817f36643fb6b3fb3410103f9887dcf8  # tags/AT_SPI2_CORE_2_28_0^0
source=("git+https://git.gnome.org/browse/at-spi2-core#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^AT_SPI2_CORE_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd $pkgname
}
  
build() {
  arch-meson $pkgname build -D enable_docs=true
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}

# vim:set ts=2 sw=2 et:
