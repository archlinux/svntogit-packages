# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-xclipboard
pkgver=1.1.4
pkgrel=1
pkgdesc="X clipboard manager"
arch=(x86_64)
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libxaw' 'libxmu' 'libxt' 'libx11')
makedepends=('xorg-util-macros')
source=(https://xorg.freedesktop.org/archive/individual/app/xclipboard-${pkgver}.tar.xz{,.sig})
sha512sums=('efa7800e0c4fb0242362c4263d80182d591bded07d1064a36b157b6202b15b0c82f083658ced2b82392eca905fb57c04a537d67e75c1237a1667ed2ce0cb09fe'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # Alan Coopersmith <alan.coopersmith@oracle.com>

build() {
  cd xclipboard-${pkgver}
  ./configure --prefix=/usr
  make
}

package(){
  cd xclipboard-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
