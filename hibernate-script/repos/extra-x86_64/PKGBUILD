# $Id$
# Maintainer: Thomas Baechler <thomas@archlinux.org>

pkgname=hibernate-script
pkgver=1.99
pkgrel=2
pkgdesc="Set of scripts for managing tux on ice, hibernation and suspend to RAM"
arch=('i686' 'x86_64')
url="http://www.tuxonice.net"
license=('GPL')
depends=('bash' 'kbd')
options=(!strip)
backup=('etc/hibernate/hibernate.conf' 'etc/hibernate/suspend2.conf' \
        'etc/hibernate/disk.conf' 'etc/hibernate/ram.conf' \
        'etc/hibernate/common.conf' 'etc/hibernate/blacklisted-modules' \
        'etc/hibernate/ususpend-both.conf' 'etc/hibernate/sysfs-ram.conf' \
        'etc/hibernate/ususpend-ram.conf' 'etc/hibernate/sysfs-disk.conf' \
        'etc/hibernate/ususpend-disk.conf')
source=(http://www.tuxonice.net/downloads/all/${pkgname}-${pkgver}.tar.gz
        hibernate-script-${pkgver}-arch.patch
        hibernate.rc
        add-freedesktop.org-locking.patch)
md5sums=('11832e77edc4a13330aaced8cea52dfb'
         '2e6dba2c90e90952b8b1f167045c38e6'
         '5d7e83902a00bf72b1de97a5450a558e'
         '626f4cf975b0ad4bd51245ca679943fe')
                  

build() {
  cd $srcdir/hibernate-script-$pkgver
  # Fix scriptlets to work properly with Archlinux
  patch -Np1 -i $srcdir/hibernate-script-${pkgver}-arch.patch || return 1
  patch -Np1 -i $srcdir/add-freedesktop.org-locking.patch || return 1
  export BASE_DIR=${pkgdir}
  export PREFIX=/usr
  export MAN_DIR=$BASE_DIR$PREFIX/share/man
  install -dm755 ${pkgdir}/etc/{rc,logrotate}.d
  ./install.sh || return 1
  install -m 755 $srcdir/hibernate.rc $pkgdir/etc/rc.d/hibernate-cleanup
}
