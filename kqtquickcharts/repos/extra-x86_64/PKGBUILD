# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kqtquickcharts
pkgver=17.12.1
pkgrel=1
pkgdesc="A QtQuick plugin to render beautiful and interactive charts"
url="https://edu.kde.org/"
arch=(x86_64)
license=(GPL LGPL FDL)
depends=(qt5-declarative)
makedepends=(extra-cmake-modules)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
        kqtquickcharts-types.patch::"https://cgit.kde.org/kqtquickcharts.git/patch/?id=4e90e87e")
sha256sums=('719f4fc3faf0dbd4c644e64b290181032399a1ec752648320f34f756d2b6f726'
            'SKIP'
            '00b36841d891537fd3b0d6ab6e33719f0806de8cb15109f2a04243074bc13d2d')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../kqtquickcharts-types.patch # Fix ktouch not loading with Qt 5.10
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
