# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=flatpak
pkgver=0.9.4
pkgrel=1
pkgdesc="Application deployment framework for desktop apps"
url="http://flatpak.org"
arch=(i686 x86_64)
license=(GPL)
depends=(dbus systemd glib2 libsoup polkit xorg-xauth ostree fuse json-glib
         libseccomp libarchive libelf libcap python bubblewrap patch elfutils)
makedepends=(intltool gobject-introspection gtk-doc git docbook-xsl xmlto
             libdwarf)
install=flatpak.install
_commit=cf2b44b04b80aaaf91834037d91bac2e17459a7b  # tags/0.9.4^0
source=("git+https://github.com/flatpak/flatpak#commit=$_commit"
        "git+https://github.com/projectatomic/bubblewrap"
        "git+https://git.gnome.org/browse/libglnx")
sha256sums=('SKIP'
            'SKIP'
            'SKIP')

prepare() {
  cd $pkgname

  git submodule init
  git config --local submodule.bubblewrap.url "$srcdir/bubblewrap"
  git config --local submodule.libglnx.url "$srcdir/libglnx"
  git submodule update

  # https://github.com/flatpak/flatpak/issues/267
  sed -i '/locale\/C\./d' tests/make-test-runtime.sh

  NOCONFIGURE=1 ./autogen.sh
}

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

build() {
  cd $pkgname

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib/$pkgname \
    --disable-static \
    --enable-gtk-doc \
    --with-system-bubblewrap \
    --with-priv-mode=setuid \
    --with-dwarf-header=/usr/include/libdwarf \
    --with-dbus-config-dir=/usr/share/dbus-1/system.d

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

check() {
  cd $pkgname
  # Xattr tests fail on tmpfs
  make -k check || :
}

package() {
  depends+=(xdg-desktop-portal)

  cd $pkgname

  make DESTDIR="$pkgdir" install

  # Fixup mode to match polkit
  install -d -o root -g 102 -m 750 "$pkgdir/usr/share/polkit-1/rules.d"
}
