# $Id$
# Maintainer:

pkgbase=isodate
pkgname=('python2-isodate' 'python-isodate')
pkgver=0.4.8
pkgrel=1
pkgdesc="An ISO 8601 date/time/duration parser and formater"
arch=('any')
url="http://pypi.python.org/pypi/isodate"
license=('BSD')
makedepends=('python2' 'python')
checkdepends=('python2-distribute' 'python-distribute')
source=(http://pypi.python.org/packages/source/i/isodate/$pkgbase-$pkgver.tar.gz)
sha1sums=('2866295b020c39abb8422d208f2694325bb1ee3e')

check() {
  cd "$srcdir/$pkgbase-$pkgver"
  python2 setup.py test
  python setup.py test
}

package_python2-isodate() {
  depends=('python2')

  cd "$srcdir/$pkgbase-$pkgver"
  python2 setup.py install --root="$pkgdir/" --optimize=1
  install -d "${pkgdir}/usr/share/licenses/${pkgname}"
  head -26 src/isodate/isodates.py > "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python-isodate() {
  depends=('python')

  cd "$srcdir/$pkgbase-$pkgver"
  python setup.py install --root="$pkgdir/" --optimize=1
  install -d "${pkgdir}/usr/share/licenses/${pkgname}"
  head -26 src/isodate/isodates.py > "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
