# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Daniel Balieiro <daniel@balieiro.com>
# Contributor: Rodrigo L. M. Flores <mail@rodrigoflores.org>
pkgname=telepathy-gabble
pkgver=0.14.0
pkgrel=1
pkgdesc="A Jabber/XMPP connection manager for Telepathy"
arch=('i686' 'x86_64')
url="http://telepathy.freedesktop.org"
groups=('telepathy')
license=('LGPL2.1')
depends=('telepathy-glib' 'libsoup' 'libnice' 'sqlite3')
makedepends=('libxslt' 'python2')
options=('!libtool')
source=(http://telepathy.freedesktop.org/releases/telepathy-gabble/${pkgname}-${pkgver}.tar.gz)
install=telepathy-gabble.install
md5sums=('73afa5533833d176b9b82b96351efe63')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr \
  	--libexecdir=/usr/lib/telepathy --with-tls=openssl
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
