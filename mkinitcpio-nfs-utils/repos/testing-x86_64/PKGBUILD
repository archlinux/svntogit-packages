# $Id$
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>

pkgname=mkinitcpio-nfs-utils
pkgver=0.3
pkgrel=4
pkgdesc="ipconfig and nfsmount tools for NFS root support in mkinitcpio"
arch=('i686' 'x86_64')
url="http://www.archlinux.org/"
license=('GPL2')
depends=('glibc')
source=("ftp://ftp.archlinux.org/other/mkinitcpio/$pkgname-$pkgver.tar.xz"
        'nfsmount-fix-wrong-umount-path.patch'
        'initcpio-install-net'
        'initcpio-hook-net')
sha256sums=('d290d489844fae100ca7b848b8eef40078124ff373203086bacc07329d1e8939'
            '6edb239f3d541b8ddee816e2d18bfb3ee1d3f5f8827769674cc8bf2a7ce38f21'
            'ce9f021763c020b9b62a761974437fd1e9e894df3effc58e71671bf83b061255'
            '29a5a4a9ebd61bfa1afbb8dcaf2187e487ee1f34b1979a85a853fa2acbeb510e')

build() {
  cd "$pkgname-$pkgver"

  # upstream commit 029622dfbfe25203275a385a5bf33d44c2409b00
  patch -Np1 < "$srcdir/nfsmount-fix-wrong-umount-path.patch"

  make
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install

  # override hooks, they need updates
  install -m644 "$srcdir/initcpio-install-net" "$pkgdir/usr/lib/initcpio/install/net"
  install -m644 "$srcdir/initcpio-hook-net" "$pkgdir/usr/lib/initcpio/hooks/net"
}
