# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=liburing
pkgver=2.2
_commit=dda4848a9911120a903bef6284fb88286f4464c9  # refs/tags/liburing-2.2^{}
pkgrel=1
pkgdesc="Linux-native io_uring I/O access library"
arch=(x86_64)
url="https://git.kernel.dk/cgit/liburing/"
license=(LGPL2.1 MIT)
depends=(glibc)
makedepends=(git)
provides=(liburing.so)
options=(debug)
source=(git+https://git.kernel.dk/liburing#commit=$_commit)
sha512sums=('SKIP')

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man
  make
}

package() {
  make DESTDIR="$pkgdir" install -C $pkgname
  install -vDm 644 $pkgname/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
