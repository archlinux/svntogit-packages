# $Id$
#Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-fonts-misc
pkgver=1.0.3
pkgrel=3
pkgdesc="X.org misc fonts"
arch=(any)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('xorg-fonts-encodings' 'xorg-fonts-alias' 'xorg-font-utils' 'fontconfig')
install=xfonts.install
replaces=('font-misc-ethiopic' 'font-misc-meltho')
provides=('font-misc-ethiopic' 'font-misc-meltho')
conflicts=('font-misc-ethiopic' 'font-misc-meltho')
source=(${url}/releases/individual/font/font-arabic-misc-${pkgver}.tar.bz2
        ${url}/releases/individual/font/font-cursor-misc-${pkgver}.tar.bz2
        ${url}/releases/individual/font/font-daewoo-misc-${pkgver}.tar.bz2
        ${url}/releases/individual/font/font-dec-misc-${pkgver}.tar.bz2
        ${url}/releases/individual/font/font-isas-misc-${pkgver}.tar.bz2
        ${url}/releases/individual/font/font-jis-misc-${pkgver}.tar.bz2
        ${url}/releases/individual/font/font-micro-misc-${pkgver}.tar.bz2
        ${url}/releases/individual/font/font-misc-ethiopic-${pkgver}.tar.bz2
        ${url}/releases/individual/font/font-misc-meltho-${pkgver}.tar.bz2
        ${url}/releases/individual/font/font-misc-misc-1.1.2.tar.bz2
        ${url}/releases/individual/font/font-mutt-misc-${pkgver}.tar.bz2
        ${url}/releases/individual/font/font-schumacher-misc-1.1.2.tar.bz2
        ${url}/releases/individual/font/font-sony-misc-${pkgver}.tar.bz2
        ${url}/releases/individual/font/font-sun-misc-${pkgver}.tar.bz2)
md5sums=('cc0726e4a277d6ed93b8e09c1f195470'
         '3e0069d4f178a399cffe56daa95c2b63'
         '0571bf77f8fab465a5454569d9989506'
         '6e7c5108f1b16d7a1c7b2c9760edd6e5'
         'a2401caccbdcf5698e001784dbd43f1a'
         'cb7b57d7800fd9e28ec35d85761ed278'
         '143c228286fe9c920ab60e47c1b60b67'
         '6306c808f7d7e7d660dfb3859f9091d2'
         'e3e7b0fda650adc7eb6964ff3c486b1c'
         'c88eb44b3b903d79fb44b860a213e623'
         '56b0296e8862fc1df5cdbb4efe604e86'
         'e805feb7c4f20e6bfb1118d19d972219'
         'beef61a9b0762aba8af7b736bb961f86'
         '948f2e07810b4f31195185921470f68d')

build() {
  cd "${srcdir}"
  for dir in *; do
    if [ -d "${dir}" ]; then
      pushd "${dir}"
      ./configure --prefix=/usr \
          --with-fontdir=/usr/share/fonts/misc
      make
      popd
    fi
  done
}

package() {
  cd "${srcdir}"
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  for dir in *; do
    if [ -d "${dir}" ]; then
      pushd "${dir}"
      make DESTDIR="${pkgdir}" install
      install -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.${dir%-*}
      popd
    fi
  done
  rm -f ${pkgdir}/usr/share/fonts/*/fonts.*
  
  # move .otf files to proper directory - FS#44250
  mv ${pkgdir}/usr/share/fonts/misc/*.otf ${pkgdir}/usr/share/fonts/OTF/
}
