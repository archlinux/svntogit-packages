# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-xcursorgen
pkgver=1.0.5
pkgrel=2
pkgdesc="Create an X cursor file from PNG images"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libx11' 'libxcursor' 'libpng')
makedepends=('xorg-util-macros')
groups=('xorg-apps' 'xorg')
source=(http://xorg.freedesktop.org/archive/individual/app/xcursorgen-${pkgver}.tar.bz2)
sha1sums=('1d0ca82422d328522b06b218f86b5901a7d5521b')

build() {
  cd "${srcdir}/xcursorgen-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/xcursorgen-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
