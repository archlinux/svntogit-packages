# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=ktnef
pkgver=18.07.80
pkgrel=1
pkgdesc="API for handling TNEF data"
arch=(x86_64)
url="https://community.kde.org/KDE_PIM"
license=(LGPL)
depends=(kcalutils kcontacts)
makedepends=(extra-cmake-modules)
conflicts=(libktnef)
provides=(libktnef)
replaces=(libktnef)
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('4fa715d50c9f4aa07d0f4a1351d00369816ca2565f6b866dc663cf6172a23c6b'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
