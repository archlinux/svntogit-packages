# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=ktnef
pkgver=20.12.1
pkgrel=1
pkgdesc="API for handling TNEF data"
arch=(x86_64)
url="https://kontact.kde.org"
license=(LGPL)
depends=(kcalutils kcontacts)
makedepends=(extra-cmake-modules)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('cd9155107c2358d0c03ffdd22d9eeba6931ab9cbb9481e05ed97be0397aa04df'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
