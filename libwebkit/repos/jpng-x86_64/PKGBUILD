# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=libwebkit
pkgver=1.1.15.4
pkgrel=3
pkgdesc="an opensource web content engine, derived from KHTML and KJS from KDE"
arch=('i686' 'x86_64')
url="http://webkitgtk.org/"
license=('custom')
depends=('libxt' 'libxslt' 'sqlite3>=3.6.22' 'gtk2>=2.18.6' 'icu>=4.2' 'gstreamer0.10-base>=0.10.25' 'libsoup>=2.28.2' 'enchant>=1.5.0')
makedepends=('gperf' 'gtk-doc>=1.11')
provides=('webkitgtk-svn')
conflicts=('webkitgtk-svn')
replaces=('webkitgtk-svn')
options=('!libtool')
source=(http://webkitgtk.org/webkit-${pkgver}.tar.gz
        webkitdownload-use-webkitnetworkresponse.patch
        webkit-add-gzip.patch)
md5sums=('47301fb7120421ac38bf714db8f5d428'
         'a041ddf393002ebd4ea4eaec972c47c2'
         'adf43a17499c136f35b899967b01b9f9')

build() {
  cd "${srcdir}/webkit-${pkgver}"
  patch -Np1 -i "${srcdir}/webkitdownload-use-webkitnetworkresponse.patch" || return 1
  patch -Np1 -i "${srcdir}/webkit-add-gzip.patch" || return 1
  ./configure --prefix=/usr \
      --enable-video \
      --with-font-backend=freetype --enable-gtk-doc \
      --enable-jit --with-unicode-backend=icu || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1

  install -Dm644 WebKit/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE" || return 1
}
