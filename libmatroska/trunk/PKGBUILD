# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor 03/08/04 <lefungus@altern.org>

pkgname=libmatroska
pkgver=1.4.9
pkgrel=1
pkgdesc='Matroska library'
arch=('x86_64')
url='https://dl.matroska.org/downloads/libmatroska/'
license=('LGPL')
depends=('libebml.so')
makedepends=('cmake')
provides=('libmatroska.so')
source=("https://dl.matroska.org/downloads/libmatroska/libmatroska-${pkgver}.tar.xz")
sha256sums=('38a61dd5d87c070928b5deb3922b63b2b83c09e2e4a10f9393eecb6afa9795c8')

prepare() {
  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd build

  cmake ../libmatroska-${pkgver} \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_INSTALL_LIBDIR='/usr/lib' \
    -DBUILD_SHARED_LIBS='ON'
  make
}

package() {
  make DESTDIR="${pkgdir}" -C build install
}

# vim: ts=2 sw=2 et:
