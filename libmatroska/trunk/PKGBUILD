# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor 03/08/04 <lefungus@altern.org>

pkgname=libmatroska
pkgver=1.5.1
pkgrel=1
pkgdesc='Matroska library'
arch=(x86_64)
url=https://dl.matroska.org/downloads/libmatroska/
license=(LGPL2.1)
depends=(libebml.so)
makedepends=(
  cmake
  git
)
provides=(libmatroska.so)
source=(git+https://github.com/Matroska-Org/libmatroska.git#tag=release-${pkgver})
sha256sums=(SKIP)

prepare() {
  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd build

  cmake ../libmatroska \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DBUILD_SHARED_LIBS=ON
  make
}

package() {
  make DESTDIR="${pkgdir}" -C build install
}

# vim: ts=2 sw=2 et:
