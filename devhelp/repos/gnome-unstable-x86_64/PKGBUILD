# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Link Dupont <link@subpop.net>

pkgname=devhelp
pkgver=3.6.0
pkgrel=2
pkgdesc="API documentation browser for GNOME"
arch=(i686 x86_64)
license=('GPL')
url="http://live.gnome.org/devhelp/"
depends=('gconf' 'webkitgtk3' 'hicolor-icon-theme')
makedepends=('pkgconfig' 'intltool' 'python2')
options=('!libtool' '!emptydirs')
install=devhelp.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('bf0576d5d70496367ccb22be53be620bbfe341bd491c1d725cfb59d566769fe1')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --with-webkit2
  make
}

package() {
  cd $pkgname-$pkgver
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain devhelp ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
