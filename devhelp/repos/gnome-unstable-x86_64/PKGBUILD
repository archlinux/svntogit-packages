# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Link Dupont <link@subpop.net>

pkgname=devhelp
pkgver=3.10.0
pkgrel=1
pkgdesc="API documentation browser for GNOME"
arch=(i686 x86_64)
license=(GPL)
url="http://live.gnome.org/devhelp/"
depends=(webkitgtk hicolor-icon-theme)
makedepends=(pkgconfig intltool python)
options=('!libtool' '!emptydirs')
install=devhelp.install
groups=('gnome-extra')
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('4d28d21a0331c67086607b737147947b288416ab38c8a0b0264f7a63bd4fce9b')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="${pkgdir}" install
}
