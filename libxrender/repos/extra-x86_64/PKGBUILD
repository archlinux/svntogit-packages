# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libxrender
pkgver=0.9.9
pkgrel=1
pkgdesc="X Rendering Extension client library"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libx11>=1.3.4' 'renderproto')
makedepends=('pkgconfig')
source=(${url}/releases/individual/lib/libXrender-${pkgver}.tar.bz2{,.sig})
sha256sums=('fc2fe57980a14092426dffcd1f2d9de0987b9d40adea663bd70d6342c0e9be1a'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') #Alan Coopersmith <alan.coopersmith@oracle.com>

build() {
  cd libXrender-${pkgver}
  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd libXrender-${pkgver}
  make DESTDIR=${pkgdir} install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
