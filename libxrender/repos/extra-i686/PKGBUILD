# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libxrender
pkgver=0.9.6
pkgrel=2
pkgdesc="X Rendering Extension client library"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libx11>=1.3.4' 'renderproto')
makedepends=('pkgconfig')
options=('!libtool')
source=(${url}/releases/individual/lib/libXrender-${pkgver}.tar.bz2)
sha1sums=('65bd96dc17da6b064f95109da02d9c9d14cb06dc')

build() {
  cd "${srcdir}/libXrender-${pkgver}"
  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd "${srcdir}/libXrender-${pkgver}"
  make DESTDIR=${pkgdir} install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
