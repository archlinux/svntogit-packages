# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdesdk-dolphin-plugins
pkgver=14.12.2
pkgrel=1
pkgdesc='Extra Dolphin plugins'
url='https://projects.kde.org/projects/kde/kdesdk/dolphin-plugins'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdesdk')
depends=('kdebase-runtime' 'kdebase-lib')
makedepends=('cmake' 'automoc4')
optdepends=('bzr: bazaar support'
            'git: git support'
            'mercurial: hg support'
            'subversion: svn support')
install=${pkgname}.install
source=("http://download.kde.org/stable/applications/${pkgver}/src/dolphin-plugins-${pkgver}.tar.xz")
sha1sums=('ff4719f288cdd75f666658a798c30bce9f8e0272')

build() {
  mkdir build
  cd build
  cmake ../dolphin-plugins-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
