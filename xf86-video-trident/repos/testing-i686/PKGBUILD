# $Id$
# Maintainer: Alexander Baldeck <alexander@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
pkgname=xf86-video-trident
pkgver=1.3.2
pkgrel=2
pkgdesc="X.org Trident video driver"
_mesaver="7.5"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=("libgl>=${_mesaver}" 'expat')
makedepends=('pkgconfig' 'xorg-server>=1.6.2' "mesa>=${_mesaver}" 'xf86driproto' 'dri2proto>=2.1' 'xf86dgaproto' 'glproto')
conflicts=('xorg-server<1.6.0')
groups=('xorg-video-drivers')
options=(!libtool)
source=(${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2
        http://downloads.sourceforge.net/sourceforge/mesa3d/MesaLib-${_mesaver}.tar.bz2
        mesa-7.1-link-shared.patch
        LICENSE)
md5sums=('26d77d8ec3887f20bef917d6429c761d'
         '459f332551f6ebb86f384d21dd15e1f0'
         '9eddc02e23ec08295822911e726ae89a'
         '92c153c192ee248572ec5fe8232a3d4d')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1

  cd "${srcdir}/Mesa-${_mesaver}"
  patch -Np1 -i "${srcdir}/mesa-7.1-link-shared.patch" || return 1
  ./configure --prefix=/usr \
    --with-dri-driverdir=/usr/lib/xorg/modules/dri \
    --with-dri-drivers=trident \
    --enable-glx-tls \
    --with-driver=dri \
    --enable-xcb \
    --disable-glu \
    --disable-glut \
    --disable-glw || return 1
  make -j1 || return 1
  cd src/mesa/drivers/dri || return 1
  make -j1 DESTDIR="${pkgdir}" install || return 1
  rm -rf "${pkgdir}/usr/lib/pkgconfig"
  rm -rf "${pkgdir}/usr/include"
  rm -f "${pkgdir}/usr/lib/xorg/modules/dri/libdricore.so"
  install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE" || return 1
}
