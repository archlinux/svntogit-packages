# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Fabio Scotoni <fabio@esse.ch>

pkgname=alpine
pkgver=2.00
pkgrel=4
_patchlevel=20
pkgdesc="The Apache-licensed PINE (a tool for reading, sending, and managing electronic messages)"
arch=('i686' 'x86_64')
url="http://www.washington.edu/alpine/"
license=('APACHE')
depends=('libldap' 'heimdal>=1.2.1' 'gettext')
optdepends=('aspell: for spell-checking support')
provides=('pine')
conflicts=('pine')
replaces=('pine')
options=('!makeflags')
# patch from http://staff.washington.edu/chappa/alpine/ :
# http://staff.washington.edu/chappa/alpine/patches/alpine-2.00/all.patch.gz
source=(ftp://ftp.cac.washington.edu/${pkgname}/${pkgname}.tar.bz2 \
	ftp://ftp.archlinux.org/other/$pkgname/all-${_patchlevel}.patch.gz)
md5sums=('84e44cbf71ed674800a5d57eed9c1c52' '5952d830d64d321040b9a3c0a0e74134')
sha1sums=('dcbd3c5419954f484ccf706feaba31ce48cdebc4' '3bc0006a5ff2bf5a67d2fc0ed390d653f5478a9b')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -p1 < ../all-${_patchlevel}.patch || return 1

  # -Wl,--as-needed does not work
  export LDFLAGS=""

  ./configure --prefix=/usr --without-passfile --without-tcl \
    --disable-shared --with-system-pinerc=/etc/alpine.d/pine.conf \
    --with-system-fixed-pinerc=/etc/alpine.d/pine.conf.fixed || return 1
  make || return 1
  make DESTDIR=${pkgdir} install || return 1
}
