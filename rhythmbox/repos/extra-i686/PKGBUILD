# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=rhythmbox
pkgver=2.98
pkgrel=4
pkgdesc="An iTunes-like music playback and management application"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.rhythmbox.org"
depends=('dconf' 'desktop-file-utils' 'gstreamer0.10-base-plugins' 
         'gstreamer0.10-good-plugins' 'gvfs-afc' 'json-glib'
         'libnotify' 'libpeas' 'media-player-info' 'python2-gobject' 
         'totem-plparser' 'tdb')
makedepends=('gnome-doc-utils' 'intltool' 'brasero' 'gobject-introspection' 
             'vala' 'gnome-common' 'gtk-doc' 'grilo' 'libdmapsharing' 'lirc-utils' 
             'libgpod' 'libmtp')
optdepends=('gstreamer0.10-ugly-plugins: Extra media codecs'
            'gstreamer0.10-bad-plugins: Extra media codecs'
            'gstreamer0.10-ffmpeg: Extra media codecs'
            'brasero: Audio CD Recorder plugin'
            'libdmapsharing: DAAP Music Sharing plugin'
            'grilo-plugins: Grilo media browser plugin'
            'lirc-utils: LIRC plugin'
            'libgpod: Portable Players - iPod plugin'
            'libmtp: Portable Players - MTP plugin'
            'zeitgeist: Zeitgeist plugin')
options=('!libtool' '!emptydirs')
install=rhythmbox.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz
	Disable-the-context-pane-plugin-when-webkit-isn-t-available.patch
	rhythmbox-2.98-xinitthreads.patch)
sha256sums=('a5c0a18a7c76133d36c7dcbdc37e304eae8272703985b17b540d68603b0c76d5'
            '6510214e7fe0a18d17d8c599ae065bd48c2e1f82931fe82e3d1cc684a0801cd6'
            'cd9af451a411e5a621c385d43f5ddd3664b3d085fc12dc7d5e5cd1f5db9e73b0')

build() {
  cd "$pkgname-$pkgver"
  patch -Np1 -i "${srcdir}/Disable-the-context-pane-plugin-when-webkit-isn-t-available.patch"
  patch -Np1 -i "${srcdir}/rhythmbox-2.98-xinitthreads.patch"
  autoreconf -fi
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/rhythmbox \
      --localstatedir=/var --disable-static \
      --enable-daap --enable-python --enable-vala \
      --with-mdns=avahi --disable-scrollkeeper --without-webkit
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  rm -r "$pkgdir/usr/lib/rhythmbox/sample-plugins"
}
