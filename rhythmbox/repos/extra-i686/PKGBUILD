# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=rhythmbox
pkgver=3.3.1
pkgrel=2
pkgdesc="Music playback and management application"
arch=(i686 x86_64)
license=(GPL)
url="https://wiki.gnome.org/Apps/Rhythmbox"
depends=(dconf gst-plugins-base gst-plugins-good libsoup json-glib libnotify libpeas
         media-player-info totem-plparser tdb webkit2gtk python-gobject libgudev)
makedepends=(itstool intltool brasero gobject-introspection vala grilo libdmapsharing lirc libgpod
             libmtp gtk-doc clutter-gst2 clutter-gtk mx)
optdepends=('gst-plugins-ugly: Extra media codecs'
            'gst-plugins-bad: Extra media codecs'
            'gst-libav: Extra media codecs'
            'brasero: Audio CD Recorder plugin'
            'libdmapsharing: DAAP Music Sharing plugin'
            'grilo-plugins: Grilo media browser plugin'
            'lirc: LIRC plugin'
            'libgpod: Portable Players - iPod plugin'
            'libmtp: Portable Players - MTP plugin'
            'gvfs-mtp: Portable Players - Android plugin'
            'mx: Visualiser plugin'
            'clutter-gtk: Visualiser plugin')
options=('!emptydirs')
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver:0:3}/$pkgname-$pkgver.tar.xz
        rhythmbox-grilo-0.3.0.patch)
sha256sums=('ef3c1023ed05025cd2aaed9ca83bf9bc557e4e8da660d7f31c5e3158fda93c61'
            'c84098f0706ad330204cc1c30b670c17b28cf3e501b987dd092a0df2bea27c40')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../rhythmbox-grilo-0.3.0.patch
}

build() {
  cd "$pkgname-$pkgver"
  NOCONFIGURE=1 ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/rhythmbox \
      --localstatedir=/var --disable-static \
      --enable-daap --enable-python --enable-vala

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  rm -r "$pkgdir/usr/lib/rhythmbox/sample-plugins"
  rm -r "$pkgdir/usr/lib/rhythmbox/plugins/rbzeitgeist"
}
