# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=rhythmbox
pkgver=0.12.8
pkgrel=1
pkgdesc="An iTunes-like music player/libary"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.rhythmbox.org"
depends=('libgpod>=0.7.2' 'libsoup-gnome>=2.29.91' 'gnome-media>=2.29.91' 'totem-plparser>=2.29.92' 'musicbrainz>=2.1.5' 'libmtp>=1.0.2' 'libnotify>=0.4.5' 'lirc-utils' 'desktop-file-utils' 'gstreamer0.10-python>=0.10.17' 'gstreamer0.10-base-plugins' 'gstreamer0.10-good-plugins' 'pygtk>=2.17.0' 'gvfs>=1.5.5' 'hicolor-icon-theme')
makedepends=('pkgconfig' 'gnome-doc-utils>=0.19.5' 'intltool' 'brasero>=2.29.92')
optdepends=('gstreamer0.10-ugly-plugins: Extra media codecs'
            'gstreamer0.10-bad-plugins: Extra media codecs'
            'gstreamer0.10-ffmpeg: Extra media codecs'
            'brasero: cd burning'
            'gnome-python: various plugins'
            'pywebkitgtk: context panel plugin'
            'python-mako: context panel plugin')
options=('!libtool' '!emptydirs')
install=rhythmbox.install
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/0.12/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('1752fab135808aa78a9afde78448e992020c58c07701f43c989fbabfc6352213')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/rhythmbox \
      --localstatedir=/var --disable-static \
      --enable-daap --enable-python \
      --with-mdns=avahi --disable-scrollkeeper || return 1

  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain rhythmbox ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
