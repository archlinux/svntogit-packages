# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=rhythmbox
pkgver=0.13.2
pkgrel=1
pkgdesc="An iTunes-like music player/libary"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.rhythmbox.org"
depends=('libgpod>=0.7.94' 'libsoup-gnome>=2.32.0' 'gnome-media>=2.32.0' 'totem-plparser>=2.32.1' 'libmusicbrainz3>=3.0.3' 'libmtp>=1.0.2' 'libnotify>=0.4.5' 'lirc-utils' 'desktop-file-utils' 'gstreamer0.10-python>=0.10.17' 'gstreamer0.10-base-plugins' 'gstreamer0.10-good-plugins' 'pygtk>=2.22.0' 'gvfs>=1.6.4' 'hicolor-icon-theme')
makedepends=('pkgconfig' 'gnome-doc-utils>=0.20.1' 'intltool' 'brasero>=2.31.92' 'libdmapsharing>=2.1.8')
optdepends=('gstreamer0.10-ugly-plugins: Extra media codecs'
            'gstreamer0.10-bad-plugins: Extra media codecs'
            'gstreamer0.10-ffmpeg: Extra media codecs'
            'brasero: cd burning'
            'gnome-python: various plugins'
            'pywebkitgtk: context panel plugin'
            'python-mako: context panel plugin'
            'libdmapsharing: DAAP support')
options=('!libtool' '!emptydirs')
install=rhythmbox.install
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/0.13/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('f422e47d7e238ebe862650efbcb83672bca704df0a37c3391ad0e386c20b19e3')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  PYTHON=python2 ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/rhythmbox \
      --localstatedir=/var --disable-static \
      --enable-daap --enable-python \
      --with-mdns=avahi --disable-scrollkeeper

  make
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain rhythmbox ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
