# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=rhythmbox
pkgver=2.98
pkgrel=2
pkgdesc="An iTunes-like music playback and management application"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.rhythmbox.org"
depends=('dconf' 'desktop-file-utils' 'grilo'
         'gstreamer0.10-base-plugins' 'gstreamer0.10-good-plugins' 'gvfs-afc' 'json-glib'
         'libdmapsharing' 'libgpod' 'libmtp' 'libnotify' 'libpeas'
         'lirc-utils' 'media-player-info' 'python2-gobject' 'python2-mako' 'totem-plparser'
         'tdb' 'libdiscid')
makedepends=('gnome-doc-utils' 'intltool' 'brasero' 'gobject-introspection' 'vala')
optdepends=('gstreamer0.10-ugly-plugins: Extra media codecs'
            'gstreamer0.10-bad-plugins: Extra media codecs'
            'gstreamer0.10-ffmpeg: Extra media codecs'
            'brasero: cd burning')
options=('!libtool' '!emptydirs')
install=rhythmbox.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz)
sha256sums=('a5c0a18a7c76133d36c7dcbdc37e304eae8272703985b17b540d68603b0c76d5')

build() {
  cd "$pkgname-$pkgver"
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/rhythmbox \
      --localstatedir=/var --disable-static \
      --enable-daap --enable-python --enable-vala \
      --with-mdns=avahi --disable-scrollkeeper --without-webkit
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  rm -r "$pkgdir/usr/lib/rhythmbox/sample-plugins"
}
