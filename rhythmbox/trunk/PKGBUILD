# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=rhythmbox
pkgver=3.4.5
pkgrel=1
pkgdesc="Music playback and management application"
arch=(x86_64)
license=(GPL)
url="https://wiki.gnome.org/Apps/Rhythmbox"
depends=(dconf gst-plugins-base gst-plugins-good libsoup json-glib libnotify
         libpeas media-player-info totem-plparser tdb python-gobject libgudev
         grilo)
makedepends=(brasero gobject-introspection vala libdmapsharing lirc libgpod
             libmtp zeitgeist gtk-doc yelp-tools git meson)
checkdepends=(check xorg-server-xvfb)
optdepends=('gst-plugins-ugly: Extra media codecs'
            'gst-plugins-bad: Extra media codecs'
            'gst-libav: Extra media codecs'
            'brasero: Audio CD Recorder plugin'
            'libdmapsharing: DAAP Music Sharing plugin'
            'grilo-plugins: Grilo media browser plugin'
            'lirc: LIRC plugin'
            'libgpod: Portable Players - iPod plugin'
            'libmtp: Portable Players - MTP plugin'
            'gvfs-mtp: Portable Players - Android plugin'
            'zeitgeist: Zeitgeist logging plugin')
options=(debug)
_commit=015ce7418d2bc6486f51f8fc0c21a4514fcbc50a  # tags/v3.4.5^0
source=("git+https://gitlab.gnome.org/GNOME/rhythmbox.git#commit=$_commit")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname

  # Allow building with a newer version of meson
  sed -i '/unsupported version of meson/d' meson.build
}

build() {
  arch-meson $pkgname build \
    -D gtk_doc=true \
    -D tests=true
  meson compile -C build
}

check() (
  glib-compile-schemas "${GSETTINGS_SCHEMA_DIR:=$PWD/$pkgname/data}"
  export GSETTINGS_SCHEMA_DIR GSETTINGS_BACKEND=memory CK_TIMEOUT_MULTIPLIER=3

  xvfb-run -s '-nolisten local' \
    meson test -C build --print-errorlogs
)

package() {
  meson install -C build --destdir "$pkgdir"

  python -m compileall -d /usr/lib "$pkgdir/usr/lib"
  python -O -m compileall -d /usr/lib "$pkgdir/usr/lib"
}
