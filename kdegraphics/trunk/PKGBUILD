# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdegraphics
pkgver=4.2.3
pkgrel=1
pkgdesc="KDE Graphics is a collection of graphic oriented applications:"
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('kde')
depends=('kdelibs' 'kdebase-runtime' 'poppler-qt' 'libspectre' 'sane' 'chmlib' 'libdjvu' 'lcms' 'qca' 'qimageblitz' 'ebook-tools')
makedepends=('pkgconfig' 'cmake' 'automoc4')
replaces=('gwenview' 'gwenview-i18n' 'libkexiv2' 'libkdcraw')
options=('docs')
install='kdegraphics.install'
source=("http://download.kde.org/stable/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2"
        '183850-workaround.patch')
md5sums=('29e9a466cd2b282a5139aeca8cc312a2'
         '1e63a65780b0aae6f817102be3d468ef')

build() {
	cd $srcdir/${pkgname}-${pkgver}
	# workaround for bug in Qt; see http://bugs.kde.org/show_bug.cgi?id=183850
	# should be removed with Qt 4.5.1
	patch -p1 -i $srcdir/183850-workaround.patch
	cd ../
	mkdir build
	cd build
	cmake ../${pkgname}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed' \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
	make DESTDIR=$pkgdir install
}
