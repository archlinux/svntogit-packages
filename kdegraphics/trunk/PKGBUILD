# $Id: PKGBUILD 59824 2009-11-28 01:40:14Z andrea $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kdegraphics
pkgname=('kdegraphics-gwenview'
         'kdegraphics-kamera'
         'kdegraphics-kcolorchooser'
         'kdegraphics-kgamma'
         'kdegraphics-kolourpaint'
         'kdegraphics-kruler'
         'kdegraphics-ksnapshot'
         'kdegraphics-libs'
         'kdegraphics-okular')
pkgver=4.4.4
pkgrel=2
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegraphics')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'kdelibs' 'kdebase-runtime'
             'poppler-qt' 'libspectre' 'sane' 'chmlib' 'libdjvu' 'lcms' 'qca'
             'qimageblitz' 'ebook-tools' 'libxxf86vm')
source=("http://download.kde.org/stable/${pkgver}/src/${pkgbase}-${pkgver}.tar.bz2"
	'fix-libjpeg8-compatibility.patch'
        'update-libkexiv2-libkipi-libdcraw.patch')

build() {
	# Fixed in 4.5
	cd ${srcdir}/${pkgbase}-${pkgver}
	patch -Np1 -i ${srcdir}/fix-libjpeg8-compatibility.patch || return 1
        patch -Np1 -i ${srcdir}/update-libkexiv2-libkipi-libdcraw.patch || return 1
	cd ${srcdir}
	mkdir build
	cd build
	cmake ../${pkgbase}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package_kdegraphics-gwenview() {
	pkgdesc='A simple image viewer'
	depends=('kdebase-runtime' 'kdegraphics-libs')
	replaces=('gwenview' 'gwenview-i18n')
	install='kdegraphics.install'
	cd $srcdir/build/gwenview
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/gwenview
	make DESTDIR=$pkgdir install
}

package_kdegraphics-kamera() {
	pkgdesc='Configure Kamera'
	depends=('kdelibs' 'libgphoto2')
	cd $srcdir/build/kamera
	make DESTDIR=$pkgdir install
}

package_kdegraphics-kcolorchooser() {
	pkgdesc='Color Chooser'
	depends=('kdelibs' 'kdebase-runtime')
	install='kdegraphics.install'
	cd $srcdir/build/kcolorchooser
	make DESTDIR=$pkgdir install
}

package_kdegraphics-kgamma() {
	pkgdesc='A monitor calibration tool'
	# note on libxxf86vm:
	#       not detected by namcap because libgl depends on it
	#       but nvidia providing libgl does not depend on libxxf86vm
	depends=('kdelibs' 'kdebase-runtime' 'libxxf86vm')
	install='kdegraphics.install'
	cd $srcdir/build/kgamma
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kgamma
	make DESTDIR=$pkgdir install
}

package_kdegraphics-kolourpaint() {
	pkgdesc='Paint Program'
	depends=('kdelibs' 'kdebase-runtime' 'qimageblitz' 'kdegraphics-libs')
	install='kdegraphics.install'
	cd $srcdir/build/kolourpaint
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kolourpaint
	make DESTDIR=$pkgdir install
}

package_kdegraphics-kruler() {
	pkgdesc='Screen Ruler'
	depends=('kdelibs' 'kdebase-runtime')
	install='kdegraphics.install'
	cd $srcdir/build/kruler
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kruler
	make DESTDIR=$pkgdir install
}

package_kdegraphics-ksnapshot() {
	pkgdesc='Screen Capture Program'
	depends=('kdelibs' 'kdebase-runtime')
	install='kdegraphics.install'
	cd $srcdir/build/ksnapshot
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/ksnapshot
	make DESTDIR=$pkgdir install
}

package_kdegraphics-libs() {
	pkgdesc='Support library for KDE Graphics Software'
	groups=()
	depends=('kdelibs' 'sane' 'lcms')
	conflicts=('kdegraphics-ksaneplugin' 'kdegraphics-strigi-analyzer'
                   'kdegraphics-svgpart' 'kdegraphics-thumbnailers')
	replaces=('libkexiv2' 'libkdcraw' 'kdegraphics-ksaneplugin'
                  'kdegraphics-strigi-analyzer' 'kdegraphics-svgpart' 'kdegraphics-thumbnailers')
	install='kdegraphics.install'
	for i in libs ksaneplugin strigi-analyzer svgpart thumbnailers; do
		cd $srcdir/build/${i}
		make DESTDIR=$pkgdir install
	done
	install -D -m644 $srcdir/${pkgbase}-${pkgver}/cmake/modules/FindKSane.cmake \
		$pkgdir/usr/share/apps/cmake/modules/FindKSane.cmake
}

package_kdegraphics-okular() {
	pkgdesc='Document Viewer'
	depends=('kdelibs' 'kdebase-runtime' 'qimageblitz' 'poppler-qt'
                 'chmlib' 'libdjvu' 'libspectre' 'ebook-tools' 'kdegraphics-libs')
	install='kdegraphics.install'
	cd $srcdir/build/okular
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/okular
	make DESTDIR=$pkgdir install
	install -D -m644 $srcdir/${pkgbase}-${pkgver}/cmake/modules/FindOkular.cmake \
		$pkgdir/usr/share/apps/cmake/modules/FindOkular.cmake
}
md5sums=('4555d85bb1e302d085d14d2c4ad01323'
         '492dc2dfe799ff81d55fcb488faf2860')
md5sums=('4555d85bb1e302d085d14d2c4ad01323'
         '492dc2dfe799ff81d55fcb488faf2860'
         'd34f1ac582c1f48023d7e2ec3cf11e2b')
