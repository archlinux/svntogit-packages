# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-palapeli
pkgver=16.12.3
pkgrel=1
pkgdesc="A single-player jigsaw puzzle game"
url="https://kde.org/applications/games/palapeli/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegames')
depends=('kdebase-runtime' 'libkdegames4' 'qhull')
makedepends=('cmake' 'automoc4')
source=("https://download.kde.org/stable/applications/${pkgver}/src/palapeli-${pkgver}.tar.xz"{,.sig})
sha256sums=('726485261d5be91a402e2dac64d25aedf98cfa46487a6dcc4fc30585e4f2498f'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

build() {
  mkdir -p build
  cd build
  cmake ../palapeli-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
