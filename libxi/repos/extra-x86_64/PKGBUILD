# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libxi
pkgver=1.7.8
pkgrel=1
pkgdesc="X11 Input extension library"
arch=('i686' 'x86_64')
url="https://xorg.freedesktop.org"
depends=('libxext' 'inputproto')
makedepends=('pkgconfig' 'xorg-util-macros' 'libxfixes' 'automake')
license=('custom')
source=(${url}/releases/individual/lib/libXi-${pkgver}.tar.bz2{,.sig})
sha256sums=('d8f2fa8d53141c41ff521627df9b2fa9c05f6f142fd9881152bab36549ac27bb'
            'SKIP')
validpgpkeys=('3C2C43D9447D5938EF4551EBE23B7E70B467F0BF') # Peter Hutterer
validpgpkeys+=('C41C985FDCF1E5364576638B687393EE37D128F8') # Matthieu Herrb <matthieu.herrb@laas.fr>

build() {
  cd libXi-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static
  make
}

package() {
  cd libXi-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
