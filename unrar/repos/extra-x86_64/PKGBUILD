# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: dorphell <dorphell@gmx.net>

pkgbase=unrar
pkgname=(unrar libunrar)
pkgver=5.7.3
pkgrel=1
epoch=1
arch=(x86_64)
url="https://www.rarlab.com/rar_add.htm"
license=(custom)
source=("https://www.rarlab.com/rar/unrarsrc-${pkgver}.tar.gz")
sha256sums=('40e856b78374f258d8a1f5f02c02f828c5392a0118c9300fd169a300b520a444')

build() {
  cp -a unrar libunrar
  make -C libunrar lib
  make -C unrar STRIP="true"
}

package_unrar() {
  pkgdesc="The RAR uncompression program"
  depends=(gcc-libs)
  cd unrar
  install -Dm755 unrar "$pkgdir"/usr/bin/unrar
  # install license
  install -Dm644 license.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_libunrar() {
  pkgdesc="Library and header file for applications that use libunrar"
  depends=(gcc-libs)
  cd libunrar
  install -Dm755 libunrar.so "$pkgdir"/usr/lib/libunrar.so
  install -Dm644 dll.hpp "$pkgdir"/usr/include/unrar/dll.hpp
  # install license
  install -Dm644 license.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
