# $Id$ 
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Kritoke <kritoke@nospam.gmail.com>

pkgname=gnome-blog
pkgver=0.9.2
pkgrel=4
pkgdesc="A gnome application that allows you to post entries to many different blog formats."
arch=('any')
license=('GPL')
depends=('python2-gconf' 'python2-libgnome' 'python2-gtkspell' 'python-gdata')
makedepends=('intltool' 'desktop-file-utils' 'gnome-python')
optdepends=('python-gnomeapplet: GNOME panel applet')
install=gnome-blog.install
url="http://www.gnome.org/~seth/gnome-blog/"
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/0.9/${pkgname}-${pkgver}.tar.bz2)
md5sums=('39ec54600a6b8dd786729c401fcfdf27')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  find . -type f | xargs sed -i 's@^#!.*python$@#!/usr/bin/python2@'
  rm -f GNOME_BlogApplet.server.in
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --sysconfdir=/etc \
    --libexecdir=/usr/lib/gnome-blog \
    --localstatedir=/var
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -dm755 "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" \
    --domain gnome-blog ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f "${pkgdir}/etc/gconf/schemas/"*.schemas
}
