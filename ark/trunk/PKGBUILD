# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org> 
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=ark
pkgver=17.04.1
pkgrel=1
pkgdesc='Archiving Tool'
arch=(i686 x86_64)
url='https://kde.org/applications/utilities/ark/'
license=(GPL)
depends=(kparts kpty libarchive libzip kitemmodels hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools python)
optdepends=('p7zip: 7Z format support' 'unrar: RAR format support')
conflicts=(kdeutils-ark)
replaces=(kdeutils-ark)
groups=(kde-applications kdeutils)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('b642dc57aa1a22e0bc9757d3154a35da3a91bd2917ef03b74e82ee304d9d3223'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build
}

build() { 
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}

