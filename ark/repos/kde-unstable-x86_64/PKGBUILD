# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org> 
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=ark
pkgver=15.11.80
pkgrel=1
pkgdesc='Archiving Tool'
arch=(i686 x86_64)
url='http://kde.org/applications/utilities/ark/'
license=(GPL)
depends=(kpty khtml libarchive hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools python)
optdepends=(p7zip zip unzip unrar)
conflicts=(kdeutils-ark)
replaces=(kdeutils-ark)
groups=(kde-applications kdeutils)
install=$pkgname.install
source=("http://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
md5sums=('4d57e95c4142c29e69603992b1f5f314')

prepare() {
  mkdir -p build
}

build() { 
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}

