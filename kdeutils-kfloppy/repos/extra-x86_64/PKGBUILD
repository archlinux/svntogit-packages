# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeutils-kfloppy
pkgver=4.8.0
pkgrel=1
pkgdesc='Floppy Formatter'
url='http://kde.org/applications/utilities/kfloppy/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
groups=('kde' 'kdeutils')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/xz/kfloppy-${pkgver}.tar.xz")
sha1sums=('5f1ea4bf5e145ce3eb8cc136ac36c57675ad489c')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../kfloppy-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $srcdir/build
  make DESTDIR=$pkgdir install
  cd $srcdir/build/doc
  make DESTDIR=$pkgdir install
}
