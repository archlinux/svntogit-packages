# $Id$
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=ecasound
pkgver=2.8.1
pkgrel=3
pkgdesc="A software package designed for multitrack audio processing"
arch=('i686' 'x86_64')
url="http://www.eca.cx/ecasound/"
license=('GPL' 'LGPL')
depends=('gcc-libs' 'readline>=6.0.00' 'audiofile'
         'jack' 'liblo' 'liboil')
makedepends=('pkg-config' 'python2' 'ruby')
optdepends=('python2: ecamonitor, ECI API'
            'ruby: ECI API'
            'mpg123: MP3 decoding'
            'lame: MP3 encoding'
            'vorbis-tools: Ogg Vorbis decoding/encoding'
            'faad2: AAC decoding'
            'faac: AAC encoding'
            'timidity++: MIDI file input'
            'libmikmod: tracker module')
options=('!libtool')
source=(http://ecasound.seul.org/download/$pkgname-$pkgver.tar.gz)
md5sums=('d9ded0074a8eeb59dd507c248220d010')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # use vendor_ruby for ruby files
  sed -e 's/CONFIG\["sitedir"\]/CONFIG\["vendordir"\]/' -i configure

  PYTHONPATH=/usr/bin/python2 ./configure \
    --prefix=/usr \
    --with-python-includes=/usr/include/python2.7 \
    --with-python-modules=/usr/lib/python2.7

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  # fix for python 2.7
  sed -i 's:bin/env python:bin/env python2:' \
    "$pkgdir/usr/bin/ecamonitor"
}

# vim:set ts=2 sw=2 et:
