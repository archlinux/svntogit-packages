# $Id$
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=ecasound
pkgver=2.9.1
pkgrel=5
pkgdesc="Command-line multitrack audio processor"
arch=('i686' 'x86_64')
url="http://www.eca.cx/ecasound/"
license=('GPL' 'LGPL')
depends=('audiofile' 'liblo>=0.28' 'liboil' 'lilv')
makedepends=('python2' 'ruby')
optdepends=('python2: ecamonitor, ECI API'
            'ruby: ECI API'
            'mpg123: MP3 decoding'
            'lame: MP3 encoding'
            'vorbis-tools: Ogg Vorbis decoding/encoding'
            'faad2: AAC decoding'
            'faac: AAC encoding'
            'timidity++: MIDI file input'
            'libmikmod: tracker module')
source=("http://ecasound.seul.org/download/$pkgname-$pkgver.tar.gz"
        'ruby2.patch')
options=('staticlibs')
md5sums=('13c7be1e4eddc0bbf3792dc17777e465'
         '6909dbd4f67f76d3bd31ce7c7fe16212')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # fix ruby 2.0 incompatibility + use vendor_ruby for ruby files
  patch -Np1 -i "$srcdir/ruby2.patch"

  PYTHONPATH=/usr/bin/python2 ./configure \
    --prefix=/usr \
    --with-python-includes=/usr/include/python2.7 \
    --with-python-modules=/usr/lib/python2.7

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  # fix for python 2.7
  sed -i 's:bin/env python:bin/env python2:' \
    "$pkgdir/usr/bin/ecamonitor"
}

# vim:set ts=2 sw=2 et:
