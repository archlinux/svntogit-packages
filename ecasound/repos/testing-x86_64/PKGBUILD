# $Id$
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=ecasound
pkgver=2.9.1
pkgrel=10
pkgdesc="Command-line multitrack audio processor"
arch=('x86_64')
url="https://ecasound.seul.org/ecasound/"
license=('GPL' 'LGPL')
depends=('audiofile' 'liblo>=0.28' 'lilv')
makedepends=('python2' 'ruby')
optdepends=('python2: ecamonitor, ECI API'
            'ruby: ECI API'
            'mpg123: MP3 decoding'
            'lame: MP3 encoding'
            'vorbis-tools: Ogg Vorbis decoding/encoding'
            'faad2: AAC decoding'
            'faac: AAC encoding'
            'timidity++: MIDI file input'
            'libmikmod: tracker module')
source=("https://ecasound.seul.org/download/$pkgname-$pkgver.tar.gz"
        'ruby2.patch')
options=('staticlibs')
sha256sums=('39fce8becd84d80620fa3de31fb5223b2b7d4648d36c9c337d3739c2fad0dcf3'
            '5dda8e8bd245d90975f57bd68e05c50f72076f7b82fab02fdd5a510275f0f188')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # fix ruby 2.0 incompatibility + use vendor_ruby for ruby files
  patch -Np1 -i "$srcdir/ruby2.patch"
}
build() {
  cd "$srcdir/$pkgname-$pkgver"

  PYTHONPATH=/usr/bin/python2 ./configure \
    --prefix=/usr \
    --with-python-includes=/usr/include/python2.7 \
    --with-python-modules=/usr/lib/python2.7 \
    --enable-liboil=no

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  # fix for python 2.7
  sed -i 's:bin/env python:bin/env python2:' \
    "$pkgdir/usr/bin/ecamonitor"
}

# vim:set ts=2 sw=2 et:
