# $Id$
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=ecasound
pkgver=2.7.2
pkgrel=1
pkgdesc="A software package designed for multitrack audio processing"
arch=('i686' 'x86_64')
url="http://www.eca.cx/ecasound/"
license=('GPL' 'LGPL')
depends=('gcc-libs' 'readline>=6.0.00' 'audiofile'
         'jack' 'liblo' 'liboil')
makedepends=('pkg-config' 'python2' 'ruby')
optdepends=('python2: ecamonitor, ECI API'
            'ruby: ECI API'
            'mpg123: MP3 decoding'
            'lame: MP3 encoding'
            'vorbis-tools: Ogg Vorbis decoding/encoding'
            'faad2: AAC decoding'
            'faac: AAC encoding'
            'timidity++: MIDI file input'
            'libmikmod: tracker module')
options=('!libtool')
changelog=$pkgname.changelog
source=(http://ecasound.seul.org/download/$pkgname-$pkgver.tar.gz)
md5sums=('40498ceed9cc7622ee969c427f13921c')
sha1sums=('6d5236155e1a1f9a9bc9e63f989baebfb1645a49')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  PYTHONPATH=/usr/bin/python2 ./configure \
    --prefix=/usr \
    --with-python-includes=/usr/include/python2.7 \
    --with-python-modules=/usr/lib/python2.7

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  # fix for python 2.7
  sed -i 's:bin/env python:bin/env python2:' \
    "$pkgdir/usr/bin/ecamonitor"
}

# vim:set ts=2 sw=2 et:
