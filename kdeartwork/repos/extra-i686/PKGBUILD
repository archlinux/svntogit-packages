# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kdeartwork
pkgname=('kdeartwork-colorschemes'
         'kdeartwork-iconthemes'
         'kdeartwork-weatherwallpapers'
         'kdeartwork-desktopthemes'
         'kdeartwork-emoticons'
         'kdeartwork-kscreensaver'
         'kdeartwork-sounds'
         'kdeartwork-styles'
         'kdeartwork-wallpapers')
pkgver=4.3.0
pkgrel=1
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeartwork')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'xscreensaver' 'eigen' 'kdebase-workspace')
source=("http://download.kde.org/stable/${pkgver}/src/${pkgbase}-${pkgver}.tar.bz2")
md5sums=('3e7872e841e87f716872086571028a37')

build() {
	cd $srcdir
	mkdir build
	cd build
	cmake ../${pkgbase}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed' \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package_kdeartwork-colorschemes() {
	pkgdesc='KDE color schemes'
	cd $srcdir/build/ColorSchemes
	make DESTDIR=$pkgdir install
}

package_kdeartwork-iconthemes() {
	pkgdesc='KDE icon themes'
	cd $srcdir/build/IconThemes
	make DESTDIR=$pkgdir install
}

package_kdeartwork-weatherwallpapers() {
	pkgdesc='KDE weather wallpapers'
	cd $srcdir/build/WeatherWallpapers
	make DESTDIR=$pkgdir install
}

package_kdeartwork-desktopthemes() {
	pkgdesc='KDE desktop themes'
	cd $srcdir/build/desktopthemes
	make DESTDIR=$pkgdir install
}

package_kdeartwork-emoticons() {
	pkgdesc='KDE emoticons'
	cd $srcdir/build/emoticons
	make DESTDIR=$pkgdir install
}

package_kdeartwork-kscreensaver() {
	pkgdesc='KDE screensaver'
	depends=('kdebase-workspace' )
	cd $srcdir/build/kscreensaver
	make DESTDIR=$pkgdir install
}

package_kdeartwork-sounds() {
	pkgdesc='KDE sounds'
	cd $srcdir/build/sounds
	make DESTDIR=$pkgdir install
}

package_kdeartwork-styles() {
	pkgdesc='KDE styles'
	depends=('kdelibs')
	cd $srcdir/build/styles
	make DESTDIR=$pkgdir install
}

package_kdeartwork-wallpapers() {
	pkgdesc='KDE wallpapers'
	cd $srcdir/build/wallpapers
	make DESTDIR=$pkgdir install
}

