# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kdeartwork
pkgname=('kdeartwork-aurorae'
         'kdeartwork-colorschemes'
         'kdeartwork-desktopthemes'
         'kdeartwork-emoticons'
         'kdeartwork-iconthemes'
         'kdeartwork-kscreensaver'
         'kdeartwork-sounds'
         'kdeartwork-styles'
         'kdeartwork-wallpapers'
         'kdeartwork-weatherwallpapers')
pkgver=4.5.3
pkgrel=1
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeartwork')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'xscreensaver' 'eigen' 'kdebase-workspace'
	'docbook-xsl')
source=("http://download.kde.org/stable/${pkgver}/src/${pkgbase}-${pkgver}.tar.bz2")
sha1sums=('3ad04f3dc270fe18262734d0a79a2405a15ed6de')

build() {
	cd $srcdir
	mkdir build
	cd build
	cmake ../${pkgbase}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package_kdeartwork-aurorae() {
	pkgdesc='An Aurorae theme based on the Oxygen plasma theme'
	cd $srcdir/build/aurorae
	make DESTDIR=$pkgdir install
}

package_kdeartwork-colorschemes() {
	pkgdesc='KDE color schemes'
	cd $srcdir/build/ColorSchemes
	make DESTDIR=$pkgdir install
}

package_kdeartwork-desktopthemes() {
	pkgdesc='KDE desktop themes'
	cd $srcdir/build/desktopthemes
	make DESTDIR=$pkgdir install
}

package_kdeartwork-emoticons() {
	pkgdesc='KDE emoticons'
	cd $srcdir/build/emoticons
	make DESTDIR=$pkgdir install
}

package_kdeartwork-iconthemes() {
	pkgdesc='KDE icon themes'
	cd $srcdir/build/IconThemes
	make DESTDIR=$pkgdir install
}

package_kdeartwork-kscreensaver() {
	pkgdesc='KDE screensaver'
	depends=('kdebase-workspace' 'kdegraphics-libs')
	cd $srcdir/build/kscreensaver
	make DESTDIR=$pkgdir install
}

package_kdeartwork-sounds() {
	pkgdesc='KDE sounds'
	cd $srcdir/build/sounds
	make DESTDIR=$pkgdir install
}

package_kdeartwork-styles() {
	pkgdesc='KDE styles'
	depends=('kdebase-workspace')
	cd $srcdir/build/styles
	make DESTDIR=$pkgdir install
}

package_kdeartwork-wallpapers() {
	pkgdesc='KDE wallpapers'
	cd $srcdir/build/wallpapers
	make DESTDIR=$pkgdir install
	cd $srcdir/build/HighResolutionWallpapers
	make DESTDIR=$pkgdir install
}

package_kdeartwork-weatherwallpapers() {
	pkgdesc='KDE weather wallpapers'
	cd $srcdir/build/WeatherWallpapers
	make DESTDIR=$pkgdir install
}
