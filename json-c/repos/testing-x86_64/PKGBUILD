# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>
# Contributor: congyiwu <congyiwu AT gmail DOT com>

pkgname=json-c
pkgver=0.13.1
pkgrel=1
pkgdesc='A JSON implementation in C'
url='https://github.com/json-c/json-c/wiki'
license=(MIT)
arch=('x86_64')
depends=('glibc')
_releasedate=20180305
source=("https://github.com/json-c/json-c/archive/json-c-${pkgver}-${_releasedate}.tar.gz")
sha256sums=('5d867baeb7f540abe8f3265ac18ed7a24f91fe3c5f4fd99ac3caba0708511b90')

build() {
  cd $pkgname-$pkgname-$pkgver-$_releasedate
  ./configure --prefix=/usr --disable-static
  make
}

check() {
  cd $pkgname-$pkgname-$pkgver-$_releasedate
  make check
}

package() {
  cd $pkgname-$pkgname-$pkgver-$_releasedate
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
