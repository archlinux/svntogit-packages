# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=freeciv
pkgver=2.5.0
pkgrel=2
pkgdesc="A multiuser clone of the famous Microprose game of Civilization"
arch=('i686' 'x86_64')
url="http://www.freeciv.org/"
license=('GPL')
depends=('gtk2' 'sdl_mixer' 'lua52' 'sqlite' 'curl' 'libltdl')
backup=('etc/freeciv/database.lua')
install=freeciv.install
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.bz2
        lua52_compat.patch)
sha1sums=('477b60e02606e47b31a019b065353c1a6da6c305'
          '248f4c8b62557677bf27a0d06feb15148d811ce7')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 < "$srcdir"/lua52_compat.patch
  ./autogen.sh
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc --enable-shared --enable-sys-lua \
    --enable-client=gtk2 --enable-fcdb=sqlite3 --enable-aimodules
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
