# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-xset
pkgver=1.2.3
pkgrel=1
pkgdesc="User preference utility for X"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libxmu')
makedepends=('xorg-util-macros')
groups=('xorg-apps' 'xorg')
source=(http://xorg.freedesktop.org/archive/individual/app/xset-${pkgver}.tar.bz2)
sha256sums=('4382f4fb29b88647e13f3b4bc29263134270747fc159cfc5f7e3af23588c8063')

build() {
  cd xset-${pkgver}
  ./configure --prefix=/usr --without-fontcache --without-xf86misc
  make
 }
 
package() {
  cd xset-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
