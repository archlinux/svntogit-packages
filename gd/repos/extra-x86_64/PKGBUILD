# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=gd
pkgver=2.3.2
pkgrel=4
pkgdesc="Library for the dynamic creation of images by programmers"
arch=('x86_64')
url="https://libgd.github.io/"
license=('custom')
depends=('fontconfig' 'libxpm' 'libwebp' 'libavif' 'libheif')
optdepends=('perl: bdftogd script')
checkdepends=('ttf-liberation')
source=("https://github.com/libgd/libgd/archive/${pkgname}-${pkgver}.tar.gz")
sha256sums=('dcc22244d775f469bee21dce1ea42552adbb72ba0cc423f9fa6a64601b3a1893')

build() {
  cd libgd-${pkgname}-${pkgver}

  ./bootstrap.sh
  ./configure \
    --prefix=/usr \
    --disable-rpath
  make
}

check() {
  cd libgd-${pkgname}-${pkgver}

  make check
}

package() {
  cd libgd-${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
