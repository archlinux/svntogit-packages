# $Id$

pkgname=gd
pkgver=2.1.0
pkgrel=3
pkgdesc="Library for the dynamic creation of images by programmers"
arch=('i686' 'x86_64')
url="http://www.libgd.org/"
license=('custom')
depends=('fontconfig' 'libvpx' 'libxpm' 'libtiff')
optdepends=('perl: bdftogd script')
source=("https://bitbucket.org/libgd/gd-libgd/downloads/libgd-${pkgver}.tar.xz"
        'color_c_null_pointer.patch')
md5sums=('03588159bf4faab9079849c8d709acc6'
         '51f5ff073eabbd7809e0ce438d5bb1c2')

prepare() {
  cd libgd-${pkgver}
  # CVE-2014-2497
  patch -p1 -i ${srcdir}/color_c_null_pointer.patch
}

build() {
  cd libgd-${pkgver}
  ./configure \
    --prefix=/usr \
    --disable-rpath \
    --with-vpx=/usr \
    --with-tiff=/usr
  make
}

check() {
  cd libgd-${pkgver}
  make check
}

package() {
  cd libgd-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
