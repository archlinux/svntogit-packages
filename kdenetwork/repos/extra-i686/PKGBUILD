# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kdenetwork
pkgname=('kdenetwork-filesharing'
         'kdenetwork-kdnssd'
         'kdenetwork-kget'
         'kdenetwork-kopete'
         'kdenetwork-kppp'
         'kdenetwork-krdc'
         'kdenetwork-krfb')
pkgver=4.4.5
pkgrel=1
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdenetwork')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'boost' 'speex' 'ortp' 'libotr'
             'qca-ossl' 'kdebase-workspace' 'kdebase-lib' 'libvncserver' 'libmsn'
             'ppp' 'libv4l' 'libidn' 'rdesktop' 'qimageblitz' 'libxss' 'libxdamage'
             'libgadu' 'telepathy-qt4')
source=("http://download.kde.org/stable/${pkgver}/src/${pkgbase}-${pkgver}.tar.bz2")
sha1sums=('5f321ce932d6b1fadf1935e527bd1848c317be7a')

build() {
	cd ${srcdir}

	mkdir build
	cd build
	cmake ../${pkgbase}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DWITH_Xmms=OFF \
		-DWITH_Decibel=OFF \
		-DWITH_LibMeanwhile=OFF
	make
}

package_kdenetwork-filesharing() {
	pkgdesc='Konqueror properties dialog plugin to share a directory with the local network'
	depends=('kdelibs' 'smbclient')
	install='kdenetwork.install'
	cd $srcdir/build/filesharing
	make DESTDIR=$pkgdir install
}

package_kdenetwork-kdnssd() {
	pkgdesc='Network Services'
	depends=('kdelibs')
	cd $srcdir/build/kdnssd
	make DESTDIR=$pkgdir install
}

package_kdenetwork-kget() {
	url="http://userbase.kde.org/KGet"
	pkgdesc='Download Manager'
	depends=('kdebase-workspace' 'kdebase-lib' 'qca')
	install='kdenetwork.install'
	cd $srcdir/build/kget
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kget
	make DESTDIR=$pkgdir install
}

package_kdenetwork-kopete() {
	url="http://kopete.kde.org/"
	pkgdesc='Instant Messenger'
	depends=('kdebase-runtime' 'kdepimlibs' 'qca-ossl' 'libotr' 'libmsn'
	         'libidn' 'qimageblitz' 'libxss' 'libv4l' 'libgadu')
	install='kdenetwork.install'
	cd $srcdir/build/kopete
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kopete
	make DESTDIR=$pkgdir install
}

package_kdenetwork-kppp() {
	pkgdesc='Internet Dial-Up Tool'
	depends=('kdebase-runtime' 'kdelibs' 'ppp')
	install='kdenetwork.install'
	cd $srcdir/build/kppp
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kppp
	make DESTDIR=$pkgdir install
}

package_kdenetwork-krdc() {
	pkgdesc='Remote Desktop Client'
	depends=('kdebase-runtime' 'kdelibs' 'libvncserver' 'rdesktop' 'telepathy-qt4')
	cd $srcdir/build/krdc
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/krdc
	make DESTDIR=$pkgdir install
}

package_kdenetwork-krfb() {
	pkgdesc='Desktop Sharing'
	# note on libxdamage:
	#       not detected by namcap because libgl depends on it
	#       but nvidia providing libgl does not depend on libxdamage
	depends=('kdebase-runtime' 'kdelibs' 'libvncserver' 'libxdamage')
	cd $srcdir/build/krfb
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/krfb
	make DESTDIR=$pkgdir install
}
