# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kdenetwork
pkgname=('kdenetwork-filesharing'
         'kdenetwork-kdnssd'
         'kdenetwork-kget'
         'kdenetwork-kopete'
         'kdenetwork-kppp'
         'kdenetwork-krdc'
         'kdenetwork-krfb')
pkgver=4.9.4
pkgrel=1
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdenetwork')
makedepends=('cmake' 'automoc4' 'boost' 'speex' 'ortp' 'libotr3' 'ppp'
	'qca-ossl' 'kdebase-workspace' 'kdebase-lib' 'libvncserver'
    'v4l-utils' 'libidn' 'qimageblitz' 'libxdamage' 'libgadu' 'libmsn'
    'libktorrent' 'libmms' 'mediastreamer' 'telepathy-qt')
source=("http://download.kde.org/stable/${pkgver}/src/${pkgbase}-${pkgver}.tar.xz"
        'use-libotr3.patch')
sha1sums=('1541bfd584fc86e29782dbfdbb94c2b183a2bd48'
          '9c3b0ee15538fbfa36aa0a4748b1f6b5a7905384')

build() {
    cd ${pkgbase}-${pkgver}
    patch -p1 -i "${srcdir}"/use-libotr3.patch
    cd ../

	mkdir build
	cd build
	cmake ../${pkgbase}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DMOZPLUGIN_INSTALL_DIR=/usr/lib/mozilla/plugins/ \
        -DWITH_Xmms=OFF \
		-DWITH_LibMeanwhile=OFF \
        -DWITH_qq=OFF
	make
}

package_kdenetwork-filesharing() {
	pkgdesc='Konqueror properties dialog plugin to share a directory with the local network'
	depends=('kdelibs' 'smbclient')
	install='kdenetwork.install'
	cd $srcdir/build/filesharing
	make DESTDIR=$pkgdir install
}

package_kdenetwork-kdnssd() {
	pkgdesc='Monitors the network for DNS-SD services'
	depends=('kdelibs')
	cd $srcdir/build/kdnssd
	make DESTDIR=$pkgdir install
}

package_kdenetwork-kget() {
	pkgdesc='Download Manager'
	depends=('kdebase-workspace' 'kdebase-lib' 'libktorrent' 'libmms')
	url="http://kde.org/applications/internet/kget/"
	install='kdenetwork-kget.install'
	cd $srcdir/build/kget
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kget
	make DESTDIR=$pkgdir install
}

package_kdenetwork-kopete() {
	pkgdesc='Instant Messenger'
	depends=('kdebase-runtime' 'kdepimlibs' 'qca-ossl' 'libotr3' 'libmsn'
	         'libidn' 'qimageblitz' 'libgadu' 'mediastreamer')
	url="http://kde.org/applications/internet/kopete/"
	install='kdenetwork-kopete.install'
	cd $srcdir/build/kopete
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kopete
	make DESTDIR=$pkgdir install
}

package_kdenetwork-kppp() {
	pkgdesc='Internet Dial-Up Tool'
	depends=('kdebase-runtime' 'ppp')
	url="http://kde.org/applications/internet/kppp/"
	install='kdenetwork-kppp.install'
	cd $srcdir/build/kppp
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kppp
	make DESTDIR=$pkgdir install
}

package_kdenetwork-krdc() {
	pkgdesc='Remote Desktop Client'
	depends=('kdebase-runtime' 'telepathy-qt')
    optdepends=('libvncserver: VNC support'
                'rdesktop: RDP support'
                'kdebase-keditbookmarks: to edit bookmarks')
	url="http://kde.org/applications/internet/krdc/"
	cd $srcdir/build/krdc
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/krdc
	make DESTDIR=$pkgdir install
}

package_kdenetwork-krfb() {
	pkgdesc='Desktop Sharing'
	# note on libxdamage:
	#       not detected by namcap because libgl depends on it
	#       but nvidia providing libgl does not depend on libxdamage
	depends=('kdebase-runtime' 'libvncserver' 'libxdamage' 'telepathy-qt')
	cd $srcdir/build/krfb
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/krfb
	make DESTDIR=$pkgdir install
}
