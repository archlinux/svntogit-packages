# $Id$
# Maintainer: Tom Gundersen <teg@jklm.no>
# Maintainer: Eric Belanger <eric@archlinux.org>

pkgname=sysvinit-tools
pkgdesc="Linux System V Init Tools"
pkgver=2.88
pkgrel=12
arch=('i686' 'x86_64')
url="http://savannah.nongnu.org/projects/sysvinit"
license=('GPL')
depends=('glibc')
source=(http://download.savannah.gnu.org/releases/sysvinit/sysvinit-${pkgver}dsf.tar.bz2
        "0001-simplify-writelog.patch"
        "0002-remove-ansi-escape-codes-from-log-file.patch")
sha1sums=('f2ca149df1314a91f3007cccd7a0aa47d990de26'
          '326112c8a9bd24cb45bd4bb2f958a25f0ac4773d'
          'bbecfa7dfa45ac7c37ed8ac59fb53f6a85064b32')

build() {
  cd "$srcdir/sysvinit-${pkgver}dsf"

  # FS#30005
  patch -p1 -d "src" -i "${srcdir}/0001-simplify-writelog.patch"
  patch -p1 -d "src" -i "${srcdir}/0002-remove-ansi-escape-codes-from-log-file.patch"

  # only build what we need...
  make -C src fstab-decode killall5 bootlogd
}

package() {
  cd "$srcdir/sysvinit-${pkgver}dsf"

  install -dm755 "$pkgdir/usr/bin" "$pkgdir/usr/share/man/man8"

  # binaries
  install -m755 src/{fstab-decode,killall5,bootlogd} "$pkgdir/usr/bin"
  ln -s killall5 "$pkgdir/usr/bin/pidof"

  # manpages
  install -m644 man/{bootlogd,pidof,killall5,fstab-decode}.8 "$pkgdir/usr/share/man/man8"
}
