# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>

pkgname=xfce4-notifyd
pkgver=0.8.0
pkgrel=1
pkgdesc="Notification daemon for the Xfce desktop"
arch=('x86_64')
url="https://docs.xfce.org/apps/notifyd/start"
license=('GPL2')
groups=('xfce4-goodies')
depends=('libxfce4ui' 'libnotify' 'hicolor-icon-theme')
makedepends=('intltool' 'python' 'xfce4-panel')
provides=('notification-daemon')
source=(https://archive.xfce.org/src/apps/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=('c7796687ea1dbe5e9725d888dba23a0bffacd544b4c8d27e9dc9ab29b06495c9')

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --disable-debug
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
