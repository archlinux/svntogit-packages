# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>
# Contributor: Jason Hall <cakersq@gmail.com>

pkgname=thin-provisioning-tools
pkgver=0.4.0
pkgrel=1
pkgdesc="A suite of tools for manipulating the metadata of the dm-thin device-mapper target"
arch=('i686' 'x86_64')
url="https://github.com/jthornber/thin-provisioning-tools"
license=('GPL3')
depends=('expat' 'gcc-libs' 'libaio')
makedepends=('boost')
source=(https://github.com/jthornber/${pkgname}/archive/v${pkgver}.tar.gz)
sha1sums=('4bb74cd7043a0ea35b18d3d2ef0efa18afcc9277')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  autoconf
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" BINDIR="${pkgdir}"/usr/bin MANDIR=/usr/share/man install 
}

