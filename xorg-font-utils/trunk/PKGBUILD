# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-font-utils
pkgver=7.6
pkgrel=1
pkgdesc="X.Org font utilities"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('gcc-libs' 'sh' 'libxfont>=1.4.1' 'libfontenc>=1.0.5' 'xproto>=7.0.16')
makedepends=('pkgconfig' 'bigreqsproto>=1.1.0')
replaces=('font-util')
provides=('font-util')
conflicts=('font-util')
install=xorg-font-utils.install
source=(${url}/releases/individual/app/bdftopcf-1.0.3.tar.bz2
        ${url}/releases/individual/app/fonttosfnt-1.0.4.tar.bz2
        ${url}/releases/individual/app/mkfontdir-1.0.6.tar.bz2
        ${url}/releases/individual/app/mkfontscale-1.0.8.tar.bz2
        ${url}/releases/individual/font/font-util-1.2.0.tar.bz2
        http://people.redhat.com/yshao/ttmkfdir2.20021109.tar.bz2
        ttmkfdir.patch
        gcc-4.3.patch)
md5sums=('4a7a4a848c43c42f7d499b60666434a4'
         '07607b26fce3bdc2b204ff3bc43d3ac3'
         'dc342dd8858416254bb5f71a9ddce589'
         '5210c9385c6cc4a00ce829d8dc9c819b'
         '1bdd8ed070e02b2165d7b0f0ed93280b'
         '79e0401393c9728865aa73bb2bd68dd4'
         '099278628ad63631e872bc5590bade74'
         '92ef5f77a467cc188f8dec87b0fdb664')

build() {
  cd "${srcdir}"

  for i in *; do
    if [ -d "${i}" ]; then
      pushd "${i}"
      if [ "${i}" = "ttmkfdir2" ]; then
        patch -Np0 -i "${srcdir}/ttmkfdir.patch"
        patch -Np1 -i "${srcdir}/gcc-4.3.patch"
        make
      else
        ./configure --prefix=/usr --mandir=/usr/share/man \
            --with-mapdir=/usr/share/fonts/util --with-fontrootdir=/usr/share/fonts
        make
      fi
      popd
    fi
  done
}

package() {
  cd "${srcdir}"

  for i in *; do
    if [ -d "${i}" ]; then
      pushd "${i}"
      if [ "${i}" = "ttmkfdir2" ]; then
        install -m755 -d "${pkgdir}/usr/bin"
        install -m755 ttmkfdir "${pkgdir}/usr/bin/"
        install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
        echo "Unknown license" > "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.ttmkfdir2"
	    else
        make DESTDIR="${pkgdir}" install
        install -D -m644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE.${i%-*}
      fi
      popd
    fi
  done
}