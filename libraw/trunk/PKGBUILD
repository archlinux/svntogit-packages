# $Id$
# Maintainer: Andre Scarpino <andrea@archlinux.org>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Joeny Ang <ang(dot)joeny(at)gmail(dot)com>
# Contributor: Brad Fanella <bradfanella@archlinux.us>

pkgname=libraw
pkgver=0.16.0
pkgrel=2
pkgdesc="A library for reading RAW files obtained from digital photo cameras (CRW/CR2, NEF, RAF, DNG, and others)"
arch=('i686' 'x86_64')
url="http://www.libraw.org/"
license=('CDDL' 'LGPL' 'custom')
depends=('lcms2' 'jasper')
makedepends=('cmake')
source=("http://www.libraw.org/data/LibRaw-${pkgver}.tar.gz")
sha256sums=('71f43871ec2535345c5c9b748f07813e49915170f9510b721a2be6478426cf96')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../LibRaw-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_EXAMPLES=OFF \
    -DENABLE_RAWSPEED=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install

  # Fix doc install dir
  install -d "${pkgdir}"/usr/share/doc
  mv "${pkgdir}"/usr/share/libraw "${pkgdir}"/usr/share/doc

  install -d "${pkgdir}"/usr/share/licenses/${pkgname}
  install -D -m644 "${srcdir}"/LibRaw-${pkgver}/LICENSE.LibRaw.pdf \
    "${pkgdir}"/usr/share/licenses/${pkgname}
}
