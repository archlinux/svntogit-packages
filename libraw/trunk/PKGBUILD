# $Id$
# Maintainer: Andre Scarpino <andrea@archlinux.org>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Joeny Ang <ang(dot)joeny(at)gmail(dot)com>
# Contributor: Brad Fanella <bradfanella@archlinux.us>

pkgname=libraw
pkgver=0.17.0
pkgrel=1
pkgdesc="A library for reading RAW files obtained from digital photo cameras (CRW/CR2, NEF, RAF, DNG, and others)"
arch=('i686' 'x86_64')
url="http://www.libraw.org/"
license=('CDDL' 'LGPL' 'custom')
depends=('lcms2' 'jasper')
source=("http://www.libraw.org/data/LibRaw-${pkgver}.tar.gz")
sha256sums=('e643c20945d548aac1eaa1f5573bf74050e0f49ec6a53a6843dc2a2cfb647310')

build() {
  cd LibRaw-${pkgver}
  ./configure --prefix=/usr \
    --disable-examples
  make
}

package() {
  cd LibRaw-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -d "${pkgdir}"/usr/share/licenses/${pkgname}
  install -D -m644 "${srcdir}"/LibRaw-${pkgver}/LICENSE.LibRaw.pdf \
    "${pkgdir}"/usr/share/licenses/${pkgname}
}
