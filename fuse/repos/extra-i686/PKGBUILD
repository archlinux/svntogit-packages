# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: Mark Rosenstand <mark@archlinux.org>

pkgname=fuse
pkgver=2.9.1
pkgrel=1
pkgdesc="A library that makes it possible to implement a filesystem in a userspace program."
arch=('i686' 'x86_64')
url="http://fuse.sourceforge.net/"
license=('GPL2')
depends=('glibc')
makedepends=('pkg-config')
backup=(etc/fuse.conf)
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz
	'fuse.conf')
options=(!libtool)
sha1sums=('ed9e8ab740576a038856109cc587d52a387b223f'
          '3b42e37a741d4651099225987dc40e7f02a716ad')

build() {
  cd "$pkgname-$pkgver"

  # fix building with glibc-2.14
  sed -i '1i#define _GNU_SOURCE' util/fusermount.c

  ./configure --prefix=/usr --libdir=/usr/lib --enable-lib \
              --enable-util --bindir=/bin
  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR=${pkgdir} install

  # Remove init script in wrong path
  # Don't add our own for now, as fusectl fs oopses on 2.6.18
  rm -rf ${pkgdir}/etc/init.d
  #install -D -m755 ${srcdir}/fuse.rc.d ${pkgdir}/etc/rc.d/fuse

  # install sample config file
  install -Dm644 ${srcdir}/fuse.conf ${pkgdir}/etc/fuse.conf

  # remove udev rules (is in the udev package}
  rm -rf ${pkgdir}/etc/udev

  # static device nodes are handled by udev
  rm -rf ${pkgdir}/dev
}
