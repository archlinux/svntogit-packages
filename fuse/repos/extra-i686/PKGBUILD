# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Mark Rosenstand <mark@archlinux.org>

pkgname=fuse
pkgver=2.8.4
pkgrel=2
pkgdesc="A library that makes it possible to implement a filesystem in a userspace program."
arch=('i686' 'x86_64')
url="http://fuse.sourceforge.net/"
license="GPL2"
depends=('glibc')
makedepends=('pkg-config')
backup=(etc/fuse.conf)
install=fuse.install
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz
	'fuse.conf')
options=(!libtool)
sha1sums=('b1bc296c3e5b90b3658edb5d8d7aa0ee97c145d7'
          '3b42e37a741d4651099225987dc40e7f02a716ad')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr --enable-lib \
              --enable-util --bindir=/bin
  make || return 1
  make DESTDIR=${pkgdir} install || return 1

  # static device nodes will cause collision.
  rm -rf ${pkgdir}/dev
 
  # Remove init script in wrong path
  # Don't add our own for now, as fusectl fs oopses on 2.6.18
  rm -rf ${pkgdir}/etc/init.d
  #install -D -m755 ${srcdir}/fuse.rc.d ${pkgdir}/etc/rc.d/fuse

  install -d ${pkgdir}/lib
  mv ${pkgdir}/usr/lib/libfuse.so.${pkgver} ${pkgdir}/lib/ || return 1
  ln -s ../../lib/libfuse.so.${pkgver} ${pkgdir}/usr/lib/libfuse.so.${pkgver} || return 1
  ln -s libfuse.so.${pkgver} ${pkgdir}/lib/libfuse.so.2 || return 1
  mv ${pkgdir}/usr/lib/libulockmgr.so.1.0.1 ${pkgdir}/lib/ || return 1
  ln -s ../../lib/libulockmgr.so.1.0.1 ${pkgdir}/usr/lib/libulockmgr.so.1.0.1 || return 1
  ln -s libulockmgr.so.1.0.1 ${pkgdir}/lib/libulockmgr.so.1 || return 1

  # install sample config file
  install -Dm644 ${srcdir}/fuse.conf ${pkgdir}/etc/fuse.conf || return 1
  # Fix udev rule location
  install -d -m755 "${pkgdir}/lib"
  mv "${pkgdir}/etc/udev" "${pkgdir}/lib/"
}
