# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=plymouth-kcm
pkgver=5.27.5
_dirver=$(echo $pkgver | cut -d. -f1-3)
pkgrel=1
pkgdesc='KCM to manage the Plymouth (Boot) theme'
arch=(x86_64)
url='https://kde.org/plasma-desktop/'
license=(LGPL)
depends=(systemsettings plymouth)
makedepends=(extra-cmake-modules)
groups=(plasma)
source=(https://download.kde.org/stable/plasma/$_dirver/$pkgname-$pkgver.tar.xz{,.sig}
        https://invent.kde.org/plasma/plymouth-kcm/-/commit/49b4435b.patch)
sha256sums=('596249b9df646d388d93a80459ecdfd77d1fa3dc070aa0c7ddf2fef9719d0064'
            'SKIP'
            '67c9f7233720f72d3d4141255760c020c6d054271d632b40823921020a4cc2d6')
validpgpkeys=('E0A3EB202F8E57528E13E72FD7574483BB57B18D'  # Jonathan Esk-Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  patch -d $pkgname-$pkgver -p1 < 49b4435b.patch # Run mkinitcpio instead of initramfs
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
