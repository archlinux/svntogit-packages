# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=spglib
pkgver=1.10.0
pkgrel=1
pkgdesc="C library for finding and handling crystal symmetries"
arch=(i686 x86_64)
url="http://spglib.sf.net/"
license=(custom)
depends=(glibc)
makedepends=()
source=($pkgname-$pkgver.tar.gz::"https://github.com/atztogo/spglib/archive/v$pkgver.tar.gz")
sha256sums=('117fff308731784bea2ddaf3d076f0ecbf3981b31ea1c1bfd5ce4f057a5325b1')

build() {
  cd $pkgname-$pkgver
  touch INSTALL NEWS README AUTHORS
  autoreconf -vi
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir"/$pkgname-$pkgver/COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
