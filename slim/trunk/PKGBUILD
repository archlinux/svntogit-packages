# $Id$
# Maintainer: Thayer Williams <thayer@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: Hugo Ideler <hugoideler@dse.nl>

pkgname=slim
pkgver=1.3.1
pkgrel=7
pkgdesc="Desktop-independent graphical login manager for X11"
arch=('i686' 'x86_64')
url="http://slim.berlios.de/"
license=('GPL2')
depends=('pam' 'libxmu' 'libpng' 'libjpeg>=8' 'libxft')
backup=('etc/slim.conf' 'etc/logrotate.d/slim' 'etc/pam.d/slim')
install=slim.install
source=(http://download.berlios.de/${pkgname}/${pkgname}-$pkgver.tar.gz
        'slim'
        'slim.logrotate'
        'slim.pam'
        'slim-restart.patch'
        'slim-tty-slowness.patch'
        'slim-1.3.1-gcc44.patch')
md5sums=('727d0acb24c0fbf0751134c37a9c895f'
         'd8ea9c4dee2811524b67f4f666311a1f'
         '43da096480bf72c3ccec8ad8400f34f0'
         'b20fe3c8487a039050986d60e45233a9'
         '648d871fe7d2a97b7d7b928019e2feee'
         'be48f19a2e2f154bed750171e8d0fc7f'
         '197e6d05af30e32f96e6fb36110ae752')

build() {
  cd "$srcdir/${pkgname}-$pkgver"
  
  # fix sluggish tty after slim start
  patch -Np1 -i $srcdir/slim-tty-slowness.patch || return 1

  patch -Np1 -i $srcdir/slim-1.3.1-gcc44.patch || return 1

  # Restart Xserver if killed
  # http://developer.berlios.de/patch/?func=detailpatch&patch_id=2378&group_id=2663
  patch -Np1 -i "$srcdir/slim-restart.patch" || return 1

  #libpng 1.4.0
  sed -i -e 's/png12/png14/g' Makefile || return 1

  make USE_PAM=1 || return 1
  make DESTDIR="$pkgdir" MANDIR=/usr/share/man install || return 1

  install -D -m755 "$srcdir/slim" "$pkgdir/etc/rc.d/slim" || return 1
  install -D -m644 "$srcdir/slim.logrotate" "$pkgdir/etc/logrotate.d/slim" || return 1
  install -D -m644 "$srcdir/slim.pam" "$pkgdir/etc/pam.d/slim" || return 1

  # Provide sane defaults
  sed -i 's|#xserver_arguments.*|xserver_arguments   -nolisten tcp vt07|' \
    "$pkgdir/etc/slim.conf" || return 1
  sed -i 's|/var/run/slim.lock|/var/lock/slim.lock|' "$pkgdir/etc/slim.conf" || return 1
}
