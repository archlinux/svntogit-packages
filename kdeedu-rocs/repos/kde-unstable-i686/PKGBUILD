# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-rocs
pkgver=4.11.90
pkgrel=1
pkgdesc="Rocs Graph Theory"
url="http://kde.org/applications/education/rocs/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4' 'boost')
install=${pkgname}.install
source=("http://download.kde.org/unstable/${pkgver}/src/rocs-${pkgver}.tar.xz")
sha1sums=('fc1c6c4cd2f8f6b5daf6e8d7f301ffbb792c05e6')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../rocs-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
