# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-rocs
pkgver=14.12.1
pkgrel=1
pkgdesc="Rocs Graph Theory"
url="http://kde.org/applications/education/rocs/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
depends=('kdebase-runtime' 'grantlee-qt4')
makedepends=('cmake' 'automoc4' 'boost')
install=${pkgname}.install
source=("http://download.kde.org/stable/applications/${pkgver}/src/rocs-${pkgver}.tar.xz")
sha1sums=('b0673223825c1ea390f4e556888488c34679ca74')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../rocs-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
