# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=libkdepim
pkgver=22.08.2
pkgrel=1
pkgdesc='Libraries for KDE PIM applications'
arch=(x86_64)
url='https://kontact.kde.org'
license=(LGPL)
depends=(kcompletion kconfigwidgets kitemviews kjobwidgets)
makedepends=(extra-cmake-modules qt5-tools doxygen)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('bcb581aa6dc4eda3fcf4c6f779e3c2d7b9da74011e46fc5ec61c2e7a68024cee'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
options=(debug)

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
