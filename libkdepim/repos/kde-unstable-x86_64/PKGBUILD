# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=libkdepim
pkgver=20.11.90
pkgrel=1
pkgdesc="Libraries for KDE PIM applications"
arch=(x86_64)
url="https://kontact.kde.org"
license=(LGPL)
depends=(kcompletion kconfigwidgets kitemviews kjobwidgets)
makedepends=(extra-cmake-modules boost qt5-tools)
source=("https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('b75706797c195d18fd2051baa8814b1993e044fe304f49162a3152630c25f326'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
