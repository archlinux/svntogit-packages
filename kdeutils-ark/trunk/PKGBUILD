# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeutils-ark
pkgver=15.04.2
pkgrel=1
pkgdesc='Archiving Tool'
url='http://kde.org/applications/utilities/ark/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdeutils')
depends=('kdebase-runtime' 'kdebase-lib' 'libarchive' 'qjson')
makedepends=('cmake' 'automoc4')
optdepends=('p7zip' 'zip' 'unzip' 'unrar')
install=${pkgname}.install
source=("http://download.kde.org/stable/applications/${pkgver}/src/ark-${pkgver}.tar.xz")
sha1sums=('24d24d72225d99debca98a92edee03bef78fde85')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../ark-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $srcdir/build
  make DESTDIR=$pkgdir install
}
