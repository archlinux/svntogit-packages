# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ignacio Galmarino <igalmarino@gmail.com>

pkgname=openresolv
pkgver=3.9.1
pkgrel=1
pkgdesc="resolv.conf management framework (resolvconf)"
arch=(any)
url="https://roy.marples.name/projects/openresolv"
license=('custom:BSD')
backup=(etc/resolvconf.conf)
provides=(resolvconf)
depends=(bash)
makedepends=(systemd)
source=("https://roy.marples.name/downloads/openresolv/${pkgname}-${pkgver}.tar.xz"
        fs33801.patch)
sha256sums=('38b8e7e131a39b1c0d4e5688618b8572adf92a5bf757ae9f272e9f81108a9ff2'
            '466678996689463af748bcdce7ea6f9f24f59004fa4db61fdc04da9f5fff2a6a')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../fs33801.patch
  sed -n '2,25{s:^# \?::;p}' resolvconf.in >LICENSE 
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib/resolvconf --sbindir=/usr/bin
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
