# $Id$
# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>

pkgname=pylint
pkgver=0.25.2
pkgrel=1
pkgdesc="Analyzes Python code looking for bugs and signs of poor quality"
arch=('any')
url="http://www.logilab.org/project/pylint"
license=('GPL')
depends=('python-logilab-astng' 'python2')
makedepends=('python2-distribute')
optdepends=('tk: Pylint GUI')
source=("http://download.logilab.org/pub/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha1sums=('52b1a671bacc115a953b1e0d423ecbaf9064ce2e')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i "s|/usr/bin/env python|/usr/bin/env python2|" epylint.py

  python2 setup.py build
}


package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  python2 setup.py install --prefix=/usr --root="${pkgdir}" --skip-build --optimize=1

  install -d "${pkgdir}/usr/share/man/man1"
  install -m644 man/* "${pkgdir}/usr/share/man/man1"
}
