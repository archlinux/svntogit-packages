# $Id$
# Maintainer: Angel Velasquez <angvp@archlinux.org>  
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>

pkgbase=pylint
pkgname=('python2-pylint' 'python-pylint')
pkgver=1.1.0
pkgrel=2
pkgdesc="Analyzes Python code looking for bugs and signs of poor quality"
arch=('any')
url="http://pylint.org"
license=('GPL')
makedepends=('python2-astroid' 'python-astroid' 'python2-setuptools' 'python-setuptools') 
optdepends=('tk: Pylint GUI')
source=(http://pypi.python.org/packages/source/p/${pkgbase}/${pkgbase}-${pkgver}.tar.gz)
sha1sums=('b33594a2c627d72007bfa8c6d7619af699e26085')

build() {
  cd "${srcdir}"
  cp -a ${pkgbase}-${pkgver}{,-py3}

  cd ${pkgbase}-${pkgver}

  sed -i "s|/usr/bin/env python|/usr/bin/env python2|" epylint.py
  python2 setup.py build

  cd ../${pkgbase}-${pkgver}-py3

  sed -i "s|/usr/bin/env python|/usr/bin/env python3|" epylint.py
  python3 setup.py build
}

package_python2-pylint() {
  depends=('python2-astroid' 'python2-setuptools')

  cd "${srcdir}"/${pkgbase}-${pkgver}

  python2 setup.py install --prefix=/usr --root="${pkgdir}" --skip-build --optimize=1

  install -d "${pkgdir}"/usr/share/man/man1

  mv man/pylint.1.orig man/pylint2.1.orig
  install -m644 man/* "${pkgdir}/usr/share/man/man1"

  for i in epylint pylint pylint-gui pyreverse symilar; do
     mv "${pkgdir}"/usr/bin/${i}{,2}
     mv "${pkgdir}"/usr/share/man/man1/${i}{,2}.1
  done

}

package_python-pylint() {
  depends=('python-astroid' 'python-setuptools')

  cd "${srcdir}"/${pkgbase}-${pkgver}-py3

  python3 setup.py install --prefix=/usr --root="${pkgdir}" --skip-build --optimize=1

  install -d "${pkgdir}/usr/share/man/man1"
  install -m644 man/* "${pkgdir}/usr/share/man/man1"
}
