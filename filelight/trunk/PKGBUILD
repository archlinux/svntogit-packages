# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=filelight
pkgver=23.04.1
pkgrel=1
pkgdesc='View disk usage information'
url='https://apps.kde.org/filelight/'
arch=(x86_64)
license=(GPL LGPL FDL)
depends=(kio qqc2-desktop-style kquickcharts kdeclarative hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools)
groups=(kde-applications kde-utilities)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('78c009da713d6fe9f5866f26c0c162d751d5b182a0dee4b1840af987e15ae348'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
