# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=filelight
pkgver=17.07.80
pkgrel=1
pkgdesc='View disk usage information'
url='https://kde.org/applications/utilities/filelight'
arch=(i686 x86_64)
license=(GPL LGPL FDL)
depends=(kio hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools python kparts)
groups=(kde-applications kdeutils)
replaces=(kdeutils-filelight)
conflicts=(kdeutils-filelight)
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('7ecffc3a59bb087a72cacf47d1eed0bea7a7d60cdf3ebd7495ec080c455f10e4'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
