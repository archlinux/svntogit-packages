# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdesdk-kcachegrind
pkgver=15.04.3
pkgrel=1
pkgdesc='Visualization of Performance Profiling Data'
url='http://kde.org/applications/development/kcachegrind/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdesdk')
depends=('kdebase-runtime' 'python2')
makedepends=('cmake' 'automoc4')
optdepends=('php: PHP support')
install=${pkgname}.install
source=("http://download.kde.org/stable/applications/${pkgver}/src/kcachegrind-${pkgver}.tar.xz")
sha1sums=('393c72269ab97f5f9cf81f9ccf8589cfcecff2c5')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../kcachegrind-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install

  # Fix python 2 path
  sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|' \
    "${pkgdir}"/usr/bin/hotshot2calltree
}
