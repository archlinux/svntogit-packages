# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdesdk-kcachegrind
pkgver=16.04.2
pkgrel=1
pkgdesc='Visualization of Performance Profiling Data'
url='http://kde.org/applications/development/kcachegrind/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdesdk')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
optdepends=('php: PHP support')
source=("http://download.kde.org/stable/applications/${pkgver}/src/kcachegrind-${pkgver}.tar.xz")
sha1sums=('cd44211ef4a18bce4fbf0ef0b097fef9a7c9669b')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kcachegrind-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install

  # Fix python 2 path
  sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|' \
    "${pkgdir}"/usr/bin/hotshot2calltree
}
