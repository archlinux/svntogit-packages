# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdesdk-kcachegrind
pkgver=4.12.3
pkgrel=1
pkgdesc='Visualization of Performance Profiling Data'
url='http://kde.org/applications/development/kcachegrind/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdesdk')
depends=('kdebase-runtime' 'python2')
makedepends=('cmake' 'automoc4')
optdepends=('php: PHP support')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/kcachegrind-${pkgver}.tar.xz"
        'kdebug-331715.patch')
sha1sums=('0e6506bb806508ff9c8100f7696c7464fa865d1b'
          '3e7f4a265079c509aa08aab92a7ceaf6208353b3')

prepare() {
  mkdir build

  # KDEBUG#331715
  cd kcachegrind-${pkgver}
  patch -p1 -i "${srcdir}"/kdebug-331715.patch
}

build() {
  cd build
  cmake ../kcachegrind-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install

  # Fix python 2 path
  sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|' \
    "${pkgdir}"/usr/bin/hotshot2calltree
}
