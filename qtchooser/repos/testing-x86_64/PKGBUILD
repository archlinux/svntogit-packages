# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=qtchooser
pkgver=41
pkgrel=1
pkgdesc='Wrap the other Qt tools by searching for different instances of Qt on the system'
arch=('i686' 'x86_64')
url='http://qt.gitorious.org/qt/qtchooser'
license=('GPL')
backup=('etc/xdg/qtchooser/default.conf')
depends=('gcc-libs')
makedepends=('git')
source=("git://gitorious.org/qt/${pkgname}.git#commit=7985f775fb9cf9b77bbbb394a832f0c5f26de47c"
        {4,5}.conf)
md5sums=('SKIP'
         'aa4d49b269e1b806f5eea170801f0aa6'
         'cf83f09a9a5a44d898b93f624d74dd11')

build() {
  cd ${pkgname}
  make
}

package() {
  cd ${pkgname}
  make INSTALL_ROOT="${pkgdir}" install

  install -d "${pkgdir}"/etc/xdg/qtchooser
  install -m644 "${srcdir}"/{4,5}.conf \
    "${pkgdir}"/etc/xdg/qtchooser/

  # Set the default Qt
  ln -s /etc/xdg/qtchooser/5.conf \
    "${pkgdir}"/etc/xdg/qtchooser/default.conf
}
