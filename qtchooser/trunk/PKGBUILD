# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=qtchooser
pkgver=39
pkgrel=1
pkgdesc='Wrap the other Qt tools by searching for different instances of Qt on the system'
arch=('i686' 'x86_64')
url='http://qt.gitorious.org/qt/qtchooser'
license=('GPL')
backup=('etc/xdg/qtchooser/default.conf')
depends=('gcc-libs')
source=("http://macieira.org/${pkgname}/${pkgname}-${pkgver}-g4717841.tar.gz"
        {3,4,5}.conf)
md5sums=('fcf1b5e8373147e48ce72b9c1ffe3d10'
         '7c5da2b7911d1c4fabc63d6ef00f0020'
         'aa4d49b269e1b806f5eea170801f0aa6'
         'cf83f09a9a5a44d898b93f624d74dd11')

build() {
  cd ${pkgname}-${pkgver}-g4717841
  make
}

package() {
  cd ${pkgname}-${pkgver}-g4717841
  make INSTALL_ROOT="${pkgdir}" install

  install -d "${pkgdir}"/etc/xdg/qtchooser
  install -m644 "${srcdir}"/{3,4,5}.conf \
    "${pkgdir}"/etc/xdg/qtchooser/

  # Set the default Qt
  ln -s /etc/xdg/qtchooser/5.conf \
    "${pkgdir}"/etc/xdg/qtchooser/default.conf
}
