# $Id$
# Maintainer : Aaron Griffin <aaron@archlinux.org>

pkgname=oprofile
pkgver=0.9.8
pkgrel=1
pkgdesc="A system-wide profiler for Linux systems, capable of profiling all running code at low overhead"
arch=('i686' 'x86_64')
url="http://oprofile.sourceforge.net"
license=('GPL')
depends=('popt' 'gcc-libs' 'zlib' 'sh')
makedepends=('qt')
optdepends=('qt: for oprof_start gui')
options=('!libtool')
install=oprofile.install
source=(http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz)
sha1sums=('28416b853641f913e5f90954f2e3837efd96f6cd')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --with-kernel-support --with-x --enable-gui=qt4
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -d "$pkgdir/var/lib/oprofile"

# installing development libraries and headers - FS#20089
  install -d  "$pkgdir/usr/include/oprofile"
  install -m644 config.h libop/op_*.h libregex/op_*.h libdb/odb.h \
    libpp/{locate_images.h,image_errors.h} libutil/op_*.h \
    libutil++/{op_*.h,bfd_support.h,utility.h,cached_value.h} "$pkgdir/usr/include/oprofile"
  install -m644 libop/libop.a libdb/libodb.a "$pkgdir/usr/lib/"
  install -m644 libabi/libabi.a "$pkgdir/usr/lib/libopabi.a"
  install -m644 libutil/libutil.a "$pkgdir/usr/lib/liboputil.a"
  install -m644 libutil++/libutil++.a "$pkgdir/usr/lib/liboputil++.a"
}
