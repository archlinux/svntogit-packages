# $Id$
# Maintainer : Aaron Griffin <aaron@archlinux.org>

pkgname=oprofile
pkgver=0.9.7
pkgrel=3
pkgdesc="A system-wide profiler for Linux systems, capable of profiling all running code at low overhead"
arch=('i686' 'x86_64')
url="http://oprofile.sourceforge.net"
license=('GPL')
depends=('popt' 'gcc-libs' 'zlib' 'sh')
makedepends=('qt')
optdepends=('qt: for oprof_start gui')
options=('!libtool')
install=oprofile.install
source=(http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz)
md5sums=('8b5d1d9b65f84420bcc3234777ad3be3')
sha1sums=('f5befd8aafc77d449e955f0585216f5d7b967de2')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --with-kernel-support --with-x --enable-gui=qt4
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -d "$pkgdir/var/lib/oprofile"

# installing development libraries - FS#20089
  install -m644 libop/op_*.h libregex/op_*.h libutil/op_*.h libutil++/op_*.h libdb/odb.h "$pkgdir/usr/include"
  install -m644 libop/libop.a libdb/libodb.a "$pkgdir/usr/lib/"
  install -m644 libabi/libabi.a "$pkgdir/usr/lib/libopabi.a"
  install -m644 libutil/libutil.a "$pkgdir/usr/lib/liboputil.a"
  install -m644 libutil++/libutil++.a "$pkgdir/usr/lib/liboputil++.a"
}
