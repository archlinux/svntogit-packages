# $Id$
# Maintainer: Dale Blount <dale@archlinux.org>
# Contributor: Manolis Tzanidakis

pkgname=spamassassin
pkgver=3.3.1
pkgrel=5
pkgdesc="A mail filter to identify spam."
arch=(i686 x86_64)
license=(APACHE)
url="http://spamassassin.apache.org"
depends=('perl>=5.14' 'perl-net-dns' 'perl-uri' 'perl-html-parser' \
		'perl-digest-sha1' 'perl-io-socket-ssl' 'openssl' \
		'perl-libwww' 'perl-mail-spf' \
	    'zlib' 're2c')
        # perl-archive-tar perl-io-zlib)
makedepends=('razor' 'perl-dbi') 
optdepends=('razor: to identify collaborately-flagged spam'
	    'gnupg: gpg support, without this use --nogpg flag for sa-update'
            're2c: regexp compiler, for compiling rules with sa-compile')
backup=(etc/conf.d/spamd etc/mail/spamassassin/local.cf \
	etc/mail/spamassassin/init.pre \
	etc/mail/spamassassin/v310.pre \
	etc/mail/spamassassin/v312.pre \
	etc/mail/spamassassin/v320.pre)
install=${pkgname}.install
options=(!emptydirs)
source=(http://mirrors.devlib.org/apache/${pkgname}/source/Mail-SpamAssassin-${pkgver}.tar.gz \
		spamd.conf.d spamd)
md5sums=('5a93f81fda315411560ff5da099382d2'
         'af501c6b0bb937a2775b747cc4b6d150'
         '0534d3071a526a27ddc2b297afa03b04')

build() {
  cd ${srcdir}/Mail-SpamAssassin-${pkgver}

  # install module in vendor directories.
  PERL_MM_USE_DEFAULT=1 /usr/bin/perl Makefile.PL INSTALLDIRS=vendor \
      CONTACT_ADDRESS=root@localhost ENABLE_SSL=yes PERL_TAINT=no
  /usr/bin/make 
}

check() {
  cd ${srcdir}/Mail-SpamAssassin-${pkgver}
  /usr/bin/make test || true
}

package() {
  cd ${srcdir}/Mail-SpamAssassin-${pkgver}
  /usr/bin/make DESTDIR=${pkgdir} install

  /bin/install -D -m644 ${srcdir}/spamd.conf.d ${pkgdir}/etc/conf.d/spamd
  /bin/install -D -m755 ${srcdir}/spamd ${pkgdir}/etc/rc.d/spamd
}
