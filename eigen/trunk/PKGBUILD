# Maintainer: Ronald van Haren <ronald@archlinux.org>
# Contributor: Chirantan Ekbote <chirantan.ekbote at gmail.com>
# Contributor: Kringel

pkgname=eigen
pkgver=3.3.7
pkgrel=2
pkgdesc="Lightweight C++ template library for vector and matrix math, a.k.a. linear algebra."
arch=('any')
url='https://eigen.tuxfamily.org'
license=('GPL3' 'LGPL2.1' 'BSD' 'custom:MPL2' 'custom:MINPACK')
makedepends=('cmake' 'pkg-config' 'freeglut' 'gcc-fortran' 'fftw' 'suitesparse' 'boost')
replaces=('eigen3')
provides=('eigen3')
source=("${pkgname}-${pkgver}.tar.bz2::https://bitbucket.org/eigen/eigen/get/${pkgver}.tar.bz2")
sha1sums=('a06faa6f358d5d1ca0da7cddb95da39e436dc9e8')

prepare() {
  mkdir -p build
}

build() {
	cd build
	cmake ../eigen-eigen-* \
		-DCMAKE_INSTALL_PREFIX=/usr \
                -DCMAKE_INSTALL_LIBDIR=lib
}

package() {
	cd build
	make DESTDIR="$pkgdir" install

	# install custom licenses
	install -Dm644 ../eigen-eigen-*/COPYING.MPL2 -t "$pkgdir/usr/share/licenses/$pkgname"
        install -Dm644 ../eigen-eigen-*/COPYING.BSD -t "$pkgdir/usr/share/licenses/$pkgname"
        install -Dm644 ../eigen-eigen-*/COPYING.MINPACK -t "$pkgdir/usr/share/licenses/$pkgname"
}
