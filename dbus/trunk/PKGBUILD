# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Link Dupont <link@subpop.net>
#
pkgname=dbus
pkgver=1.4.20
pkgrel=1
pkgdesc="Freedesktop.org message bus system"
url="http://www.freedesktop.org/Software/dbus"
arch=(i686 x86_64)
license=('GPL' 'custom')
depends=("dbus-core>=${pkgver}" 'libx11')
source=(http://dbus.freedesktop.org/releases/${pkgname}/${pkgname}-${pkgver}.tar.gz
        30-dbus)
md5sums=('79eca2f2c1894ac347acce128314428b'
         '9fafe8b28460aeaa6054309ef4c5ed92')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ECHO="echo" ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/usr/lib/dbus-1.0 --with-dbus-user=81 \
      --with-system-pid-file=/var/run/dbus.pid \
      --disable-verbose-mode --disable-static \
      --enable-inotify --disable-dnotify \
      --disable-tests --disable-asserts
  make -C tools dbus-launch
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  install -m755 -d "${pkgdir}/usr/bin"
  install -m755 -d "${pkgdir}/usr/share/man/man1"
  install -m755 tools/dbus-launch "${pkgdir}/usr/bin/"
  install -m644 doc/dbus-launch.1 "${pkgdir}/usr/share/man/man1/"

  install -m755 -d "${pkgdir}/etc/X11/xinit/xinitrc.d"
  install -m755 "${srcdir}/30-dbus" "${pkgdir}/etc/X11/xinit/xinitrc.d/"

  install -d -m755 "${pkgdir}/usr/share/licenses/dbus"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/dbus/"
}
