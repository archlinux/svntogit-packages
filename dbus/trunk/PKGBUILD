# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Link Dupont <link@subpop.net>
#
pkgname=dbus
pkgver=1.4.0
pkgrel=1
pkgdesc="Freedesktop.org message bus system"
url="http://www.freedesktop.org/Software/dbus"
arch=(i686 x86_64)
license=('GPL' 'custom')
depends=("dbus-core>=${pkgver}" 'libx11')
makedepends=('libsm' 'libice')
source=(http://dbus.freedesktop.org/releases/${pkgname}/${pkgname}-${pkgver}.tar.gz
        30-dbus)
md5sums=('f59618b18d2fb2bd1fce9e1c5a2a3282'
         'd14e59575f04e55d21a04907b6fd9f3c')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/usr/lib/dbus-1.0 --with-dbus-user=81 \
      --with-system-pid-file=/var/run/dbus.pid \
      --disable-verbose-mode --disable-static \
      --enable-inotify --disable-dnotify \
      --disable-tests --disable-asserts
  cd tools
  make dbus-launch
  install -m755 -d "${pkgdir}/usr/bin"
  install -m755 -d "${pkgdir}/usr/share/man/man1"
  install -m755 dbus-launch "${pkgdir}/usr/bin/" || return 1
  install -m644 dbus-launch.1 "${pkgdir}/usr/share/man/man1/"

  install -m755 -d "${pkgdir}/etc/X11/xinit/xinitrc.d"
  install -m755 "${srcdir}/30-dbus" "${pkgdir}/etc/X11/xinit/xinitrc.d/"

  install -d -m755 "${pkgdir}/usr/share/licenses/dbus"
  install -m644 ../COPYING "${pkgdir}/usr/share/licenses/dbus/"
}
