# $Id$
# Maintainer:

pkgbase=pysmbc
pkgname=('python2-pysmbc' 'python-pysmbc')
pkgver=1.0.15.3
pkgrel=1
arch=('i686' 'x86_64')
url="http://pypi.python.org/pypi/pysmbc/"
license=('GPL')
makedepends=('python2' 'python' 'smbclient')
source=(http://pypi.python.org/packages/source/p/${pkgbase}/${pkgbase}-${pkgver}.tar.bz2{,.asc})
md5sums=('67d4a23ffbd5f5fcf4b4b68f57d4f195'
         'SKIP')

prepare() {
  cp -r ${pkgbase}-${pkgver} ${pkgbase}-${pkgver}-python2
  sed -i 's|/usr/bin/python|/usr/bin/python2|' ${pkgbase}-${pkgver}-python2/smbc/xattr.py
}

build(){
  cd ${pkgbase}-${pkgver}
  python setup.py build
  cd "${srcdir}"/${pkgbase}-${pkgver}-python2
  python2 setup.py build
}

package_python-pysmbc() {
  pkgdesc="Python 3 bindings for libsmbclient"
  depends=('python' 'smbclient')

  cd ${pkgbase}-${pkgver}
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-pysmbc() {
  pkgdesc="Python 2 bindings for libsmbclient"
  depends=('python2' 'smbclient')

  cd ${pkgbase}-${pkgver}-python2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
