# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Shane Peelar <lookatyouhacker@gm@il(d0t)c0m>

pkgname=pysmbc
pkgver=1.0.13
pkgrel=2
pkgdesc="Python bindings for libsmbclient"
arch=('i686' 'x86_64')
url="http://pypi.python.org/pypi/pysmbc/"
license=('GPL')
depends=('python2' 'smbclient')
source=("http://pypi.python.org/packages/source/p/${pkgname}/${pkgname}-${pkgver}.tar.bz2"
        'pkg-config.patch')
md5sums=('019dbb3bc6ee217f7389a2330cda9fe0'
         '4b1d5498780181cdc9a217894c1cf85c')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i "${srcdir}"/pkg-config.patch
}

build(){
  cd ${pkgname}-${pkgver}
  python2 setup.py build
}

package() {
  cd ${pkgname}-${pkgver}
  python2 setup.py install --root="$pkgdir" --optimize=1
}
