# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: damir <damir@archlinux.org>
# Contributor: Kevin Edmonds <edmondskevin@hotmail.com>

pkgname=libmtp
pkgver=1.1.16
pkgrel=1
pkgdesc="Library implementation of the Media Transfer Protocol"
arch=(x86_64)
url="http://libmtp.sourceforge.net"
license=(LGPL)
depends=(libusb)
source=("https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz"{,.asc})
sha256sums=('5b68f9dd1ed38de558de687f40e255a39e4d5e32ef3de601f0ac19dd5682bba0'
            'SKIP')
validpgpkeys=(7C4AFD61D8AAE7570796A5172209D6902F969C95) # Marcus Mei√üner <meissner@suse.de>

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --with-udev=/usr/lib/udev
  make
}

check() {
  cd $pkgname-$pkgver
  make -k check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
