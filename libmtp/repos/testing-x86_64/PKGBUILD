# $Id$
# Maintainer: Tom Gundersen <teg@jklm.no>
# Contributor: damir <damir@archlinux.org>
# Contributor: Kevin Edmonds <edmondskevin@hotmail.com>

pkgname=libmtp
pkgver=1.1.6
pkgrel=6
pkgdesc="Library implementation of the Media Transfer Protocol"
arch=("i686" "x86_64")
url="http://libmtp.sourceforge.net"
license=('LGPL')
depends=('libusb' 'libgcrypt')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
md5sums=('87835626dbcf39e62bfcdd4ae6da2063')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --with-udev=/usr/lib/udev
  make
}

check() {
  cd $pkgname-$pkgver
  make -k check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  
  # fix broken udev rule
  sed -i "/^Unable to open/d" ${pkgdir}/usr/lib/udev/rules.d/69-libmtp.rules
}
