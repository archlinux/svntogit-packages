# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=folks
pkgver=0.9.7
pkgrel=1
pkgdesc="Library to aggregates people into metacontacts"
arch=(i686 x86_64)
url="http://telepathy.freedesktop.org/wiki/Folks"
license=(LGPL2.1)
depends=(telepathy-glib libgee libxml2 evolution-data-server zeitgeist)
makedepends=(gobject-introspection intltool vala git)
install=folks.install
options=('!emptydirs')
source=(git://git.gnome.org/folks#commit=0085cb463849e5b30d4b4c58da63a5df3fd0d0be)
md5sums=('SKIP')

build() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
  ./configure --prefix=/usr --disable-schemas-compile \
    --disable-libsocialweb-backend --disable-fatal-warnings
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
