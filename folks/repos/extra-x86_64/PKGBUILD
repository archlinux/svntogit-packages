# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=folks
pkgver=0.8.0
pkgrel=1
pkgdesc="Library to aggregates people into metacontacts"
arch=(i686 x86_64)
url="http://telepathy.freedesktop.org/wiki/Folks"
license=('LGPL2.1')
depends=('telepathy-glib' 'libgee' 'libxml2' 'evolution-data-server' 'libsocialweb' 'libzeitgeist')
makedepends=('gobject-introspection' 'intltool')
install=folks.install
options=('!libtool' '!emptydirs')
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('8c56c7594dd502ef5877e93181136e41f4ea03de4971372a7a04e7f1492eed9d')

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr --disable-schemas-compile
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
