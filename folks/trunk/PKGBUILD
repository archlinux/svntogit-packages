# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=folks
pkgver=0.11.3
pkgrel=2
pkgdesc="Library to aggregates people into metacontacts"
arch=(i686 x86_64)
url="https://wiki.gnome.org/Projects/Folks"
license=(LGPL2.1)
depends=(telepathy-glib libgee libxml2 evolution-data-server)
makedepends=(gobject-introspection intltool vala git)
_commit=6d1d4ed7e96564ee13ab0a7db71e9da0e766d51a
source=("git://git.gnome.org/folks#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname

  ./configure --prefix=/usr --disable-schemas-compile \
    --disable-libsocialweb-backend --disable-fatal-warnings

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
