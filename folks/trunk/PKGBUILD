# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=folks
pkgver=0.15.3+6+gdb1de866
pkgrel=1
pkgdesc="Library to aggregates people into metacontacts"
arch=(x86_64)
url="https://wiki.gnome.org/Projects/Folks"
license=(LGPL2.1)
depends=(libgee libxml2 evolution-data-server)
makedepends=(gobject-introspection gtk-doc vala git meson python-dbusmock)
provides=(libfolks.so libfolks-{dummy,eds}.so)
_commit=db1de866217d98f6b25edc92ca54d82c4dc082a8  # master
source=("git+https://gitlab.gnome.org/GNOME/folks.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  local meson_args=(
    -D b_lto=false  # LTO copies some GType constructors
    -D docs=true
    -D telepathy_backend=false
  )
  arch-meson $pkgname build "${meson_args[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs -t 4
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 et:
