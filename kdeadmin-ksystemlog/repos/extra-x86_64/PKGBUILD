# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeadmin-ksystemlog
pkgver=15.08.3
pkgrel=1
pkgdesc='System log viewer tool'
url='http://kde.org/applications/system/ksystemlog/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdeadmin')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
source=("http://download.kde.org/stable/applications/${pkgver}/src/ksystemlog-${pkgver}.tar.xz"
        'syslog-path.patch')
sha1sums=('44cdc221b8e7f0927cf870c84ce01c5e924d6e1a'
          '20095ce6e0f3e5b6800a7c6e52de6fddba62c031')

prepare() {
  cd ksystemlog-${pkgver}
  patch -p2 -i "${srcdir}"/syslog-path.patch
}

build() {
  mkdir -p build
  cd build
  cmake ../ksystemlog-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
