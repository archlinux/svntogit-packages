# $Id$
# Maintainer:
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Leif Warner <abimelech@gmail.com>

pkgbase=python2-rdflib
pkgname=('python2-rdflib' 'python-rdflib')
pkgver=4.1.0
pkgrel=2
pkgdesc="A Python library for working with RDF, a simple yet powerful language for representing information"
arch=('any')
url="http://rdflib.net/"
license=('BSD')
makedepends=('python2-isodate' 'python-isodate' 'python2-setuptools' 'python-setuptools')
checkdepends=('python2-nose' 'python-nose' 'python2-pyparsing' 'python-pyparsing' 
              'python2-six' 'python-six')
source=(rdflib-$pkgver.tar.gz::https://github.com/RDFLib/rdflib/tarball/$pkgver)
sha1sums=('5d7922ea3de794721bc70d3eb0618bcb4fdce9fc')

prepare() {
  cp -r RDFLib-rdflib-* python2-rdflib
  cd python2-rdflib
  sed -i 's|/usr/bin/env python|/usr/bin/env python2|' rdflib/extras/{describer.py,infixowl.py} \
    rdflib/plugins/parsers/{ntriples.py,notation3.py,structureddata.py,pyRdfa/extras/httpheader.py} \
    rdflib/tools/rdfpipe.py
}

check() {
  cd RDFLib-rdflib-*
  python setup.py test
  cd ../python2-rdflib
  python2 setup.py test
}

package_python2-rdflib() {
  depends=('python2-isodate')

  cd python2-rdflib
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644  LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cd "$pkgdir/usr/bin"
  for i in * ; do 
    mv $i $i-py2
  done
}

package_python-rdflib() {
  depends=('python-isodate')

  cd RDFLib-rdflib-*
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644  LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
