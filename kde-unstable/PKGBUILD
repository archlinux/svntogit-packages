# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=kalzium
pkgver=20.11.90
pkgrel=1
pkgdesc="Periodic Table of Elements"
url="https://kde.org/applications/education/kalzium/"
arch=(x86_64)
license=(GPL LGPL FDL)
depends=(kparts knewstuff kplotting kunitconversion qt5-script avogadrolibs openbabel vtk hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools eigen ocaml facile python boost)
groups=(kde-applications kde-education)
source=("https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
        kalzium-openbabel3.patch)
sha256sums=('5274798a5eeb489ba8457463738584d5755381d61c6654ffdf90cec6a7345921'
            'SKIP'
            '742734255681bcd57569f16d58deb3f2bb38b353352fa77d6e65772846334a9c')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  patch -d $pkgname-$pkgver -p1 -i ../kalzium-openbabel3.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
