# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kdebugsettings
pkgver=20.07.80
pkgrel=1
pkgdesc='An application to enable/disable qCDebug'
arch=(x86_64)
url='https://utils.kde.org/'
license=(GPL)
depends=(kdbusaddons ki18n kitemviews kcompletion kcoreaddons)
makedepends=(extra-cmake-modules)
groups=(kde-applications kde-utilities)
source=("https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('3c9a45554b651d8980b531d753d2228270d1af6c34058dc97a2a01cb7b48848d'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}

