# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=gwenview
pkgver=17.11.90
pkgrel=1
pkgdesc="A fast and easy to use image viewer"
url='https://kde.org/applications/graphics/gwenview/'
arch=(x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kdegraphics)
depends=(kactivities baloo libkipi libkdcraw kparts kitemmodels cfitsio)
optdepends=('qt5-imageformats: support for tiff, webp, and more image formats'
            'kimageformats: support for dds, xcf, exr, psd, and more image formats'
            'kipi-plugins: export to various online services')
makedepends=(extra-cmake-modules kdoctools python)
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('d24a04f4a5efe697e948ecb97868e86dca5c0e6ae95704daa9ae660f1d7d9b7d'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DGWENVIEW_SEMANTICINFO_BACKEND="Baloo" \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
