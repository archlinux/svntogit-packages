# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kitinerary
pkgver=18.11.80
pkgrel=1
pkgdesc="Data model and extraction system for travel reservation information"
arch=(x86_64)
url="https://kontact.kde.org"
license=(LGPL)
depends=(qt5-declarative kmime kpkpass kcalcore kcontacts poppler)
makedepends=(extra-cmake-modules zxing-cpp)
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('6718a1c63b7972a47d2d8d24b9eb1daf71bf2f95a4a3a8cf0aaae0005249aea7'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
