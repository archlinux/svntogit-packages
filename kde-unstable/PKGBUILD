# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Roja <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=rocs
pkgver=19.03.90
pkgrel=1
pkgdesc="Graph Theory IDE"
url="https://kde.org/applications/education/rocs/"
arch=(x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kdeedu)
depends=(grantlee ktexteditor kdeclarative qt5-webkit qt5-xmlpatterns hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools boost)
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('64e9e513ed400c8fa31b57bf3d2ef1f48177301c447537fabfd3bb027d077565'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
