# Maintainer: Antonio Rojas <arojas@archlinux.org> 
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=dragon
pkgver=17.07.80
pkgrel=1
pkgdesc='A multimedia player where the focus is on simplicity, instead of features'
arch=(i686 x86_64)
url='https://kde.org/applications/multimedia/dragon'
license=(GPL)
depends=(hicolor-icon-theme kparts)
makedepends=(extra-cmake-modules kdoctools python)
conflicts=(kdemultimedia-dragonplayer)
replaces=(kdemultimedia-dragonplayer)
groups=(kde-applications kdemultimedia)
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('b470c2308a2396290682d05cadf5561efcbfba91f8a03db46bf22cb3d6e854e3'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() { 
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}

