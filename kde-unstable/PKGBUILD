# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=akonadi
pkgver=15.08.0
pkgrel=1
pkgdesc="PIM layer, which provides an asynchronous API to access all kind of PIM data"
arch=('i686' 'x86_64')
url='http://community.kde.org/KDE_PIM/Akonadi'
license=('LGPL')
depends=('shared-mime-info' 'qt5-base' 'libxslt')
makedepends=('extra-cmake-modules' 'postgresql' 'mariadb')
checkdepends=('xorg-server-xvfb')
optdepends=('postgresql: PostgreSQL backend'
            'mariadb: MariaDB backend')
install="${pkgname}.install"
source=("http://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
md5sums=('e6d025189fc54594c84ca52f2a478b62')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DDATABASE_BACKEND=SQLITE \
    -DBUILD_TESTING=OFF \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

check() {
  # 17 akonadi-dbconfigtest failed as we changed the default backend
  # 24~30 failed due to qt cannot find the sqldriver which is not installed yet
  cd build
  xvfb-run -a dbus-launch --exit-with-session make test || warning "Tests failed"
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
