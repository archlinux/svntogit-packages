# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=mbox-importer
pkgver=16.11.90
pkgrel=1
pkgdesc="Import mbox files to KMail"
arch=(i686 x86_64)
url="https://community.kde.org/KDE_PIM"
license=(GPL2)
depends=(mailcommon)
makedepends=(extra-cmake-modules boost python)
conflicts=('kmail<16.11')
groups=(kde-applications kdepim)
source=("http://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
md5sums=('815d7675f9c33fcd47e56e85c4fef47e'
         'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
