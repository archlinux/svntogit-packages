# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=pimcommon
pkgver=19.03.90
pkgrel=1
pkgdesc="Common libraries for KDE PIM"
arch=(x86_64)
url="https://kontact.kde.org"
license=(LGPL)
depends=(libkdepim kpimtextedit knewstuff kimap purpose)
makedepends=(extra-cmake-modules boost qt5-tools kdesignerplugin)
conflicts=('libkdepim<16')
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('618137b5e341f8f4a60ef2c30bb21d9be66c147e3ce620d2f8bccf8ad9172322'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
