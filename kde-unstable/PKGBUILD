# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kmag
pkgver=17.07.80
pkgrel=1
arch=(i686 x86_64)
pkgdesc='Screen Magnifier'
url='https://kde.org/applications/utilities/kmag/'
license=(GPL FDL)
groups=(kde-applications kdeaccessibility)
depends=(kio hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools python)
conflicts=(kdeaccessibility-kmag)
replaces=(kdeaccessibility-kmag)
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('0283ccd9e8dfc62a59315b55aae8a45473fa5faa54c5779c9a7d63f672e7f13f'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
