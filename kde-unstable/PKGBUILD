# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kompare
pkgver=15.07.80
pkgrel=1
pkgdesc='Diff/Patch Frontend'
url='http://kde.org/applications/development/kompare/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdesdk')
depends=('libkomparediff2' 'ktexteditor' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'python' 'kdoctools')
install=${pkgname}.install
conflicts=('kdesdk-kompare')
replaces=('kdesdk-kompare')
source=("http://download.kde.org/unstable/applications/${pkgver}/src/kompare-${pkgver}.tar.xz" 
'save-preferences.patch')
sha1sums=('bc4d5cb3b92f2f24c2c1ae0c32d215c7dd993cef'
          '8c4271c98bfb200c1630c37167aa410a775dcbf2')

prepare() {
  mkdir build

# Fix saving preferences https://bugs.kde.org/show_bug.cgi?id=346249
  cd $pkgname-$pkgver
  patch -p1 -i "$srcdir"/save-preferences.patch
}

build() {
  cd build
  cmake ../kompare-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
