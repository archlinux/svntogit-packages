# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=pimcommon
pkgver=21.07.90
pkgrel=1
pkgdesc='Common libraries for KDE PIM'
arch=(x86_64)
url='https://kontact.kde.org'
license=(LGPL)
depends=(libkdepim kimap kldap akonadi-contacts akonadi-search purpose)
makedepends=(extra-cmake-modules boost qt5-tools)
source=(https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('7a8c0c57beccd71bf896ed6baf805a7d94b6495c316e1cc9f9a7eedff2dd63f4'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
