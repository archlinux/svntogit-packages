# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=elisa
pkgver=19.11.80
pkgrel=1
pkgdesc='A simple music player aiming to provide a nice experience for its users'
url='https://community.kde.org/Elisa'
arch=(x86_64)
license=(LGPL3)
groups=(kde-applications kdemultimedia)
depends=(baloo kirigami2 kcmutils) # upnp-player-qt
makedepends=(extra-cmake-modules kdoctools)
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
         fix-build.patch)
sha256sums=('1833401b15712065f7b2695625ef196cfcb2b2442658286be533ba2014b95755'
            'SKIP'
            '808ef8f2826525854ec4a6939878a746ace7e1c13d9a49385392ea0b3a41aa6f')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../fix-build.patch
}

build() {
  cd build 
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF 
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
