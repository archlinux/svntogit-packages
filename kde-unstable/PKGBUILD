# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kcron
pkgver=15.03.95
pkgrel=1
pkgdesc='Configure and schedule tasks'
url='http://userbase.kde.org/KCron'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdeadmin')
depends=('kio')
makedepends=('extra-cmake-modules' 'kdoctools' 'python')
conflicts=('kdeadmin-kcron')
replaces=('kdeadmin-kcron')
source=("http://download.kde.org/unstable/applications/${pkgver}/src/kcron-${pkgver}.tar.xz")
sha1sums=('4765310e2b2e7fc7c5ce261cbd995898bec9e6c5')

build() {
  mkdir build
  cd build
  cmake ../kcron-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTINS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
