# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=kscreen
pkgver=5.9.95
pkgrel=1
pkgdesc="KDE's screen management software"
arch=('i686' 'x86_64')
url='https://www.kde.org/workspaces/plasmadesktop/'
license=('LGPL')
depends=('kxmlgui' 'libkscreen' 'qt5-graphicaleffects' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'python')
conflicts=('kscreen-frameworks')
replaces=('kscreen-frameworks')
groups=('plasma')
source=("https://download.kde.org/unstable/plasma/${pkgver}/kscreen-$pkgver.tar.xz"{,.sig})
sha256sums=('c6084083f1a76153a1ac7424b9c661feed4cd7e6fa045b8eb5c1290b332879cf'
            'SKIP')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell
              '348C8651206633FD983A8FC4DEACEA00075E1D76'  # KDE Neon
              'D07BD8662C56CB291B316EB2F5675605C74E02CF') # David Edmundson

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kscreen-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
