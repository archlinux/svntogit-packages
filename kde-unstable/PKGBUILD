# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: birdflesh <antkoul at gmail dot com>
# Contributor: said
# Contributor: Panagiotis Papadopoulos <pano_90 AT gmx DOT net>

pkgname=kamoso
pkgver=20.11.90
pkgrel=1
pkgdesc="A webcam recorder from KDE community"
url="https://userbase.kde.org/Kamoso"
arch=(x86_64)
license=(GPL)
groups=(kde-applications kde-multimedia)
depends=(purpose gst-plugins-good gst-plugins-bad kirigami2)
makedepends=(extra-cmake-modules kdoctools boost python)
source=("https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('5ad6b5596de7ead794b1def9411913f878f29e7dd6e7788c7dfc679aca9876d0'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF 
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}

