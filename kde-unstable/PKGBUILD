# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kleopatra
pkgver=22.03.90
pkgrel=1
pkgdesc='Certificate Manager and Unified Crypto GUI'
arch=(x86_64)
url='https://apps.kde.org/kleopatra/'
license=(GPL LGPL FDL)
depends=(hicolor-icon-theme libkleo kcmutils kmime)
makedepends=(extra-cmake-modules kdoctools boost)
groups=(kde-applications kde-pim)
source=(https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('824974ab7f858571f5621b60139e8c0f3998ea2880474b14e6659b834a207d08'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
options=(debug)

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
