# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kcalc
pkgver=20.11.90
pkgrel=1
pkgdesc='Scientific Calculator'
url='https://kde.org/applications/utilities/kcalc/'
arch=(x86_64)
license=(GPL LGPL FDL)
depends=(kinit)
makedepends=(extra-cmake-modules kdoctools)
groups=(kde-applications kde-utilities)
source=("https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('64b05638a8ed81c6a737aebc87401c36669ef161f6810e295346eeae4faf3ebd'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
