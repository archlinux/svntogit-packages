# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: James Rayner <iphitus@gmail.com>
# Contributor: leeghoofd <abcdefg@solcon.nl>

pkgname=yakuake
pkgver=19.11.80
pkgrel=1
pkgdesc="A drop-down terminal emulator based on KDE konsole technology"
arch=(x86_64)
url='https://yakuake.kde.org/'
license=(GPL)
depends=(konsole kwayland hicolor-icon-theme)
makedepends=(extra-cmake-modules)
groups=(kdeutils kde-applications)
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('57aa79538e0203c885b5584ca89a25becb31f00aab1389ce28ce0cbcdf4d865b'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
