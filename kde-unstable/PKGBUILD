# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Travis Willard <travisw@wmpub.ca>
# Contributor: Georg Grabler <ggrabler@gmail.com>

pkgname=ktorrent
pkgver=20.11.90
pkgrel=1
pkgdesc="A powerful BitTorrent client for KDE"
arch=(x86_64)
url='https://www.kde.org/applications/internet/ktorrent/'
license=(GPL2)
depends=(libktorrent kcmutils knotifyconfig kross phonon-qt5 qt5-webengine)
makedepends=(extra-cmake-modules kdoctools boost plasma-workspace kdnssd taglib syndication kplotting geoip)
optdepends=('plasma-workspace: shutdown plugin' 'kdnssd: zeroconf plugin' 'taglib: mediaplayer plugin' 'geoip: info widget plugin'
            'syndication: syndication plugin' 'kplotting: statistics plugin')
groups=(kde-applications kde-network)
source=("https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('63f079f14480f78612b7acf779eb0803816225b55ce93c9c1c2c7f6b6babc9a4'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DWITH_SYSTEM_GEOIP=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
