# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=libkleo
pkgver=20.11.90
pkgrel=1
pkgdesc="KDE PIM cryptographic library"
arch=(x86_64)
url="https://kontact.kde.org"
license=(LGPL)
depends=(qgpgme kitemmodels kpimtextedit)
makedepends=(extra-cmake-modules boost)
source=("https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('1557fb80a7674fcd2f14dc15d18777db2832a110ac1150af380ef2a3c7a4895f'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
