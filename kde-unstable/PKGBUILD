# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kldap
pkgver=23.03.80
pkgrel=2
pkgdesc='LDAP access API for KDE'
arch=(x86_64)
url='https://kontact.kde.org'
license=(LGPL)
depends=(kio qtkeychain-qt5)
makedepends=(extra-cmake-modules kdoctools doxygen qt5-tools)
source=(https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('7f0d88ce7d410c6271bfc4698ba5ceebbbb99daaa6068a6dc15a3b54221e7371'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
