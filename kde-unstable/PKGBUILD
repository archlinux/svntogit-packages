# Maintainer:Antonio Rojas <arojas@archlinux.org>

pkgname=plasma-pa
pkgver=5.22.90
pkgrel=1
pkgdesc='Plasma applet for audio volume management using PulseAudio'
arch=(x86_64)
url='https://kde.org/plasma-desktop/'
license=(LGPL)
depends=(plasma-workspace pulseaudio perl)
makedepends=(extra-cmake-modules kdoctools)
groups=(plasma)
source=(https://download.kde.org/unstable/plasma/$pkgver/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('f21c9d11969fc0a8aed46fb8ce983d0081527a785080f9b8f2a8df9b8335935b'
            'SKIP')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
