# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kwordquiz
pkgver=17.11.90
pkgrel=1
pkgdesc="Flash Card Trainer"
url="https://kde.org/applications/education/kwordquiz/"
arch=(x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kdeedu)
depends=(knotifyconfig libkeduvocdocument knewstuff kdelibs4support)
makedepends=(extra-cmake-modules python kdoctools kdesignerplugin)
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('7a93097147aa51a6108d9464f0053890217f9e0a6fa44a0b984723e14f37c9ae'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
