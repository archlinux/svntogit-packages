# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=qt6-imageformats
_qtver=6.5.0-beta2
pkgver=${_qtver/-/}
pkgrel=1
arch=(x86_64)
url='https://www.qt.io'
license=(GPL3 LGPL3 FDL custom)
pkgdesc='Plugins for additional image formats: TIFF, MNG, TGA, WBMP'
depends=(qt6-base jasper libwebp libmng)
makedepends=(cmake ninja)
groups=(qt6)
_pkgfn=${pkgname/6-/}-everywhere-src-$_qtver
source=(https://download.qt.io/development_releases/qt/${pkgver%.*}/$_qtver/submodules/$_pkgfn.tar.xz
        fix-webp-build.patch)
sha256sums=('c2e652317d530adaa68ba001766b70191ace017fb902103ebc82b90f5d3649ce'
            '10e2aa7ff3074bc15e6b86462cdb5bcd6bc3a1f979a2147c7326d7930722b9ee')

prepare() {
  patch -d $_pkgfn -p1 < fix-webp-build.patch # https://bugreports.qt.io/browse/QTBUG-110720
}

build() {
  cmake -B build -S $_pkgfn -G Ninja \
    -DCMAKE_MESSAGE_LOG_LEVEL=STATUS
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt6-base "$pkgdir"/usr/share/licenses/$pkgname
}
