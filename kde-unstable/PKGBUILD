# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux,org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=dolphin-plugins
pkgver=15.11.80
pkgrel=1
pkgdesc="Extra Dolphin plugins"
arch=(i686 x86_64)
url="https://projects.kde.org/dolphin-plugins"
license=(LGPL)
depends=(dolphin)
makedepends=(extra-cmake-modules kdoctools)
conflicts=(kdesdk-dolphin-plugins)
replaces=(kdesdk-dolphin-plugins)
groups=(kde-applications kdesdk)
source=("http://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
md5sums=('8189bf781cfb585fae25a35fc172c717')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
