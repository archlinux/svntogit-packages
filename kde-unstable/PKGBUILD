# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=akonadi-search
pkgver=15.11.80
pkgrel=1
pkgdesc="Libraries and daemons to implement searching in Akonadi"
url='https://projects.kde.org/akonadi-search'
arch=(i686 x86_64)
license=(GPL LGPL FDL)
depends=(xapian-core akonadi-mime kcontacts kcalcore krunner)
makedepends=(extra-cmake-modules kdoctools boost)
source=("http://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
md5sums=('60e691e747175cc4a301ba1eea540451')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
