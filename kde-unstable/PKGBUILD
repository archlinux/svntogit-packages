# Maintainer: Antonio Rojas <arojas@archlinux,org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=keditbookmarks
pkgver=20.11.90
pkgrel=1
pkgdesc="Bookmark Organizer and Editor"
arch=(x86_64)
url="https://www.kde.org/"
license=(LGPL)
depends=(kparts)
makedepends=(extra-cmake-modules kdoctools)
groups=(kde-applications kde-utilities)
source=("https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('0475525f5e5b2d8e5b7c0cd7e550a23a7e4ab45e58a613f104cb73b1566f95e4'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
