# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kblackbox
pkgver=15.11.80
pkgrel=1
pkgdesc="A game of hide and seek played on a grid of boxes"
url="http://kde.org/applications/games/kblackbox/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegames')
depends=('libkdegames' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'python' 'kdoctools')
install=${pkgname}.install
conflicts=('kdegames-kblackbox')
replaces=('kdegames-kblackbox')
source=("http://download.kde.org/unstable/applications/${pkgver}/src/kblackbox-${pkgver}.tar.xz")
sha1sums=('1423907c1415cb504d3746aa9ff3daa57054042b')

build() {
  mkdir -p build
  cd build
  cmake ../kblackbox-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
