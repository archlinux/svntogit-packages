# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kaccounts-providers
pkgver=19.11.90
pkgrel=1
pkgdesc='Small system to administer web accounts for the sites and services across the KDE desktop, including: Google, Facebook, Owncloud, IMAP, Jabber and others'
arch=(x86_64)
url='https://www.kde.org/applications/internet/'
license=(GPL)
groups=(kde-applications kdenetwork)
depends=(kaccounts-integration)
makedepends=(extra-cmake-modules intltool)
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('bb8eaac26dd09945ca46ed84ac8aa9e124f3a47d0f638c23975cb449520d205f'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
