# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=kalzium
pkgver=19.03.90
pkgrel=1
pkgdesc="Periodic Table of Elements"
url="https://kde.org/applications/education/kalzium/"
arch=(x86_64)
license=(GPL LGPL FDL)
depends=(khtml knewstuff kplotting kunitconversion avogadrolibs openbabel hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools eigen ocaml facile python)
conflicts=(kdeedu-kalzium)
replaces=(kdeedu-kalzium)
groups=(kde-applications kdeedu)
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('0586b48c995bce2b6844e0128e3b3b7da751fb71b9b35a65c70a55053c845c60'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
