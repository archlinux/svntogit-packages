# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=angelfish
pkgver=23.03.90
pkgrel=1
pkgdesc='Web browser for Plasma Mobile'
url='https://apps.kde.org/angelfish/'
arch=(x86_64)
license=(GPL LGPL)
groups=(kde-applications kde-network)
depends=(kirigami-addons purpose qqc2-desktop-style qt5-webengine qt5-svg qt5-feedback)
makedepends=(extra-cmake-modules corrosion)
source=(https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('09051556cbb4265f3027edd187a3433578646635d7b4e9bd23aaf6544adfc266'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
options=(!lto)

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
