# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: anex <assassin.anex[@]gmail.com

pkgname=signon-kwallet-extension
pkgver=17.11.90
pkgrel=1
pkgdesc="KWallet integration for signon framework"
arch=(x86_64)
url="https://kde.org"
license=(LGPL)
groups=(kde-applications kdenetwork)
depends=(signond kwallet)
makedepends=(extra-cmake-modules)
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('a92def331463bca1a63b47346f01b129bffb67351b29454972e58bfdafba18bd'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
