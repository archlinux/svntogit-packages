# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=tokodon
pkgver=23.03.80
pkgrel=1
pkgdesc='Kirigami-based RSS reader'
url='https://apps.kde.org/tokodon/'
arch=(x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kde-network)
depends=(qqc2-desktop-style kirigami-addons kdbusaddons knotifications kitemmodels kio qtkeychain-qt5 qt5-websockets)
makedepends=(extra-cmake-modules)
source=(https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('09e48b13b299e986d5d890134f5bf34e5a7fe5168c53ae37585636ed0f999520'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
