# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=khangman
pkgver=17.07.80
pkgrel=1
pkgdesc="Hangman Game"
url="https://kde.org/applications/education/khangman/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdeedu')
depends=('libkeduvocdocument' 'knewstuff' 'kdeclarative')
makedepends=('extra-cmake-modules' 'kdoctools' 'python')
replaces=('kdeedu-khangman')
conflicts=('kdeedu-khangman')
source=("https://download.kde.org/unstable/applications/${pkgver}/src/khangman-${pkgver}.tar.xz"{,.sig})
sha256sums=('e76b55a4616f5ac7b07cfd0ebe7d330f4a96aceb2dd90ec70c4d68851435f286'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../khangman-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DSYSCONF_INSTALL_DIR=/etc
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
