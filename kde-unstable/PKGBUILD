# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegraphics-thumbnailers
pkgver=20.11.90
pkgrel=1
pkgdesc="Thumbnailers for various graphics file formats"
url="https://www.kde.org/applications/graphics/"
arch=(x86_64)
license=(GPL LGPL FDL)
depends=(libkexiv2 libkdcraw kio ghostscript)
makedepends=(extra-cmake-modules)
groups=(kde-applications kde-graphics)
source=("https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('22c7f65837947a5c9cf9d4a5e517267d6db66fc315fd3d8b807c724101ab479e'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
