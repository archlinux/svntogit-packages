# Maintainer: Laurent Carlier <lordheavym@gmail.com>

pkgname=libepoxy
pkgver=1.5.7
pkgrel=1
pkgdesc="Library handling OpenGL function pointer management"
url="https://github.com/anholt/libepoxy"
arch=(x86_64)
license=(MIT)
depends=(glibc)
makedepends=(python mesa-libgl git meson doxygen graphviz)
_commit=f9e76359cf7da5fde70440361ffad5e9c8aee36b  # tags/1.5.7^0
source=("git+https://github.com/anholt/libepoxy#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build -D docs=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
  install -Dm644 $pkgname/COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set sw=2 et:
