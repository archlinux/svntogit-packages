# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=xcb-util-renderutil
pkgver=0.3.8
pkgrel=1
pkgdesc="Utility libraries for XC Binding - Convenience functions for the Render extension"
arch=('i686' 'x86_64')
url="http://xcb.freedesktop.org"
license=('custom')
depends=('libxcb>=1.7')
makedepends=('xorg-util-macros')
options=('!libtool')
source=("http://xcb.freedesktop.org/dist/${pkgname}-${pkgver}.tar.bz2"
        'LICENSE')
md5sums=('b346ff598ee093c141f836fbc0f8f721'
         '2672c2e72dedb40f773e989dd622f298')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --disable-static
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() { 
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -D -m644 "${srcdir}/LICENSE" \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
