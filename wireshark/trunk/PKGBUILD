# $Id$
# Maintainer: Guillaume ALAUX <guillaume at alaux dot net>
# Contributor: Florian Pritz <bluewind at jabber dot ccc dot de>
pkgname=(wireshark-cli wireshark-gtk)
pkgbase=wireshark
pkgver=1.4.7
pkgrel=2
arch=('i686' 'x86_64')
license=('GPL2')
makedepends=('bison' 'flex' 'gtk2' 'krb5' 'libpcap' 'bash' 'gnutls' 'libcap' 'lua')
url="http://www.wireshark.org/"
options=(!libtool)
source=(http://www.wireshark.org/download/src/${pkgbase}-${pkgver}.tar.bz2)
md5sums=('b5065426d5524ddc1667314f8256c2b1')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  export CFLAGS="-fno-unit-at-a-time ${CFLAGS}"

  ./configure \
      --prefix=/usr \
      --with-ssl \
      --with-zlib=no \
      --with-lua
  make all
}

package_wireshark-cli() {
  pkgdesc="A free network protocol analyzer for Unix/Linux and Windows - CLI version"
  depends=('krb5' 'libpcap' 'bash' 'gnutls' 'libcap' 'glib2' 'lua')
  install=wireshark.install
  conflicts=(wireshark)

  cd "${srcdir}/${pkgbase}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  #wireshark uid group is 150
  chgrp 150 "${pkgdir}/usr/bin/dumpcap"
  chmod 754 "${pkgdir}/usr/bin/dumpcap"
  rm "$pkgdir/usr/bin/wireshark"
}

package_wireshark-gtk() {
  pkgdesc="A free network protocol analyzer for Unix/Linux and Windows - GTK frontend"
  depends=('gtk2' 'wireshark-cli')
  install=wireshark-gtk.install
  replaces=(wireshark)
  conflicts=(wireshark)

  cd "${srcdir}/${pkgbase}-${pkgver}"

  install -Dm755 .libs/wireshark "$pkgdir/usr/bin/wireshark"
  for d in 16 32 48; do
      install -Dm644 image/hi${d}-app-wireshark.png ${pkgdir}/usr/share/icons/hicolor/${d}x${d}/apps/wireshark.png
  done
  install -Dm644 wireshark.desktop ${pkgdir}/usr/share/applications/wireshark.desktop
}
