# $Id$
# Maintainer: Hugo Doria <hugo@archlinux.org>

pkgname=wireshark
pkgver=1.2.9
pkgrel=1
pkgdesc="A free network protocol analyzer for Unix/Linux and Windows"
arch=('i686' 'x86_64')
license=('GPL2')
depends=('gtk2' 'heimdal' 'libpcap' 'bash' 'gnutls' 'libcap')
makedepends=('bison' 'flex')
url="http://www.wireshark.org/"
install=wireshark.install
options=(!libtool)
source=(http://www.wireshark.org/download/src/${pkgname}-${pkgver}.tar.gz wireshark.desktop wireshark.png wireshark-1.1.2--as-needed.patch)

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  export CFLAGS="-fno-unit-at-a-time ${CFLAGS}"

  patch -Np1 -i $srcdir/wireshark-1.1.2--as-needed.patch || return 1
  autoreconf -f -i || return 1

  ./configure --prefix=/usr --with-ssl || return 1
  make all || return 1
  make DESTDIR=${pkgdir} install || return 1

  install -Dm644 ${srcdir}/wireshark.png ${pkgdir}/usr/share/icons/wireshark.png
  install -Dm644 ${srcdir}/wireshark.desktop ${pkgdir}/usr/share/applications/wireshark.desktop
  #wireshark uid group is 150
  chgrp 150 "${pkgdir}/usr/bin/dumpcap" || return 1
  chmod 754 "${pkgdir}/usr/bin/dumpcap" || return 1
}
md5sums=('582fe8d14a6e7c66851e5148a2f6e5d8'
         '504ede44c02b2a52f6cbd3bffdf93b8d'
         'e771800a8c977fe223583a453bc27397'
         'cc87d7eb31f256df751b3371b1c426e9')
