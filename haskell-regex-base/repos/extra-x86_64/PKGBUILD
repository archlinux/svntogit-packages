# Maintainer: Vesa Kaihlavirta <vesa@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>
_hkgname=regex-base
pkgname=haskell-regex-base
pkgrel=1.1
pkgver=0.93.2
pkgdesc="Interface API for regex-posix,pcre,parsec,tdfa,dfa"
url="http://hackage.haskell.org/cgi-bin/hackage-scripts/package/regex-base"
license=('custom:BSD3')
arch=('i686' 'x86_64')
depends=(ghc=6.12.3 sh 'haskell-mtl=1.1.0.2')
options=('strip')
source=(http://hackage.haskell.org/packages/archive/regex-base/$pkgver/regex-base-$pkgver.tar.gz)
install=haskell-regex-base.install
md5sums=('e7b93b0b17eff8d3068ecb2f5d5f6ea3')

build() {
    cd ${srcdir}/${_hkgname}-$pkgver
    runhaskell Setup configure -O -p --enable-split-objs --enable-shared --prefix=/usr --docdir=/usr/share/doc/${pkgname}
    runhaskell Setup build
    runhaskell Setup haddock
    runhaskell Setup register   --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd ${srcdir}/${_hkgname}-${pkgver}
    install -D -m744 register.sh   ${pkgdir}/usr/share/haskell/${pkgname}/register.sh
    install    -m744 unregister.sh ${pkgdir}/usr/share/haskell/${pkgname}/unregister.sh
    install -d -m755 ${pkgdir}/usr/share/doc/ghc/html/libraries
    ln -s /usr/share/doc/${pkgname}/html ${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}
    runhaskell Setup copy --destdir=${pkgdir}
    install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
    rm -f ${pkgdir}/usr/share/doc/${pkgname}/LICENSE
}
