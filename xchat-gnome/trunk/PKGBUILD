# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=xchat-gnome
pkgver=0.26.1
pkgrel=3
pkgdesc="GNOME frontend to the popular X-Chat IRC client."
arch=('i686' 'x86_64')
license=('GPL')
url="http://xchat-gnome.navi.cx/"
depends=('libgnomeui>=2.18.1-2' 'libsexy>=0.1.11' 'gconf>=2.18.0.1-4'
         'hicolor-icon-theme' 'desktop-file-utils' 'libcanberra' 'libsm')
makedepends=('gettext' 'perlxml' 'python' 'tcl' 'libnotify' 'gnome-doc-utils>=0.10.3'
             'intltool' 'pkgconfig')
optdepends=('libnotify: notifications'
            'python: plugin'
            'tcl: plugin')
install=xchat-gnome.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/0.26/${pkgname}-${pkgver}.tar.gz
        gtk2-2.20.patch)
md5sums=('e355d71d76cd97a0764e37bfacf09101'
         'dafc2536a0c5ee3f8015af81fce69656')
options=('!libtool')

build() {
  cd "$srcdir/${pkgname}-${pkgver}"
  patch -Np1 -i $srcdir/gtk2-2.20.patch

  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var --disable-static \
	      --enable-ipv6 --enable-shm \
	      --disable-gtkfe --disable-scrollkeeper || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1
  
  mkdir -p "$pkgdir/usr/share/gconf/schemas"
  gconf-merge-schema "$pkgdir/usr/share/gconf/schemas/${pkgname}.schemas" \
    "$pkgdir"/etc/gconf/schemas/*.schemas
  rm -f "$pkgdir"/etc/gconf/schemas/*.schemas
}
