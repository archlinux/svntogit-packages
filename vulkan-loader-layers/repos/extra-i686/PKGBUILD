# $Id$
# Maintainer: Laurent Carlier <lordheavym@gmail.com>

pkgbase=vulkan-loader-layers
pkgname=(vulkan-icd-loader vulkan-validation-layers)
_pkgname=Vulkan-LoaderAndValidationLayers
pkgver=1.0.39.0
pkgrel=1
arch=(i686 x86_64)
url="https://www.khronos.org/vulkan/"
license=('custom')
depends=()
makedepends=(git cmake python-lxml libx11 libxrandr wayland)
source=("https://github.com/KhronosGroup/${_pkgname}/archive/sdk-${pkgver}.tar.gz")
sha256sums=('146efb378dc143f9dc15843b29aec8e5a0ad74161628931f0057d9517457a846')

build() {
  cd "${srcdir}/${_pkgname}-sdk-${pkgver}"

  ./update_external_sources.sh

  rm -rf build ; mkdir build ; cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_SYSCONFDIR=/etc \
    -DCMAKE_INSTALL_DATADIR=/share \
    -DCMAKE_SKIP_RPATH=True \
    -DBUILD_TESTS=Off \
    -DBUILD_WSI_XCB_SUPPORT=On \
    -DBUILD_WSI_XLIB_SUPPORT=On \
    -DBUILD_WSI_WAYLAND_SUPPORT=On \
    -DBUILD_WSI_MIR_SUPPORT=Off \
    -DCMAKE_BUILD_TYPE=Release \
    ..
  make
  make DESTDIR=install install
}

package_vulkan-icd-loader() {
  pkgdesc="Vulkan Installable Client Driver (ICD) Loader"
  depends=(glibc)
  optdepends=('vulkan-driver: packaged vulkan driver') # vulkan-driver: vulkan-intel/vulkan-radeon/nvidia-utils/....
  cd "${srcdir}/${_pkgname}-sdk-${pkgver}"
  
  install -dm755 ${pkgdir}/usr/{lib,include/vulkan}
  install -dm755 ${pkgdir}/usr/share/licenses/${pkgname}
  
  cp -rv  build/loader/libvulkan.so* ${pkgdir}/usr/lib
  install -m644 include/vulkan/vk_icd.h ${pkgdir}/usr/include/vulkan/
  
  install -m644 LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/
}

package_vulkan-validation-layers() {
  pkgdesc="Vulkan Software Development Kit"
  depends=(gcc-libs vulkan-icd-loader vulkan-headers libx11)
  conflicts=('vulkan-sdk')
  replaces=('vulkan-sdk')
  groups=('vulkan-devel')
  cd "${srcdir}/${_pkgname}-sdk-${pkgver}"
  
  install -dm755 ${pkgdir}/usr/{bin,lib,include/vulkan}
  install -dm755 ${pkgdir}/usr/share/{doc,licenses}/${pkgname}
  install -dm755 ${pkgdir}/usr/share/vulkan/explicit_layer.d

  install -m755 build/demos/vulkaninfo ${pkgdir}/usr/bin/
  
  install -m644 build/layers/*.so ${pkgdir}/usr/lib

  install -m644 include/vulkan/* ${pkgdir}/usr/include/vulkan/
  # already in vulkan-headers
  rm ${pkgdir}/usr/include/vulkan/{vulkan,vk_platform}.h
  # already in vulkan-icd-loader
  rm ${pkgdir}/usr/include/vulkan/vk_icd.h
  # FS#50652
  rm ${pkgdir}/usr/include/vulkan/vulkan.hpp

  # fix library paths  
  for i in layers/linux/*.json ; do 
    sed 's@./@@' $i > ${pkgdir}/usr/share/vulkan/explicit_layer.d/$(basename $i)
  done
  
  install -m644 loader/LoaderAndLayerInterface.md "${pkgdir}"/usr/share/doc/${pkgname}/

  install -m644 LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/
}
