# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>

pkgname=socat
pkgver=1.7.4.1
pkgrel=1
pkgdesc='Multipurpose relay'
url='http://www.dest-unreach.org/socat/'
license=('GPL2')
arch=('x86_64')
depends=('glibc' 'readline' 'libreadline.so' 'openssl')
checkdepends=('bash' 'net-tools' 'procps-ng')
source=("http://www.dest-unreach.org/socat/download/${pkgname}-${pkgver}.tar.gz")
sha256sums=('0c7e635070af1b9037fd96869fc45eacf9845cb54547681de9d885044538736d')
b2sums=('3ebc3a5ff62e4ad89be31868b6fd78008d51ff99ed4a782dcc24b0ff50f2196d6000d95c1b5b602579df2eef4c64c27dd12f48cf43a5b8386de0a7a2d2e55cdd')

build() {
  cd "${pkgname}-${pkgver}"
  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man \

  make
}

check() {
  cd "${pkgname}-${pkgver}"
  # TODO: one of the tests always fails, but lets keep
  # it for manual sanity check
  make test||true
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
