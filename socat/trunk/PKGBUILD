# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>

pkgname=socat
pkgver=1.7.3.1
pkgrel=1
pkgdesc='Multipurpose relay'
url='http://www.dest-unreach.org/socat/'
license=('GPL2')
arch=('i686' 'x86_64')
depends=('readline' 'openssl')
source=("http://www.dest-unreach.org/socat/download/${pkgname}-${pkgver}.tar.gz"
        'add-missing-checks.patch')
sha1sums=('a6f1d8ab3e85f565dbe172f33a9be6708dd52ffb'
          'cfdaa269dac053ceec4c4f763d9075983c4c9307')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	patch -Np1 -i ../add-missing-checks.patch
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure \
		--prefix=/usr \
		--mandir=/usr/share/man \

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
