# $Id$
# Contributor: Jochem Kossen <j.kossen@home.nl>
# Contributor: Kevin Piche <kevin@archlinux.org>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=abcde
pkgver=2.4.1
pkgrel=2
pkgdesc="A frontend command-line utility that grabs tracks off a CD, encodes them to ogg or mp3 format, and tags them, all in one go."
arch=('any')
url="http://code.google.com/p/abcde/"
license=('GPL')
depends=('bash' 'cd-discid' 'wget' 'vorbis-tools')
optdepends=('eject: Eject support'
	'cdparanoia: Paranoia ripping support'
	'cdrkit: icedax ripping support'
	'lame: MP3 encoding support'
	'flac: FLAC encoding support'
	'id3: ID3 v1/v2 tag support'
	'id3v2: ID3 v2 tag support')
backup=(etc/${pkgname}.conf)
source=(ftp://ftp.archlinux.org/other/${pkgname}/${pkgname}-${pkgver}.tar.gz
	'fix-mp3-tagging.patch')
md5sums=('5df055a1b6332d9ddbcfc5756369ee3d'
         'c3975dc8389ff2edba6684bbc378d97d')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}

	patch -Np0 < ${srcdir}/fix-mp3-tagging.patch || return 1

	sed -e "s:normalize-audio:normalize:g" -i ${pkgname}
	sed -e "s:/usr/bin/less:/bin/less:g" -i ${pkgname}
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}

	install -Dm 0755 ${pkgname} ${pkgdir}/usr/bin/${pkgname} || return 1
	install -Dm 0755 cddb-tool ${pkgdir}/usr/bin/cddb-tool || return 1
	install -Dm 0644 cddb-tool.1 ${pkgdir}/usr/share/man/man1/cddb-tool.1 || return 1
	install -Dm 0644 ${pkgname}.1 ${pkgdir}/usr/share/man/man1/${pkgname}.1 || return 1
	install -Dm 0644 ${pkgname}.conf ${pkgdir}/etc/${pkgname}.conf || return 1
}
