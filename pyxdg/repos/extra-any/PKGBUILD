# $Id$
# Maintainer: Hugo Doria <hugo@archlinux.org>
# Contributor: Mario A. Vazquez <mario_vazq@hotmail.com>

pkgbase=pyxdg
pkgname=('python2-xdg' 'python-xdg')
pkgver=0.25
pkgrel=4
pkgdesc="Python library to access freedesktop.org standards"
arch=('any')
url="http://freedesktop.org/Software/pyxdg"
license=("LGPL")
makedepends=('python2' 'python')
source=(http://www.freedesktop.org/~takluyver/${pkgbase}-${pkgver}.tar.gz{,.asc})
sha1sums=('1272ee62b8c3909ac4844a34a95f8b1fb3398af2'
          'b5ec82567cc4d7bcf3a151eaf70c42dd3ed5b194')
validpgpkeys=('6391296E3F25C0A721C1F1AFAF88B3AC5DECB224') # Thomas Kluyver

package_python2-xdg() {
  depends=('python2')
  conflicts=('pyxdg')
  provides=('pyxdg')
  replaces=('pyxdg')

  cd "${srcdir}/${pkgbase}-${pkgver}"
  python2 setup.py install --prefix=/usr --root="${pkgdir}"
}

package_python-xdg() {
  depends=('python')

  cd "${srcdir}/${pkgbase}-${pkgver}"
  python setup.py install --prefix=/usr --root="${pkgdir}" --skip-build --optimize=1
}
