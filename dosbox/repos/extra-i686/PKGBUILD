# $Id$
# Maintainer :
# Contributor: James Rayner <james@archlinux.org>
# Contributor: Ben <ben@benmazer.net>

pkgname=dosbox
pkgver=0.74
pkgrel=1
pkgdesc="An emulator with builtin DOS for running DOS Games"
arch=('i686' 'x86_64')
url="http://dosbox.sourceforge.net/"
license=('GPL')
depends=('sdl_net' 'sdl_sound' 'libgl' 'libpng' 'alsa-lib' 'gcc-libs')
makedepends=('mesa')
source=("http://downloads.sourceforge.net/${pkgname}/$pkgname-$pkgver.tar.gz"
	'dosbox.png' 'dosbox.desktop')
md5sums=('b9b240fa87104421962d14eee71351e8'
         '2aac25fc06979e375953fcc36824dc5e'
         '85169ca599028bee8e29e0b3b7b34dd8')

build(){
  cd ${srcdir}/${pkgname}-${pkgver}
  sed -i 's/png_check_sig/png_sig_cmp/' configure || return 1
  ./configure --prefix=/usr \
    --sysconfdir=/etc/dosbox || return 1
  make || return 1
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install || return 1

  install -Dm644 ${srcdir}/${pkgname}.png \
    ${pkgdir}/usr/share/pixmaps/${pkgname}.png || return 1
  install -Dm644 ${srcdir}/${pkgname}.desktop \
    ${pkgdir}/usr/share/applications/${pkgname}.desktop || return 1
}
