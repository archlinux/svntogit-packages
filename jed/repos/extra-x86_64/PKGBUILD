# $Id$
# Maintainer:
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

pkgname=jed
pkgver=0.99.19
_pkgver=0.99-19
pkgrel=1
pkgdesc="A freely available text editor for Unix and others OS"
arch=('i686' 'x86_64')
url="http://www.jedsoft.org/jed"
license=('GPL')
depends=('gpm' 'slang' 'libxft')
makedepends=('libxext' 'libxt')
options=('!makeflags')
source=(ftp://space.mit.edu/pub/davis/${pkgname}/v0.99/${pkgname}-${_pkgver}.tar.bz2)
md5sums=('c9b2f58a3defc6f61faa1ce7d6d629ea')

build() {
  cd ${srcdir}/${pkgname}-${_pkgver}
  CFLAGS=`/usr/bin/freetype-config  --cflags`\ ${CFLAGS}\ -DXJED_HAS_XRENDERFONT=1 ./configure --prefix=/usr

  #Enable mouse support
  sed -i -e 's/#MOUSEFLAGS/MOUSEFLAGS/' -e 's/#MOUSELIB/MOUSELIB/' \
    -e 's/#GPMMOUSEO/GPMMOUSEO/' -e 's/#OBJGPMMOUSEO/OBJGPMMOUSEO/' \
     src/Makefile

  #Enable XRender support
  sed -i -e 's/#XRENDERFONTLIBS/XRENDERFONTLIBS/' src/Makefile

  make JED_ROOT=/usr/share/jed || return 1
  make JED_ROOT=/usr/share/jed xjed || return 1
  make JED_ROOT=/usr/share/jed CFLAGS+=-I/usr/include/freetype2 rgrep || return 1
}

package() {
  cd ${srcdir}/${pkgname}-${_pkgver}
  make DESTDIR=${pkgdir} install || return 1
  
  # fix path
  mv ${pkgdir}/usr/jed ${pkgdir}/usr/share/
}
