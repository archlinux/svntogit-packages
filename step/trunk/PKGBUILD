# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=step
pkgver=16.04.1
pkgrel=1
pkgdesc="Interactive Physical Simulator"
url="http://kde.org/applications/education/step/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdeedu')
depends=('libqalculate' 'gsl' 'knewstuff' 'khtml' 'kdelibs4support' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'python' 'kdoctools' 'eigen' 'mesa')
conflicts=('kdeedu-step')
replaces=('kdeedu-step')
source=("http://download.kde.org/stable/applications/${pkgver}/src/step-${pkgver}.tar.xz")
sha1sums=('ca652881c63f0bec2ad839ad8197e86ee12c92e3')

build() {
  mkdir -p build
  cd build
  cmake ../step-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
