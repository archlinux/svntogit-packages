# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=vino
pkgver=3.10.1
pkgrel=2
pkgdesc="a VNC server for the GNOME desktop"
arch=(i686 x86_64)
license=(GPL)
depends=(libnotify libxtst libsm telepathy-glib gtk3 libsecret avahi desktop-file-utils)
makedepends=(intltool networkmanager gnome-common)
groups=(gnome)
url="http://www.gnome.org"
options=(!emptydirs)
install=vino.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('faf10d667c43d00cd37dc6343b44012456de9d2e39d8ce679c719e65d2dfbe46')

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/vino \
      --localstatedir=/var \
      --disable-http-server --with-secret

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
