# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=vino
pkgver=3.2.2
pkgrel=1
pkgdesc="a VNC server for the GNOME desktop"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libnotify' 'libxtst' 'libsm' 'libsoup' 'telepathy-glib' 'gtk3' 'libgnome-keyring' 'avahi' 'desktop-file-utils')
makedepends=('intltool' 'networkmanager')
groups=('gnome-extra')
url="http://www.gnome.org"
options=(!emptydirs)
install=vino.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('51089c35b5187c37ebcd4f8ad921123f9a56713316876531189a3f3765dc87d1')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/vino \
      --localstatedir=/var \
      --enable-gnome-keyring \
      --enable-avahi \
      --disable-http-server
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
