# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Nezmer <Nezmer@gmail.com>

pkgname=fribidi
pkgver=1.0.12
pkgrel=1
pkgdesc="A Free Implementation of the Unicode Bidirectional Algorithm"
url="https://github.com/fribidi/fribidi/"
arch=(x86_64)
license=(LGPL)
depends=(glibc)
makedepends=(git meson)
provides=(libfribidi.so)
options=(debug)
_commit=6428d8469e536bcbb6e12c7b79ba6659371c435a  # tags/v1.0.12^0
source=("git+https://github.com/fribidi/fribidi#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd fribidi
  git describe --tags | sed 's/^v//;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd fribidi
}

build() {
  arch-meson fribidi build \
    -D docs=false
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 et:
