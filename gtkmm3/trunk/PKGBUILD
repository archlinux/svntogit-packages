# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgbase=gtkmm3
pkgname=(gtkmm3 gtkmm3-docs)
pkgver=3.22.2+2+g3002ed86
pkgrel=1
pkgdesc="C++ bindings for GTK+ 3"
url="http://www.gtkmm.org/"
arch=(x86_64)
license=(LGPL)
depends=(gtk3 pangomm atkmm)
makedepends=(git mm-common glibmm-docs cairomm-docs pangomm-docs atkmm-docs)
_commit=3002ed863d2d85d57431e3aa1df9721d5dd3787c  # gtkmm-3-22
source=("git+https://git.gnome.org/browse/gtkmm#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd gtkmm
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd gtkmm
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd gtkmm
  ./configure --prefix=/usr --enable-maintainer-mode
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package_gtkmm3() {
  cd gtkmm
  sed -i -e 's/^doc_subdirs/#doc_subdirs/' Makefile
  make DESTDIR="$pkgdir" install
}

package_gtkmm3-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  cd gtkmm
  make -C docs DESTDIR="$pkgdir" install
}
