# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgbase=gtkmm3
pkgname=(gtkmm3 gtkmm3-docs)
pkgver=3.24.4
pkgrel=1
pkgdesc="C++ bindings for GTK+ 3"
url="https://www.gtkmm.org/"
arch=(x86_64)
license=(LGPL)
depends=(gtk3 pangomm atkmm)
makedepends=(git mm-common glibmm-docs cairomm-docs pangomm-docs atkmm-docs meson)
checkdepends=(xorg-server-xvfb)
options=(!emptydirs)
_commit=1be91eed6ff3ae6b109f7bddc6f13d1e9a3d7a1d  # tags/3.24.4^0
source=("git+https://gitlab.gnome.org/GNOME/gtkmm.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd gtkmm
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd gtkmm
}

build() {
  arch-meson gtkmm build -D maintainer-mode=true
  meson compile -C build
}

check() {
  xvfb-run -s '-nolisten local' \
    meson test -C build --print-errorlogs
}

package_gtkmm3() {
  DESTDIR="$pkgdir" meson install -C build

  mkdir -p docs/usr/share
  mv "$pkgdir"/usr/share/{devhelp,doc} docs/usr/share
}

package_gtkmm3-docs() {
  pkgdesc+=" (documentation)"
  depends=()
  options=(!strip)

  mv docs/* "$pkgdir"
}
