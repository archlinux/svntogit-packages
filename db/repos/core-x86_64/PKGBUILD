# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>

pkgname=db
pkgver=6.2.32
pkgrel=1
pkgdesc="The Berkeley DB embedded database system"
arch=(x86_64)
url='https://www.oracle.com/technology/software/products/berkeley-db/index.html'
license=(AGPL3)
depends=(gcc-libs sh)
source=(https://download.oracle.com/berkeley-db/db-${pkgver}.tar.gz)
b2sums=('d3254fba1300d6c7dda5d872c5fa349e8704d71be9c4d37ccf3357782adeb879ce30e066cf083bd97554405c2e94071a96cee16090ad9f797a4fa657ff7e8c46')

build() {
  cd ${pkgname}-${pkgver}/build_unix
  ../dist/configure \
    --prefix=/usr \
    --enable-compat185 \
    --enable-shared \
    --enable-static \
    --enable-cxx \
    --enable-dbm \
    --enable-stl
  make LIBSO_LIBS=-lpthread
}

package() {
  make -C ${pkgname}-${pkgver}/build_unix DESTDIR="${pkgdir}" install
  rm -r "${pkgdir}"/usr/docs
}
