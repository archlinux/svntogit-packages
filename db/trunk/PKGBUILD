# $Id$
# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>

pkgname=db
pkgver=6.0.20
pkgrel=1
pkgdesc="The Berkeley DB embedded database system"
arch=('i686' 'x86_64')
url="http://www.oracle.com/technology/software/products/berkeley-db/index.html"
license=('custom')
depends=('gcc-libs' 'sh')
options=('!libtool')
install=db.install
source=(http://download.oracle.com/berkeley-db/db-${pkgver}.tar.gz)
sha1sums=('03155e4ebdb6572a3cc684965f2ae307ac75a458')

build() {
  cd $pkgname-${pkgver}/build_unix
  ../dist/configure --prefix=/usr \
    --enable-compat185 \
    --enable-shared \
    --enable-static \
    --enable-cxx \
    --enable-dbm
  make LIBSO_LIBS=-lpthread
}

package() {
  cd $pkgname-${pkgver}/build_unix
  make DESTDIR="${pkgdir}" install
  rm -r "${pkgdir}"/usr/docs
  install -Dm644 "${srcdir}"/${pkgname}-${pkgver}/LICENSE \
    "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
