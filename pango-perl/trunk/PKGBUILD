# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=pango-perl
pkgver=1.227
pkgrel=9
pkgdesc="Perl bindings for Pango"
arch=(x86_64)
license=('LGPL')
url="http://gtk2-perl.sourceforge.net/"
makedepends=('perl-extutils-pkgconfig' 'perl-extutils-depends')
depends=('pango' 'glib-perl' 'cairo-perl')
options=('!emptydirs')
source=(https://downloads.sourceforge.net/sourceforge/gtk2-perl/Pango-${pkgver}.tar.gz)
sha256sums=('34b0a422df3fecd7597587048552457d48ae764c43bbefd2a9d62ceb6c8bac71')

build() {
  cd Pango-$pkgver
  export PERL_USE_UNSAFE_INC=1
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd Pango-$pkgver
  make install DESTDIR="${pkgdir}"
}
