# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Nick B <Shirakawasuna at gmail _dot_com>

pkgname=avogadro
pkgver=1.0.1
pkgrel=6
pkgdesc="An advanced molecular editor based on Qt"
arch=('i686' 'x86_64')
url="http://avogadro.openmolecules.net/wiki/Main_Page"
license=('GPL2')
depends=('eigen' 'openbabel' 'pyqt' 'boost-libs' 'glew' 'python2-numpy')
makedepends=('cmake' 'boost')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.bz2"
        'sip-4.11.patch'
        'fix-pycapsule.patch')
md5sums=('0d5c391197101f0aab7be6b59f81e6fd'
        'e78c3f96d0dc702947f75de1c094f580'
        '45b0f3be5913575311e92b74a5520966')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -Np1 -i ${srcdir}/sip-4.11.patch
  patch -Np1 -i ${srcdir}/fix-pycapsule.patch

  cd ${srcdir}
  mkdir build
  cd build

  # use the cmake module in system to enable python 2.7 support
  rm ../${pkgname}-${pkgver}/cmake/modules/FindPythonLibs.cmake

  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install

  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/cmake/modules/FindAvogadro.cmake \
    ${pkgdir}/usr/share/cmake-2.8/Modules/FindAvogadro.cmake
}
