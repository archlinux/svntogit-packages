# $Id$
# Maintainer: Paul Mattal <paul@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=dnsmasq
pkgver=2.42
pkgrel=1
pkgdesc="A lightweight DNS/DHCP server ideal for small/home networks using NAT"
arch=(i686 x86_64)
source=(http://www.thekelleys.org.uk/$pkgname/$pkgname-$pkgver.tar.gz \
    dnsmasq.rc.d $pkgname-capability-config.patch)
license=('GPL')
url="http://www.thekelleys.org.uk/dnsmasq/doc.html"
depends=('glibc' 'bash')
backup=(etc/dnsmasq.conf)
md5sums=('78a23a31a35b8427a8589b76c1ae3ff6' 'e956eee1ec24212a3254d2b168187495'\
         'ef61cd6c3ce1fd0767a98b02975c9c9c')
install=$pkgname.install

build() {
  cd $startdir/src/$pkgname-$pkgver || return 1
  # patch to add a note about capability module in the config file
  patch -Np0 -i ../$pkgname-capability-config.patch || return 1

  # build
  cd $startdir/src/$pkgname-$pkgver || return 1
  make || return 1

  # install
  mkdir -p $startdir/pkg/etc/rc.d || return 1
  make PREFIX=/usr DESTDIR=$startdir/pkg install || return 1
  install -D -m 0755 \
    $startdir/src/dnsmasq.rc.d $startdir/pkg/etc/rc.d/dnsmasq || return 1
  install -D -m 0644 \
    dnsmasq.conf.example $startdir/pkg/etc/dnsmasq.conf || return 1

  # make the required directories for DHCP server default directories
  mkdir -p $startdir/pkg/var/db || return 1
  mkdir -p $startdir/pkg/var/lib/misc || return 1
}
# vim: ts=2 sw=2 et ft=sh
