# $Id$
# Maintainer: Dave Reisner <dreisner@archlinux.org>
# Contributor: Paul Mattal <paul@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=dnsmasq
pkgver=2.68
pkgrel=1
pkgdesc="Lightweight, easy to configure DNS forwarder and DHCP server"
url="http://www.thekelleys.org.uk/dnsmasq/doc.html"
arch=('i686' 'x86_64')
license=('GPL')
depends=('glibc' 'libdbus')
install=$pkgname.install
backup=('etc/dnsmasq.conf')
source=("http://www.thekelleys.org.uk/$pkgname/$pkgname-$pkgver.tar.xz"
        'dnsmasq.service')
md5sums=('fec885ba0a9df0f7d8f2e44db48f19ae'
         '7ac45726cabef4145db40d758cc7fedf')

prepare() {
  cd "$pkgname-$pkgver"

  # link against dbus. this ugliness is needed to ensure that the
  # compile time opts report properly on startup. yuck.
  sed -i '/^#ifdef DNSMASQ_COMPILE_OPTS/ i#define HAVE_DBUS' src/config.h
}

build() {
  cd "$pkgname-$pkgver"

  make "CFLAGS=$CPPFLAGS $CFLAGS" "LDFLAGS=$LDFLAGS"
}

package() {
  cd "$pkgname-$pkgver"

  make BINDIR=/usr/bin PREFIX=/usr DESTDIR="$pkgdir" install

  install -Dm644 "dbus/dnsmasq.conf" "$pkgdir"/etc/dbus-1/system.d/dnsmasq.conf
  install -Dm644 "dnsmasq.conf.example" "$pkgdir"/etc/dnsmasq.conf
  install -Dm644 "$srcdir/dnsmasq.service" "$pkgdir"/usr/lib/systemd/system/dnsmasq.service
}

# vim: ts=2 sw=2 et ft=sh
