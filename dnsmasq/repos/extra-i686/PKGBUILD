# $Id$
# Maintainer: Dave Reisner <dreisner@archlinux.org>
# Contributor: Paul Mattal <paul@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=dnsmasq
pkgver=2.67
pkgrel=2
pkgdesc="Lightweight, easy to configure DNS forwarder and DHCP server"
url="http://www.thekelleys.org.uk/dnsmasq/doc.html"
arch=('i686' 'x86_64')
license=('GPL')
depends=('glibc' 'dbus-core')
install=$pkgname.install
backup=('etc/dnsmasq.conf')
source=("http://www.thekelleys.org.uk/$pkgname/$pkgname-$pkgver.tar.xz"
        '0001-Fix-check-for-local-domains-in-CNAME-case.-Fixes-d56.patch'
        'dnsmasq.service')
md5sums=('3560068c6cc644a01924fa089a70bb9c'
         '38bcc0c2b8dbc480e58a3478cae34bf6'
         '7ac45726cabef4145db40d758cc7fedf')

prepare() {
  cd "$pkgname-$pkgver"

  # link against dbus. this ugliness is needed to ensure that the
  # compile time opts report properly on startup. yuck.
  sed -i '/^#ifdef DNSMASQ_COMPILE_OPTS/ i#define HAVE_DBUS' src/config.h

  patch -Np1 <"$srcdir/0001-Fix-check-for-local-domains-in-CNAME-case.-Fixes-d56.patch"
}

build() {
  cd "$pkgname-$pkgver"

  make "CFLAGS=$CPPFLAGS $CFLAGS" "LDFLAGS=$LDFLAGS"
}

package() {
  cd "$pkgname-$pkgver"

  make BINDIR=/usr/bin PREFIX=/usr DESTDIR="$pkgdir" install

  install -Dm644 "dbus/dnsmasq.conf" "$pkgdir"/etc/dbus-1/system.d/dnsmasq.conf
  install -Dm644 "dnsmasq.conf.example" "$pkgdir"/etc/dnsmasq.conf
  install -Dm644 "$srcdir/dnsmasq.service" "$pkgdir"/usr/lib/systemd/system/dnsmasq.service
}

# vim: ts=2 sw=2 et ft=sh
