# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgbase="cups"
pkgname=('libcups' 'cups')
pkgver=1.4.4
pkgrel=1
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.cups.org/"
makedepends=('libtiff>=3.9.2-2' 'libpng>=1.4.0'  'acl' 'openslp' 'pam' 'xdg-utils' 'heimdal>=1.3.1' 'gnutls>=2.8.3' 'poppler>=0.12.3'
             'xinetd' 'gzip' 'autoconf' 'php' 'libusb' 'dbus-core' 'avahi'  'hicolor-icon-theme')
source=(ftp://ftp.easysw.com/pub/cups/${pkgver}/cups-${pkgver}-source.tar.bz2
	cups-avahi.patch
        cups cups.logrotate cups.pam)
#options=('!emptydirs')
md5sums=('8776403ad60fea9e85eab9c04d88560d'
         '3388dbe3fc20f16f37912e60a8ec46bc'
         '5c85b7d8d2ddd02c2c64955cebbf55ea'
         'f861b18f4446c43918c8643dcbbd7f6d'
         '96f82c38f3f540b53f3e5144900acf17')

build() {
  cd ${srcdir}/${pkgbase}-${pkgver}
  # Avahi support in the dnssd backend. patch from Fedora
  patch -Np1 -i ${srcdir}/cups-avahi.patch || return 1
  # Rebuild configure script for --enable-avahi.
  aclocal -I config-scripts
  autoconf -I config-scripts
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
     --with-logdir=/var/log/cups -with-docdir=/usr/share/cups/doc \
     --with-cups-user=daemon --with-cups-group=lp --enable-pam=yes \
     --disable-ldap --libdir=/usr/lib --enable-raw-printing \
     --enable-dbus --with-dbusdir=/etc/dbus-1 --enable-ssl=yes --enable-gnutls --disable-threads --enable-dnssd\
     --with-php=/usr/bin/php-cgi --with-pdftops=pdftops --with-optim="$CFLAGS"
  make || return 1
}

package_libcups() {
pkgdesc="The CUPS Printing System - client libraries and headers"
depends=('gnutls>=2.8.3' 'libtiff>=3.9.2-2' 'libpng>=1.4.0' 'heimdal>=1.3.1')
optdepends=('avahi: for the "dnssd" CUPS backend')


  cd ${srcdir}/${pkgbase}-${pkgver}
  make BUILDROOT=${pkgdir} install-headers install-libs || return 1
  # put this into the libs pkg to make other software find the libs(no pkg-config file included)
  mkdir -p ${pkgdir}/usr/bin
  install -m755 ${srcdir}/${pkgbase}-${pkgver}/cups-config ${pkgdir}/usr/bin/cups-config
}

package_cups() {
pkgdesc="The CUPS Printing System - deamon package"
install=cups.install
backup=(etc/cups/cupsd.conf
        etc/cups/mime.convs
        etc/cups/mime.types
        etc/cups/snmp.conf
        etc/cups/printers.conf
        etc/cups/classes.conf
        etc/cups/client.conf
        etc/cups/subscriptions.conf
        etc/dbus-1/system.d/cups.conf
        etc/logrotate.d/cups
        etc/pam.d/cups
        etc/xinetd.d/cups-lpd)
depends=('acl' 'openslp' 'pam' "libcups>=${pkgver}" 'xdg-utils' 'poppler>=0.12.3' 'libusb' 'dbus-core' 'hicolor-icon-theme')
optdepends=('php: for included phpcups.so module')

  cd ${srcdir}/${pkgbase}-${pkgver}
  make BUILDROOT=${pkgdir} install-data install-exec || return 1

  # this one we ship in the libcups pkg
  rm -f ${pkgdir}/usr/bin/cups-config

  # kill the sysv stuff
  rm -rf ${pkgdir}/etc/rc*.d
  rm -rf ${pkgdir}/etc/init.d
  install -D -m755 ../cups ${pkgdir}/etc/rc.d/cups
  install -D -m644 ../cups.logrotate ${pkgdir}/etc/logrotate.d/cups
  install -D -m644 ../cups.pam ${pkgdir}/etc/pam.d/cups
  
  # fix perms on /var/spool and /etc
  chmod 755 ${pkgdir}/var/spool
  chmod 755 ${pkgdir}/etc

  # install ssl directory where to store the certs, solves some samba issues
  install -dm700 -g lp ${pkgdir}/etc/cups/ssl
  install -dm511 -g lp ${pkgdir}/var/run/cups/certs 

  # install some more configuration files that will get filled by cupsd
  touch ${pkgdir}/etc/cups/printers.conf
  touch ${pkgdir}/etc/cups/classes.conf
  touch ${pkgdir}/etc/cups/client.conf
  echo "# see 'man client.conf'" >> ${pkgdir}/etc/cups/client.conf
  echo "ServerName /var/run/cups/cups.sock #  alternative: ServerName hostname-or-ip-address[:port] of a remote server" >> ${pkgdir}/etc/cups/client.conf
  touch ${pkgdir}/etc/cups/subscriptions.conf 
  chgrp lp ${pkgdir}/etc/cups/{printers.conf,classes.conf,client.conf,subscriptions.conf}
  
  # fix .desktop file
  sed -i 's|^Exec=htmlview http://localhost:631/|Exec=xdg-open http://localhost:631/|g' ${pkgdir}/usr/share/applications/cups.desktop
  
  # compress some driver files, adopted from Fedora
  find ${pkgdir}/usr/share/cups/model -name "*.ppd" | xargs gzip -n9f
}
