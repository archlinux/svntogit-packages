# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=md4c
pkgver=0.4.3
pkgrel=1
pkgdesc="C Markdown parser"
arch=(x86_64)
url="https://github.com/mity/md4c"
license=(MIT)
depends=(glibc)
makedepends=(cmake)
source=($pkgname-$pkgver.tar.gz::"https://github.com/mity/md4c/archive/release-$pkgver.tar.gz")
sha256sums=('3486c2bd3fd64a8237f7835d9d1c60ea8a362f1910cb1c64f001971220476a4a')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-release-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir"/$pkgname-release-$pkgver/LICENSE.md -t "$pkgdir"/usr/share/licenses/$pkgname/
}
