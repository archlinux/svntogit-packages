# $Id$
#
# Contributor: Paul Mattal <paul@mattal.com>
# Contributor: Federico Quagliata (quaqo) <quaqo@despammed.com>
# Contributor: GARETTE Emmanuel <gnunux at laposte dot net>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=dovecot
pkgver=1.1.4
pkgrel=1
pkgdesc="An IMAP and POP3 server written with security primarily in mind"
arch=('i686' 'x86_64')
url="http://dovecot.org/"
license=("LGPL")
depends=('pam>=1.0.1-2' 'libmysqlclient>=5.0.60' 'postgresql-libs>=8.3.3' \
	 'heimdal>=1.2' 'libcap' 'bzip2')
makedepends=()
provides=('imap-server' 'pop3-server')
replaces=()
options=('!libtool')
install=$pkgname.install
source=(http://dovecot.org/releases/1.1/$pkgname-$pkgver.tar.gz dovecot.sh)
md5sums=('0050dd609cb456c8e52565a85373df28'
         'd020d43eab4ded6fb379dadc570a9490')

build() {
  cd $startdir/src/$pkgname-$pkgver || return 1

  # configure with openssl, mysql, and postgresql support
  # dont relocate /usr/libexec to /usr/lib because it causes filename conflicts
  ./configure --prefix=/usr --sysconfdir=/etc/dovecot --with-mysql \
  	--with-pgsql --with-ssl=openssl --with-ssldir=/etc/dovecot/ssl \
	--with-gssapi --enable-header-install || return 1

  # build
  make || return 1

  # install
  make DESTDIR=$startdir/pkg install || return 1

  # remove empty directories for cleanliness
  # /usr/share will usually end up empty except for documentation, but we
  # should not rely on that to remain true, so we do it this way
  rm -rf $startdir/pkg/usr/share/doc || return 1
  rmdir $startdir/pkg/usr/share || return 1

  # install the launch script
  install -D -m755 $startdir/src/$pkgname.sh $startdir/pkg/etc/rc.d/$pkgname \
  	|| return 1
}
