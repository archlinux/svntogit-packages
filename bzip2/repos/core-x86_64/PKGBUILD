# $Id$
# Maintainer:
# Contributor: Judd <jvinet@zeroflux.org> 

pkgname=bzip2
pkgver=1.0.5
pkgrel=5
pkgdesc="A high-quality data compression program"
arch=('i686' 'x86_64')
license=('custom')
url="http://sources.redhat.com/bzip2"
groups=('base')
depends=('glibc')
source=(http://www.bzip.org/$pkgver/bzip2-$pkgver.tar.gz)
md5sums=('3c15a0c8d1d3ee1c46a1634d00617b1a')

build() {
  cd $srcdir/$pkgname-$pkgver
  
  # add large-file support
  sed -e 's/^CFLAGS=\(.*\)$/CFLAGS=\1 \$(BIGFILES)/' -i ./Makefile-libbz2_so
  
  # use our optimization
  sed -i "s|-O2|${CFLAGS}|g" Makefile
  sed -i "s|-O2|${CFLAGS}|g" Makefile-libbz2_so
  
  make -f Makefile-libbz2_so || return 1
  make bzip2recover libbz2.a || return 1

  install -dm755 $pkgdir/bin
  install -m755 bzip2-shared $pkgdir/bin/bzip2
  install -m755 bzip2recover $pkgdir/bin
  ln -sf bzip2 $pkgdir/bin/bunzip2
  ln -sf bzip2 $pkgdir/bin/bzcat

  install -dm755 $pkgdir/usr/bin
  install -m755 bzdiff bzgrep bzmore $pkgdir/usr/bin

  install -Dm755 libbz2.so.1.0.4 $pkgdir/lib/libbz2.so.1.0.4
  ln -s libbz2.so.1.0.4 $pkgdir/lib/libbz2.so
  ln -s libbz2.so.1.0.4 $pkgdir/lib/libbz2.so.1
  ln -s libbz2.so.1.0.4 $pkgdir/lib/libbz2.so.1.0
  
  install -Dm644 libbz2.a ${pkgdir}/usr/lib/libbz2.a
  ln -sf ../../lib/libbz2.so ${pkgdir}/usr/lib/libbz2.so

  install -Dm644 bzlib.h $pkgdir/usr/include/bzlib.h  

  install -Dm644 bzip2.1 $pkgdir/usr/share/man/man1/bzip2.1
  ln -sf bzip2.1 $pkgdir/usr/share/man/man1/bunzip2.1
  ln -sf bzip2.1 $pkgdir/usr/share/man/man1/bzcat.1
  ln -sf bzip2.1 $pkgdir/usr/share/man/man1/bzip2recover.1

  install -Dm644 $srcdir/${pkgname}-${pkgver}/LICENSE \
                 $pkgdir/usr/share/licenses/${pkgname}/LICENSE
}

