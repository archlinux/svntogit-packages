# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>

pkgname=glade
pkgver=3.38.1
pkgrel=2
pkgdesc="User Interface Builder for GTK+ applications"
url="https://glade.gnome.org/"
arch=(x86_64)
license=(GPL LGPL)
depends=(gtk3 libxml2)
makedepends=(gtk-doc gobject-introspection python-gobject itstool docbook-xsl
             git webkit2gtk meson gjs)
checkdepends=(xorg-server-xvfb)
optdepends=('devhelp: development help')
groups=(gnome-extra)
provides=(libgladeui-2.so)
_commit=9b3f55387f03920e1bf1cea9b7e9c911421cbb3e  # tags/GLADE_3_38_1^0
source=("git+https://gitlab.gnome.org/GNOME/glade.git#commit=$_commit"
        python-dep.diff)
sha256sums=('SKIP'
            'dc1ed48403d473fcacef37fc6dd693e0442e7fed31bb852b20957c71595e2124')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^GLADE_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd $pkgname

  # Fix build with Python 3.9
  git apply -3 ../python-dep.diff
}

build() {
  arch-meson $pkgname build -D gtk_doc=true
  meson compile -C build
}

check() {
  # Requires Glade to be installed to pass
  # GladeUI-FATAL-CRITICAL: Unable to load image (Failed to open file "/usr/share/glade/pixmaps/plus.png": No such file or directory)
  dbus-run-session xvfb-run \
    -s '-screen 0 1920x1080x24 -nolisten local' \
    meson test -C build --print-errorlogs || :
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
