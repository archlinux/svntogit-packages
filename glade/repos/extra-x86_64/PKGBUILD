# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>

pkgname=glade
pkgver=3.40.0
pkgrel=1
pkgdesc="User Interface Builder for GTK+ applications"
url="https://glade.gnome.org/"
arch=(x86_64)
license=(GPL LGPL)
depends=(gtk3 libxml2)
makedepends=(gtk-doc gobject-introspection python-gobject itstool docbook-xsl
             git webkit2gtk meson gjs)
checkdepends=(xorg-server-xvfb)
optdepends=('devhelp: development help')
provides=(libgladeui-2.so)
groups=(gnome-extra)
options=(debug)
_commit=b3823efd0acea2b0a5629b261cf56b1d3f2664aa  # tags/GLADE_3_40_0^0
source=("git+https://gitlab.gnome.org/GNOME/glade.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd glade
  git describe --tags | sed 's/^GLADE_//;s/_/./g;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd glade
}

build() {
  arch-meson glade build -D gtk_doc=true
  meson compile -C build
}

check() {
  dbus-run-session xvfb-run -s '-nolisten local' \
    meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
