# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>

pkgname=glade
pkgver=3.20.1+6+gcac71ec3
pkgrel=1
pkgdesc="User Interface Builder for GTK+ applications"
url="https://glade.gnome.org/"
arch=(i686 x86_64)
license=(GPL LGPL)
depends=(gtk3 libxml2)
makedepends=(intltool gtk-doc gobject-introspection python-gobject itstool docbook-xsl git
             gnome-common)
optdepends=('python: Python widgets support'
            'devhelp: help browser')
_commit=cac71ec3b049057586359f605c83ab93b8628480  # glade-3-20
source=("git+https://git.gnome.org/browse/glade#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^GLADE_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --enable-gtk-doc
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
