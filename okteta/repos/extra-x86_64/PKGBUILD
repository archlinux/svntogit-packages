# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=okteta
pkgver=0.26.7
pkgrel=1
epoch=1
pkgdesc='KDE hex editor for viewing and editing the raw data of files'
url='https://kde.org/applications/utilities/okteta/'
arch=(x86_64)
license=(GPL LGPL FDL)
depends=(kcmutils kparts knewstuff qca qt5-script hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools qt5-tools)
source=(https://download.kde.org/stable/$pkgname/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('f123b55690d6cf5f547e9a5db738a26729a82e7bd02cc0002238ce4d9fd530e3'
            'SKIP')
validpgpkeys=(E191FD5BE6F46870F09E82B2024E7FB43D015474  # Friedrich W. H. Kossebau <kossebau@kde.org>
              0A48BC961075B4BA8523E3790A345FB086E797D9  # https://share.kde.org/s/8dKaJTw8pR8iiwJ
              E6EBD6EB0518FE5BCCA5F6A76AACDD263C2CEFD4) # https://collaborate.kde.org/s/DsLy35eqG6qH7MJ
options=(debug)

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
