# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=okteta
pkgver=15.12.0
pkgrel=1
pkgdesc='Hex Editor'
url='http://kde.org/applications/utilities/okteta/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdesdk')
depends=('kcmutils' 'kparts' 'knewstuff' 'qca-qt5' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'kdoctools' 'python' 'qt5-tools')
replaces=('kdesdk-okteta')
conflicts=('kdesdk-okteta<4.14.3-2')
install=${pkgname}.install
source=("http://download.kde.org/stable/applications/${pkgver}/src/okteta-${pkgver}.tar.xz")
sha1sums=('c3bc9f0d3b79b56372e9985d9c6d6dabcff67b81')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../okteta-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DSYSCONF_INSTALL_DIR=/etc \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
