# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=okteta
pkgver=16.12.2
pkgrel=1
pkgdesc='Hex Editor'
url='http://kde.org/applications/utilities/okteta/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdesdk')
depends=('kcmutils' 'kparts' 'knewstuff' 'qca-qt5' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'kdoctools' 'python' 'qt5-tools')
replaces=('kdesdk-okteta')
conflicts=('kdesdk-okteta<4.14.3-2')
source=("http://download.kde.org/stable/applications/${pkgver}/src/okteta-${pkgver}.tar.xz"{,.sig})
sha1sums=('a267389088576ee515c8622ea6759c30431b9177'
          'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../okteta-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
