# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=keychain
pkgver=2.7.1
pkgrel=4
pkgdesc="A front-end to ssh-agent, allowing one long-running ssh-agent process per system, rather than per login"
arch=('any')
url="http://www.funtoo.org/Keychain"
license=('GPL2')
depends=('sh')
source=(http://www.funtoo.org/archive/keychain/${pkgname}-${pkgver}.tar.bz2
	keychain-openssh68.patch)
sha1sums=('e7ad7da374ba81e57792bb2695eb6c352f769de7'
          '339005a006e70f2bb52d19f1f61dc0e6fb1f59bc')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i "${srcdir}/keychain-openssh68.patch"
}

build() {
  cd ${pkgname}-${pkgver}
  make
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm755 keychain "${pkgdir}/usr/bin/keychain"
  install -Dm644 keychain.1 "${pkgdir}/usr/share/man/man1/keychain.1"
}
