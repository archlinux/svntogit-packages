# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Firmicus <francois.archlinux.org>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: <AttilaHÂ©web.de>

pkgname=ttf-linux-libertine
pkgver=5.3.0
_reldate=2012_07_02
pkgrel=3
url='http://linuxlibertine.sourceforge.net/'
pkgdesc='Serif (Libertine) and Sans Serif (Biolinum) OpenType fonts with large Unicode coverage'
license=('GPL' 'custom:OFL')
arch=('any')
depends=('fontconfig' 'xorg-fonts-encodings' 'xorg-font-utils')
provides=('ttf-font')
source=("http://downloads.sourceforge.net/project/linuxlibertine/linuxlibertine/${pkgver}/LinLibertine"{TTF,OTF}"_${pkgver}_${_reldate}.tgz")
sha256sums=('24a593a949808d976850131a953c0c0d7a72299531dfbb348191964cc038d75d'
            '3eb84d13e59d390a3f0b15f93bbb590de693dd5f29c4e8083317dbbadf6fb985')

install=install

package() {
	cd "${srcdir}"
	install -dm755 "${pkgdir}"/usr/share/fonts/{OTF,TTF}
	install -m644 *.ttf "${pkgdir}"/usr/share/fonts/TTF
	install -m644 *.otf "${pkgdir}"/usr/share/fonts/OTF

	install -Dm644 OFL-1.1.txt "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
	install -Dm644 LICENCE.txt "${pkgdir}"/usr/share/licenses/"${pkgname}"/README
}
