# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=strigi
pkgver=0.7.8
pkgrel=9
pkgdesc="Fast crawling desktop search engine with Qt GUI"
arch=('i686' 'x86_64')
url='http://strigi.sourceforge.net/'
license=('GPL2')
depends=('dbus' 'exiv2' 'libxml2' 'bzip2')
makedepends=('cmake')
optdepends=('kdegraphics-strigi-analyzer: strigi analyzers for various graphics file'
            'kdesdk-strigi-analyzers: strigi analyzers for KDE SDK')
source=("http://www.vandenoever.info/software/${pkgname}/${pkgname}-${pkgver}.tar.bz2"
        'gcc48.patch')
md5sums=('d69443234f4286d71997db9de543331a'
         'd48d65014650644ba61821813101c2f4')

prepare() {
  mkdir build
  
  cd ${pkgname}-${pkgver}/libstreams
  patch -p1 -i "${srcdir}"/gcc48.patch
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DENABLE_INOTIFY=ON \
    -DENABLE_LOG4CXX=OFF \
    -DENABLE_FAM=OFF \
    -DENABLE_CLUCENE=OFF \
    -DENABLE_CLUCENE_NG=OFF \
    -DENABLE_FFMPEG=OFF \
    -DENABLE_QT4=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
