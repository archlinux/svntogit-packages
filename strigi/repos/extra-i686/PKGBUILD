# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=strigi
pkgver=0.7.5
pkgrel=3
pkgdesc="Fast crawling desktop search engine with Qt4 GUI"
arch=('i686' 'x86_64')
url="http://www.vandenoever.info/software/strigi/"
license=('GPL2')
depends=('bzip2' 'exiv2' 'libxml2' 'boost-libs')
makedepends=('qt' 'cmake' 'pkg-config' 'boost')
source=("http://www.vandenoever.info/software/${pkgname}/${pkgname}-${pkgver}.tar.bz2")
md5sums=('0559e2ab65d187d30916e9ffe36e0fb6')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_INOTIFY=ON \
    -DENABLE_LOG4CXX=OFF \
    -DENABLE_FAM=OFF \
    -DENABLE_CLUCENE=OFF \
    -DENABLE_CLUCENE_NG=OFF \
    -DENABLE_FFMPEG=OFF \
    -DENABLE_XINE=OFF
  make
}

package() {
  cd "${srcdir}/build/libstreams"
  make DESTDIR="${pkgdir}" install

  cd "${srcdir}/build/libstreamanalyzer"
  make DESTDIR="${pkgdir}" install
  
  cd "${srcdir}/build/strigiutils"
  make DESTDIR="${pkgdir}" install
}
