# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=strigi
pkgver=0.7.7
pkgrel=2
pkgdesc="Fast crawling desktop search engine with Qt4 GUI"
arch=('i686' 'x86_64')
url='http://strigi.sourceforge.net/'
license=('GPL2')
depends=('exiv2' 'libxml2' 'boost-libs')
makedepends=('qt' 'cmake' 'pkg-config' 'boost')
optdepends=('kdegraphics-strigi-analyzer: strigi analyzers for various graphics file'
            'kdesdk-strigi-analyzer: strigi analyzer for KDE SDK')
#source=("http://www.vandenoever.info/software/${pkgname}/${pkgname}-${pkgver}.tar.bz2")
source=("ftp://ftp.archlinux.org/other/${pkgname}/${pkgname}-${pkgver}.tar.bz2"
        'gcc47.patch')
md5sums=('ca0a0fd5c2b99879f6330837aeede996'
         '1acb9b31a4859dc427fc4d387fd8aa2f')

build() {
  pushd ${pkgname}-${pkgver}/strigidaemon
  patch -p1 -i "${srcdir}"/gcc47.patch
  popd

  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_INOTIFY=ON \
    -DENABLE_LOG4CXX=OFF \
    -DENABLE_FAM=OFF \
    -DENABLE_CLUCENE=OFF \
    -DENABLE_CLUCENE_NG=OFF \
    -DENABLE_FFMPEG=OFF
  make
}

package() {
  pushd build/libstreams
  make DESTDIR="${pkgdir}" install
  popd

  pushd build/libstreamanalyzer
  make DESTDIR="${pkgdir}" install
  popd

  pushd build/strigiclient/lib/searchclient/qtdbus
  make DESTDIR="${pkgdir}" install
  popd
  
  pushd build/strigiutils
  make DESTDIR="${pkgdir}" install
  popd
}
