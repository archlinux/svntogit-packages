# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=strigi
pkgver=0.7.6
pkgrel=2
pkgdesc="Fast crawling desktop search engine with Qt4 GUI"
arch=('i686' 'x86_64')
url="http://www.vandenoever.info/software/strigi/"
license=('GPL2')
depends=('bzip2' 'exiv2' 'libxml2' 'boost-libs')
makedepends=('qt' 'cmake' 'pkg-config' 'boost')
optdepends=('kdegraphics-strigi-analyzer: strigi analyzers for various graphics file'
            'kdesdk-strigi-analyzer: strigi analyzer for KDE SDK')
#source=("http://www.vandenoever.info/software/${pkgname}/${pkgname}-${pkgver}.tar.bz2")
source=("ftp://ftp.archlinux.org/other/${pkgname}/${pkgname}-${pkgver}.tar.bz2"
        'libstreamanalyzer_pdf.patch')
md5sums=('41cddd39e4ae3f590d82c859a14e9261'
         '3e722a93cf5c6238e625ca58463afac2')

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}/libstreamanalyzer
  patch -p1 -i "${srcdir}"/libstreamanalyzer_pdf.patch

  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_INOTIFY=ON \
    -DENABLE_LOG4CXX=OFF \
    -DENABLE_FAM=OFF \
    -DENABLE_CLUCENE=OFF \
    -DENABLE_CLUCENE_NG=OFF \
    -DENABLE_FFMPEG=OFF
  make
}

package() {
  cd "${srcdir}/build/libstreams"
  make DESTDIR="${pkgdir}" install

  cd "${srcdir}/build/libstreamanalyzer"
  make DESTDIR="${pkgdir}" install

  cd "${srcdir}/build/strigiclient/lib/searchclient/qtdbus"
  make DESTDIR="${pkgdir}" install
  
  cd "${srcdir}/build/strigiutils"
  make DESTDIR="${pkgdir}" install
}
