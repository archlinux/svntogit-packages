# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdesdk-thumbnailers
pkgver=4.12.4
pkgrel=1
pkgdesc='Plugins for the thumbnailing system'
url='https://projects.kde.org/projects/kde/kdesdk/kdesdk-thumbnailers'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdesdk')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
source=("http://download.kde.org/stable/${pkgver}/src/kdesdk-thumbnailers-${pkgver}.tar.xz")
sha1sums=('47416ab0a631963c1f3143829b330254a0160b9f')

build() {
  mkdir build
  cd build
  cmake ../kdesdk-thumbnailers-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
