# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=five-or-more
pkgver=3.32.0
pkgrel=1
pkgdesc="Remove colored balls from the board by forming lines"
url="https://wiki.gnome.org/Apps/Five%20or%20more"
arch=(x86_64)
license=(GPL)
depends=(gtk3 librsvg libgnome-games-support)
makedepends=(gobject-introspection yelp-tools appstream-glib git meson vala)
groups=(gnome-extra)
_commit=49cb447c5f62e5274c60731bf11ce078bb5f46bc  # tags/3.32.0^0
source=("git+https://gitlab.gnome.org/GNOME/five-or-more.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  meson test -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
