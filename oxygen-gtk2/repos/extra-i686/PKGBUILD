# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: birdflesh <antkoul at gmail dot com>

pkgname=oxygen-gtk2
pkgver=1.4.0
pkgrel=1
pkgdesc="Port of the default KDE widget theme (Oxygen) to GTK2"
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/playground/artwork/oxygen-gtk/'
license=('LGPL')
depends=('gtk2')
conflicts=('oxygen-gtk')
replaces=('oxygen-gtk')
makedepends=('cmake')
source=("http://download.kde.org/stable/${pkgname}/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2")
md5sums=('ccc9e468a5ea04159ca2040ee3f434e1')

build() {
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
