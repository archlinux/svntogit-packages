# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
pkgname=js
pkgver=1.8.5
pkgrel=2
pkgdesc="JavaScript interpreter and libraries"
arch=(i686 x86_64)
url="https://developer.mozilla.org/En/SpiderMonkey/1.8.5"
license=('GPL2')
depends=('nspr' 'gcc-libs')
makedepends=('python2' 'zip')
replaces=('spidermonkey')
conflicts=('spidermonkey')
source=(http://ftp.mozilla.org/pub/mozilla.org/js/js185-1.0.0.tar.gz
        js185-destdir.patch)
md5sums=('a4574365938222adca0a6bd33329cb32'
         '364834a8391888642c53d78c3a949d94')
build() {
  cd "$srcdir/$pkgname-$pkgver/js/src"

  patch -Np0 -i $srcdir/js185-destdir.patch

  export CXXFLAGS="$CXXFLAGS -DJS_C_STRINGS_ARE_UTF8"

  ./configure --prefix=/usr --with-system-nspr \
     --enable-threadsafe

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver/js/src"
  make DESTDIR="$pkgdir" install

  install -m 0755 shell/js ${pkgdir}/usr/bin

  #cleanup
  rm -f "${pkgdir}/usr/lib/libmozjs185-1.0.a"
}

# vim:set ts=2 sw=2 et:
