# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-xload
pkgver=1.1.3
pkgrel=1
pkgdesc="System load average display for X"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libxaw' 'libxmu' 'libxt' 'libx11')
makedepends=('xorg-util-macros')
source=(https://xorg.freedesktop.org/archive/individual/app/xload-${pkgver}.tar.bz2{,.sig})
sha512sums=('8dbc4f0cee7b30d7381be819152b27653d9b0049266e59bde2e2c972a88c4f2bdea766fc407bbdfef15d0aab30fedc756189a1048170a812853133a5094fe7a5'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # "Alan Coopersmith <alan.coopersmith@oracle.com>"

build() {
  cd xload-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd xload-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
