# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: tardo <tardo@nagi-fanboi.net>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=mod_dnssd
pkgver=0.6
pkgrel=6
pkgdesc="Zeroconf module for Apache2"
arch=('i686' 'x86_64')
url="http://0pointer.de/lennart/projects/mod_dnssd"
license=('APACHE')
depends=('avahi' 'apache')
source=(http://0pointer.de/lennart/projects/$pkgname/${pkgname}-${pkgver}.tar.gz
        fix_undefined_reference.patch) # from upstream be2fb9f6158f80068
md5sums=('bed3d95a98168bf0515922d1c05020c5'
         'ea6269aa24f8a0eb2d1adb5dd160635a')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 < "${srcdir}/fix_undefined_reference.patch"
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --disable-lynx
  make
}

package() {
  cd ${pkgname}-${pkgver}
  install -D -m755 src/.libs/mod_dnssd.so "${pkgdir}/usr/lib/httpd/modules/mod_dnssd.so"
}
