# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-photos
pkgver=3.20.2
pkgrel=1
pkgdesc="Access, organize, and share your photos on GNOME"
arch=(i686 x86_64)
url="https://wiki.gnome.org/Apps/Photos"
license=(GPL)
depends=(exempi gtk3 libexif librsvg babl gegl gnome-desktop gnome-online-accounts lcms2 tracker
         grilo gfbgraph libgdata gnome-online-miners dleyna-server dleyna-renderer libgexiv2 libraw)
makedepends=(intltool itstool python)
groups=(gnome-extra)
source=(https://download.gnome.org/sources/${pkgname}/${pkgver:0:4}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('ec6b95ad1c4aeeb065a65d2d48335036c0750761c7f6762bafcf874791272b46')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/gnome-photos
  make
}

check() {
  cd $pkgname-$pkgver
  # disabled: needs dogtail which we don't have
  #make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
