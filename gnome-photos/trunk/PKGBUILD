# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-photos
pkgver=3.18.2
pkgrel=1
pkgdesc="Access, organize, and share your photos on GNOME"
arch=(i686 x86_64)
url="https://www.gnome.org/"
license=(GPL)
depends=(exempi gtk3 libexif librsvg babl gegl gnome-desktop gnome-online-accounts lcms2 tracker grilo gfbgraph libgdata gnome-online-miners dleyna-server dleyna-renderer)
makedepends=(intltool itstool)
groups=(gnome-extra)
install=gnome-photos.install
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/${pkgver:0:4}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('7f6169c663b7a0e1b971d5af4def3d9a633e16a24e7d2c593b51be0053f9a0d8')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/gnome-photos
  make
}

check() {
  cd $pkgname-$pkgver
# disabled: needs dogtail which we don't have
#  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
