# $Id$
# Maintainer:
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Charly Ghislain <charlyghislain@gmail.com>

pkgname=libmsn
pkgver=4.2.1
pkgrel=2
pkgdesc="A reusable, open-source, fully documented library for connecting to Microsoft's MSN"
url="http://sourceforge.net/projects/libmsn/"
arch=('i686' 'x86_64')
license=('GPL2')
depends=('openssl')
makedepends=('cmake')
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.bz2"
        'gcc47.patch')
md5sums=('38e46e589720eefd71f92b6b76993bf0'
         '2d202093a4434af14b79f02f3bc2c0fc')

prepare() {
  mkdir build

  cd ${pkgname}-${pkgver}
  patch -p1 -i "${srcdir}"/gcc47.patch
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
