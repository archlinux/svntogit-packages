# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdenetwork-krfb
pkgver=15.07.90
pkgrel=1
pkgdesc='Desktop Sharing'
url='http://www.kde.org/applications/system/krfb'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdenetwork')
# note on libxdamage:
#   not detected by namcap because libgl depends on it
#   but nvidia providing libgl does not depend on libxdamage
depends=('kdebase-runtime' 'libvncserver' 'libxdamage' 'telepathy-qt4')
makedepends=('cmake' 'automoc4')
source=("http://download.kde.org/unstable/applications/${pkgver}/src/krfb-${pkgver}.tar.xz")
sha1sums=('d14c6e1c978607de5215131fa115376cb9d51ca3')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../krfb-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
