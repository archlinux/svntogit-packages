# $Id$
# Maintainer : RÃ©my Oudompheng <remy@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>

pkgname=namcap
pkgver=3.2.7
pkgrel=3
pkgdesc="A Pacman package analyzer"
arch=('any')
url="http://projects.archlinux.org/namcap.git/"
license=('GPL')
depends=('python' 'pyalpm>=0.5' 'licenses'
         'binutils' 'elfutils' 'python-pyelftools')
makedepends=('python-setuptools')
source=(https://sources.archlinux.org/other/${pkgname}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('fedf490e57bcdb4b11c7bb0e1f1801791f860bbad927d1e384cfa4dbf1047e7cf591be5136f1cd7bf5d0fba9713b83e93fee87e7080d3332adf0210727f6c49a')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}
  sed -i 's/libalpm.so.10/libalpm.so.11/' Namcap/tests/package/test_sodepends.py
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  python setup.py build
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  env PARSE_PKGBUILD_PATH=${srcdir}/${pkgname}-${pkgver} \
      PATH=${srcdir}/${pkgname}-${pkgver}:$PATH \
      python setup.py test
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  python setup.py install --root=${pkgdir}
}

