# $Id$
# Maintainer: Firmicus <firmicus āt gmx dōt net>
# Maintainer: Rémy Oudompheng <remy@archlinux.org>

pkgname=texlive-music
pkgver=2015.38340
_revnr=${pkgver#2015.}
pkgrel=1
pkgdesc="TeX Live - Music typesetting packages"
license=('GPL')
arch=(any)
depends=('texlive-core')
optdepends=('python2: for scripts from the lilyglyphs packages')
groups=('texlive-most')
url='http://tug.org/texlive/'
source=("https://sources.archlinux.org/other/texlive/$pkgname-$pkgver-src.zip" "$pkgname.maps")
options=('!emptydirs')
install=texlive.install
md5sums=('122db9c7f8b55c3caa1f12647c426b3b'
         '6af96d016e18492867d6f4ba2eff0a31')

build() {
   for p in *.tar.xz; do
	   bsdtar -xf $p
   done
   rm -rf {tlpkg,doc,source} || true
}

package() {
   install -m755 -d $pkgdir/var/lib/texmf/arch/installedpkgs
   sed -i '/^#/d' CONTENTS
   install -m644 CONTENTS $pkgdir/var/lib/texmf/arch/installedpkgs/${pkgname}_${_revnr}.pkgs
   install -m644 $pkgname.maps $pkgdir/var/lib/texmf/arch/installedpkgs/
   install -m755 -d $pkgdir/usr/share
   wanteddirs=$(for d in *; do test -d $d && [[ $d != texmf* ]] && echo $d; done) || true
   for dir in $wanteddirs; do
     find $dir -type d -exec install -d -m755 $pkgdir/usr/share/texmf-dist/'{}' \;
     find $dir -type f -exec install -m644 '{}' $pkgdir/usr/share/texmf-dist/'{}' \;
   done
   if [[ -d texmf-dist ]]; then
     find texmf-dist -type d -exec install -d -m755 $pkgdir/usr/share/'{}' \;
     find texmf-dist -type f -exec install -m644 '{}' $pkgdir/usr/share/'{}' \;
   fi
   if [[ -d $pkgdir/usr/share/texmf-dist/scripts ]]; then
     find $pkgdir/usr/share/texmf-dist/scripts -type f -exec chmod a+x '{}' \;
   fi
   # fix python2 command name in scripts.
   sed -i 's/env python/env python2/' $pkgdir/usr/share/texmf-dist/scripts/lilyglyphs/*.py
   # script symlinks
   install -m755 -d $pkgdir/usr/bin
   ln -s /usr/share/texmf-dist/scripts/lilyglyphs/lily-glyph-commands.py $pkgdir/usr/bin/lily-glyph-commands
   ln -s /usr/share/texmf-dist/scripts/lilyglyphs/lily-image-commands.py $pkgdir/usr/bin/lily-image-commands
   ln -s /usr/share/texmf-dist/scripts/lilyglyphs/lily-rebuild-pdfs.py $pkgdir/usr/bin/lily-rebuild-pdfs
   ln -s /usr/share/texmf-dist/scripts/m-tx/m-tx.lua $pkgdir/usr/bin/m-tx
   ln -s /usr/share/texmf-dist/scripts/musixtex/musixflx.lua $pkgdir/usr/bin/musixflx
   ln -s /usr/share/texmf-dist/scripts/musixtex/musixtex.lua $pkgdir/usr/bin/musixtex
   ln -s /usr/share/texmf-dist/scripts/pmx/pmx2pdf.lua $pkgdir/usr/bin/pmx2pdf
   ln -s /usr/share/texmf-dist/scripts/pmxchords/pmxchords.lua $pkgdir/usr/bin/pmxchords
}
