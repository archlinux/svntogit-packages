# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gpaste
pkgver=3.16.3
pkgrel=1
pkgdesc="Clipboard management system"
url="http://www.imagination-land.org/tags/GPaste.html"
license=(GPL3)
arch=(i686 x86_64)
depends=(gtk3)
makedepends=(intltool vala appstream-glib gobject-introspection gnome-shell gnome-control-center)
optdepends=("wgetpaste: Upload clipboard contents")
options=('!emptydirs')
install=gpaste.install
source=("http://www.imagination-land.org/files/$pkgname/$pkgname-$pkgver.tar.xz")
sha256sums=('55e33922c828873b7ddee26380cac62a5855949cc88a91d273ac65146abd61e5')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
    --libexecdir=/usr/lib \
    --sysconfdir=/etc \
    --enable-vala
  make
}

check () {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  install -Dm644 data/completions/gpaste \
    "$pkgdir/usr/share/bash-completion/completions/gpaste"
  install -Dm644 data/completions/_gpaste \
    "$pkgdir/usr/share/zsh/site-functions/_gpaste"
}
