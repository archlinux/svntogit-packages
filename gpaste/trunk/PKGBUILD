# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=gpaste
pkgver=43.1
pkgrel=1
pkgdesc="Clipboard management system"
url="https://www.imagination-land.org/tags/GPaste.html"
license=(BSD)
arch=(x86_64)
depends=(gtk3 gtk4 libadwaita gcr-4)
makedepends=(vala appstream-glib gobject-introspection gnome-shell
             gnome-control-center git meson)
optdepends=("wgetpaste: Upload clipboard contents")
options=(debug)
_commit=298fcde2dc38e35f9ac8477d76d97f8f9e42fed7  # tags/v43.1^0
source=("git+https://github.com/keruspe/gpaste#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd gpaste
  git describe --tags | sed 's/^v//;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd gpaste
}

build() {
  arch-meson gpaste build -D gcr3=false
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 gpaste/COPYING
}

# vim:set sw=2 sts=-1 et:
