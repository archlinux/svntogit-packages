# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=gpaste
pkgver=3.42.2
pkgrel=2
pkgdesc="Clipboard management system"
url="https://www.imagination-land.org/tags/GPaste.html"
license=(BSD)
arch=(x86_64)
depends=(gtk3)
makedepends=(vala appstream-glib gobject-introspection gnome-shell
             gnome-control-center git meson)
optdepends=("wgetpaste: Upload clipboard contents")
_commit=84592d5c53d7c578b5f6a4444f72fa890f321f1c  # tags/v3.42.2^0
source=("git+https://github.com/keruspe/gpaste#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd gpaste
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd gpaste

  # https://bugs.archlinux.org/task/72849
  sed -i '3s/3\.42\.1/3.42.2/' meson.build
}

build() {
  arch-meson gpaste build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 gpaste/COPYING
}
