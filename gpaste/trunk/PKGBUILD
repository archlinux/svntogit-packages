# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gpaste
pkgver=3.22.2
pkgrel=1
pkgdesc="Clipboard management system"
url="http://www.imagination-land.org/tags/GPaste.html"
license=(GPL3)
arch=(i686 x86_64)
depends=(gtk3)
makedepends=(intltool vala appstream-glib gobject-introspection gnome-shell gnome-control-center git)
optdepends=("wgetpaste: Upload clipboard contents")
options=(!emptydirs)
_commit=f84c623ba49daeb3ffb8a0853f1716ff1f5ff28d  # tags/v3.22.2^0
source=("git+https://github.com/keruspe/gpaste#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --libexecdir=/usr/lib --sysconfdir=/etc \
    --enable-vala --enable-applet
  make
}

check () {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install

  install -Dm644 data/completions/gpaste-client \
    "$pkgdir/usr/share/bash-completion/completions/gpaste-client"
  install -Dm644 data/completions/_gpaste-client \
    "$pkgdir/usr/share/zsh/site-functions/_gpaste-client"

  # Don't autostart the applet, ever
  rm "$pkgdir/etc/xdg/autostart/org.gnome.GPaste.Applet.desktop"
}
