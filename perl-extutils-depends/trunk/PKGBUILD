# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Sarah Hay <sarah@archlinux.org>
# Contributor: Arjan Timmerman <arjan@soulfly.nl>

pkgname=perl-extutils-depends
pkgver=0.308
pkgrel=1
pkgdesc="The Perl depends module"
arch=('any')
url="http://search.cpan.org/dist/ExtUtils-Depends/"
license=('PerlArtistic')
depends=('perl')
options=('!emptydirs')
source=(http://www.cpan.org/authors/id/X/XA/XAOC/ExtUtils-Depends-${pkgver}.tar.gz)
md5sums=('cea7ebb99d05fe47c24a1de043b478f2')

build() {
  cd ExtUtils-Depends-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd ExtUtils-Depends-${pkgver}
  make test
}

package() {
  cd ExtUtils-Depends-${pkgver}
  make DESTDIR="${pkgdir}" install
}
