# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
pkgname=fam
pkgver=2.7.0
pkgrel=11
pkgdesc="File Alteration Monitor"
arch=('i686' 'x86_64')
license=('LGPL' 'GPL')
depends=('portmap')
url="http://oss.sgi.com/projects/fam/"
backup=(etc/fam/fam.conf)
options=('!makeflags') # Don't !libtool
source=(ftp://oss.sgi.com/projects/fam/download/stable/${pkgname}-${pkgver}.tar.gz
	fam-2.7.0-dnotify.patch
	fam-2.7.0-largefiles.patch
	fam-2.7.0-noc++.patch
	fam-2.7.0-gcc43.patch
	include_fam_h.patch
	fam)
md5sums=('1bf3ae6c0c58d3201afc97c6a4834e39'
         '073d1763318344635ea316293390205c'
         '47b41e0b0498793af004696a096d7da1'
         '0274cd113d3dcb015653b5606c7714ac'
         '13c14778038aa9601a27498448e855e0'
         '2638b8ffacb9f03b6e438e08ea7b290a'
         '45b648907ba1c7edec17fb544f1f4a13')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -Np1 -i ${srcdir}/fam-2.7.0-dnotify.patch || return 1
  patch -Np1 -i ${srcdir}/fam-2.7.0-largefiles.patch || return 1
  patch -Np0 -i ${srcdir}/include_fam_h.patch || return 1
  patch -Np1 -i ${srcdir}/fam-2.7.0-noc++.patch || return 1
  patch -Np1 -i ${srcdir}/fam-2.7.0-gcc43.patch || return 1

  chmod 755 configure || return 1

  autoheader || return 1
  aclocal || return 1
  automake -a -c || return 1
  autoconf || return 1
  libtoolize --copy --force || return 1

  ./configure --prefix=/usr --sysconfdir=/etc/fam
  make || return 1
  make DESTDIR=${pkgdir} install
  install -D -m755 ../fam ${pkgdir}/etc/rc.d/fam
}
