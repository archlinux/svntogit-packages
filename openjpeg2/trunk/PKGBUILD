# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=openjpeg2
pkgver=2.1.2
pkgrel=1
pkgdesc="An open source JPEG 2000 codec, version ${pkgver}"
arch=(i686 x86_64)
license=('custom: BSD')
url="http://www.openjpeg.org"
makedepends=('cmake' 'doxygen')
             #'fcgi' 'xerces2-java')
depends=('zlib' 'libpng' 'libtiff' 'lcms2')
source=("$pkgname-$pkgver.tar.gz::https://github.com/uclouvain/openjpeg/archive/v$pkgver.tar.gz")
md5sums=('40a7bfdcc66280b3c1402a0eb1a27624')

prepare() {
  cd ${srcdir}
  mkdir build
  # Install doxygen docs to the right directory
  sed -i -e "s:DESTINATION\ share/doc:DESTINATION\ share/doc/${pkgname}:" openjpeg-${pkgver}/doc/CMakeLists.txt
}

build() {
  cd "${srcdir}/build"

  # CMAKE flags
  # MJ2 executables: '-DBUILD_MJ2:bool=on' (default: 'OFF')
  # JPWL executables and JPWL library: '-DBUILD_JPWL:bool=on' (default: 'OFF')
  # JPIP client (java compiler recommended) library and executables: '-DBUILD_JPIP:bool=on' (default: 'OFF')
  # JPIP server (need fcgi) library and executables: '-DBUILD_JPIP_SERVER:bool=on' (default: 'OFF')

  cmake "../openjpeg-${pkgver}" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_DOC=on

#    -DBUILD_MJ2=ON \
#    -DBUILD_JPWL=ON \
#    -DBUILD_JPIP=ON \
#    -DBUILD_JPIP_SERVER=ON \
#    -DBUILD_JP3D=ON # --help -LAH

  make
}

package() {
  cd "${srcdir}/build"
  make DESTDIR="${pkgdir}" install
  
  install -m755 -d "${pkgdir}/usr/share/licenses/openjpeg2"
  mv ${pkgdir}/usr/share/doc/openjpeg-2.1/LICENSE ${pkgdir}/usr/share/licenses/openjpeg2
  rmdir ${pkgdir}/usr/share/doc/openjpeg-2.1
}
