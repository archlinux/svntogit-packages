# $Id$
# Maintainer: Jan de Groot <jan@archlinux.org>

pkgname=at-spi
pkgver=1.30.1
pkgrel=1
pkgdesc="Assistive Technology Service Provider Interface"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libbonobo>=2.24.2' 'libxtst' 'gtk2>=2.20.0' 'gconf>=2.28.1')
makedepends=('pkgconfig' 'python' 'intltool')
options=('!libtool' '!emptydirs')
url="http://www.gnome.org"
install=at-spi.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/1.30/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('8dbe3f3b6028f01afd841859e49c0bf159a1ccca1f90be2647365062c5d0346d')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --libexecdir=/usr/lib/at-spi || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain at-spi ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
