# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Andrew Simmons <andrew.simmons@gmail.com>

pkgname=thunar-media-tags-plugin
pkgver=0.2.0
pkgrel=1
pkgdesc="Adds special features for media files to the Thunar File Manager"
arch=('i686' 'x86_64')
license=('GPL')
url="http://goodies.xfce.org/projects/thunar-plugins/thunar-media-tags-plugin"
groups=('xfce4-goodies')
depends=('taglib' 'thunar')
makedepends=('intltool')
options=('!libtool')
source=(http://archive.xfce.org/src/thunar-plugins/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2)
sha1sums=('1111b4dae10156ab91adb4eadb26586593b1b023')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--libexecdir=/usr/lib/xfce4 \
	--localstatedir=/var \
	--disable-static \
	--disable-debug
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
