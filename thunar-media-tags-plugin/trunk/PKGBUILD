# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Andrew Simmons <andrew.simmons@gmail.com>

pkgname=thunar-media-tags-plugin
pkgver=0.3.0
pkgrel=1
pkgdesc="Adds special features for media files to the Thunar File Manager"
arch=('x86_64')
license=('GPL')
url="http://goodies.xfce.org/projects/thunar-plugins/thunar-media-tags-plugin"
groups=('xfce4-goodies')
depends=('taglib' 'thunar')
makedepends=('intltool')
source=(https://archive.xfce.org/src/thunar-plugins/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=('e265c4415abac40337cc5566c6f706efcf0be4e97723abe22ba8b874c93a591b')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
