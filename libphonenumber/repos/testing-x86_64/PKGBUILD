# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=libphonenumber
pkgver=8.12.46
pkgrel=3
epoch=1
pkgdesc="Google's common library for parsing, formatting, and validating international phone numbers"
url="https://github.com/googlei18n/libphonenumber"
arch=(x86_64)
license=(Apache)
depends=(icu protobuf boost-libs abseil-cpp)
makedepends=(boost cmake gtest git)
options=(debug)
_commit=3b8e317bc7e7b332f515b10f16b5b096b9771fad  # tags/v8.12.46^0
source=("git+$url#commit=$_commit" absl.diff)
sha256sums=('SKIP'
            'ab519dfe99dba11526a105199a9b090ae3be958d3f179f82eb5a288e358c553c')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^libphonenumber-\|^v//;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname

  # Use our abseil-cpp
  git apply -3 ../absl.diff
}

build() {
  CXXFLAGS+=" -Wno-error=deprecated-declarations"  # readdir_r deprecation
  CXXFLAGS+=" -Wno-error=unused-variable"          # this is nuts
  cmake -S $pkgname/cpp -B build \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  cmake --build build
}

check() {
  cmake --build build --target tests
}

package() {
  depends+=(libicu{uc,i18n}.so libprotobuf.so libboost_thread.so)
  provides+=(libgeocoding.so libphonenumber.so)

  DESTDIR="$pkgdir" cmake --install build
}
