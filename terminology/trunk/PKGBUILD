# Maintainer: Ronald van Haren <ronald@archlinux.org>
# Contributor: Doug Newgard <scimmia22 at outlook dot com>
# Contributor: Paul Ezvan <paul@ezvan.fr>

pkgname=terminology
pkgver=1.0.0
pkgrel=1
pkgdesc="EFL based terminal emulator"
arch=('i686' 'x86_64')
url="https://www.enlightenment.org/about-terminology"
license=('BSD')
depends=('efl')
source=("http://download.enlightenment.org/rel/apps/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha1sums=('8621d9b88995b7f0e71437438853f8c4c473bc98')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  export CFLAGS="${CFLAGS} -fvisibility=hidden"
  
  ./configure \
    --prefix=/usr

  make
}

package(){
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  # install license files
  install -Dm644 "${srcdir}/${pkgname}-${pkgver}/COPYING" \
        "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
