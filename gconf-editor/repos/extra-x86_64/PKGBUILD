# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gconf-editor
pkgver=3.0.1
pkgrel=1
pkgdesc="Graphical gconf registry editor"
arch=(i686 x86_64)
license=('GPL')
depends=('gconf' 'hicolor-icon-theme')
makedepends=('pkgconfig' 'intltool' 'gnome-doc-utils')
url="http://www.gnome.org"
groups=('gnome-extra')
options=(!emptydirs)
install=gconf-editor.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2
        fix-assertion-failed-crash.patch)
sha256sums=('9afc4fc0d0afe019998736f06c04cbfa0393c813e2aa755133d95e83835f8869'
            '6550714ee06d09632244d0f4f41402224599f5230ef71376ea34291ce0d651bc')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -i "${srcdir}/fix-assertion-failed-crash.patch"
  ./configure --prefix=/usr --sysconfdir=/etc \
     --localstatedir=/var --disable-scrollkeeper
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="$pkgdir" install

  install -m755 -d "$pkgdir/usr/share/gconf/schemas"
  gconf-merge-schema "$pkgdir/usr/share/gconf/schemas/$pkgname.schemas" --domain gconf-editor "$pkgdir"/etc/gconf/schemas/*.schemas
  rm -f "$pkgdir"/etc/gconf/schemas/*.schemas
}
