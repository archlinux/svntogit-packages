# $Id$
# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: simo <simo@archlinux.org>

pkgname=python-imaging
pkgver=1.1.7
pkgrel=4
pkgdesc="PIL. Provides image processing capabilities for python"
arch=('i686' 'x86_64')
url="http://www.pythonware.com/products/pil/index.htm"
license=('custom:"pil"')
depends=('python2' 'libjpeg' 'freetype2' 'lcms') 
makedepends=('tk' 'sane')
optdepends=('tk' 'sane')
provides=("pil=${pkgver}")
conflicts=('pil')
replaces=('pil')
source=(http://effbot.org/downloads/Imaging-${pkgver}.tar.gz)
md5sums=('fc14a54e1ce02a0225be8854bfba478e')

build() {
  cd "${srcdir}"/Imaging-${pkgver}
  python2 setup.py build_ext
  cd Sane
  python2 setup.py build_ext
}

package() {
  cd "${srcdir}"/Imaging-${pkgver}
  python2 setup.py install --root="${pkgdir}" --optimize=1
  
  pushd Sane
  python2 setup.py install --root="${pkgdir}" --optimize=1
  popd
  
  install -dm755 "${pkgdir}"/usr/include/python2.7/
  install -m644 -t "${pkgdir}"/usr/include/python2.7/ libImaging/*.h 

  # do not have files ending in .py in /usr/bin
  for f in pildriver pilprint pilconvert pilfile pilfont; do
    mv "${pkgdir}"/usr/bin/${f}{.py,}
  done

  # Install license
  install -Dm644 "${srcdir}"/Imaging-${pkgver}/README \
                 "${pkgdir}"/usr/share/licenses/${pkgname}/README
}
