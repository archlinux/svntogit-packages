# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Ryan Coyner <rcoyner@gmail.com>

pkgname=mod_wsgi
pkgver=3.3
pkgrel=2
pkgdesc="Python WSGI adapter module for Apache"
arch=('i686' 'x86_64')
url="http://www.modwsgi.org/"
license=('APACHE')
depends=('apache' 'python2')
install=mod_wsgi.install
source=(http://modwsgi.googlecode.com/files/${pkgname}-${pkgver}.tar.gz)
md5sums=('6172bb2bbabcd0c25867c2bc06f99dbb')
sha1sums=('f32d38e5d3ed5de1efd5abefb52678f833dc9166')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
    --with-apxs=/usr/sbin/apxs \
    --with-python=/usr/bin/python2
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
