# $Id$
# Maintainer: Alexander Baldeck <alexander@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
pkgname=unichrome-dri
pkgver=7.4
pkgrel=2
pkgdesc="DRI drivers for Unichrome-based videochipsets"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libdrm>=2.4.5' 'expat>=2.0.1' "libgl>=${pkgver}")
makedepends=('dri2proto>=1.99.3' 'glproto>=1.4.9' 'pkgconfig' 'libxfixes' 'libxdamage' 'libxxf86vm' 'libxext')
options=('!libtool')
source=(http://downloads.sourceforge.net/mesa3d/MesaLib-${pkgver}.tar.bz2
	mesa-7.1-link-shared.patch LICENSE)
md5sums=('7ecddb341a2691e0dfdb02f697109834' 'f0baa948d9810f268413111ee439d24b'\
         'af58249a42f58bdbf39d20ad54f3b5f9')

build() {
  cd "${srcdir}/Mesa-${pkgver}"
  patch -Np1 -i "${srcdir}/mesa-7.1-link-shared.patch" || return 1
  export CFLAGS="${CFLAGS} -ffast-math"
  ./configure --prefix=/usr \
    --with-dri-driverdir=/usr/lib/xorg/modules/dri \
    --with-dri-drivers=unichrome \
    --enable-glx-tls \
    --with-driver=dri \
    --enable-xcb \
    --disable-glu \
    --disable-glut \
    --disable-glw || return 1
  make || return 1
  cd src/mesa/drivers/dri || return 1
  make DESTDIR="${pkgdir}" install || return 1
  rm -rf "${pkgdir}/usr/lib/pkgconfig"
  rm -rf "${pkgdir}/usr/include"
  rm -f "${pkgdir}/usr/lib/xorg/modules/dri/libdricore.so"
  install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
