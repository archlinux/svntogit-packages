# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=ncurses
pkgver=5.7
pkgrel=3
pkgdesc="System V Release 4.0 curses emulation library"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/ncurses/"
license=('MIT')
depends=('glibc')
source=(ftp://ftp.gnu.org/pub/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz)
md5sums=('cce05daf61a64501ef6cd8da1f727ec6')

build() {
  cd ${srcdir}/
  mkdir ncurses{,w}-build
  
  cd ${srcdir}/ncursesw-build
  ../${pkgname}-${pkgver}/configure --prefix=/usr --mandir=/usr/share/man \
     --with-shared --with-normal --without-debug --without-ada \
     --with-install-prefix=${pkgdir} --enable-widec 
  make || return 1

  # libncurses.so.5 for external binary support 
  cd ${srcdir}/ncurses-build
  [ $CARCH = "x86_64" ] && CONFIGFLAG="--with-chtype=long"
  ../${pkgname}-${pkgver}/configure --prefix=/usr \
    --with-shared --with-normal --without-debug --without-ada \
    --with-install-prefix=${pkgdir} $CONFIGFLAG
  make || return 1
}

package() {
  cd ${srcdir}/ncursesw-build
  make install

  # move libraries needed for boot to /lib (we call tput in initscripts)
  install -dm755 ${pkgdir}/lib
  mv ${pkgdir}/usr/lib/libncursesw.so.5* ${pkgdir}/lib
  ln -sf ../../lib/libncursesw.so.5 ${pkgdir}/usr/lib/libncursesw.so

  # Fool packages looking to link to non-wide-character ncurses libraries
  for lib in curses ncurses form panel menu ; do \
    rm -f ${pkgdir}/usr/lib/lib${lib}.so ; \
    echo "INPUT(-l${lib}w)" >${pkgdir}/usr/lib/lib${lib}.so ; \
    ln -sf lib${lib}w.a ${pkgdir}/usr/lib/lib${lib}.a ; \
  done
  ln -sf libncurses++w.a ${pkgdir}/usr/lib/libncurses++.a

  # install tput to /bin
  install -dm755 ${pkgdir}/bin/
  mv ${pkgdir}/usr/bin/tput ${pkgdir}/bin/tput

  # Some packages look for -lcurses during build
  rm -f ${pkgdir}/usr/lib/libcursesw.so
  echo "INPUT(-lncursesw)" >${pkgdir}/usr/lib/libcursesw.so
  ln -sf libncurses.so ${pkgdir}/usr/lib/libcurses.so
  ln -sf libncursesw.a ${pkgdir}/usr/lib/libcursesw.a
  ln -sf libncurses.a ${pkgdir}/usr/lib/libcurses.a

  # non-widec compatibility library
  cd ${srcdir}/ncurses-build        
  install -Dm755 lib/libncurses.so.${pkgver} ${pkgdir}/usr/lib/libncurses.so.${pkgver}
  ln -sf libncurses.so.${pkgver} ${pkgdir}/usr/lib/libncurses.so.5

  # install license, rip it from the readme
  cd ${srcdir}/${pkgname}-${pkgver}
  install -dm755 ${pkgdir}/usr/share/licenses/$pkgname
  grep -B 100 '$Id' README > ${pkgdir}/usr/share/licenses/${pkgname}/license.txt
}
