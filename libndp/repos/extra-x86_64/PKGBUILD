# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=libndp
pkgver=1.7
pkgrel=1
pkgdesc="Library for Neighbor Discovery Protocol"
arch=(x86_64)
url="http://libndp.org/"
license=(LGPL2.1)
depends=(glibc)
makedepends=(git)
_commit=96674e7d4f4d569c2c961e865cc16152dfab5f09  # tags/v1.7
source=("git+https://github.com/jpirko/libndp#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $pkgname
  ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --libexecdir=/usr/lib
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
