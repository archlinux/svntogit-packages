# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-power-manager
pkgver=3.12.0
pkgrel=1
pkgdesc="System power information and statistics"
arch=(i686 x86_64)
url="http://www.gnome.org/projects/gnome-power-manager/"
license=(GPL)
depends=(gtk3 upower dconf hicolor-icon-theme)
makedepends=(intltool)
options=(!emptydirs)
install=gnome-power-manager.install
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('4e24dbfc56961a2889b1c67554695e0329f99ba658aac15747fcbee263393d59')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var \
      --libexecdir=/usr/lib/gnome-power-manager \
      --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
