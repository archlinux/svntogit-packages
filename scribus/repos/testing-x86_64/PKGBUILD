# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: tobias <tobias@archlinux.org>
# Contributor: Ben <ben@benmazer.net>

pkgname=scribus
pkgver=1.3.3.13
pkgrel=2
pkgdesc="A desktop publishing program"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.scribus.net"
install=${pkgname}.install
depends=('libcups>=1.3.10' 'lcms>=1.18' 'qt3>=3.3.8' 'ghostscript>=8.64' \
         'libart-lgpl>=2.3.20' 'python>=2.6.1' 'libxml2>=2.7.3' 'cairo' \
         'desktop-file-utils')
makedepends=('cmake')
options=(!libtool !makeflags)
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.bz2)
md5sums=('e698b0d118c7f037e57163cba302d96e')

build() {
  # Source the QT profile
  source /etc/profile.d/qt3.sh

  cd ${srcdir}/scribus-${pkgver}
  cmake . -DCMAKE_INSTALL_PREFIX:PATH=/usr \
   -DWANT_CAIRO=1 -DLIB_SUFFIX=""
  make || return 1
  make DESTDIR=${pkgdir} install

  install -Dm644 vnd.scribus.desktop \
    ${pkgdir}/usr/share/applications/scribus.desktop
}
