# $Id$
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: tobias <tobias@archlinux.org>
# Contributor: Ben <ben@benmazer.net>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=scribus
pkgver=1.3.9
pkgrel=1
pkgdesc="A desktop publishing program"
arch=('i686' 'x86_64')
url="http://www.scribus.net"
license=('GPL')
install=scribus.install
depends=('libcups>=1.3.11' 'lcms>=1.18' 'qt' 'ghostscript>=8.70' \
         'libart-lgpl>=2.3.20' 'python2' 'libxml2>=2.7.3' 'cairo' \
         'desktop-file-utils' 'shared-mime-info')
makedepends=('cmake')
options=(!libtool !makeflags)
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.bz2)
md5sums=('a54dbf2e10cd88d683d0d34565d29378')
sha1sums=('86bc4db2d8fbb91fe94f8535327991e9128c5862')

build() {
	cd "${srcdir}"
	sed -i 's|#!/usr/bin/python|#!/usr/bin/python2|' ${pkgname}-${pkgver}/scribus/plugins/scriptplugin/scripts/ColorChart.py
	mkdir build
	cd build
	cmake ../${pkgname}-${pkgver} \
	   -DCMAKE_INSTALL_PREFIX=/usr \
	   -DCMAKE_BUILD_TYPE=Release \
	   -DCMAKE_SKIP_RPATH=ON \
	   -DWANT_CAIRO=1 \
	   -DLIB_SUFFIX=""
	make
}

package() {
	cd "${srcdir}/build"
	make DESTDIR="${pkgdir}" install

	# fix icon path in .desktop file
	sed -i 's|Icon=scribus|Icon=/usr/share/scribus/icons/scribus.png|' \
	   "${srcdir}/${pkgname}-${pkgver}/scribus.desktop"

	install -Dm644 "${srcdir}/${pkgname}-${pkgver}/scribus.desktop" \
	   "${pkgdir}/usr/share/applications/scribus.desktop"
}
