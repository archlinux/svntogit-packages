# $Id$

pkgname=fltk
pkgver=1.1.10
pkgrel=1
pkgdesc="Graphical user interface toolkit for X"
arch=('i686' 'x86_64')
license=('LGPL')
url="http://www.fltk.org/"
depends=('libjpeg' 'libpng' 'gcc-libs' 'libxft' 'libxext')
makedepends=('mesa')
source=("http://ftp.easysw.com/pub/$pkgname/$pkgver/$pkgname-$pkgver-source.tar.bz2")
md5sums=('a1765594bc427ff892e36089fe1fa672')

build() {
  cd $srcdir/$pkgname-$pkgver
  export LDFLAGS="${LDFLAGS//-Wl,--as-needed}"
  export LDFLAGS="${LDFLAGS//,--as-needed}"
  ./configure --prefix=/usr --enable-threads --enable-xft --enable-shared
  make || return 1
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
}
