# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-screensaver
pkgver=2.24.0
pkgrel=1
pkgdesc="Screensaver designed to integrate well with the GNOME desktop."
arch=(i686 x86_64)
license=('GPL')
url="http://live.gnome.org/GnomeScreensaver"
backup=(etc/pam.d/gnome-screensaver)
depends=('gnome-menus>=2.24.0' 'libxxf86misc' 'libgnomekbd>=2.24.0' 'libxss' 'libgl' 'libnotify' 'gnome-desktop>=2.24.0')
makedepends=('pkgconfig' 'gettext' 'perlxml' 'mesa' 'xscreensaver')
groups=('gnome')
options=(!emptydirs)
install=gnome-screensaver.install
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/2.24/${pkgname}-${pkgver}.tar.bz2
	gnome-screensaver.pam)
md5sums=('aef361edcc32468d8dc50cf710d9bb80'
         '4b4d2672a787e0d2ed792b4c308ccf8d')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
              --libexecdir=/usr/lib/gnome-screensaver \
              --localstatedir=/var --with-gdm-config=/usr/share/gdm/defaults.conf \
	      --with-xscreensaverdir=/usr/share/xscreensaver/config \
	      --with-xscreensaverhackdir=/usr/lib/xscreensaver \
	      --with-dpms-ext || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1

  install -m644 "${srcdir}/gnome-screensaver.pam" "${pkgdir}/etc/pam.d/gnome-screensaver" || return 1

  #Migrate xscreensaver configuration files
  cd "${pkgdir}/usr/share/applications/screensavers"
  "${srcdir}/${pkgname}-${pkgver}/data/migrate-xscreensaver-config.sh" /usr/share/xscreensaver/config/*.xml || return 1
  
  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
