# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-screensaver
pkgver=2.30.0
pkgrel=1
pkgdesc="Screensaver designed to integrate well with the GNOME desktop."
arch=('i686' 'x86_64')
license=('GPL')
url="http://live.gnome.org/GnomeScreensaver"
backup=(etc/pam.d/gnome-screensaver)
depends=('gnome-menus>=2.30.0' 'libgnomekbd>=2.30.0' 'libgl' 'libnotify>=0.4.5' 'gnome-desktop>=2.30.0' 'libxxf86misc')
makedepends=('pkgconfig' 'intltool' 'mesa' 'xscreensaver' 'libxss' 'libxslt')
optdepends=('xscreensaver')
groups=('gnome')
options=(!emptydirs)
install=gnome-screensaver.install
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/2.30/${pkgname}-${pkgver}.tar.bz2
	      gnome-screensaver.pam)
sha256sums=('f653d3e4a872d435bce9d74ea8d14e54090c4c803521d5fe940e948c1dccc060'
            '2744d1fc39da46fc681b7cf1c9230d2035b2bb8d9f510213fbe439c114eb76a2')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/gnome-screensaver \
      --localstatedir=/var \
      --with-xscreensaverdir=/usr/share/xscreensaver/config \
      --with-xscreensaverhackdir=/usr/lib/xscreensaver || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1

  install -m644 "${srcdir}/gnome-screensaver.pam" "${pkgdir}/etc/pam.d/gnome-screensaver" || return 1

  #Migrate xscreensaver configuration files
  sed -e 's|Exec=|Exec=/usr/lib/xscreensaver/|' \
      -i data/xscreensaver-config.xsl || return 1
  cd "${pkgdir}/usr/share/applications/screensavers"
  "${srcdir}/${pkgname}-${pkgver}/data/migrate-xscreensaver-config.sh" \
      /usr/share/xscreensaver/config/*.xml || return 1
  
  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gnome-screensaver ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
