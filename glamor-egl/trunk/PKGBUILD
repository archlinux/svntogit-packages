# $Id: PKGBUILD 179700 2013-03-09 11:48:07Z andyrtr $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Eugeni Dodonov <eugeni@dodonov.net>

pkgname=glamor-egl
pkgver=0.5.1
pkgrel=2
pkgdesc='OpenGL based 2D rendering acceleration library'
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('mesa-libgl')
makedepends=('xorg-server-devel' 'X-ABI-VIDEODRV_VERSION=15')
conflicts=('xorg-server<1.15.0' 'X-ABI-VIDEODRV_VERSION<15' 'X-ABI-VIDEODRV_VERSION>=16' 'glamor-git')
source=(${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('f42805b68e2bddc90cb5d9d3811e4f2176659c5686f295865839b20b7be7f359')

build() {
  cd ${pkgname}-${pkgver}
  
  ./configure --prefix=/usr \
    --disable-static \
    --enable-glx-tls \
    #--with-xorg-conf-dir=/etc/X11/xorg.conf.d
    #--help
    #--enable-glamor-gles2 \ https://bugs.archlinux.org/task/34284

  make
}

package() {
  cd ${pkgname}-${pkgver}

  make "DESTDIR=${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
