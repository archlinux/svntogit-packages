# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Mantas M. <grawity@gmail.com>

pkgname=ding-libs
pkgver=0.6.0
pkgrel=1
pkgdesc="'D is not GLib' utility libraries"
arch=('i686' 'x86_64')
url="https://fedorahosted.org/sssd/"
license=('GPL3' 'LGPL3')
depends=('glibc')
makedepends=('doxygen')
checkdepends=('check')
source=(https://fedorahosted.org/released/$pkgname/$pkgname-$pkgver.tar.gz{,.asc})
sha1sums=('c8ec86cb93a26e013a13b12a7b0b3fbc1bca16c1'
          'SKIP')
validpgpkeys=('7B54CAE8A03D66F3D70A5B516F5A90EB44FBC7C7'  # "Pavel Reichl <preichl@redhat.com>"
              'E4E366758CA0716AAB8048671EC6AB7532E7BC25') # "Jakub Hrozek <jhrozek@redhat.com>"

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-static
  make all docs
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir/" install
} 
