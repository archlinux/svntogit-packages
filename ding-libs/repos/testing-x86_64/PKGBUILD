# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Mantas M. <grawity@gmail.com>

pkgname=ding-libs
pkgver=0.4.0
pkgrel=3
pkgdesc="'D is not GLib' utility libraries"
arch=('i686' 'x86_64')
url="https://fedorahosted.org/sssd/"
license=('GPL3' 'LGPL3')
depends=('glibc')
makedepends=('doxygen')
checkdepends=('check')
source=(https://fedorahosted.org/released/$pkgname/$pkgname-$pkgver.tar.gz{,.asc})
sha1sums=('38d9ebb0e44dfb7a8d97c2f650a586260c8909f9'
          'SKIP')
validpgpkeys=('7B54CAE8A03D66F3D70A5B516F5A90EB44FBC7C7') # "Pavel Reichl <preichl@redhat.com>"

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-static
  make all docs
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir/" install
} 
