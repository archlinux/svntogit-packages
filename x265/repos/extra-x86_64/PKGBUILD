# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: kfgz <kfgz at interia pl>

pkgname=x265
pkgver=1.6
pkgrel=1
pkgdesc='Open Source H265/HEVC video encoder'
arch=('i686' 'x86_64')
url='https://bitbucket.org/multicoreware/x265'
license=('GPL')
depends=('gcc-libs')
makedepends=('yasm' 'cmake')
provides=('libx265.so')
source=($pkgname-$pkgver.tar.bz2::$url/get/$pkgver.tar.bz2)
md5sums=('d6fcde2d021899bc93f987cd6819a233')

build() {
  cd multicoreware-x265-*/build/linux
  [[ $CARCH == x86_64 ]] && LDFLAGS+=',-z,noexecstack'
  cmake -G "Unix Makefiles" -DCMAKE_INSTALL_PREFIX=/usr ../../source
  make
}

package() {
  cd multicoreware-x265-*/build/linux
  make DESTDIR="$pkgdir" install
}
