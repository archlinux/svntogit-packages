# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: kfgz <kfgz at interia pl>

pkgname=x265
pkgver=1.7
pkgrel=2
pkgdesc='Open Source H265/HEVC video encoder'
arch=('i686' 'x86_64')
url='https://bitbucket.org/multicoreware/x265'
license=('GPL')
depends=('gcc-libs')
makedepends=('yasm' 'cmake')
provides=('libx265.so')
source=($pkgname-$pkgver.tar.bz2::$url/get/$pkgver.tar.bz2
        x265-1.7-fix-slowness-with-gcc-5.1.patch)
md5sums=('d6020c277b05ab44f8222342aaa9c99f'
         '30c7fc491f22cbdec1ee17caab7d620a')

prepare() {
  cd multicoreware-x265-*
  patch -p1 -i "$srcdir"/x265-1.7-fix-slowness-with-gcc-5.1.patch
}

build() {
  cd multicoreware-x265-*/build/linux
  [[ $CARCH == x86_64 ]] && LDFLAGS+=',-z,noexecstack'
  cmake -G "Unix Makefiles" -DCMAKE_INSTALL_PREFIX=/usr ../../source
  make
}

package() {
  cd multicoreware-x265-*/build/linux
  make DESTDIR="$pkgdir" install
}
