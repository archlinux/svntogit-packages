# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Paul Mattal <paul@mattal.com>

pkgname=help2man
pkgver=1.48.3
pkgrel=1
pkgdesc="Conversion tool to create man files"
arch=('x86_64')
url="https://www.gnu.org/software/help2man/"
license=('GPL')
depends=('perl-locale-gettext')
source=("https://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha512sums=('e000e7380604c0986b14cb64b852d55eff48595884bddd389e876056dde6ce72b37aa16d92230e6f8f6e128003b819954bae4aa90933a58b51d3665d3c0ba356'
            'SKIP')
validpgpkeys=('87EA44D150D89615E39A3FEEF0DC8E00B28C5995') # Brendan O'Dea <bod@debian.org>

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
	      --infodir=/usr/share/info \
	      --libdir=/usr/lib
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install
}
