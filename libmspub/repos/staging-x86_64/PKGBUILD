# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Simone Sclavi 'Ito' <darkhado@gmail.com>

pkgname=libmspub
pkgver=0.1.4
pkgrel=12
pkgdesc="Microsoft Publisher file format parser library (latest snapshot)"
arch=('x86_64')
url="https://wiki.documentfoundation.org/DLP/Libraries/libmspub"
license=('GPL2' 'LGPL2.1' 'MPL')
depends=('libwpd' 'icu' 'librevenge')
makedepends=('libwpg' 'boost' 'doxygen')
source=(https://dev-www.libreoffice.org/src/${pkgname}/${pkgname}-${pkgver}.tar.xz
        buildfix.diff)
sha256sums=('ef36c1a1aabb2ba3b0bedaaafe717bf4480be2ba8de6f3894be5fd3702b013ba'
            'be1a871f9ebfb15d989605137d5de0c5dc67a3e383e0e1178037f8a9bdfc89dc')

prepare() {
    cd "${pkgname}"-${pkgver}
    patch -Np1 -i ../buildfix.diff
    autoreconf -vfi
}

build() {
    cd "${pkgname}"-${pkgver}
    ./configure --prefix=/usr --with-docs
    make
}

package() {
    cd "${pkgname}"-${pkgver}
    make DESTDIR="$pkgdir" install
}
