# $Id$
# Maintainer: Angel Velasquez <angvp@archlinux.org> 

pkgbase=python-astroid
pkgname=('python2-astroid' 'python-astroid')
pkgver=1.0.0
pkgrel=1
pkgdesc="Useful miscellaneous modules used by Logilab projects"
arch=('any')
url="http://www.logilab.org/project/logilab-common"
license=('LGPL')
makedepends=('python2' 'python')
source=(https://pypi.python.org/packages/source/a/astroid/astroid-${pkgver}.tar.gz)
sha1sums=('2ebba76d115cb8a2d84d8777d8535ddac86daaa6')

build() {
  cd "${srcdir}"

  cp -a astroid-${pkgver}{,-py3}

  cd astroid-${pkgver}
  python2 setup.py build

  cd ../astroid-${pkgver}-py3
  python3 setup.py build
}

package_python2-astroid() {
  depends=('python2')
  replaces=('python2-logilab-astng')
  conflicts=('python2-logilab-astng')
  cd "${srcdir}"/astroid-${pkgver}

  python2 setup.py install --optimize=1 --skip-build --prefix=/usr --root="${pkgdir}"

  # fix permissions ...
  find "${pkgdir}" -type f -exec chmod +r {} \;
}

package_python-astroid() {
  depends=('python')
  replaces=('python-logilab-astng')
  conflicts=('python-logilab-astng')
  cd "${srcdir}"/astroid-${pkgver}-py3

  python3 setup.py install --optimize=1 --skip-build --prefix=/usr --root="${pkgdir}"

  # fix permissions ...
  find "${pkgdir}" -type f -exec chmod +r {} \;
}
