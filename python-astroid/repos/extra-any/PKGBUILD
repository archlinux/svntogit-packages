# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-astroid
pkgver=2.5.4
pkgrel=1
pkgdesc="Useful miscellaneous modules used by Logilab projects"
arch=('any')
url="https://github.com/PyCQA/astroid"
license=('LGPL')
depends=('python-six' 'python-lazy-object-proxy' 'python-wrapt')
makedepends=('python-setuptools')
checkdepends=('python-dateutil' 'python-pytest' 'python-numpy' 'python-nose' 'python-pytest-runner')
replaces=('python-logilab-astng')
conflicts=('python-logilab-astng')
source=(https://github.com/PyCQA/astroid/archive/astroid-$pkgver.tar.gz)
sha512sums=('d054ea855fa5bfeae6794d9fed84e025a9a7f31ec0f19bd79f7a3a91ad22a62d574d2b4aec0ce8f38f1c248311c51aa7da4231128c8c1e9fd128f66851d26593')

prepare() {
  cd astroid-astroid-$pkgver
  sed -i -e 's/six~=[0-9.*]*/six/;s/wrapt~=[0-9.*]*/wrapt/;s/lazy_object_proxy==[0-9.*]*/lazy_object_proxy/' astroid/__pkginfo__.py
}

build() {
  cd astroid-astroid-$pkgver
  python setup.py build
}

check() {
  cd astroid-astroid-$pkgver
  pytest
}

package() {
  cd astroid-astroid-$pkgver
  python setup.py install --optimize=1 --skip-build --prefix=/usr --root="$pkgdir"
}
