# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-astroid
pkgver=2.3.3
pkgrel=2
pkgdesc="Useful miscellaneous modules used by Logilab projects"
arch=('any')
url="https://github.com/PyCQA/astroid"
license=('LGPL')
depends=('python-six' 'python-lazy-object-proxy' 'python-wrapt')
makedepends=('python-setuptools')
checkdepends=('python-dateutil' 'python-pytest' 'python-numpy' 'python-nose' 'python-pytest-runner')
replaces=('python-logilab-astng')
conflicts=('python-logilab-astng')
source=(https://github.com/PyCQA/astroid/archive/astroid-$pkgver.tar.gz)
sha512sums=('cdca9eb7be5619a9bb9bda88c2519bb2708f37ebe51eb4a89966369b722a8fafc18ba860ae565c39ff6afa2dfd2a20395b2f0b762932073c37b249570f86abe5')

prepare() {
  cd astroid-astroid-$pkgver
  sed -i -e 's/six~=[0-9.*]*/six/' -e 's/wrapt==[0-9.*]*/wrapt/' astroid/__pkginfo__.py
}

build() {
  cd astroid-astroid-$pkgver
  python setup.py build
}

check() {
  cd astroid-astroid-$pkgver
  pytest
}

package() {
  cd astroid-astroid-$pkgver
  python setup.py install --optimize=1 --skip-build --prefix=/usr --root="$pkgdir"
}
