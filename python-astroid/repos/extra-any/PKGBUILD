# $Id$
# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-astroid
pkgname=('python2-astroid' 'python-astroid')
pkgver=1.4.1
pkgrel=1
pkgdesc="Useful miscellaneous modules used by Logilab projects"
arch=('any')
url="https://bitbucket.org/logilab/astroid"
license=('LGPL')
makedepends=('python-setuptools' 'python2-setuptools' 'python-six' 'python2-six' 'mercurial'
             'python-lazy-object-proxy' 'python2-lazy-object-proxy' 'python-wrapt' 'python2-wrapt'
             'python2-singledispatch')
source=("hg+https://bitbucket.org/logilab/astroid#tag=astroid-$pkgver")
sha1sums=('SKIP')

prepare() {
  cp -a astroid{,-py2}
}

build() {
  cd astroid
  python setup.py build

  cd ../astroid-py2
  python2 setup.py build
}

check() {
  cd astroid
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" python -Wi -m unittest discover -s build/lib/astroid/tests -p "unittest*.py"

  cd ../astroid-py2
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" python2 -Wi -m unittest discover -s build/lib/astroid/tests -p "unittest*.py"
}

package_python-astroid() {
  depends=('python-six' 'python-lazy-object-proxy' 'python-wrapt')
  replaces=('python-logilab-astng')
  conflicts=('python-logilab-astng')

  cd astroid

  python setup.py install --optimize=1 --skip-build --prefix=/usr --root="${pkgdir}"
}

package_python2-astroid() {
  depends=('python2-six' 'python2-lazy-object-proxy' 'python2-wrapt' 'python2-singledispatch')
  replaces=('python2-logilab-astng')
  conflicts=('python2-logilab-astng')

  cd astroid-py2

  python2 setup.py install --optimize=1 --skip-build --prefix=/usr --root="${pkgdir}"
}
