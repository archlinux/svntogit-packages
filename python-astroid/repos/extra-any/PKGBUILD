# $Id$
# Maintainer: Angel Velasquez <angvp@archlinux.org> 

pkgbase=python-astroid
pkgname=('python2-astroid' 'python-astroid')
pkgver=1.2.1
pkgrel=1
pkgdesc="Useful miscellaneous modules used by Logilab projects"
arch=('any')
url="https://bitbucket.org/logilab/astroid"
license=('LGPL')
makedepends=('python' 'python2')
checkdepends=('python-logilab-common' 'python2-logilab-common')
source=("https://pypi.python.org/packages/source/a/astroid/astroid-${pkgver}.zip")
sha1sums=('597c668553d9ee1431ff6da07e12769072477859')

prepare() {
  cp -a astroid-${pkgver}{,-py3}
}

build() {
  cd astroid-${pkgver}
  python2 setup.py build

  cd ../astroid-${pkgver}-py3
  python3 setup.py build
}

check() {
  cd astroid-${pkgver}/test
  PYTHONPATH="$PWD/../build/lib:$PYTHONPATH" python2 -Wi -m unittest discover -p "unittest*.py"

  cd ../../astroid-${pkgver}-py3/test
  PYTHONPATH="$PWD/../build/lib:$PYTHONPATH" python -Wi -m unittest discover -p "unittest*.py" || warning "Tests failed"
}

package_python2-astroid() {
  depends=('python2' 'python2-logilab-common')
  replaces=('python2-logilab-astng')
  conflicts=('python2-logilab-astng')

  cd astroid-${pkgver}

  python2 setup.py install --optimize=1 --skip-build --prefix=/usr --root="${pkgdir}"
}

package_python-astroid() {
  depends=('python' 'python-logilab-common')
  replaces=('python-logilab-astng')
  conflicts=('python-logilab-astng')

  cd astroid-${pkgver}-py3

  python3 setup.py install --optimize=1 --skip-build --prefix=/usr --root="${pkgdir}"
}
