# $Id$
# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-astroid
pkgname=('python2-astroid' 'python-astroid')
pkgver=1.3.5
pkgrel=1
pkgdesc="Useful miscellaneous modules used by Logilab projects"
arch=('any')
url="https://bitbucket.org/logilab/astroid"
license=('LGPL')
makedepends=('python-setuptools' 'python2-setuptools' 'python-six' 'python2-six')
checkdepends=('python-logilab-common' 'python2-logilab-common')
source=("https://pypi.python.org/packages/source/a/astroid/astroid-${pkgver}.tar.gz")
sha1sums=('6bc81d001b5ebf7c49dfc388e48538bd95d671ba')

prepare() {
  cp -a astroid-${pkgver}{,-py3}
}

build() {
  cd astroid-${pkgver}
  python2 setup.py build

  cd ../astroid-${pkgver}-py3
  python3 setup.py build
}

check() {
  cd astroid-${pkgver}
  PYTHONPATH="$PWD/../build/lib:$PYTHONPATH" python2 -Wi -m unittest discover -s build/lib/astroid/tests -p "unittest*.py"

  cd ../astroid-${pkgver}-py3
  PYTHONPATH="$PWD/../build/lib:$PYTHONPATH" python -Wi -m unittest discover -s build/lib/astroid/tests -p "unittest*.py"
}

package_python2-astroid() {
  depends=('python2-logilab-common' 'python2-six')
  replaces=('python2-logilab-astng')
  conflicts=('python2-logilab-astng')

  cd astroid-${pkgver}

  python2 setup.py install --optimize=1 --skip-build --prefix=/usr --root="${pkgdir}"
}

package_python-astroid() {
  depends=('python-logilab-common' 'python-six')
  replaces=('python-logilab-astng')
  conflicts=('python-logilab-astng')

  cd astroid-${pkgver}-py3

  python3 setup.py install --optimize=1 --skip-build --prefix=/usr --root="${pkgdir}"
}
