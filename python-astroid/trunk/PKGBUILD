# $Id$
# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-astroid
pkgver=2.0
pkgrel=2
pkgdesc="Useful miscellaneous modules used by Logilab projects"
arch=('any')
url="https://github.com/PyCQA/astroid"
license=('LGPL')
depends=('python-six' 'python-lazy-object-proxy' 'python-wrapt')
makedepends=('python-setuptools')
checkdepends=('python-dateutil' 'python-pytest' 'python-numpy' 'python-nose'
              'python-typed-ast')
replaces=('python-logilab-astng')
conflicts=('python-logilab-astng')
source=(https://github.com/PyCQA/astroid/archive/astroid-$pkgver.tar.gz)
sha512sums=('d91ffbe2cf8aef0e6ddb32ab11b638c41d5ffc3b6a57f555e14de1fbc9375699c6be78330f2e26cad4c1c8b761d2a8c40bed0bf749a41b94d858f52d1081e9c5')

build() {
  cd "$srcdir"/astroid-astroid-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/astroid-astroid-$pkgver
  pytest
}

package() {
  cd "$srcdir"/astroid-astroid-$pkgver
  python setup.py install --optimize=1 --skip-build --prefix=/usr --root="$pkgdir"
}
