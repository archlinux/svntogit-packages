# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-astroid
pkgver=2.4.1
pkgrel=1
pkgdesc="Useful miscellaneous modules used by Logilab projects"
arch=('any')
url="https://github.com/PyCQA/astroid"
license=('LGPL')
depends=('python-six' 'python-lazy-object-proxy' 'python-wrapt')
makedepends=('python-setuptools')
checkdepends=('python-dateutil' 'python-pytest' 'python-numpy' 'python-nose' 'python-pytest-runner')
replaces=('python-logilab-astng')
conflicts=('python-logilab-astng')
source=(https://github.com/PyCQA/astroid/archive/astroid-$pkgver.tar.gz)
sha512sums=('4bcee07579bdbd28843c5cf5872713aa29ffaad4695a2fc9f169c184ce11acc77c21db037476c0d3c55f9fb3cdaec32cb377daf4922f6601aa868e2088535853')

prepare() {
  cd astroid-astroid-$pkgver
  sed -i -e 's/six~=[0-9.*]*/six/' -e 's/wrapt==[0-9.*]*/wrapt/' astroid/__pkginfo__.py
}

build() {
  cd astroid-astroid-$pkgver
  python setup.py build
}

check() {
  cd astroid-astroid-$pkgver
  pytest
}

package() {
  cd astroid-astroid-$pkgver
  python setup.py install --optimize=1 --skip-build --prefix=/usr --root="$pkgdir"
}
