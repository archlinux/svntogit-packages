# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kimap
pkgver=21.07.90
pkgrel=1
pkgdesc='Job-based API for interacting with IMAP servers'
arch=(x86_64)
url='https://kontact.kde.org'
license=(LGPL)
depends=(kmime kio)
makedepends=(extra-cmake-modules)
source=(https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('7a467eb66695a1503f8036126b4db93bd0f1ebd36c9792ad7c383c6241fdb875'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
