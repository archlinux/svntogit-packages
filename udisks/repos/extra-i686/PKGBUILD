# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=udisks
pkgver=1.0.3
pkgrel=1
pkgdesc="Disk Management Service"
arch=('i686' 'x86_64')
url="http://www.freedesktop.org/wiki/Software/udisks"
license=('GPL')
depends=('udev' 'sg3_utils' 'glib2' 'dbus-glib' 'polkit' 'parted' 'device-mapper' 'libatasmart' 'lsof' 'lvm2' 'eject')
makedepends=('intltool' 'docbook-xsl' 'gtk-doc')
options=(!libtool)
#source=(http://hal.freedesktop.org/releases/${pkgname}-${pkgver}.tar.gz)
source=(ftp://ftp.archlinux.org/other/${pkgname}/${pkgname}-${pkgver}.tar.xz
        01-mkfs-tempdir.patch)
sha256sums=('a4d18b2de59c1567df5342f26960c6c3e70a0320f7bafe42893f2708c9b57dd5'
            '786c0adb1a37e16b351c906527e3cbd17193bb51d8c1dd0889f216556c419c6c')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/01-mkfs-tempdir.patch"
  ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/usr/lib/udisks --disable-static
  #fix location for bash_completion helper
  sed -i -e 's|profile.d|bash_completion.d|' tools/Makefile

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}/var/run"
}
