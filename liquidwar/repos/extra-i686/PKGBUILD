# $Id$
# Maintainer:

pkgname=liquidwar
pkgver=5.6.4
pkgrel=7
pkgdesc="A unique multiplayer wargame"
arch=('i686' 'x86_64')
url="http://www.ufoot.org/liquidwar/"
license=('GPL')
depends=('allegro4')
makedepends=('python2')
install=liquidwar.install
source=(http://www.ufoot.org/download/$pkgname/v5/$pkgver/$pkgname-$pkgver.tar.gz
        http://www.ufoot.org/sig/$pkgname-$pkgver.tar.gz.sig
	liquidwar-5.6.4-ovflfix.patch liquidwar-5.6.4-underlink.patch)
sha1sums=('a3006c4da9517c3af3b7bf165b2c86fa369a71c6'
          'SKIP'
          'e2102792c54a01b7cd14adb6d72404044175ec7e'
          '442097d99402890ed27ea1de6b7ae984baa2676a')

prepare() {
  cd $pkgname-$pkgver
  patch -p0 -i "$srcdir/liquidwar-5.6.4-ovflfix.patch"
  patch -p0 -i "$srcdir/liquidwar-5.6.4-underlink.patch"
  sed -i 's/python/python2/' doc/Makefile.in
}

build() {
  cd $pkgname-$pkgver
  [ "$CARCH" == "x86_64" ] && EXTRAOPTS="--disable-asm"
  PYTHON=yes ./configure --prefix=/usr $EXTRAOPTS
  make GAMEDIR=/usr/bin DATADIR=/usr/share/liquidwar
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" GAMEDIR=/usr/bin DATADIR=/usr/share/liquidwar install_nolink
}
