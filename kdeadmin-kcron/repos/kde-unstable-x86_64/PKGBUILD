# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeadmin-kcron
pkgver=4.11.95
pkgrel=1
pkgdesc='Configure and schedule tasks'
url='http://userbase.kde.org/KCron'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeadmin')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
source=("http://download.kde.org/unstable/${pkgver}/src/kcron-${pkgver}.tar.xz")
sha1sums=('6cf65963d0dcfc6b8be42030f25c0da182f778e7')

build() {
  mkdir build
  cd build
  cmake ../kcron-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
