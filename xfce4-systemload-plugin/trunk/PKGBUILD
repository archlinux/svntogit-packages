# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: AndyRTR <andyrtr@archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=xfce4-systemload-plugin
pkgver=1.3.2
pkgrel=1
pkgdesc="System load plugin for the Xfce panel"
arch=('x86_64')
license=('custom')
url="https://docs.xfce.org/panel-plugins/xfce4-systemload-plugin"
groups=('xfce4-goodies')
depends=('xfce4-panel' 'libgtop')
makedepends=('intltool')
source=(https://archive.xfce.org/src/panel-plugins/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=('bb303fc3020e053ad1fa0b8fcbf0d7681c5563bb8f649357d6a95a577802b072')

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
