# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgbase=calligra-l10n
pkgver=2.9.8
pkgrel=1
pkgdesc="Localization for Calligra"
arch=('any')
url='http://www.calligra-suite.org/'
license=('LGPL')
makedepends=('cmake' 'automoc4' 'kdelibs')
options=('docs')

_languages=(
  'bs     "Bosnian"'
  'ca     "Catalan"'
  'ca@valencia     "Catalan (Valencian)"'
  'cs     "Czech"'
  'da     "Danish"'
  'de     "German"'
  'el     "Greek"'
  'en_GB  "British English"'
  'es     "Spanish"'
  'et     "Estonian"'
  'fi     "Finnish"'
  'fr     "French"'
  'gl     "Galician"'
  'hu     "Hungarian"'
  'it     "Italian"'
  'ja     "Japanese"'
  'kk     "Kazakh"'
  'nb     "Norwegian Bokmal"'
  'nl     "Dutch"'
  'pl     "Polish"'
  'pt     "Portuguese"'
  'pt_BR  "Brazilian Portuguese"'
  'ru     "Russian"'
  'sk     "Slovak"'
  'sv     "Swedish"'
  'uk     "Ukrainian"'
  'zh_CN  "Chinese Simplified"'
  'zh_TW  "Chinese Traditional"'
)

pkgname=()
source=()

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}
  _pkgname=calligra-l10n-${_locale,,}
  
  pkgname+=($_pkgname)
  source+=("http://download.kde.org/stable/calligra-${pkgver}/${pkgbase}/${pkgbase}-$_locale-${pkgver}.tar.xz")
  eval "package_$_pkgname() {
    _package $_lang
  }"
done

md5sums=('a1a491f70c6594edca755d6613dc2c64'
         '54902e6a397d4c1ac8fe6042b805001d'
         '15bcc21aee70a538d3fcf33a307fad07'
         'b17bb56023af39a5518d5d7c75c7c463'
         'e54e5477f1c42ad6225c5bdcdc7caf87'
         '7a6e8859eb56d040559c3a4bd9d7aaa3'
         '4d202e263e51206f63995321bda82d97'
         'e6e91b306e0e7de6129b7c665355a13c'
         'f0cacf278530175cf57c9366bdab3db1'
         '138ec478806bcc76941ac577527a3b4d'
         '77982329654d098054e12416f46acdb5'
         'c353bb1d21e33f049662702665dcf166'
         '195b47754718351b4c739a63871c977f'
         'f6c0bbdfa8b3cad659eb121a88e602ad'
         'e868165656f5fbdff0b8dae0a93cb26c'
         'b83858e17e496c133612a0bca2789b31'
         '24b1cf23878ae8be175f03d49a2d1ff5'
         'c953408681b7d906d770f21f566bdf36'
         'b484601a4db433f136f0f77e41972fef'
         '79bfdbc78cd93c3f809861721b10ff6a'
         'ef51766ee73e60b60f60038af87b155f'
         '09380d452d5d4a3c2d3fad95fb996a5e'
         'fb85a38c94ccdfd059b45baf18c277a8'
         '6b7ae45432b3950776da13c9bf69f875'
         '377510c1981a77c444bf82e058be07e2'
         '5fdbe829018b350ce4e1c14fb3626e13'
         'e3842f012ee05e15f98c8be5673eae51'
         '9651782bc8913abb154606de7de697a3')

build() {
  for _lang in "${_languages[@]}"; do
    _locale=${_lang%% *}
    
    mkdir -p build/$_locale
    cd build/$_locale
    cmake $srcdir/$pkgbase-$_locale-$pkgver \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX=/usr
    make
    cd ../..
  done
}

_package() {
  pkgdesc="$2 localization for Calligra"
  cd build/$1
  make DESTDIR="$pkgdir" install
}
