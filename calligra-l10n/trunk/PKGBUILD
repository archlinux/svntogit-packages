# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgbase=calligra-l10n
pkgver=2.9.4
pkgrel=1
pkgdesc="Localization for Calligra"
arch=('any')
url='http://www.calligra-suite.org/'
license=('LGPL')
makedepends=('cmake' 'automoc4' 'kdelibs')
options=('docs')

_languages=(
  'bs     "Bosnian"'
  'ca     "Catalan"'
  'ca@valencia     "Catalan (Valencian)"'
  'cs     "Czech"'
  'da     "Danish"'
  'de     "German"'
  'el     "Greek"'
  'en_GB  "British English"'
  'es     "Spanish"'
  'et     "Estonian"'
  'fi     "Finnish"'
  'fr     "French"'
  'gl     "Galician"'
  'hu     "Hungarian"'
  'it     "Italian"'
  'ja     "Japanese"'
  'kk     "Kazakh"'
  'nb     "Norwegian Bokmal"'
  'nl     "Dutch"'
  'pl     "Polish"'
  'pt     "Portuguese"'
  'pt_BR  "Brazilian Portuguese"'
  'ru     "Russian"'
  'sk     "Slovak"'
  'sv     "Swedish"'
  'uk     "Ukrainian"'
  'zh_CN  "Chinese Simplified"'
  'zh_TW  "Chinese Traditional"'
)

pkgname=()
source=()

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}
  _pkgname=calligra-l10n-${_locale,,}
  
  pkgname+=($_pkgname)
  source+=("http://download.kde.org/stable/calligra-${pkgver}/${pkgbase}/${pkgbase}-$_locale-${pkgver}.tar.xz")
  eval "package_$_pkgname() {
    _package $_lang
  }"
done

md5sums=('5c6459eb4dffe6bf9f75ad39f4648165'
         'f84bc429dcbf7c622338f68a0dd52773'
         'cb6f2273c4b7e7208c98e4f896e34fb2'
         '71b04ece0bf73e0403fa6f318aa39f99'
         '32d89d0b7c006c366f79cc63d6554a8e'
         '54acdac0e6e2559f896ab8207b49341b'
         '7d9c33670daa3d6faaa63158b678ac53'
         '29eac1e253f83206effa198a5d6862e8'
         'b0143072e41a8b5de27157d4bff1fca8'
         '6e8decc43bf0c2b7fcbd923c60ce6fc5'
         '3085a1383b0698ba96ce81ab271f046b'
         '108135435df5428c963509091cb02dc7'
         '0a025604c602837ba7ff3ee438b47575'
         '1f35e6863adc78931d9b1af2bbffba00'
         'a28cb1daa6acccd3b7a4981ef4e1ab85'
         '3eb3b3c32ea80ed9a8e0c813438d4ca0'
         '083ee522c5649874d87c512ba468ff31'
         '636c381ddb937c3ca1d4128f5547c782'
         '54748c2091e8e686e995c1a45c88b21c'
         '0562dc6650d96702ee4f24a69ee8b644'
         '325487455521a3d0bfa22340a65cf91a'
         'bc07c506e9e602fad8c0d2bd4fa74ef6'
         '48c9c77c73a36c3beaf0cf4f5792461e'
         '96235bed627011d3d2ba91794a6ad659'
         'ce531462a3e2af48353212f8a4062629'
         '37f6d873659aed7cd32fa134877d865f'
         '1a295eef214dc59b8a09d86e9e03269b'
         '9a0269924fbd19cc8f21a4afe13987aa')

build() {
  for _lang in "${_languages[@]}"; do
    _locale=${_lang%% *}
    
    mkdir -p build/$_locale
    cd build/$_locale
    cmake $srcdir/$pkgbase-$_locale-$pkgver \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX=/usr
    make
    cd ../..
  done
}

_package() {
  pkgdesc="$2 localization for Calligra"
  cd build/$1
  make DESTDIR="$pkgdir" install
}
