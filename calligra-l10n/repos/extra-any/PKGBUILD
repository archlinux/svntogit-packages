# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgbase=calligra-l10n
pkgver=2.9.1
pkgrel=1
pkgdesc="Localization for Calligra"
arch=('any')
url='http://www.calligra-suite.org/'
license=('LGPL')
makedepends=('cmake' 'automoc4' 'kdelibs')
options=('docs')

_languages=(
  'bs     "Bosnian"'
  'ca     "Catalan"'
  'ca@valencia     "Catalan (Valencian)"'
  'cs     "Czech"'
  'da     "Danish"'
  'de     "German"'
  'el     "Greek"'
  'en_GB  "British English"'
  'es     "Spanish"'
  'et     "Estonian"'
  'fi     "Finnish"'
  'fr     "French"'
  'gl     "Galician"'
  'hu     "Hungarian"'
  'it     "Italian"'
  'ja     "Japanese"'
  'kk     "Kazakh"'
  'nb     "Norwegian Bokmal"'
  'nl     "Dutch"'
  'pl     "Polish"'
  'pt     "Portuguese"'
  'pt_BR  "Brazilian Portuguese"'
  'ru     "Russian"'
  'sk     "Slovak"'
  'sv     "Swedish"'
  'tr     "Turkish"'
  'uk     "Ukrainian"'
  'zh_CN  "Chinese Simplified"'
  'zh_TW  "Chinese Traditional"'
)

pkgname=()
source=()

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}
  _pkgname=calligra-l10n-${_locale,,}
  
  pkgname+=($_pkgname)
  source+=("http://download.kde.org/stable/calligra-${pkgver}/${pkgbase}/${pkgbase}-$_locale-${pkgver}.tar.xz")
  eval "package_$_pkgname() {
    _package $_lang
  }"
done

md5sums=('cba77be979019517c35aeb1411061f74'
         'd2a492476166abe353b722d3be83f484'
         '86174201d1a67ee486699e39e792afe5'
         'ed4fe0690d0f55776564066c469aff31'
         '2e1aa8454f058d60d6de027bf9e6013e'
         '462bdf3a1c357162947a2ac78193c0c8'
         '686701808bb98526e93a3122d8a72211'
         'f72642f72021426bd366d2b82703fe92'
         '23e37fd33625b3cb65864c86044cc307'
         '0b2f118ccb93256750e93da95af9922c'
         '9e2537324e6b896c9f174289d44d2401'
         '333d06893bf797ae2f15374950431379'
         '2f5092542e13c984a07fa06a13071de5'
         '6f74a2430e93a8212b52ea4a7f3db80c'
         '6cc374e9019fb4c2f4096ab167c31787'
         'cd144c35a06e68c7800235f82a34117b'
         'adf1689fb17cbe2bee3f00c8de68d26b'
         '0747c067bbb212f1c5011e2b21ff3fff'
         'b2c3a57f77fd2d07b51bdddc5390a81f'
         'd94a51ff6dda4cbfe957d192775a738e'
         '68627ac4b6594297baee20c1ae1a6313'
         '7f93061b116d0e3bb8a42230d721f52b'
         '790ea91505d5754564651a9797ad5b27'
         '2fb7ac4e427fb4f94b7a2f03b0df59db'
         '462715b91ff71b696f6c592e2879b453'
         '732a10099b75a3068646afcb154bdeee'
         'f085a670997dbc599dae1bd18ecfb6f3'
         '35061f847c9df4a7d618dc2bfbd5e3d2'
         '653556c54ee517d685bfdd274610d00f')

build() {
  for _lang in "${_languages[@]}"; do
    _locale=${_lang%% *}
    
    mkdir -p build/$_locale
    cd build/$_locale
    cmake $srcdir/$pkgbase-$_locale-$pkgver \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX=/usr
    make
    cd ../..
  done
}

_package() {
  pkgdesc="$2 localization for Calligra"
  cd build/$1
  make DESTDIR="$pkgdir" install
}
