# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgbase=calligra-l10n
pkgver=2.9.6
pkgrel=1
pkgdesc="Localization for Calligra"
arch=('any')
url='http://www.calligra-suite.org/'
license=('LGPL')
makedepends=('cmake' 'automoc4' 'kdelibs')
options=('docs')

_languages=(
  'bs     "Bosnian"'
  'ca     "Catalan"'
  'ca@valencia     "Catalan (Valencian)"'
  'cs     "Czech"'
  'da     "Danish"'
  'de     "German"'
  'el     "Greek"'
  'en_GB  "British English"'
  'es     "Spanish"'
  'et     "Estonian"'
  'fi     "Finnish"'
  'fr     "French"'
  'gl     "Galician"'
  'hu     "Hungarian"'
  'it     "Italian"'
  'ja     "Japanese"'
  'kk     "Kazakh"'
  'nb     "Norwegian Bokmal"'
  'nl     "Dutch"'
  'pl     "Polish"'
  'pt     "Portuguese"'
  'pt_BR  "Brazilian Portuguese"'
  'ru     "Russian"'
  'sk     "Slovak"'
  'sv     "Swedish"'
  'uk     "Ukrainian"'
  'zh_CN  "Chinese Simplified"'
  'zh_TW  "Chinese Traditional"'
)

pkgname=()
source=()

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}
  _pkgname=calligra-l10n-${_locale,,}
  
  pkgname+=($_pkgname)
  source+=("http://download.kde.org/stable/calligra-${pkgver}/${pkgbase}/${pkgbase}-$_locale-${pkgver}.tar.xz")
  eval "package_$_pkgname() {
    _package $_lang
  }"
done

md5sums=('15d8651b6175d198f7a9f3e1c90ae8fd'
         '0befb2ddfc294427e8e35ffee290a956'
         'fc15c2255d182eb869fc28a1603efa04'
         '58a1b0b2d524b270457096803f2bded5'
         '2a0583e77368e5feee5c6bed3c2de89f'
         '715a52f56c41b776acf870bcf0df3591'
         'faf4f6e78684e57865106822148b8c3b'
         '28eec40b0065f056de5d44bc06c229c1'
         '7d8f994234911e475d81f667cca9dd4a'
         'f39485124287adc007dab3642b57eb9a'
         '0eeab69fef10dae98c921c5ad127812c'
         '6491144954bcdb75c3438ebf47797220'
         '9e0c1531241f0774c70c9dcd0836abb8'
         'cd48ca919ffb1ee0152325eec19f4706'
         '215fc821eac11759802c68c2b385f9b6'
         'b8f729b6ebba19cb6a796513a21d6ba0'
         '6b40753e50a400200182250f056bee56'
         'ef9c45f9dfb25307b5f2cbbd46975ab6'
         '41d4a7b398cd4fe1dd63cf898c997501'
         'fbc4dbdce3a80d6ae5582c42272fcad6'
         'a874dd9c4edb591a4f84fe00c120bab0'
         '446a54dfbb7b45712fc2b8981e127926'
         '14d76da4021541686dd5d793f70d9029'
         '1a915d0f6df72c8a1fde8e34d658b6e4'
         '51dbdd45beda57deae4b30f2b0d49fe4'
         '8c81a876eb06a89c93094b5d392f52c0'
         'a4aa2f21f565eea3e31f84e9c2d9b8f9'
         '324cb2be2ba0b9e1e1aa69e3315da683')

build() {
  for _lang in "${_languages[@]}"; do
    _locale=${_lang%% *}
    
    mkdir -p build/$_locale
    cd build/$_locale
    cmake $srcdir/$pkgbase-$_locale-$pkgver \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX=/usr
    make
    cd ../..
  done
}

_package() {
  pkgdesc="$2 localization for Calligra"
  cd build/$1
  make DESTDIR="$pkgdir" install
}
