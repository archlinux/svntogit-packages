# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgbase=calligra-l10n
pkgver=2.9.2
pkgrel=1
pkgdesc="Localization for Calligra"
arch=('any')
url='http://www.calligra-suite.org/'
license=('LGPL')
makedepends=('cmake' 'automoc4' 'kdelibs')
options=('docs')

_languages=(
  'bs     "Bosnian"'
  'ca     "Catalan"'
  'ca@valencia     "Catalan (Valencian)"'
  'cs     "Czech"'
  'da     "Danish"'
  'de     "German"'
  'el     "Greek"'
  'en_GB  "British English"'
  'es     "Spanish"'
  'et     "Estonian"'
  'fi     "Finnish"'
  'fr     "French"'
  'gl     "Galician"'
  'hu     "Hungarian"'
  'it     "Italian"'
  'ja     "Japanese"'
  'kk     "Kazakh"'
  'nb     "Norwegian Bokmal"'
  'nl     "Dutch"'
  'pl     "Polish"'
  'pt     "Portuguese"'
  'pt_BR  "Brazilian Portuguese"'
  'ru     "Russian"'
  'sk     "Slovak"'
  'sv     "Swedish"'
  'tr     "Turkish"'
  'uk     "Ukrainian"'
  'zh_CN  "Chinese Simplified"'
  'zh_TW  "Chinese Traditional"'
)

pkgname=()
source=()

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}
  _pkgname=calligra-l10n-${_locale,,}
  
  pkgname+=($_pkgname)
  source+=("http://download.kde.org/stable/calligra-${pkgver}/${pkgbase}/${pkgbase}-$_locale-${pkgver}.tar.xz")
  eval "package_$_pkgname() {
    _package $_lang
  }"
done

md5sums=('46abda11730b2855396da5c49be8118d'
         '5dfd8a63a543ccc031d6691300fc14d9'
         '12d6a77988ec5776d935c0278c464f6f'
         '8be7e15d3c49ce2f1a23f98215e76fe0'
         '6343ff6c408f89fb8a54fb284c0f6653'
         'b732a3c8a46268b02c393f7da6983651'
         'd08e35dedf029b2ad6343697a227db33'
         'da5f5eaaa8c0e4694a7d5620349300ca'
         'e5b63067ab038df231197a7ebed6ce68'
         '1e72fb67933711140d822af999b3a483'
         '77ae0624ee424f99b1248c429ce1d8af'
         'effa0b3806dcb230294ca9e9e5d64c42'
         '3c77bf246bb7e4663e260d714a8d4920'
         '48059f6475940f96e89554ed3a5fa4ce'
         'f9240d55cb05a1af64f50bcabc2d6866'
         '7a4dc57842926e644cdaf462a3b8bddf'
         '0df375c87b322eaf59bc463c741d4fc6'
         '4be303ea33737e8bb082bc55cf481b06'
         '5a53ebae95d6dd082f606489c4073467'
         '0034bffa695f030a54ead482e651b934'
         'f86648ecee94c2c6ed006a3dbd8a88f8'
         'b3ce8fc10b4be2d7ccad9205522b69c3'
         '2070b40dfd4ecf860a761a327f3463fc'
         'fa9b7fb496b948845462d87b82c12362'
         '04fc1e7c595e34dc12d008ad5b70bda6'
         '71749e6c0d1e2c8262bd1e871fdab090'
         '7d8475a9a1d1ab4469089902f4460534'
         'e66bdc04698f29d074bd874ba2ac713b'
         '2626d400c350627494b4d5937765ccb5')

build() {
  for _lang in "${_languages[@]}"; do
    _locale=${_lang%% *}
    
    mkdir -p build/$_locale
    cd build/$_locale
    cmake $srcdir/$pkgbase-$_locale-$pkgver \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX=/usr
    make
    cd ../..
  done
}

_package() {
  pkgdesc="$2 localization for Calligra"
  cd build/$1
  make DESTDIR="$pkgdir" install
}
