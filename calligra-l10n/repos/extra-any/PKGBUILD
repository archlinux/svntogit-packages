# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgbase=calligra-l10n
pkgver=2.9.11
pkgrel=1
pkgdesc="Localization for Calligra"
arch=('any')
url='http://www.calligra-suite.org/'
license=('LGPL')
makedepends=('cmake' 'automoc4' 'kdelibs')
options=('docs')

_languages=(
  'bs     "Bosnian"'
  'ca     "Catalan"'
  'ca@valencia     "Catalan (Valencian)"'
  'cs     "Czech"'
  'da     "Danish"'
  'de     "German"'
  'el     "Greek"'
  'en_GB  "British English"'
  'es     "Spanish"'
  'et     "Estonian"'
  'fi     "Finnish"'
  'fr     "French"'
  'gl     "Galician"'
  'hu     "Hungarian"'
  'it     "Italian"'
  'ja     "Japanese"'
  'kk     "Kazakh"'
  'nb     "Norwegian Bokmal"'
  'nl     "Dutch"'
  'pl     "Polish"'
  'pt     "Portuguese"'
  'pt_BR  "Brazilian Portuguese"'
  'ru     "Russian"'
  'sk     "Slovak"'
  'sv     "Swedish"'
  'uk     "Ukrainian"'
  'zh_CN  "Chinese Simplified"'
  'zh_TW  "Chinese Traditional"'
)

pkgname=()
source=()

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}
  _pkgname=calligra-l10n-${_locale,,}
  
  pkgname+=($_pkgname)
  source+=("http://download.kde.org/stable/calligra-${pkgver}/${pkgbase}/${pkgbase}-$_locale-${pkgver}.tar.xz")
  eval "package_$_pkgname() {
    _package $_lang
  }"
done

md5sums=('f87a80d5f9ef18e5bce4f9b546ac54f2'
         'a2d73c39ee84ee2f69842509da092f0b'
         '946784625e39f505330bf9cf9895cd32'
         'e644cdb5e8694b9dfd1937877d461710'
         '02a50ae334a195845fbd9b58730c8a50'
         '99f7c7354f593147a384850b4f365a8b'
         '6d54da69aa0d5b79f37edae7b8edb726'
         'f3e020a8daefa5f26485e01ffa8a4520'
         '83760e705c0dfeaa2f4f098305da3f79'
         '20a2d9f1ceb5e1b31faee72bf5fb4d0f'
         'ef1fd56d9871a5235d66083bb5132968'
         '3042e50532305db27624be2cc4d78394'
         'f7f539b7a32710502176b0a157aa5057'
         '90ac8f87fb1f76692e19532d9e7246e6'
         '602972907def3acfd0023fd3019a1512'
         '5755ce1e4d163fc07de6b4bb2abcb6e2'
         '89080fa5d2448f84b6c406a5c206261c'
         '6af6cfbae29929718e37b73f4e574b8a'
         '7c07d0a8e4722286cd75bd669e141ed2'
         '3598ca1fdcadbee772bbfbfd2e61836b'
         '8029eee21ad33758a9b705f1c987d83b'
         'cf743c38b3bc4d886f3fa709df8aabb9'
         '23be50c6e72729526fb649a697215e69'
         '306843e947b62056dca378c5898fd68c'
         '5058af16b0a81e1087bd2a9226850ce8'
         '830cb6222edb3ad805ec21a9466f4e81'
         'c3b5b8c568920fdfe9719b5da524a101'
         '1d6acdd91f14331b465c2365272d0a2d')

build() {
  for _lang in "${_languages[@]}"; do
    _locale=${_lang%% *}
    
    mkdir -p build/$_locale
    cd build/$_locale
    cmake $srcdir/$pkgbase-$_locale-$pkgver \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX=/usr
    make
    cd ../..
  done
}

_package() {
  pkgdesc="$2 localization for Calligra"
  cd build/$1
  make DESTDIR="$pkgdir" install
}
