# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgbase=calligra-l10n
pkgver=2.9.7
pkgrel=1
pkgdesc="Localization for Calligra"
arch=('any')
url='http://www.calligra-suite.org/'
license=('LGPL')
makedepends=('cmake' 'automoc4' 'kdelibs')
options=('docs')

_languages=(
  'bs     "Bosnian"'
  'ca     "Catalan"'
  'ca@valencia     "Catalan (Valencian)"'
  'cs     "Czech"'
  'da     "Danish"'
  'de     "German"'
  'el     "Greek"'
  'en_GB  "British English"'
  'es     "Spanish"'
  'et     "Estonian"'
  'fi     "Finnish"'
  'fr     "French"'
  'gl     "Galician"'
  'hu     "Hungarian"'
  'it     "Italian"'
  'ja     "Japanese"'
  'kk     "Kazakh"'
  'nb     "Norwegian Bokmal"'
  'nl     "Dutch"'
  'pl     "Polish"'
  'pt     "Portuguese"'
  'pt_BR  "Brazilian Portuguese"'
  'ru     "Russian"'
  'sk     "Slovak"'
  'sv     "Swedish"'
  'uk     "Ukrainian"'
  'zh_CN  "Chinese Simplified"'
  'zh_TW  "Chinese Traditional"'
)

pkgname=()
source=()

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}
  _pkgname=calligra-l10n-${_locale,,}
  
  pkgname+=($_pkgname)
  source+=("http://download.kde.org/stable/calligra-${pkgver}/${pkgbase}/${pkgbase}-$_locale-${pkgver}.tar.xz")
  eval "package_$_pkgname() {
    _package $_lang
  }"
done

md5sums=('e887ee1798750fc1ddad3bd940b19e31'
         '97364b3650deee41d6771f9899149857'
         '90a38be9cf4867908972175906c26959'
         'fc6811095a9c1e3c66cec25f60140052'
         'a49edf952c0e299b7f5bba633ce5aea2'
         '1684a36e5e1c7fe0bb023ce17f2af1df'
         '411a6f14595f421d2072c4a072540a4a'
         'f5302c87dc436f071a10e7682f42e5bc'
         '8ce0647b45163abf4c8f032046107a26'
         '888514998136f94edb469db44a4f4470'
         '271818bf3a90d4db2b11774274ce8341'
         '2c1587ad975ea8a1453ae1b4179a54dd'
         'ab94fe4f015323fdcf1e7d0bc497dc97'
         '8de5dd1d7694b02fc764f0f4dc4bf0e3'
         '85de3b6c6fae7c9aadee0d8d4467bbbd'
         'f166f87fea53e6cb8c0add26eaa08147'
         '51364e2566389b2358946a28f5a79c3b'
         '364a016be8de54b9f9d9109b06e2c1cc'
         '11c63431c6c2fc26d623bc0a85658111'
         '4ea0db3d79a36f8b5f7583373ae8b291'
         '0b1450b7e92687254f2115a3a043bfa6'
         'a83760c7bb4c08d270dcb9cc037a5b33'
         '450f01a510723ea3e0458a1f64a6f6dd'
         '4756a82583c9cb094afe870a8ca18ea9'
         'eb46b8bc81c124c1363469afa903d09f'
         'd994ed816979cd9d4dc719a173119d21'
         'e2c5bf88ca392b5aefbd35abd0fa8e6c'
         'fdf231a46ec9815bdb0c353d022d6c15')

build() {
  for _lang in "${_languages[@]}"; do
    _locale=${_lang%% *}
    
    mkdir -p build/$_locale
    cd build/$_locale
    cmake $srcdir/$pkgbase-$_locale-$pkgver \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX=/usr
    make
    cd ../..
  done
}

_package() {
  pkgdesc="$2 localization for Calligra"
  cd build/$1
  make DESTDIR="$pkgdir" install
}
