# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgbase=calligra-l10n
pkgver=2.9.3
pkgrel=1
pkgdesc="Localization for Calligra"
arch=('any')
url='http://www.calligra-suite.org/'
license=('LGPL')
makedepends=('cmake' 'automoc4' 'kdelibs')
options=('docs')

_languages=(
  'bs     "Bosnian"'
  'ca     "Catalan"'
  'ca@valencia     "Catalan (Valencian)"'
  'cs     "Czech"'
  'da     "Danish"'
  'de     "German"'
  'el     "Greek"'
  'en_GB  "British English"'
  'es     "Spanish"'
  'et     "Estonian"'
  'fi     "Finnish"'
  'fr     "French"'
  'gl     "Galician"'
  'hu     "Hungarian"'
  'it     "Italian"'
  'ja     "Japanese"'
  'kk     "Kazakh"'
  'nb     "Norwegian Bokmal"'
  'nl     "Dutch"'
  'pl     "Polish"'
  'pt     "Portuguese"'
  'pt_BR  "Brazilian Portuguese"'
  'ru     "Russian"'
  'sk     "Slovak"'
  'sv     "Swedish"'
  'uk     "Ukrainian"'
  'zh_CN  "Chinese Simplified"'
  'zh_TW  "Chinese Traditional"'
)

pkgname=()
source=()

for _lang in "${_languages[@]}"; do
  _locale=${_lang%% *}
  _pkgname=calligra-l10n-${_locale,,}
  
  pkgname+=($_pkgname)
  source+=("http://download.kde.org/stable/calligra-${pkgver}/${pkgbase}/${pkgbase}-$_locale-${pkgver}.tar.xz")
  eval "package_$_pkgname() {
    _package $_lang
  }"
done

md5sums=('3c7627839f77d3400c0a30ac37d3f3f0'
         'd08b198e0067bd5cdeb3427156b78f31'
         '38f6412f07d1f7a6c3a2b673b08ce877'
         'fa6dcaa9d1383bdf1dd0d1dcc357fb73'
         '7b4978bcab76f255afd240e00b1e7422'
         'f6e970a14a33bbe8df680e4f00404ba0'
         'c09cdbc712c4752e6351b3130d7f09a0'
         '4d6387b9b48909021716edd8428e1cd2'
         '19adb03876480cd46cfa08631ab0c714'
         'f129516d83a89f757ca0bea5d8807ea0'
         '9c711e6d6e1b4059cb19ebc3ffb094b2'
         'af6bd3d2a17d36be13fa0e25720d228b'
         '7a0411791618f6861f195f34d69b0300'
         '13e308e690006beba5cfef14807efbee'
         '1f6fa2b85a28c27504dde1419f21a6f6'
         '601acb26f8c6bf718f22ada6a5483cf5'
         '38b7bde1b22b7897b934dc94e7b2f612'
         'c0e9deb4b4d143782ac7b55b47a3f9dc'
         '1259cc3ba2927ca7d64c216f51e55f59'
         '320d6cae2c91b571575a9d9495d71a58'
         'd74825e82ac8ead660ca48b17c9ef787'
         'd7b4da7fa722575334529c91bd4e7b14'
         '47de04130b223a2c726bc9ece98d1f98'
         '78334532879a20035b72d5eac58b397c'
         'c39e73261ebaa1b22ebc82a52073fc2c'
         'dc660171238fd9221201f73d0c622281'
         '4df00fe1c254a63d4fcd2cbb5e0700e8'
         '923edb520f9252a01df937bd1b974f6a')

build() {
  for _lang in "${_languages[@]}"; do
    _locale=${_lang%% *}
    
    mkdir -p build/$_locale
    cd build/$_locale
    cmake $srcdir/$pkgbase-$_locale-$pkgver \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX=/usr
    make
    cd ../..
  done
}

_package() {
  pkgdesc="$2 localization for Calligra"
  cd build/$1
  make DESTDIR="$pkgdir" install
}
