# $Id$
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: riai <riai@bigfoot.com>, Ben <ben@benmazer.net>

pkgname=eric4
pkgver=4.4.18
pkgrel=1
pkgdesc="A full-featured Python 2.x and Ruby IDE in PyQt4"
arch=('any')
url="http://eric-ide.python-projects.org/"
license=('GPL3')
depends=('python2-qt' 'python2-qscintilla')
conflicts=('eric<5')
install=$pkgname.install
changelog=$pkgname.changelog
source=("http://downloads.sourceforge.net/eric-ide/$pkgname-$pkgver.tar.gz"
        "$pkgname.desktop")
md5sums=('fa53c6a2f9a1f0da35d6f7daac6d003a'
         'f609e117d5132c055a5868b74e13ac77')

package() {
	cd "$srcdir/$pkgname-$pkgver"

	# our pyxml is patched
	# not version-depending because not critical
	sed -i 's/v < (0, 8, 6)/v < (0, 8, 4)/' install.py

	python2 install.py -c -b /usr/bin -i "$pkgdir"

	sed -i 's/; /\n/' \
    "$pkgdir/usr/lib/python2.7/site-packages/eric4.pth"

  # freedesktop.org compatibility
	install -Dm644 "$srcdir/$pkgname.desktop" \
		"$pkgdir/usr/share/applications/$pkgname.desktop"
	install -Dm644 eric/icons/default/eric.png \
		"$pkgdir/usr/share/pixmaps/$pkgname.png"
}

# vim:set ts=2 sw=2 et:
