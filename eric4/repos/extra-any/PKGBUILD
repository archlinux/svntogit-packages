# $Id$
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# See .contrib for older/other contributors

pkgname=eric4
pkgver=4.5.17
pkgrel=1
pkgdesc="A full-featured Python 2.x and Ruby IDE in PyQt4"
arch=('any')
url="http://eric-ide.python-projects.org/"
license=('GPL3')
depends=('python2-qscintilla')
conflicts=('eric<5')
install=$pkgname.install
source=("http://downloads.sourceforge.net/eric-ide/$pkgname-$pkgver.tar.gz"
        "$pkgname.desktop")
md5sums=('4dfeb662c30ae21c7efe6ed0f45d2aef'
         'bce515b66a2c63dfc76a0f92edbe2ecb')

package() {
  cd "$srcdir/$pkgname-$pkgver"

  python2 install.py -c -b /usr/bin -i "$pkgdir"

  # freedesktop.org compatibility
  install -Dm644 "$srcdir/$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 eric/icons/default/eric.png \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
}

# vim:set ts=2 sw=2 et:
