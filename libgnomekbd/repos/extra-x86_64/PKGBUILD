# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=libgnomekbd
pkgver=3.28.0
pkgrel=1
epoch=1
pkgdesc="Keyboard management library"
url="https://gitlab.gnome.org/GNOME/libgnomekbd"
arch=(x86_64)
license=(LGPL)
depends=(libxklavier gtk3 dconf)
makedepends=(gobject-introspection git meson)
provides=(libgnomekbd{,ui}.so)
options=(debug)
_commit=358fcc6570d5910a5cedae2dbb3be58519ec1876  # tags/3.28.0^0
source=("git+https://gitlab.gnome.org/GNOME/libgnomekbd.git#commit=$_commit"
        meson.diff)
sha256sums=('SKIP'
            '2d78355197d67758dc853d42f165ee03aa1c88d7a1c5a263c24da725836226b4')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname

  # Fixes for meson build
  git apply -3 ../meson.diff
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
