# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Deon Spengler <deon@spengler.co.za>

pkgname=ddcutil
pkgver=1.2.1
pkgrel=1
pkgdesc='Query and change Linux monitor settings using DDC/CI and USB.'
url='http://ddcutil.com/'
arch=('x86_64')
license=('GPL2')
depends=('glib2' 'i2c-tools' 'libusb' 'libdrm' 'libxrandr')
makedepends=('systemd')
source=(https://github.com/rockowitz/ddcutil/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('f80c45ed47a059c71ddf7dc56e8daf9b666120960d1911175d390f95ab9691a62cffb7ce4555ddc418e3e9cfe7a575a70778c35809cdea598d7610dac915ccbc')

prepare() {
  cd $pkgname-$pkgver
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim: ft=sh ts=2 sw=2 et
