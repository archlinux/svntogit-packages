# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Paul Mattal <paul@archlinux.org>
# Contributor: Martin Kemp <mdkemp@elys.com>

pkgname=ethtool
pkgver=4.2
pkgrel=1
epoch=1
pkgdesc="Utility for controlling network drivers and hardware"
arch=('i686' 'x86_64')
url="https://www.kernel.org/pub/software/network/ethtool/"
license=('GPL')
depends=('glibc')
source=(https://www.kernel.org/pub/software/network/$pkgname/$pkgname-$pkgver.tar.{xz,sign})
md5sums=('8433e71a256678d859ecc264132063c5'
         'SKIP')
validpgpkeys=(AC2B29BD34A6AFDDB3F68F35E7BFC8EC95861109) # Ben Hutchings <bwh@kernel.org>

build() {
    cd $pkgname-$pkgver
    ./configure --prefix=/usr --mandir=/usr/share/man \
        --sbindir=/usr/bin
    make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
    cd $pkgname-$pkgver
    make DESTDIR="$pkgdir" install
}
