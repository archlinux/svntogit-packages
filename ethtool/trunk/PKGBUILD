# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Paul Mattal <paul@archlinux.org>
# Contributor: Martin Kemp <mdkemp@elys.com>

pkgname=ethtool
pkgver=3.16
pkgrel=1
epoch=1
pkgdesc="Utility for controlling network drivers and hardware"
arch=('i686' 'x86_64')
url="https://www.kernel.org/pub/software/network/ethtool/"
license=('GPL')
depends=('glibc')
source=(https://www.kernel.org/pub/software/network/$pkgname/$pkgname-$pkgver.tar.xz)
md5sums=('7eee202accb86104adc8463a36a1a468')

build() {
    cd $pkgname-$pkgver
    ./configure --prefix=/usr --mandir=/usr/share/man \
        --sbindir=/usr/bin
    make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
    cd $pkgname-$pkgver
    make DESTDIR="$pkgdir" install
}
