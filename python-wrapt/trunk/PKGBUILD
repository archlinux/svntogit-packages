# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Troy C < rstrox -ta yahoo -tod com >

pkgname=python-wrapt
pkgver=1.14.0
pkgrel=1
pkgdesc="A Python module for decorators, wrappers and monkey patching"
arch=("x86_64")
url="https://pypi.python.org/pypi/wrapt"
license=("BSD")
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://github.com/GrahamDumpleton/wrapt/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('ef31279873688208bb17d875a3bbdba3d506f5d9febeae28f1bdb740830395d86aa21dcbe747d592f098b745e5a599c352e122a642f923eab0eb39a30f19695d')

build() {
  cd wrapt-$pkgver
  python setup.py build
}

check() {
  cd wrapt-$pkgver
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-3.10:$PYTHONPATH" py.test
}

package() {
  cd wrapt-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
