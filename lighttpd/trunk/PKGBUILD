# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=lighttpd
pkgver=1.4.19
pkgrel=3
pkgdesc='a secure, fast, compliant and very flexible web-server'
license=('custom')
arch=('i686' 'x86_64')
url="http://www.lighttpd.net/"
depends=('pcre' 'openssl' 'zlib' 'bzip2' 'attr' 'libldap')
makedepends=('fcgi' 'libmysqlclient' 'lua' 'libxml2' 'e2fsprogs' 'sqlite3' 'gdbm' 'pkgconfig')
optdepends=('libxml2: mod_webdav' \
            'e2fsprogs: mod_webdav' \
            'lua: mod_cml/mod_magnet' \
            'libmysqlclient: mod_mysql_vhost' \
            'sqlite3: mod_webdav' \
            'gdbm: mod_trigger_b4_dl' \
            )
backup=('etc/lighttpd/lighttpd.conf' 'etc/logrotate.d/lighttpd')
options=('!libtool' 'emptydirs')
install='lighttpd.install'
source=("http://www.lighttpd.net/download/lighttpd-${pkgver}.tar.bz2" \
        'lighttpd.rc.d' 'lighttpd.logrotate.d')
md5sums=('d787374e4e4aaa09d5cfa9ab9d23ad40'
         '245930c049d2088af099b364964534bb'
         '857e174643fd7761a2f0d8431a679f6c')

build() {
	cd $srcdir/$pkgname-$pkgver
	./configure --prefix=/usr \
		--libexecdir=/usr/lib/lighttpd/modules \
		--sysconfdir=/etc/lighttpd \
		--sharedstatedir=/usr/var \
		--localstatedir=/var \
		--libdir=/usr/lib/lighttpd \
		--includedir=/usr/include/lighttpd \
		--with-mysql \
		--with-ldap \
		--with-attr \
		--with-openssl \
		--with-kerberos5 \
		--without-fam \
		--with-webdav-props \
		--with-webdav-locks \
		--with-gdbm \
		--with-memcache \
		--with-lua || return 1
	make || return 1
	make DESTDIR=$pkgdir install || return 1

	install -D -m755 ../lighttpd.rc.d $pkgdir/etc/rc.d/lighttpd
	install -D -m644 ../lighttpd.logrotate.d $pkgdir/etc/logrotate.d/lighttpd
	install -d -m755 $pkgdir/var/run/lighttpd/
	install -d -m755 -o http -g http $pkgdir/var/log/lighttpd/

	install -D -m644 doc/lighttpd.conf $pkgdir/etc/lighttpd/lighttpd.conf

	# set sane defaults
	sed -e 's|/srv/www/htdocs/|/srv/http/|' \
	    -e 's|/srv/www/|/srv/http/|' \
	    -e 's|#server.username            = "wwwrun"|server.username            = "http"|' \
	    -e 's|#server.groupname           = "wwwrun"|server.groupname           = "http"|' \
	    -e 's|#server.pid-file            = "/var/run/lighttpd.pid"|server.pid-file            = "/var/run/lighttpd/lighttpd.pid"|' \
	    -e 's|/usr/local/bin/php-cgi|/usr/bin/php-cgi|' \
	    -i ${pkgdir}/etc/lighttpd/lighttpd.conf || return 1

	install -D -m644 COPYING $pkgdir/usr/share/licenses/$pkgname/COPYING
}