# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=lighttpd
pkgver=1.4.30
pkgrel=3
pkgdesc='a secure, fast, compliant and very flexible web-server'
license=('custom')
arch=('i686' 'x86_64')
url="http://www.lighttpd.net/"
depends=('pcre' 'bzip2' 'libldap' 'util-linux')
makedepends=('fcgi' 'libmysqlclient' 'lua' 'libxml2' 'e2fsprogs' 'sqlite3' 'gdbm' 'pkgconfig')
optdepends=('libxml2: mod_webdav'
            'lua: mod_cml/mod_magnet'
            'libmysqlclient: mod_mysql_vhost'
            'sqlite3: mod_webdav')
backup=('etc/lighttpd/lighttpd.conf' 'etc/logrotate.d/lighttpd')
options=('!libtool' 'emptydirs')
source=("http://download.lighttpd.net/lighttpd/releases-1.4.x/lighttpd-${pkgver}.tar.xz"
        "http://download.lighttpd.net/lighttpd/releases-1.4.x/lighttpd-${pkgver}.tar.xz.asc"
        'lighttpd.rc.d' 'lighttpd.logrotate.d' 'lighttpd.conf')
sha256sums=('c237692366935b19ef8a6a600b2f3c9b259a9c3107271594c081a45902bd9c9b'
            '941ab747aeb97ca111fb670d21eefecdf745debc5e139bc5bd6881ee66b2742a'
            'ceff3a88e876f6104c094379d507b89bc504777d0f98a0930406a70624fcf024'
            '550aa1abaced6bc9e8d65c2b069bfc96bf4d06734b17eeb140c5d00705de1230'
            'ee56422fe48f2683ccb5ca2e3dc6bad79ea8e1cbd043b21d2ea73b87018e35aa')

build() {
	cd $srcdir/$pkgname-$pkgver

	./configure --prefix=/usr \
		--libexecdir=/usr/lib/lighttpd/modules \
		--sysconfdir=/etc/lighttpd \
		--with-mysql \
		--with-ldap \
		--with-attr \
		--with-openssl \
		--with-kerberos5 \
		--without-fam \
		--with-webdav-props \
		--with-webdav-locks \
		--with-gdbm \
		--with-memcache \
		--with-lua
	make
}

check() {
	cd $srcdir/$pkgname-$pkgver
	make check
}

package() {
	cd $srcdir/$pkgname-$pkgver
	make DESTDIR=$pkgdir install

	install -D -m755 $srcdir/lighttpd.rc.d $pkgdir/etc/rc.d/lighttpd
	install -D -m644 $srcdir/lighttpd.logrotate.d $pkgdir/etc/logrotate.d/lighttpd
	install -D -m644 $srcdir/lighttpd.conf $pkgdir/etc/lighttpd/lighttpd.conf
	install -d -m755 -o http -g http $pkgdir/var/{log,cache}/lighttpd/

	pushd doc/config >/dev/null
	find . -type f ! -name 'Makefile*' -exec install -D -m644 {} ${pkgdir}/usr/share/doc/lighttpd/config/{} \;
	popd >/dev/null

	install -D -m644 COPYING $pkgdir/usr/share/licenses/$pkgname/COPYING
}
