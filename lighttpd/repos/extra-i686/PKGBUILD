# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=lighttpd
pkgver=1.4.33
pkgrel=3
pkgdesc='A secure, fast, compliant and very flexible web-server'
license=('custom')
arch=('i686' 'x86_64')
url="http://www.lighttpd.net/"
depends=('pcre' 'bzip2' 'libldap' 'util-linux' 'systemd')
makedepends=('fcgi' 'libmariadbclient' 'lua51' 'libxml2' 'e2fsprogs' 'sqlite' 'gdbm' 'pkgconfig')
optdepends=('libxml2: mod_webdav'
            'lua51: mod_cml/mod_magnet'
            'libmariadbclient: mod_mysql_vhost'
            'sqlite: mod_webdav')
backup=('etc/lighttpd/lighttpd.conf' 'etc/logrotate.d/lighttpd')
options=('emptydirs')
install='lighttpd.install'
source=("http://download.lighttpd.net/lighttpd/releases-1.4.x/lighttpd-${pkgver}.tar.xz"
        "http://download.lighttpd.net/lighttpd/releases-1.4.x/lighttpd-${pkgver}.tar.xz.asc"
        "http://download.lighttpd.net/lighttpd/security/lighttpd-1.4.33_fix_setuid.patch"
        "http://download.lighttpd.net/lighttpd/security/lighttpd-1.4.33_fix_setuid.patch.asc"
        "http://download.lighttpd.net/lighttpd/security/lighttpd-1.4.33_fix_fam_use_after_free.patch"
        "http://download.lighttpd.net/lighttpd/security/lighttpd-1.4.33_fix_fam_use_after_free.patch.asc"
        "http://download.lighttpd.net/lighttpd/security/lighttpd-1.4.33_fix_ssl_sni.patch"
        "http://download.lighttpd.net/lighttpd/security/lighttpd-1.4.33_fix_ssl_sni.patch.asc"
        'lighttpd.logrotate.d' 'lighttpd.conf' 'lighttpd.tmpfiles' 'lighttpd.service')
sha256sums=('2886aedc23857ca44df91b8fe6f36059ec82a859ae0eb230220e42abc331610c'
            'SKIP'
            '87792bcf62a554ac094263b931a8ae369d975b7e6bf83778f8f0a6c0ff31b979'
            'SKIP'
            '1772cae2a0678c93dd5fa54f2622a563c3eb0a0047ba2a6648a5680d50d3bfde'
            'SKIP'
            'babf177f2e88a1444550825ce8e8ad1a95dbc78cfaa59c2c3f121ccbb37799d2'
            'SKIP'
            '41f6c0042bb61021553779f861910e335834f6c15e4411756cdc6233b31076fe'
            'fece4581bebf39768571962dedce176b2b5f487c0abb5c1cfb35395de216c01f'
            'd8a185145a7c08b4fd8c8e6c12dae3e176389dd9b1c66e239757b2ba5108c871'
            '503fd8557297f4fb814d74effcb59633135b3c2407a87f5abe97467f0822c147')

prepare() {
	cd $srcdir/$pkgname-$pkgver

	# http://download.lighttpd.net/lighttpd/security/lighttpd_sa_2013_02.txt
	patch -p1 -i $srcdir/lighttpd-1.4.33_fix_setuid.patch
	# http://download.lighttpd.net/lighttpd/security/lighttpd_sa_2013_03.txt
	patch -p1 -i $srcdir/lighttpd-1.4.33_fix_fam_use_after_free.patch
	# http://download.lighttpd.net/lighttpd/security/lighttpd_sa_2013_01.txt
	patch -p1 -i $srcdir/lighttpd-1.4.33_fix_ssl_sni.patch
}

build() {
	cd $srcdir/$pkgname-$pkgver

	./configure --prefix=/usr \
		--sbindir=/usr/bin \
		--libdir=/usr/lib/lighttpd/ \
		--sysconfdir=/etc/lighttpd \
		--with-mysql \
		--with-ldap \
		--with-attr \
		--with-openssl \
		--with-kerberos5 \
		--without-fam \
		--with-webdav-props \
		--with-webdav-locks \
		--with-gdbm \
		--with-memcache \
		--with-lua
	make
}

check() {
	cd $srcdir/$pkgname-$pkgver
	make check
}

package() {
	cd $srcdir/$pkgname-$pkgver
	make DESTDIR=$pkgdir install

	install -D -m644 $srcdir/lighttpd.logrotate.d $pkgdir/etc/logrotate.d/lighttpd
	install -D -m644 $srcdir/lighttpd.conf $pkgdir/etc/lighttpd/lighttpd.conf
	install -d -m755 -o http -g http $pkgdir/var/{log,cache}/lighttpd/
	install -D -m644 ${srcdir}/lighttpd.tmpfiles ${pkgdir}/usr/lib/tmpfiles.d/lighttpd.conf
	install -D -m644 ${srcdir}/lighttpd.service ${pkgdir}/usr/lib/systemd/system/lighttpd.service

	pushd doc/config >/dev/null
	find . -type f ! -name 'Makefile*' -exec install -D -m644 {} ${pkgdir}/usr/share/doc/lighttpd/config/{} \;
	popd >/dev/null

	install -D -m644 COPYING $pkgdir/usr/share/licenses/$pkgname/COPYING
}
