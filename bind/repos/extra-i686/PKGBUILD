# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>
# Contributor: Mario Vazquez <mario_vazq@hotmail.com>

pkgname=bind

# Use a period and not a hyphen before the patch level for proper versioning.
pkgver=9.8.0.P4
_pkgver=9.8.0-P4
pkgrel=1

pkgdesc='Berkeley Internet Name Daemon (BIND) is the reference implementation of the Domain Name System (DNS) protocols'
arch=('i686' 'x86_64')
url='http://www.isc.org/software/bind/'
license=('custom:ISC')
provides=('dns-server')
backup=('etc/logrotate.d/named'
        'etc/conf.d/named'
        'etc/named.conf')
depends=('openssl' 'krb5' 'libxml2')
options=('!makeflags' '!libtool')
source=("http://ftp.isc.org/isc/bind9/${_pkgver}/${pkgname}-${_pkgver}.tar.gz"
        'root.hint::ftp://ftp.rs.internic.net/domain/db.cache'
        'so_bsdcompat.patch'
        'notools.patch'
        'named'
        'named.conf'
        'named.conf.d'
        'named.logrotate'
        'localhost.zone'
        '127.0.0.zone')
sha1sums=('969864200c1516a8bea54266de60f316d79182b4'
          'cbdecd1ea8e7b612b33b63d66bc57273795d1f54'
          'b433ba99f23e3db305f8ce293fe4ce6d1b8d4cfb'
          '17444f9d759a4bde7688bdaa304dac57a138e4c1'
          '46232e9db243c6c05e170a1781d7a7f413be5d03'
          '5ca7a5f2a132548a090a045a2df3acea6b35d9eb'
          '7848edbfb9a848843f57c11c02b0289eefd42d00'
          '9ffb5c3f72390a517aeae557e32349d5d278cb63'
          '76a0d4cd1b913db177a5a375bebc47e5956866ec'
          '53be0f1437ebe595240d8dbdd819939582b97fb9')

install=install

build() {
	cd "${srcdir}/${pkgname}-${_pkgver}"

	patch -p1 -i ../so_bsdcompat.patch
	patch -p1 -i ../notools.patch

	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--disable-static \
		--disable-linux-caps \
		--with-openssl \
		--with-gssapi \
		--with-libxml2 \
		--with-libtool \
		--with-dlz-dlopen \

	make
}

package() {
	cd "${srcdir}/${pkgname}-${_pkgver}"

	make DESTDIR="${pkgdir}" install

	rmdir "${pkgdir}/var/run"
	install -d "${pkgdir}"/usr/share/doc/bind
	install doc/arm/*.html "${pkgdir}"/usr/share/doc/bind/
	install -D -m644 COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

	install -D -m755 ../named "${pkgdir}"/etc/rc.d/named
	install -D -m644 ../named.conf.d "${pkgdir}"/etc/conf.d/named
	install -D -m600 ../named.logrotate "${pkgdir}"/etc/logrotate.d/named
	install -D -m640 -o 0 -g 40 ../named.conf "${pkgdir}"/etc/named.conf

	install -d -m750 -o 0 -g 40 "${pkgdir}"/var/named
	install    -m640 -o 0 -g 40 ../root.hint "${pkgdir}"/var/named/
	install    -m640 -o 0 -g 40 ../127.0.0.zone "${pkgdir}"/var/named/
	install    -m640 -o 0 -g 40 ../localhost.zone "${pkgdir}"/var/named/
}
