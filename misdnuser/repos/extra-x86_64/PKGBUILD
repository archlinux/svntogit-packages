# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgname=misdnuser
pkgver=2.0.22
pkgrel=2
pkgdesc="Tools and library for mISDN"
arch=('x86_64')
url="http://www.misdn.org"
license=('GPL')
makedepends=('git')
depends=('isdn4k-utils' 'spandsp')
backup=('etc/capi20.conf') 
options=('!makeflags' '!strip')
source=(misdnuser::git+https://github.com/ISDN4Linux/mISDNuser#commit=710b87dcb9b237b20c995086d9a2fd393330a006
        mISDNcapid.service
        c2faxrecv-mISDNcapid.service
        mISDNcapid.conf)

build() {
  # only enable for debugging!
  #export CFLAGS+=" -g -O0"
  #export CXXFLAGS+=" -g -O0"
  cd ${srcdir}/${pkgname}
  make
  ./configure --prefix=/usr --sbindir=/usr/bin --sysconfdir=/etc --enable-capi --enable-softdsp --with-mISDN_group=uucp --localstatedir=/var
  make
}

package() {
  cd ${srcdir}/${pkgname}
  mkdir ${pkgdir}/var
  make DESTDIR=${pkgdir} install
  # fix udev rule
  mkdir -p ${pkgdir}/usr/lib/udev/rules.d
  mv ${pkgdir}/etc/udev/rules.d/45-misdn.rules ${pkgdir}/usr/lib/udev/rules.d
  rm -r ${pkgdir}/etc/udev/
  # add systemd files
  install -D -m644 ${srcdir}/mISDNcapid.service ${pkgdir}/usr/lib/systemd/system/mISDNcapid.service
  # mISDNcapid:
  # tends to crash on avmfritz card, add an extra systemd file for
  # restarting the services until segfaults are fixed!
  install -D -m644 ${srcdir}/c2faxrecv-mISDNcapid.service ${pkgdir}/usr/lib/systemd/system/c2faxrecv-mISDNcapid.service
  install -D -m644 ${srcdir}/mISDNcapid.conf ${pkgdir}/usr/lib/tmpfiles.d/mISDNcapid.conf
  # remove /var/run/mISDNcapid
  rm -r ${pkgdir}/var
}
md5sums=('SKIP'
         'bfd47480a54b41394ba5cc3f53587130'
         '32ead7f063e007c784aa883d441f33c2'
         'e381cb71f474b7e64842c040049cc93e')
