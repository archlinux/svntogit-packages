# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgname=misdnuser
pkgver=2.0.15_20120823
pkgrel=1
pkgdesc="Tools and library for mISDN"
arch=('i686' 'x86_64')
url="http://www.misdn.org"
license=('GPL')
depends=('isdn4k-utils' 'spandsp')
backup=('etc/capi20.conf') 
options=('!makeflags' '!libtool' '!strip')
source=(ftp://ftp.archlinux.org/other/misdnuser/${pkgname}-${pkgver}.tar.gz
        misdncapid.rc
        mISDNcapid.service
        c2faxrecv-mISDNcapid.service
        mISDNcapid.conf)

build() {
  # only enable for debugging!
  #export CFLAGS+=" -g -O0"
  #export CXXFLAGS+=" -g -O0"
  cd ${srcdir}/${pkgname}-${pkgver}
  make
  ./configure --prefix=/usr --sysconfdir=/etc --enable-capi --enable-softdsp --with-mISDN_group=uucp
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  install -D -m 755 ${srcdir}/misdncapid.rc ${pkgdir}/etc/rc.d/misdncapid
  # fix udev rule
  mkdir -p ${pkgdir}/usr/lib/udev/rules.d
  mv ${pkgdir}/etc/udev/rules.d/45-misdn.rules ${pkgdir}/usr/lib/udev/rules.d
  rm -r ${pkgdir}/etc/udev/
  # add systemd files
  install -D -m644 ${srcdir}/mISDNcapid.service ${pkgdir}/usr/lib/systemd/system/mISDNcapid.service
  # mISDNcapid:
  # tends to crash on avmfritz card, add an extra systemd file for
  # restarting the services until segfaults are fixed!
  install -D -m644 ${srcdir}/c2faxrecv-mISDNcapid.service ${pkgdir}/usr/lib/systemd/system/c2faxrecv-mISDNcapid.service
  install -D -m644 ${srcdir}/mISDNcapid.conf ${pkgdir}/usr/lib/tmpfiles.d/mISDNcapid.conf
}

md5sums=('df5013bcaf5ee320a58eb6dfe0cd4673'
         '2009563b144de037ecb20f8d727a772e'
         '8f305ee6e35fa12a8bec0632bfe7a076'
         '32ead7f063e007c784aa883d441f33c2'
         '42c3b46880a68c3883ee1ed00af34b45')
