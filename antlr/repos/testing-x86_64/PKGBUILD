# $Id $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: George Giorgidze <giorgidze (at) gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>

pkgbase=antlr
pkgname=('antlr' 'python-antlr')
pkgver=2.7.7
pkgrel=8
arch=('i686' 'x86_64')
url="http://www.antlr.org/"
license=('custom')
makedepends=('java-environment' 'python2' 'sh')
source=("http://www.antlr2.org/download/${pkgbase}-${pkgver}.tar.gz"
	'runantlr.sh'
	'gcc4.4.patch')
md5sums=('01cc9a2a454dd33dcd8c856ec89af090'
	'5210a7d580014a043a2daaa402364588'
	'8574c93f40e6477e83c29f9b07de49da')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"

  patch -Np0 -i ${srcdir}/gcc4.4.patch

  ./configure --prefix=/usr \
    --disable-examples \
    --disable-csharp
  make
}

package_antlr() {
  pkgdesc="ANother Tool for Language Recognition, (formerly PCCTS) is a language tool that provides a framework for constructing recognizers, compilers, and translators from grammatical descriptions containing C++ or Java actions"
  depends=('java-runtime')

  cd ${srcdir}/${pkgbase}-${pkgver}/lib/cpp
  make prefix="${pkgdir}/usr" install

  install -Dm644 ${srcdir}/${pkgbase}-${pkgver}/antlr/antlr.jar \
    "${pkgdir}/usr/share/java/antlr.jar"
  install -Dm755 ${srcdir}/runantlr.sh ${pkgdir}/usr/bin/runantlr
  install -Dm644 ${srcdir}/${pkgbase}-${pkgver}/LICENSE.txt \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python-antlr() {
  pkgdesc="ANother Tool for Language Recognition, (formerly PCCTS) is a language tool that provides a framework for constructing recognizers, compilers, and translators from grammatical descriptions containing C++ or Java actions (Python version)"
  depends=('antlr' 'python2')

  cd ${srcdir}/${pkgbase}-${pkgver}/lib/python
  python2 setup.py install --root="${pkgdir}"
}
