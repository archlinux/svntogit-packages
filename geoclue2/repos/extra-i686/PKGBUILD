# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: György Balló <ballogy@freestart.hu>

pkgname=geoclue2
pkgver=2.0.0
pkgrel=1
pkgdesc="Modular geoinformation service built on the D-Bus messaging system"
arch=(i686 x86_64)
url="http://geoclue.freedesktop.org/"
license=(LGPL)
depends=(libsoup json-glib)
makedepends=(intltool geoip)
optdepends=('geoip: geoip-lookup server binary')
source=(http://freedesktop.org/software/geoclue/releases/2.0/geoclue-$pkgver.tar.xz
        https.patch)
md5sums=('401ff99d530b177c62afacef0a33efd9'
         '20da579a0f1f6476a9c2ba9adebec32c')

prepare() {
  cd geoclue-$pkgver
  patch -Np1 -i ../https.patch
}

build() {
  cd geoclue-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib/$pkgname --disable-static
  make
}

package() {
  cd geoclue-$pkgver
  make DESTDIR="$pkgdir" install
}
