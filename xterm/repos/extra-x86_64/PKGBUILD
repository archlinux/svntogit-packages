# $Id$
#  ppc Maintainer: Alexander Baldeck <alexander@archlinux.org>
# i686 Maintainer: Jan de Groot <jgc@archlinux.org>
pkgname=xterm
pkgver=255
pkgrel=1
pkgdesc="X Terminal Emulator"
arch=(i686 x86_64)
url="http://invisible-island.net/xterm/"
license=('custom')
depends=('libxft>=2.1.14' 'libxaw>=1.0.7' 'ncurses' 'xorg-apps')
groups=('xorg')
source=(ftp://invisible-island.net/${pkgname}/${pkgname}-${pkgver}.tgz
        LICENSE)
md5sums=('158c6b39ea8934a11e4376a3a9741252'
         '10ecc3f8ee91e3189863a172f68282d2')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
      --libdir=/etc \
      --mandir=/usr/share/man \
      --with-app-defaults=/usr/share/X11/app-defaults/ \
      --with-x \
      --disable-full-tgetent \
      --disable-imake \
      --enable-ansi-color \
      --enable-88-color \
      --enable-256-color \
      --enable-broken-osc \
      --enable-broken-st \
      --enable-load-vt-fonts \
      --enable-i18n \
      --enable-wide-chars \
      --enable-doublechars \
      --enable-warnings \
      --enable-tcap-query \
      --enable-logging \
      --enable-dabbrev \
      --enable-freetype \
      --enable-luit \
      --enable-mini-luit \
      --enable-narrowproto || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
  chmod 0755 "${pkgdir}/usr/bin/xterm" || return 1

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 "${srcdir}/LICENSE" \
      "${pkgdir}/usr/share/licenses/${pkgname}/" || return 1
}
