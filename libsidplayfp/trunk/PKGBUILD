# Maintainer:
# Contributor: Alexander F. Rødseth <xyproto@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Simon Perry <aur [at] sanxion [dot] net>
# Contributor: Jonas Nyrén <jonas.nyren*mindkiller.com>

pkgname=libsidplayfp
pkgver=2.2.0
pkgrel=1
pkgdesc='Library to play Commodore 64 music, derived from libsidplay2'
url='https://sourceforge.net/projects/sidplay-residfp/'
arch=(x86_64)
license=(GPL)
makedepends=(vice xa)
optdepends=('vice: better SID support')
source=("https://downloads.sourceforge.net/project/sidplay-residfp/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('d66f52a610478f82fcf8e1b471ad30467089f4e21e1c27f2c734b6d3ee9368a5')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  DESTDIR="$pkgdir" make -C $pkgname-$pkgver install
  install -d "$pkgdir/usr/share/sidplayfp"
  ln -t "$pkgdir/usr/share/sidplayfp" -s /usr/lib/vice/C64/{kernal,basic,chargen}
}
