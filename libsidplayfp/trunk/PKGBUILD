# Maintainer:
# Contributor: Alexander F. Rødseth <xyproto@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Simon Perry <aur [at] sanxion [dot] net>
# Contributor: Jonas Nyrén <jonas.nyren*mindkiller.com>

pkgname=libsidplayfp
pkgver=2.4.2
pkgrel=1
pkgdesc='Library to play Commodore 64 music, derived from libsidplay2'
url='https://github.com/libsidplayfp/libsidplayfp'
arch=(x86_64)
license=(GPL)
depends=(libgcrypt)
makedepends=(vice xa)
optdepends=('vice: better SID support')
source=("https://downloads.sourceforge.net/project/sidplay-residfp/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('62ebe085361eb2ee57f0f08abe2246599b180727703fc300101a8d1a97c6de7d')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  DESTDIR="$pkgdir" make -C $pkgname-$pkgver install
  install -d "$pkgdir/usr/share/sidplayfp"
  ln -t "$pkgdir/usr/share/sidplayfp" -s /usr/share/vice/C64/{kernal,basic,chargen}
}
