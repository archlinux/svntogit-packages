# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=liboil
pkgver=0.3.17
pkgrel=3
pkgdesc="Library of simple functions that are optimized for various CPUs."
arch=('i686' 'x86_64')
license=('custom')
url="http://liboil.freedesktop.org/"
depends=('glibc')
makedepends=('glib2' 'pkgconfig' 'docbook-xsl')
options=('!libtool' '!makeflags')
source=(${url}/download/${pkgname}-${pkgver}.tar.gz)
md5sums=('47dc734f82faeb2964d97771cfd2e701')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/liboil/COPYING"
}
