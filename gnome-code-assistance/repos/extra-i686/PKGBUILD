# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-code-assistance
pkgver=3.16.0
pkgrel=2
pkgdesc="Code assistance services for GNOME"
arch=(i686 x86_64)
license=(GPL3)
depends=(python-dbus python-gobject)
makedepends=(intltool gobject-introspection llvm clang ruby-dbus gjs vala libgee go gnome-common)
optdepends=('clang: Assistance for C and C++'
            'ruby-dbus: Assistance for Ruby and CSS'
            'gjs: Assistance for JavaScript'
            'vala: Assistance for Vala'
            'libgee: Assistance for Vala'
            'go: Assistance for Go')
groups=(gnome-extra)
url="http://www.gnome.org"
install=gnome-code-assistance.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('47539b05a13886eca9fe0c079d3e9cee5205a9cfd45a788cdd57734990fc457e')

prepare() {
  cd $pkgname-$pkgver
  sed -i 's/libvala-0.26/libvala-0.28/g' configure.ac
  autoreconf -fvi
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib
  make
}

package(){
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
