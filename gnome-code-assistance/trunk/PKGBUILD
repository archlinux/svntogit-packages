# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-code-assistance
pkgver=3.16.1+6+ga46522a
pkgrel=1
pkgdesc="Code assistance services for GNOME"
arch=(x86_64)
url="https://wiki.gnome.org/Projects/CodeAssistance"
license=(GPL3)
depends=(python-dbus python-gobject)
makedepends=(intltool gobject-introspection llvm clang ruby-dbus gjs vala libgee go-pie python-pylint
             gnome-common git)
optdepends=('clang: Assistance for C and C++'
            'ruby-dbus: Assistance for Ruby and CSS'
            'gjs: Assistance for JavaScript'
            'vala: Assistance for Vala'
            'libgee: Assistance for Vala'
            'go: Assistance for Go'
            'python-pylint: Assistance for Python')
groups=(gnome-extra)
_commit=a46522ac93b0ff046a80b3672a51e2d20ac8d295  # master
source=("git+https://git.gnome.org/browse/gnome-code-assistance#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib
  make
}

package(){
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
