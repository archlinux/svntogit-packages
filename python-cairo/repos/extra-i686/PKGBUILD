# $Id$
# Maintainer: Angel Velasquez <angvp@archlinux.org>  
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=python-cairo
pkgver=1.10.0
_waf_version=1.7.15
pkgrel=6
pkgdesc="Python bindings for the cairo graphics library"
arch=('i686' 'x86_64')
url="http://www.cairographics.org/pycairo"
license=('LGPL3')
depends=('python' 'cairo')
source=(http://cairographics.org/releases/pycairo-${pkgver}.tar.bz2
        http://waf.googlecode.com/files/waf-${_waf_version}.tar.bz2)
md5sums=('e6fd3f2f1e6a72e0db0868c4985669c5'
         'a62272a3b4eb05b7e5842b4f9839cb98')

build() {
  cd pycairo-${pkgver}

  # use newer waf
  cp "$srcdir/waf-$_waf_version/waf" ./

  # fix compatibility with newer waf
  # http://cgit.freedesktop.org/pycairo/commit/?id=c57cd129407c904f8c2f752a59d0183df7b01a5e
  sed -e "s/ctx.\(tool_options\|check_tool\)(/ctx.load(/" -i wscript
  
  # Ensure that ./waf has created the cached unpacked version
  # of the wafadmin source tree.
  # This will be created to a subdirectory like
  #    .waf3-1.5.18-a7b91e2a913ce55fa6ecdf310df95752
  python3 ./waf --version

  ./waf configure --prefix=/usr
  ./waf build
}

package() {
  cd pycairo-${pkgver}
  ./waf install --destdir="${pkgdir}"
}
