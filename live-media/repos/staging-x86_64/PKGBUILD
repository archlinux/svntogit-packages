# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Gilles CHAUVIN <gcnweb@gmail.com>

pkgname=live-media
pkgver=2022.04.26
pkgrel=2
pkgdesc='Set of C++ libraries for multimedia streaming'
url='http://live555.com/liveMedia'
arch=('x86_64')
license=('LGPL')
depends=('gcc-libs' 'openssl')
options=('staticlibs')
source=(https://download.videolan.org/contrib/live555/live.${pkgver}.tar.gz)
sha256sums=('24fd24d53da1ac8f8282173039e99c9952186c18404b1fc3b1d4c0e9f49414e7')
sha512sums=('0226a451129df1d47d10bc96ef2a9ab8ffb0116fd0daac8b16a1dd57b319b9058b587955a01bc4a939c3f64659915815fe182c8c7b02cb286313ff132dcbe144')

build() {
  cd live
  export CFLAGS+=" -ffat-lto-objects"
  export CXXFLAGS+=" -ffat-lto-objects"
  sed -E 's|(-DSOCKLEN_T=socklen_t)|\1 -fPIC -DRTSPCLIENT_SYNCHRONOUS_INTERFACE=1|g' -i config.linux
  ./genMakefiles linux
  make
}

package() {
  cd live
  for dir in BasicUsageEnvironment groupsock liveMedia UsageEnvironment; do
    install -Dm 644 ${dir}/*.a -t "${pkgdir}/usr/lib"
    install -Dm 644 ${dir}/include/*.h* -t "${pkgdir}/usr/include/${dir}"
  done
  find testProgs -type f -perm 755 -print0|xargs -0 -I '{}' install -Dm 755 '{}' -t "${pkgdir}/usr/bin"
}

# vim: ts=2 sw=2 et:
