# $Id$
# Maintainer: Angel Velasquez <angvp@archlinux.org> 
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=htop
pkgver=1.0
pkgrel=1
pkgdesc="Interactive process viewer"
arch=('i686' 'x86_64')
url="http://htop.sourceforge.net/"
license=('GPL')
depends=('ncurses')
makedepends=('python2')
optdepends=('lsof' 'strace')
options=('!emptydirs')
changelog=ChangeLog
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz)
md5sums=('325112ca7947ea1f6d6441f631e00384')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	sed -i 's|ncursesw/curses.h|curses.h|' RichString.h RichString.c configure 
	sed -i 's|python|python2|' scripts/MakeHeader.py

	./configure --prefix=/usr \
      --enable-unicode \
      --enable-openvz \
	  --enable-vserver \
      --enable-cgroup
	make 
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install 
}
