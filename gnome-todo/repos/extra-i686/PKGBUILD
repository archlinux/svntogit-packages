# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-todo
pkgver=3.24.0.1+21+ge7aaeee
pkgrel=1
pkgdesc="Task manager for GNOME"
url="https://wiki.gnome.org/Apps/Todo"
arch=(i686 x86_64)
license=(GPL)
depends=(evolution-data-server libpeas)
makedepends=(intltool gobject-introspection gnome-common appstream-glib git)
optdepends=('python3: Some plugins')
groups=(gnome-extra)
_commit=e7aaeee65ca0af0d0e365099956a2408b97bf2b2  # gnome-3-24
source=("git+https://git.gnome.org/browse/gnome-todo#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^GNOME_TODO_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  CPPFLAGS+=" -O"
  ./configure --prefix=/usr --disable-maintainer-mode --disable-debug
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make V=1
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
