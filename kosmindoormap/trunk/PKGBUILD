# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kosmindoormap
pkgver=20.12.1
pkgrel=1
pkgdesc="OSM multi-floor indoor map renderer"
arch=(x86_64)
url="https://www.kde.org"
license=(LGPL)
depends=(kpublictransport ki18n protobuf)
makedepends=(extra-cmake-modules)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('dde6422b097490ab2573233dc731ccc96c3277232b6bcc21c3dd77fb1576d3a0'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
