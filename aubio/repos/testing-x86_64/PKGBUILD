# $Id$
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: Tobias Kieslich tobias [funnychar] archlinux org
# Contributor: see .contrib

pkgname=aubio
pkgver=0.4.1
pkgrel=3
pkgdesc="A tool for extracting annotations from audio signals"
arch=('i686' 'x86_64')
url="http://aubio.org/"
license=('GPL')
depends=('fftw' 'jack' 'ffmpeg')
makedepends=('python2-numpy')
optdepends=('python2-numpy: Python 2.x interface')
source=("http://aubio.org/pub/$pkgname-$pkgver.tar.bz2")
md5sums=('ffe7d24f6bd8e9053aeaba6c0231efb0')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # python2 compat (for python interface)
  sed -i 's:bin/env python:bin/env python2:' \
    "$srcdir/$pkgname-$pkgver/python/lib/aubio/__init__.py"
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  python2 waf configure --prefix=/usr --enable-fftw3f
  python2 waf build $MAKEFLAGS
  cd python && python2 setup.py build # python interface
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  python2 waf --destdir="$pkgdir" install

  # python interface
  cd python || return 1
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
