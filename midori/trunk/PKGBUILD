# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: rabyte <rabyte.at.gmail.dot.com>
# Contributor: Johannes Krampf <wuischke.at.amule.dot.org>

pkgname=midori
#_gitdate=20090306
pkgver=0.3.2 #_git${_gitdate}
pkgrel=2
pkgdesc="A lightweight web browser based on Gtk WebKit"
arch=('i686' 'x86_64')
url="http://www.twotoasts.de/index.php?/pages/midori_summary.html"
license=('LGPL2.1')
install=midori.install
depends=('libwebkit' 'libunique' 'hicolor-icon-theme' 'desktop-file-utils' 'libnotify>=0.7.1' 'libxss' 'sqlite3' 'glib2')
makedepends=('libxml2' 'intltool' 'gtk-doc' 'librsvg' 'python2>=2.7.1' 'vala' 'docutils') # 'git'
optdepends=('vala: for use in extensions')
source=(http://archive.xfce.org/src/apps/$pkgname/0.3/$pkgname-$pkgver.tar.bz2
	#midori-${_gitdate}.tar.bz2
)
md5sums=('99d9f0b936e7052b41c3d1e7a7d7d672')

# source PKGBUILD && mksource
mksource() {
        mkdir /tmp/$pkgname-source
        pushd /tmp/$pkgname-source
	git clone -v git://git.xfce.org/kalikiana/midori
	cd midori
	git archive --prefix=midori-${_gitdate}/ --format=tar HEAD | bzip2 > /tmp/$pkgname-source/midori-${_gitdate}.tar.bz2
        popd
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
#  cd ${srcdir}/${pkgname}-${_gitdate}
  # python2 
  export PYTHON=/usr/bin/python2
  sed -i -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" wscript waf
  
  ./configure --prefix=/usr --jobs=${MAKEFLAGS/-j/} \
	--enable-addons \
	--disable-hildon \
	--enable-unique \
	--enable-nls \
	--enable-docs \
	--enable-apidocs \
	--enable-userdocs
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
