# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=strace
pkgver=4.16
pkgrel=1
pkgdesc="A useful diagnositic, instructional, and debugging tool"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/strace/"
license=('custom')
depends=('perl' 'libunwind')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-$pkgver.tar.xz"{,.asc})
md5sums=('2873366cac98770efcbed6e748d5ef23'
         'SKIP')
validpgpkeys=('296D6F29A020808E8717A8842DB5BD89A340AEB7') # Dmitry V. Levin <ldv@altlinux.org>

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr \
    --with-libunwind

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}/" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
