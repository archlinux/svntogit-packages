# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

pkgname=libice
pkgver=1.1.0
pkgrel=1
pkgdesc="X11 Inter-Client Exchange library"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
depends=('glibc')
makedepends=('xtrans' 'xorg-util-macros' 'xorgproto')
source=(${url}/releases/individual/lib/libICE-${pkgver}.tar.xz{,.sig})
license=('custom')
sha512sums=('bf8ca11a20bc120337003a8ef1c190e26e3650dede653287aae7893abd8c72191401d90ddc4bb9b367ae832eee23a22d02b0d4a3b9071c392f865379034b2838'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # "Alan Coopersmith <alan.coopersmith@oracle.com>"

build() {
  cd libICE-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd libICE-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
