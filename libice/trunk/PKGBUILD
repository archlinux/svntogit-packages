# $Id$
# Contributor: Alexander Baldeck <alexander@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libice
pkgver=1.0.8
pkgrel=1
pkgdesc="X11 Inter-Client Exchange library"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org/"
depends=('glibc' 'xproto>=7.0.18')
makedepends=('pkgconfig' 'xtrans>=1.2.5')
options=('!libtool')
source=(${url}/releases/individual/lib/libICE-${pkgver}.tar.bz2)
license=('custom')
sha1sums=('ddb14df8bbc43df7322978f5f9f802936e2a7324')

build() {
  cd "${srcdir}/libICE-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "${srcdir}/libICE-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
