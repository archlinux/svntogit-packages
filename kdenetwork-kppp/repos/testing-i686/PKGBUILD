# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdenetwork-kppp
pkgver=4.12.2
pkgrel=1
pkgdesc='Internet Dial-Up Tool'
url='http://kde.org/applications/internet/kppp/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdenetwork')
depends=('kdebase-runtime' 'ppp')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/kppp-${pkgver}.tar.xz")
sha1sums=('789eab4a389db2c08adce672dc44bee3ad317c30')

build() {
  mkdir build
  cd build
  cmake ../kppp-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
