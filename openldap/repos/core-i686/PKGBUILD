# $Id$
# Maintainer:

pkgbase=openldap
pkgname=('libldap' 'openldap')
pkgver=2.4.33
pkgrel=3
arch=('i686' 'x86_64')
url="http://www.openldap.org/"
license=('custom')
makedepends=('libltdl' 'libsasl' 'e2fsprogs' 'util-linux')
options=('!makeflags')
source=(ftp://ftp.openldap.org/pub/OpenLDAP/openldap-release/${pkgbase}-${pkgver}.tgz
        slapd slapd.default slapd.service slapd.tmpfiles
        ntlm.patch)
sha1sums=('0cea642ba2dae1eb719da41bfedb9eba72ad504d'
          'b684038a7f5bc7d2ba470b2d16ee47b5aae64c65'
          'd89b8a533045123f1ab46c9c430cf132d58a20a4'
          '9c7f3441e6a6ee13cdcfe8d965081c7a574c3393'
          'f86a82e35ebe15026980467c9dee4007e686b795'
          'e4afd9f1c810ef4c4cd8fe1101dfe5887f2b7eef')

build() {
  cd "${srcdir}"/${pkgbase}-${pkgver}
  patch -Np1 -i "${srcdir}"/ntlm.patch

  sed -i 's|-m 644 $(LIBRARY)|-m 755 $(LIBRARY)|' libraries/{liblber,libldap,libldap_r}/Makefile.in
  sed -i 's|#define LDAPI_SOCK LDAP_RUNDIR LDAP_DIRSEP "run" LDAP_DIRSEP "ldapi"|#define LDAPI_SOCK LDAP_DIRSEP "run" LDAP_DIRSEP "openldap" LDAP_DIRSEP "ldapi"|' include/ldap_defaults.h
  sed -i 's|%LOCALSTATEDIR%/run|/run/openldap|' servers/slapd/slapd.conf
  sed -i 's|-$(MKDIR) $(DESTDIR)$(localstatedir)/run|-$(MKDIR) $(DESTDIR)/run/openldap|' servers/slapd/Makefile.in

  LDFLAGS="$LDFLAGS -L\"${pkgdir}\"/libldap/usr/lib" 
  ./configure --prefix=/usr --mandir=/usr/share/man --libexecdir=/usr/lib \
    --sysconfdir=/etc --localstatedir=/var/lib/openldap \
    --enable-ipv6 --enable-syslog --enable-local \
    --enable-bdb --enable-hdb \
    --enable-crypt --enable-dynamic \
    --with-threads --disable-wrappers \
    --without-fetch \
    --enable-spasswd --with-cyrus-sasl \
    --enable-overlays=mod --enable-modules=yes
  make
}

check() {
  cd "${srcdir}"/${pkgbase}-${pkgver}
  make test
}

package_libldap() {
  pkgdesc="Lightweight Directory Access Protocol (LDAP) client libraries"
  depends=('libsasl' 'e2fsprogs')
  backup=('etc/openldap/ldap.conf')
  options=('!libtool')

  cd "${srcdir}"/${pkgbase}-${pkgver}
  for dir in include libraries doc/man/man3 ; do
    pushd ${dir}
    make DESTDIR="${pkgdir}" install
    popd
  done
  install -Dm644 doc/man/man5/ldap.conf.5.tmp "${pkgdir}"/usr/share/man/man5/ldap.conf.5
  
# get rid of duplicate default conf files
  rm "${pkgdir}"/etc/openldap/*.default

  ln -sf liblber.so "${pkgdir}"/usr/lib/liblber.so.2
  ln -sf libldap.so "${pkgdir}"/usr/lib/libldap.so.2

  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

package_openldap() {
  pkgdesc="Lightweight Directory Access Protocol (LDAP) client and server"
  depends=("libldap>=${pkgver}" 'libltdl' 'util-linux')
  backup=('etc/openldap/slapd.conf' 'etc/conf.d/slapd')
  options=('!libtool' 'emptydirs')
  install=openldap.install

  cd "${srcdir}"/${pkgbase}-${pkgver}
  for dir in clients servers doc/man/man{1,5,8} ; do
    pushd ${dir}
    make DESTDIR="${pkgdir}" install
    popd
  done
  rm "${pkgdir}"/usr/share/man/man5/ldap.conf.5
  rm -r "${pkgdir}"/run

# get rid of duplicate default conf files
  rm "${pkgdir}"/etc/openldap/*.default

  ln -s ../lib/slapd "${pkgdir}"/usr/sbin/slapd

  chown root:439 "${pkgdir}"/etc/openldap/{slapd.conf,DB_CONFIG.example}
  chmod 640 "${pkgdir}"/etc/openldap/{slapd.conf,DB_CONFIG.example}

  install -dm700 -o 439 -g 439 "${pkgdir}"/var/lib/openldap
  install -dm700 -o 439 -g 439 "${pkgdir}"/etc/openldap/slapd.d
  install -Dm755 "${srcdir}"/slapd "${pkgdir}"/etc/rc.d/slapd
  install -Dm644 "${srcdir}"/slapd.default "${pkgdir}"/etc/conf.d/slapd
  install -Dm644 "${srcdir}"/slapd.service "${pkgdir}"/usr/lib/systemd/system/slapd.service
  install -Dm644 "${srcdir}"/slapd.tmpfiles "${pkgdir}"/usr/lib/tmpfiles.d/slapd.conf
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
