# $Id$
# Maintainer:

pkgbase=openldap
pkgname=('libldap' 'openldap')
pkgver=2.4.39
pkgrel=1
arch=('i686' 'x86_64')
url="http://www.openldap.org/"
license=('custom')
makedepends=('libltdl' 'libsasl' 'e2fsprogs' 'util-linux' 'chrpath' 'unixodbc')
options=('!makeflags')
source=(ftp://ftp.openldap.org/pub/OpenLDAP/openldap-release/${pkgbase}-${pkgver}.tgz
        slapd.service slapd.tmpfiles openldap-ntlm.patch)
sha1sums=('2b8e8401214867c361f7212e7058f95118b5bd6c'
          '2441815efbfa01ad7a1d39068e5503b53d1d04b0'
          '59241a813d7508294e4ef1cec3bfe1f5495e109d'
          'e4afd9f1c810ef4c4cd8fe1101dfe5887f2b7eef')

# see http://www.openldap.org/faq/data/cache/756.html
# there's no proper backend support for anything apart from
# BerkeleyDB, if we don't want to drop local backend server support 
# we are forced to keep Berkeley DB here

prepare() {
  cd ${pkgbase}-${pkgver}
  patch -p1 -i "${srcdir}"/openldap-ntlm.patch
  sed -i 's|-m 644 $(LIBRARY)|-m 755 $(LIBRARY)|' libraries/{liblber,libldap,libldap_r}/Makefile.in
  sed -i 's|#define LDAPI_SOCK LDAP_RUNDIR LDAP_DIRSEP "run" LDAP_DIRSEP "ldapi"|#define LDAPI_SOCK LDAP_DIRSEP "run" LDAP_DIRSEP "openldap" LDAP_DIRSEP "ldapi"|' include/ldap_defaults.h
  sed -i 's|%LOCALSTATEDIR%/run|/run/openldap|' servers/slapd/slapd.{conf,ldif}
  sed -i 's|-$(MKDIR) $(DESTDIR)$(localstatedir)/run|-$(MKDIR) $(DESTDIR)/run/openldap|' servers/slapd/Makefile.in
}

build() {
  cd ${pkgbase}-${pkgver}
  ./configure --prefix=/usr --libexecdir=/usr/lib \
    --sysconfdir=/etc --localstatedir=/var/lib/openldap --sbindir=/usr/bin \
    --enable-dynamic --enable-syslog --enable-ipv6 --enable-local \
    --enable-crypt --enable-spasswd --enable-modules \
    --enable-backends=mod --disable-ndb --enable-overlays=mod \
    --with-cyrus-sasl --with-threads
  make

  cd contrib/slapd-modules/nssov
  make prefix=/usr libexecdir=/usr/lib sysconfdir=/etc/openldap
  chrpath -d .libs/nssov.so
}

check() {
  cd ${pkgbase}-${pkgver}
  make test
}

package_libldap() {
  pkgdesc="Lightweight Directory Access Protocol (LDAP) client libraries"
  depends=('libsasl' 'e2fsprogs')
  backup=('etc/openldap/ldap.conf')

  cd ${pkgbase}-${pkgver}
  for dir in include libraries doc/man/man3 ; do
    pushd ${dir}
    make DESTDIR="${pkgdir}" install
    popd
  done
  install -Dm644 doc/man/man5/ldap.conf.5.tmp "${pkgdir}"/usr/share/man/man5/ldap.conf.5
  
# get rid of duplicate default conf files
  rm "${pkgdir}"/etc/openldap/*.default

  ln -sf liblber.so "${pkgdir}"/usr/lib/liblber.so.2
  ln -sf libldap.so "${pkgdir}"/usr/lib/libldap.so.2

  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

package_openldap() {
  pkgdesc="Lightweight Directory Access Protocol (LDAP) client and server"
  depends=("libldap>=${pkgver}" 'libltdl')
  optdepends=('unixodbc: for sql backend support')
  backup=('etc/openldap/slapd.conf' 'etc/openldap/slapd.ldif')
  options=('emptydirs')
  install=openldap.install

  cd ${pkgbase}-${pkgver}
  for dir in clients servers doc/man/man{1,5,8}; do
    pushd ${dir}
    make DESTDIR="${pkgdir}" install
    popd
  done

  pushd contrib/slapd-modules/nssov
  install -m755 .libs/nssov.so.0.0.0 "${pkgdir}"/usr/lib/openldap
  ln -s nssov.so.0.0.0 "${pkgdir}"/usr/lib/openldap/nssov.so
  ln -s nssov.so.0.0.0 "${pkgdir}"/usr/lib/openldap/nssov.so.0
  install -m444 ldapns.schema "${pkgdir}"/etc/openldap/schema
  install -m644 slapo-nssov.5 "${pkgdir}"/usr/share/man/man5/slapo-nssov.5
  popd

  rm "${pkgdir}"/usr/share/man/man5/ldap.conf.5
  rm -r "${pkgdir}"/run

# get rid of duplicate default conf files
  rm "${pkgdir}"/etc/openldap/*.default

  ln -s ../lib/slapd "${pkgdir}"/usr/bin/slapd

  chown root:439 "${pkgdir}"/etc/openldap/{slapd.{conf,ldif},DB_CONFIG.example}
  chmod 640 "${pkgdir}"/etc/openldap/{slapd.{conf,ldif},DB_CONFIG.example}

  install -dm700 -o 439 -g 439 "${pkgdir}"/var/lib/openldap
  install -dm700 -o 439 -g 439 "${pkgdir}"/etc/openldap/slapd.d
  install -Dm644 "${srcdir}"/slapd.service "${pkgdir}"/usr/lib/systemd/system/slapd.service
  install -Dm644 "${srcdir}"/slapd.tmpfiles "${pkgdir}"/usr/lib/tmpfiles.d/slapd.conf
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
