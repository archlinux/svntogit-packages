# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kross-interpreters
pkgver=20.08.1
pkgrel=1
pkgdesc='Language interpreters to enable in-process scripting with Kross'
arch=(x86_64)
url='https://www.kde.org/applications/development/'
license=(GPL)
depends=(kross)
makedepends=(extra-cmake-modules kdoctools python2 ruby)
optdepends=('python2: kross-python plugin' 'ruby: kross ruby plugin')
groups=(kde-applications)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('1fb7d75d83e63d1a7147468dd25eeb6aeb06b4679f126c658a8b40ef257f9a4e'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}

