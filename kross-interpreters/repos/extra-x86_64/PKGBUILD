# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kross-interpreters
pkgver=16.12.2
pkgrel=1
pkgdesc='Language interpreters to enable in-process scripting with Kross'
arch=(i686 x86_64)
url='https://www.kde.org/applications/development/'
license=(GPL)
depends=(kross)
makedepends=(extra-cmake-modules kdoctools python2 ruby)
optdepends=('python2: kross-python plugin' 'ruby: kross ruby plugin')
groups=(kde-applications kdebindings)
source=("http://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('4d21b5ad9d78354cff3417540a0d9cfa94565895b456fedc5933c0c6c52788b3'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}

