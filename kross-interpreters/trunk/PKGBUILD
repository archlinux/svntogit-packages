# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kross-interpreters
pkgver=16.04.1
pkgrel=1
pkgdesc='Language interpreters to enable in-process scripting with Kross'
arch=(i686 x86_64)
url='https://www.kde.org/applications/development/'
license=(GPL)
depends=(kross kdelibs4support)
makedepends=(extra-cmake-modules kdoctools python2)
optdepends=('python2: kross-python plugin')
groups=(kde-applications kdebindings)
source=("http://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
sha256sums=('da1862c9943ee3a40c190610485cc94c3bde4ee9d239b7b26034ceda260de3c3')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}

