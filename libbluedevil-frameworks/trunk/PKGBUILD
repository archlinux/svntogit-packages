# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=libbluedevil-frameworks
pkgver=5.2.0
pkgrel=1
pkgdesc='KDE bluetooth framework libraries'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/workspace/libbluedevil'
license=('GPL2')
depends=('qt5-base' 'bluez')
makedepends=('extra-cmake-modules')
conflicts=('libbluedevil')
source=("http://download.kde.org/stable/plasma/$pkgver/libbluedevil-$pkgver.tar.xz")
md5sums=('e91647f5b8e7905586470444de91e2c1')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../libbluedevil-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
