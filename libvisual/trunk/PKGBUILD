# $Id$
# Maintainer: 

pkgname=libvisual
pkgver=0.4.0
pkgrel=6
pkgdesc="Abstraction library that comes between applications and audio visualisation plugins"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/libvisual/"
license=('LGPL')
source=("http://downloads.sourceforge.net/sourceforge/libvisual/libvisual-${pkgver}.tar.gz"
        "libvisual-0.4.0-inlinedefineconflict.patch")
md5sums=('f4e78547c79ea8a8ad111cf8b85011bb'
         'e49d55a9ea20c949512c1d2143086936')

prepare()
{
  cd ${srcdir}/${pkgname}-${pkgver}

  # libvisual's definition of "inline" causes issues when compiling with the
  # new C++11 ABI (patch from Fedora)
  patch -Np1 -i ../libvisual-0.4.0-inlinedefineconflict.patch
}

build()
{
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
