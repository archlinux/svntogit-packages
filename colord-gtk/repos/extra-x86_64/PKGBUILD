# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=colord-gtk
pkgver=0.2.0
pkgrel=1
pkgdesc="Additional GTK support code for colord"
arch=(x86_64)
url="https://www.freedesktop.org/software/colord/"
license=(GPL2)
depends=(lcms2 gtk3 colord)
makedepends=(gobject-introspection vala gtk-doc git docbook-utils docbook-xsl
             meson)
_commit=f74545cfce2a996840b9a6964f227dfd3cf0173d  # tags/COLORD_GTK_0_2_0^0
source=("git+https://github.com/hughsie/colord-gtk#commit=$_commit")
sha1sums=('SKIP')
validpgpkeys=('163EB50119225DB3DF8F49EA17ACBA8DFA970E17')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^COLORD_GTK_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build -D vapi=true
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}

# vim:set ts=2 sw=2 et:
