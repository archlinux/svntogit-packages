# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor:  Adrian Bacircea <adrian.bacircea@gmail.com>

pkgname=libbpf
pkgver=1.0.0
pkgrel=1
pkgdesc='Library for loading eBPF programs and reading and manipulating eBPF objects from user-space'
url='https://github.com/libbpf/libbpf'
arch=('x86_64')
license=('LGPL2.1')
depends=('glibc' 'libelf' 'linux-api-headers')
makedepends=('rsync')
provides=('libbpf.so')
options=('debug')
source=(https://github.com/libbpf/libbpf/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('e99aea1ff477114549b41c272a975169a79ffc1daf4bcaba586cd13d0fc0b23c336cb406fd8e64b73350fe16e2d423fa68a29601d15e2477955c7a92358fb7f8')
b2sums=('5bde5a9147330f11dcf1e37aace971624293aae3914459a6b8be327530fa2c2774edaf1b798225ade2771edd88bddc2a74d7f523791f83558417b759eb417f7a')

build() {
  cd ${pkgname}-${pkgver}
  make -C src
}

package() {
  cd ${pkgname}-${pkgver}
  make -C src DESTDIR="${pkgdir}" LIBSUBDIR=lib install install_headers
  install -Dm 644 LICENSE* -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
