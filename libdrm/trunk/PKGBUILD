# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libdrm
pkgver=2.4.21
pkgrel=2
pkgdesc="Userspace interface to kernel DRM services"
arch=(i686 x86_64)
license=('custom')
depends=('glibc')
options=('!libtool')
url="http://dri.freedesktop.org/"
source=(http://dri.freedesktop.org/${pkgname}/${pkgname}-${pkgver}.tar.bz2
        libdrm-2.4.21-b803918f3f.patch
        no-pthread-stubs.patch
        COPYING)
md5sums=('273ed9dad986e3a931649f3d8762ff74'
         'd67fab8381b371fe2a8f86e2819aef2b'
         '9b5ec3bbebe86921e2057694c42f65b8'
         'ba65e71c481b94ef0fb6c23c7f21ffa1')
     
build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/libdrm-2.4.21-b803918f3f.patch"
  patch -Np1 -i "${srcdir}/no-pthread-stubs.patch"
  libtoolize --force
  aclocal
  autoconf
  automake
  ./configure --prefix=/usr \
      --enable-intel --enable-radeon \
      --enable-vmwgfx-experimental-api --enable-nouveau-experimental-api \
      --enable-udev
  make
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 "${srcdir}/COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/"
}
