# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libdrm
pkgver=2.4.18
pkgrel=2
pkgdesc="Userspace interface to kernel DRM services"
arch=(i686 x86_64)
license=('custom')
depends=('glibc')
options=('!libtool')
url="http://dri.freedesktop.org/"
source=(http://dri.freedesktop.org/${pkgname}/${pkgname}-${pkgver}.tar.bz2
        no-pthread-stubs.patch
	nouveau_fix.patch
	nouveau_fix2.patch
        COPYING)
md5sums=('d2b5fbfd37742af7d2169f7d26ce3007'
         '9b5ec3bbebe86921e2057694c42f65b8'
         '298588fad4d1d6f4f746a0fabb91f09d'
         '8199a3e929dab7e4fa87dccec7bf1e54'
         'ba65e71c481b94ef0fb6c23c7f21ffa1')
           
build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/no-pthread-stubs.patch" || return 1
  # revert incompatible late nouveau commits that would requiere a new ddx driver shot based on a not yet released kernel :S
  patch -Rp1 -i "${srcdir}/nouveau_fix.patch" || return 1
  patch -Rp1 -i "${srcdir}/nouveau_fix2.patch" || return 1
  libtoolize --force || return 1
  aclocal || return 1
  autoconf || return 1
  automake || return 1
  ./configure --prefix=/usr \
      --enable-nouveau-experimental-api \
      --enable-udev || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 "${srcdir}/COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/" || return 1
}
