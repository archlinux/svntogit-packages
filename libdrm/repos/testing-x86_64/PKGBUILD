# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libdrm
pkgver=2.4.5
pkgrel=3
pkgdesc="Userspace interface to kernel DRM services"
arch=(i686 x86_64)
depends=('glibc')
options=('!libtool' 'force')
url="http://dri.freedesktop.org/"
source=(http://dri.freedesktop.org/${pkgname}/${pkgname}-${pkgver}.tar.bz2
	no-pthread-stubs.patch
	nouveau_private.h
	nouveau_dma.h
	drm-e2d7dfb6.patch)
md5sums=('0d09025f069ff1a986cf3339e51908b3'
         'e5f8f3b9f6357c2f82950cdfb8e9c9ec'
         '0cb6d0d40b4031604a9063f6e384b548'
	 'a9c94dcf9625fa99862f695a744258e8'
	 '2921adb7079226c7bc2a3b9863dabcbc')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  cp "${srcdir}/nouveau_private.h" "${srcdir}/nouveau_dma.h" \
    libdrm/nouveau/ || return 1
  patch -Np1 -i "${srcdir}/no-pthread-stubs.patch" || return 1
  patch -Np1 -i "${srcdir}/drm-e2d7dfb6.patch" || return 1
  libtoolize --force || return 1
  aclocal || return 1
  autoconf || return 1
  automake || return 1
  ./configure --prefix=/usr --enable-nouveau-experimental-api || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
}
