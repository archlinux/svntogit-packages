# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeaccessibility-kmag
pkgver=4.13.0
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc='Screen Magnifier'
url='http://kde.org/applications/utilities/kmag/'
license=('GPL' 'FDL')
groups=('kde' 'kdeaccessibility')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/kmag-${pkgver}.tar.xz")
sha1sums=('fb5b783a8bd04720d0b0d0b53fc3b5026cf186c4')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../kmag-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $srcdir/build
  make DESTDIR=$pkgdir install
}
