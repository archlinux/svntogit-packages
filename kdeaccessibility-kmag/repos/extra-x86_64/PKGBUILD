# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeaccessibility-kmag
pkgver=16.12.1
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc='Screen Magnifier'
url='http://kde.org/applications/utilities/kmag/'
license=('GPL' 'FDL')
groups=('kde-applications' 'kdeaccessibility')
depends=('kdebase-runtime' 'libkdeaccessibilityclient')
makedepends=('cmake' 'automoc4')
source=("http://download.kde.org/stable/applications/${pkgver}/src/kmag-${pkgver}.tar.xz"{,.sig})
sha1sums=('f3abf6e87a558a76dc38ce8c6f3d1b00580c1909'
          'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kmag-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
