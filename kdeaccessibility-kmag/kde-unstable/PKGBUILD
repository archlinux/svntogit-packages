# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeaccessibility-kmag
pkgver=15.11.80
pkgrel=2
arch=('i686' 'x86_64')
pkgdesc='Screen Magnifier'
url='http://kde.org/applications/utilities/kmag/'
license=('GPL' 'FDL')
groups=('kde-applications' 'kdeaccessibility')
depends=('kdebase-runtime' 'libkdeaccessibilityclient')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/unstable/applications/${pkgver}/src/kmag-${pkgver}.tar.xz")
sha1sums=('261e9a5183c91fc77156d874b5251e4cbd6b6112')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kmag-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
