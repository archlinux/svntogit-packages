# $Id$
# Maintainer: SÃ©bastien Luttringer
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Mark Smith <markzzzsmith@yahoo.com.au>

pkgname=radvd
pkgver=2.10
pkgrel=1
pkgdesc='IPv6 Router Advertisement Daemon'
url='http://www.litech.org/radvd/'
license=('custom')
depends=('glibc')
makedepends=('check')
arch=('i686' 'x86_64')
backup=('etc/radvd.conf')
validpgpkeys=('B11F2EED32FB6728F700337C411FA8C112D91A31') # Reuben Hawkins
source=("http://www.litech.org/radvd/dist/$pkgname-$pkgver.tar.xz"{,.asc}
        "$pkgname.service")
sha1sums=('a6b41df6ccc3ecc704809f6072baa5dc1c92dc54'
          'SKIP'
          '2fdba3b0ed28c67dc1c04bfd1439fa4dece88bbc')

#prepare() {
#  patch -p1 -d $pkgname-$pkgver < '01-fix-rdnssinfo-length-fix.patch'
#}

build() {
  cd $pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --with-pidfile=/run/radvd.pid \
    --mandir=/usr/share/man
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  # default config
  install -D -m 644 radvd.conf.example "$pkgdir/etc/radvd.conf"
  # license
  install -D -m 644 COPYRIGHT "$pkgdir/usr/share/licenses/$pkgname/COPYRIGHT"
  # systemd
  install -D -m 644 ../$pkgname.service \
    "$pkgdir/usr/lib/systemd/system/$pkgname.service"
}

# vim:set ts=2 sw=2 et:
