# Maintainer: 
# Contributor:	Jesse Jaara	<gmail.com: jesse.jaara>
# Contributor:	Jussi Timperi	<gmail.com: jussi.timperi>

pkgname=libvoikko
pkgver=4.1.1
pkgrel=2
pkgdesc="A spelling and grammar checker, hyphenator and collection of related linguistic data for Finnish language"
arch=(x86_64)
url="http://voikko.sourceforge.net"
license=(GPL2)
depends=(gcc-libs)
makedepends=(python)
source=(http://www.puimula.org/voikko-sources/libvoikko/$pkgname-$pkgver.tar.gz)
sha512sums=('eb8e32dc50e2baa4184a2030537c6fe86054053a75b543d38c7bbc0faf1fbea51020ccfd03d5de94b1d78a811a58eabefc7e87c3eae55f7b7b51409bd1dd8665')

build() {
  cd $pkgname-$pkgver

# Workaround encoding error
  export LANG=en_US.UTF-8
  ./configure --prefix=/usr --enable-hfst=false --with-dictionary-path=/usr/share/voikko
  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install
  mkdir -p "$pkgdir"/usr/lib/python3.7/site-packages
  cp python/libvoikko.py "$pkgdir"/usr/lib/python3.7/site-packages/libvoikko.py
}

