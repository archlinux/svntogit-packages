# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>

pkgname=xfdesktop
pkgver=4.10.0
pkgrel=4
pkgdesc="A desktop manager for Xfce"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('GPL2')
groups=('xfce4')
depends=('libxfce4ui' 'thunar' 'garcon' 'hicolor-icon-theme' 'libwnck')
makedepends=('intltool')
conflicts=('xfce4-menueditor')
replaces=('xfce4-menueditor')
options=('!libtool')
install=xfdesktop.install
source=(http://archive.xfce.org/src/xfce/$pkgname/4.10/$pkgname-$pkgver.tar.bz2
        add-backgrounds-xfce-to-search-path.patch
        avoid-duplicating-volume-icons.patch)
sha256sums=('897ae6ee435dcc89809ad70c15c5d15347d1cf4fc8033238b17dcc47836c2d7b'
            'fcb27e24abfd50c5555fe01ae946a2bc5a133d3f5cee076eef517ccc5f668dd5'
            'f2ad30fe8fed9e2f5a65c214f03b65353527d285a4ea0ec22a1feac5c452446b')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Add backgrounds/xfce/ to the single image list in the settings app
  # https://bugzilla.xfce.org/show_bug.cgi?id=8799
  patch -Np1 -i "$srcdir/add-backgrounds-xfce-to-search-path.patch"

  # Workaround for GVFS 1.14.0 which emits volume-added signals for
  # pre-existing volumes.
  patch -Np1 -i "$srcdir/avoid-duplicating-volume-icons.patch"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --enable-gio-unix \
    --enable-thunarx \
    --enable-notifications \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
