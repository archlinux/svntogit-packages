# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: tobias <tobias funnychar archlinux.org>

pkgname=xfdesktop
pkgver=4.6.2
pkgrel=1
pkgdesc="A desktop manager for Xfce"
arch=(i686 x86_64)
license=('GPL2')
url="http://www.xfce.org/"
groups=('xfce4')
depends=("libxfce4menu>=4.6.2" 'thunar>=1.0.2' 'hicolor-icon-theme' 'libxfcegui4>=4.6.4' 'libwnck>=2.30.0')
makedepends=("xfce4-panel>=4.6.4" 'pkgconfig' 'intltool')
optdepends=('xfce4-panel: panel plugin')
conflicts=('xfce4-menueditor')
replaces=('xfce4-menueditor')
options=('!libtool')
install=xfdesktop.install
source=(http://archive.xfce.org/src/xfce/${pkgname}/4.6/${pkgname}-${pkgver}.tar.bz2)
md5sums=('e800ea9fee7a5c5eaf2ae96e23a83e3a')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib \
    --localstatedir=/var --disable-static || return 1
  make || return 1
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install || return 1
}
