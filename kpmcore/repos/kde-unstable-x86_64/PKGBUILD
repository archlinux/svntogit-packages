# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kpmcore
pkgver=20.11.90
pkgrel=1
pkgdesc="Library for managing partitions"
arch=(x86_64)
url="https://www.kde.org/applications/system/kdepartitionmanager/"
license=(GPL2)
depends=(smartmontools kcoreaddons kwidgetsaddons ki18n polkit-qt5 qca)
makedepends=(extra-cmake-modules kauth)
optdepends=('e2fsprogs: ext2/3/4 support'
	    'xfsprogs: XFS support'
	    'jfsutils: JFS support'
	    'reiserfsprogs: Reiser support'
	    'ntfs-3g: NTFS support'
            'dosfstools: FAT32 support'
            'fatresize: FAT resize support'
            'f2fs-tools: F2FS support'
            'exfat-utils: exFAT support'
            'nilfs-utils: nilfs support'
            'udftools: UDF support')
source=("https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('d0b80360e4cf2fe010b296552b8c82ea5768137c617110f6d0008b33b965c36d'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
