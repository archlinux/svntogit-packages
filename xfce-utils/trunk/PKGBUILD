# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: tobias <tobias funnychar archlinux.org>

pkgname=xfce-utils
pkgver=4.6.2
pkgrel=1
pkgdesc="Utilities for Xfce"
arch=(i686 x86_64)
license=('GPL2')
url="http://www.xfce.org/"
groups=('xfce4')
depends=("libxfcegui4>=$pkgver" 'hicolor-icon-theme')
makedepends=('pkgconfig' 'intltool')
optdepends=('perl:	enables migration script for 4.4 configuration files'
            'xscreensaver|gnome-screensaver|xlockmore: for locking screen with xflock4')
options=('!libtool')
install=${pkgname}.install
source=(http://archive.xfce.org/src/xfce/${pkgname}/4.6/${pkgname}-${pkgver}.tar.bz2)
md5sums=('5d23407700d7e8c9751e17a5bc955109')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib \
    --localstatedir=/var --disable-static
  make || return 1
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install || return 1
}
