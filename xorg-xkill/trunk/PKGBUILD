# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-xkill
pkgver=1.0.5
pkgrel=1
pkgdesc="Kill a client by its X resource"
arch=(x86_64)
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libx11' 'libxmu')
makedepends=('xorg-util-macros')
groups=('xorg-apps' 'xorg')
source=(https://xorg.freedesktop.org/archive/individual/app/xkill-${pkgver}.tar.bz2{,.sig})
sha512sums=('4603a1794f114f5cea2c885020c9d073c8421d1e785c99ff6b936ae97139c8dac8020a187b5bbd0fa6d56ed6fab879441b42ba900cc5f2f5a534dafdc86b9249'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # "Alan Coopersmith <alan.coopersmith@oracle.com>"

build() {
  cd xkill-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd xkill-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
