# $Id$
# Maintainer: Thomas Baechler <thomas@archlinux.org>

pkgname=v86d
pkgver=0.1.10
pkgrel=1
pkgdesc="userspace helper for uvesafb that runs x86 code in an emulated environment"
arch=('i686' 'x86_64')
url="http://dev.gentoo.org/~spock/projects/uvesafb/"
license=('GPL2')
depends=('glibc')
conflicts=('mkinitcpio<0.5.99')
backup=('etc/modprobe.d/uvesafb.conf')
options=('!makeflags')
source=(http://dev.gentoo.org/~spock/projects/uvesafb/archive/v86d-$pkgver.tar.bz2
        v86d
        v86d_hook
        modprobe.uvesafb)
md5sums=('51c792ba7b874ad8c43f0d3da4cfabe0'
         '33f3131e7bf041ed6a67e92bff272f4c'
         '813958b8f6c75a7063e4858f31509694'
         '2d7cc8dc6a41916a13869212d0191147')

build() {
  cd "$srcdir/v86d-$pkgver"
  ./configure --with-x86emu
  # we only need /usr/include/video/uvesafb.h
  make KDIR=/usr
}

package() {
  cd "$srcdir/v86d-$pkgver"
  make DESTDIR="$pkgdir" install

  install -D -m644 "$srcdir/v86d" "$pkgdir/lib/initcpio/install/v86d"
  install -D -m644 "$srcdir/v86d_hook" "$pkgdir/lib/initcpio/hooks/v86d"
  install -D -m644 "$srcdir/modprobe.uvesafb" "$pkgdir/etc/modprobe.d/uvesafb.conf"
}
