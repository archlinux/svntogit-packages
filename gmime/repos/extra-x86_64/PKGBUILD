# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Ben <ben@benmazer.net>

pkgname=gmime
pkgver=2.6.20
pkgrel=2
pkgdesc="Core mime parsing library"
arch=('i686' 'x86_64')
license=('GPL')
url="http://spruce.sourceforge.net/gmime/"
depends=('glib2' 'gpgme' 'zlib')
makedepends=('gtk-sharp-2' 'gobject-introspection')
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver:0:3}/$pkgname-$pkgver.tar.xz)
sha256sums=('e0a170fb264c2ae4cecd852f4e7aaddb8d58e8f3f0b569ce2d2a4704f55bdf65')

build() {
  # get rid of that .wapi errors in fakeroot
  export MONO_SHARED_DIR="$srcdir/weird"
  mkdir -p "$MONO_SHARED_DIR"

  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr --enable-smime \
      --disable-static
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
