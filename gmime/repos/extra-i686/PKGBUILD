# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Ben <ben@benmazer.net>

pkgname=gmime
pkgver=2.6.15
pkgrel=1
pkgdesc="Core mime parsing library"
arch=('i686' 'x86_64')
license=('GPL')
url="http://spruce.sourceforge.net/gmime/"
depends=('glib2' 'gpgme' 'zlib')
makedepends=('gtk-sharp-2')
options=('!libtool')
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('b4c2a0b99b82063387cd750a38421ebaa0636f339e67984a84371bcb697dc99a')

build() {
  # get rid of that .wapi errors in fakeroot
  export MONO_SHARED_DIR="$srcdir/weird"
  mkdir -p "$MONO_SHARED_DIR"

  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr --enable-smime \
      --disable-static
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
