# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Ben <ben@benmazer.net>

pkgname=gmime
pkgver=2.4.10
pkgrel=1
pkgdesc="Core mime parsing library"
arch=(i686 x86_64)
license=('GPL')
url="http://spruce.sourceforge.net/gmime/"
depends=('glib2>=2.22.2' 'zlib')
makedepends=('gtk-sharp-2>=2.12.8' 'pkgconfig')
options=('!libtool')
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/2.4/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('59c71aa8d0f06c66f863bc0462868237a770ce30d1402af2515235fa41db51ca')

build() {
  # get rid of that .wapi errors in fakeroot
  export MONO_SHARED_DIR="${srcdir}/weird"
  mkdir -p "${MONO_SHARED_DIR}"

  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr || return 1
  make || return 1
  make DESTDIR=${pkgdir} install || return 1

  # These are gmime alternatives for the same shareutils tools
  mv ${pkgdir}/usr/bin/uuencode ${pkgdir}/usr/bin/guuencode || return 1
  mv ${pkgdir}/usr/bin/uudecode ${pkgdir}/usr/bin/guudecode || return 1
}
