# Maintainer : Tom Gundersen <teg@jklm.no>
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Gabriel Martinez < reitaka at gmail dot com >

pkgname=libplist
pkgver=2.2.0
pkgrel=3
pkgdesc="A library to handle Apple Property List format whereas it's binary or XML"
url="https://libimobiledevice.org/"
arch=('x86_64')
license=('GPL2' 'LGPL2.1')
depends=('glib2')
makedepends=('python' 'cython' 'autoconf-archive' 'git' 'python-setuptools')
options=('!makeflags')
_commit=c5a30e9267068436a75b5d00fcbf95cb9c1f4dcd
source=("git+https://github.com/libimobiledevice/libplist#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  # Update ac_python_devel.m4 to newer ax_python_devel.m4
  git cherry-pick -n 42bb64ba96
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname  
  ./configure --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {    
  cd $pkgname 
  make check  
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
  install -D -m644 cython/plist.pxd "${pkgdir}"/usr/include/plist/cython/plist.pxd
}
