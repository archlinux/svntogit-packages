# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Aaron Griffin <aaron@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=ltrace
pkgver=0.7.3
pkgrel=3
pkgdesc="Tracks runtime library calls in dynamically linked programs"
arch=('x86_64')
url="https://www.ltrace.org/"
license=('GPL')
depends=('elfutils' 'libunwind')
makedepends=('dejagnu')
backup=('etc/ltrace.conf')
source=(https://sources.archlinux.org/other/$pkgname/$pkgname-$pkgver.tar.bz2)
sha256sums=('0e6f8c077471b544c06def7192d983861ad2f8688dd5504beae62f0c5f5b9503')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --disable-werror
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"

  make -k check || true
}

package(){
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
