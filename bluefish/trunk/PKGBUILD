# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=bluefish
pkgver=2.2.5
pkgrel=1
pkgdesc="A powerful HTML editor for experienced web designers and programmers"
arch=('i686' 'x86_64')
url="http://bluefish.openoffice.nl/"
license=('GPL3')
depends=('gtk3' 'enchant' 'python2' 'desktop-file-utils')
makedepends=('gucharmap' 'intltool')
optdepends=('gucharmap: for using the charmap plugin')
install=bluefish.install
source=(http://www.bennewitz.com/bluefish/stable/source/bluefish-${pkgver}.tar.bz2{,.sig})
sha1sums=('efdd577a20a439e58f14f68e725f00be230d5de1'
          'SKIP')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -i 's|/usr/bin/env python|/usr/bin/env python2|' \
    src/plugin_zencoding/zencoding/{,filters/,actions/}*.py \
    data/{cssmin.py,jsbeautify,jsmin.py,lorem-ipsum-generator}
}

build() {
  cd ${pkgname}-${pkgver}
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --enable-spell-check --disable-update-databases --disable-xml-catalog-update
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
