# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=bluefish
pkgver=2.2.10
pkgrel=3
pkgdesc="A powerful HTML editor for experienced web designers and programmers"
arch=('x86_64')
url="http://bluefish.openoffice.nl/"
license=('GPL3')
depends=('gtk3' 'enchant' 'python2')
makedepends=('gucharmap' 'intltool')
optdepends=('gucharmap: for using the charmap plugin')
install=bluefish.install
source=(https://www.bennewitz.com/bluefish/stable/source/bluefish-${pkgver}.tar.bz2{,.sig})
sha1sums=('abb2080b5cbc65cd9c7551572ac21012c5ee2031'
          'SKIP')
validpgpkeys=('58FD02766D031E832560A6A226073EFCDAC576E6')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -i 's|/usr/bin/env python|/usr/bin/env python2|' \
    src/plugin_zencoding/zencoding/{,filters/,actions/}*.py \
    data/{jsbeautify,jsmin.py,lorem-ipsum-generator}
  sed -e 's|\[enchant\]|\[enchant-2\]|g' -e 's|\[enchant >|\[enchant-2 >|g' \
    -e 's|enchant/enchant.h|enchant-2/enchant.h|g' -e 's|BF_dependencies enchant|BF_dependencies enchant-2|g' \
    -i configure.ac
  autoreconf -vi
}

build() {
  cd ${pkgname}-${pkgver}
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --enable-spell-check --disable-update-databases --disable-xml-catalog-update
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
