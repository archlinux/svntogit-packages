# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=epiphany-extensions
pkgver=3.2.0
pkgrel=2
url="http://www.gnome.org/projects/epiphany/"
pkgdesc="Various extentions for the Epiphany web browser"
arch=('i686' 'x86_64')
license=('GPL')
depends=('epiphany' 'opensp' 'hicolor-icon-theme')
makedepends=('gnome-doc-utils' 'intltool' 'gnome-common')
options=('!libtool' '!emptydirs')
install=epiphany-extensions.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz
        rss-link-dbus-glib.patch)
sha256sums=('0707c9109bc6070f5f4e6ef3a9653e18ad5200eb57f396852b4c13cbac69baae'
            '6e1ba177e8480984b4c5447aca5e7cf8bcfacbb90bcf041fa1b6d2a4dc3e818b')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -Np1 -i "$srcdir/rss-link-dbus-glib.patch"
  autoreconf -fi

  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var \
      --disable-scrollkeeper
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
