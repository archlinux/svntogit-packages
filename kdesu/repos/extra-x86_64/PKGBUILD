# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdesu
pkgver=5.98.0
pkgrel=3
pkgdesc='Integration with su for elevated privileges'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(kpty kconfig)
makedepends=(extra-cmake-modules doxygen qt5-tools qt5-doc)
groups=(kf5)
source=(https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz{,.sig}
        https://invent.kde.org/frameworks/kdesu/-/commit/b90b931e.patch
        https://invent.kde.org/frameworks/kdesu/-/commit/bda57d5f.patch)
sha256sums=('629fe9bce7c106a6ad9379fd0174e518a0f5b74e1ae2cda985944c17ff59cb95'
            'SKIP'
            '7ed7024093fca56833f845b2b0ac44e3bad6f6d62c25ae256eca6619447405a0'
            '20cc56df388a7341936b55e740aaf348d95a50d39a4c3150f505245c6bf579a2')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>
options=(debug)

prepare() {
# Drop setgid usage
  patch -d $pkgname-$pkgver -p1 < b90b931e.patch
  patch -d $pkgname-$pkgver -p1 < bda57d5f.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
