# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=php-apcu
pkgver=4.0.7
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc='A userland caching module for PHP'
url='http://pecl.php.net/package/APCu'
depends=('php')
replaces=('php-apc')
conflicts=('php-apc')
provides=("php-apc=${pkgver}")
license=('PHP')
source=("http://pecl.php.net/get/apcu-$pkgver.tgz")
backup=('etc/php/conf.d/apcu.ini')
md5sums=('9feb03d3e5217242a39244964f098b52')

build() {
	cd $srcdir/apcu-$pkgver
	phpize
	./configure --prefix=/usr
	make
}

check() {
	cd $srcdir/apcu-$pkgver
	export NO_INTERACTION=1
	make test
	echo
}

package() {
	cd $srcdir/apcu-$pkgver
	make INSTALL_ROOT=$pkgdir install
	echo ';extension=apcu.so' > apcu.ini
	install -D -m644 apcu.ini $pkgdir/etc/php/conf.d/apcu.ini
	install -D -m644 apc.php $pkgdir/usr/share/webapps/php-apcu/apc.php
	install -D -m644 INSTALL $pkgdir/usr/share/doc/php-apcu/install.txt
}
