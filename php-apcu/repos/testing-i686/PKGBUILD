# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=php-apcu
pkgver=4.0.3
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc='A userland caching module for PHP'
url='http://pecl.php.net/package/APCu'
depends=('php')
replaces=('php-apc')
conflicts=('php-apc')
provides=("php-apc=${pkgver}")
license=('PHP')
source=("http://pecl.php.net/get/apcu-$pkgver.tgz")
backup=('etc/php/conf.d/apcu.ini')
md5sums=('4db3d1ed48c447d9adebec13a8846d48')

build() {
	cd $srcdir/apcu-$pkgver
	phpize
	./configure --prefix=/usr
	make
}

# check() {
# 	cd $srcdir/apcu-$pkgver
# 	printf "n\n" | make test
# 	echo
# }

package() {
	cd $srcdir/apcu-$pkgver
	make INSTALL_ROOT=$pkgdir install
	echo ';extension=apcu.so' > apcu.ini
	install -D -m644 apcu.ini $pkgdir/etc/php/conf.d/apcu.ini
	install -D -m644 apc.php $pkgdir/usr/share/php-apcu/apc.php
	install -D -m644 INSTALL $pkgdir/usr/share/doc/php-apcu/install.txt
}
