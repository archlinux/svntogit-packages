# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=katomic
pkgver=15.03.97
pkgrel=1
pkgdesc="A fun and educational game built around molecular geometry"
url="http://kde.org/applications/games/katomic/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegames')
depends=('libkdegames' 'knotifyconfig')
makedepends=('extra-cmake-modules' 'python' 'kdoctools')
install=${pkgname}.install
conflicts=('kdegames-katomic')
replaces=('kdegames-katomic')
source=("http://download.kde.org/unstable/applications/${pkgver}/src/katomic-${pkgver}.tar.xz")
sha1sums=('7e244de31019a74610765aa2013c33384117bd0f')

build() {
  mkdir build
  cd build
  cmake ../katomic-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
