# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libxft
pkgver=2.2.0
pkgrel=2
pkgdesc="FreeType-based font drawing library for X"
arch=('i686' 'x86_64')
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('fontconfig' 'libxrender')
makedepends=('pkgconfig')
options=('!libtool')
source=("${url}/releases/individual/lib/libXft-${pkgver}.tar.bz2")
sha1sums=('ed29784259f4e26df78141035560ae8a7c62e83f')

build() {
  cd "${srcdir}/libXft-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static
  make
}

package() {
  cd "${srcdir}/libXft-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"

}
