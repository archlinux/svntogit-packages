# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libxft
pkgver=2.3.0
pkgrel=2
pkgdesc="FreeType-based font drawing library for X"
arch=('i686' 'x86_64')
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('fontconfig' 'libxrender')
makedepends=('pkgconfig')
options=('!libtool')
source=("${url}/releases/individual/lib/libXft-${pkgver}.tar.bz2"
        git_fixes.diff)
sha1sums=('687f38a810b9ae58e1169c295d9d438370235e23'
          'a87a56f62905bfcd85469caf349d07f37f84a989')

build() {
  cd "${srcdir}/libXft-${pkgver}"
  # fix broken bold fonts in urxvt terinals - https://bugs.freedesktop.org/show_bug.cgi?id=47178 + https://bugs.freedesktop.org/show_bug.cgi?id=47196
  patch -Np1 -i ${srcdir}/git_fixes.diff
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static
  make
}

package() {
  cd "${srcdir}/libXft-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"

}
