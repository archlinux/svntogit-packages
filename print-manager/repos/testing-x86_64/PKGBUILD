# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=print-manager
pkgver=20.12.0
pkgrel=1
pkgdesc="A tool for managing print jobs and printers"
arch=(x86_64)
url='https://www.kde.org/applications/utilities/'
license=(GPL LGPL FDL)
depends=(kcmutils kirigami2)
makedepends=(extra-cmake-modules kdoctools plasma-framework)
optdepends=('system-config-printer: auto-detect the printer driver')
groups=(kde-applications kde-utilities)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('da373dbc907eda513ee8d41b8b2b3a118d390bae62b829c77076f63b1c3168d3'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
