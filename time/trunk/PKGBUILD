# Maintainer: Dave Reisner <dreisner@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.us>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: damir <damir@archlinux.org>

pkgname=time
pkgver=1.9
pkgrel=2
pkgdesc="Utility for monitoring a program's use of system resources"
arch=('x86_64')
url='https://www.gnu.org/software/time/'
license=('GPL')
depends=('glibc')
makedepends=('git')
source=(https://git.savannah.gnu.org/cgit/time.git/snapshot/time-$pkgver.tar.gz
        git://git.savannah.gnu.org/gnulib.git#commit=9462eb64fbf6646e9109b003a6768538d2b7dad4)
sha1sums=('724d5857f555261c847d80f2783ca41891879c86'
          'SKIP')

prepare() {
  cd "$pkgname-$pkgver"

  ./bootstrap --no-git --gnulib-srcdir="$srcdir/gnulib"
} 

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr --infodir=/usr/share/info
  make
}

check() {
  make -C "$pkgname-$pkgver" check
}

package() {
  cd "$pkgname-$pkgver"

  make exec_prefix="$pkgdir/usr" infodir="$pkgdir/usr/share/info" install
}
