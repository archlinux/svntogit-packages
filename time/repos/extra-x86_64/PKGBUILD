# $Id$
# Maintainer: Dave Reisner <dreisner@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.us>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: damir <damir@archlinux.org>

pkgname=time
pkgver=1.8
pkgrel=1
pkgdesc="Utility for monitoring a program's use of system resources"
arch=('i686' 'x86_64')
url="https://directory.fsf.org/wiki/Time"
license=('GPL')
depends=('glibc')
makedepends=('git')
source=(
  http://git.savannah.gnu.org/cgit/time.git/snapshot/time-$pkgver.tar.gz
  git://git.savannah.gnu.org/gnulib.git#commit=7df04f9b8a0adb1575ca0555775ec10860143cbf)
sha1sums=('5d09ac1b69ae5c7346be1bbcad67b6ac40a5cd25'
          'SKIP')

prepare() {
  cd "$pkgname-$pkgver"

  ./bootstrap --no-git --gnulib-srcdir="$srcdir/gnulib"
} 

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr --infodir=/usr/share/info
  make
}

package() {
  cd "$pkgname-$pkgver"

  make exec_prefix="$pkgdir/usr" infodir="$pkgdir/usr/share/info" install
}
