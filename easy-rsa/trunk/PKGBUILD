# $Id$
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>

pkgname=easy-rsa
pkgver=2.2.2
pkgrel=1
pkgdesc="Simple shell based CA utility"
arch=(any)
url="https://github.com/OpenVPN/easy-rsa"
depends=('openssl' 'sh')
license=('custom')
backup=('usr/share/easy-rsa/vars'
        'usr/share/easy-rsa/openssl-1.0.0.cnf')
source=(${pkgname}-${pkgver}::https://github.com/OpenVPN/easy-rsa/archive/${pkgver}.tar.gz)
md5sums=('040238338980617bc9c2df4274349593')

prepare() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  autoreconf -i
}

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver
  make install DESTDIR="${pkgdir}"
  rm -f ${pkgdir}/usr/share/openvpn/easy-rsa/openssl-0.9.?.cnf
  # Install license
  install -d -m755 "${pkgdir}"/usr/share/licenses/${pkgname}/
  ln -sf /usr/share/doc/${pkgname}/{COPYING,COPYRIGHT.GPL} "${pkgdir}"/usr/share/licenses/${pkgname}/
}
