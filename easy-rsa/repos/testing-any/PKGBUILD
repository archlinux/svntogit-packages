# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>

pkgname=easy-rsa
pkgver=3.1.0
pkgrel=1
pkgdesc='Simple shell based CA utility'
arch=('any')
url='https://github.com/OpenVPN/easy-rsa'
depends=('openssl' 'sh' 'grep')
license=('custom')
backup=('etc/easy-rsa/openssl-easyrsa.cnf'
	'etc/easy-rsa/vars')
validpgpkeys=(
	'65FF3F24AA08E882CB444C94D731D97A606FD463'	# Josh Cepek <josh.cepek@usa.net>
	'6F4056821152F03B6B24F2FCF8489F839D7367F3')	# Eric Crist <ecrist@secure-computing.net>
source=("https://github.com/OpenVPN/easy-rsa/releases/download/v${pkgver}/EasyRSA-${pkgver}.tgz"{,.sig})
sha256sums=('01b07ecb58dc003e47d4e74f9fc4da35ec6842e96cd003d66ed72a43df983659'
            'SKIP')

package() {
	cd EasyRSA-${pkgver}/

	install -D -m0755 easyrsa "${pkgdir}"/usr/bin/easyrsa

	install -D -m0644 openssl-easyrsa.cnf "${pkgdir}"/etc/easy-rsa/openssl-easyrsa.cnf
	install -D -m0644 vars.example "${pkgdir}"/etc/easy-rsa/vars
	install -d -m0755 "${pkgdir}"/etc/easy-rsa/x509-types/
	install -m0644 x509-types/* "${pkgdir}"/etc/easy-rsa/x509-types/

	install -d -m0755  "${pkgdir}"/usr/share/doc/easy-rsa/
	install -m0644 doc/* ChangeLog "${pkgdir}"/usr/share/doc/easy-rsa/

	install -D -m0644 COPYING.md "${pkgdir}"/usr/share/licenses/easy-rsa/COPYING
}

