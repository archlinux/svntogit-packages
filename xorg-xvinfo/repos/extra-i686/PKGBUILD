# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-xvinfo
pkgver=1.1.3
pkgrel=1
pkgdesc="Prints out the capabilities of any video adaptors associated with the display that are accessible through the X-Video extension"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libx11' 'libxv')
makedepends=('xorg-util-macros')
groups=('xorg-apps' 'xorg')
source=(http://xorg.freedesktop.org/archive/individual/app/xvinfo-${pkgver}.tar.bz2{,.sig})
sha256sums=('9fba8b68daf53863e66d5004fa9c703fcecf69db4d151ea2d3d885d621e6e5eb'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') #Alan Coopersmith <alan.coopersmith@oracle.com>

build() {
  cd xvinfo-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd xvinfo-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
