# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Veeti Paananen <veeti.paananen@rojekti.fi>

pkgname=cmark
pkgver=0.30.0
pkgrel=1
pkgdesc='CommonMark parsing and rendering library and program in C'
arch=(x86_64)
url='https://github.com/jgm/cmark'
license=('custom:BSD2')
depends=(glibc)
makedepends=(cmake python)
source=(https://github.com/jgm/cmark/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('53bbb8cdcac5431b88000f69df0aecfc7bd0d9ec0f7bc5a343281ca75e98304ef2674f55f76733acd81f8e8b9079eefabc9b3f3ef7dcd64087497282f17034a9')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

check() {
  cmake --build build --target test
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dm 644 $pkgname-$pkgver/COPYING -t "$pkgdir"/usr/share/licenses/$pkgname
}
