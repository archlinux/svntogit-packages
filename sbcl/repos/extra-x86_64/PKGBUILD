# $Id$
# Contributor: John Proctor <jproctor@prium.net>
# Maintainer: Kevin Piche <kevin@archlinux.org>

pkgname=sbcl
pkgver=1.0.23
pkgrel=1
pkgdesc="Steel Bank Common Lisp"
arch=(i686 x86_64)
license=('custom')
depends=('glibc')
makedepends=('sbcl' 'texinfo')
source=("http://dl.sourceforge.net/sourceforge/sbcl/${pkgname}-${pkgver}-source.tar.bz2" \
	"sbcl-default-sbcl-home.patch")
md5sums=('70a55d758ddd90843d7f93a109fa7ffa' '053ef3ab65501635406134382df17278')
url="http://www.sbcl.org/"
install=sbcl.install

build() {
  cd ${startdir}/src/${pkgname}-${pkgver}
  patch -Np0 -i ../sbcl-default-sbcl-home.patch || return 1
  # Make a multi-threaded SBCL
  cat >customize-target-features.lisp <<EOF
(lambda (features)
  (flet ((enable (x) (pushnew x features))
         (disable (x) (setf features (remove x features))))
  (enable :sb-thread)))
EOF

  sh make.sh sbcl
  mkdir -p ${startdir}/pkg/usr
  pushd doc/manual
  make info || return 1
  popd 
  INSTALL_ROOT=${startdir}/pkg/usr sh install.sh

  # drop unwanted files
  find ${startdir}/pkg -name CVS -exec rm -rf {} \;
  find ${startdir}/pkg -name .cvsignore -exec rm -rf {} \;
  find ${startdir}/pkg -name Makefile -exec rm -rf {} \;

  rm $startdir/pkg/usr/share/info/dir
  gzip -9nf $startdir/pkg/usr/share/info/*
 
  # license
  install -D -m644 ${startdir}/src/${pkgname}-${pkgver}/COPYING \
                   ${startdir}/pkg/usr/share/licenses/${pkgname}/license.txt
}
