# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-artikulate
pkgver=4.13.97
pkgrel=1
pkgdesc="Improve your pronunciation by listening to native speakers"
url="https://projects.kde.org/projects/kde/kdeedu/artikulate"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
depends=('kdebase-runtime' 'qt-gstreamer' 'kqtquickcharts')
makedepends=('cmake' 'automoc4' 'boost')
source=("http://download.kde.org/unstable/${pkgver}/src/artikulate-${pkgver}.tar.xz")
sha1sums=('b1b6d15d8de48990fdc2c6d63176e6d7a72dd2bb')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../artikulate-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
