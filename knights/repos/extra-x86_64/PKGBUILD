# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=knights
pkgver=18.12.0
pkgrel=1
pkgdesc="Chess board by KDE with XBoard protocol support"
arch=(x86_64)
url="https://www.kde.org/applications/games/knights/"
license=(GPL LGPL FDL)
groups=(kde-applications kdegames)
depends=(libkdegames kplotting plasma-framework hicolor-icon-theme)
optdepends=('gnuchess: for playing against the computer')
makedepends=(extra-cmake-modules kdoctools)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('0b83ddcbd5ebc7addadfed899ef5437548e9907d201bebcb1d9af95c3049e982'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF   
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
