# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=kontact
pkgver=18.12.2
pkgrel=1
pkgdesc="KDE Personal Information Manager"
arch=(x86_64)
url="https://kontact.kde.org"
license=(GPL2)
depends=(kontactinterface kmail-account-wizard kdepim-apps-libs)
makedepends=(extra-cmake-modules kdoctools boost)
groups=(kde-applications kdepim)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('b512c1f5ae50dfa324c931853e1b5f1f64b5f3c873d3e5a93c917889d17c11be'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
