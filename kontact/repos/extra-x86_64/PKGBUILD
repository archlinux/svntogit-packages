# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=kontact
pkgver=21.12.3
pkgrel=1
pkgdesc='KDE Personal Information Manager'
arch=(x86_64)
url='https://apps.kde.org/kontact/'
license=(GPL2)
depends=(kontactinterface kmail-account-wizard)
makedepends=(extra-cmake-modules kdoctools boost)
optdepends=(akregator kaddressbook kjots kmail knotes korganizer zanshin)
groups=(kde-applications kde-pim)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('20b923182a5ea30540bb8ce6db0883d07434d0f3490f8ac80354654ac93ae19a'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
options=(debug)

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
