# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=yasm
pkgver=1.2.0
pkgrel=4
pkgdesc="A rewrite of NASM to allow for multiple syntax supported (NASM, TASM, GAS, etc.)"
arch=('i686' 'x86_64')
url="http://www.tortall.net/projects/yasm/"
license=('BSD' 'GPL2' 'LGPL2.1' 'PerlArtistic')
depends=('glibc')
options=('staticlibs')
source=(http://www.tortall.net/projects/yasm/releases/${pkgname}-${pkgver}.tar.gz)
sha1sums=('773d28f27f83c44ac35079add0d3167ca6c1def8')

prepare() {
  cd ${pkgname}-${pkgver}
# FS#38940
  sed -i 's/#define NHASH 31/#define NHASH 4096/' modules/preprocs/nasm/nasm-pp.c
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 BSD.txt "${pkgdir}/usr/share/licenses/${pkgname}/BSD.txt"
}
