# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-kalgebra
pkgver=4.14.3
pkgrel=1
pkgdesc="Graph Calculator"
url="http://kde.org/applications/education/kalgebra/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
depends=('kdebase-runtime' 'kdeedu-analitza')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/kalgebra-${pkgver}.tar.xz")
sha1sums=('69c20ccb8db7c9fa08847f08a7071c719fdf28f4')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../kalgebra-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
