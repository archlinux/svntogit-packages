# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-docs
pkgver=1.7.1
pkgrel=1
pkgdesc="X.org documentations"
arch=(any)
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('man-db')
groups=('xorg')
source=(${url}/archive/individual/doc/${pkgname}-${pkgver}.tar.bz2{,.sig})
sha256sums=('24b8677c3462c10465cf50d40576d76682acd5835526093a575865b2aa242c4b'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') #Alan Coopersmith <alan.coopersmith@oracle.com>

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  cd "${pkgdir}/usr/share/licenses/${pkgname}"
  ln -vsf /usr/share/doc/xorg-docs/License.xml .
}
