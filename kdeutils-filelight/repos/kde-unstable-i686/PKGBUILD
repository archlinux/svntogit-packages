# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeutils-filelight
pkgver=4.9.95
pkgrel=1
pkgdesc='View disk usage information'
url='http://kde.org/applications/utilities/filelight'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
groups=('kde' 'kdeutils')
replaces=('filelight')
conflicts=('filelight')
install=${pkgname}.install
source=("http://download.kde.org/unstable/${pkgver}/src/filelight-${pkgver}.tar.xz")
sha1sums=('9c559eacc9658a4aa227d2bdf28504019cac5e8f')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../filelight-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $srcdir/build
  make DESTDIR=$pkgdir install
  cd $srcdir/build/doc
  make DESTDIR=$pkgdir install
}
