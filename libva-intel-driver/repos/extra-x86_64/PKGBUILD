# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

pkgname=libva-intel-driver
pkgver=1.2.2
pkgrel=2
pkgdesc='VA-API implementation for Intel G45 and HD Graphics family'
arch=('i686' 'x86_64')
url='http://freedesktop.org/wiki/Software/vaapi'
license=('MIT')
depends=('libva')
replaces=('libva-driver-intel')
source=(http://freedesktop.org/software/vaapi/releases/$pkgname/$pkgname-$pkgver.tar.bz2
        Adjust_the_default_value_for_contrast_saturation.patch)
sha1sums=('ece479b51926764edd2be6599f9c067cae2a3942'
          'f2c2c40512b8c209dc7aefb8b20ad02bb9471a3f')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../Adjust_the_default_value_for_contrast_saturation.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
