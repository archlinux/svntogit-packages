# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: Xavier D. <magicrhesus@ouranos.be>
# Contributor: Zarra <zarraxx@gmail.com>

pkgname=dbus-c++
_pkgname=lib$pkgname
pkgver=0.9.0
pkgrel=5
pkgdesc="A C++ API for D-BUS"
url="http://sourceforge.net/projects/dbus-cplusplus/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dbus>=1.2.0' 'glib2')
makedepends=('autoconf' 'automake' 'libtool')
source=("http://sourceforge.net/projects/dbus-cplusplus/files/dbus-c%2B%2B/$pkgver/libdbus-c%2B%2B-$pkgver.tar.gz"
	      'gcc47.patch')
md5sums=('e752116f523fa88ef041e63d3dee4de2'
         '9319b402b15f37d881b51bc758368900')

build() {
    cd "$srcdir/$_pkgname-$pkgver"

    patch -Np0 -i "$srcdir/gcc47.patch"
    export LDFLAGS+=" -lexpat -lpthread" # -lpthread needed for i686

    ./autogen.sh --prefix=/usr \
                 --disable-static \
                 --enable-glib \
                 --disable-ecore
    make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
