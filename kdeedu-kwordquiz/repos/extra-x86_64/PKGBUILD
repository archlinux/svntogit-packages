# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-kwordquiz
pkgver=4.9.3
pkgrel=1
pkgdesc="Flash Card Trainer"
url="http://kde.org/applications/education/kwordquiz/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
depends=('kdebase-runtime' 'libkdeedu')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/kwordquiz-${pkgver}.tar.xz")
sha1sums=('bacfc371da7c0612fa8bf730280b12f0e29bdc46')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../kwordquiz-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
