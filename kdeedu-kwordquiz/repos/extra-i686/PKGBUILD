# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-kwordquiz
pkgver=14.12.2
pkgrel=1
pkgdesc="Flash Card Trainer"
url="http://kde.org/applications/education/kwordquiz/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
depends=('kdebase-runtime' 'libkdeedu')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/applications/${pkgver}/src/kwordquiz-${pkgver}.tar.xz")
sha1sums=('25bf6846709d77a6e5c44e21dd619bc02e24be9c')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../kwordquiz-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
