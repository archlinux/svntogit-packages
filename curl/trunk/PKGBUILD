# $Id$
# Maintainer: Dave Reisner <dreisner@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Lucien Immink <l.immink@student.fnt.hvu.nl>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=curl
pkgver=7.61.0
pkgrel=2
pkgdesc="An URL retrieval utility and library"
arch=('x86_64')
url="https://curl.haxx.se"
license=('MIT')
depends=('ca-certificates' 'krb5' 'libssh2' 'openssl' 'zlib' 'libpsl' 'libnghttp2')
provides=('libcurl.so')
options=('strip' 'debug')
source=("https://curl.haxx.se/download/$pkgname-$pkgver.tar.gz"{,.asc}
        '0001-header-output-switch-off-all-styles-not-just-unbold.patch')
sha512sums=('e55193d1893e7619c8a599299bc030db1307260b19803d01983d4229820e3da8afd274eaee9c5be57911591ffe1fe44ac10d2da38f2d3d3204d8ce9df8d06a93'
            'SKIP'
            'af8df41040778853bef695f0d1cde78efbb907a5f49bd596393096791daa90fccc556a9e8701a7ab8db379fbefe27fffc590c95f33b0917406c1fe16a036281d')
validpgpkeys=('27EDEAF22F3ABCEB50DB9A125CC908FDB71E12C2'   # Daniel Stenberg
              '914C533DF9B2ADA2204F586D78E11C6B279D5C91')  # Daniel Stenberg (old key)

prepare() {
  cd "$pkgname-$pkgver"

  patch -Np1 < ../0001-header-output-switch-off-all-styles-not-just-unbold.patch
}

build() {
  cd "$pkgname-$pkgver"

  ./configure \
      --prefix=/usr \
      --mandir=/usr/share/man \
      --disable-ldap \
      --disable-ldaps \
      --disable-manual \
      --enable-ipv6 \
      --enable-versioned-symbols \
      --enable-threaded-resolver \
      --with-gssapi \
      --with-random=/dev/urandom \
      --with-ca-bundle=/etc/ssl/certs/ca-certificates.crt

  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  make DESTDIR="$pkgdir" install -C scripts

  # license
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
