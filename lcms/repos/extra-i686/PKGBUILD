# $Id$
# Maintainer: Tobias Kieslich <neri@archlinux.org>
# Contributor: Ben <contrasutra@myrealbox.com>

pkgname=lcms
pkgver=1.19
pkgrel=5
pkgdesc="Lightweight color management development library/engine"
arch=('i686' 'x86_64')
license=('custom')
depends=('libtiff')
url="http://www.littlecms.com"
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.gz
        cve-2013-4276.patch)
md5sums=('8af94611baf20d9646c7c2c285859818'
         'fa1db4861cfa05f4c4a2c826e1c35502')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i ../cve-2013-4276.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm 644 COPYING \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
