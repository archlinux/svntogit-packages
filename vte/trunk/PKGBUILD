# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=vte
pkgname=(vte vte3 vte-common)
pkgver=0.28.2
pkgrel=1
pkgdesc="Virtual Terminal Emulator widget for use with"
arch=('i686' 'x86_64')
license=('LGPL')
options=('!libtool' '!emptydirs')
makedepends=('pygtk' 'intltool' 'gobject-introspection' 'gtk2' 'gtk3' 'pygobject-devel' 'gtk-doc')
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/GNOME/sources/vte/0.28/vte-${pkgver}.tar.xz)
sha256sums=('86cf0b81aa023fa93ed415653d51c96767f20b2d7334c893caba71e42654b0ae')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  #warning: type-punning to incomplete type might break strict-aliasing rules
  export CFLAGS="$CFLAGS -fno-strict-aliasing"

  mkdir build-gtk2
  pushd build-gtk2
  PYTHON=python2 ../configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/vte \
      --localstatedir=/var --disable-static \
      --enable-introspection --with-gtk=2.0 \
      --enable-gtk-doc
  make
  popd

  mkdir build-gtk3
  pushd build-gtk3
  PYTHON=python2 ../configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/vte \
      --localstatedir=/var --disable-static \
      --enable-introspection --with-gtk=3.0 \
      --enable-gtk-doc
  make
  popd
}

package_vte(){
  pkgdesc+=" GTK2"
  depends=('gtk2' 'vte-common')
  cd "${srcdir}/${pkgbase}-${pkgver}"/build-gtk2
  make DESTDIR="${pkgdir}" install

  rm "${pkgdir}"/usr/lib/vte/gnome-pty-helper
}

package_vte3(){
  pkgdesc+=" GTK3"
  depends=('gtk3' 'vte-common')
  cd "${srcdir}/${pkgbase}-${pkgver}"/build-gtk3
  make DESTDIR="${pkgdir}" install

  rm "${pkgdir}"/usr/lib/vte/gnome-pty-helper
}

package_vte-common() {
  pkgdesc="Common files used by vte and vte3"
  cd "${srcdir}/${pkgbase}-${pkgver}"/build-gtk3

  make -C gnome-pty-helper DESTDIR="${pkgdir}" install
}
