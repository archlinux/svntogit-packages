# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor : shining <shiningxc.at.gmail.com>
# Contributor : cyberdune <cyberdune@gmail.com>

pkgname=octave
pkgver=3.2.4
pkgrel=1
pkgdesc="A high-level language, primarily intended for numerical computations."
arch=('i686' 'x86_64')
url="http://www.octave.org"
license=('GPL')
depends=('fftw' 'pcre' 'curl' 'lapack' 'libx11' 'graphicsmagick' 'glpk' 'hdf5' 'gcc-libs')
makedepends=('texinfo' 'graphicsmagick' 'gcc-fortran' 'umfpack')
optdepends=('texinfo: for help-support in octave'
            'gnuplot: alternative plotting'
	    'umfpack: LU decomposition of some large sparse matrices')
source=("ftp://ftp.octave.org/pub/octave/octave-$pkgver.tar.bz2"
	'octave-3.2.0_as_needed.patch'  'octave-3.2.0_parallel_make.patch')
options=('!emptydirs')
install=octave.install
md5sums=('608196657f4fa010420227b77333bb71'
         '33c8886cd908ace40f8b60334df1c34f'
         'd59d783a8d7e8d8306caed4b2b8671a4')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # gentoo patches
  patch -Np1 -i ${srcdir}/octave-3.2.0_as_needed.patch || return 1
  patch -Np1 -i ${srcdir}/octave-3.2.0_parallel_make.patch || return 1
  
  # http://www.nabble.com/Random-rounding-errors-td16010966.html
  FFLAGS="-O -ffloat-store" \

  ./configure --prefix=/usr --libexecdir=/usr/lib \
  --enable-shared --disable-static

  make || return 1
  make DESTDIR="${pkgdir}" install || return 1

}
