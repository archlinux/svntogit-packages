# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor : shining <shiningxc.at.gmail.com>
# Contributor : cyberdune <cyberdune@gmail.com>

pkgname=octave
pkgver=3.0.5
pkgrel=4
pkgdesc="A high-level language, primarily intended for numerical computations."
arch=('i686' 'x86_64')
url="http://www.octave.org"
license=('GPL')
depends=('fftw' 'pcre' 'curl' 'blas' 'lapack')
makedepends=('texinfo')
optdepends=('texinfo: for help-support in octave'
            'gnuplot: to be able to plot in octave')
provides=(octave3)
conflicts=(octave3)
replaces=(octave3)
source=("ftp://ftp.octave.org/pub/octave/octave-$pkgver.tar.bz2")
options=('!emptydirs')
install=octave.install
md5sums=('bfba44eeda8fa6e29c2bbcd2604c34eb')

build() {
  cd "${srcdir}/octave-${pkgver}"

  ./autogen.sh

  # http://www.nabble.com/Random-rounding-errors-td16010966.html
  FFLAGS="-O -ffloat-store" \

  ./configure --prefix=/usr --libexecdir=/usr/lib \
  --without-hdf5 --without-umfpack

  make || return 1
  make DESTDIR="${pkgdir}" install || return 1

  cd ${pkgdir}/usr/share/info
  rm -f dir
  gzip * || return 1
}

