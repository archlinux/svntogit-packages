# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor : shining <shiningxc.at.gmail.com>
# Contributor : cyberdune <cyberdune@gmail.com>

pkgname=octave
pkgver=3.4.3
pkgrel=1
pkgdesc="A high-level language, primarily intended for numerical computations."
arch=('i686' 'x86_64')
url="http://www.octave.org"
license=('GPL')
depends=('fftw' 'pcre' 'curl' 'lapack' 'libx11' 'graphicsmagick' 'glpk' 'hdf5' 'gcc-libs' 'qhull')
makedepends=('texinfo' 'graphicsmagick' 'gcc-fortran' 'umfpack' 'texlive-core' 'fltk')
optdepends=('texinfo: for help-support in octave'
            'gnuplot: alternative plotting'
	    'umfpack: LU decomposition of some large sparse matrices'
	    'fltk: alternative plotting')
source=("ftp://ftp.gnu.org/gnu/octave/octave-$pkgver.tar.bz2")
options=('!emptydirs')
install=octave.install
sha1sums=('fe622c28a38f8730c59e46211bc7b18e7f51a679')
	
build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # http://www.nabble.com/Random-rounding-errors-td16010966.html
  FFLAGS="-O -ffloat-store" \

  ./configure --prefix=/usr --libexecdir=/usr/lib \
  --enable-shared --disable-static --with-quantum-depth=16

  LANG=C make
}

package(){
  cd ${srcdir}/${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install

  # add octave library path to ld.so.conf.d
  install -d ${pkgdir}/etc/ld.so.conf.d
  echo "/usr/lib/${pkgname}-${pkgver}" > ${pkgdir}/etc/ld.so.conf.d/${pkgname}.conf
}
