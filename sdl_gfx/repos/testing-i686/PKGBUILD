# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=sdl_gfx
pkgver=2.0.24
pkgrel=1
pkgdesc="SDL Graphic Primitives"
arch=('i686' 'x86_64')
license=('LGPL')
options=(!libtool)
url="http://www.ferzkopp.net/joomla/software-mainmenu-14/4-ferzkopps-linux-software/19-sdlgfx"
source=(http://www.ferzkopp.net/Software/SDL_gfx-2.0/SDL_gfx-${pkgver}.tar.gz)
depends=('sdl')
md5sums=('838514185ff9a3b6377760aaa52fef8a')

build() {
  cd SDL_gfx-${pkgver}

  if [ "${CARCH}" == "x86_64" ]; then
    ./configure --prefix=/usr --disable-mmx
  else
    ./configure --prefix=/usr
  fi

  make
}

package() {
  cd SDL_gfx-${pkgver}
  make DESTDIR=${pkgdir} install
}
