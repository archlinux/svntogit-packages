# $Id$
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgname=syslinux
pkgver=5.01
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc="Collection of boot loaders that boot from FAT, ext2/3/4 and btrfs filesystems, from CDs and via PXE"
url="http://syslinux.zytor.com/"
license=('GPL2')
depends=('perl' 'glibc')
optdepends=('perl-passwd-md5:  For md5pass'
            'perl-digest-sha1: For sha1pass'
            'mtools:           For mkdiskimage and syslinux support'
            'gptfdisk:         For GPT support'
            'util-linux:       For isohybrid')
makedepends=('nasm')
backup=('boot/syslinux/syslinux.cfg')
install=syslinux.install
source=(http://www.kernel.org/pub/linux/utils/boot/syslinux/$pkgname-${pkgver}.tar.bz2
        syslinux-dont-build-dos-windows-targets.patch
        syslinux.cfg
        syslinux-install_update)
md5sums=('6c21244277fa4e444967ba45469de863'
         '25af85a05e7b212b7fb8b7587175e492'
         'd0c58b16de6e5660a28b3bc248185a38'
         'c1c4fcbafc39dc8a8fb00f6f452b8741')

build() {
  # Do not try to build syslinux with our default LDFLAGS, it will fail
  unset LDFLAGS
  cd "$srcdir"/$pkgname-${pkgver}
  # Do not try to build the Windows or DOS installers
  patch -p1 -i "$srcdir"/syslinux-dont-build-dos-windows-targets.patch
  # Fix FHS manpage path
  sed 's|/usr/man|/usr/share/man|g' -i mk/syslinux.mk
  make installer
  make -C utils
  make -C com32
}

package() {
  cd "$srcdir"/$pkgname-${pkgver}
  make INSTALLROOT="$pkgdir" AUXDIR=/usr/lib/syslinux install

  # Install the default configuration
  install -D -m644 "$srcdir"/syslinux.cfg "$pkgdir"/boot/syslinux/syslinux.cfg
  # Install the installation and update script
  # This script is maintained at git://gist.github.com/772138.git
  install -D -m755 "$srcdir"/syslinux-install_update "$pkgdir"/usr/sbin/syslinux-install_update
  # move extlinux binary to /usr/sbin
  mv "$pkgdir"/sbin/extlinux "$pkgdir"/usr/sbin/extlinux
  rmdir "$pkgdir"/sbin
}
