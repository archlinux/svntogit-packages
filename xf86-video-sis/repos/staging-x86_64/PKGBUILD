# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xf86-video-sis
pkgver=0.10.7
pkgrel=5
pkgdesc="X.org SiS video driver"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('glibc')
makedepends=('xorg-server-devel' 'X-ABI-VIDEODRV_VERSION=15' 'xf86dgaproto')
conflicts=('xorg-server<1.15.0' 'X-ABI-VIDEODRV_VERSION<15' 'X-ABI-VIDEODRV_VERSION>=16')
optdepends=('sis-dri: DRI1 support from community repo')
groups=('xorg-drivers' 'xorg')
source=(${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2
	0001-Disable-UploadToScreen-and-DownloadFromScreen.patch
	git-fixes.patch
	Xi.patch)
sha256sums=('be2eb6acba081e88dabc5be9db379e3da89a4d4edeb68064f204bf343a411cd0'
            '3108c60b3030c894fbdb1426b05d7a8281e5fa4f385e1a80e22c8813a3fb6f5b'
            '7c56b4becd4334a6bdd600ea073226bdb3e0768e4c6b81aaf98b36f1e02f5217'
            '7a1a0b784664a0f011bd13395be1854dee407e901d2707ab1a6b8d20caa0a672')

build() {
  cd ${pkgname}-${pkgver}
  patch -Np1 -i "${srcdir}/0001-Disable-UploadToScreen-and-DownloadFromScreen.patch"
  patch -Np1 -i ${srcdir}/git-fixes.patch
  patch -Np1 -i ${srcdir}/Xi.patch
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
