# $Id: PKGBUILD 232534 2015-03-04 08:30:10Z fyan $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: birdflesh <antkoul at gmail dot com>

pkgname=libkscreen4
_pkgname=libkscreen
pkgver=1.0.5
pkgrel=2
pkgdesc="KDE's screen management library"
arch=('i686' 'x86_64')
url='https://projects.kde.org/libkscreen'
license=('GPL')
depends=('kdelibs' 'qjson')
makedepends=('cmake' 'automoc4')
conflicts=('libkscreen<5')
replaces=('libkscreen<5')
source=("http://download.kde.org/stable/${_pkgname}/${pkgver}/src/${_pkgname}-${pkgver}.tar.xz")
md5sums=('b1cc2805cd5ff468d534d75cbab69426')

prepare() {
  mkdir -p build
  
# Don't build tests
  sed -e '/tests/d' -i $_pkgname-$pkgver/CMakeLists.txt
}

build() {
  cd build
  cmake ../$_pkgname-$pkgver \
    -DQT_QMAKE_EXECUTABLE=qmake-qt4 \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
