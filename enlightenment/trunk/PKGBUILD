# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>

pkgname=enlightenment
pkgver=1.0.7
_themever=1.0.1
pkgrel=1
pkgdesc="A fast, flexible, and very extensible Window Manager"
arch=('i686' 'x86_64')
url="http://www.enlightenment.org"
license=('BSD' 'custom')
depends=('libxinerama' 'libxft' 'libxxf86vm' 'esound' 'imlib2' 'perl' \
         'libxcomposite' 'libxdamage' 'libxrandr')
makedepends=('xbitmaps')
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/e16-${pkgver}.tar.gz \
        http://downloads.sourceforge.net/enlightenment/e16-themes-${_themever}.tar.gz)
md5sums=('c0594e9ace69497f5ab6b838dac8ed6f' 'bbd9cfd969610c29c7627c2ba1ce3094')
sha1sums=('69a38c68f680778c3689a4369bfcbdb907cfc8a6' 'ae34e2df1294198795736bf953bf695aba93b710')

build() {
  cd "${srcdir}/e16-${pkgver}"
  sed -i 's|xsessiondir = $(datadir)/xsessions|xsessiondir = /etc/X11/sessions|' misc/Makefile.in
  ./configure --prefix=/usr --sysconfdir=/etc --enable-sound --enable-mans
  make

  cd "${srcdir}/e16-themes-${_themever}"
  ./configure --prefix=/usr
}

package() {
  cd "${srcdir}/e16-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -D -m755 "${pkgdir}/usr/share/e16/misc/Xclients.e16-gnome.sh" "${pkgdir}/usr/bin/e16-gnome"
  install -D -m755 "${pkgdir}/usr/share/e16/misc/Xclients.e16-kde.sh" "${pkgdir}/usr/bin/e16-kde"
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
  install -D -m644 fonts/COPYRIGHT.Vera "${pkgdir}/usr/share/licenses/${pkgname}/COPYRIGHT.Vera"

  cd "${srcdir}/e16-themes-${_themever}"
  make DESTDIR="${pkgdir}" install
  chown -R root:root "${pkgdir}"/usr/share/*
}
