# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=enlightenment
pkgver=1.0.9
_themever=1.0.1
pkgrel=2
pkgdesc="A fast, flexible, and very extensible Window Manager"
arch=('i686' 'x86_64')
url="http://www.enlightenment.org"
license=('BSD' 'custom')
depends=('libxinerama' 'imlib2' 'perl' 'pango' 'libxcomposite' \
         'libxdamage' 'libxrandr' 'libpulse' 'audiofile')
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/e16-${pkgver}.tar.gz \
        http://downloads.sourceforge.net/enlightenment/e16-themes-${_themever}.tar.gz)
md5sums=('ad85044366efe64cb67f4a8e49a37f84'
         'bbd9cfd969610c29c7627c2ba1ce3094')
sha1sums=('97929090b5d66fa28067b21b17f48cedea1f81bb'
          'ae34e2df1294198795736bf953bf695aba93b710')

build() {
  cd "${srcdir}/e16-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --enable-sound-pulse 
  make

  cd "${srcdir}/e16-themes-${_themever}"
  ./configure --prefix=/usr
}

package() {
  cd "${srcdir}/e16-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -D -m755 "${pkgdir}/usr/share/e16/misc/Xclients.e16-gnome.sh" "${pkgdir}/usr/bin/e16-gnome"
  install -D -m755 "${pkgdir}/usr/share/e16/misc/Xclients.e16-kde.sh" "${pkgdir}/usr/bin/e16-kde"
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
  install -D -m644 fonts/COPYRIGHT.Vera "${pkgdir}/usr/share/licenses/${pkgname}/COPYRIGHT.Vera"

  cd "${srcdir}/e16-themes-${_themever}"
  make DESTDIR="${pkgdir}" install
  chown -R root:root "${pkgdir}"/usr/share/*
}
