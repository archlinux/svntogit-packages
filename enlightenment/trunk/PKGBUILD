# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=enlightenment
pkgver=1.0.2
_themever=1.0.0
pkgrel=1
pkgdesc="A fast, flexible, and very extensible Window Manager"
arch=('i686' 'x86_64')
url="http://www.enlightenment.org"
license=('BSD' 'custom')
depends=('freetype2' 'libxinerama' 'libxft' 'libsm' 'libxxf86vm' 'esound' 'imlib2' 'perl' 'libxcomposite' 'libxdamage' 'libxrandr')
makedepends=('xbitmaps')
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/e16-${pkgver}.tar.gz \
        http://downloads.sourceforge.net/enlightenment/e16-themes-${_themever}.tar.gz)
md5sums=('67d139ff8fed77eea8f4acd4f9e87402' '0697ae00a32938f45d786b40a0b276fb')
sha1sums=('8d1f7591da87578fbe053e57564316b88aff1561' '2fedae04c7563a6bc43497bec0f189e8d439c183')

build() {
  cd "${srcdir}/e16-${pkgver}"
  sed -i 's|xsessiondir = $(datadir)/xsessions|xsessiondir = /etc/X11/sessions|' misc/Makefile.in || return 1
  ./configure --prefix=/usr --sysconfdir=/etc --enable-fsstd --enable-sound --enable-mans || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
  install -D -m755 "${pkgdir}/usr/share/e16/misc/Xclients.e16-gnome.sh" "${pkgdir}/usr/bin/e16-gnome" || return 1
  install -D -m755 "${pkgdir}/usr/share/e16/misc/Xclients.e16-kde.sh" "${pkgdir}/usr/bin/e16-kde" || return 1
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING" || return 1
  install -D -m644 fonts/COPYRIGHT.Vera "${pkgdir}/usr/share/licenses/${pkgname}/COPYRIGHT.Vera" || return 1

  cd "${srcdir}/e16-themes-${_themever}"
  ./configure --prefix=/usr --enable-fsstd || return 1
  make DESTDIR="${pkgdir}" install || return 1
  chown -R root:root "${pkgdir}"/usr/share/* || return 1
}
