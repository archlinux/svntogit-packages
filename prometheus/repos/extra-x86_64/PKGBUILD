# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>

pkgname=prometheus
pkgver=2.30.0
pkgrel=1

pkgdesc='An open-source systems monitoring and alerting toolkit'
url='https://prometheus.io'
arch=('x86_64')
license=('Apache')

depends=('glibc')
makedepends=('go' 'git' 'npm' 'nodejs')

backup=('etc/prometheus/prometheus.yml' 'etc/conf.d/prometheus')

source=("prometheus-v$pkgver.tar.gz::https://github.com/prometheus/prometheus/archive/v$pkgver.tar.gz"
        prometheus.service
        prometheus.sysusers
        prometheus.conf)

sha256sums=('900dc07f54c1251f22d18c2a5751bb1b0192b3d9960406a2c7ea3098a688d53c'
            'f358986865bd3d06dc47a265aeb3b5ef64aa644e831f07731c2fc7af64d9e98f'
            '2747fabb4e56b808361eb7dd7acf9729ab8973d1ebe2f857dd56f6c71f71e45f'
            '6d32deb125381cbebac11b6953a7d9a65eb7e50f209dc1e22c63facf678a3070')

build() {
  cd prometheus-$pkgver

  LDFLAGS="-extldflags $LDFLAGS \
    -X github.com/prometheus/common/version.Version=$pkgver \
    -X github.com/prometheus/common/version.Revision=$pkgver \
    -X github.com/prometheus/common/version.Branch=tarball \
    -X github.com/prometheus/common/version.BuildUser=someone@builder \
    -X github.com/prometheus/common/version.BuildDate=$(date -u '+%Y%m%d-%H:%M:%S' --date=@${SOURCE_DATE_EPOCH})"

  # The makefile seems to not be able to execute npm properly, so run the make assets target manually
  cd web/ui/module/codemirror-promql
  npm install
  npm run build

  cd $srcdir/prometheus-$pkgver/web/ui
  GOOS= GOARCH= go generate -x -v

  cd $srcdir/prometheus-$pkgver
  # Build the react app, normally a part of the assets target
  make web/ui/static/react

  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external $LDFLAGS" \
    ./cmd/prometheus
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external $LDFLAGS" \
    ./cmd/promtool
}

check() {
  cd prometheus-$pkgver

  go test -short ./...
}

package() {
  install -Dm644 prometheus.service "$pkgdir"/usr/lib/systemd/system/prometheus.service
  install -Dm644 prometheus.sysusers "$pkgdir"/usr/lib/sysusers.d/prometheus.conf
  install -Dm644 prometheus.conf "${pkgdir}"/etc/conf.d/prometheus

  cd prometheus-$pkgver

  install -Dm755 -t "$pkgdir"/usr/bin prometheus promtool
  install -Dm640 -g210 -t "$pkgdir"/etc/prometheus documentation/examples/prometheus.yml
  install -dm750 -o210 -g210 "$pkgdir"/var/lib/prometheus

  # Web
  install -dm755 "$pkgdir"/usr/share/prometheus/web/ui
  cp -R web/ui/{static,templates} "$pkgdir"/usr/share/prometheus/web/ui/

  # Examples
  install -Dm644 -t "$pkgdir"/usr/share/doc/prometheus/examples documentation/examples/prometheus*.yml
  cp -R consoles console_libraries "$pkgdir"/usr/share/doc/prometheus/examples

}
