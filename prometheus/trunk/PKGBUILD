# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>

pkgname=prometheus
pkgver=2.17.0
pkgrel=1

pkgdesc='An open-source systems monitoring and alerting toolkit'
url='https://prometheus.io'
arch=('x86_64')
license=('Apache')

depends=('glibc')
makedepends=('go-pie' 'git' 'bzr' 'yarn')

backup=('etc/prometheus/prometheus.yml')

source=("prometheus-v$pkgver.tar.gz::https://github.com/prometheus/prometheus/archive/v$pkgver.tar.gz"
        prometheus.service
        prometheus.sysusers)

sha256sums=('b5e508f1c747aaf50dd90a48e5e2a3117fec2e9702d0b1c7f97408b87a073009'
            '7f0e6829300032c83912da9d89001c6d03f7f0fe20c4277294129c4ac03c83f3'
            '2747fabb4e56b808361eb7dd7acf9729ab8973d1ebe2f857dd56f6c71f71e45f')

build() {
  cd prometheus-$pkgver

  LDFLAGS="-extldflags $LDFLAGS \
    -X github.com/prometheus/common/version.Version=$pkgver \
    -X github.com/prometheus/common/version.Revision=$pkgver \
    -X github.com/prometheus/common/version.Branch=tarball \
    -X github.com/prometheus/common/version.BuildUser=someone@builder \
    -X github.com/prometheus/common/version.BuildDate=$(date -d@"$SOURCE_DATE_EPOCH" +%Y%m%d-%H:%M:%S)"

  make assets
  go build \
    -gcflags "all=-trimpath=$PWD" \
    -asmflags "all=-trimpath=$PWD" \
    -ldflags "$LDFLAGS" \
    ./cmd/prometheus
  go build \
    -gcflags "all=-trimpath=$PWD" \
    -asmflags "all=-trimpath=$PWD" \
    -ldflags "$LDFLAGS" \
    ./cmd/promtool
}

check() {
  cd prometheus-$pkgver

  go test -short ./...
}

package() {
  install -Dm644 prometheus.service "$pkgdir"/usr/lib/systemd/system/prometheus.service
  install -Dm644 prometheus.sysusers "$pkgdir"/usr/lib/sysusers.d/prometheus.conf

  cd prometheus-$pkgver

  install -Dm755 -t "$pkgdir"/usr/bin prometheus promtool
  install -Dm640 -g210 -t "$pkgdir"/etc/prometheus documentation/examples/prometheus.yml
  install -dm755 -o210 -g210 "$pkgdir"/var/lib/prometheus

  # Web
  install -dm755 "$pkgdir"/usr/share/prometheus/web/ui
  cp -R web/ui/{static,templates} "$pkgdir"/usr/share/prometheus/web/ui/

  # Examples
  install -Dm644 -t "$pkgdir"/usr/share/doc/prometheus/examples documentation/examples/prometheus*.yml
  cp -R consoles console_libraries "$pkgdir"/usr/share/doc/prometheus/examples

}
