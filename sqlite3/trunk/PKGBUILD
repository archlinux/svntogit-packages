# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgbase="sqlite3"
pkgname=('sqlite3' 'sqlite3-tcl' 'sqlite3-doc') 
_amalgamationver=3070500
_amalgamationver2=${_amalgamationver/00/}
pkgver=${_amalgamationver2//0/.}
pkgrel=1
pkgdesc="A C library that implements an SQL database engine"
arch=('i686' 'x86_64')
license=('custom')
url="http://www.sqlite.org/"
makedepends=('tcl' 'readline>=6.0.00')
source=( #http://www.sqlite.org/sqlite-src-${_amalgamationver}.zip - # the no more recommended ZIP archive of the complete source tree for SQLite - Makefile and configure script in this tarball are not supported
	http://www.sqlite.org/sqlite-autoconf-$_amalgamationver.tar.gz # tarball containing the amalgamation for SQLite 3.7.5 together with an configure script and makefile for building it.
	http://www.sqlite.org/sqlite-tea-$_amalgamationver.tar.gz # tarball of the amalgamation together with a Tcl Extension Architecture (TEA) compatible configure script and makefile
	http://www.sqlite.org/sqlite-doc-${_amalgamationver}.zip
	license.txt)
options=('!libtool' '!emptydirs')
md5sums=('a9604a82613ade2e7f4c303f233e477f'
         '3413e8cfb7990e39260bf45dca017bf0'
         '74f9b304143a76e3da8c0063888770ee'
         'c1cdbc5544034d9012e421e75a5e4890')

build() {
#  cd ${srcdir}/sqlite-src-${_amalgamationver}
#  export LTLINK_EXTRAS="-ldl"
#  export CFLAGS="$CFLAGS -DSQLITE_ENABLE_FTS3=1 -DSQLITE_ENABLE_COLUMN_METADATA=1 -DSQLITE_ENABLE_UNLOCK_NOTIFY -DSQLITE_SECURE_DELETE"
#  ./configure --prefix=/usr \
#	--enable-threadsafe \
#	--enable-cross-thread-connections \
#	--disable-static \
#	--enable-load-extension

  # rpath removal
#  sed -i 's|^hardcode_libdir_flag_spec=.*|hardcode_libdir_flag_spec=""|g' libtool
#  sed -i 's|^runpath_var=LD_RUN_PATH|runpath_var=DIE_RPATH_DIE|g' libtool

#  make

  export CFLAGS="$CFLAGS -DSQLITE_ENABLE_FTS3=1 -DSQLITE_ENABLE_COLUMN_METADATA=1 -DSQLITE_ENABLE_UNLOCK_NOTIFY -DSQLITE_SECURE_DELETE"

  # build sqlite
  cd "$srcdir"/sqlite-autoconf-$_amalgamationver
  ./configure --prefix=/usr \
	--disable-static
  make
  
  # build the tcl extension    
  cd "$srcdir"/sqlite-tea-$_amalgamationver
  ./configure --prefix=/usr \
	--with-system-sqlite
  make

}

package_sqlite3() {
 
 pkgdesc="A C library that implements an SQL database engine"
 depends=('readline>=6.0.00')
 conflicts=('sqlite')
 replaces=('sqlite')
 
#  cd ${srcdir}/sqlite-src-${_amalgamationver}
#  make DESTDIR=${pkgdir} install
#  install -Dm0644 sqlite3.1 ${pkgdir}/usr/share/man/man1/sqlite3.1

  cd ${srcdir}/sqlite-autoconf-$_amalgamationver
  make DESTDIR=${pkgdir} install
  
  # license
  install -D -m644 ${srcdir}/license.txt ${pkgdir}/usr/share/licenses/${pkgname}/license.txt
}

package_sqlite3-tcl() {
 
 pkgdesc="sqlite3 Tcl Extension Architecture (TEA)"
 depends=('sqlite3>=3.7.5')

  cd ${srcdir}/sqlite-tea-$_amalgamationver
  make DESTDIR=${pkgdir} install
}

package_sqlite3-doc() {
 pkgdesc="most of the static HTML files that comprise this website, including all of the SQL Syntax and the C/C++ interface specs and other miscellaneous documentation"
 #arch=('any') - not yet supported

  cd ${srcdir}/sqlite-doc-${_amalgamationver}
  mkdir -p ${pkgdir}/usr/share/doc/${pkgbase}
  cp -R *  ${pkgdir}/usr/share/doc/${pkgbase}/
}
