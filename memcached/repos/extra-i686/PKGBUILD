# $Id$
# Maintainer: Dan McGee <dan@archlinux.org>
# Contributor: Michael Irwin <6d6469@gmail.com>

pkgname=memcached
pkgver=1.4.18
pkgrel=1
pkgdesc="A distributed memory object caching system"
arch=(i686 x86_64)
url="http://memcached.org/"
license=('GPL')
depends=('libevent')
optdepends=('perl: for memcached-tool usage')
install=memcached.install
source=(http://www.memcached.org/files/$pkgname-$pkgver.tar.gz
        memcached.service
        0001-don-t-drop-to-a-condition-without-holding-the-lock.patch
        0002-flag-crawler-as-running-during-the-request-to-run.patch)
sha256sums=('6a5cc8d0874f13c5043d741af83fbdc4c681316d32b8313c0dbc82ca96f97439'
            'e768a48192aefa2e2f443c86b3c085043005ffc313da40f3074c060a18c8359d'
            'dc25b5128e4b5f21d2a2d6186517139281e2e3bfc6142eaa72ef5840dbfc1236'
            '07f959563571125248914392fb2a1e3f75a8692f5724c7ed98f0f16737801527')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 < ../0001-don-t-drop-to-a-condition-without-holding-the-lock.patch
  patch -Np1 < ../0002-flag-crawler-as-running-during-the-request-to-run.patch
  ./configure --prefix=/usr
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make test
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -D -m 755 scripts/memcached-tool "$pkgdir"/usr/bin/memcached-tool
  install -D -m 644 ../memcached.service "$pkgdir"/usr/lib/systemd/system/memcached.service
}
