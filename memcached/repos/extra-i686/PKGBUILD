# $Id$
# Maintainer: Dan McGee <dan@archlinux.org>
# Contributor: Michael Irwin <6d6469@gmail.com>

pkgname=memcached
pkgver=1.4.23
pkgrel=1
pkgdesc="A distributed memory object caching system"
arch=(i686 x86_64)
url="http://memcached.org/"
license=('GPL')
depends=('libevent')
optdepends=('perl: for memcached-tool usage')
install=memcached.install
source=(http://www.memcached.org/files/$pkgname-$pkgver.tar.gz
        0001-fix-major-off-by-one-issue.patch
        memcached.service)
sha256sums=('2c70e691fced8db874a98bfb954015e0520151bde5a7db9a926447320ce7a777'
            '6ac80a1e529c98c0c62b237cc82774346d95b975a29d35895ebf8f8e50fccbc4'
            'e768a48192aefa2e2f443c86b3c085043005ffc313da40f3074c060a18c8359d')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 < ../0001-fix-major-off-by-one-issue.patch
  ./configure --prefix=/usr
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make test
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -D -m 755 scripts/memcached-tool "$pkgdir"/usr/bin/memcached-tool
  install -D -m 644 ../memcached.service "$pkgdir"/usr/lib/systemd/system/memcached.service
}
