# $Id$
# Maintainer: Dan McGee <dan@archlinux.org>
# Contributor: Michael Irwin <6d6469@gmail.com>

pkgname=memcached
pkgver=1.4.10
pkgrel=1
pkgdesc="A distributed memory object caching system"
arch=(i686 x86_64)
url="http://memcached.org/"
license=('GPL')
depends=('libevent')
optdepends=('perl: for memcached-tool usage')
backup=('etc/conf.d/memcached')
source=(http://memcached.googlecode.com/files/$pkgname-$pkgver.tar.gz
        memcached.conf
		memcached.sh)
changelog=ChangeLog
sha256sums=('2cc3de341b561464da29fc5d5e2a4facc0f6aafc151f0e1d94228705e0758fdf'
            '0e57a0f528b387f1f04f628ec74856c615d82d44f4d6bc041c33b3814ce13d0a'
            '72efa639c5a39c7c14f07cc51731ebbf82299870d9cf31cf3aaa981bc084b4eb')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -D -m 755 scripts/memcached-tool "$pkgdir"/usr/bin/memcached-tool
  # This POS is entirely RedHat specific, so not including it now
  #install -D -m 755 scripts/memcached.service "$pkgdir"/lib/systemd/system/memcached.service
  #sed -i -e 's#^EnvironmentFile.*#EnvironmentFile=/etc/conf.d/memcached#' "$pkgdir"/lib/systemd/system/memcached.service
  install -D -m 644 "$srcdir/memcached.conf" "$pkgdir"/etc/conf.d/memcached
  install -D -m 755 "$srcdir/memcached.sh" "$pkgdir"/etc/rc.d/memcached
}
