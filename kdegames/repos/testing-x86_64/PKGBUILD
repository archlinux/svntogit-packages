# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdegames
pkgver=4.2.4
pkgrel=1
pkgdesc="KDE Games"
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('kde')
depends=('kdelibs' 'kdebase-runtime' 'qca' 'ggz-client-libs')
makedepends=('pkgconfig' 'cmake' 'automoc4')
options=('docs')
install='kdegames.install'
source=("http://download.kde.org/stable/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2")
md5sums=('ac4a0315a35ad47b95a6a656127f8731')

build() {
	cd $srcdir
	mkdir build
	cd build
	cmake ../${pkgname}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed' \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
	make DESTDIR=$pkgdir install
	# will be generated during install
	rm -rf $pkgdir/etc
	install -D -m644 $srcdir/${pkgname}-${pkgver}/kreversi/module.dsc $pkgdir/usr/share/ggz/kreversi.dsc
	install -D -m644 $srcdir/${pkgname}-${pkgver}/ksquares/src/module.dsc $pkgdir/usr/share/ggz/ksquares.dsc
	install -D -m644 $srcdir/${pkgname}-${pkgver}/kbattleship/src/module.dsc $pkgdir/usr/share/ggz/kbattleship.dsc
}
