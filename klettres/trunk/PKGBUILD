# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=klettres
pkgver=16.04.1
pkgrel=1
pkgdesc="Learn The Alphabet"
url="http://kde.org/applications/education/klettres/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdeedu')
depends=('knewstuff' 'kemoticons' 'kitemmodels' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'python' 'kdoctools')
replaces=('kdeedu-klettres')
conflicts=('kdeedu-klettres')
source=("http://download.kde.org/stable/applications/${pkgver}/src/klettres-${pkgver}.tar.xz")
sha1sums=('fa11f2844f562312494327f23e937fceaaf9f08c')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
