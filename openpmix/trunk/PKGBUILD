# Maintainer: David Runge <dvzrv@archlinux.org>

_name=pmix
pkgname=openpmix
pkgver=4.2.2
pkgrel=1
pkgdesc="Extended version of the PMI standard"
arch=(x86_64)
url="https://github.com/openpmix/openpmix"
license=(BSD)
depends=(curl glibc hwloc jansson libevent zlib)
makedepends=(perl python)
provides=(libpmix.so pmix)
backup=(etc/$pkgname/pmix-mca-params.conf)
source=($pkgname-$pkgver.tar.gz::https://github.com/$pkgname/$pkgname/archive/refs/tags/v$pkgver.tar.gz)
sha512sums=('7b07b7be82899cf27efbbeaca4a2fa176ca194e0b8dd1c57557892ba437e3c00a3ce5dcfd57ba4d9d637858d8345eea2553eaf4e5bfc7f0999f97c15334f6ef8')
b2sums=('376346bd05eac5c2bfc705bc5de2217da8241bd2cc607208b242acebf26efbe15758ec9999d62a2388e3c48737d6035d33774e52e80a105ad03294597b24f105')

prepare() {
  cd $pkgname-$pkgver
  ./autogen.pl
}

build() {
  cd $pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc/$pkgname \
    --with-curl \
    --with-jansson
  # prevent excessive overlinking due to libtool
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make V=1
}

check() {
  make -k check -C $pkgname-$pkgver
}

package() {
  depends+=(libcurl.so)

  make DESTDIR="$pkgdir" install -C $pkgname-$pkgver
  install -vDm 644 $pkgname-$pkgver/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
