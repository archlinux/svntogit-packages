# $Id$
# Maintainer: dorphell <dorphell@archlinux.org>
# Contributor: Jose Javier  <jojapa@terra.es>
pkgname=tftp-hpa
pkgver=5.2
pkgrel=1
pkgdesc="Official tftp server"
arch=('i686' 'x86_64')
url="http://www.kernel.org/pub/software/network/tftp/"
license=('BSD')
depends=('readline>=6.0.00')
conflicts=('netkit-tftp')
backup=('etc/conf.d/tftpd')
source=(http://www.kernel.org/pub/software/network/tftp/tftp-hpa/$pkgname-$pkgver.tar.gz tftpd.rc tftpd.conf LICENSE)

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --mandir=/usr/share/man --without-tcpwrappers 
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}" 
  make INSTALLROOT="${pkgdir}" install
  install -d "${pkgdir}/etc/rc.d"
  install -d "${pkgdir}/var/tftpboot"
  install -m755 "${srcdir}/tftpd.rc" "${pkgdir}/etc/rc.d/tftpd"
  install -D -m644 "${srcdir}/tftpd.conf" "${pkgdir}/etc/conf.d/tftpd"
  rm "${pkgdir}/usr/share/man/man8/tftpd.8"
  install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
md5sums=('3de3038e7c2bf6fc5d496825893ac8e7'
         '83fbb6f52205d95951a3c059e5351ca2'
         '414a79dc891bced056d99a9bb790fc9e'
         '6ce21e27b6fdc1a1adf85c81e42aeecf')
