# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>

pkgbase=koffice
pkgname=('koffice-interfaces'
	'koffice-kounavail'
	'koffice-kspread'
	'koffice-kspread-doc'
	'koffice-plugins'
	'koffice-karbon'
	'koffice-karbon-doc'
	'koffice-kplato'
	'koffice-kplato-doc'
	'koffice-kword'
#	'koffice-kword-doc' # see http://userbase.kde.org/KWord/Manual for documentation (not supplied by upstream anymore in the sources)
	'koffice-servicetypes'
	'koffice-kchart'
	'koffice-kchart-doc'
	'koffice-kpresenter'
	'koffice-kpresenter-doc'
	'koffice-libs'
	'koffice-templates'
	'koffice-filters'
#	'koffice-kdgantt' # results in empty package / has no function
	'koffice-krita'
#	'koffice-krita-doc' # no krita-doc anymore upstream
	'koffice-pics'
	'koffice-tools'
	'koffice-thesaurus-doc')
pkgver=2.1.1
pkgrel=1
arch=('i686' 'x86_64')
url='http://koffice.kde.org'
license=('GPL' 'LGPL' 'FDL')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'boost' 'eigen' \
  'kdelibs' 'gsl' 'qca' 'lcms' 'glew' 'qimageblitz' \
  'kdepimlibs' 'pstoedit' 'wv2' 'poppler-qt' 'libwpd' 'openexr' 'opengtl')
groups=('koffice')
source=("http://download.kde.org/stable/koffice-${pkgver}/koffice-${pkgver}.tar.bz2"
	'libpng14.patch')
md5sums=('9955f30f43ed3f6970870f55015a1a18'
         '8f139e0be95f3566cb860bd384c5aa8a')

build() {
	cd ${srcdir}/koffice-${pkgver}

	# buildtime patch
  	patch -Np0 -i ${srcdir}/libpng14.patch || return 1
	        
	mkdir ../build
	cd ../build
	cmake ../koffice-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed' \
		-DCMAKE_INSTALL_PREFIX=/usr
	make || return 1
}

package_koffice-interfaces(){
	pkgdesc="Interfaces for the KDE office suite"
	depends=('kdelibs' 'koffice-libs')
	cd ${srcdir}/build/interfaces
	make DESTDIR=${pkgdir} install
}

package_koffice-kounavail(){
	pkgdesc="Kounavail for the KDE office suite"
	depends=('kdelibs' 'koffice-libs')
        cd ${srcdir}/build/kounavail
        make DESTDIR=${pkgdir} install
}

package_koffice-kspread(){
	pkgdesc="Spreadsheet application for KDE"
	depends=('hicolor-icon-theme' 'gsl' 'koffice-libs' 'koffice-templates' 'koffice-kchart')
	optdepends=('koffice-filters: import/export filters')
	install=koffice.install
        cd ${srcdir}/build/kspread
        make DESTDIR=${pkgdir} install
}

package_koffice-kspread-doc(){
	pkgdesc="Documentation for kspread"
	cd ${srcdir}/build/doc/kspread
        make DESTDIR=${pkgdir} install
}

package_koffice-plugins(){
	pkgdesc="Plugins for the KDE office suite"
	depends=('koffice-libs')
        cd ${srcdir}/build/plugins
        make DESTDIR=${pkgdir} install
}

package_koffice-karbon(){
	pkgdesc="Vector drawing application for KDE"
	depends=('hicolor-icon-theme' 'koffice-libs' 'koffice-templates')
        optdepends=('koffice-filters: import/export filters')
	install=koffice.install
        cd ${srcdir}/build/karbon
        make DESTDIR=${pkgdir} install
}

package_koffice-karbon-doc(){
	pkgdesc="Documentation for karbon"
        cd ${srcdir}/build/doc/karbon
        make DESTDIR=${pkgdir} install
}

package_koffice-kplato(){
	pkgdesc="Project management application for KDE"
	depends=('hicolor-icon-theme' 'koffice-libs' 'kdepimlibs' 'koffice-templates' 'koffice-plugins')
	install=koffice.install
        cd ${srcdir}/build/kplato
        make DESTDIR=${pkgdir} install
}

package_koffice-kplato-doc(){
	pkgdesc="Documentation for kplato"
        cd ${srcdir}/build/doc/kplato
        make DESTDIR=${pkgdir} install
}

package_koffice-kword(){
	pkgdesc="Wordprocessor for KDE"
	depends=('hicolor-icon-theme' 'koffice-libs' 'koffice-templates' 'koffice-pics' \
		'koffice-kounavail' 'koffice-plugins')
        optdepends=('koffice-filters: import/export filters')
	install=koffice.install
        cd ${srcdir}/build/kword
        make DESTDIR=${pkgdir} install
}

#package_koffice-kword-doc(){
#	pkgdesc="Documentation for kword"
#        cd ${srcdir}/build/doc/kword
#        make DESTDIR=${pkgdir} install
#}

package_koffice-servicetypes(){
	pkgdesc="Servicetypes for the KDE office suite"
        cd ${srcdir}/build/servicetypes
        make DESTDIR=${pkgdir} install
}

package_koffice-kchart(){
	pkgdesc="Charting tool for KDE"
	depends=('hicolor-icon-theme' 'koffice-interfaces' 'koffice-templates')
        optdepends=('koffice-filters: import/export filters')
	install=koffice.install
        cd ${srcdir}/build/kchart
        make DESTDIR=${pkgdir} install
}

package_koffice-kchart-doc(){
	pkgdesc="Documentation for kchart"
        cd ${srcdir}/build/doc/kchart
        make DESTDIR=${pkgdir} install
}

package_koffice-kpresenter(){
	pkgdesc="Presentation tool for KDE"
	depends=('hicolor-icon-theme' 'koffice-libs' 'koffice-templates')
        optdepends=('koffice-filters: import/export filters')
	install=koffice.install
        cd ${srcdir}/build/kpresenter
        make DESTDIR=${pkgdir} install
}

package_koffice-kpresenter-doc(){
	pkgdesc="Documentation for kpresenter"
        cd ${srcdir}/build/doc/kpresenter
        make DESTDIR=${pkgdir} install
}

package_koffice-libs(){
	pkgdesc="Libs for the KDE office suite"
	depends=('kdepimlibs' 'qca' 'lcms' 'pstoedit' 'wv2' 'libwpd' 'koffice-servicetypes')
        cd ${srcdir}/build/libs
        make DESTDIR=${pkgdir} install
}

package_koffice-templates(){
	pkgdesc="Templates for the KDE office suite"
        cd ${srcdir}/build/templates
        make DESTDIR=${pkgdir} install
}

package_koffice-filters(){
	pkgdesc="Filters for de KDE office suite"
        cd ${srcdir}/build/filters
        make DESTDIR=${pkgdir} install
}

#package_koffice-kdgantt(){
#	pkgdesc="Gantt for KDE"
#        cd ${srcdir}/build/kdgantt
#        make DESTDIR=${pkgdir} install
#}

package_koffice-krita(){
	pkgdesc="Graphics editor for KDE"
	depends=('hicolor-icon-theme' 'glew' 'qimageblitz' 'koffice-libs' \
		'koffice-templates' 'poppler-qt' 'shared-mime-info' \
		'openexr' 'opengtl')
        optdepends=('koffice-filters: import/export filters')
	install=krita.install
        cd ${srcdir}/build/krita
        make DESTDIR=${pkgdir} install
}

#package_koffice-krita-doc(){
#	pkgdesc="Documentation for krita"
#        cd ${srcdir}/build/doc/krita
#        make DESTDIR=${pkgdir} install
#}

package_koffice-pics(){
	pkgdesc="Icons for the KDE office suite"
	install=koffice.install
        cd ${srcdir}/build/pics
        make DESTDIR=${pkgdir} install
}

package_koffice-tools(){
	pkgdesc="Tools for the KDE office suite"
	depends=('koffice-libs')
        cd ${srcdir}/build/tools
        make DESTDIR=${pkgdir} install
}

package_koffice-thesaurus-doc(){
	pkgdesc="Documentation for koffice-thesaurus"
	cd ${srcdir}/build/doc/thesaurus
        make DESTDIR=${pkgdir} install
}
	
