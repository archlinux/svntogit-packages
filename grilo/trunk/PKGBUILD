# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=grilo
pkgver=0.3.15
pkgrel=2
pkgdesc="Framework that provides access to various sources of multimedia content"
url="https://wiki.gnome.org/Projects/Grilo"
arch=(x86_64)
license=(LGPL)
depends=(libxml2 libsoup liboauth totem-pl-parser)
makedepends=(gobject-introspection gtk-doc vala git meson gtk3)
optdepends=('grilo-plugins: Plugins for grilo'
            'gtk3: Test UI')
provides=(libgr{ilo,lnet,lpls}-0.3.so)
_commit=14a4a66d92b78d7db6e872cf09db40fbfb6c470d  # tags/grilo-0.3.15^0
source=("git+https://gitlab.gnome.org/GNOME/grilo.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd grilo
  git describe --tags | sed 's/^grilo-//;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd grilo
}

build() {
  arch-meson grilo build -D soup3=false
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
