# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=grilo
pkgver=0.3.0
pkgrel=1
pkgdesc="Framework that provides access to various sources of multimedia content"
url="http://www.gnome.org"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('gtk3' 'libxml2' 'libsoup' 'liboauth' 'totem-plparser')
makedepends=('gobject-introspection' 'gtk-doc' 'vala' 'intltool')
optdepends=('grilo-plugins: Plugins for grilo')
options=('!emptydirs')
source=(https://download.gnome.org/sources/${pkgname}/${pkgver:0:3}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('1fd1a87d606f56adb3086954baec3ea6e25d9ba3fb010f11d1d3ddc9ec66bc60')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static --disable-debug \
    --libdir=/usr/lib
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
