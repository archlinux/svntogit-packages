# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Maintainer: Vesa Kaihlavirta <vesa@archlinux.org>
# Contributor: Roman Cheplyaka <roma@ro-che.info>

pkgname=libedit
_pkgver=20180525-3.1
pkgver=${_pkgver/-/_}
pkgrel=1
pkgdesc='Command line editor library providing generic line editing, history, and tokenization functions'
url='http://thrysoee.dk/editline/'
arch=('x86_64')
license=('BSD')
depends=('ncurses')
source=("http://thrysoee.dk/editline/libedit-${_pkgver}.tar.gz")
sha256sums=('c41bea8fd140fb57ba67a98ec1d8ae0b8ffa82f4aba9c35a87e5a9499e653116')

build() {
	cd "${srcdir}/${pkgname}-${_pkgver}"
	./configure --prefix=/usr --enable-widec
	make
}

package() {
	cd "${srcdir}/${pkgname}-${_pkgver}"
	make prefix="${pkgdir}"/usr install

	rm "${pkgdir}"/usr/share/man/man3/history.3 # conflicts with readline
	cp "${pkgdir}"/usr/share/man/man3/editline.3 "${pkgdir}"/usr/share/man/man3/el.3
	install -Dm644 COPYING "${pkgdir}"/usr/share/licenses/libedit/LICENSE
}
