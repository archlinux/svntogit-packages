# $Id$
# Maintainer: Vesa Kaihlavirta <vesa@archlinux.org>
# Contributor: Roman Cheplyaka <roma@ro-che.info>

pkgname=libedit
pkgver=20110227_3.0
pkgrel=1
pkgdesc='Command line editor library providing generic line editing, history, and tokenization functions'
arch=('i686' 'x86_64')
url='http://www.thrysoee.dk/editline/'
license=('BSD')
depends=('ncurses')
source=("http://www.thrysoee.dk/editline/libedit-${pkgver/_/-}.tar.gz")
sha1sums=('afe84b9e16fc903d5a3eceda90013d7ad03db794')
options=('!libtool')

build() {
	cd "${srcdir}/$pkgname-${pkgver/_/-}"
	./configure --prefix=/usr --enable-widec --enable-static=no
	make
}

package() {
	cd "${srcdir}/$pkgname-${pkgver/_/-}"
	make prefix="$pkgdir"/usr install

	cp "$pkgdir"/usr/share/man/man3/editline.3 "$pkgdir"/usr/share/man/man3/el.3
	install -D -m0644 COPYING "$pkgdir"/usr/share/licenses/libedit/LICENSE
}
