# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Maintainer: Vesa Kaihlavirta <vesa@archlinux.org>
# Contributor: Roman Cheplyaka <roma@ro-che.info>

pkgname=libedit
pkgver=20140620_3.1
pkgrel=1
pkgdesc='Command line editor library providing generic line editing, history, and tokenization functions'
url='http://www.thrysoee.dk/editline/'
arch=('i686' 'x86_64')
license=('BSD')
depends=('ncurses')
source=("http://www.thrysoee.dk/editline/libedit-${pkgver/_/-}.tar.gz")
sha1sums=('9c0fc40ac9336af9af0799bcdfd3537a6ad258ff')

build() {
	cd "${srcdir}/${pkgname}-${pkgver/_/-}"
	./configure --prefix=/usr --enable-widec
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver/_/-}"
	make prefix="${pkgdir}"/usr install

	cp "${pkgdir}"/usr/share/man/man3/editline.3 "${pkgdir}"/usr/share/man/man3/el.3
	install -Dm644 COPYING "${pkgdir}"/usr/share/licenses/libedit/LICENSE
}
