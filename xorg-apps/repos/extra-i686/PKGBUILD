# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-apps
pkgver=7.5
pkgrel=3
pkgdesc="Various X.Org applications"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=(libxcursor libxkbfile 'libpng>=1.4.0' libxft libfontenc libxaw)
makedepends=(pkgconfig xbitmaps)
source=(${url}/releases/individual/app/oclock-1.0.1.tar.bz2
        ${url}/releases/individual/app/luit-1.0.4.tar.bz2
        ${url}/releases/individual/app/xclock-1.0.4.tar.bz2
        ${url}/releases/individual/app/xpr-1.0.3.tar.bz2
        ${url}/releases/individual/app/xwd-1.0.3.tar.bz2
        ${url}/releases/individual/app/xwud-1.0.2.tar.bz2
        ${url}/releases/individual/app/x11perf-1.5.1.tar.bz2
        ${url}/releases/individual/app/xbiff-1.0.2.tar.bz2
        ${url}/releases/individual/app/xclipboard-1.1.0.tar.bz2
        ${url}/releases/individual/app/xconsole-1.0.3.tar.bz2
        ${url}/releases/individual/app/xcursorgen-1.0.3.tar.bz2
        ${url}/releases/individual/app/xeyes-1.1.0.tar.bz2
        ${url}/releases/individual/app/xkill-1.0.2.tar.bz2
        ${url}/releases/individual/app/xload-1.0.2.tar.bz2
        ${url}/releases/individual/app/xlogo-1.0.2.tar.bz2
        ${url}/releases/individual/app/xmag-1.0.3.tar.bz2
        ${url}/releases/individual/app/xmessage-1.0.2.tar.bz2
        ${url}/releases/individual/app/xcalc-1.0.3.tar.bz2
        ${url}/releases/individual/app/xman-1.1.0.tar.bz2
        ${url}/releases/individual/app/xedit-1.1.2.tar.bz2
        LICENSE)
md5sums=('91f49547f9ed3cd0137c8b7c3183e360'
         '4e45233e310d72dce307709761cf241b'
         'bb9fd5e00d39c348a0078b97fdf8258f'
         '1c2c540d240def3ea65ff2030f059f8a'
         '007cea1f389abde5c93162dcd5541351'
         '20bc5d85634964d4ceee77cdfcd131ed'
         '66e4aa4645f83809071eb69553ed0222'
         '1d4ad06725f9dc4b877ecd210b7b1607'
         '113eccea3ee73a1ef7b43d2291ca1e09'
         '0e1a3110bebabecc2897d67a973526b0'
         '69df079b3950a0db4e5f4e6f0e00ddee'
         '933f6d2b132d14f707f1f3c87b39ebe2'
         'dca031355b4afcf8517a8258eeb730c3'
         'b41ed6b4bcfc9897366c27a94d2bf150'
         '7365c0852750134119734bc1879f60ca'
         '32f7ed4c089365cadb9382f6fbd750a9'
         'b4b561ef11fd184989a6062962e86748'
         '0b981e360b54584fcb6dc2ec4658c406'
         '5e5b3351bac26cc1f8490faf1c1402bb'
         '67193be728414d45a1922911e6437991'
         '1a3977bcff6dc0fc1086e0650fcea31a')

build() {
  cd "${srcdir}"
  for i in *; do
    if [ -d "${i}" ]; then
      pushd "${i}"
      case "${i}" in 
        x11perf*)
          sed -e 's|^LIBPATH = $(libdir)|LIBPATH = $(datadir)|' \
	          -i Makefile.* || return 1
	      ;;
	      xedit*)
	        sed -e 's|^XEDITDIR = ${libdir}|XEDITDIR = ${datadir}|' \
	          -i Makefile.* || return 1
	      ;;
        xcursorgen*)
          sed -e 's/libpng12/libpng14/g' -i configure
        ;;
      esac
      ./configure --prefix=/usr --disable-xprint --mandir=/usr/share/man \
        --with-localealiasfile=/usr/share/X11/locale/locale.alias
      make || return 1
      make DESTDIR="${pkgdir}" install || return 1
      popd
    fi
  done
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
