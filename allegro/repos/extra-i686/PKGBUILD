# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: arjan <arjan@archlinux.org>

pkgname=allegro
pkgver=5.0.5
pkgrel=1
pkgdesc="Portable library mainly aimed at video game and multimedia programming"
arch=('i686' 'x86_64')
url="http://alleg.sourceforge.net/"
license=('custom')
depends=('jack' 'libxpm' 'libxxf86vm' 'libxxf86dga' 'libxcursor' 'libpng' 'libgl' 'libjpeg')
makedepends=('cmake' 'mesa' 'freetype2')
source=(http://downloads.sourceforge.net/alleg/${pkgname}-${pkgver}.tar.gz)
md5sums=('4d879afe7062f7dc9b8913cba2e44bdf')

build() {
  cd "${srcdir}"
  mkdir build && cd build

  cmake "../${pkgname}-${pkgver}" \
  	-DCMAKE_BUILD_TYPE=Release \
  	-DCMAKE_INSTALL_PREFIX=/usr \
	-DWANT_DOCS=OFF

  make
}

package() {
  cd "${srcdir}"/build

  make DESTDIR="${pkgdir}" install

  install -D -m644 ../${pkgname}-${pkgver}/LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
