# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=libgnome-games-support
pkgver=1.2.3
pkgrel=1
pkgdesc="Code shared between GNOME games"
url="https://git.gnome.org/browse/libgnome-games-support"
arch=(x86_64)
license=(GPL3 LGPL3)
depends=(libgee gtk3)
makedepends=(vala git autoconf-archive)
_commit=6de7b69a06903aefe6e7b011c4bed22f8f2790bc  # tags/1.2.3^0
source=("git+https://git.gnome.org/browse/libgnome-games-support#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-really//;s/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
