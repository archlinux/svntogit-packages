# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=qtav
pkgver=1.11.0
pkgrel=4
pkgdesc="A cross-platform multimedia playback framework based on Qt and FFmpeg"
arch=('i686' 'x86_64')
url="http://www.qtav.org/"
license=('LGPL')
depends=('qt5-declarative' 'ffmpeg' 'libxv' 'openal' 'uchardet' 'desktop-file-utils' 'qt5-quickcontrols')
makedepends=('git')
source=("git+https://github.com/wang-bin/QtAV.git#tag=v$pkgver")
sha256sums=('SKIP')

prepare() {
  cd QtAV
  git submodule update --init

  # fix mkspecs install dir
  sed -e 's|\$\$\[QT_INSTALL_BINS\]\/\.\.\/mkspecs|\$\$\[QT_INSTALL_ARCHDATA\]\/mkspecs|g' -i tools/install_sdk/install_sdk.pro
}

build() {
  cd QtAV

  qmake-qt5 "CONFIG+=no_rpath recheck"
  make
}

package() {
  cd QtAV

  make INSTALL_ROOT="${pkgdir}" install
}
