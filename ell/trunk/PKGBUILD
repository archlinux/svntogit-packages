# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Jameson Pugh <imntreal@gmail.com>

pkgname=ell
pkgver=0.49
pkgrel=1
pkgdesc="Embedded Linux library"
arch=(x86_64)
url="https://01.org/ell"
#url="https://git.kernel.org/pub/scm/libs/ell/ell.git/"
license=('LGPL2.1')
depends=('gcc-libs')
checkdepends=('dbus')
options=('!lto' 'debug')
# https://mirrors.edge.kernel.org/pub/linux/libs/ell/sha256sums.asc
source=(https://mirrors.edge.kernel.org/pub/linux/libs/$pkgname/$pkgname-$pkgver.tar.{xz,sign})
sha256sums=('a7ff8ecbc76b187d942dd22b61cb489711400897c790319ffb7e944791687c3f'
            'SKIP')
validpgpkeys=('E932D120BC2AEC444E558F0106CA9F5D1DCF2659') # "Marcel Holtmann <marcel@holtmann.org>"

build() {
  cd "$pkgname-$pkgver"
#  export CFLAGS+=" -ffat-lto-objects"; export CXXFLAGS+=" -ffat-lto-objects"
 ./configure --prefix=/usr
  make
}

check() {
  cd "$pkgname-$pkgver"

# lto error also with export CFLAGS+=" -ffat-lto-objects"
#/usr/bin/ld: /tmp/ccJicHpn.ltrans0.ltrans.o: in function `__wrap_l_getrandom':
#<artificial>:(.text+0x3a71): undefined reference to `l_getrandom'
#collect2: error: ld returned 1 exit status
#make[3]: *** [Makefile:1629: unit/test-ecdh] Error 1

  make -k check
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
