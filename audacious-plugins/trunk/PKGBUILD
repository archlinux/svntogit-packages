# $Id$
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: William Rea <sillywilly@gmail.com>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=audacious-plugins
pkgver=3.0.1
pkgrel=1
pkgdesc='Plugins for Audacious'
arch=('i686' 'x86_64')
url='http://audacious-media-player.org/'
license=('GPL')

makedepends=("audacious>=${pkgver}"
             'oss' 'alsa-lib' 'pulseaudio' 'jack2' 'lame' 'libvorbis' 'flac'
             'mpg123' 'faad2' 'ffmpeg' 'fluidsynth' 'libcdio' 'libsidplay' 'wavpack'
             'libnotify' 'lirc-utils' 'curl' 'libmtp'
             'neon' 'libmms' 'libcue')

optdepends=('oss: Open Sound System v4 output'
            'alsa-lib: Advanced Linux Sound Arch. output'
            'pulseaudio: PulseAudio output'
            'jack2: Jack Audio Connection Kit output'
            'lame: FileWriter MP3 output'
            'libvorbis: Vorbis input, FileWriter Vorbis output'
            'flac: FLAC input, FileWriter FLAC output'

            'mpg123: MP3 input'
            'faad2: AAC input'
            'ffmpeg: ffaudio input'
            'fluidsynth: MIDI FluidSynth backend input'
            'libcdio: CD Digital Audio input'
            'libsidplay: Commodore 64 audio input'
            'wavpack: WavPack input'

            'libnotify: libnotify OSD'
            'lirc-utils: LIRC'
            'curl: AudioScrobbler Client'
            'libmtp: Upload to MTP device'

            'neon: neon-based http transport'
            'libmms: libmms-based mms transport'
            'libcue: CUE playlist format')

source=("http://distfiles.atheme.org/${pkgname}-${pkgver}.tar.gz")
sha1sums=('59f8f1f6a197eac8ccab7b84fb541b4fefac9458')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	sed -e 's/gdk-x11-2.0/gdk-x11-3.0/g' -i configure # hack to enable global hotkeys with gtk3

	[[ ${CARCH} == x86_64 ]] && extraopts='' || extraopts='--disable-sse2'
	./configure \
		--prefix=/usr \
		--enable-chardet \
		--enable-amidiplug \
		--disable-adplug \
		${extraopts} \

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
