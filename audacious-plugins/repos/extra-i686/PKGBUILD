# $Id$
# Maintainer: Alexander Fehr <pizzapunk gmail com>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=audacious-plugins
pkgver=1.5.1
pkgrel=3
pkgdesc="Plugins for Audacious"
arch=('i686' 'x86_64')
url="http://audacious-media-player.org/"
license=('GPL')
depends=('audacious' 'libmad' 'libvorbis')
makedepends=('jack-audio-connection-kit' 'lame' 'fluidsynth' 'libcdio>=0.81-2' 'libsidplay' 'wavpack'
             'libmpcdec' 'taglib' 'lirc-utils' 'curl' 'libmtp>=0.3.7-1' 'sdl' 'projectm' 'imlib2'
             'neon' 'libmms' 'gtkglext')
optdepends=('jack-audio-connection-kit: JACK output plugin'
            'lame: FileWriter output plugin'
            'fluidsynth: FluidSynth backend for AMIDI-Plug input plugin'
            'libcdio: CD Audio input plugin'
            'libsidplay: SID input plugin'
            'wavpack: WavPack input plugin'
            'libmpcdec: Musepack input plugin'
            'taglib: Musepack input plugin'
            'lirc-utils: LIRC plugin'
            'curl: Audioscrobbler plugin'
            'libmtp: MTP upload plugin'
            'sdl: Paranormal and projectM visualization plugins'
            'projectm: projectM visualization plugin'
            'imlib2: RootVis visualization plugin'
            'neon: HTTP transport plugin'
            'libmms: MMS transport plugin')
source=(http://distfiles.atheme.org/audacious-plugins-$pkgver.tbz2
        audacious-plugins-amidi.patch
	audacious-plugins-1.5.1-libmtp.patch)
md5sums=('b237754a615cf9a3d77a93bf18de26fc'
         'd8887eb15957b106bcb84acc0e243ac9'
         'b5344559449b9bc49331213a6052aef8')


build() {
  cd "$srcdir/audacious-plugins-$pkgver"

  patch -Np1 -i "$srcdir/audacious-plugins-amidi.patch" || return 1
  patch -Np1 -i "$srcdir/audacious-plugins-1.5.1-libmtp.patch" || return 1

  if [[ $CARCH = "i686" ]]; then
    ./configure --prefix=/usr --disable-esd --disable-arts --disable-pulse \
      --enable-amidiplug --disable-timidity --disable-adplug \
      --enable-chardet --disable-sse2 || return 1
  elif [[ $CARCH = "x86_64" ]]; then
    ./configure --prefix=/usr --disable-esd --disable-arts --disable-pulse \
      --enable-amidiplug --disable-timidity --disable-adplug \
      --enable-chardet || return 1
  fi

  make || return 1
  make DESTDIR="$pkgdir" install || return 1
}
