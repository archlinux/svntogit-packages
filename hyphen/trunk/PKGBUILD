# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>

pkgbase=hyphen
pkgname=('hyphen' 'hyphen-en')
pkgver=2.5
pkgrel=1
arch=('i686' 'x86_64')
url="http://hunspell.sf.net"
license=('GPL' 'LGPL' 'MPL')
groups=()
makedepends=('glibc' 'perl')
options=('!libtool')
source=(http://downloads.sourceforge.net/hunspell/${pkgname}-${pkgver}.tar.gz)
md5sums=('57857a1c856cf7d24577eb6431464ffa')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --disable-static
  make || return 1
}

package_hyphen() {
 pkgdesc="library for high quality hyphenation and justification"
 depends=('glibc' 'perl')

  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install-libLTLIBRARIES || return 1
  make DESTDIR="$pkgdir/" install-binSCRIPTS || return 1
  make DESTDIR="$pkgdir/" install-includeHEADERS || return 1
  
  # license
  install -D -m644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

package_hyphen-en() {
 pkgdesc="English hyphenation rules"
 # arch=('any')
 depends=('hyphen')

  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install-hyphDATA || return 1

  pushd ${pkgdir}/usr/share/hyphen/
   en_US_aliases="en_AG en_AU en_BS en_BW en_BZ en_CA en_DK en_GB en_GH en_HK en_IE en_IN en_JM en_NA en_NZ en_PH en_SG en_TT en_ZA en_ZW"
   for lang in $en_US_aliases; do
        ln -s hyph_en_US.dic hyph_$lang.dic
   done
  popd
}