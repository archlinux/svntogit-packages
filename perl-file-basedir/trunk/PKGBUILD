# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: kevin <kevin@archlinux.org>
# Contributor: K. Piche <kpiche@rogers.com>

pkgname=perl-file-basedir
_realname=File-BaseDir
pkgver=0.08
pkgrel=2
pkgdesc='Use the Freedesktop.org base directory specification'
arch=('any')
license=('GPL' 'PerlArtistic')
url="http://search.cpan.org/dist/${_realname}/"
depends=('perl-ipc-system-simple')
makedepends=('perl-module-build>0.4205')
checkdepends=('perl-test-pod' 'perl-test-pod-coverage' 'perl-file-which' 'xdg-user-dirs')
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/K/KI/KIMRYAN/${_realname}-$pkgver.tar.gz")
sha512sums=('6b44edca34906c34d574933c21c0e3a1a236a431a3ac12caeabcc2638688dfe1c02efc9a1e75a2c2cd522de0dca00df0620cd262c184ec938cc97766d1121f99')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  mkdir .config
  cd "${srcdir}/${_realname}-${pkgver}"
  HOME="$srcdir" make test
}

package() {
  cd ${srcdir}/${_realname}-${pkgver}
  make install DESTDIR=${pkgdir}
}
