# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=pyside6
_qtver=6.0.1
pkgver=${_qtver/-/}
pkgrel=2
arch=(x86_64)
url='https://www.qt.io'
license=(LGPL)
pkgdesc='Enables the use of Qt6 APIs in Python applications'
depends=(shiboken6 qt6-declarative)
makedepends=(cmake python-setuptools qt6-tools qt6-quickcontrols2 qt6-svg qt6-3d)
optdepends=('qt6-tools: QtHelp, QtUiTools bindings' 'qt6-quickcontrols2: QtQuickControls2 bindings' 'qt6-svg: QtSvg bindings' 'qt6-3d: Qt3D bindings')
_pkgfn=pyside-setup-opensource-src-$_qtver
source=("https://download.qt.io/official_releases/QtForPython/pyside6/PySide6-$pkgver-src/${_pkgfn}.tar.xz")
sha256sums=('baac59a71d5d8d28badd4b484b3722500a6616684f932f0652b33a5b5feaf365')

build() {
  cmake -B build -S $_pkgfn/sources/pyside6 \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTS=OFF \
    -DPYTHON_EXECUTABLE=/usr/bin/python
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

# Install egg-info
  export PATH="/usr/lib/qt6/bin:$PATH"
  cd $_pkgfn
  python setup.py egg_info --build-type=pyside6
  _pythonpath=`python -c "from sysconfig import get_path; print(get_path('platlib'))"`
  cp -r PySide6.egg-info "$pkgdir"/$_pythonpath
}
