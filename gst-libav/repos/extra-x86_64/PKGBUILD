# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gst-libav
pkgver=1.18.4
pkgrel=1
pkgdesc="Multimedia graph framework - libav plugin"
url="https://gstreamer.freedesktop.org/"
arch=(x86_64)
license=(GPL)
depends=(gst-plugins-base-libs bzip2 ffmpeg)
makedepends=(python git meson)
provides=("gst-ffmpeg=$pkgver-$pkgrel")
_commit=e896aabe3c3d278510fb567712c4a55ed0eae075  # tags/1.18.4^0
source=("git+https://gitlab.freedesktop.org/gstreamer/gst-libav.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build \
    -D doc=disabled \
    -D package-name="GStreamer FFmpeg Plugin (Arch Linux)" \
    -D package-origin="https://www.archlinux.org/"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
