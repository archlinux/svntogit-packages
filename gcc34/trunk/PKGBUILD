# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
pkgname=gcc34
pkgver=3.4.6
pkgrel=2
pkgdesc="The GNU Compiler Collection"
arch=(i686 x86_64)
url="http://gcc.gnu.org"
license=('GPL') # with exception, per fsf.org
depends=('libstdc++5' 'gcc-libs')
options=('!libtool')
source=(ftp://gcc.gnu.org/pub/gcc/releases/gcc-${pkgver}/gcc-{core,g++}-${pkgver}.tar.bz2 \
	gcc-localeversion.patch
	gcc-3.3-pure64.patch)
md5sums=('5324ace5145b12afd9ca867af7ec084d'
         'ef81fd74bc9fd964120af47243a1360f'
         'e93d6f49b254dc2879a4e181603599b0'
         'eb834abd7620a5f11492ee2c243b8346')

build() {
  if ! locale -a | grep ^de_DE; then
    echo "You need the de_DE locale to build gcc."
    return 1
  fi

  export CFLAGS="${CFLAGS/-mtune=generic/}"
  export CXXFLAGS="${CXXFLAGS/-mtune=generic/}"

  cd ${startdir}/src/gcc-${pkgver}

  patch -Np0 -i ${startdir}/src/gcc-localeversion.patch || return 1

  if [ "${CARCH}" = "x86_64" ]; then
    patch -Np1 -i ../gcc-3.3-pure64.patch || return 1
  fi

  # Don't run fixincludes
  sed -i -e 's@\./fixinc\.sh@-c true@' gcc/Makefile.in
  # Don't install libiberty
  sed -i 's/install_to_$(INSTALL_DEST) //' libiberty/Makefile.in

  mkdir ../gcc-build
  cd ../gcc-build
  ../gcc-${pkgver}/configure --prefix=/usr --enable-shared \
      --enable-languages=c,c++ --enable-threads=posix \
      --mandir=/usr/share/man --libexecdir=/usr/lib \
      --enable-__cxa_atexit  --disable-multilib --libdir=/usr/lib \
      --enable-clocale=gnu --program-suffix=-3.4

  make bootstrap || return 1
  make -j1 DESTDIR=${startdir}/pkg install || return 1

  rm -f ${startdir}/pkg/usr/lib/lib{stdc++,supc++,gcc_s}.*
  rm -f ${startdir}/pkg/usr/share/locale/*/LC_MESSAGES/libstdc++.mo
  rm -rf ${startdir}/pkg/usr/share/man/man7
}
