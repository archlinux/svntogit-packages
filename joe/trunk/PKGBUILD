# $Id$
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>
pkgname=joe
pkgver=4.4
pkgrel=1
pkgdesc="Joe's own editor"
arch=('i686' 'x86_64')
url="http://joe-editor.sourceforge.net/"
license=('GPL')
depends=('ncurses')
optdepends=('gpm: console mouse support')
backup=('etc/joe/ftyperc' 'etc/joe/jicerc.ru' 'etc/joe/jmacsrc'
        'etc/joe/joerc' 'etc/joe/jpicorc' 'etc/joe/jstarrc' 'etc/joe/rjoerc')
source=("http://downloads.sourceforge.net/joe-editor/${pkgname}-${pkgver}.tar.gz")
sha256sums=('a5704828bbca29acb9e200414fef522c66cdf9ce28150f402d6767da43460979')

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}

  ./configure --prefix=/usr --sysconfdir=/etc --mandir=/usr/share/man
  make
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}/" install
}
