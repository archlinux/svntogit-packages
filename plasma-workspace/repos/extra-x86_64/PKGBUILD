# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Antonio Rojas
# Contributor: Alexey D. <lq07829icatm at rambler.ru>

pkgbase=plasma-workspace
pkgname=('plasma-workspace' 'drkonqi' 'kuiserver' 'plasma-wayland-session')
pkgver=5.5.2
pkgrel=2
pkgdesc='KDE Plasma Workspace'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/workspace/plasma-workspace'
license=('LGPL')
# note on libxdamage:
# not detected by namcap because libgl depends on it
# but nvidia providing libgl does not depend on libxdamage
depends=('kjsembed' 'knotifyconfig' 'libxdamage' 'libksysguard' 'libkscreen' 'ktexteditor' 'libqalculate'
         'qt5-tools' 'kded' 'kde-cli-tools' 'xorg-xrdb' 'xorg-xsetroot'
         'xorg-xmessage' 'xorg-xprop' 'milou' 'breeze' 'prison-frameworks' 'kwin')
makedepends=('extra-cmake-modules' 'kdoctools' 'gpsd' 'baloo'
             'krunner' 'kxmlrpcclient' 'networkmanager-qt')
groups=('plasma')
source=("http://download.kde.org/stable/plasma/${pkgver}/${pkgbase}-${pkgver}.tar.xz" 'kde.pam'
	xembedsniproxy-crash.patch::"https://bugsfiles.kde.org/attachment.cgi?id=96251")
md5sums=('771be8128b03bc50d79b3579e631ae15'
         '378ee33a9ec2870a1a899f2e05ee00d4'
         '28113c15b8609db604b779a8d732fa91')

prepare() {
  mkdir -p build

  cd ${pkgbase}-${pkgver}
  # be sure to use the Qt5 version of qtpaths
  sed -e 's:qtpaths:qtpaths-qt5:' -i startkde/start*.cmake

  patch -p1 -i ../xembedsniproxy-crash.patch
}

build() {
  cd build
  cmake ../${pkgbase}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib \
    -DLIBEXEC_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package_plasma-workspace() {
  depends+=('kuiserver')
  optdepends=('plasma-workspace-wallpapers: additional wallpapers'
              'gpsd: GPS based geolocation' 'networkmanager-qt: IP based geolocation')
  conflicts=('kdebase-workspace')

  cd build
  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir"/kde.pam "$pkgdir"/etc/pam.d/kde

  # Remove conflicts with drkonqi
  rm "$pkgdir"/usr/lib/drkonqi
  rm -r "$pkgdir"/usr/share/drkonqi

  # Remove conflicts with kuiserver
  rm "$pkgdir"/usr/bin/kuiserver5
  rm "$pkgdir"/usr/lib/libkdeinit5_kuiserver5.so
  rm "$pkgdir"/usr/share/dbus-1/services/kf5_org.kde.kuiserver.service
  rm "$pkgdir"/usr/share/kservices5/kuiserver.desktop

  # Split plasma-wayland scripts
  rm -r "$pkgdir"/usr/share/wayland-sessions
}

package_drkonqi() {
  pkgdesc='KDE crash handler'
  depends=('kxmlrpcclient' 'gdb')

  cd build/drkonqi
  make DESTDIR="$pkgdir" install
}

package_kuiserver() {
  pkgdesc='KDE Progress Info UI server'
  depends=('kxmlgui')

  cd build/kuiserver
  make DESTDIR="$pkgdir" install
}

package_plasma-wayland-session() {
  pkgdesc='Plasma Wayland session'
  depends=('plasma-workspace' 'qt5-wayland' 'kwayland-integration' 'xorg-server-xwayland')
  groups=()

  install -Dm644 build/plasmawayland.desktop "$pkgdir"/usr/share/wayland-sessions/plasmawayland.desktop
}
