# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Antonio Rojas
# Contributor: Alexey D. <lq07829icatm at rambler.ru>

pkgbase=plasma-workspace
pkgname=('plasma-workspace' 'drkonqi')
pkgver=5.3.2
pkgrel=3
pkgdesc='KDE Plasma Workspace'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/workspace/plasma-workspace'
license=('LGPL')
# note on libxdamage:
# not detected by namcap because libgl depends on it
# but nvidia providing libgl does not depend on libxdamage
depends=('knewstuff' 'kjsembed' 'knotifyconfig' 'libxdamage' 'kwayland'
         'libksysguard' 'libkscreen' 'ktexteditor' 'libqalculate'
         'qt5-tools' 'kded' 'kde-cli-tools' 'xorg-xrdb' 'xorg-xsetroot'
         'xorg-xmessage' 'xorg-xprop' 'milou' 'breeze' 'prison-frameworks')
makedepends=('extra-cmake-modules' 'kdoctools' 'kwin' 'gpsd' 'baloo'
             'krunner' 'kxmlrpcclient' 'networkmanager-qt')
groups=('plasma')
source=("http://download.kde.org/stable/plasma/${pkgver}/${pkgbase}-${pkgver}.tar.xz"
        'kde.pam'
        'remove-baloo-result-header.patch'
        'baloo-use-filepath-as-id.patch')
md5sums=('3d6a6e1ffcbf2d3e27668dc471523907'
         '929b182dec8a096206ad493477c09d2c'
         'fd0937f725cb04379a17d27857a0aa02'
         '418a2de6f3596d20da9fd69715e73a70')

prepare() {
  mkdir build

  cd ${pkgbase}-${pkgver}
  # be sure to use the Qt5 version of qtpaths
  sed -i 's:qtpaths:qtpaths-qt5:' startkde/startkde.cmake

  # Patch for Baloo 5.13
  patch -p1 -i ../remove-baloo-result-header.patch
  patch -p1 -i ../baloo-use-filepath-as-id.patch
}

build() {
  cd build
  cmake ../${pkgbase}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib \
    -DLIBEXEC_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package_plasma-workspace() {
  optdepends=('plasma-workspace-wallpapers: additional wallpapers'
              'gpsd: GPS based geolocation' 'networkmanager-qt: IP based geolocation')
  conflicts=('kdebase-workspace')

  cd build
  make DESTDIR="${pkgdir}" install

  install -D "${srcdir}"/kde.pam \
    "${pkgdir}"/etc/pam.d/kde

  # Remove conflicts with drkonqi
  rm "${pkgdir}"/usr/lib/drkonqi
  rm -r "${pkgdir}"/usr/share/drkonqi
}

package_drkonqi() {
  pkgdesc='KDE crash handler'
  depends=('kdewebkit' 'kxmlrpcclient' 'gdb')

  cd build/drkonqi
  make DESTDIR="${pkgdir}" install
}
