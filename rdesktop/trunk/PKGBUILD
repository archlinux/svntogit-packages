# $Id$
# Maintainer:
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Dan McGee <dan@archlinux.org>

pkgname=rdesktop
pkgver=1.8.1
pkgrel=1
pkgdesc="An open source client for Windows Remote Desktop Services"
arch=('i686' 'x86_64')
url="http://www.rdesktop.org/"
license=('GPL3')
depends=('libao' 'libsamplerate' 'xorg-xrandr' 'pcsclite')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        rdesktop-send_physical_buttons.diff)
sha1sums=('57bb41f98ddf9eeef875c613d790fee37971d0f8'
          '809a9dc11ad4f704f79fc4bc2a70b76a4f42596d')

prepare() {
  cd ${pkgname}-${pkgver}
  # FS#15113
  patch -i "${srcdir}/rdesktop-send_physical_buttons.diff"
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr \
    --enable-smartcard \
    --with-ipv6
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
