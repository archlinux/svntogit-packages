# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libxfixes
pkgver=5.0.2
pkgrel=1
pkgdesc="X11 miscellaneous 'fixes' extension library"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libx11' 'fixesproto>=5.0')
makedepends=('xorg-util-macros')
source=("${url}/releases/individual/lib/libXfixes-${pkgver}.tar.bz2")
sha256sums=('9bd20edfec084a1bed481d48dd4815dee88139fffad091418cdda081129a9aea')

build() {
  cd "${srcdir}/libXfixes-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static
  make
}

package() {
  cd "${srcdir}/libXfixes-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
