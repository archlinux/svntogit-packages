# $Id$
# Maintainer:
# Contributor: Alessio 'mOLOk' Bolognino <themolok@gmail.com>

pkgname=smplayer-themes
pkgver=16.6.0
pkgrel=1
epoch=1
pkgdesc="Themes for SMPlayer"
arch=('any')
url="http://smplayer.sourceforge.net/"
license=('GPL')
depends=('smplayer')
makedepends=('optipng')
source=(http://downloads.sourceforge.net/smplayer/$pkgname-$pkgver.tar.bz2)
md5sums=('7c2e475290e3edda69e52ec8682743dd')

prepare() {
  cd $pkgname-$pkgver

  # Fix invalid PNG icons to work with libpng 1.6
  find -name '*.png' -exec optipng -quiet -force -fix {} +
}

build() {
  cd $pkgname-$pkgver
  make
}

package() {
  cd $pkgname-$pkgver
  make PREFIX="$pkgdir/usr" install
}
