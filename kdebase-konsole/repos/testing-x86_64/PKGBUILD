# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdebase-konsole
pkgver=4.14.1
pkgrel=1
arch=('i686' 'x86_64')
url='http://kde.org/applications/system/konsole/'
pkgdesc="Terminal"
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdebase')
depends=('kdebase-runtime' 'kdebase-lib')
makedepends=('cmake' 'automoc4')
source=("http://download.kde.org/stable/${pkgver}/src/konsole-${pkgver}.tar.xz")
sha1sums=('a8600271694b6d2450401664fc074ad5c360e9fe')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../konsole-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
