# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdebase-konsole
pkgver=4.9.0
pkgrel=1
arch=('i686' 'x86_64')
url='http://kde.org/applications/system/konsole/'
pkgdesc="Terminal"
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdebase')
depends=('kdebase-runtime' 'kdebase-lib')
makedepends=('cmake' 'automoc4')
source=("http://download.kde.org/stable/${pkgver}/src/konsole-${pkgver}.tar.xz")
sha1sums=('4ee3d7da93512788959f4d52e58d7a98c646e854')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../konsole-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
