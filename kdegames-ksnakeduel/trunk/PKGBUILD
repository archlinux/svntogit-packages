# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-ksnakeduel
pkgver=14.12.3
pkgrel=1
pkgdesc="A simple snake duel game"
url="http://kde.org/applications/games/ksnakeduel/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'libkdegames')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/applications/${pkgver}/src/ksnakeduel-${pkgver}.tar.xz")
sha1sums=('a86f00dbb12e4dbe82773c767b4eecf36790af1f')

build() {
  mkdir build
  cd build
  cmake ../ksnakeduel-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
