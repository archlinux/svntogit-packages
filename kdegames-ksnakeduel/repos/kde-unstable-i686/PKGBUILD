# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-ksnakeduel
pkgver=4.11.80
pkgrel=1
pkgdesc="A simple snake duel game"
url="http://kde.org/applications/games/ksnakeduel/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'libkdegames')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/unstable/${pkgver}/src/ksnakeduel-${pkgver}.tar.xz")
sha1sums=('326cc040382c828eaaf1d0af04a035d992a3a618')

build() {
  mkdir build
  cd build
  cmake ../ksnakeduel-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
