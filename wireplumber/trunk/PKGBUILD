# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgbase=wireplumber
pkgname=(wireplumber wireplumber-docs)
_commit=80b3559963f0ad40a7bfa6c23b0098275c0b5ebe  # tags/0.4.11
pkgver=0.4.11
pkgrel=2
pkgdesc="Session / policy manager implementation for PipeWire"
url="https://pipewire.pages.freedesktop.org/wireplumber/"
arch=(x86_64)
license=(MIT)
makedepends=(git meson doxygen graphviz 'pipewire>=0.3.52' systemd glib2 lua
             gobject-introspection python-sphinx python-sphinx_rtd_theme
             python-breathe python-lxml)
options=(debug)
source=("$pkgbase::git+https://gitlab.freedesktop.org/pipewire/wireplumber.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgbase
  git describe --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd $pkgbase

  # Fix sound inside VM
  git cherry-pick -n c16e637c329bc9dda8544b18f5bd47a8d63ee253
}

build() {
  local meson_options=(
    -D system-lua=true
    -D elogind=disabled
  )

  arch-meson $pkgbase build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

package_wireplumber() {
  depends=('pipewire>=0.3.52' lua libpipewire-0.3.so libsystemd.so
           libg{lib,module,object,io}-2.0.so)
  optdepends=('wireplumber-docs: Documentation')
  provides=(pipewire-session-manager libwireplumber-0.4.so)
  conflicts=(pipewire-media-session)
  install=wireplumber.install

  meson install -C build --destdir "$pkgdir"

  _pick docs "$pkgdir"/usr/share/doc

  install -Dt "$pkgdir/usr/share/doc/$pkgname" -m644 $pkgbase/{NEWS,README}*
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 $pkgbase/LICENSE
}

package_wireplumber-docs() {
  pkgdesc+=" - documentation"

  mv docs/* "$pkgdir"

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 $pkgbase/LICENSE
}

# vim:set sw=2 et:
