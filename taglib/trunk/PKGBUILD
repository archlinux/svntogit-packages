# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=taglib
pkgver=1.6.3
pkgrel=1
pkgdesc="A Library for reading and editing the meta-data of several popular audio formats"
arch=('i686' 'x86_64')
url="http://developer.kde.org/~wheeler/taglib.html"
license=('GPL2')
depends=('sh' 'zlib' 'gcc-libs')
makedepends=('cmake' 'pkgconfig')
source=(http://developer.kde.org/~wheeler/files/src/$pkgname-$pkgver.tar.gz)
options=('!libtool')
md5sums=('ddf02f4e1d2dc30f76734df806e613eb')

build() {
  cd ${srcdir}
  mkdir build
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DWITH_MP4=ON \
    -DWITH_ASF=ON
  make VERBOSE=1 || return 1
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install || return 1
}
