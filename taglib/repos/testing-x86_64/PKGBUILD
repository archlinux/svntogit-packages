# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: Ike Devolder <ike.devolder@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=taglib
pkgver=1.11.1
pkgrel=2
pkgdesc="A Library for reading and editing the meta-data of several popular audio formats"
arch=(x86_64)
url="http://taglib.github.io/"
license=(LGPL MPL)
depends=(zlib)
makedepends=(cmake boost)
source=("http://taglib.github.io/releases/$pkgname-$pkgver.tar.gz")
md5sums=('cee7be0ccfc892fa433d6c837df9522a')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_SHARED_LIBS=ON \
    -DWITH_MP4=ON \
    -DWITH_ASF=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
