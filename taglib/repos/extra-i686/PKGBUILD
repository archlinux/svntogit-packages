# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: Ike Devolder <ike.devolder@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=taglib
pkgver=1.10
pkgrel=1
pkgdesc="A Library for reading and editing the meta-data of several popular audio formats"
arch=(i686 x86_64)
url="http://taglib.github.io/"
license=(LGPL MPL)
depends=(zlib)
makedepends=(cmake)
source=("http://taglib.github.io/releases/$pkgname-$pkgver.tar.gz")
md5sums=('5b4441a15423b046dd92a096e09ea22c')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DWITH_MP4=ON \
    -DWITH_ASF=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
