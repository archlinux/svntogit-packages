# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Maxime Arthaud <maxime@arthaud.me>

pkgname=zxing-cpp
pkgver=1.0.6
pkgrel=1
pkgdesc="A command-line tool to decode QRCode"
arch=(x86_64)
url="https://github.com/nu-book/zxing-cpp"
license=(Apache)
depends=(gcc-libs)
makedepends=(cmake)
source=($pkgname-$pkgver.tar.gz::"https://github.com/nu-book/zxing-cpp/archive/v$pkgver.tar.gz")
sha256sums=('c0c380b27ad9d8e90fe91e741810bbf695c58ca4a31e4403fb564182f19df0f6')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_BUILD_TYPE=None \
    -DBUILD_SHARED_LIBRARY=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
