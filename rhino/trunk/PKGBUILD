# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Maintainer: Guillaume ALAUX <guillaume@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgbase=rhino
pkgname=('java-rhino' 'rhino' 'rhino-javadoc')
pkgver=1.7.6
_pkgver=${pkgver//\./_}
pkgrel=1
arch=('any')
url="http://www.mozilla.org/rhino/"
license=('MPL')
makedepends=('apache-ant')
source=(https://github.com/mozilla/${pkgbase}/releases/download/Rhino${_pkgver}_RELEASE/${pkgbase}${pkgver}.zip
        http://archive.apache.org/dist/xmlbeans/binaries/xmlbeans-2.5.0.tgz
        rhino
        rhino.1
        rhino-debugger
        rhino-debugger.1
        rhino-jsc
        rhino-jsc.1)
sha256sums=('45f4431699887a72a7f383ef927ef1b2e79d1fe597260af8df11ea93255b10f5'
            '497b0031e6c998f724f4b462185e85a20c441f7d678b99f42030e9f96be315d3'
            '4c8bea243b86dc7b3f186a255fedd5d7c13b2aa9f24b81db4f7dd2ed7582e724'
            'b3ae7bfc673e999d644d277b317a68289c41c650c49e7614b2e14b4094119388'
            '9ce7073e0c40bf5f600ed948d6f6abcded6ebebc5e78bc2bdd35ce70bd7611f5'
            'afad5dd37f0157138077f5e070b0434491f5e0389bb6bccc5a9cfde275e5c4d8'
            '5115144b0c025b4cac09d9a5f3bd5e7aeaba6ce3b2055f2f3b659ea0c13e4f2a'
            '15bb5da739c5d27634aae479ff5c94f685967de0d59cf7dc53b595d94a0b6860')

build() {
  cd "${srcdir}/${pkgbase}${pkgver}"
  ant \
    -Dxbean.jar=${srcdir}/xmlbeans-2.5.0/lib/xbean.jar \
    -Djsr173.jar=${srcdir}/xmlbeans-2.5.0/lib/jsr173_1.0_api.jar \
    jar \
    javadoc
}

check() {
  cd "${srcdir}/${pkgbase}${pkgver}"
  ant \
    junit
}

package_java-rhino() {
  pkgdesc="Open-source implementation of JavaScript written entirely in Java - JAR"
  depends=('java-runtime-headless')

  cd ${srcdir}/${pkgbase}${pkgver}/build/${pkgbase}${pkgver}
  install -Dm644 js.jar ${pkgdir}/usr/share/java/js-${pkgver}.jar
  ln -s js-${pkgver}.jar ${pkgdir}/usr/share/java/js.jar
}

package_rhino() {
  pkgdesc="Open-source implementation of JavaScript written entirely in Java"
  depends=('java-rhino' 'java-jline')

  cd ${srcdir}

  install -dm755 ${pkgdir}/usr/bin
  install -m755 rhino rhino-debugger rhino-jsc ${pkgdir}/usr/bin/
  install -dm755 ${pkgdir}/usr/share/man/man1
  install -m755 rhino.1 rhino-debugger.1 rhino-jsc.1 ${pkgdir}/usr/share/man/man1/
}

package_rhino-javadoc() {
  pkgdesc="Open-source implementation of JavaScript written entirely in Java - Javadoc"

  cd ${srcdir}/${pkgbase}${pkgver}/build/${pkgbase}${pkgver}
  install -m755 -d ${pkgdir}/usr/share/${pkgbase}
  cp -r javadoc ${pkgdir}/usr/share/${pkgbase}
}
