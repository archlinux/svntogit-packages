# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=grilo-plugins
pkgver=0.2.13
pkgrel=1
pkgdesc="Plugins for Grilo"
url="http://www.gnome.org"
arch=(i686 x86_64)
license=(LGPL)
depends=(grilo)
makedepends=(libgdata gmime libtracker-sparql libdmapsharing json-glib intltool avahi itstool
             libmediaart lua gom gnome-online-accounts)
optdepends=('avahi: Freebox plugin'
            'dleyna-server: DLNA plugin'
            'gmime: Podcasts plugin'
            'gnome-online-accounts: Flickr plugin'
            'gom: Bookmarks plugin'
            'json-glib: TMDb plugin'
            'libdmapsharing: DMAP sharing plugin'
            'libgdata: Youtube plugin'
            'libmediaart: local-metadata plugin'
            'lua: LUA factory plugin'
            'tracker: Tracker plugin')
options=('!emptydirs')
groups=('gnome')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver:0:3}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('a5544831ba81a4e7caea30da89874fd20142a362d389366e1113d0e64be51201')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
