# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=grilo-plugins
pkgver=0.3.12
pkgrel=1
epoch=1
pkgdesc="A collection of plugins for the Grilo framework"
url="https://gitlab.gnome.org/GNOME/grilo-plugins"
arch=(x86_64)
license=(LGPL)
depends=(grilo lua libmediaart libgdata tracker gom libdmapsharing chromaprint
         gst-plugins-bad gmime3)
makedepends=(git meson gperf yelp-tools)
checkdepends=(gst-plugins-good gst-plugins-base)
optdepends=('dleyna-server: DLNA plugin'
            'tracker-miners: Tracker plugin')
groups=(gnome)
_commit=2e8a97e73aed257ef3cbb09340d22478c4cd25e2  # tags/grilo-plugins-0.3.12^0
source=("git+https://gitlab.gnome.org/GNOME/grilo-plugins.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^grilo-plugins-//;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  depends+=(libgr{ilo,lnet,lpls}-0.3.so)
  DESTDIR="$pkgdir" meson install -C build
}
