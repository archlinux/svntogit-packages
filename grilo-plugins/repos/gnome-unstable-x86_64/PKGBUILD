# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=grilo-plugins
pkgver=0.2.12
pkgrel=1
pkgdesc="Plugins for Grilo"
url="http://www.gnome.org"
arch=(i686 x86_64)
license=(LGPL)
depends=(grilo sqlite libgcrypt)
makedepends=(gupnp-av libgdata gmime libtracker-sparql libdmapsharing
             json-glib intltool avahi itstool libmediaart)
optdepends=('gupnp-av: uPnP plugin'
            'libgdata: Youtube plugin'
            'gnome-online-accounts: Flickr plugin'
            'gmime: Podcasts plugin'
            'tracker: Tracker plugin'
            'libdmapsharing: DMAP sharing plugin'
            'json-glib: TMDb plugin'
            'avahi: Freebox plugin'
            'libmediaart: local-metadata plugin')
options=('!emptydirs')
groups=('gnome')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver:0:3}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('15bed8a633c81b251920ab677d455433e641388f605277ca88e549cc89012b48')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static \
    --enable-shoutcast --disable-pocket
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
