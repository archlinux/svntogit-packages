# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>

pkgname=gtk-xfce-engine
pkgver=2.10.0
pkgrel=1
pkgdesc="Xfce Gtk+-2.0 engine and themes"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('GPL2')
groups=('xfce4')
depends=('gtk2')
provides=("gtk2-xfce-engine=$pkgver-$pkgrel")
conflicts=('gtk2-xfce-engine')
replaces=('gtk2-xfce-engine' 'gtk3-xfce-engine')
source=(http://archive.xfce.org/src/xfce/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=('c1c3b301bd2f2eecf9bb0500d88d96c04ba37bf44d3971f951f4d3d0bbb41967')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
