# $Id$
# Maintainer: Kevin Piche <kevin@archlinux.org>
# Contributor: Andrew Simmons <andrew.simmons@gmail.com>

pkgname=perl-date-manip
_realname=Date-Manip
pkgver=6.37
pkgrel=1
pkgdesc="Date::Manip - date manipulation routines"
arch=('any')
license=('PerlArtistic')
url="http://search.cpan.org/dist/${_realname}/"
depends=('perl-yaml-syck')
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/S/SB/SBECK/${_realname}-${pkgver}.tar.gz)
md5sums=('71601cb8c03f9f6716a3835eeb071219')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  # install module in vendor directories.
  perl Build.PL installdirs=vendor
  perl Build
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  perl Build  destdir="${pkgdir}" install
}
