# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=prison
pkgver=5.101.0
pkgrel=2
pkgdesc='A barcode API to produce QRCode barcodes and DataMatrix barcodes'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(GPL)
depends=(qt5-multimedia libdmtx qrencode zxing-cpp)
makedepends=(extra-cmake-modules doxygen qt5-tools qt5-doc qt5-declarative)
optdepends=('qt5-declarative: QML bindings')
groups=(kf5)
source=(https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz{,.sig}
        https://invent.kde.org/frameworks/prison/-/commit/14fb6f0a.patch)
sha256sums=('b54fcf84d18f24562e74e0bfa252a51ff09dd0a1ab81f5919053e4e868b5cdf6'
            'SKIP'
            '1f0c6241a58953e33557b50d873527eb6c3c06b821fb857a84c1827110e18723')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>
options=(debug)

prepare() {
  patch -d $pkgname-$pkgver -p1 < 14fb6f0a.patch # Fix build with zxing-cpp 2.0
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
