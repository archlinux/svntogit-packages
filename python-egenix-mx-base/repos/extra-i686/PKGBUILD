# $Id$
# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>

pkgbase=python-egenix-mx-base
pkgname=('python2-egenix-mx-base')
_origname=egenix-mx-base
pkgver=3.2.5
pkgrel=1
pkgdesc="A collection of Python tools"
arch=('i686' 'x86_64')
license=('custom')
url="http://www.egenix.com/products/python/mxBase/"
makedepends=('python2')
conflicts=('python-egenix-mx-base<=3.2.4-1')
replaces=('python-egenix-mx-base<=3.2.4-1')
source=("http://downloads.egenix.com/python/${_origname}-${pkgver}.tar.gz")
sha1sums=('d64607ff3bdd0c908f1f4ce39d458514135c9a86')

build() {
  cd "${srcdir}/${_origname}-${pkgver}"

  find . -name \*.py -exec sed -i -e "s|^#!.*python|#!/usr/bin/python2|" {} \;

  python2 setup.py build
}

package_python2-egenix-mx-base() {
  depends=('python2>=2.7')
  cd "${srcdir}/${_origname}-${pkgver}"
  python2 setup.py install --root="${pkgdir}" --skip-build --optimize=1
  install -D -m644 COPYRIGHT "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
