# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=imlib
pkgver=1.9.15
pkgrel=9
pkgdesc="General image handling library for X11 and Gtk"
arch=('i686' 'x86_64')
url="http://freshmeat.net/projects/imlib/"
license=('GPL')
depends=('gtk' 'giflib' 'libpng>=1.4.0' 'libtiff>=3.9.2-2' 'libjpeg>=8' 'libsm')
makedepends=('libxt')
options=('!libtool')
source=(http://ftp.gnome.org/pub/GNOME/sources/imlib/1.9/${pkgname}-${pkgver}.tar.bz2
        debian-bug448360.patch CAN-2004-1026.patch aclocal-fixes.patch libpng14.patch)
md5sums=('7db987e6c52e4daf70d7d0f471238eae' '5f9da697934b6bd3b497ac9160ce4f5c'\
         'b273d36aa60adbfaacaf6062234e4c1f' '33b832f0dc6c9723cd0dfe9c8d0a6797'\
         '9d8a029f5fb05978db0dbcd4ff38fe23')
sha1sums=('c9a732a354fbb3c7e1a426e5d19fc92d73f8f720' 'fe2fd9ce4d7bc62271e724153de39012de8ec5ee'\
         'ec1b47281c1a7fb21abe841f948ecc56ed13f310' 'af54cb1ee0c8c6122b277284ebdd1022e31df3b5'\
         '20ed6063ba787ac7c2677bb4b135c0b7285c52af')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/debian-bug448360.patch" || return 1
  patch -Np1 -i "${srcdir}/CAN-2004-1026.patch" || return 1
  patch -Np0 -i "${srcdir}/aclocal-fixes.patch" || return 1
  patch -Np1 -i "${srcdir}/libpng14.patch" || return 1
  ./configure --prefix=/usr --sysconfdir=/etc --mandir=/usr/share/man --enable-shm || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
}
