# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-utils
pkgver=2.30.0
pkgrel=1
pkgdesc="Common utilities for GNOME"
arch=(i686 x86_64)
license=('GPL')
depends=('libgtop>=2.28.0' 'gnome-panel>=2.30.0' 'libcanberra>=0.23')
makedepends=('gnome-doc-utils>=0.18.0' 'pkgconfig' 'intltool')
options=('!libtool' '!emptydirs')
url="http://www.gnome.org"
groups=('gnome-extra')
install=gnome-utils.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.30/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('745bb25fce536dc5337e7b5cbcbce9eee66b3cc0c500de302759b98ebcef6a8f')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure --prefix=/usr --sysconfdir=/etc \
        --libexecdir=/usr/lib/gnome-utils \
        --localstatedir=/var --disable-static \
        --disable-scrollkeeper || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gnome-utils-2.0 ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
  
  # remove it also from Xfce menu that ships its own thingy
  sed -i -e "s/NotShowIn\=KDE\;/NotShowIn\=KDE\;XFCE\;/" ${pkgdir}/usr/share/applications/gnome-dictionary.desktop || return 1
}
