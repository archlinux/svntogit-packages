#  $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-utils
pkgver=2.32.0
pkgrel=1
pkgdesc="Common utilities for GNOME"
arch=(i686 x86_64)
license=('GPL')
depends=('libgtop>=2.28.1' 'libcanberra>=0.25' 'libsm' 'gconf>=2.32.0')
makedepends=('gnome-doc-utils>=0.20.1' 'pkgconfig' 'intltool' 'gnome-panel-bonobo>=2.32.0.2')
optdepends=('gnome-panel-bonobo: GDict panel applet')
options=('!libtool' '!emptydirs')
url="http://www.gnome.org"
groups=('gnome-extra')
install=gnome-utils.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.32/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('033a55cf442096b28ea6292043354feafe000d1e5f4ebb962a9b6d72eb9a93fa')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure --prefix=/usr --sysconfdir=/etc \
        --libexecdir=/usr/lib/gnome-utils \
        --localstatedir=/var --disable-static \
        --disable-scrollkeeper
  make
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gnome-utils-2.0 ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas

  # remove it also from Xfce menu that ships its own thingy
  sed -i -e "s/NotShowIn\=KDE\;/NotShowIn\=KDE\;XFCE\;/" ${pkgdir}/usr/share/applications/gnome-dictionary.desktop
  sed -i -e "s/NotShowIn\=KDE\;/NotShowIn\=KDE\;XFCE\;/" ${pkgdir}/usr/share/applications/gnome-screenshot.desktop
}
