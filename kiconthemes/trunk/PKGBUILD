# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kiconthemes
pkgver=r6.0c6d924
pkgrel=1
pkgdesc='KIconThemes'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kiconthemes'
license=('LGPL')
depends=('qt5-svg' 'kconfigwidgets' 'kitemviews')
makedepends=('extra-cmake-modules' 'git')
conflicts=('kiconthemes')
provides=('kiconthemes')
source=('git://anongit.kde.org/kiconthemes.git')
md5sums=('SKIP')

pkgver() {
  cd kiconthemes
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kiconthemes \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
