# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-getting-started-docs
pkgver=3.26.2
pkgrel=1
pkgdesc="Help a new user get started in GNOME"
url="https://git.gnome.org/browse/gnome-getting-started-docs"
license=('CCPL:cc-by-sa-3.0')
arch=(any)
# gst plugins required to decode help videos
depends=(yelp gst-plugins-base gst-plugins-good)
makedepends=(yelp-tools git)
groups=(gnome)
_commit=621be0138977dd86f0def233c994673f675b5fa6  # tags/3.26.2^0
source=("git+https://git.gnome.org/browse/gnome-getting-started-docs#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
