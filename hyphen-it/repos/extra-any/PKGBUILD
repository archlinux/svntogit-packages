# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=hyphen-it
pkgver=20071127
pkgrel=3
pkgdesc="Italian hyphenation rules"
arch=('any')
url="http://wiki.services.openoffice.org/wiki/Dictionaries"
license=('LGPL')
optdepends=('hyphen: offers hyphenation library functions')
source=("http://vesta.informatik.rwth-aachen.de/ftp/pub/mirror/OpenOffice/contrib/dictionaries/hyph_it_IT.zip")
md5sums=('ee57402fa3930e0641d627ec7f4f1619')

package() {
  install -d "${pkgdir}"/usr/share/hyphen

  install -m644 hyph_it_IT.dic "${pkgdir}"/usr/share/hyphen/hyph_it_IT.dic

  pushd "${pkgdir}"/usr/share/hyphen/
  it_IT_aliases="it_CH la_VA"
  for lang in $it_IT_aliases; do
    ln -s hyph_it_IT.dic hyph_$lang.dic
  done
  popd

  # the symlinks
  install -d "${pkgdir}"/usr/share/myspell/dicts
  pushd "${pkgdir}"/usr/share/myspell/dicts
    for file in "${pkgdir}"/usr/share/hyphen/*; do
      ln -sv /usr/share/hyphen/$(basename $file) .
    done
  popd
}
