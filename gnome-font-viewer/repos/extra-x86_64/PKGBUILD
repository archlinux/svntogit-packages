#  $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-font-viewer
pkgver=3.24.0
pkgrel=1
pkgdesc="A font viewer utility for GNOME"
url="https://git.gnome.org/browse/gnome-font-viewer"
arch=(i686 x86_64)
license=(GPL)
depends=(gtk3 gnome-desktop)
makedepends=(intltool gnome-common git)
groups=(gnome)
_commit=7d5da57b85f760ca298800cb5edeb7500fe4dd9c  # tags/3.24.0^0
source=("git+https://git.gnome.org/browse/gnome-font-viewer#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  git revert -n 419afa1a577a1717f40eff7394c791e382180bfe
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
