#  $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-font-viewer
pkgver=3.26.0
pkgrel=1
pkgdesc="A font viewer utility for GNOME"
url="https://git.gnome.org/browse/gnome-font-viewer"
arch=(x86_64)
license=(GPL)
depends=(gtk3 gnome-desktop)
makedepends=(git meson)
groups=(gnome)
_commit=b4dc10b30d66dce4302828cecef3969b7221f010  # tags/3.26.0^0
source=("git+https://git.gnome.org/browse/gnome-font-viewer#commit=$_commit")
sha256sums=('SKIP')

check() {
  cd build
  meson test
}

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  mkdir build
  cd $pkgname
}

build() {
  cd build
  meson setup --prefix=/usr --buildtype=release ../$pkgname
  ninja
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}
