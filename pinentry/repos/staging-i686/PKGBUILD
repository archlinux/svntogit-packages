# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
pkgname=pinentry
pkgver=0.9.6
pkgrel=1
pkgdesc="a collection of simple PIN or passphrase entry dialogs which utilize the Assuan protocol"
arch=('i686' 'x86_64')
license=('GPL')
url="http://gnupg.org/related_software/pinentry/"
depends=('ncurses' 'libcap>=2.16' 'libassuan')
makedepends=('gtk2' 'qt4')
optdepends=('gtk2: for gtk2 backend'
            'qt4: for qt4 backend')
source=(ftp://ftp.gnupg.org/gcrypt/$pkgname/$pkgname-$pkgver.tar.bz2{,.sig})
install=pinentry.install
sha1sums=('296992d7b26ba2a517b093ccae26bc00b88b3bb5'
          'SKIP')
validpgpkeys=('D8692123C4065DEA5E0F3AB5249B39D24F25E3B6') # Werner Koch

build() {
  cd $srcdir/$pkgname-$pkgver
  #for file in qt4/*.moc; do
  #     moc-qt4 ${file/.moc/.h} > ${file}
  # done

  # https://bugs.gnupg.org/gnupg/issue1961
  CXXFLAGS+=' -std=c++11'

  ./configure --prefix=/usr \
	--enable-pinentry-curses \
	--enable-pinentry-gtk2 \
        --enable-pinentry-qt4 \
	--enable-fallback-curses
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=${pkgdir} install
}
