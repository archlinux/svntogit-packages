# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
pkgname=pinentry
pkgver=0.8.3
pkgrel=1
pkgdesc="a collection of simple PIN or passphrase entry dialogs which utilize the Assuan protocol"
arch=('i686' 'x86_64')
license=('GPL')
url="http://gnupg.org/related_software/pinentry/"
depends=('ncurses' 'libcap>=2.16')
makedepends=('gtk2' 'qt4')
optdepends=('gtk2: for gtk2 backend'
            'qt4: for qt4 backend')  
source=(ftp://ftp.gnupg.org/gcrypt/$pkgname/$pkgname-$pkgver.tar.bz2
        qt4-pinentry-window.patch)
install=pinentry.install
md5sums=('2ae681cbca0d9fb774b2c90b11ebf56c'
         '450b8713fe42f1bd93311ef84767c3d8')

build() {
  cd $srcdir/$pkgname-$pkgver
  #fix: https://bugs.archlinux.org/task/29998
  patch -Np1 -i ../qt4-pinentry-window.patch
  for file in qt4/*.moc; do
       moc-qt4 ${file/.moc/.h} > ${file}
   done
  ./configure --prefix=/usr \
	--enable-pinentry-curses \
        --disable-pinentry-gtk \
        --disable-pinentry-qt \
	--enable-pinentry-gtk2 \
        --enable-pinentry-qt4 \
	--enable-fallback-curses
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=${pkgdir} install 
}
