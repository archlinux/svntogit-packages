# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=telepathy-qt
pkgver=0.9.2
pkgrel=1
pkgdesc="A library for Qt-based Telepathy clients"
arch=('i686' 'x86_64')
url="http://telepathy.freedesktop.org/wiki/"
license=('LGPL')
options=('!libtool')
depends=('qt' 'telepathy-farstream')
makedepends=('libxslt' 'python2' 'cmake' 'doxygen')
conflicts=('telepathy-qt4')
replaces=('telepathy-qt4')
provides=('telepathy-qt4')
source=("http://telepathy.freedesktop.org/releases/${pkgname}/${pkgname}-${pkgver}.tar.gz")
md5sums=('e49df0c6d19bbac622209d0f87f0d1b2')

build() {
    cd "$srcdir"
    mkdir build
    cd build
    cmake ../${pkgname}-${pkgver} \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr
    make
}

package() {
    cd "$srcdir"/build
    make DESTDIR="$pkgdir" install
}
