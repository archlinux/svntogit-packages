# $Id$
# Maintainer: Tom Gundersen <teg@jklm.no>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=libtirpc
pkgver=1.0.1
pkgrel=2
pkgdesc="Transport Independent RPC library (SunRPC replacement)"
arch=('i686' 'x86_64')
url="http://libtirpc.sourceforge.net/"
license=('BSD')
depends=('krb5')
backup=('etc/netconfig')
# git tree: git://linux-nfs.org/~steved/libtirpc
source=(http://downloads.sourceforge.net/sourceforge/libtirpc/${pkgname}-${pkgver}.tar.bz2
        add_missing_rwlock_unlocks_in_xprt_register.diff)
sha1sums=('8da1636f98b5909c0d587e7534bc1e91f5c1a970'
          'c451b0eb527098c0d9e308e4978866b7aaaf688e')

prepare() {
  cd ${pkgname}-${pkgver}
  # upstream post release fix to properly work with rpcbind
  patch -Np1 -i ../add_missing_rwlock_unlocks_in_xprt_register.diff
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconf=/etc
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -D -m644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
