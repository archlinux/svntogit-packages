# $Id$
# Maintainer: Tom Gundersen <teg@jklm.no>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=libtirpc
pkgver=1.0.3
pkgrel=2
pkgdesc="Transport Independent RPC library (SunRPC replacement)"
arch=('x86_64')
url="http://libtirpc.sourceforge.net/"
license=('BSD')
depends=('krb5')
backup=('etc/netconfig')
# git tree: git://linux-nfs.org/~steved/libtirpc
source=(http://downloads.sourceforge.net/sourceforge/libtirpc/${pkgname}-${pkgver}.tar.bz2
        restore_using_reserve_ports_for_client_connections.diff)
sha1sums=('48adb32dc7c3b73c66f001c239da76b8398abf11'
          '6a290ad197f044ed88607760a69dd24746152bc3')

prepare(){
  cd ${pkgname}-${pkgver}
  patch -Np1 -i ../restore_using_reserve_ports_for_client_connections.diff
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconf=/etc
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -D -m644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
