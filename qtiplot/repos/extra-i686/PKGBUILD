# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: damir <damir.archlinux.org>

pkgname=qtiplot
pkgver=0.9.7.4
pkgrel=2
pkgdesc="Data analysis and scientific plotting - free clone of Origin"
arch=('i686' 'x86_64')
url="http://soft.proindependent.com/qtiplot.html"
depends=('muparser>=1.30' 'gsl' 'python>=2.6' 'pyqt' 'sip' \
	'boost>=1.36' 'libgl' 'qwt')
# build against liborigin provided in the package ...
# build against qwtplot3d provided in the package ...
# as systemwide one doesn't provide all needed functions
makedepends=('pkgconfig')
license=('GPL2')

source=("http://soft.proindependent.com/src/qtiplot-$pkgver.tar.bz2" \
	"$pkgname.desktop" "$pkgname.png" \
        "qtiplot-gcc4.3.patch" "liborigin-gcc4.3.patch" \
	"qtiplot.archlinux.patch" "qtiplot-0.9.7.3-qwt3d.patch" \
	"dynamicgsl.patch" "qtiplot-sip.patch" "qtiplot-nopython.patch")

md5sums=('dd149d024501e34cda685023522b4d8d'         '56bd53f4f1367c285086acb969f13348'
         'ad8affbd6f0d5cbdcde46c923ee2668a'         '927149a0c3623209d4aaef867031ba76'
         'ad2333cfb8df37d33a6449c2c49ec945'         '4a37f3bbf2cff51dcd93305a920d4415'
         '20ae1d441f299aa2c2c54e3bed3a9786'         '2c858d26255ecde7bcd157515ed767d6'
         'f68e62cd6c6762345f1a123321f49e57'         'b65f2b1266b42b9c298ac4dee3d19c90')

build() {

  cd $srcdir/$pkgname-$pkgver

  # patching
  patch -Np0 < $srcdir/dynamicgsl.patch || return 1

  cd $srcdir/$pkgname-$pkgver/$pkgname
  patch -Np0 -i $srcdir/qtiplot-gcc4.3.patch || return 1
  patch -Np0 -i $srcdir/qtiplot-0.9.7.3-qwt3d.patch || return 1
  patch -Np0 -i $srcdir/liborigin-gcc4.3.patch || return 1
  patch -Np0 -i $srcdir/qtiplot.archlinux.patch || return 1
  patch -Np0 -i $srcdir/qtiplot-sip.patch || return 1
  patch -Np2 -i $srcdir/qtiplot-nopython.patch || return 1

  sed -i 's#d_python_config_folder + "#"/usr/share/qtiplot#' src/core/ApplicationWindow.cpp

  export QMAKESPEC=linux-g++

  qmake qtiplot.pro || return 1
  make QTDIR=/usr/ QMAKESPEC=linux-g++ || return 1
  INSTALL_ROOT=${pkgdir} make install || return 1

  # make it nice:
  install -D -m644 $srcdir/$pkgname.png \
  	$pkgdir/usr/share/pixmaps/$pkgname.png || return 1
  install -D -m644 $srcdir/$pkgname.desktop \
  	$pkgdir/usr/share/applications/$pkgname.desktop || return 1
}
