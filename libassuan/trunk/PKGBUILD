# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgname=libassuan
pkgver=2.4.2
pkgrel=1
pkgdesc="A IPC library used by some GnuPG related software"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnupg.org/related_software/libassuan"
depends=('libgpg-error')
source=(ftp://ftp.gnupg.org/gcrypt/$pkgname/$pkgname-$pkgver.tar.bz2{,.sig})
install=libassuan.install
sha1sums=('ac1047f9764fd4a4db7dafe47640643164394db9'
          'SKIP')
validpgpkeys=('D8692123C4065DEA5E0F3AB5249B39D24F25E3B6') # Werner Koch

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
