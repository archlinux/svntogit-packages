# $Id$
# Maintainer:
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Adri√† Arrufat <swiftscythe@gmail.com>

pkgname=mediastreamer
pkgver=2.7.3
pkgrel=4
pkgdesc="A library written in C that allows you to create and run audio and video streams"
arch=('i686' 'x86_64')
url="http://www.linphone.org/"
license=('GPL')
depends=('ortp' 'speex' 'v4l-utils' 'ffmpeg')
makedepends=('cmake' 'automoc4')
source=("http://download-mirror.savannah.gnu.org/releases/linphone/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        "disable-v4l1.patch")
md5sums=('5213307f557d86aa648f1a53a885138c'
         '7ee15689eed1adbdcf0ddab8fadca34a')
options=('!libtool')

build() {
  cd "${srcdir}"/$pkgname-$pkgver/
  patch -p2 -i "${srcdir}"/disable-v4l1.patch

  ./configure --prefix=/usr \
    --libexecdir=/usr/lib/mediastreamer/
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver
  make DESTDIR="${pkgdir}" install
}
