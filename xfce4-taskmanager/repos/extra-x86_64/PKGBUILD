# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>

pkgname=xfce4-taskmanager
pkgver=1.5.2.201.g58cf9b2
pkgrel=1
pkgdesc="Easy to use task manager"
arch=('x86_64')
url="https://docs.xfce.org/apps/xfce4-taskmanager"
license=('GPL2')
depends=('libxfce4ui' 'xfconf' 'libxmu' 'libwnck3')
makedepends=('intltool' 'git' 'xfce4-dev-tools')
groups=('xfce4-goodies')
#source=(https://archive.xfce.org/src/apps/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
_commit=58cf9b2694d539a4066517a7e2ac88587b1b5937
source=(git+https://gitlab.xfce.org/apps/xfce4-taskmanager.git#commit=$_commit)
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --long | sed "s/^$pkgname-//;s/-/./g"
}

build() {
  cd $pkgname

  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-debug
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
