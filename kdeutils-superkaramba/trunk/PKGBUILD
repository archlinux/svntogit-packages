# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeutils-superkaramba
pkgver=15.08.1
pkgrel=1
pkgdesc='An engine for cool desktop eyecandy'
url='http://kde.org/applications/utilities/superkaramba/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdebase-runtime' 'qimageblitz')
makedepends=('cmake' 'automoc4')
groups=('kde-applications' 'kdeutils')
install=${pkgname}.install
source=("http://download.kde.org/stable/applications/${pkgver}/src/superkaramba-${pkgver}.tar.xz")
sha1sums=('641259c163e59bff7632843347d975793596ba86')

build() {
  cd "${srcdir}"
  mkdir -p build
  cd build
  cmake ../superkaramba-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $srcdir/build
  make DESTDIR=$pkgdir install
}
