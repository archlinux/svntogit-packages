# $Id$
# Maintainer: Tom Gundersen <teg@jklm.no>
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>
# Maintainer: Aaron Griffin <aaron@archlinux.org>

pkgname=initscripts
pkgver=2011.07.3
pkgrel=1
pkgdesc="System initialization/bootup scripts"
arch=('i686' 'x86_64')
url="http://www.archlinux.org"
license=('GPL2')
groups=('base')
backup=(etc/inittab etc/rc.conf etc/rc.local etc/rc.local.shutdown etc/conf.d/wireless)
depends=('glibc' 'bash' 'grep' 'coreutils' 'udev>=171' 'iproute2'
         'ncurses' 'kbd' 'findutils' 'sysvinit')
optdepends=('bridge-utils: Network bridging support'
            'dhcpcd: DHCP network configuration'
            'net-tools: legacy network support'
            'wireless_tools: Wireless networking')
install=initscripts.install
source=("ftp://ftp.archlinux.org/other/initscripts/${pkgname}-${pkgver}.tar.xz"
        'wireless.conf.d')

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  install -D -m644 ${srcdir}/wireless.conf.d ${pkgdir}/etc/conf.d/wireless
}
md5sums=('1eb0cd1bbe10a7b05c473c285a36849e'
         '027576534885b8d5dded9be546057b12')
