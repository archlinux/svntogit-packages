# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdelibs
pkgver=4.1.2
pkgrel=2
pkgdesc="KDE Core Libraries"
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('kde')
depends=('libxcursor' 'phonon' 'shared-mime-info' 'qt>=4.4' 'libxpm'
	 'enchant' 'jasper' 'openexr' 'strigi' 'bzip2' 'libxslt' 'libxtst'
	 'giflib' 'soprano' 'ca-certificates' 'heimdal' 'pmount' 'xdg-utils')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'intltool' 'avahi' 'libgl' 'hspell')
replaces=('arts')
options=('docs')
install='kdelibs.install'
source=("http://download.kde.org/stable/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2"
        'kde-applications-menu.patch' 'archlinux-menu.patch' 'kdedglobalaccel.patch')
md5sums=('cb5f43d2b11669fb17b0a1ecf3736c17'
         '3daa0f01fe26325fb3dbd807a04898ad'
         '0ad42200df2d2b8aab01d2ef4b21e02f'
         '5798b93694522b8d2d98ddb60ccf9a3a')

build() {
	cd $srcdir/${pkgname}-${pkgver}
	# fix crash with broken config files (see Bug #171870 and #171206)
	patch -p0 -i $srcdir/kdedglobalaccel.patch || return 1
	# avoid file conflict with gnome-menu
	patch -p1 -i $srcdir/kde-applications-menu.patch || return 1
	# add Archlinux menu entry
	patch -p0 -i $srcdir/archlinux-menu.patch || return 1
	cd ..
	mkdir build
	cd build
	cmake ../${pkgname}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DKDE_DISTRIBUTION_TEXT='Arch Linux' \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DSYSCONF_INSTALL_DIR=/etc \
		-DHTML_INSTALL_DIR=/usr/share/doc/kde/html \
		-DKDE_DEFAULT_HOME='.kde4' \
		-DWITH_FAM=OFF
	make
	make DESTDIR=$pkgdir install

	# cert bundle seems to be hardcoded
	# link it to the one from ca-certificates
	rm -f $pkgdir/usr/share/apps/kssl/ca-bundle.crt
	ln -sf /etc/ssl/certs/ca-certificates.crt $pkgdir/usr/share/apps/kssl/ca-bundle.crt
}
