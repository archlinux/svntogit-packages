# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdelibs
pkgver=4.10.3
pkgrel=3
pkgdesc="KDE Core Libraries"
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kdelibs'
license=('GPL' 'LGPL' 'FDL')
depends=('strigi' 'attica' 'libxss' 'soprano' 'krb5' 'grantlee'
        'shared-desktop-ontologies' 'qca' 'libdbusmenu-qt' 'polkit-qt'
        'shared-mime-info' 'enchant' 'giflib' 'jasper' 'openexr'
        'docbook-xsl' 'upower' 'udisks2' 'libxcursor' 'phonon'
        'media-player-info' 'libxtst' 'libutempter' 'qtwebkit')
makedepends=('cmake' 'automoc4' 'avahi' 'libgl' 'hspell' 'mesa')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/${pkgname}-${pkgver}.tar.xz"
        'kde-applications-menu.patch' 'archlinux-menu.patch' 'qt4.patch'
        'security-fix.patch'
        'fix-xfreepixmap-call.patch')
sha1sums=('aa154998e227121be82f5b04442e908e7dece01b'
          '86ee8c8660f19de8141ac99cd6943964d97a1ed7'
          '63a850ab4196b9d06934f2b4a13acd9f7739bc67'
          'ed1f57ee661e5c7440efcaba7e51d2554709701c'
          '506562009a667f18481be0d2c5035b181fce38bb'
          '556c5473219d8208ec920f46c08bf61fda5b60d9')

prepare() {
       cd ${pkgname}-${pkgver}
       # avoid file conflict with gnome-menus
       patch -p1 -i "${srcdir}"/kde-applications-menu.patch
       # add Archlinux menu entry
       patch -p1 -i "${srcdir}"/archlinux-menu.patch
       # qmake refers to Qt5
       patch -p1 -i "${srcdir}"/qt4.patch

       # KDEBUG#319428
       patch -p1 -i "${srcdir}"/security-fix.patch
       
       # KDEBUG#319137
       patch -p1 -i "${srcdir}"/fix-xfreepixmap-call.patch
}

build() {
       mkdir build
       cd build
       cmake ../${pkgname}-${pkgver} \
               -DCMAKE_BUILD_TYPE=Release \
               -DKDE4_BUILD_TESTS=OFF \
               -DCMAKE_SKIP_RPATH=ON \
               -DKDE_DISTRIBUTION_TEXT='Arch Linux' \
               -DCMAKE_INSTALL_PREFIX=/usr \
               -DSYSCONF_INSTALL_DIR=/etc \
               -DHTML_INSTALL_DIR=/usr/share/doc/kde/html \
               -DKDE_DEFAULT_HOME='.kde4' \
               -DWITH_FAM=OFF \
               -DWITH_HUpnp=OFF \
               -DWITH_SOLID_UDISKS2=ON
       make
}

package() {
       cd "${srcdir}"/build
       make DESTDIR="${pkgdir}" install

       # cert bundle seems to be hardcoded
       # link it to the one from ca-certificates
       rm -f "${pkgdir}"/usr/share/apps/kssl/ca-bundle.crt
       ln -sf /etc/ssl/certs/ca-certificates.crt "${pkgdir}"/usr/share/apps/kssl/ca-bundle.crt
}
