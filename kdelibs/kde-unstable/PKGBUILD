# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdelibs
pkgver=4.2.98
pkgrel=2
pkgdesc="KDE Core Libraries"
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
depends=('qt' 'shared-mime-info' 'hal' 'xz-utils' 'heimdal'
	 'enchant' 'jasper' 'openexr' 'strigi' 'libxtst'
	 'giflib' 'soprano>2.2.3' 'ca-certificates' 'xdg-utils')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'intltool' 'avahi' 'libgl' 'hspell' 'libxslt')
replaces=('arts')
install='kdelibs.install'
source=(http://download.kde.org/unstable/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2
        'kde-applications-menu.patch' 'archlinux-menu.patch' 'abs-syntax-highlight.patch')
md5sums=('57d29dbd24afb72c03545ba583c8347f'
         'e94450ba5430ea9c1e33bad9ae38ca2d'
         'a6b31e1e5812340f010ef4cdd77e356c'
         'a471b0c4642028917f4e17025eaa6505')

build() {
	cd $srcdir/${pkgname}-${pkgver}

	# avoid file conflict with gnome-menu
	patch -p1 -i $srcdir/kde-applications-menu.patch
	# add Archlinux menu entry
	patch -p1 -i $srcdir/archlinux-menu.patch
	# add syntax highlightning for PKGBUILD and .install files
	patch -p0 -i $srcdir/abs-syntax-highlight.patch
	cd ..
	mkdir build
	cd build
	cmake ../${pkgname}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed' \
		-DKDE_DISTRIBUTION_TEXT='Arch Linux' \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DSYSCONF_INSTALL_DIR=/etc \
		-DHTML_INSTALL_DIR=/usr/share/doc/kde/html \
		-DKDE_DEFAULT_HOME='.kde4' \
		-DWITH_FAM=OFF
	make
}

package() {
	cd $srcdir/build
	make DESTDIR=$pkgdir install

	# cert bundle seems to be hardcoded
	# link it to the one from ca-certificates
	rm -f $pkgdir/usr/share/apps/kssl/ca-bundle.crt
	ln -sf /etc/ssl/certs/ca-certificates.crt $pkgdir/usr/share/apps/kssl/ca-bundle.crt
}
