# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdelibs
pkgver=4.6.90
pkgrel=1
pkgdesc="KDE Core Libraries"
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
depends=('strigi' 'attica' 'libxss' 'xz' 'openssl' 'soprano' 'krb5'
        'shared-desktop-ontologies' 'qca' 'libdbusmenu-qt' 'polkit-qt' 'grantlee'
        'shared-mime-info' 'enchant' 'giflib' 'jasper' 'openexr' 'xdg-utils'
        'phonon' 'hicolor-icon-theme' 'upower' 'udisks' 'libxcursor'
        'docbook-xsl')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'intltool' 'avahi' 'libgl'
        'hspell')
replaces=('kdelibs-experimental')
install='kdelibs.install'
source=("http://download.kde.org/unstable/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2"
        'kde-applications-menu.patch' 'archlinux-menu.patch')
sha1sums=('e04a0ecdb83a2cb086e13c9a2541b264dce00993'
          '86ee8c8660f19de8141ac99cd6943964d97a1ed7'
          '63a850ab4196b9d06934f2b4a13acd9f7739bc67')

build() {
       cd ${srcdir}/${pkgname}-${pkgver}

       # avoid file conflict with gnome-menu
       patch -p1 -i $srcdir/kde-applications-menu.patch
       # add Archlinux menu entry
       patch -p1 -i $srcdir/archlinux-menu.patch

       cd ${srcdir}
       mkdir build
       cd build
       cmake ../${pkgname}-${pkgver} \
               -DCMAKE_BUILD_TYPE=Release \
               -DCMAKE_SKIP_RPATH=ON \
               -DKDE_DISTRIBUTION_TEXT='Arch Linux' \
               -DCMAKE_INSTALL_PREFIX=/usr \
               -DSYSCONF_INSTALL_DIR=/etc \
               -DHTML_INSTALL_DIR=/usr/share/doc/kde/html \
               -DKDE_DEFAULT_HOME='.kde4' \
               -DWITH_FAM=OFF
       make
}

package() {
       cd $srcdir/build
       make DESTDIR=$pkgdir install

       # cert bundle seems to be hardcoded
       # link it to the one from ca-certificates
       rm -f $pkgdir/usr/share/apps/kssl/ca-bundle.crt
       ln -sf /etc/ssl/certs/ca-certificates.crt $pkgdir/usr/share/apps/kssl/ca-bundle.crt
}
