# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdelibs
pkgver=4.2.4
pkgrel=5
pkgdesc="KDE Core Libraries"
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('kde')
depends=('phonon' 'shared-mime-info' 'libxpm' 'hal' 
	 'enchant' 'jasper' 'openexr' 'strigi' 'libxtst'
	 'giflib' 'soprano' 'ca-certificates' 'xdg-utils' 'libxft')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'intltool' 'avahi' 'libgl' 'hspell')
replaces=('arts')
options=('docs')
install='kdelibs.install'
source=("http://download.kde.org/stable/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2"
        'kde-applications-menu.patch' 'archlinux-menu.patch' 'libjpeg-7.patch'
        'CVE-2009-1725.patch')
md5sums=('9ed29086c33bc73256a7fa5268df560b'
         'e94450ba5430ea9c1e33bad9ae38ca2d'
         '0ad42200df2d2b8aab01d2ef4b21e02f'
         'f1578c3987049ed0610732e6cd5263d9'
         '7a6609225d996bdec640fd833bc4169c')

build() {
	cd $srcdir/${pkgname}-${pkgver}

	# avoid file conflict with gnome-menu
	patch -p1 -i $srcdir/kde-applications-menu.patch
	# add Archlinux menu entry
	patch -p0 -i $srcdir/archlinux-menu.patch
	# fix compatibility with libjpeg-7; see https://bugs.kde.org/show_bug.cgi?id=198779
	# will be in 4.3.0
	patch -p0 -i $srcdir/libjpeg-7.patch
	# see http://bugs.archlinux.org/task/15706
	patch -p0 -i $srcdir/CVE-2009-1725.patch || return 1
	cd ..
	mkdir build
	cd build
	cmake ../${pkgname}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed' \
		-DKDE_DISTRIBUTION_TEXT='Arch Linux' \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DSYSCONF_INSTALL_DIR=/etc \
		-DHTML_INSTALL_DIR=/usr/share/doc/kde/html \
		-DKDE_DEFAULT_HOME='.kde4' \
		-DWITH_FAM=OFF
	make
	make DESTDIR=$pkgdir install

	# cert bundle seems to be hardcoded
	# link it to the one from ca-certificates
	rm -f $pkgdir/usr/share/apps/kssl/ca-bundle.crt
	ln -sf /etc/ssl/certs/ca-certificates.crt $pkgdir/usr/share/apps/kssl/ca-bundle.crt
}
