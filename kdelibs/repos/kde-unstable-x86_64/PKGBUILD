# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdelibs
pkgver=4.8.97
pkgrel=1
pkgdesc="KDE Core Libraries"
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kdelibs'
license=('GPL' 'LGPL' 'FDL')
depends=('strigi' 'attica' 'libxss' 'soprano' 'krb5' 'grantlee'
        'shared-desktop-ontologies' 'qca' 'libdbusmenu-qt' 'polkit-qt'
        'shared-mime-info' 'enchant' 'giflib' 'jasper' 'openexr'
        'docbook-xsl' 'upower' 'udisks' 'libxcursor' 'phonon' 'qtwebkit'
        'media-player-info')
makedepends=('cmake' 'automoc4' 'avahi' 'libgl' 'hspell')
install=${pkgname}.install
source=("http://download.kde.org/unstable/${pkgver}/src/${pkgname}-${pkgver}.tar.xz"
        'kde-applications-menu.patch' 'archlinux-menu.patch'
        'use-pythondontwritebytecode.patch')
sha1sums=('adbbc70a2e0d243f3875469cafe22b63b0625a7e'
          '86ee8c8660f19de8141ac99cd6943964d97a1ed7'
          '63a850ab4196b9d06934f2b4a13acd9f7739bc67'
          'bbacbbe2194f3961cd7557d98a9ddef158ca11aa')

build() {
       cd "${srcdir}"/${pkgname}-${pkgver}

       # avoid file conflict with gnome-menus
       patch -p1 -i "${srcdir}"/kde-applications-menu.patch
       # add Archlinux menu entry
       patch -p1 -i "${srcdir}"/archlinux-menu.patch

       # Set PYTHONDONTWRITEBYTECODE (KDEBUG#276151)
       patch -p0 -i "${srcdir}"/use-pythondontwritebytecode.patch
       
       cd "${srcdir}"
       mkdir build
       cd build
       cmake ../${pkgname}-${pkgver} \
               -DCMAKE_BUILD_TYPE=Release \
               -DCMAKE_SKIP_RPATH=ON \
               -DKDE_DISTRIBUTION_TEXT='Arch Linux' \
               -DCMAKE_INSTALL_PREFIX=/usr \
               -DSYSCONF_INSTALL_DIR=/etc \
               -DHTML_INSTALL_DIR=/usr/share/doc/kde/html \
               -DKDE_DEFAULT_HOME='.kde4' \
               -DWITH_FAM=OFF \
               -DWITH_HUpnp=OFF
       make
}

package() {
       cd "${srcdir}"/build
       make DESTDIR="${pkgdir}" install

       # cert bundle seems to be hardcoded
       # link it to the one from ca-certificates
       rm -f "${pkgdir}"/usr/share/apps/kssl/ca-bundle.crt
       ln -sf /etc/ssl/certs/ca-certificates.crt "${pkgdir}"/usr/share/apps/kssl/ca-bundle.crt
}
