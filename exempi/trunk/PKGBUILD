# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Andr√© Klitzing <aklitzing@online.de>
pkgname=exempi
pkgver=2.2.0
pkgrel=1
pkgdesc="A library to parse XMP metadata"
arch=(i686 x86_64)
url="http://libopenraw.freedesktop.org/wiki/Exempi"
license=('custom:BSD')
depends=('gcc-libs' 'expat' 'zlib')
makedepends=('boost')
options=(!libtool)
source=(http://libopenraw.freedesktop.org/download/$pkgname-$pkgver.tar.bz2)
md5sums=('f46d96975613593ee17aaf48cd350228')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -d "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 "$srcdir/$pkgname-$pkgver/COPYING" \
  	"$pkgdir/usr/share/licenses/$pkgname"
}
