# Maintainer:
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Andr√© Klitzing <aklitzing@online.de>

pkgname=exempi
pkgver=2.6.2
pkgrel=1
pkgdesc="A library to parse XMP metadata"
url="https://libopenraw.freedesktop.org/wiki/Exempi"
arch=(x86_64)
license=(custom:BSD)
depends=(gcc-libs expat zlib)
makedepends=(boost)
source=(https://libopenraw.freedesktop.org/download/$pkgname-$pkgver.tar.bz2{,.asc})
sha256sums=('4d17d4c93df2a95da3e3172c45b7a5bf317dd31dafd1c7a340169728c7089d1d'
            'SKIP')
validpgpkeys=('6C44DB3E0BF3EAF5B433239A5FEE05E6A56E15A3') # Hubert Figuiere

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr # --disable-static
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
