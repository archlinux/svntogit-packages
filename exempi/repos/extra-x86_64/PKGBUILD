# Maintainer:
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Andr√© Klitzing <aklitzing@online.de>

pkgname=exempi
pkgver=2.6.3
pkgrel=1
pkgdesc="A library to parse XMP metadata"
url="https://libopenraw.freedesktop.org/wiki/Exempi"
arch=(x86_64)
license=(custom:BSD)
depends=(gcc-libs expat zlib)
makedepends=(boost)
source=(https://libopenraw.freedesktop.org/download/$pkgname-$pkgver.tar.bz2{,.asc})
sha256sums=('b0749db18a9e78cf771737954a838cdcdb1d5415888bac1ba9caf8cba77c656c'
            'SKIP')
validpgpkeys=('6C44DB3E0BF3EAF5B433239A5FEE05E6A56E15A3') # Hubert Figuiere

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr # --disable-static
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
