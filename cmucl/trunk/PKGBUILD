# Contributor: John Proctor <jproctor@prium.net>
# Contributor: Alain Kalker <a.c.kalker@gmail.com>
# Maintainer: Juergen Hoetzel <juergen@archlinux.org>

pkgname=cmucl
pkgver=21a
pkgrel=1
pkgdesc="CMU Common Lisp"
depends_i686=('glibc' 'lesstif')
depends_x86_64=('lib32-glibc' 'lesstif')
provides=('common-lisp')
makedepends=('sed' 'bc')
license=('custom')
#license PublicDomain
source=("https://common-lisp.net/project/cmucl/downloads/release/${pkgver}/cmucl-src-${pkgver}.tar.bz2"
        "https://common-lisp.net/project/cmucl/downloads/release/${pkgver}/cmucl-${pkgver}-x86-linux.tar.bz2")
sha256sums=('41604a4f828a134dbf8a58623f45bd81b76ae05fc5c4cea5ccb74edfdc9e3167'
            '02d00c025723e7a2fa7a44e43d3f27ec39c9b871ea0332e472a439ada2620d71')

url="https://www.cons.org/cmucl/"
arch=('i686')

prepare() {
  cd "${srcdir}"

  if [[ $CARCH == "i686" ]]; then
    sed -i 's|i386\*|i686\*|' bin/build-all.sh
  fi
}

build() {
  cd "${srcdir}"
  export CMUCLLIB="${srcdir}"/lib/cmucl/lib
  # Build using binary-dist lisp
  ./bin/build-all.sh -o "${srcdir}"/bin/lisp
}

package() {
  cd "${srcdir}"
  # install distribution
  ./bin/make-dist.sh -I "${pkgdir}"/usr -M share/man/man1 linux-4
  # move docs
  mv "${pkgdir}"/usr/doc "${pkgdir}"/usr/share/
  # backwards compatibility
  ln -sf lisp "${pkgdir}"/usr/bin/cmucl
  # license
  install -D -m644 "${srcdir}"/src/general-info/COPYRIGHTS \
                   "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

