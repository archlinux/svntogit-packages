# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-bovo
pkgver=4.12.4
pkgrel=1
pkgdesc="A Gomoku like game for two players"
url="http://kde.org/applications/games/bovo/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'libkdegames')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/bovo-${pkgver}.tar.xz")
sha1sums=('e73ffabfacb8ab1d5d9f36d4e7d15009c5cd22d7')

build() {
  mkdir build
  cd build
  cmake ../bovo-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
