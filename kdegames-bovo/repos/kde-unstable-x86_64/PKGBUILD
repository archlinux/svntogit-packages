# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-bovo
pkgver=4.11.90
pkgrel=1
pkgdesc="A Gomoku like game for two players"
url="http://kde.org/applications/games/bovo/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'libkdegames')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/unstable/${pkgver}/src/bovo-${pkgver}.tar.xz")
sha1sums=('a3a7a2c1f82a74acd050db207c41d74b3903965d')

build() {
  mkdir build
  cd build
  cmake ../bovo-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
