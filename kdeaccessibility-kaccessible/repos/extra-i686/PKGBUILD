# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeaccessibility-kaccessible
pkgver=17.08.0
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc='Provides accessibility services like focus tracking and a screenreader'
url='https://accessibility.kde.org/'
license=('GPL' 'FDL')
groups=('kde-applications' 'kdeaccessibility')
depends=('kdelibs' 'speech-dispatcher')
makedepends=('cmake' 'automoc4')
source=("https://download.kde.org/stable/applications/${pkgver}/src/kaccessible-${pkgver}.tar.xz"{,.sig})
sha256sums=('c50dffedea80046b9458bd19934eecd3667f68fa8456aaf6e2e7ae527b8ed476'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cd "${srcdir}"
  mkdir -p build
  cd build
  cmake ../kaccessible-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $srcdir/build
  make DESTDIR=$pkgdir install
}
