# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor:  Ionut Biru <ibiru@archlinux.org>
# Contributor: Kessia 'even' Pinheiro <kessiapinheiro at gmail.com
# Contributor: Bjorn Lindeijer <bjorn lindeijer nl>

pkgname=telepathy-glib
pkgver=0.24.1
pkgrel=2
pkgdesc="GLib bindings for the Telepathy D-Bus protocol"
arch=(x86_64)
url="http://telepathy.freedesktop.org"
license=(LGPL2.1)
depends=(dbus-glib)
makedepends=(libxslt vala gobject-introspection python2)
source=(https://telepathy.freedesktop.org/releases/$pkgname/$pkgname-$pkgver.tar.gz{,.asc})
sha256sums=('9e0df1d8f857e0270cf0b32e2d1ca5a24aa7282873361785d573f72ad7f7d5eb'
            'SKIP')
validpgpkeys=('1B5A1BA811AAF0F12169E28AAC558671711E31B1')  # Guillaume Desmottes

build() {
  cd $pkgname-$pkgver
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr \
      --libexecdir=/usr/lib/telepathy \
      --enable-vala-bindings \
      --enable-static=no
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
