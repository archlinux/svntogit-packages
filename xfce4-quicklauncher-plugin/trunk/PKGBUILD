# $Id$
# Maintainer: 
# Contributor: Tobias Kieslich <tobias (at) archlinux.org>

pkgname=xfce4-quicklauncher-plugin
pkgver=1.9.4
pkgrel=4
pkgdesc="plugin that creates 4 little application launcher in the Xfce4 panel"
arch=('i686' 'x86_64')
license=('GPL2')
url="http://xfce-goodies.berlios.de/"
groups=('xfce4-goodies')
depends=('xfce4-panel')
makedepends=('pkgconfig' 'perlxml')
options=('!libtool')
source=(http://goodies.xfce.org/releases/${pkgname}/${pkgname}-${pkgver}.tar.bz2
	xfce4-quicklauncher-plugin-1.9.4-desktop-file.patch
	xfce4-quicklauncher-plugin-1.9.4-fix-missing-english-translation.patch
	xfce4-quicklauncher-plugin-1.9.4-fix-multiscreen.patch
	xfce4-quicklauncher-plugin-1.9.4-save-settings.patch
	xfce4-quicklauncher-plugin-1.9.4-xfce4-settings-manager.patch)
md5sums=('299e17f196ecfa5fb018cf65abb19b56'
         'a7826c9f8199a2f1e914fd39b7f9e2bf'
         '4ea4d06ab7284e78ddc4d60304f02cdf'
         '5e8126c05def1211fb4a2a65f2812a1b'
         '474237b205035214df1c723407251ade'
         '9e7f789129b08c787978c7a72a55fae7')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  # Fedora patches
  patch -Np0 -i ${srcdir}/xfce4-quicklauncher-plugin-1.9.4-desktop-file.patch || return 1
  patch -Np0 -i ${srcdir}/xfce4-quicklauncher-plugin-1.9.4-fix-multiscreen.patch || return 1
  patch -Np1 -i ${srcdir}/xfce4-quicklauncher-plugin-1.9.4-save-settings.patch || return 1
  patch -Np1 -i ${srcdir}/xfce4-quicklauncher-plugin-1.9.4-fix-missing-english-translation.patch || return 1
  patch -Np0 -i ${srcdir}/xfce4-quicklauncher-plugin-1.9.4-xfce4-settings-manager.patch || return 1
  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib/xfce4 \
    --localstatedir=/var --disable-static
  make || return 1
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install || return 1
}
