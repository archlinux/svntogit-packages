# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Kevin Piche <kevin@archlinux.org>

pkgname=perl-net-ip
_realname=Net-IP
pkgver=1.26
pkgrel=11
pkgdesc="Perl Module:  Easy manipulation of IPv4 and IPv6 addresses"
arch=('any')
license=('PerlArtistic')
url="https://search.cpan.org/dist/Net-IP/"
depends=('perl')
options=(!emptydirs)
source=("https://www.cpan.org/authors/id/M/MA/MANU/Net-IP-$pkgver.tar.gz")
sha256sums=('040f16f3066647d761b724a3b70754d28cbd1e6fe5ea01c63ed1cd857117d639')

build() {
  cd Net-IP-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Net-IP-$pkgver
  make test
}

package() {
  cd Net-IP-$pkgver
  make install DESTDIR="$pkgdir"
}
