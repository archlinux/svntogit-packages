# $Id$
# Maintainer: tobias <tobias@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

# ardour, liblrdf and raptor are heavily dependent on each other. Updating of
# one package mostly will require an update of all the other packages. I vote
# for fixed dependencies speaking in terms of versions

pkgname=ardour
pkgver=2.8.7
pkgrel=1
pkgdesc="A multichannel hard disk recorder and digital audio workstation"
arch=('i686' 'x86_64')
url="http://ardour.org"
license=('GPL2')
depends=('liblrdf' 'libgnomecanvasmm' 'liblo' 'aubio' 'libusb')
makedepends=('scons' 'boost' 'pkgconfig' 'soundtouch')
install=${pkgname}.install
source=(ftp://ftp.archlinux.org/other/ardour/${pkgname}-${pkgver}.tar.bz2
        ${pkgname}.desktop)
md5sums=('77fbcf4d472c9567da367274bdc6b53d'
         '8aeaf433ebf781733db48e5a16b0c4da')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i '/-O3/d' SConstruct || return 1

  scons $MAKEFLAGS ARCH="$CFLAGS" \
        PREFIX="/usr" \
        NLS=1 \
        FREEDESKTOP=0 \
        SYSLIBS=0 \
        VST=0 \
        WIIMOTE=0 \
        LV2=0 \
        FREESOUND=1 \
        LIBLO=1 \
        AUBIO=1 \
        TRANZPORT=1 \
        DIST_LIBDIR=lib \
        DESTDIR="${pkgdir}" || return 1

  scons PREFIX="/usr" \
        NLS=1 \
        FREEDESKTOP=0 \
        SYSLIBS=0 \
        VST=0 \
        WIIMOTE=0 \
        LV2=0 \
        FREESOUND=1 \
        LIBLO=1 \
        AUBIO=1 \
        TRANZPORT=1 \
        DIST_LIBDIR=lib \
        DESTDIR="${pkgdir}" install || return 1

  # install some freedesktop.org compatibility
  install -Dm644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop" || return 1
}
