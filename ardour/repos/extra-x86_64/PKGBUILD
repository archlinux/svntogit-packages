# $Id$
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: SpepS <dreamspepser at yahoo dot it>
# Contributor: see .contrib

pkgname=ardour
_pkgname=Ardour3
pkgver=3.5.380
pkgrel=1
pkgdesc="Professional-grade digital audio workstation"
arch=('i686' 'x86_64')
url="http://ardour.org/"
license=('GPL')
depends=('liblrdf' 'liblo>=0.28' 'libsmf' 'lilv'
         'aubio' 'libgnomecanvasmm' 'suil')
makedepends=('python2' 'boost' 'cppunit' 'doxygen' 'graphviz')
optdepends=('xjadeo: video monitoring')
install=$pkgname.install
source=("ftp://ftp.archlinux.org/other/$pkgname/$_pkgname-$pkgver.tar.bz2"
        "$pkgname.desktop")
md5sums=('eeed6ed0a22ecf3f1179318ac9594616'
         '316bd21ece5e2208a104fd09cac521af')

prepare() {
  cd "$srcdir/$_pkgname-$pkgver"

  # python2 fix
  sed -i 's/python ${SRC}/python2 ${SRC}/' libs/pbd/wscript
}

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  python2 waf configure --prefix=/usr \
                        --configdir=/etc \
                        --docs

  python2 waf build $MAKEFLAGS
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"

  python2 waf install --destdir="$pkgdir"

  # install some freedesktop.org compatibility
  install -Dm644 "$srcdir/$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
}

# vim:set ts=2 sw=2 et:
