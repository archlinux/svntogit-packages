# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>

pkgname=source-highlight
pkgver=3.1.5
pkgrel=1
pkgdesc="Convert source code to syntax highlighted document"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/src-highlite/"
license=('GPL')
depends=('gcc-libs' 'bash' 'boost-libs')
makedepends=('ctags' 'boost')
options=('!libtool')
install=$pkgname.install
source=("ftp://ftp.gnu.org/gnu/src-highlite/${pkgname}-${pkgver}.tar.gz")
md5sums=('3117ee47c1c6b0668667e20d2aca3757')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr \
              --sysconfdir=/etc \
	      --with-bash-completion=${pkgdir}/etc/bash_completion.d
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make prefix="${pkgdir}/usr" install
}
