# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=w3m
_gitcommit=d69111c10189628f02903e076238d7bd390d6a6a
pkgver=0.5.3.git20171120
pkgrel=1
pkgdesc='Text-based Web browser as well as pager'
url='https://anonscm.debian.org/git/collab-maint/w3m.git/'
license=('custom')
arch=('x86_64')
makedepends=('git' 'imlib2')
optdepends=('imlib2: for graphics support')
depends=('openssl' 'gc' 'ncurses' 'gpm')
source=("git://anonscm.debian.org/collab-maint/w3m.git#commit=${_gitcommit}")
sha256sums=('SKIP')

build() {
	cd "${srcdir}/${pkgname}"
	./configure \
		--prefix=/usr \
		--libexecdir=/usr/lib \
		--enable-image=x11,fb \
		--with-imagelib=imlib2 \
		--with-termlib=ncurses \
		--disable-w3mmailer \

	make
}

package() {
	cd "${srcdir}/${pkgname}"
	make DESTDIR="${pkgdir}" install

	install -d "${pkgdir}"/usr/share/{doc,licenses}/w3m
	install -m644 doc/* "${pkgdir}/usr/share/doc/w3m"
	ln -s ../../doc/w3m/README "${pkgdir}/usr/share/licenses/w3m"
}
