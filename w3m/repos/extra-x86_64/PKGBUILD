# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=w3m
_gitcommit=6376b90cdf93c367f39d1f74e3689367a4ca141b
_pkgver=0.5.3.git20210102-6
pkgver=${_pkgver/-/_}
pkgrel=1
pkgdesc='Text-based Web browser as well as pager'
url='https://salsa.debian.org/debian/w3m'
license=('custom')
arch=('x86_64')
makedepends=('git' 'imlib2')
optdepends=('imlib2: for graphics support')
depends=('openssl' 'gc' 'ncurses' 'gpm')
source=("git+https://salsa.debian.org/debian/w3m.git#commit=${_gitcommit}")
sha256sums=('SKIP')

# There's also the maintainer's github repo, usually in sync with Debian's:
# https://github.com/tats/w3m

build() {
	cd "${srcdir}/${pkgname}"
	./configure \
		--prefix=/usr \
		--libexecdir=/usr/lib \
		--enable-image=x11,fb \
		--with-imagelib=imlib2 \
		--with-termlib=ncurses \
		--disable-w3mmailer \

	make
}

package() {
	cd "${srcdir}/${pkgname}"
	make DESTDIR="${pkgdir}" install

	install -d "${pkgdir}"/usr/share/{doc,licenses}/w3m
	install -m644 doc/* "${pkgdir}/usr/share/doc/w3m"
	ln -s ../../doc/w3m/README "${pkgdir}/usr/share/licenses/w3m"
}
