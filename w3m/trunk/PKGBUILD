# $Id$
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=w3m
pkgver=0.5.3
pkgrel=1
pkgdesc='Text-based Web browser, as well as pager'
arch=('i686' 'x86_64')
url='http://w3m.sourceforge.net/'
license=('custom')
depends=('openssl' 'gc' 'ncurses' 'zlib')
makedepends=('imlib2')
optdepends=('imlib2: for graphics support') 
source=("http://downloads.sourceforge.net/sourceforge/w3m/$pkgname-$pkgver.tar.gz")
sha1sums=('444b6c8cf7094ee95f8e9de96b37f814b9d83237')

build() {
	cd "$srcdir/$pkgname-$pkgver"
	./configure \
		--prefix=/usr \
		--libexecdir=/usr/lib \
		--enable-image=x11,fb \
		--with-imagelib=imlib2 \
		--with-termlib=ncurses \
		--disable-w3mmailer \
		--disable-mouse
	make
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install

	install -d "$pkgdir"/usr/share/{doc,licenses}/"$pkgname"
	find doc/* | grep -v CVS | xargs -i install "{}" "$pkgdir/usr/share/doc/$pkgname/"
	ln -s "../../doc/$pkgname/README" "$pkgdir/usr/share/licenses/$pkgname/"
}