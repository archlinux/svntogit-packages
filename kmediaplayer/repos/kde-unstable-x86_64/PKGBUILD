# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kmediaplayer
pkgver=r5.2997c79
pkgrel=1
pkgdesc='KMediaPlayer'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kmediaplayer'
license=('LGPL')
depends=('kparts')
makedepends=('extra-cmake-modules' 'git')
groups=('kf5')
source=('git://anongit.kde.org/kmediaplayer.git')
md5sums=('SKIP')

pkgver() {
  cd kmediaplayer
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kmediaplayer \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
