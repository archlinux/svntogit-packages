# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: riai <riai@bigfoot.com>, Ben <ben@benmazer.net>

pkgname=eric
pkgver=4.4.9
pkgrel=2
pkgdesc="A full-featured Python and Ruby IDE in PyQt4"
arch=('any')
url="http://eric-ide.python-projects.org/index.html"
license=('GPL3')
depends=('python2-qt' 'python2-qscintilla')
conflicts=('eric4')
replaces=('eric4')
provides=('eric4')
install=eric.install
changelog=eric.changelog
source=(http://downloads.sourceforge.net/eric-ide/${pkgname}4-${pkgver}.tar.gz
        'eric.desktop')
md5sums=('e8c9451850c70f4009785cc0d83adc25'
         '77c494bb839c5744869d34355ea69095')

package() {
	cd ${srcdir}/${pkgname}4-${pkgver}

	# our pyxml is patched
	# not version-depending because not critical
	sed -i 's/v < (0, 8, 6)/v < (0, 8, 4)/' install.py

	python2 install.py -c -b /usr/bin -i ${pkgdir}

	install -D -m644 ${srcdir}/eric.desktop \
		${pkgdir}/usr/share/applications/eric.desktop
	install -D -m644 eric/icons/default/eric.png \
		${pkgdir}/usr/share/pixmaps/eric.png

	# Creating version independant symlinks
	ln -s /usr/bin/eric4 ${pkgdir}/usr/bin/eric
	ln -s /usr/bin/eric4-api ${pkgdir}/usr/bin/eric-api
	ln -s /usr/bin/eric4-compare ${pkgdir}/usr/bin/eric-compare
	ln -s /usr/bin/eric4-configure \
		${pkgdir}/usr/bin/eric-configure
	ln -s /usr/bin/eric4-diff ${pkgdir}/usr/bin/eric-diff
	ln -s /usr/bin/eric4-doc ${pkgdir}/usr/bin/eric-doc
	ln -s /usr/bin/eric4-editor ${pkgdir}/usr/bin/eric-editor
	ln -s /usr/bin/eric4-helpviewer \
		${pkgdir}/usr/bin/eric-helpviewer
	ln -s /usr/bin/eric4-plugininstall \
		${pkgdir}/usr/bin/eric-plugininstall
	ln -s /usr/bin/eric4-pluginrepository \
		${pkgdir}/usr/bin/eric-pluginrepository
	ln -s /usr/bin/eric4-pluginuninstall \
		${pkgdir}/usr/bin/eric-pluginuninstall
	ln -s /usr/bin/eric4-qregexp ${pkgdir}/usr/bin/eric-qregexp
	ln -s /usr/bin/eric4-re ${pkgdir}/usr/bin/eric-re
	ln -s /usr/bin/eric4-tray ${pkgdir}/usr/bin/eric-tray
	ln -s /usr/bin/eric4-trpreviewer \
		${pkgdir}/usr/bin/eric-trpreviewer
	ln -s /usr/bin/eric4-uipreviewer \
		${pkgdir}/usr/bin/eric-uipreviewer
	ln -s /usr/bin/eric4-unittest \
		${pkgdir}/usr/bin/eric-unittest
}

# vim:set ts=2 sw=2 et:
