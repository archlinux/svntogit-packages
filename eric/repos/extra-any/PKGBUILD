# $Id$
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: riai <riai@bigfoot.com>, Ben <ben@benmazer.net>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Felix Rohrbach <fxrh@gmx.de>

pkgname=eric
pkgver=5.1.6
pkgrel=1
pkgdesc="A full-featured Python 3.x and Ruby IDE in PyQt4"
arch=('any')
url="http://eric-ide.python-projects.org/"
license=('GPL3')
depends=('python-qt' 'python-qscintilla')
conflicts=('eric5')
provides=('eric5')
replaces=('eric5')
source=("http://downloads.sourceforge.net/eric-ide/${pkgname}5-$pkgver.tar.gz"
        "$pkgname.desktop")
md5sums=('aae03cadf252c2556429c0c4cc17acd8'
         'eb0ee7ce0b5c3ff815a9c0145f7ae536')

package() {
  cd "$srcdir/${pkgname}5-$pkgver"

  python install.py -c -b /usr/bin -i "$pkgdir"

  # freedesktop.org compatibility
  install -Dm644 "$srcdir/$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"

  rm "$pkgdir/usr/share/applications/${pkgname}5.desktop"

  # Remove files conflicting with eric4
  # what are we doing here?
  # TODO: tell upstream to fix their stuff
  #   and allow installation of eric4 + eric5
  #   OR package these files separately eg. 'eric-common'
  rm "$pkgdir/usr/share/qt/qsci/api/python/zope-2.10.7.api"
  rm "$pkgdir/usr/share/qt/qsci/api/python/zope-2.11.2.api"
  rm "$pkgdir/usr/share/qt/qsci/api/python/zope-3.3.1.api"
  rm "$pkgdir/usr/share/qt/qsci/api/ruby/Ruby-1.8.7.api"

  # some canonical symlinks
  cd "$pkgdir/usr/bin"

  for i in *; do
    ln -s /usr/bin/$i ${i/eric5/eric}
  done
}

# vim:set ts=2 sw=2 et:
