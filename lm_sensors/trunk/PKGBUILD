# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=lm_sensors
pkgver=3.1.2
pkgrel=3
pkgdesc="Collection of user space tools for general SMBus access and hardware monitoring"
arch=('i686' 'x86_64')
url="http://www.lm-sensors.org/"
license=('GPL')
depends=('perl' 'sysfsutils' 'rrdtool')
backup=('etc/sensors3.conf' 'etc/conf.d/healthd' 'etc/conf.d/sensord')
options=('!emptydirs')
install=sensors.install
source=(http://dl.lm-sensors.org/lm-sensors/releases/lm_sensors-${pkgver}.tar.bz2 \
	sensors.rc fancontrol.rc sensors-detect.patch healthd healthd.conf healthd.rc \
        sensord.conf sensord.rc daemonarg.patch)
md5sums=('58a9a225808ac4587c4c8cbd12b40b5c' 'c370f5e620bfe41113354a1e22c0c18c'\
         '232bedf043dd5dedde82df1a399c682c' '47c40b381d1f25d6634ae84cecf35f33'\
         '6549050897c237514aeaa2bb6cfd29ea' 'f649261f52bd4329347bf93f5f83cb0a'\
         '970408d2e509dc4138927020efefe323' '96a8dd468e81d455ec9b165bdf33e0b7'\
         '41a5c20854bbff00ea7174bd2276b736' '9e42cc9ccab264b467ed4c843c27d4a3')
sha1sums=('4a3b13e4f9c18c29e7f853739bdd88ca7799fcc7' 'b2e664b9b87759991f02d0a1e8cac5e95098c0a5'\
         'a068ac0a3115a6191a487e11422506baa922b40a' '47095a32a918d6be50bd8daa8aaa9c24940d60e9'\
         '78b5cd36c3cb8e98b972cdd8c4a12687d79a79a8' 'c6ddfebc20685ba69700f66038c6b00a7c0bdb80'\
         'e662881f5d3f3f35a1bc97ba45d2c471dd28c37f' 'de8d4d65406815c389f8a04e2a8508a1ae6749c8'\
         '72a60251d1d55a67307dab4105d9f3f01a080af4' '54d5f74b5f9dce5205daae702d6c12b34f7305e5')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 < ../sensors-detect.patch || return 1

  make PROG_EXTRA=sensord BUILD_STATIC_LIB=0 \
    PREFIX=/usr MANDIR=/usr/share/man DESTDIR="${pkgdir}" install || return 1

  install -D -m755 "${srcdir}/sensors.rc" "${pkgdir}/etc/rc.d/sensors" || return 1
  install -D -m755 "${srcdir}/fancontrol.rc" "${pkgdir}/etc/rc.d/fancontrol" || return 1
  install -D -m755 "${srcdir}/healthd" "${pkgdir}/usr/sbin/healthd" || return 1
  install -D -m755 "${srcdir}/healthd.rc" "${pkgdir}/etc/rc.d/healthd" || return 1
  install -D -m644 "${srcdir}/healthd.conf" "${pkgdir}/etc/conf.d/healthd" || return 1
  install -D -m755 "${srcdir}/sensord.rc" "${pkgdir}/etc/rc.d/sensord" || return 1
  install -D -m644 "${srcdir}/sensord.conf" "${pkgdir}/etc/conf.d/sensord" || return 1

  cd ${pkgdir}
  patch -p1 < ${srcdir}/daemonarg.patch || return 1
}
