# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Alessio 'mOLOk' Bolognino <themolok@gmail.com>
# Contributor: daniel g. siegel <dgsiegel@gmail.com>

pkgname=libsmbios
pkgver=2.2.28
pkgrel=4
pkgdesc="A library for providing access to as much BIOS information as possible"
arch=('i686' 'x86_64')
url="http://linux.dell.com/libsmbios/main/index.html"
license=('GPL' 'custom')
depends=('gcc-libs')
makedepends=('libxml2' 'python2' 'chrpath')
optdepends=('python2: tools')
source=(http://linux.dell.com/libsmbios/download/${pkgname}/${pkgname}-${pkgver}/${pkgname}-${pkgver}.tar.bz2)
md5sums=('4e167e8d108e287defc3c461f50ef159')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  mkdir build
  cd build
  PYTHON=python2 ../configure --prefix=/usr --sbindir=/usr/bin --sysconfdir=/etc --disable-static
  mkdir -p out/libsmbios_c
  mkdir -p out/libsmbios_c++
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/build"
  make DESTDIR="${pkgdir}" install

  chrpath -d "${pkgdir}/usr/bin/smbios-sys-info-lite"

  cp -a ../src/include/* "${pkgdir}/usr/include/"
  cp -a out/public-include/* "${pkgdir}/usr/include/"

  rm -rf "${pkgdir}/etc/yum"
  rm -rf "${pkgdir}/usr/lib/yum-plugins"

  sed -e 's|#!/usr/bin/python|#!/usr/bin/python2|g' \
      -i "${pkgdir}"/usr/bin/smbios-{{wakeup,token,wireless}-ctl,lcd-brightness,rbu-bios-update,passwd,sys-info}

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 ../COPYING-OSL "${pkgdir}/usr/share/licenses/${pkgname}/"

  python2 -m compileall "${pkgdir}/usr/lib/python2.7/site-packages/libsmbios_c/"
}
