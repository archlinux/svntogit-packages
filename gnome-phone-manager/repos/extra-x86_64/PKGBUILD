# $Id$
# Maintainer: Roman Kyrylych <roman@archlinux.org>

pkgname=gnome-phone-manager
pkgver=0.68
pkgrel=1
pkgdesc="Control your mobile phone from your GNOME desktop."
arch=('i686' 'x86_64')
url="http://live.gnome.org/PhoneManager"
license=('GPL')
depends=('evolution-data-server' 'gnokii' 'gnome-bluetooth' 'gnome-icon-theme' 'libcanberra')
makedepends=('intltool' 'gnome-common')
options=('!emptydirs' '!libtool')
install=gnome-phone-manager.install
source=(http://ftp.gnome.org/pub/GNOME/sources/gnome-phone-manager/${pkgver}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('3777b62ee36492274532ece965f4688600457014f17a6f21dd01fb63bbb77f8f')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 install

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gnome-phone-manager ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
