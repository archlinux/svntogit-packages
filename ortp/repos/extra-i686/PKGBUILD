# $Id$
# Maintainer:
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Jaroslaw Swierczynski <swiergot@aur.archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=ortp
pkgver=0.23.0
pkgrel=1
pkgdesc="A Real-time Transport Protocol (RTP) library"
arch=('i686' 'x86_64')
url="http://www.linphone.org/index.php/eng/code_review/ortp"
license=('GPL3')
depends=('libzrtpcpp' 'libsrtp')
source=("http://download-mirror.savannah.gnu.org/releases/linphone/ortp/ortp-$pkgver.tar.gz"{,.sig})
sha1sums=('3f4712307ceba27c6498abd2090f411e02084dbd'
          'SKIP')
sha1sums=('87a154295636652cc9ac7310dc02e0258db08790'
          'SKIP')

prepare() {
  cd ${pkgname}-${pkgver}

  # Reference: http://lists.gnu.org/archive/html/linphone-users/2014-02/msg00008.html
  sed -i 's|libzrtpcppcore|libzrtpcpp|g' configure{,.ac}
}

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr \
    --enable-zrtp \
    --disable-strict
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
