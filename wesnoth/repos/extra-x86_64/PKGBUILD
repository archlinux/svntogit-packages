# $Id$
# Contributor: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Jacobo Arvelo <unix4all@ya.com>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>

pkgname=wesnoth
pkgver=1.8.2
pkgrel=2
pkgdesc="A turn-based strategy game on a fantasy world"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.wesnoth.org/"
depends=('sdl_ttf' 'sdl_net' 'sdl_mixer' 'sdl_image' 'libvorbis' 'fribidi' 'freetype2' 'gcc-libs' 'boost-libs' 'pango' 'lua')
makedepends=('boost')
install=wesnoth.install
source=(http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.bz2
        wesnoth-libpng-1.4.0.patch)
md5sums=('5dd150057da9f5048826f16452ab0346'
         'ca7412ffc4682ef55ddf82d56b0e2f08')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -i ../wesnoth-libpng-1.4.0.patch || return 1
  ./autogen.sh || return 1
  ./configure --prefix=/usr --localstatedir=/var --enable-tools --enable-editor --enable-server
  make || return 1
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
