# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kwindowsystem
pkgver=5.6.0
pkgrel=3
pkgdesc='Access to the windowing system'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kwindowsystem'
license=('LGPL')
depends=('qt5-x11extras' 'libxfixes')
makedepends=('extra-cmake-modules' 'qt5-tools')
groups=('kf5')
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"
        'kdebug-340348.patch' 'kdebug-337626.patch')
md5sums=('bcfa5cd27b2735ffb26379b24b437190'
         '464d5aefb97a2bcb3395d2a6239f670a'
         'e8c9ffc08782f99cdb88a706c4496f29')

prepare() {
  mkdir build

  cd ${pkgname}-${pkgver}
  patch -p1 -i "${srcdir}"/kdebug-340348.patch
  patch -p1 -i "${srcdir}"/kdebug-337626.patch
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
