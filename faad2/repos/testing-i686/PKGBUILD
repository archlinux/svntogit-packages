# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Sarah Hay <sarah@archlinux.org>

pkgname=faad2
pkgver=2.6.1
pkgrel=1
pkgdesc="ISO AAC audio decoder"
arch=('i686' 'x86_64')
url="http://www.audiocoding.com/"
license=('custom:GPL')
depends=('glibc')
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/faac/${pkgname}-${pkgver}.tar.gz 
        faad2-2.6.1-libtool22.patch
        faad2-2.6.1-broken-pipe.patch
        faad2-2.6.1-implicitdeclar.patch
	faad2-2.6.1-abi_has_changed.patch
	LICENSE)
md5sums=('74e92df40c270f216a8305fc87603c8a' 'ca9515b6617d7fd060d0d172616eb192'\
         'bb6a345dd2e6c4df6609e6a374134066' 'bc43fd3f8cd582a8bc092b9b5e611865'\
         '06e83633c609d20436794b62a382b3dc' 'b6a1c72534807b8a038b71e784f57e32')
sha1sums=('b4ad33d3c4dfa6dbf3011a3da34c631926cabfad'
          'ca4ee04aa1757b8f9deeef52b5bf35dc823da844'
          '0b03728f40d00e552f738900e2f6215177af5417'
          'd4ab5d38c9b75ed6417529c4d1a7ca204293c224'
          'f7e9d89fc551e0b61a7b7555749ed7a3129a4ce8'
          '6225fd997671206f5222c4a86d9e092359552c43')

build() {
  cd ${srcdir}/${pkgname}
  patch -p1 < ../faad2-2.6.1-libtool22.patch || return 1
  patch -p1 < ../faad2-2.6.1-broken-pipe.patch || return 1
  patch -p1 < ../faad2-2.6.1-implicitdeclar.patch || return 1
  patch -p1 < ../faad2-2.6.1-abi_has_changed.patch || return 1

  autoreconf -vif || return 1
  ./configure --prefix=/usr || return 1
  make || return 1
  make DESTDIR=${pkgdir} install || return 1
  install -m644 ${srcdir}/${pkgname}/common/mp4ff/mp4ff.h ${pkgdir}/usr/include/ || return 1
  install -m644 ${srcdir}/${pkgname}/common/mp4ff/mp4ff_int_types.h ${pkgdir}/usr/include/ || return 1
  install -m644 ${srcdir}/${pkgname}/common/mp4ff/.libs/*.a ${pkgdir}/usr/lib/ || return 1
  install -D -m644 ${srcdir}/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE || return 1
}
