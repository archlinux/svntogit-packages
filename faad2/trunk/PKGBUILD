# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=faad2
pkgver=2.7
pkgrel=4
pkgdesc="ISO AAC audio decoder"
arch=('i686' 'x86_64')
url="http://www.audiocoding.com/"
license=('GPL')
depends=('glibc')
options=('!makeflags')
source=(http://downloads.sourceforge.net/sourceforge/faac/${pkgname}-${pkgver}.tar.bz2
        faad2-2.7-libmp4ff-install-mp4ff_int_types_h.patch
	faad2-2.7-libmp4ff-shared-lib.patch
	faad2-2.7-man1_MANS.patch)
sha1sums=('b0e80481d27ae9faf9e46c8c8dfb617a0adb91b5'
          '1cf28cfd32dbae6cc484b149fd6adfea33506544'
          '247658aa795cd6d44a153fae8c5cb62e427f3ecf'
          '4b548a08f7611a5f4f395c7c1d4cef5e37368af2')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i "${srcdir}/faad2-2.7-libmp4ff-shared-lib.patch"
  patch -p0 -i "${srcdir}/faad2-2.7-libmp4ff-install-mp4ff_int_types_h.patch"
  patch -p1 -i "${srcdir}/faad2-2.7-man1_MANS.patch"
  autoreconf --force --install
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make CFLAGS="${CFLAGS} -fPIC"
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
