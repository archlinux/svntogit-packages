# Maintainer: RÃ©my Oudompheng <remy@archlinux.org>

pkgname='perl-perlio-utf8-strict'
_dist=PerlIO-utf8_strict
pkgver=0.008
pkgrel=1
pkgdesc="Fast and correct UTF-8 PerlIO"
arch=('x86_64')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl')
makedepends=('perl-test-exception')
url="https://metacpan.org/release/${_dist}"
source=(https://search.cpan.org/CPAN/authors/id/L/LE/LEONT/${_dist}-${pkgver}.tar.gz)
md5sums=('0e2a940e1759c4d322958ef6ebc2e52f')
sha256sums=('5f798ded50dcc7d421b57850f837310666d817f4c67c15ba0f5a1d38c74df459')

build() {
    cd "${srcdir}/${_dist}-${pkgver}"
    unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
    export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
    perl Makefile.PL
    make
}

check() {
    cd "${srcdir}/${_dist}-${pkgver}"
    unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
    export PERL_MM_USE_DEFAULT=1
    make test
}

package() {
    cd "${srcdir}/${_dist}-${pkgver}"
    unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
    make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
}
