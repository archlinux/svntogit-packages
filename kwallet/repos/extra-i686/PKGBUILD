# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kwallet
pkgver=5.8.0
pkgrel=1
pkgdesc='Secure and unified container for user passwords'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kwallet'
license=('LGPL')
depends=('knotifications') # qgpgme
makedepends=('extra-cmake-modules' 'python')
groups=('kf5')
conflicts=('kwalletframeworks')
replaces=('kwalletframeworks')
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
md5sums=('3c5803acbbe295726017b80d7b5d1d49')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
