# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kwallet
pkgver=5.15.0
pkgrel=1
pkgdesc='Secure and unified container for user passwords'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kwallet'
license=('LGPL')
depends=('knotifications' 'kiconthemes' 'kservice' 'gpgmepp')
makedepends=('extra-cmake-modules' 'python' 'kdoctools' 'boost')
optdepends=('kwalletmanager: Configuration GUI')
groups=('kf5')
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
md5sums=('291d29f8b9333abd9ea2d240d3eab1d9')

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
