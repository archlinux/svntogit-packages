# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>

pkgname=stellarium
pkgver=0.18.2
pkgrel=3
pkgdesc="A stellarium with great graphics and a nice database of sky-objects"
arch=('x86_64')
url="http://stellarium.org/"
license=('GPL2')
depends=('libpng' 'libgl' 'freetype2' 'openssl' 'qt5-script'
	'qt5-serialport' 'qt5-multimedia' 'qt5-location' 'gpsd')
makedepends=('cmake' 'boost' 'mesa' 'mesa-libgl' 'qt5-tools')
source=(https://github.com/Stellarium/stellarium/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz{,.asc})
validpgpkeys=('79151C2E6351E7278DA1A730BF38D4D02A328DFF')  # Alexander Wolf <alex.v.wolf@gmail.com>
sha256sums=('7582a8ef96ab673a17a248b97bc9deb69b9d65f07e9689f1761c284e36b028db'
            'SKIP')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  cmake . -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=RELEASE
  make
}


package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
 
  make DESTDIR="${pkgdir}" install
}
