# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>

pkgname=stellarium
pkgver=0.20.2
pkgrel=1
pkgdesc="A stellarium with great graphics and a nice database of sky-objects"
arch=('x86_64')
url="https://stellarium.org/"
license=('GPL2')
depends=('libpng' 'libgl' 'freetype2' 'openssl' 'qt5-script'
	'qt5-serialport' 'qt5-multimedia' 'qt5-location' 'gpsd')
makedepends=('cmake' 'boost' 'mesa' 'qt5-tools')
source=(https://github.com/Stellarium/stellarium/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz{,.asc})
validpgpkeys=('79151C2E6351E7278DA1A730BF38D4D02A328DFF')  # Alexander Wolf <alex.v.wolf@gmail.com>
sha256sums=('400ef964c2a1612157a4b219464ea919b4e330322a0db894835b6e11c4af5e9b'
            'SKIP')

build() {
  cd ${pkgname}-${pkgver}

  cmake . -DCMAKE_INSTALL_PREFIX=/usr
  make
}


package() {
  cd ${pkgname}-${pkgver}
 
  make DESTDIR="${pkgdir}" install
}
