# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>

pkgname=stellarium
pkgver=0.11.1
pkgrel=1
pkgdesc="A stellarium with great graphics and a nice database of sky-objects"
arch=("i686" "x86_64")
url="http://stellarium.free.fr/"
license=('GPL2')
depends=('libpng' 'mesa' 'libgl' 'freetype2' 'qt' 'openssl')
makedepends=('cmake' 'boost')
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz
	stellarium.desktop
        stellarium.png)
sha1sums=('e721a9cef0c736354c477fdb25cce607a9a3b52e'
          'c9cc79212542238227b5bd6af99f60fe056f4ab2'
          'b890d3b6c4dcfcfcc696514202af11b2a50c0fea')
	
build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  cmake . -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=RELEASE
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
 
  make DESTDIR=${pkgdir} install

  # install desktop file & pixmap
  install -Dm644 ${srcdir}/stellarium.desktop \
	${pkgdir}/usr/share/applications/stellarium.desktop
  install -Dm644 ${srcdir}/stellarium.png \
	${pkgdir}/usr/share/pixmaps/stellarium.png
}
