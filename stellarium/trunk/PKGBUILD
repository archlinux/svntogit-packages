# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>

pkgname=stellarium
pkgver=0.10.5
pkgrel=1
pkgdesc="A stellarium with great graphics and a nice database of sky-objects"
arch=("i686" "x86_64")
url="http://stellarium.free.fr/"
license=('GPL2')
depends=('libpng' 'mesa' 'libgl' 'freetype2' 'qt' 'openssl')
makedepends=('cmake' 'boost')
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz
	stellarium.desktop
        stellarium.png)
md5sums=('4038585e3554febde92f2166d52bc8a8'
         'b4f9ebd082c4e112c2bbfac9f2c8465d'
         '134ab08e73f4f5d995a7931a7957b7a3')
	
build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  cmake . -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=RELEASE || return 1
  make || return 1
}
package() {
  cd ${srcdir}/${pkgname}-${pkgver}
 
  make DESTDIR=${pkgdir} install || return 1

  # install desktop file & pixmap
  install -Dm644 ${srcdir}/stellarium.desktop \
	${pkgdir}/usr/share/applications/stellarium.desktop || return 1
  install -Dm644 ${srcdir}/stellarium.png \
	${pkgdir}/usr/share/pixmaps/stellarium.png || return 1
}
