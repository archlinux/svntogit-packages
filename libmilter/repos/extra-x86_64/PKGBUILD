# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: mutantmonkey <mutantmonkey@gmail.com>

pkgname=libmilter
_pkgname=sendmail
pkgver=8.15.1
pkgrel=2
pkgdesc='Implementation of the sendmail Mail Filter API'
url='https://www.milter.org/developers/api/'
arch=('i686' 'x86_64')
options=('staticlibs')
license=('custom:Sendmail open source license')
source=("ftp://ftp.sendmail.org/pub/${_pkgname}/${_pkgname}.${pkgver}.tar.gz"
        'fd-passing-libmilter.patch')
sha1sums=('ab5a2e80927c52c3621240d6bc424fb4b4d65f99'
          '9d9ed55077dffd17aee2376fbce57ef77c0139c0')

prepare() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	patch -p1 -i ../fd-passing-libmilter.patch # FS#49421

	# From http://www.j-chkmail.org/wiki/doku.php/doc/installation/start#libmilter
	cat >> devtools/Site/site.config.m4 <<EOF
dnl Include our flags
APPENDDEF(\`conf_libmilter_ENVDEF',\`${CPPFLAGS} ${CFLAGS}')
dnl Enable libmilter with a pool of workers
APPENDDEF(\`conf_libmilter_ENVDEF',\`-D_FFR_WORKERS_POOL=1 -DMIN_WORKERS=4')
dnl Use poll instead of select
APPENDDEF(\`conf_libmilter_ENVDEF',\`-DSM_CONF_POLL=1')
dnl Enable IPv6
APPENDDEF(\`conf_libmilter_ENVDEF',\`-DNETINET6=1')
dnl Add -fPIC
APPENDDEF(\`conf_libmilter_ENVDEF',\`-fPIC')
EOF
}

build() {
	cd "${srcdir}/${_pkgname}-${pkgver}/${pkgname}"
	./Build
}

package() {
	cd "${srcdir}/${_pkgname}-${pkgver}/${pkgname}"
	install -d "${pkgdir}/usr/lib"
	./Build DESTDIR="${pkgdir}" install
	install -Dm644 ../LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	chown -R root:root "${pkgdir}"
}
