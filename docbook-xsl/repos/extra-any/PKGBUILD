# $Id$
# Maintainer:
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Sean Middleditch <elanthis@awesomeplay.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=docbook-xsl
pkgver=1.76.1
pkgrel=2
pkgdesc='XML stylesheets for Docbook-xml transformations'
arch=('any')
license=('custom')
url='http://docbook.sourceforge.net/'
depends=('libxml2' 'libxslt' 'docbook-xml')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/docbook/${pkgname}-${pkgver}.tar.bz2")
md5sums=('b5340507cb240cc7ce00632b9c40bff5')

package() {
	_pkgroot=${pkgdir}/usr/share/xml/docbook/xsl-stylesheets-${pkgver}

	cd ${srcdir}/${pkgname}-${pkgver}
	install -dm755 ${_pkgroot}/{common,params}
	install -m644 common/*.{xml,xsl,dtd,ent} ${_pkgroot}/common/
	install -m644 params/*.xml ${_pkgroot}/params/

	for fn in fo highlighting html roundtrip; do
		install -dm755 ${_pkgroot}/${fn}
		install -m644 ${fn}/*.{xml,xsl} ${_pkgroot}/${fn}/
	done

	for fn in eclipse htmlhelp javahelp lib manpages profiling template website xhtml{,-1_1}; do
		install -dm755 ${_pkgroot}/${fn}
		install -m644 ${fn}/*.xsl ${_pkgroot}/${fn}/
	done

	install -dm755 ${pkgdir}/etc/xml
	install -m644 VERSION ${_pkgroot}/
	install -Dm644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
