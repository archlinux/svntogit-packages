# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdebindings-smokegen
pkgver=4.11.4
pkgrel=1
pkgdesc="A general purpose C++ parser with a plugin infrastructure"
url="https://projects.kde.org/projects/kde/kdebindings/smoke/smokegen"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
conflicts=('kdebindings-smoke')
source=("http://download.kde.org/stable/${pkgver}/src/smokegen-${pkgver}.tar.xz")
sha1sums=('fe838306771119b460ac78badb09c455a8a8c3cd')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../smokegen-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
