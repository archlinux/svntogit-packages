# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdebindings-smokegen
pkgver=4.12.1
pkgrel=1
pkgdesc="A general purpose C++ parser with a plugin infrastructure"
url="https://projects.kde.org/projects/kde/kdebindings/smoke/smokegen"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
conflicts=('kdebindings-smoke')
source=("http://download.kde.org/stable/${pkgver}/src/smokegen-${pkgver}.tar.xz")
sha1sums=('a49111553d529e9606e2f3b4f589b551f25e87fd')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../smokegen-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
