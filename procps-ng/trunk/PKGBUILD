# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=procps-ng
pkgver=3.3.8
pkgrel=3
pkgdesc='Utilities for monitoring your system and its processes'
url='http://sourceforge.net/projects/procps-ng/'
license=('GPL' 'LGPL')
arch=('i686' 'x86_64')
depends=('ncurses')
source=("http://downloads.sourceforge.net/project/${pkgname}/Production/${pkgname}-${pkgver}.tar.xz")
sha1sums=('4fa6a373f969989a9e490df5eb49a2df62eca19a')

groups=('base')

conflicts=('procps')
provides=('procps')
replaces=('procps')

options=('!libtool')
install=install

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure \
		--prefix=/usr \
		--exec-prefix=/ \
		--sysconfdir=/etc \
		--libdir=/usr/lib \
		--bindir=/usr/bin \
		--sbindir=/usr/bin \
		--disable-static
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	# provided by util-linux
	rm "${pkgdir}/usr/bin/kill"
	rm "${pkgdir}/usr/share/man/man1/kill.1"
}
