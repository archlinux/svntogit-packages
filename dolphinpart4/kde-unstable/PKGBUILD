# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=dolphinpart4
pkgver=15.04.3
pkgrel=3
arch=(i686 x86_64)
url='http://kde.org/applications/system/dolphin/'
pkgdesc='KDE4 Dolphin KPart'
license=(GPL LGPL FDL)
depends=(kdebase-lib libkactivities4)
makedepends=(cmake automoc4)
provides=(kdebase-dolphin)
source=("http://download.kde.org/stable/applications/${pkgver}/src/kde-baseapps-${pkgver}.tar.xz")
sha1sums=('5f5168f3ad9d60b4dc9ba88b1d649364a0534fc6')

prepare() {
  mkdir -p build

  cd kde-baseapps-$pkgver
  sed -i '41,57d' CMakeLists.txt
}

build() {
  cd build
  cmake ../kde-baseapps-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build/dolphin
  make DESTDIR="$pkgdir" install

# provided by dolphin
  rm -r "$pkgdir"/usr/{bin,lib/*.so,share/{appdata,applications,config.kcfg}}
}
