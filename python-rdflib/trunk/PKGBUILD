# Maintainer:
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Leif Warner <abimelech@gmail.com>

pkgname=python-rdflib
pkgver=4.2.2
pkgrel=2
pkgdesc="A Python library for working with RDF, a simple yet powerful language for representing information"
arch=('any')
url="http://rdflib.net/"
license=('BSD')
depends=('python-isodate')
makedepends=('python-setuptools')
checkdepends=('python-nose' 'python-pyparsing' 'python-six' 'python-html5lib' 'python-sparqlwrapper'
              'python-networkx' 'python-decorator')
source=(rdflib-$pkgver.tar.gz::https://github.com/RDFLib/rdflib/tarball/$pkgver
        py3.7.diff)
sha1sums=('08fe9d3480af78ca70380eeae082524ce70bb9d3'
          '71e6b688c981a3f1387f68944580c1e5d5363915')

prepare() {
  patch -d RDFLib-rdflib-* -p1 < py3.7.diff
}

build() {
  cd RDFLib-rdflib-*
  python setup.py build
}

check() {
  cd RDFLib-rdflib-*
  #python setup.py test # Will download and install rdflib again
  nosetests3 --where=./build/src \
    --with-doctest \
    --doctest-extension=.doctest \
    --doctest-tests
}

package() {
  cd RDFLib-rdflib-*
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644  LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
