# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=audiocd-kio
pkgver=16.11.90
pkgrel=1
pkgdesc="Kioslave for accessing audio CDs"
url='https://www.kde.org/applications/multimedia/'
arch=(i686 x86_64)
license=(GPL LGPL FDL)
depends=(kdelibs4support libkcddb libkcompactdisc lame)
makedepends=(extra-cmake-modules kdoctools kdesignerplugin python)
groups=(kde-applications kdemultimedia)
replaces=(kdemultimedia-audiocd-kio)
conflicts=(kdemultimedia-audiocd-kio)
source=("http://download.kde.org/unstable/applications/${pkgver}/src/audiocd-kio-${pkgver}.tar.xz"{,.sig})
sha1sums=('a35c5f9fe76553bd684cc324825b69fe30250855'
          'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
