# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=audiocd-kio
pkgver=20.08.2
pkgrel=1
pkgdesc="Kioslave for accessing audio CDs"
url='https://www.kde.org/applications/multimedia/'
arch=(x86_64)
license=(GPL LGPL FDL)
depends=(kcmutils libkcddb libkcompactdisc lame)
makedepends=(extra-cmake-modules kdoctools)
optdepends=('opus-tools: encoding to opus format')
groups=(kde-applications kde-multimedia)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('5f38c549fe8c3a4659ac8291c223327b765834e7668bc15a96047e20cdb11f01'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
