# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=kscreen
pkgver=5.7.3
pkgrel=1
pkgdesc="KDE's screen management software"
arch=('i686' 'x86_64')
url='https://www.kde.org/workspaces/plasmadesktop/'
license=('LGPL')
depends=('kxmlgui' 'libkscreen' 'qt5-graphicaleffects' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'python')
conflicts=('kscreen-frameworks')
replaces=('kscreen-frameworks')
groups=('plasma')
source=("http://download.kde.org/stable/plasma/${pkgver}/kscreen-$pkgver.tar.xz"{,.sig})
sha256sums=('3c8407bb22a2f4dd3325541232014b6c540477a9101db5ae47c1448399d788fc'
            'SKIP')
validpgpkeys=('13C16D03EDE728514473AA73A506E6D4DD4D5088'  # Jonathan Riddell
              '348C8651206633FD983A8FC4DEACEA00075E1D76'  # KDE Neon
              'D07BD8662C56CB291B316EB2F5675605C74E02CF') # David Edmundson

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kscreen-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
