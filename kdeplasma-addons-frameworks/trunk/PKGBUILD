# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeplasma-addons-frameworks
pkgver=5.1.0.1
pkgrel=1
pkgdesc='KDE Plasma Workspace'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/workspace/kdeplasma-addons'
license=('LGPL')
depends=('krunner' 'kdelibs4support')
makedepends=('extra-cmake-modules' 'kdoctools')
conflicts=('kdebase-workspace')
groups=('plasma-next')
source=("http://download.kde.org/stable/plasma/5.1.0/kdeplasma-addons-$pkgver.tar.xz")
md5sums=('3b65b3334ad67db7ff536ce38c857175')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kdeplasma-addons-5.1.0 \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
