# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Adri√† Arrufat (archdria) <adria dot arrufat at protonmail ldot ch>

pkgname=libhttpseverywhere
pkgver=0.6.5
pkgrel=1
pkgdesc="Leverage the power of HTTPS Everywhere"
url="https://git.gnome.org/browse/libhttpseverywhere"
arch=(x86_64)
license=(LGPL3)
depends=(libgee libxml2 libarchive libsoup glib2 json-glib)
makedepends=(meson vala gobject-introspection git)
_commit=7e102fa1897aa82345ec0937f4fc7e035b1eb317  # tags/0.6.5^0
source=("git+https://git.gnome.org/browse/libhttpseverywhere#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  mkdir build
  cd $pkgname
}

build() {
  cd build
  arch-meson ../$pkgname -Denable_valadoc=true
  ninja
}

check() {
  cd build
  meson test
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}
