# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: buddabrod <buddabrod@gmail.com>

pkgname=xf86-video-nouveau
_gitdate=20100314
pkgver=0.0.15_git${_gitdate} # see configure.ac
pkgrel=1
pkgdesc="Open Source 3D acceleration driver for nVidia cards (experimental)"
arch=('i686' 'x86_64')
url="http://nouveau.freedesktop.org/wiki/"
license=('GPL') #and MIT, not yet a license file, see http://nouveau.freedesktop.org/wiki/FAQ#head-09f75d03eb30011c754038a3893119a70745de4e
depends=('nouveau-drm>=0.0.16_20100313' 'libdrm>=2.4.19')
#optdepends=('nouveau-dri:	highly experimental gallium3d features')
makedepends=('pkgconfig' 'xorg-server>=1.7.5' 'mesa>=7.7' 'glproto>=1.4.11' 'xorg-util-macros' 'xf86driproto' 'autoconf')
conflicts=('xorg-server<1.7.0')
options=('!libtool') # 'force'
install=${pkgname}.install
source=(ftp://ftp.archlinux.org/other/$pkgname/xf86-video-nouveau-${_gitdate}.tar.bz2)
md5sums=('7bed078b49db2d9305c9f333a6be759d')

# source PKGBUILD && mksource
mksource() {
	mkdir /tmp/${pkgname}-${_gitdate}
	pushd /tmp/${pkgname}-${_gitdate}
	git clone -v --depth 1 git://anongit.freedesktop.org/nouveau/xf86-video-nouveau
	cd xf86-video-nouveau
	git archive --prefix=xf86-video-nouveau-${_gitdate}/ --format=tar HEAD | bzip2 > /tmp/${pkgname}-${_gitdate}/${pkgname}-${_gitdate}.tar.bz2
	popd
}

build() {
  cd ${srcdir}/xf86-video-nouveau-${_gitdate}
  ./autogen.sh --prefix=/usr || return 1
  make || return 1
  make DESTDIR=${pkgdir} install || return 1
}
