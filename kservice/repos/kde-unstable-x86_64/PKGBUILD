# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kservice
pkgver=r9.d443a72
pkgrel=1
pkgdesc='KService'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kservice'
license=('LGPL')
depends=('ki18n' 'kconfig' 'kcrash' 'kdbusaddons')
makedepends=('extra-cmake-modules' 'git')
groups=('kf5')
source=('git://anongit.kde.org/kservice.git')
md5sums=('SKIP')

pkgver() {
  cd kservice
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build

  # There's something wrong with our kdoctools but I don't get it
  sed -i 's/add_subdirectory(docs)/#add_subdirectory(docs)/' \
    kservice/CMakeLists.txt
}

build() {
  cd build
  cmake ../kservice \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
#    -DSYSCONF_INSTALL_DIR=/etc
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
