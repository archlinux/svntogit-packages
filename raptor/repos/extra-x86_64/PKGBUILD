# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: eric <eric@archlinux.org>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>

_name=raptor2
pkgname=raptor
pkgver=2.0.15
pkgrel=19
pkgdesc="A C library that parses RDF/XML/N-Triples into RDF triples"
arch=(x86_64)
url="https://librdf.org/raptor"
license=(Apache GPL2 LGPL2.1)
depends=(glibc libxml2 libxslt xz zlib)
makedepends=(curl gtk-doc icu)
provides=(libraptor2.so)
options=(debug)
source=(
  https://librdf.org/dist/source/$_name-$pkgver.tar.gz{,.asc}
  $pkgname-2.0.15-CVE-2017-18926.patch::https://github.com/dajobe/raptor/commit/590681e546cd9aa18d57dc2ea1858cb734a3863f.patch
  $pkgname-2.0.15-out_of_bounds.patch
  $pkgname-2.0.15-disable_broken_test_suites.patch
)
sha512sums=('563dd01869eb4df8524ec12e2c0a541653874dcd834bd1eb265bc2943bb616968f624121d4688579cdce11b4f00a8ab53b7099f1a0850e256bb0a2c16ba048ee'
            'SKIP'
            '203ae75dae8242fb7988a199df8a7337b0660871f5caa6c9098167536ba880ad55765bb60fd4315020f208ed3ae8dc03eb1b91241851410a961a797192ecb969'
            '140f90d74fad8cdc6ef7fa14fa655e425552947d57068021362f8ae9efdded8ed31295e9bdd495fe70e646967062e28fa3ecd5831f9902134da5fe0a82607735'
            'bfa5e2c10e33bab9ba85541ca78e903dbcbe07e2fbb654687e0b65fe46531bf385d7b3de36619b9919b7b4db3b803624d294cb8bfd63a89b96fc85cd2fda278e')
b2sums=('0a39c7b5705bfbf2daa0ca633f79693953b4dfe24c144008d1646a9840a36d4d7ce153b527450647127ec2522047dbd0a6e71f307ee5656951f7e4b610adfd22'
        'SKIP'
        'a3f83e34686dfd55ad1c4b9e97f6ae046be31b8224846dfa2b83ba9228cb987c6ebb19f37f48c196fab56a1e1d007d71225ed12acc2188e088b95c7cff5a0beb'
        'ab11eeb648bf2cf8ac4ef4bbff4cdb77d1b713ad086268b656c7249363bf5752b535763f9a9fd34be4d72b3ff420cd87ee6970d3da6e24e5e2b067b9dbb9c29e'
        '5f623afff5566d47d132c24e32b6b683b7eff15a9a819387b1764b1c222ae47f6e1948892b2a93af77375a3616f9276b00e6686108720da1005b47232b3f6b5f')
validpgpkeys=('F879F0DEDA780198DD08DC6443EC92504F71955A') # Dave Beckett <dave@dajobe.org>

prepare() {
  # fix CVE-2017-18926:
  # https://bugs.archlinux.org/task/68613
  patch -d $_name-$pkgver -Np1 -i ../$pkgname-2.0.15-CVE-2017-18926.patch
  # fix out-of-bounds read:
  # https://bugs.librdf.org/mantis/view.php?id=650
  patch -d $_name-$pkgver -Np1 -i ../$pkgname-2.0.15-out_of_bounds.patch
  # remove broken test suites: https://github.com/dajobe/raptor/issues/49
  patch -d $_name-$pkgver -Np1 -i ../$pkgname-2.0.15-disable_broken_test_suites.patch

  cd $_name-$pkgver
  autoreconf -fiv
}

build() {
  cd $_name-$pkgver
  ./configure --prefix=/usr \
              --disable-static \
              --with-icu-config=/usr/bin/icu-config
  # prevent excessive overlinking due to libtool
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  make check -C $_name-$pkgver
}

package() {
  depends+=(libcurl.so libicuuc.so)

  make DESTDIR="$pkgdir" install -C $_name-$pkgver
  install -vDm 644 $_name-$pkgver/{AUTHORS,ChangeLog*,README} -t "$pkgdir/usr/share/doc/$pkgname/"
}
