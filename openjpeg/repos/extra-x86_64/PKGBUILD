# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=openjpeg
pkgver=1.5.1
pkgrel=1
pkgdesc="An open source JPEG 2000 codec"
arch=(i686 x86_64)
license=('BSD')
url="http://www.openjpeg.org"
depends=('zlib')
source=(http://openjpeg.googlecode.com/files/openjpeg-${pkgver}.tar.gz)
sha1sums=('1b0b74d1af4c297fd82806a9325bb544caf9bb8b')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # make sure we use system libs
  rm -rf thirdparty
  ./configure --prefix=/usr \
	--disable-static --disable-silent-rules
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/openjpeg"
  install -m644 LICENSE "${pkgdir}/usr/share/licenses/openjpeg/LICENSE"
}
