# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=aalib
pkgver=1.4rc5
pkgrel=13
pkgdesc="A portable ASCII art graphic library"
arch=('x86_64')
url="http://aa-project.sourceforge.net/aalib/"
license=('LGPL')
depends=('gpm' 'libx11')
makedepends=('libxt')
source=(https://downloads.sourceforge.net/sourceforge/aa-project/${pkgname}-${pkgver}.tar.gz
	aclocal-fixes.patch)
sha1sums=('a23269e950a249d2ef93625837cace45ddbce03b'
          '98d8c5a13672a8107945d694ff4520e0bc87d3e9')

prepare() {
  cd ${pkgname}-1.4.0
  patch -p0 -i "${srcdir}/aclocal-fixes.patch"
}

build() {
  cd ${pkgname}-1.4.0
  ./configure --prefix=/usr --mandir=/usr/share/man --infodir=/usr/share/info
  make
}

package() {
  cd ${pkgname}-1.4.0
  make DESTDIR="${pkgdir}" install
}
