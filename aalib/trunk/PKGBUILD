# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=aalib
pkgver=1.4rc5
pkgrel=15
pkgdesc="A portable ASCII art graphic library"
arch=('x86_64')
url="http://aa-project.sourceforge.net/aalib/"
license=('LGPL')
depends=('gpm' 'libx11')
makedepends=('libxt')
source=(https://downloads.sourceforge.net/sourceforge/aa-project/${pkgname}-${pkgver}.tar.gz
	aclocal-fixes.patch)
sha256sums=('fbddda9230cf6ee2a4f5706b4b11e2190ae45f5eda1f0409dc4f99b35e0a70ee'
            'f40742677a3e0d58c99a09da7574635411156586acd22867e71dd46c88088bce')

prepare() {
  cd ${pkgname}-1.4.0
  patch -p0 -i "${srcdir}/aclocal-fixes.patch"
}

build() {
  cd ${pkgname}-1.4.0
  ./configure --prefix=/usr --mandir=/usr/share/man --infodir=/usr/share/info
  make
}

package() {
  cd ${pkgname}-1.4.0
  make DESTDIR="${pkgdir}" install
}
