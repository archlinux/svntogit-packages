# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>

pkgbase=qscintilla
pkgname=('qscintilla-qt5' 'python-qscintilla-qt5' 'python2-qscintilla-qt5'
         'python-qscintilla-qt5-common')
pkgver=2.11.1
pkgrel=1
license=('GPL')
arch=('x86_64')
url="http://www.riverbankcomputing.co.uk/software/qscintilla/intro"
makedepends=('python-pyqt5' 'python2-pyqt5' 'qt5-tools' 'sip')
options=('!buildflags') # Fix Qt Designer plugin
source=("https://www.riverbankcomputing.com/static/Downloads/QScintilla/$pkgver/QScintilla_gpl-$pkgver.tar.gz")
sha256sums=('dae54d19e43dba5a3f98ac084fc0bcfa6fb713fa851f1783a01404397fd722f5')

build() {
    cd "$srcdir"/QScintilla_gpl-${pkgver}
    export QMAKEFEATURES=$PWD/Qt4Qt5/features/

    cd Qt4Qt5
    qmake-qt5
    make

    cd ../designer-Qt4Qt5
    qmake-qt5 INCLUDEPATH+=../Qt4Qt5 QMAKE_LIBDIR+=../Qt4Qt5
    make

    cd ../
    cp -rf Python Python2
    cd Python
    python configure.py --pyqt=PyQt5 -n ../Qt4Qt5/ -o ../Qt4Qt5/ -c --qmake /usr/bin/qmake-qt5
    make

    cd ../Python2
    python2 configure.py --pyqt=PyQt5 -n ../Qt4Qt5/ -o ../Qt4Qt5/ -c --qmake /usr/bin/qmake-qt5
    make
}

package_qscintilla-qt5() {
    pkgdesc="A port to Qt5 of Neil Hodgson's Scintilla C++ editor class"
    depends=('qt5-base')

    cd QScintilla_gpl-${pkgver}/Qt4Qt5
    make DESTDIR="${pkgdir}" INSTALL_ROOT="${pkgdir}" install -j1

    cd ../designer-Qt4Qt5
    make DESTDIR="${pkgdir}" INSTALL_ROOT="${pkgdir}" install
}

package_python-qscintilla-qt5-common() {
    pkgdesc="Common python qscintilla bindings files shared between python-qscintilla-qt5 and python2-qscintilla-qt5"
    depends=('qscintilla-qt5')

    cd QScintilla_gpl-${pkgver}/Python
    make DESTDIR="${pkgdir}" INSTALL_ROOT="${pkgdir}" install -j1

    # Provided by python-qscintilla
    rm -r "${pkgdir}"/usr/lib
}

package_python-qscintilla-qt5() {
    pkgdesc="Python 3.x bindings for QScintilla2"
    depends=('python-qscintilla-qt5-common' 'python-pyqt5')

    cd QScintilla_gpl-${pkgver}/Python
    make DESTDIR="${pkgdir}" INSTALL_ROOT="${pkgdir}" install -j1

    # Provided by python-qscintilla-common
    rm -r "${pkgdir}"/usr/share
}

package_python2-qscintilla-qt5() {
    pkgdesc="Python 2.x bindings for QScintilla2"
    depends=('python-qscintilla-qt5-common' 'python2-pyqt5')

    cd QScintilla_gpl-${pkgver}/Python2
    make DESTDIR="${pkgdir}" INSTALL_ROOT="${pkgdir}" install -j1

    # Provided by python-qscintilla
    rm -r "${pkgdir}"/usr/share
}
