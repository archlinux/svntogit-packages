# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=bluedevil
pkgver=2.1.1
pkgrel=1
epoch=1
pkgdesc='KDE bluetooth framework'
arch=('i686' 'x86_64')
url="https://projects.kde.org/projects/extragear/base/bluedevil"
license=('GPL')
depends=('kdebase-runtime' 'libbluedevil')
makedepends=('cmake' 'automoc4')
optdepends=('kdebase-workspace: to run the plasmoid')
install="${pkgname}.install"
source=("http://download.kde.org/stable/${pkgname}/${pkgver}/src/${pkgname}-${pkgver}.tar.xz")
md5sums=('9a6b011331678ea08f303049bcdbb69f')

prepare() {
  mkdir build
}

build(){
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
