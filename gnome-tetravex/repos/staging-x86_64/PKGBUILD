# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-tetravex
pkgver=3.22.0+4+g817dbf6
pkgrel=2
pkgdesc="Complete the puzzle by matching numbered tiles"
url="https://wiki.gnome.org/Apps/Tetravex"
arch=(x86_64)
license=(GPL)
depends=(gtk3)
makedepends=(intltool itstool gobject-introspection vala yelp-tools appstream-glib git)
groups=(gnome-extra)
_commit=817dbf6801d272b338f87326ff1e2ac74035865f  # master
source=("git+https://git.gnome.org/browse/gnome-tetravex#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --disable-schemas-compile
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
