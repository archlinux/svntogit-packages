# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-tetravex
pkgver=3.32.0
pkgrel=1
pkgdesc="Complete the puzzle by matching numbered tiles"
url="https://wiki.gnome.org/Apps/Tetravex"
arch=(x86_64)
license=(GPL)
depends=(gtk3)
makedepends=(meson gobject-introspection vala yelp-tools appstream-glib git)
groups=(gnome-extra)
_commit=71406876aea3f909b5452ac838221330650ecd43  # tags/3.32.0^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-tetravex.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  meson test -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
