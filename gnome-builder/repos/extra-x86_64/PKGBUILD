# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-builder
pkgver=3.26.4
pkgrel=2
pkgdesc="An IDE for writing GNOME-based software"
url="https://wiki.gnome.org/Apps/Builder"
arch=(x86_64)
license=(GPL3)
depends=(gtksourceview3 devhelp libgit2-glib gjs python-gobject clang desktop-file-utils
         ctags libpeas vte3 vala python-jedi autoconf-archive sysprof flatpak gspell libdazzle
         template-glib jsonrpc-glib python-sphinx)
makedepends=(intltool llvm gobject-introspection gtk-doc yelp-tools appstream-glib vala git
             mm-common meson)
optdepends=('gnome-code-assistance: Legacy assistance services')
groups=(gnome-extra)
_commit=571dc9c73e17248ae8fa0ebc3439584a38070f32  # tags/3.26.4^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-builder.git#commit=$_commit"
        "git+https://git.gnome.org/browse/libgd"
        "git+https://gitlab.gnome.org/GNOME/jsonrpc-glib.git"
        "git+https://gitlab.gnome.org/GNOME/template-glib.git"
        "git+https://gitlab.gnome.org/GNOME/libdazzle.git"
        enchant-2.diff)
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'a9c69574433f83de76e488d5da6656e455dcbea80ff788141bae269c04ebc92b')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^GNOME_BUILDER_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd $pkgname

  # enchant 2.2
  patch -Np1 -i ../enchant-2.diff

  git submodule init
  git config --local submodule.subprojects/libgd.url "$srcdir/libgd"
  git config --local submodule.subprojects/jsonrpc-glib.url "$srcdir/jsonrpc-glib"
  git config --local submodule.subprojects/template-glib.url "$srcdir/template-glib"
  git config --local submodule.subprojects/libdazzle.url "$srcdir/libdazzle"
  git submodule update
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}
