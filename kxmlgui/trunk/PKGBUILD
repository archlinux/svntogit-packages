# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kxmlgui
pkgver=5.10.0
pkgrel=2
pkgdesc='User configurable main windows'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kxmlgui'
license=('LGPL')
depends=('kglobalaccel' 'ktextwidgets' 'attica-qt5')
makedepends=('extra-cmake-modules' 'python')
groups=('kf5')
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz" 
'session-restore.patch')
md5sums=('35b8161e519fc6a2a466330a9f24c05f'
         '4dc0c7ec892840d27cacb99085219991')

prepare() {
  mkdir -p build

# Fix session restore for KF5 applications https://bugs.kde.org/show_bug.cgi?id=346768
  cd $pkgname-$pkgver
  patch -p1 -i "$srcdir"/session-restore.patch
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DLIBEXEC_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
