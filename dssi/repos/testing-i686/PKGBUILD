# $Id$
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Pajaro

pkgname=dssi
pkgver=1.1.1
pkgrel=8
pkgdesc="An API for audio processing plugins & softsynths with UIs"
arch=('i686' 'x86_64')
url="http://dssi.sourceforge.net/"
license=('LGPL' 'BSD' 'custom')
depends=('liblo>=0.28' 'jack')
makedepends=('ladspa' 'qt4')
optdepends=('qt4: example plugins')
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz"
        'license.txt')
md5sums=('619ab73c883b02dc37ddb37001591f8b'
         '258ca4d0ca28c5c4d4675147621fac18')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir/license.txt" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
