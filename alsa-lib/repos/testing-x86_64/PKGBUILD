# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=alsa-lib
pkgver=1.2.5
pkgrel=2
pkgdesc="An alternative implementation of Linux sound support"
arch=('x86_64')
url="https://www.alsa-project.org"
license=('LGPL2.1')
depends=('glibc' 'alsa-topology-conf' 'alsa-ucm-conf')
provides=('libasound.so' 'libatopology.so')
install="${pkgname}.install"
source=("https://www.alsa-project.org/files/pub/lib/${pkgname}-${pkgver}.tar.bz2"{,.sig}
        "${pkgname}-1.2.5-load_all_cards.patch::https://github.com/alsa-project/alsa-lib/commit/ddfc32abf5697de1618b9e7ffdf57a0f97013090.patch"
        "${pkgname}-1.2.5-private_device_prefix1.patch::https://github.com/alsa-project/alsa-lib/commit/0e4ba2ea8c0402f12a645032a14693eb9b1278e6.patch"
        "${pkgname}-1.2.5-private_device_prefix2.patch::https://github.com/alsa-project/alsa-lib/commit/9621d0bff2e60b43e329ffa5059ab19f2914ec14.patch")
sha512sums=('d9b53484737a3684aac7e08b4b18400aac10fec1a78a8bf9101944bea2c50710bc7355e9bb0012541b067e2d5ee7b1b818bf1bdbffbb2b2d7ad0eb15efe49725'
            'SKIP'
            'c5ee4d27001c31772c24595051424869961e1b39b45565256650b63471e2be1236b17cdec5e66e182f1894ef8b09b5f6f959b8f8c6e46a2a0131a8512a9d2516'
            'dfe42b349bfc73682158dfeebe97c3f43d28cdccad307a239b2ed1e30d289cfe9b1577799c34917b07ac98f48b28939aba9c6e8b0db322fc6dd36bdb96d56208'
            '294b79947cbbb00208495ada2baffdaef5e7aa4ea6b49d94733b86bcde59eef7589b87d2d856991405bcb5df8ca48f13c4e89c0a0c163f1abff98aad2c52cd88')
b2sums=('84fe4e22d4316bfc84f15f2493f40f113eb3db7722f230550586533e80daf214140164e8ddc1a399b82a3fd0fb21125ff225cfeb45eaa19320c2e2aa3997c662'
        'SKIP'
        'b43d5cb0efbe0c7c440e31fceb6c6b6a4f8b234ee4b12e74382ad83b16d67778a3d72838554c8813fec2a6200ce2cadac360cb78de275cf73a9c9010f3ba0287'
        '2b6062724be3bfd224a3e615d0a1d557a847f628bd01bd9eb155782f3926f7734b63ea1e10369e7294af9d82ad80ad23bb269dbc7a3659a9273758248d08c487'
        '7b1a251d4d6fe7f47cc7ec6dc5f20536fca8f455cb321bcbd6f32544e3ecedce4f5fed3191fbbfd9ef79ad74b2ca71dbe12faafe231ac3bd53301a91bcf9934f')
validpgpkeys=('F04DF50737AC1A884C4B3D718380596DA6E59C91') # ALSA Release Team (Package Signing Key v1) <release@alsa-project.org>

prepare() {
  cd "$pkgname-$pkgver"
  # fix loading of multiple cards
  # https://github.com/alsa-project/alsa-lib/issues/143
  patch -Np1 -i ../"${pkgname}-1.2.5-load_all_cards.patch"
  # probably fix issues with pipewire:
  # https://github.com/alsa-project/alsa-lib/issues/142
  patch -Np1 -i ../"${pkgname}-1.2.5-private_device_prefix1.patch"
  patch -Np1 -i ../"${pkgname}-1.2.5-private_device_prefix2.patch"
  autoreconf -vfi
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr \
              --without-debug
  make
}

check() {
  cd "$pkgname-$pkgver"
  export LD_LIBRARY_PATH="${PWD}/src/.libs/:${LD_LIBRARY_PATH}"
  make -k check
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  make DESTDIR="$pkgdir" install -C doc
  install -vDm 644 {MEMORY-LEAK,TODO,NOTES,ChangeLog,doc/asoundrc.txt} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
