# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgbase=msmtp
pkgname=('msmtp' 'msmtp-mta')
pkgver=1.8.0
pkgrel=1
pkgdesc="A mini smtp client"
arch=('x86_64')
url="https://marlam.de/msmtp/"
license=('GPL3')
makedepends=('texlive-plainextra' 'libsecret')
source=(https://marlam.de/msmtp/releases/msmtp-$pkgver.tar.xz) # {,.sig} no key available
sha256sums=('bd730cbf000d1b8382849ea21d569a387e63f936be00dc07c569f67915e53ccd')

build() {
  cd ${pkgbase}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc --with-ssl=gnutls
  make
  make -C doc html pdf
}

package_msmtp() {
  pkgdesc="A mini smtp client"
  depends=('libsecret' 'gnutls')

  cd ${pkgbase}-${pkgver}
  make DESTDIR="${pkgdir}" install 
  make DESTDIR="${pkgdir}" -C doc install-html install-pdf

# Installing example configs and scripts to /usr/share/doc/msmtp
# as they are not installed by default (Debian and Gentoo do it this way)
  install -d "${pkgdir}/usr/share/doc/msmtp"
  cp -r scripts/{find_alias,msmtpqueue,msmtpq,set_sendmail} "${pkgdir}/usr/share/doc/msmtp/"
  install -D -m644 doc/*.example "${pkgdir}/usr/share/doc/msmtp/"

  install -D -m644 scripts/vim/msmtp.vim "${pkgdir}/usr/share/vim/vimfiles/syntax/msmtp.vim"

  chmod 755 "$pkgdir"/usr/share/doc/msmtp/{find_alias,msmtpqueue,set_sendmail}/*.sh
  chmod 755 "$pkgdir"/usr/share/doc/msmtp/msmtpq/msmtp*
}

package_msmtp-mta() {
  pkgdesc="A mini smtp client - the regular MTA"
  depends=('msmtp')
  provides=('smtp-forwarder')
  conflicts=('smtp-forwarder')

  install -d "${pkgdir}"/usr/bin
  ln -s msmtp "${pkgdir}/usr/bin/sendmail"
}
