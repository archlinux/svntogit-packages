# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>

pkgname=deluge
pkgver=1.2.3
pkgrel=6
pkgdesc="A bittorrent client written with python and pygtk"
arch=('i686' 'x86_64')
url="http://deluge-torrent.org/"
license=('GPL3')
depends=('pyxdg' 'setuptools' 'libtorrent-rasterbar>=0.15.0' 'twisted' 'pyopenssl' 'xdg-utils' 'python-chardet')
makedepends=( 'intltool' 'pygtk' 'librsvg' 'python-mako')
optdepends=('python-notify: libnotify notifications'
	    'pygtk: needed for gtk ui'
	    'librsvg: needed for gtk ui'
	    'python-mako: needed for web ui')
source=(http://download.deluge-torrent.org/source/${pkgname}-${pkgver}.tar.bz2 deluged deluged.conf.d deluge-web
	fix_freeze.patch)
install=deluge.install
backup=(etc/conf.d/deluged)
md5sums=('f1dbc8734c1881b115b98f572e523a4d' '7d5790eeb14cd0b47d1bb3e0a19c9935'
         '332d23fe0d21ce92b494efd73322856c' '6c913ec7322eaa60494ac9d53103005f'
         '09af9b6badccb2fd5d24f8cbe9a109a7')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -Np1 -i "${srcdir}/fix_freeze.patch" || return 1

  python setup.py build || return 1
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 || return 1

  install -d -m755 "${pkgdir}"/srv/deluge || return 1
  install -D -m644 deluge/data/pixmaps/deluge.svg "${pkgdir}"/usr/share/pixmaps/deluge.svg || return 1
  install -D -m755 "${srcdir}"/deluged "${pkgdir}"/etc/rc.d/deluged || return 1
  install -D -m755 "${srcdir}"/deluge-web "${pkgdir}"/etc/rc.d/deluge-web || return 1
  install -D -m644 "${srcdir}"/deluged.conf.d "${pkgdir}"/etc/conf.d/deluged || return 1
}
