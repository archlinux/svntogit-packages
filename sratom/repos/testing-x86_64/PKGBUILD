# $Id: PKGBUILD 72422 2012-06-14 07:51:14Z speps $
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=sratom
pkgver=0.4.4
pkgrel=1
pkgdesc="An LV2 Atom RDF serialisation library"
arch=('i686' 'x86_64')
url="http://drobilla.net/software/sratom/"
license=('custom:ISC')
depends=('lv2' 'sord>=0.12.0')
makedepends=('python2')
source=("http://download.drobilla.net/$pkgname-$pkgver.tar.bz2")
md5sums=('07449e40511f5911298ee4c79ad0530a')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # remove ldconfig --speps
  sed -i '/ldconfig/d' wscript

  python2 waf configure --prefix=/usr
  python2 waf build $MAKEFLAGS
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  python2 waf install --destdir="$pkgdir"

  # license
  install -Dm644 COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
