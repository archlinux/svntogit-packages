# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=orc
pkgver=0.4.28
pkgrel=1
pkgdesc="Optimized Inner Loop Runtime Compiler"
url="https://gitlab.freedesktop.org/gstreamer/orc"
arch=(x86_64)
license=(custom)
depends=(glibc)
makedepends=(valgrind git gtk-doc meson)
_commit=31cb4bfc51de81b5c2569abdcff830b83c74499c  # tags/orc-0.4.28^0
source=("git+https://gitlab.freedesktop.org/gstreamer/orc.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^orc-//;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  meson test -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 $pkgname/COPYING
}
