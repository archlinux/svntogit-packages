# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdepimlibs4
pkgver=4.14.10
_kdeappver=15.04.3
pkgrel=3
pkgdesc="KDE4 PIM Libraries"
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kdepimlibs'
license=('GPL' 'LGPL')
depends=('kdelibs' 'gpgme' 'libakonadi-qt4' 'libical' 'prison' 'qjson')
makedepends=('cmake' 'automoc4' 'boost' 'cyrus-sasl' 'akonadi-qt4')
conflicts=('kdepimlibs')
provides=('kdepimlibs')
replaces=('kdepimlibs')
install='kdepimlibs4.install'
source=("http://download.kde.org/stable/applications/${_kdeappver}/src/kdepimlibs-${pkgver}.tar.xz")
sha1sums=('8db2f59b8a33f4d1bbebfdff8b772d30669c9932')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kdepimlibs-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install

# Conflicts with KF5
  rm -r "$pkgdir"/usr/{bin,share/config.kcfg}
}
