# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgbase=mysql
pkgname=('libmysqlclient' 'mysql-clients' 'mysql')
pkgver=5.5.8
pkgrel=4
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.mysql.com/"
makedepends=('cmake' 'openssl' 'tcp_wrappers')
options=('!libtool')
source=("http://ftp.gwdg.de/pub/misc/mysql/Downloads/MySQL-5.5/${pkgbase}-${pkgver}.tar.gz"
        'mysqld'
        'my.cnf'
        'fix-set-default-charset.patch')
md5sums=('42e866302b61f5e213afd33e04677017'
         '2234207625baa29b2ff7d7b4f088abce'
         '1c949c0dbea5206af0db14942d9927b6'
         '6f72b482580d020933ea9a7a4f638051')

build() {
  cd "${srcdir}"/${pkgbase}-${pkgver}
  patch -Np1 -i ${srcdir}/fix-set-default-charset.patch

  cd "${srcdir}"
  mkdir build
  cd build

  # CFLAGS/CXXFLAGS as suggested upstream
  CFLAGS="-fPIC ${CFLAGS} -fno-strict-aliasing -DBIG_JOINS=1 -fomit-frame-pointer" \
  CXXFLAGS="-fPIC ${CXXFLAGS} -fno-strict-aliasing -DBIG_JOINS=1 -felide-constructors -fno-rtti" \

  cmake ../${pkgbase}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DMYSQL_DATADIR=/var/lib/mysql \
    -DSYSCONFDIR=/etc/mysql \
    -DINSTALL_INFODIR=share/mysql/docs \
    -DINSTALL_MANDIR=share/man \
    -DINSTALL_PLUGINDIR=share/mysql/lib/plugin \
    -DINSTALL_SCRIPTDIR=bin \
    -DINSTALL_DOCREADMEDIR=share/mysql \
    -DINSTALL_SUPPORTFILESDIR=share/mysql \
    -DINSTALL_MYSQLSHAREDIR=share/mysql \
    -DINSTALL_SHAREDIR=share/mysql \
    -DWITH_READLINE=OFF \
    -DWITH_SSL=system \
    -DWITH_LIBWRAP=ON \
    -DDEFAULT_CHARSET=utf8 \
    -DDEFAULT_COLLATION=utf8_general_ci \
    -DWITH_EXTRA_CHARSETS=complex \
    -DWITH_EMBEDDED_SERVER=ON \
    -DMYSQL_TCP_PORT=3306 \
    -DMYSQL_UNIX_ADDR=/var/run/mysqld/mysqld.sock \
    -DENABLED_LOCAL_INFILE=ON \
    -DWITHOUT_PARTITION_STORAGE_ENGINE=1 \
    -DWITHOUT_EXAMPLE_STORAGE_ENGINE=1 \
    -DWITHOUT_ARCHIVE_STORAGE_ENGINE=1 \
    -DWITHOUT_BLACKHOLE_STORAGE_ENGINE=1 \
    -DWITHOUT_FEDERATED_STORAGE_ENGINE=1 \
    -DWITH_INNOBASE_STORAGE_ENGINE=1

  make
}

package_libmysqlclient(){
  pkgdesc="MySQL client libraries"
  depends=('openssl' 'gcc-libs' 'tcp_wrappers')
  
  cd "${srcdir}"/build
  for dir in include libmysql libmysqld libservices; do
    make -C ${dir} DESTDIR="${pkgdir}" install
  done

  install -d "${pkgdir}/usr/bin"
  install -m755 scripts/mysql_config "${pkgdir}/usr/bin/"
}

package_mysql-clients(){
  pkgdesc="MySQL client tools"
  depends=('libmysqlclient')
  
  cd "${srcdir}"/build/client
  make DESTDIR="${pkgdir}" install

  # provided by libmysqlclient
  rm -rf "${pkgdir}/usr/lib/"

  # provided by mysql
  rm -f "${pkgdir}"/usr/bin/{mysql_upgrade,mysqlbinlog,mysqltest}
}

package_mysql(){
  pkgdesc="A fast SQL database server"
  backup=('etc/mysql/my.cnf')
  install=mysql.install
  depends=('mysql-clients')
  optdepends=('perl-dbi' 'perl-dbd-mysql')

  cd "${srcdir}"/build
  make DESTDIR=${pkgdir} install

  install -Dm644 ${srcdir}/my.cnf ${pkgdir}/etc/mysql/my.cnf
  install -Dm755 ${srcdir}/mysqld ${pkgdir}/etc/rc.d/mysqld

  # provided by libmysqlclient
  rm -f ${pkgdir}/usr/bin/{mysql_config,mysql_client_test_embedded,mysqltest_embedded}
  rm -rf ${pkgdir}/usr/lib/
  rm -rf ${pkgdir}/usr/include/
  
  # provided by mysql-clients
  rm -f ${pkgdir}/usr/bin/{mysql,mysqladmin,mysqlcheck,mysqldump,mysqlimport,mysqlshow,mysqlslap}

  # not needed
  rm -rf ${pkgdir}/usr/{mysql-test,sql-bench}

  # These shouldn't be here
  rm -rf ${pkgdir}/usr/docs
  install -d ${pkgdir}/var/lib/mysql
  cp -rfa ${pkgdir}/usr/data/* ${pkgdir}/var/lib/mysql/
  rm -rf ${pkgdir}/usr/data
}
