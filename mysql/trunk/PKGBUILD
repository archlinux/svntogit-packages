# $Id$
# Maintainer: Alexander Baldeck <alexander@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=mysql
pkgver=5.1.32
pkgrel=1
pkgdesc="A fast SQL database server"
arch=(i686 x86_64)
backup=(etc/my.cnf etc/conf.d/mysqld)
depends=("mysql-clients>=${pkgver}" 'tcp_wrappers' 'zlib' 'perl')
makedepends=('libtool' 'patch')
url=('http://www.mysql.com/')
options=('!libtool')
optdepends=('perl-dbi' 'perl-dbd-mysql')
license=('GPL')
source=(ftp://ftp.pucpr.br/mysql/Downloads/MySQL-5.1/mysql-${pkgver}.tar.gz
        mysql-no-clients.patch
        mysql-fix-libs.patch
        mysqld
        my.cnf
        mysqld.conf.d)

build() {
  cd ${startdir}/src/${pkgname}-${pkgver}
  patch -Np1 -i ${startdir}/src/mysql-no-clients.patch || return 1
  patch -Np1 -i ${startdir}/src/mysql-fix-libs.patch || return 1

  ./configure --prefix=/usr --libexecdir=/usr/sbin \
  --localstatedir=/var --sysconfdir=/etc \
  --without-debug --without-docs --without-readline \
  --enable-local-infile --with-ssl \
  --with-charset=latin1 --with-collation=latin1_general_ci \
  --with-extra-charsets=complex --enable-thread-safe-client \
  --with-libwrap

  pushd include || return
  make || return 1
  popd

  pushd libmysql                                                                                                                 
  make link_sources get_password.lo || return                                                                                    
  popd

  pushd storage || return
  make || return 1
  popd

  # fixes
  #sed -i -e 's/^.*HAVE_GETHOSTBYNAME_R_GLIBC2_STYLE.*$/#define\ HAVE_GETHOSTBYNAME_R_GLIBC2_STYLE/g' include/config.h || return 1
  #sed -i -e 's/size_socket/socklen_t/g' sql/mysqld.cc || return 1

  make || return 1
  make DESTDIR=${startdir}/pkg install

  rm -rf $pkgdir/usr/{mysql-test,sql-bench,lib,include}
  install -D -m644 ../my.cnf ${startdir}/pkg/etc/my.cnf
  install -D -m755 ../mysqld ${startdir}/pkg/etc/rc.d/mysqld
  install -D -m644 ../mysqld.conf.d ${startdir}/pkg/etc/conf.d/mysqld
  rm -f ${startdir}/pkg/usr/bin/mysql_config
}
md5sums=('f2d14b5e3b9d50b809c9b9985e4513b7'
         'b4c9effb2ebf85a6aae9e94f5bbef344'
         'd1921e85821a66196c24ef6e5346b572'
         '247e9fa664bccaca62ca0e42a6cfae20'
         '0ee035590ffc61d32de994f461fd2bd2'
         '4a9077fc95ec6db1d5420e0cdc74d31c')
