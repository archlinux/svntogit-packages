# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgbase=mysql
pkgname=('libmysqlclient' 'mysql-clients' 'mysql')
pkgver=5.1.54
pkgrel=1
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.mysql.com/"
makedepends=('tcp_wrappers' 'zlib' 'perl' 'openssl' 'libtool' 'patch')
options=('!libtool')
source=("http://ftp.gwdg.de/pub/misc/mysql/Downloads/MySQL-5.1/${pkgbase}-${pkgver}.tar.gz"
        'mysqld'
        'my.cnf'
        'skip-abi-check.patch')
md5sums=('2a0f45a2f8b5a043b95ce7575796a30b'
         '2234207625baa29b2ff7d7b4f088abce'
         '0337741fa9afbe57939993636081a827'
         'a97e574945e19de3908575b956241026')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  patch -Np0 -i "${srcdir}/skip-abi-check.patch"
  # CFLAGS/CXXFLAGS as suggested upstream
  CFLAGS="-fPIC ${CFLAGS} -fno-strict-aliasing -DBIG_JOINS=1 -fomit-frame-pointer" \
  CXXFLAGS="-fPIC ${CXXFLAGS} -fno-strict-aliasing -DBIG_JOINS=1 -felide-constructors -fno-rtti" \
  ./configure --prefix=/usr \
    --libexecdir=/usr/sbin \
    --localstatedir=/var \
    --sysconfdir=/etc/mysql \
    --without-docs \
    --without-readline \
    --with-ssl \
    --with-libwrap \
    --with-charset=utf8 \
    --with-collation=utf8_general_ci \
    --with-extra-charsets=complex \
    --with-embedded-server \
    --with-unix-socket-path=/var/run/mysqld/mysqld.sock \
    --enable-local-infile \
    --with-plugins=partition,ftexample,archive,blackhole,federated,heap,innobase,innodb_plugin,ndbcluster
  make
}

package_libmysqlclient(){
  pkgdesc="MySQL client libraries"
  depends=('openssl' 'zlib' 'gcc-libs' 'tcp_wrappers')
  
  cd "${srcdir}/${pkgbase}-${pkgver}"
  for dir in include libmysql libmysql_r libmysqld; do
    make -C ${dir} DESTDIR="${pkgdir}" install
  done
  install -m644 include/*.h "${pkgdir}/usr/include/mysql/"

  install -d "${pkgdir}/usr/bin"
  install -m755 scripts/mysql_config "${pkgdir}/usr/bin/"
  install -d "${pkgdir}/usr/share/man/man1"
  for man in mysql_config mysql_client_test_embedded mysqltest_embedded; do
    install -m644 man/$man.1 "${pkgdir}/usr/share/man/man1/$man.1"
  done
  
  # create library symlinks in /usr/lib
  ln -sf mysql/libmysqlclient.so.16 ${pkgdir}/usr/lib/libmysqlclient.so.16
  ln -sf libmysqlclient.so.16 ${pkgdir}/usr/lib/libmysqlclient.so
  ln -sf libmysqlclient.so.16 ${pkgdir}/usr/lib/libmysqlclient.so.1
  ln -sf mysql/libmysqlclient_r.so.16  ${pkgdir}/usr/lib/libmysqlclient_r.so.16
  ln -sf libmysqlclient_r.so.16 ${pkgdir}/usr/lib/libmysqlclient_r.so
  ln -sf libmysqlclient_r.so.16 ${pkgdir}/usr/lib/libmysqlclient_r.so.1
}

package_mysql-clients(){
  pkgdesc="MySQL client tools"
  depends=('libmysqlclient')
  
  cd "${srcdir}/${pkgbase}-${pkgver}"
  make -C client DESTDIR="${pkgdir}" install

  # install man pages
  install -d "${pkgdir}/usr/share/man/man1"
  for man in mysql mysqladmin mysqlcheck mysqldump mysqlimport mysqlshow mysqlslap; do
    install -m644 man/$man.1 "${pkgdir}/usr/share/man/man1/$man.1"
  done

  # provided by mysql
  rm "${pkgdir}"/usr/bin/{mysql_upgrade,mysqlbinlog,mysqltest}
}

package_mysql(){
  pkgdesc="A fast SQL database server"
  backup=('etc/mysql/my.cnf')
  install=mysql.install
  depends=('mysql-clients')
  optdepends=('perl-dbi' 'perl-dbd-mysql')

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make DESTDIR=${pkgdir} install

  install -Dm644 ${srcdir}/my.cnf ${pkgdir}/etc/mysql/my.cnf
  install -Dm755 ${srcdir}/mysqld ${pkgdir}/etc/rc.d/mysqld

  # provided by libmysqlclient
  rm ${pkgdir}/usr/bin/{mysql_config,mysql_client_test_embedded,mysqltest_embedded}
  rm ${pkgdir}/usr/lib/mysql/libmysqlclient*
  rm ${pkgdir}/usr/lib/mysql/libmysqld.a
  rm ${pkgdir}/usr/include/mysql/*.h
  rm ${pkgdir}/usr/share/man/man1/{mysql_config,mysql_client_test_embedded,mysqltest_embedded}.1
  
  # provided by mysql-clients
  rm ${pkgdir}/usr/bin/{mysql,mysqladmin,mysqlcheck,mysqldump,mysqlimport,mysqlshow,mysqlslap}
  rm ${pkgdir}/usr/share/man/man1/{mysql,mysqladmin,mysqlcheck,mysqldump,mysqlimport,mysqlshow,mysqlslap}.1

  # not needed
  rm -r ${pkgdir}/usr/{mysql-test,sql-bench}
  rm ${pkgdir}/usr/lib/mysql/plugin/*.a
}
