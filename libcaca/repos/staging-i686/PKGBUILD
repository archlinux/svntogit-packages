# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libcaca
pkgver=0.99.beta18
pkgrel=3
pkgdesc="Color AsCii Art library"
arch=('i686' 'x86_64')
license=('custom:WTFPL')
url="http://caca.zoy.org/wiki/libcaca"
depends=('imlib2' 'ncurses')
options=(!emptydirs)
source=(http://libcaca.zoy.org/files/libcaca/${pkgname}-${pkgver}.tar.gz)
md5sums=('93d35dbdb0527d4c94df3e9a02e865cc')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --mandir=/usr/share/man \
    --enable-shared --disable-doc \
    --disable-cxx --disable-gl --disable-csharp --disable-static
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/libcaca/COPYING"
}
