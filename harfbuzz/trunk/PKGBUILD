# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=(harfbuzz)
pkgname=(harfbuzz harfbuzz-icu)
pkgver=0.9.19
pkgrel=1
pkgdesc="OpenType text shaping engine"
arch=(i686 x86_64)
url="http://www.freedesktop.org/wiki/Software/HarfBuzz"
license=(MIT)
makedepends=(glib2 freetype2 graphite cairo icu)
options=(!libtool)
source=(http://www.freedesktop.org/software/harfbuzz/release/${pkgbase}-${pkgver}.tar.bz2)
sha256sums=('d2da0f060d47f6ad9de8c8781bb21fa4b9eae8ea1cd1e956b814095baa002f35')

build() {
  cd $pkgbase-$pkgver
  ./configure --prefix=/usr \
    --with-glib --with-freetype --with-cairo --with-icu --with-graphite2
  make
}

package_harfbuzz() {
  depends=(glib2 freetype2 graphite)
  optdepends=('cairo: hb-view program')

  cd $pkgbase-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/harfbuzz/COPYING"

# Split harfbuzz-icu
  mkdir -p ../hb-icu/usr/{include/harfbuzz,lib/pkgconfig}; cd ../hb-icu
  mv "$pkgdir"/usr/lib/libharfbuzz-icu* ./usr/lib
  mv "$pkgdir"/usr/lib/pkgconfig/harfbuzz-icu.pc ./usr/lib/pkgconfig
  mv "$pkgdir"/usr/include/harfbuzz/hb-icu.h ./usr/include/harfbuzz
}

package_harfbuzz-icu(){
  pkgdesc="$pkgdesc (ICU integration)"
  depends=(harfbuzz icu)

  mv hb-icu/* "$pkgdir"

  install -Dm644 $pkgbase-$pkgver/COPYING "$pkgdir/usr/share/licenses/harfbuzz-icu/COPYING"
}
