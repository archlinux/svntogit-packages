# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=telepathy-kde-approver
_pkgname=ktp-approver
pkgver=17.07.80
pkgrel=1
pkgdesc='KDE Channel Approver for Telepathy'
arch=('i686' 'x86_64')
url='https://community.kde.org/Real-Time_Communication_and_Collaboration'
license=('GPL')
groups=('kde-applications' 'kdenetwork' 'telepathy-kde')
depends=('telepathy-qt' 'knotifications' 'kservice')
makedepends=('extra-cmake-modules' 'kdoctools' 'python')
source=("https://download.kde.org/unstable/applications/${pkgver}/src/$_pkgname-${pkgver}.tar.xz"{,.sig})
sha256sums=('e52b1afd802bc204a255faee0db4903acefa21f7bee7b6293201db53f6c883ee'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$_pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE_INSTALL_LIBDIR=lib \
    -DSYSCONF_INSTALL_DIR=/etc 
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
