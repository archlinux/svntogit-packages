# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=fvwm
pkgver=2.6.1
pkgrel=3
pkgdesc="A multiple large virtual desktop window manager originally derived from twm"
arch=('i686' 'x86_64')
url="http://www.fvwm.org"
license=('GPL' 'custom')
depends=('imlib' 'fribidi' 'perl' 'libstroke' 'libxpm' 'librsvg' 'libxinerama' 'libxcursor')
makedepends=('libxslt')
provides=('fvwm-devel')
conflicts=('fvwm-devel')
replaces=('fvwm-devel')
options=('!emptydirs' '!makeflags')
source=(ftp://ftp.fvwm.org/pub/fvwm/version-${pkgver%%.*}/${pkgname}-${pkgver}.tar.bz2 fvwm.desktop)
md5sums=('fbbbed1bb41fa0878b17ee209947602d'
         'a12c52729a74355838404ee2f5716e0c')
sha1sums=('cf7165d40180b45409e604736066cf9fcc792e36'
          '6fd8fd767f32f88cc8b4743a2ba49bcfcdb32bcb')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib --with-stroke-library
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -D -m644 sample.fvwmrc/{decor_examples,DecorMwm,DecorWin95,new-features,system.*} "${pkgdir}/usr/share/doc/fvwm"
  install -D -m644 ../fvwm.desktop "${pkgdir}/usr/share/xsessions/fvwm.desktop"
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
