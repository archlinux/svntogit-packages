# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=gtkhtml4
_pkgbasename=gtkhtml
pkgver=4.8.1
pkgrel=1
pkgdesc="A lightweight HTML renderer/editor widget for GTK3"
arch=(i686 x86_64)
license=('GPL')
depends=('gtk3' 'enchant' 'iso-codes' 'libsoup')
makedepends=('intltool')
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/$_pkgbasename/${pkgver:0:3}/$_pkgbasename-$pkgver.tar.xz)
sha256sums=('558bf8ec61d9e24cd67301613fc26ab54a495b4b75af5770d1259128aaeefa63')

build() {
    cd "$_pkgbasename-$pkgver"
    ./configure --prefix=/usr --sysconfdir=/etc \
        --libexecdir=/usr/lib/gtkhtml4 \
        --localstatedir=/var --disable-static
    make
}

package() {
    cd "$_pkgbasename-$pkgver"
    make DESTDIR="$pkgdir" install
}
