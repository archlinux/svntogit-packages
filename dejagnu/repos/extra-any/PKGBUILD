# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>

pkgname=dejagnu
pkgver=1.6.2
pkgrel=1
pkgdesc='Framework for testing other programs'
arch=(any)
url='http://www.gnu.org/software/dejagnu/'
license=(GPL)
depends=(sh expect)
source=(https://ftp.gnu.org/gnu/dejagnu/${pkgname}-${pkgver}.tar.gz{,.sig})
md5sums=('e1b07516533f351b3aba3423fafeffd6'
         'SKIP')
validpgpkeys=('152F3EADD335FF629611128567DADC3E3F743649') # Ben Elliston

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
