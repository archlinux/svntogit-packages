# $Id$
# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>

pkgbase=python-logilab-common
pkgname=('python2-logilab-common' 'python-logilab-common')
pkgver=1.1.0
pkgrel=1
pkgdesc="Useful miscellaneous modules used by Logilab projects"
arch=('any')
url="http://www.logilab.org/project/logilab-common"
license=('LGPL')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-six' 'python2-six' 'python-pytz' 'python2-pytz' 'python2-egenix-mx-base')
source=("https://pypi.python.org/packages/source/l/logilab-common/logilab-common-$pkgver.tar.gz")
sha1sums=('fec3fc5fbcb24c9b68dd750af0f1831be51acf89')

prepare() {
  cp -a logilab-common-${pkgver}{,-py3}
}

build() {
  cd logilab-common-${pkgver}
  python2 setup.py build

  cd ../logilab-common-${pkgver}-py3
  python3 setup.py build
}

check() {
  cd logilab-common-${pkgver}
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" python2 bin/pytest

  cd ../logilab-common-${pkgver}-py3
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" python3 bin/pytest || warning "Tests failed"
}

package_python2-logilab-common() {
  depends=('python2')
  optdepends=('python2-six: for logilab.common.testlib')

  cd "${srcdir}"/logilab-common-${pkgver}

  python2 setup.py install --optimize=1 --skip-build --prefix=/usr --root="${pkgdir}"

  mv "${pkgdir}"/usr/bin/pytest{,2}

  # fix permissions ...
  # find "${pkgdir}" -type f -exec chmod +r {} \;
}

package_python-logilab-common() {
  depends=('python')
  optdepends=('python-six: for logilab.common.testlib')

  cd "${srcdir}"/logilab-common-${pkgver}-py3

  python3 setup.py install --optimize=1 --skip-build --prefix=/usr --root="${pkgdir}"

  # fix permissions ...
  # find "${pkgdir}" -type f -exec chmod +r {} \;
}
