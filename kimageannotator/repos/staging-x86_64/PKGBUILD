# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: dracorp aka Piotr Rogoza <piotr.r.public at gmail.com>

pkgname=kimageannotator
pkgver=0.4.1
pkgrel=1
pkgdesc='Tool for annotating images'
arch=(x86_64)
url='https://github.com/DamirPorobic/kImageAnnotator'
license=(GPL)
depends=(qt5-svg kcolorpicker)
makedepends=(cmake qt5-tools)
source=($pkgname-$pkgver.tar.gz::https://github.com/ksnip/kImageAnnotator/archive/v$pkgver.tar.gz)
sha256sums=('64e62f7379a0741f675515e57c21311ebbc6544da8c8a003a5a1ec4964902ac8')

build() {
  cmake -B build -S kImageAnnotator-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_SHARED_LIBS=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}

