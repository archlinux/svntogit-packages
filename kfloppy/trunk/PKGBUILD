# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kfloppy
pkgver=20.12.0
pkgrel=1
pkgdesc='Floppy Formatter'
url='https://kde.org/applications/utilities/kfloppy/'
arch=(x86_64)
license=(GPL LGPL FDL)
depends=(kxmlgui kcompletion hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools)
groups=(kde-applications kde-utilities)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('8b8edbef665820f6b7d270f5034c4ddf04b5b8aabfffdda49d626610a929351a'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
