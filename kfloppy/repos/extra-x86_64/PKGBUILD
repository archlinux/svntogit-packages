# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kfloppy
pkgver=17.04.1
pkgrel=1
pkgdesc='Floppy Formatter'
url='https://kde.org/applications/utilities/kfloppy/'
arch=(i686 x86_64)
license=(GPL LGPL FDL)
depends=(kio hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools python)
conflicts=(kdeutils-kfloppy)
replaces=(kdeutils-kfloppy)
groups=(kde-applications kdeutils)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('6b791ae615774696745dfae1520090591b780ecbf4c1abab80156af471dc5db9'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
