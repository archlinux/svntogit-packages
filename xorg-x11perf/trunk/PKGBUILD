# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-x11perf
pkgver=1.5.3
pkgrel=2
pkgdesc="Simple X server performance benchmarker"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libx11' 'libxmu' 'libxrender' 'libxft' 'libxext')
makedepends=('xorg-util-macros')
groups=('xorg-apps' 'xorg')
source=(http://xorg.freedesktop.org/archive/individual/app/x11perf-${pkgver}.tar.bz2)
sha1sums=('2fd00ba38c36c569751bbe3d60917df2a39e10ba')

build() {
  cd "${srcdir}/x11perf-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/x11perf-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
