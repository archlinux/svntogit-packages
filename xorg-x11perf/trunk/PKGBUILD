# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-x11perf
pkgver=1.6.1
pkgrel=1
pkgdesc="Simple X server performance benchmarker"
arch=(x86_64)
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libx11' 'libxmu' 'libxrender' 'libxft' 'libxext')
makedepends=('xorg-util-macros')
groups=('xorg-apps' 'xorg')
source=(https://xorg.freedesktop.org/archive/individual/app/x11perf-${pkgver}.tar.bz2{,.sig})
sha512sums=('b386151bfe3d7c04ab9953ae1882f3c5b627d6d1afb9a8cfe87c58236a88883fc11bbadb2226b9aa61df84b4fce625a1c9e3d146a124ac17e8d7d605b35d9c0b'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # Alan Coopersmith <alan.coopersmith@oracle.com>

build() {
  cd x11perf-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd x11perf-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
