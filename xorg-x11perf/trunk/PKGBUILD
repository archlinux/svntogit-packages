# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-x11perf
pkgver=1.6.0
pkgrel=2
pkgdesc="Simple X server performance benchmarker"
arch=(x86_64)
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libx11' 'libxmu' 'libxrender' 'libxft' 'libxext')
makedepends=('xorg-util-macros')
groups=('xorg-apps' 'xorg')
source=(https://xorg.freedesktop.org/archive/individual/app/x11perf-${pkgver}.tar.bz2{,.sig})
sha512sums=('a9508c9dd6db2663b5bd30f5e1f5f80b1d22ce5221ae3dc292e746f8e554fda2d3130f7311f2545e20244dceb51e867fe80fec82c9ac5911dcdc90a73b231623'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # Alan Coopersmith <alan.coopersmith@oracle.com>

build() {
  cd x11perf-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd x11perf-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
