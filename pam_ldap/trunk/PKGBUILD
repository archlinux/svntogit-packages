# $Id$
# Maintainer:
# Contributor: Comete <la_comete@tiscali.fr>

pkgname=pam_ldap
pkgver=186
pkgrel=5
pkgdesc="LDAP authentication module for the PAM framework"
arch=('i686' 'x86_64')
url="http://www.padl.com/OSS/pam_ldap.html"
license=('LGPL')
depends=('libldap' 'pam')
backup=('etc/pam_ldap.conf')
options=('!makeflags')
source=("http://www.padl.com/download/${pkgname}-${pkgver}.tar.gz")
md5sums=('58c8689921c5c4578363438acd8503c2')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --libdir=/usr/lib \
      --with-ldap-conf-file=/etc/pam_ldap.conf \
      --mandir=/usr/share/man
  PATH="${PATH}:${srcdir}/${pkgname}-${pkgver}" make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  mv "${pkgdir}/etc/ldap.conf" "${pkgdir}/etc/pam_ldap.conf"
}
