# Maintainer: Justin Davis (juster) <jrcd83@gmail.com>

pkgname=perl-net-server
pkgver=2.010
pkgrel=2
pkgdesc='Extensible, general Perl server engine'
arch=('any')
url="https://metacpan.org/release/Net-Server"
license=('PerlArtistic' 'GPL')
depends=('perl')
options=('!emptydirs')
source=("https://search.cpan.org/CPAN/authors/id/R/RH/RHANDOM/Net-Server-$pkgver.tar.gz")
sha512sums=('b4c1a85263e81c4ad6f32e28acb5176002ad591a1140dca060b706901758f48d3c63c4077606d772540e18fc6fa6772e71d213d0a4940e903c597b0e0f844365')

build() {
  cd Net-Server-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Net-Server-$pkgver
  make test
}

package() {
  cd Net-Server-$pkgver
  make install DESTDIR="$pkgdir"
}
