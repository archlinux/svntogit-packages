# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=akonadi-mime
pkgver=21.12.3
pkgrel=1
pkgdesc='Libraries and daemons to implement basic email handling'
arch=(x86_64)
url='https://kontact.kde.org'
license=(LGPL)
depends=(libakonadi kmime)
makedepends=(extra-cmake-modules kdoctools boost qt5-tools doxygen)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('bbcf7ef38a113765176b5325c7c458562084bfd1a56e6680b8e584f48b5b99ad'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
options=(debug)

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
