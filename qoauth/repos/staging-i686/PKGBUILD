# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Peter Richard Lewis <plewis@aur.archlinux.org>

pkgname=qoauth
pkgver=1.0.1
pkgrel=6
pkgdesc="Support with OAuth-powered network services"
url='http://github.com/ayoy/qoauth'
license=('GPL')
arch=('i686' 'x86_64')
depends=('qca-qt4')
makedepends=('cmake' 'automoc4')
source=("http://files.ayoy.net/qoauth/release/current/src/${pkgname}-${pkgver}-src.tar.bz2")
md5sums=('bcb6d01e6c9a6fb22099c9e0f5889578')

prepare() {
  cd ${pkgname}-${pkgver}-src

  # Install libraries in /usr/lib/
  sed -i 's|lib64|lib|' src/src.pro
}

build() {
  cd ${pkgname}-${pkgver}-src
  qmake-qt4
  make
}

package() {
  cd ${pkgname}-${pkgver}-src
  make INSTALL_ROOT="${pkgdir}" install
}
