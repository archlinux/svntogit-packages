# $Id$
# Maintainer: Aaron Griffin <aaron@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=cmus
pkgver=2.3.3
pkgrel=1
pkgdesc="A very feature-rich ncurses-based music player"
arch=('i686' 'x86_64')
url="http://cmus.sourceforge.net/"
license=('GPL')
# namcap assured me it was safe to remove ncurses and the like... /shrug
depends=('sh' 'libao' 'libmpcdec' 'ffmpeg' 'flac' 'libmad' 'faad2')
makedepends=('libmp4v2')
optdepends=('libmp4v2')
changelog=ChangeLog
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-v$pkgver.tar.bz2")
md5sums=('220e875e4210a6b54882114ef7094a79')
sha1sums=('ca8c40c86ead8af05a1ca703caa4131217832114')

build() {
  cd "$srcdir/$pkgname-v$pkgver"
  ./configure prefix=/usr CONFIG_ARTS=n
  make
}

package() {
  cd "$srcdir/$pkgname-v$pkgver"
  make DESTDIR="$pkgdir" install
}
