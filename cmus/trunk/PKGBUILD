# $Id$
# Maintainer: Aaron Griffin <aaron@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=cmus
pkgver=2.4.3
pkgrel=1
pkgdesc="A very feature-rich ncurses-based music player"
arch=('i686' 'x86_64')
url="http://cmus.sourceforge.net/"
license=('GPL')
depends=('ncurses')
makedepends=('libao' 'libmpcdec' 'ffmpeg' 'flac' 'libmad' 'faad2' 'libmodplug'
             'libvorbis' 'libpulse' 'wavpack' 'libmp4v2')
optdepends=('alsa-lib: for ALSA output plugin support'
            'libao: for AO output plugin support'
            'libpulse: for PulseAudio output plugin support'
            'ffmpeg: for ffmpeg input plugin support'
            'libmodplug: for modplug input plugin support'
            'faad2: for input AAC plugin support'
            'libmad: for mp3 input plugin support'
            'libmpcdec: for musepack input plugin support'
            'wavpack: for wavpack input plugin support'
            'libvorbis: for vorbis input plugin support'
            'flac: for flac input plugin support'
            'libmp4v2: for mp4 input plugin support')
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-v$pkgver.tar.bz2")
sha1sums=('ba63a417b0df42fa9a4850dd8b922a95a426f7f5')

build() {
  cd "$srcdir/$pkgname-v$pkgver"
  ./configure prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-v$pkgver"
  make DESTDIR="$pkgdir" install
  install -D -m 644 contrib/_cmus "$pkgdir/usr/share/zsh/site-functions/_cmus"
}
