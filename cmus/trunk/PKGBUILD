# $Id$
# Maintainer: Aaron Griffin <aaron@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=cmus
pkgver=2.3.0
pkgrel=2
pkgdesc="A very feature-rich ncurses-based music player"
arch=('i686' 'x86_64')
url="http://cmus.sourceforge.net/"
license=('GPL')
# namcap assured me it was safe to remove ncurses and the like... /shrug
depends=('sh' 'libao' 'libmpcdec' 'ffmpeg' 'flac' 'libmad' 'faad2')
makedepends=('libmp4v2')
optdepends=('libmp4v2')
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-v$pkgver.tar.bz2")
md5sums=('8039df4b08b9cbc2e050ab2922e6d99d')

build() {
  cd $srcdir/$pkgname-v$pkgver
  sed -i 's|ffmpeg/avcodec.h|libavcodec/avcodec.h|' ffmpeg.c || return 1
  sed -i 's|ffmpeg/avformat.h|libavformat/avformat.h|' ffmpeg.c || return 1
  sed -i 's|ffmpeg/avio.h|libavformat/avio.h|' ffmpeg.c || return 1

  export CONFIG_ARTS=n
  ./configure prefix=/usr || return 1
  make || return 1
}

package() {
  cd $srcdir/$pkgname-v$pkgver
  make DESTDIR=$pkgdir install || return 1
}
