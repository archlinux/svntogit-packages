# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdebase-runtime
pkgver=4.9.90
pkgrel=1
pkgdesc="Plugins and applications necessary for the running of KDE applications"
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kde-runtime'
license=('GPL' 'LGPL')
depends=('kactivities' 'smbclient' 'libssh' 'libcanberra' 'oxygen-icons'
         'xorg-xauth')
makedepends=('pkg-config' 'cmake' 'automoc4' 'kdepimlibs' 'openslp' 'doxygen'
             'networkmanager')
optdepends=('kdepimlibs: to generate drkonqi reports'
            'htdig: to build the search index in khelpcenter'
            'rarian: needed by khelpcenter'
            'gdb: drkonq crash handler')
install="${pkgname}.install"
source=("http://download.kde.org/unstable/${pkgver}/src/kde-runtime-${pkgver}.tar.xz")
sha1sums=('2cc69d6f462a35fd3d696fd77e1d54daedf27d06')

build() {
    cd "${srcdir}"
	mkdir build
	cd build
	cmake ../kde-runtime-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_INSTALL_PREFIX=/usr \
        -DWITH_QNtrack=OFF
	make
}

package() {
	cd "$srcdir/build"
	make DESTDIR="$pkgdir" install
	rm -f "${pkgdir}/usr/share/icons/hicolor/index.theme"

    ln -sf /usr/lib/kde4/libexec/kdesu "${pkgdir}/usr/bin/"
}
