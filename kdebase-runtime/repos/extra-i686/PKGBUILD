# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdebase-runtime
pkgver=4.8.1
pkgrel=1
pkgdesc="KDE Base Runtime Environment"
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL')
depends=('kdelibs' 'ntrack' 'smbclient' 'libssh' 'libcanberra' 'oxygen-icons'
        'xorg-xauth')
makedepends=('pkg-config' 'cmake' 'automoc4' 'kdepimlibs' 'openslp' 'doxygen'
             'xine-lib')
optdepends=('htdig: to build the search index in khelpcenter'
            'rarian: needed by khelpcenter'
            'gdb: drkonq crash handler')
install="${pkgname}.install"
source=("http://download.kde.org/stable/${pkgver}/src/kde-runtime-${pkgver}.tar.xz")
sha1sums=('82b57dc38335716fb382a665b536a9aece4684d6')

build() {
    cd "${srcdir}"
	mkdir build
	cd build
	cmake ../kde-runtime-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package() {
	cd "$srcdir/build"
	make DESTDIR="$pkgdir" install
	rm -f "${pkgdir}/usr/share/icons/hicolor/index.theme"
	ln -sf /usr/lib/kde4/libexec/kdesu "${pkgdir}/usr/bin/"

    # Fix Python2 path
    sed -i 's|/usr/bin/env python|/usr/bin/env python2|' \
      "${pkgdir}"/usr/bin/nepomuk-simpleresource-rcgen
}
