# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdebase-runtime
pkgver=15.08.3
pkgrel=1
pkgdesc="Plugins and applications necessary for the running of KDE applications"
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kde-runtime'
license=('GPL' 'LGPL')
makedepends=('pkg-config' 'cmake' 'automoc4' 'kdepimlibs4' 'openslp' 'doxygen' 'networkmanager' 'boost')
depends=('libkactivities4' 'smbclient' 'libssh' 'libcanberra' 'oxygen-icons' 'xorg-xauth' 'libwebp')
optdepends=('kdepimlibs4: for kwalletd, and is needed by DrKonqi to send crash reports to KDE.org'
            'gdb: needed by DrKonqi to generate backtrace' 'khelpcenter: to display applications documentation')
provides=(notification-daemon)
conflicts=(kdesu4)
replaces=(kdesu4)
install=$pkgname.install
source=("http://download.kde.org/stable/applications/${pkgver}/src/kde-runtime-${pkgver}.tar.xz")
sha1sums=('edc6a2d454aed325b6c8c8f962e67ff8aeb9f8bf')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kde-runtime-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_QNtrack=OFF \
    -DSAMBA_INCLUDE_DIR=/usr/include/samba-4.0 \
    -DWITH_NepomukCore=OFF \
    -DBUILD_khelpcenter=OFF \
    -DBUILD_doc=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

  rm "$pkgdir"/usr/share/icons/hicolor/index.theme
  ln -s /usr/lib/kde4/libexec/kdesu "$pkgdir"/usr/bin/kdesu4

# FS#36668
  chown :nobody "$pkgdir"/usr/lib/kde4/libexec/kdesud
  chmod g+s "$pkgdir"/usr/lib/kde4/libexec/kdesud
}
