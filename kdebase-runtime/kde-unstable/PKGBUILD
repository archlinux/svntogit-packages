# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kdebase-runtime
pkgname=('kdebase-runtime' 'khelpcenter4')
pkgver=15.03.97
pkgrel=2
pkgdesc="Plugins and applications necessary for the running of KDE applications"
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kde-runtime'
license=('GPL' 'LGPL')
makedepends=('kdelibs' 'libkactivities4' 'smbclient' 'libssh' 'libcanberra' 'libwebp'
	'pkg-config' 'cmake' 'automoc4' 'kdepimlibs' 'openslp' 'doxygen' 'networkmanager' 'boost')
source=("http://download.kde.org/unstable/applications/${pkgver}/src/kde-runtime-${pkgver}.tar.xz")
sha1sums=('0e72a7b80400d420176cbb64830836dd5a970942')

prepare() {
  mkdir build

  cd kde-runtime-${pkgver}
}

build() {
  cd build
  cmake ../kde-runtime-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_QNtrack=OFF \
    -DSAMBA_INCLUDE_DIR=/usr/include/samba-4.0 \
    -DWITH_NepomukCore=OFF
  make
}

package_khelpcenter4() {
  pkgdesc=("Application to show KDE Applications' documentation")
  depends=('kdebase-runtime' 'htdig' 'rarian')
  provides=('khelpcenter')
  conflicts=('khelpcenter')

  cd build/khelpcenter
  make DESTDIR="$pkgdir" install
}

package_kdebase-runtime() {
  depends=("kdelibs" 'kactivities' 'libkactivities4' 'smbclient' 'libssh' 'libcanberra'
         'oxygen-icons' 'xorg-xauth' 'libwebp')
  optdepends=('kdepimlibs: for kwalletd, and is needed by DrKonqi to send crash reports to KDE.org'
            'gdb: needed by DrKonqi to generate backtrace' 'khelpcenter: to display applications documentation')
  provides=('notification-daemon')
  install=$pkgname.install

  cd build
  make DESTDIR="$pkgdir" install

  rm "${pkgdir}"/usr/share/icons/hicolor/index.theme

  ln -sf /usr/lib/kde4/libexec/kdesu "${pkgdir}/usr/bin/"

  # FS#36668
  chown :nobody "${pkgdir}"/usr/lib/kde4/libexec/kdesud
  chmod g+s "${pkgdir}"/usr/lib/kde4/libexec/kdesud

# Remove khelpcenter
  cd "$pkgdir"
  find -name '*khelpcenter*' | xargs rm -fr
  find -name 'khc*' | xargs rm -fr
  rm usr/share/applications/kde4/Help.desktop
}
