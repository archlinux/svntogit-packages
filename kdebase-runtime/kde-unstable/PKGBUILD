# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdebase-runtime
pkgver=4.10.90
pkgrel=1
pkgdesc="Plugins and applications necessary for the running of KDE applications"
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kde-runtime'
license=('GPL' 'LGPL')
depends=("kdelibs>=${pkgver}" 'kactivities' 'smbclient' 'libssh' 'libcanberra' 'oxygen-icons'
         'xorg-xauth')
makedepends=('pkg-config' 'cmake' 'automoc4' 'kdepimlibs' 'openslp' 'doxygen'
             'networkmanager')
optdepends=('kdepimlibs: needed by DrKonqi to send crash reports to KDE.org'
            'gdb: needed by DrKonqi to generate backtrace'
            'htdig: to build the search index in the KHelpCenter'
            'rarian: needed by KHelpCenter')
install="${pkgname}.install"
source=("http://download.kde.org/unstable/${pkgver}/src/kde-runtime-${pkgver}.tar.xz")
sha1sums=('6835354be548a1389f895c63c24e0c8b6066165a')

prepare() {
    mkdir build
}

build() {
	cd build
	cmake ../kde-runtime-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DKDE4_BUILD_TESTS=OFF \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DWITH_QNtrack=OFF \
        -DSAMBA_INCLUDE_DIR=/usr/include/samba-4.0
	make
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
	rm -f "${pkgdir}/usr/share/icons/hicolor/index.theme"

	ln -sf /usr/lib/kde4/libexec/kdesu "${pkgdir}/usr/bin/"
}
