# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=libwnck3
pkgver=43.0
pkgrel=2
pkgdesc="Library to manage X windows and workspaces (via pagers, tasklists, etc.)"
url="https://gitlab.gnome.org/GNOME/libwnck"
arch=(x86_64)
license=(LGPL)
depends=(gtk3 startup-notification libxres)
makedepends=(gobject-introspection gtk-doc git meson)
options=(debug)
_commit=6147abd0c1fe35b96e1fbb63fa94d23f91fb2934  # tags/43.0^0
source=("git+https://gitlab.gnome.org/GNOME/libwnck.git#commit=$_commit"
        0001-handle-avoid-segfault-in-invalidate_icons.patch)
b2sums=('SKIP'
        'f7c2c5261aab62e424cdb84c1a10b831423c82725472adcec8c41b9e6e55f65bf8705451ee7774824d8c05af3332ba6d34a0be7649e389f8942b759f9849fa68')

pkgver() {
  cd libwnck
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd libwnck

  # https://bugs.archlinux.org/task/76007
  git apply -3 ../0001-handle-avoid-segfault-in-invalidate_icons.patch
}

build() {
  arch-meson libwnck build -D gtk_doc=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
