# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Eduard "bekks" Warkentin <eduard.warkentin@gmail.com>
# Contributor: Henning Garus <henning.garus@gmail.com>

pkgname=xdelta3
pkgver=3.0.4
pkgrel=1
pkgdesc="A diff utility which works with binary files"
arch=('i686' 'x86_64')
url="http://xdelta.org/"
license=('GPL')
depends=('xz')
source=(http://xdelta.googlecode.com/files/$pkgname-$pkgver.tar.gz)
md5sums=('c04f7fd9da45741a0095438cfe527b89')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  ./xdelta3 test
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="${pkgdir}" install
}
