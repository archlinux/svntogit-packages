# $Id: $
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Eduard "bekks" Warkentin <eduard.warkentin@gmail.com>
# Contributor: Henning Garus <henning.garus@gmail.com>

pkgname=xdelta3
pkgver=3.0.0
pkgrel=1
pkgdesc="A diff utility which works with binary files"
arch=('i686' 'x86_64')
url="http://xdelta.org/"
license=('GPL')
makedepends=('python2')
optdepends=('python2: for python modules')
source=(http://xdelta.googlecode.com/files/xdelta$pkgver.tar.gz
        xdelta3-makefile.patch)
md5sums=('5fe038be3a266d2a7913e10d1cec6d88'
         '378e9a735d92fb2fc695ed9ce2fad2d5')

build() {
  cd $srcdir/xdelta$pkgver
  patch -Np1 -i $srcdir/xdelta3-makefile.patch
  sed -i "/PYVER = /s/2.6/2.7/" Makefile
  
  make xdelta3
  make xdelta3module.so
}

package() {
  cd $srcdir/xdelta$pkgver
  
  install -D xdelta3 $pkgdir/usr/bin/xdelta3
  python2 ./setup.py install --root ${pkgdir}
  install -m644 {xdelta3.py,xdelta3module.so} ${pkgdir}/usr/lib/python2.7/site-packages
}

