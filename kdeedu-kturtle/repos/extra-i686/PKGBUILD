# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-kturtle
pkgver=4.14.2
pkgrel=1
pkgdesc="Educational Programming Environment"
url="http://kde.org/applications/education/kturtle/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/kturtle-${pkgver}.tar.xz")
sha1sums=('4db5b9ab7fe104237672add66822944c1eda7f8e')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../kturtle-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
