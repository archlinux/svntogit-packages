# $Id$
# Maintainer: Dave Reisner <dreisner@archlinux.org>
# Contributor: feydaykyn <feydaykyn@yahoo.fr>

pkgname=leveldb
pkgver=1.17
pkgrel=1
pkgdesc="A fast and lightweight key/value database library"
arch=('i686' 'x86_64')
url="https://code.google.com/p/leveldb/"
license=('BSD')
depends=('gperftools' 'snappy')
makedepends=('git')
source=("git+https://code.google.com/p/leveldb/#tag=$pkgver")
sha1sums=('SKIP')

build() {
  make -C "$pkgname"
}

check() {
  make -C "$pkgname" check
}

package() {
  cd "$pkgname"

  install -dm755 \
      "$pkgdir"/usr/{include/leveldb,lib} \
      "$pkgdir"/usr/share/doc/"$pkgname"

  # libraries
  install -m644 -t "$pkgdir/usr/lib" "libleveldb.so.${pkgver%.?*}" libleveldb.a
  cp -P libleveldb.so{,.1} "$pkgdir/usr/lib"

  # headers
  install -m644 -t "$pkgdir/usr/include/leveldb" include/leveldb/*

  # documentation
  cp -r doc/* "$pkgdir/usr/share/doc/$pkgname"

  # license
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
