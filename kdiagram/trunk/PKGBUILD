# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kdiagram
pkgver=2.6.1
pkgrel=1
pkgdesc="Powerful libraries for creating business diagrams"
arch=(x86_64)
url="http://www.kde.org/"
license=(GPL2)
depends=(qt5-svg)
makedepends=(extra-cmake-modules qt5-tools doxygen)
source=("http://download.kde.org/stable/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('bde71ca5ef70b3077696d56b24e0e1f69ae255c0bf7bf453e9596afdd25dcdb0'
            'SKIP')
validpgpkeys=(42A60D06F449E895F40F09C01638B8A616108B8A) # Dag Andersen <danders@get2net.dk>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
