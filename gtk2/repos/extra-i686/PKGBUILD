# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gtk2
pkgver=2.12.10
pkgrel=2
pkgdesc="The GTK+ Toolkit (v2)"
arch=(i686 x86_64)
url="http://www.gtk.org/"
install=gtk2.install
depends=('atk>=1.22.0' 'pango>=1.20.3' 'libxcursor' 'libxinerama' 'libxrandr>=1.2.1' 'libxi' 'libcups>=1.3.7' 'libxcomposite' 'libxdamage')
makedepends=('pkgconfig')
replaces=('gtkprint-cups')
conflicts=('gtkprint-cups')
options=('!libtool')
backup=(etc/gtk-2.0/gtkrc)
license=('LGPL')
source=(http://ftp.gnome.org/pub/gnome/sources/gtk+/2.12/gtk+-${pkgver}.tar.bz2
	gtkclipboard-check.patch
	gtk2-windowpos.patch)
md5sums=('5fcbdd2254eedb6b9ae828afc01d9eaa'
         '4025d3c15d6c6f73a032f403ffd4ff1c'
	 'bbff943e597f0000b9d47cde0ffba289')

build() {
  cd ${startdir}/src/gtk+-${pkgver}
  # Workaround patch for flash
  patch -Np1 -i ${startdir}/src/gtkclipboard-check.patch || return 1

  # bgo 536757 / FS#10613
  patch -Np0 -i ${startdir}/src/gtk2-windowpos.patch || return 1

  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var --with-xinput=xfree \
	      --with-included-loaders=png || return 1
  make || return 1
  make DESTDIR=${startdir}/pkg install || return 1

  echo 'gtk-fallback-icon-theme = "gnome"' > ${startdir}/pkg/etc/gtk-2.0/gtkrc || return 1
}
