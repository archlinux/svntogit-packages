# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Dave Reisner <dreisner@archlinux.org>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Douglas Soares de Andrade
# Contributor: Roberto Alsina <ralsina@kde.org>

pkgname=varnish
pkgver=6.2.0
pkgrel=2
pkgdesc="High-performance HTTP accelerator"
arch=('x86_64')
url="https://www.varnish-cache.org/"
license=('BSD')
depends=('gcc' 'libedit' 'pcre' 'libnsl')
makedepends=('python-docutils' 'python-sphinx')
optdepends=('python: needed for vmod development')
backup=('etc/varnish/default.vcl')
install=$pkgname.install
source=("https://varnish-cache.org/_downloads/varnish-$pkgver.tgz"
        varnish-vcl-reload
        varnish.service
        varnish.sysusers)
sha256sums=('c37af353aca25a83d22f9c5ce0ae800fe433e4d02e1457e02886a5849f988e53'
            '0369e3e735e4c6150f08677df8b7cdae1a36ea75fd0e03734abe814a94312f80'
            '49b48981ccb97aee77ef59373db3856652d7badc909f2504df01970d834183f2'
            'b58dd6b00eb81c75e4bb30421b85b2be88c049d5b72a8ee553ba690a5414972b')

prepare() {
  cd "varnish-$pkgver"

  ./autogen.sh
}

build() {
  cd "varnish-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var/lib \
    --sbindir=/usr/bin

  make
}

check() {
  cd "varnish-$pkgver"

  make check
}

package() {
  cd "varnish-$pkgver"

  make DESTDIR="$pkgdir" install

  install -m755 "$srcdir/varnish-vcl-reload" "$pkgdir/usr/bin"
  install -Dm644 "$srcdir/$pkgname.service" "$pkgdir/usr/lib/systemd/system/$pkgname.service"

  # create varnish:varnish user/group
  install -Dm644 "$srcdir/$pkgname.sysusers" "$pkgdir/usr/lib/sysusers.d/varnish.conf"

  # config
  install -Dm644 "etc/example.vcl" "$pkgdir/etc/varnish/default.vcl"
  install -Dm644 "etc/builtin.vcl" "$pkgdir/usr/share/doc/varnish/builtin.vcl"

  # license
  install -Dm644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
