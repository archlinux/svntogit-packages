# Maintainer: Dave Reisner <dreisner@archlinux.org>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Douglas Soares de Andrade
# Contributor: Roberto Alsina <ralsina@kde.org>

pkgname=varnish
pkgver=5.2.1
pkgrel=3
pkgdesc="High-performance HTTP accelerator"
arch=('x86_64')
url="https://www.varnish-cache.org/"
license=('BSD')
depends=('gcc' 'libedit' 'pcre' 'libnsl')
makedepends=('python-docutils')
optdepends=('python: needed for vmod development')
backup=('etc/varnish/default.vcl')
options=('!buildflags')
install=$pkgname.install
source=("https://github.com/varnishcache/varnish-cache/archive/varnish-$pkgver.tar.gz"
        hack-up-vcstools-to-work-with-python-2-and-3.patch
        varnish-vcl-reload
        varnish.service
        varnish.sysusers)
sha256sums=('f44decf0f382a2ac76aaaa2478167a6b41909a3f6179eeefce402d19e694aba0'
            '97a00962474a5bf97fd71e4c0e792861157aa8ac872fc9c5636b763f11445e8c'
            '0369e3e735e4c6150f08677df8b7cdae1a36ea75fd0e03734abe814a94312f80'
            '49b48981ccb97aee77ef59373db3856652d7badc909f2504df01970d834183f2'
            'b58dd6b00eb81c75e4bb30421b85b2be88c049d5b72a8ee553ba690a5414972b')

prepare() {
  cd "varnish-cache-varnish-$pkgver"

  patch -Np1 <"$srcdir/hack-up-vcstools-to-work-with-python-2-and-3.patch"

  ./autogen.sh
}

build() {
  cd "varnish-cache-varnish-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var/lib \
    --sbindir=/usr/bin

  make
}

check() {
  cd "varnish-cache-varnish-$pkgver"

  # Sometimes fails on i686 in address remapping test. Not sure if flaky
  # test or otherwise...
  make check
}

package() {
  cd "varnish-cache-varnish-$pkgver"

  make DESTDIR="$pkgdir" install

  install -m755 "$srcdir/varnish-vcl-reload" "$pkgdir/usr/bin"
  install -Dm644 "$srcdir/$pkgname.service" "$pkgdir/usr/lib/systemd/system/$pkgname.service"

  # create varnish:varnish user/group
  install -Dm644 "$srcdir/$pkgname.sysusers" "$pkgdir/usr/lib/sysusers.d/varnish.conf"

  # config
  install -Dm644 "etc/example.vcl" "$pkgdir/etc/varnish/default.vcl"
  install -Dm644 "etc/builtin.vcl" "$pkgdir/usr/share/doc/varnish/builtin.vcl"

  # license
  install -Dm644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
