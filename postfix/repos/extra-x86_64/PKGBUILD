# $Id$
# Contributor: Jeff Brodnax <tullyarcher@bellsouth.net>
# Contributor: Paul Mattal <paul@archlinux.org>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=postfix
pkgver=2.9.3
pkgrel=2
pkgdesc='Fast, easy to administer, secure mail server'
url='http://www.postfix.org/'
license=('custom')
arch=('i686' 'x86_64')
depends=('pcre' 'libsasl' 'libldap' 'db'
         'libmysqlclient' 'postgresql-libs' 'sqlite' 'tinycdb')
backup=('etc/postfix/'{access,aliases,canonical,generic,header_checks,main.cf,master.cf,relocated,transport,virtual})
source=("ftp://ftp.porcupine.org/mirrors/postfix-release/official/${pkgname}-${pkgver}.tar.gz" \
        'aliases.patch' \
        'rc.d')
sha1sums=('fd6c1ba3bbceb1a29b95655a742a2c547e11ca1d'
          '5fc3de6c7df1e5851a0a379e825148868808318b'
          '40c6be2eb55e6437a402f43775cdb3d22ea87a66')

provides=('smtp-server' 'smtp-forwarder')
conflicts=('smtp-server' 'smtp-forwarder')

install=install

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make makefiles DEBUG='' CCARGS=' \
		-DUSE_SASL_AUTH -DUSE_CYRUS_SASL -I/usr/include/sasl \
		-DHAS_LDAP \
		-DUSE_TLS \
		-DHAS_MYSQL -I/usr/include/mysql \
		-DHAS_PGSQL -I/usr/include/postgresql \
		-DHAS_SQLITE \
		-DHAS_CDB \
	' AUXLIBS=' \
		-lsasl2 \
		-lldap -llber \
		-lssl -lcrypto \
		-lmysqlclient -lz -lm \
		-lpq \
		-lsqlite3 -lpthread \
		-lcdb \
	' OPT="${CFLAGS} ${LDFLAGS}"

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	sh postfix-install -non-interactive \
		install_root="${pkgdir}" \
		daemon_directory="/usr/lib/${pkgname}" \
		sample_directory="/etc/${pkgname}/sample" \
		manpage_directory="/usr/share/man" \
		readme_directory="/usr/share/doc/${pkgname}" \

	install -D -m755 ../rc.d "${pkgdir}/etc/rc.d/${pkgname}"
	install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	sed 's/^\(\$manpage[^:]*\):/\1.gz:/' -i "${pkgdir}/usr/lib/${pkgname}/postfix-files"
	cd "${pkgdir}"; patch -p0 -i "${srcdir}"/aliases.patch
}
