# $Id$
# Contributor: Jeff Brodnax <tullyarcher@bellsouth.net>
# Maintainer: Paul Mattal <paul@archlinux.org>
pkgname=postfix
pkgver=2.7.2
pkgrel=1
pkgdesc="Secure, fast, easy to administer drop in replacement for Sendmail (MTA)"
url="http://www.postfix.org/"
arch=('i686' 'x86_64')
license=('custom')
depends=('pcre' 'libsasl' 'libmysqlclient' 'postgresql-libs>=8.4' 'libldap>=2.4' 'db')
backup=(etc/postfix/aliases etc/postfix/virtual etc/postfix/relocated \
        etc/postfix/access etc/postfix/header_checks etc/postfix/transport \
        etc/postfix/generic etc/postfix/canonical \
        etc/postfix/main.cf etc/postfix/master.cf)
source=("ftp://ftp.porcupine.org/mirrors/postfix-release/official/${pkgname}-${pkgver}.tar.gz" \
        'aliases.patch' \
        "${pkgname}")
sha1sums=('2415c63c98ba0e0273bcb490ee7753a3891f5a73'
          '5fc3de6c7df1e5851a0a379e825148868808318b'
          '6f41e9ce5c0125fbd4eb016464c6ad1fd18eccea')

provides=('smtp-server' 'smtp-forwarder')
replaces=('postfix-mysql' 'postfix-pgsql')
conflicts=('postfix-mysql' 'postfix-pgsql' 'smtp-server' 'smtp-forwarder')

install="${pkgname}.install"

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	
	make makefiles \
		CCARGS="-DUSE_SASL_AUTH -I/usr/include/sasl \
			-DUSE_CYRUS_SASL \
			-DHAS_LDAP \
			-DUSE_TLS \
			-DHAS_MYSQL -I/usr/include/mysql \
			-DHAS_PGSQL -I/usr/include/postgresql" \
		AUXLIBS="-lsasl2 -lssl -lcrypto -lldap -llber -lmysqlclient -lz -lm -lpq"
	make OPT="${CFLAGS}"
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	sh postfix-install -non-interactive \
		install_root="${pkgdir}" \
		daemon_directory="/usr/lib/${pkgname}" \
		sample_directory="/etc/${pkgname}/sample" \
		manpage_directory="/usr/share/man"

	install -D -m755 "../${pkgname}" "${pkgdir}/etc/rc.d/${pkgname}"
	install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

	cd ${pkgdir}
	patch -p0 < "${srcdir}"/aliases.patch
}
