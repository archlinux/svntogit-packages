# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Travis Willard <travisw@wmpub.ca> 
# Contributor: Denis (dtonator@gmail.com)

pkgname=xmoto
pkgver=0.5.1
pkgrel=3
pkgdesc="A challenging 2D motocross platform game, where physics play an important role."
arch=('i686' 'x86_64')
url="http://xmoto.tuxfamily.org"
license=('GPL')
depends=('bzip2' 'libjpeg' 'libpng' 'lua' 'sdl_mixer' 'ode=0.11' 'curl>=7.16.2' 
	 'mesa' 'sqlite3' 'sdl_ttf' 'desktop-file-utils' 'sdl_net')
install=xmoto.install
source=(http://download.tuxfamily.org/${pkgname}/${pkgname}/${pkgver}/${pkgname}-${pkgver}-src.tar.gz
	xmoto_gcc44.patch)
md5sums=('10cb822ec8c2c7e9466806633e69be1f'
         '40bba4510c0188dc6e722d44de5471ef')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # fix gcc4.4 building
  patch -Np0 -i ${srcdir}/xmoto_gcc44.patch || return 1

  # build and install 
  ./configure --prefix=/usr --disable-sdltest

  make || return 1
  make DESTDIR=${pkgdir} mangdir=/usr/share/man/man6 install

  # install desktop file
  install -Dm0644 ${srcdir}/${pkgname}-${pkgver}/extra/xmoto.desktop \
	${pkgdir}/usr/share/applications/xmoto.desktop

  # install icon for desktop file
  install -Dm0644 ${srcdir}/${pkgname}-${pkgver}/extra/xmoto.xpm \
	${pkgdir}/usr/share/pixmaps/xmoto.xpm
}
