# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux,org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=dolphin
pkgver=15.07.90
pkgrel=2
pkgdesc="File Manager"
arch=(i686 x86_64)
url="http://kde.org/applications/system/dolphin/"
license=(LGPL)
depends=(baloo-widgets knewstuff kio-extras ktexteditor kactivities-frameworks)
makedepends=(extra-cmake-modules kdoctools python)
conflicts=('kdebase-dolphin<15.04.3-3')
replaces=(kdebase-dolphin)
groups=(kde-applications kdebase)
source=("http://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"
	"http://download.kde.org/unstable/applications/$pkgver/src/kde-baseapps-$pkgver.tar.xz")
install=$pkgname.install
md5sums=('5a05b3fa09aec381e5a65ce0f037cc32'
         '563037d14e7c0930271946d52bca84a2')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

# Workaround for missing service menus https://bugs.kde.org/show_bug.cgi?id=350769
  install -Dm644 "$srcdir"/kde-baseapps-$pkgver/lib/konq/konq*.desktop "$pkgdir"/usr/share/kservicetypes5/
}
