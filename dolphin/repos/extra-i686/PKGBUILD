# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux,org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=dolphin
pkgver=15.08.3
pkgrel=1
pkgdesc="File Manager"
arch=(i686 x86_64)
url="http://kde.org/applications/system/dolphin/"
license=(LGPL)
depends=(baloo-widgets knewstuff kio-extras ktexteditor kactivities-frameworks kde-templates)
makedepends=(extra-cmake-modules kdoctools python)
optdepends=('kde-cli-tools: For editing file type options' 'ffmpegthumbs: Video thumbnails')
conflicts=('kdebase-dolphin<15.08')
replaces=(kdebase-dolphin)
groups=(kde-applications kdebase)
source=("http://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"
	"http://download.kde.org/stable/applications/$pkgver/src/kde-baseapps-$pkgver.tar.xz")
install=$pkgname.install
md5sums=('14cfa16663881b27a6db452c6af49948'
         '779739bd60107177f1a54b4a9ee42b93')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

# Workaround for missing service menus https://bugs.kde.org/show_bug.cgi?id=350769
  install -Dm644 "$srcdir"/kde-baseapps-$pkgver/lib/konq/konq*.desktop "$pkgdir"/usr/share/kservicetypes5/
}
