# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=ktuberling
pkgver=15.11.80
pkgrel=2
pkgdesc="A simple constructor game suitable for children and adults alike"
url="http://kde.org/applications/games/ktuberling/"
arch=(i686 x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kdegames)
depends=(hicolor-icon-theme libkdegames knotifyconfig)
makedepends=(extra-cmake-modules kdoctools kdelibs4support)
conflicts=(kdegames-ktuberling)
replaces=(kdegames-ktuberling)
install=$pkgname.install
source=("http://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
sha1sums=('8a08a030d3947a848cb1a80cfce7c2e05036477b')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
