# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libxres
pkgver=1.2.0
pkgrel=1
pkgdesc="X11 Resource extension library"
arch=('x86_64')
url="https://xorg.freedesktop.org"
license=('custom')
depends=('libxext')
makedepends=('resourceproto' 'damageproto' 'compositeproto' 'scrnsaverproto' 'xorg-util-macros')
source=("${url}/releases/individual/lib/libXres-${pkgver}.tar.bz2")
sha256sums=('ff75c1643488e64a7cfbced27486f0f944801319c84c18d3bd3da6bf28c812d4')

build() {
  cd "${srcdir}/libXres-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static
  make
}

package() {
  cd "${srcdir}/libXres-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
