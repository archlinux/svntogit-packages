# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=scour
pkgver=0.38.1
pkgrel=2
pkgdesc='An SVG scrubber'
arch=(any)
url=https://github.com/scour-project/scour
license=(Apache)
depends=(
  python-six
)
makedepends=(
  git
  python-setuptools
)
_tag=23835da44a11fcdbb877f11870ffc442f268352c
source=(git+https://github.com/scour-project/scour.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd scour

  git describe --tags | sed -r 's/^v//; s/^0([0-9])/0.\1/'
}

build() {
  cd scour
  python setup.py build
}

package() {
  cd scour
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

# vim: ts=2 sw=2 et:
