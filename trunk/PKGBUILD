# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Thomas Arnhold <thomas at arnhold dot org>

pkgname=libe-book
pkgver=0.1.2
pkgrel=2
pkgdesc="Library for import of reflowable e-book formats."
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/libebook/"
license=('LGPL2.1' 'MPL')
depends=('librevenge' 'libxml2')
makedepends=('gperf' 'boost' 'cppunit' 'doxygen')
source=(https://sourceforge.net/projects/${pkgname//-/}/files//${pkgname}-${pkgver}/${pkgname}-${pkgver}.tar.xz
        boost159.patch)
sha1sums=('950ee08e7a41585636aebb7774fdc6f05817a18c'
          '8b5b88a05db43773aae486c8410c9bb79e0f6d16')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -Np1 -i ../boost159.patch
  autoreconf -vi
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr #--disable-werror
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=$pkgdir install
}

