# $Id$
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgbase=pulseaudio
pkgname=(pulseaudio libpulse)
pkgdesc="A featureful, general-purpose sound server"
pkgver=1.0
pkgrel=4
arch=(i686 x86_64)
url="http://pulseaudio.org/"
license=(GPL LGPL)
makedepends=(libasyncns libcap attr libxtst libsm libsamplerate libtool rtkit
             speex tdb udev dbus-core xcb-util avahi bluez gconf intltool jack
             lirc-utils openssl fftw orc json-c)
options=(!emptydirs !libtool !makeflags)
source=(http://freedesktop.org/software/$pkgbase/releases/$pkgbase-$pkgver.tar.xz
        $pkgbase.xinit version-number.patch dont-require-dbus.patch)
sha256sums=('e63464f8a0975446e2b4cfe768b615a66735c8d092b2366d2bdc7a2a04a249be'
            'a0db6cdc74fbf0ca10e2343c08e1e228f109221c6c0ff91b0bfade5c4bdf03cf'
            '6b067ae9e8da703510fb24eae8ee53f3a7b6a90bcf88941ee50997ad679a4530'
            '68c8ddf7ddb4035405ec61e7aa72e88f1430a30f75a36f31761fbfffa8f7dad4')

build() {
  cd "$srcdir/$pkgbase-$pkgver"

  patch -Np1 -i "$srcdir/version-number.patch"
  patch -Np1 -i "$srcdir/dont-require-dbus.patch"

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --with-database=tdb \
    --disable-hal \
    --disable-tcpwrap \
    --disable-rpath \
    --disable-default-build-tests

  make
}

package_pulseaudio() {
  depends=("libpulse=$pkgver-$pkgrel" rtkit libtool speex tdb udev fftw orc)
  optdepends=('avahi: zeroconf support'
              'bluez: bluetooth support'
              'gconf: configuration through gconf (paprefs)'
              'jack: jack support'
              'lirc-utils: infra-red support'
              'openssl: RAOP support'
              'python2-pyqt: Equalizer GUI (qpaeq)')
  backup=(etc/pulse/{daemon.conf,default.pa,system.pa})
  install=pulseaudio.install
  cd "$srcdir/$pkgbase-$pkgver"
  make DESTDIR="$pkgdir" install

  # Lower resample quality, saves CPU
  sed -e '/resample-method/iresample-method=speex-float-0' \
      -i "$pkgdir/etc/pulse/daemon.conf"

  # Disable cork-request module, can result in e.g. media players unpausing
  # when there's a Skype call incoming
  sed -e 's|/usr/bin/pactl load-module module-x11-cork-request|#&|' \
      -i "$pkgdir/usr/bin/start-pulseaudio-x11"

  # Make some modules optional
  sed -e $'/load-module module-console-kit/{i.nofail\n;a.fail\n;}' \
      -e '/load-module module-jackdbus-detect/s/^/#/' \
      -i "$pkgdir/etc/pulse/default.pa"

  # Python fix
  sed -i '1s:python$:&2:' "$pkgdir/usr/bin/qpaeq"

  install -Dm755 "$srcdir/pulseaudio.xinit" "$pkgdir/etc/X11/xinit/xinitrc.d/pulseaudio"

  rm "$pkgdir/etc/dbus-1/system.d/pulseaudio-system.conf"

### Split libpulse

  mkdir -p "$srcdir"/libpulse/{etc/pulse,usr/{lib,share/man/man5}}

  mv {"$pkgdir","$srcdir/libpulse"}/etc/pulse/client.conf

  mv "$pkgdir"/usr/lib/libpulse{,dsp,-simple,-mainloop-glib}.so* \
     "$pkgdir"/usr/lib/libpulsecommon-*.so \
     "$srcdir/libpulse/usr/lib"

  mv {"$pkgdir","$srcdir/libpulse"}/usr/lib/pkgconfig
  mv {"$pkgdir","$srcdir/libpulse"}/usr/include

  mv {"$pkgdir","$srcdir/libpulse"}/usr/share/man/man5/pulse-client.conf.5
  mv {"$pkgdir","$srcdir/libpulse"}/usr/share/vala
}

package_libpulse() {
  pkgdesc="$pkgdesc (client library)"
  depends=(dbus-core xcb-util libasyncns libcap libxtst libsm libsamplerate json-c)
  optdepends=('alsa-plugins: ALSA support'
              'avahi: zeroconf support')
  backup=(etc/pulse/client.conf)
  mv "$srcdir"/libpulse/* "$pkgdir"
}
