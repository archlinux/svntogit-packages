# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-xwininfo
pkgver=1.1.2
pkgrel=1
pkgdesc="Command-line utility to print information about windows on an X server"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libxcb')
makedepends=('xorg-util-macros' 'libx11')
groups=('xorg-apps' 'xorg')
source=(http://xorg.freedesktop.org/archive/individual/app/xwininfo-${pkgver}.tar.bz2)
sha1sums=('c90b3c289f95474aa8eb6cf1bf4caf3b9dbf82d3')

build() {
  cd "${srcdir}/xwininfo-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/xwininfo-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
