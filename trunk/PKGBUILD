# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=texinfo
pkgver=6.0
pkgrel=1
pkgdesc='GNU documentation system for on-line information and printed output'
arch=('i686' 'x86_64')
url='http://www.gnu.org/software/texinfo/'
license=('GPL3')
groups=('base' 'base-devel')
depends=('ncurses' 'findutils' 'gzip' 'perl' 'sh')
install=texinfo.install
source=(ftp://ftp.gnu.org/pub/gnu/$pkgname/$pkgname-$pkgver.tar.xz{,.sig}
        texinfo-6.0-disable-failing-info-test.patch)
md5sums=('02818e62a5b8ae0213a7ff572991bb50'
         'SKIP'
         '8bfaa21315a8a6184811de8478005d22')
validpgpkeys=('EAF669B31E31E1DECBD11513DDBC579DAB37FBA9') # Gavin Smith

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../texinfo-6.0-disable-failing-info-test.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

check() {
  make -C $pkgname-$pkgver check
}

package() {
  make -C $pkgname-$pkgver DESTDIR="$pkgdir" install
}
