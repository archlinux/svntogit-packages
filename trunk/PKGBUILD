# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=openconnect
pkgver=3.02
pkgrel=1
epoch=1
pkgdesc="Open client for Cisco AnyConnect VPN"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.infradead.org/openconnect.html"
depends=('libxml2' 'openssl' 'libproxy')
options=('!libtool' '!emptydirs')
source=(ftp://ftp.infradead.org/pub/${pkgname}/${pkgname}-${pkgver}.tar.gz)
md5sums=('c12688474f432a6d590958cc1c1ff076')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i "s|/usr/libexec|/usr/lib/networkmanager|" Makefile
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  make DESTDIR="${pkgdir}" LIBDIR=/usr/lib install-lib

  install  -Dm0644 openconnect.8 "${pkgdir}"/usr/share/man/man8/openconnect.8
}
