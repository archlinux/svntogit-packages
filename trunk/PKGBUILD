# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Zuf <kontakt.zuf@gmail.com>
# Contributor: Darwin Bautista <djclue917@gmail.com>
# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=kdenlive
pkgver=23.04.0
pkgrel=2
pkgdesc='A non-linear video editor for Linux using the MLT video framework'
arch=(x86_64)
url='https://apps.kde.org/kdenlive/'
license=(GPL)
groups=(kde-applications kde-multimedia)
depends=(qt5-networkauth knewstuff knotifyconfig kfilemetadata purpose mlt breeze-icons frei0r-plugins)
makedepends=(extra-cmake-modules kdoctools v4l-utils doxygen qt5-tools)
optdepends=('ffmpeg: for FFmpeg plugin'
            'dvgrab: for firewire capture'
            'recordmydesktop: for screen capture'
            'opencv: for motion tracking'
            'plasma-desktop: theme configuration'
            'opentimelineio: timeline export/import'
            'bigsh0t: VR360 effects'
            'mediainfo: for technical clip information'
            'noise-suppression-for-voice: noise suppression effect')
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        https://invent.kde.org/multimedia/kdenlive/-/commit/364e23f6.patch)
makedepends+=(git) # patch is binary
sha256sums=('eba17db62714e56eb9433499d559dab0657677c6876b0735a756b0d9afc48e6a'
            'SKIP'
            'abc63b230a9f98981533640a011c793ccf03f693d78330c19c1dbb6eca08feb4')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

prepare() {
  cd $pkgname-$pkgver
  git apply ../364e23f6.patch # Fix project corruption
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
