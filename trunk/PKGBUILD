# $Id$
# Maintainer:
# Contributor: Thomas Baechler <thomas@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=libdvdread
pkgver=4.1.3
pkgrel=2
pkgdesc="Provides a simple foundation for reading DVD video disks"
arch=(i686 x86_64)
url="http://www.mplayerhq.hu/MPlayer/releases/dvdnav/"
license=('GPL')
depends=('glibc')
makedepends=('libdvdcss')
optdepends=('libdvdcss: for decoding encrypted DVDs')
options=('!libtool')
source=("http://www.mplayerhq.hu/MPlayer/releases/dvdnav/${pkgname}-${pkgver}.tar.bz2"
	'DVDFileStat.patch')
md5sums=('6dc068d442c85a3cdd5ad3da75f6c6e8'
	'7314874e246634019be929a96340437a')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # http://lists.mplayerhq.hu/pipermail/dvdnav-discuss/2009-February/000986.html (FS#19324)
  patch -Np1 -i ${srcdir}/DVDFileStat.patch || return 1

  ./autogen.sh --prefix=/usr || return 1
  make || return 1
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install || return 1
}
