# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=fluidsynth
pkgver=2.2.9
pkgrel=2
pkgdesc="A real-time software synthesizer based on the SoundFont 2 specifications"
arch=(x86_64)
url="https://www.fluidsynth.org/"
license=(LGPL2.1)
groups=(pro-audio)
depends=(glibc sdl2)
makedepends=(alsa-lib cmake dbus doxygen glib2 jack ladspa libinstpatch
libpulse libsndfile portaudio readline systemd-libs)
provides=(libfluidsynth.so soundfont-synthesizer)
backup=(etc/conf.d/$pkgname)
options=(debug)
source=(
  $pkgname-$pkgver.tar.gz::https://github.com/$pkgname/$pkgname/archive/v$pkgver.tar.gz
  $pkgname-2.2.9-user_service_hardening.patch::https://github.com/FluidSynth/fluidsynth/pull/1148/commits/ecd5f01ad3078e391a289563e9a77db015a6bc3a.patch
)
sha512sums=('90f44f6d7a4ed504eea5e2d39d8da2e0072bef746f3d969e367f0bab36a4cff479a98e795dc8b6b58b565fb266c44a137a14e98c7e4c77d1be3b300b8c066600'
            '25ef95db02aa06e4efdca59b744986f30108624141a8d57497b93514ffc5b6a158097fea1ea468e4ffa90614791dfc16fe141d66e5cb36cd76cf1de242fc15d9')
b2sums=('a6b4a19737d01e86a80084cdf88897da9af7d2c50be80fb0227a1d70effb8551667ae27f43ce63591830acfdef31ff0d99d51e3160b9b09c274637b259a82833'
        '453cc16f804dcb04f6ef2639fa32bfd1e99414584dbe3519ed26d73bd0aee2ad7a333e124a350ab02443694f0766473b4faa69246def24ec85af32ba23b3d049')

prepare() {
  # remove sandboxing options only available to systemd system services: https://github.com/FluidSynth/fluidsynth/issues/1147
  patch -Np1 -d $pkgname-$pkgver -i ../$pkgname-2.2.9-user_service_hardening.patch
}

build() {
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=None \
        -DFLUID_DAEMON_ENV_FILE=/etc/conf.d/fluidsynth \
        -Denable-ladspa=ON \
        -Denable-portaudio=ON \
        -DLIB_SUFFIX="" \
        -Wno-dev \
        -B build \
        -S $pkgname-$pkgver
  make -C build VERBOSE=1
}

check() {
  make -C build -k check
}

package() {
  depends+=(libasound.so libdbus-1.so libglib-2.0.so libgmodule-2.0.so
  libgobject-2.0.so libinstpatch-1.0.so libjack.so libportaudio.so
  libpulse-simple.so libreadline.so libsndfile.so libsystemd.so)

  make -C build DESTDIR="$pkgdir" install
  # systemd user unit
  install -vDm 644 build/$pkgname.service -t "$pkgdir/usr/lib/systemd/user/"
  # system-wide configuration file
  install -vDm 644 build/$pkgname.conf "$pkgdir/etc/conf.d/$pkgname"
  # docs
  install -vDm 644 $pkgname-$pkgver/{AUTHORS,ChangeLog,{CONTRIBUTING,README}.md,THANKS,TODO} -t "$pkgdir/usr/share/doc/$pkgname/"
}
