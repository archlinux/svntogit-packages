# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=p11-kit
pkgver=0.23.19
pkgrel=2
pkgdesc="Provides a way to load and enumerate PKCS#11 modules"
arch=(x86_64)
url="https://p11-glue.freedesktop.org"
license=(BSD)
depends=(glibc libtasn1 libffi systemd)
makedepends=(gtk-doc git meson)
provides=(libp11-kit.so)
install=p11-kit.install
source=("git+https://github.com/p11-glue/p11-kit?signed#tag=$pkgver")
sha256sums=('SKIP')
validpgpkeys=('C0F67099B808FB063E2C81117BFB1108D92765AF'  # Stef Walter
              '462225C3B46F34879FC8496CD605848ED7E69871') # Daiki Ueno

prepare() {
  cd p11-kit
}

build() {
  arch-meson p11-kit build \
    --buildtype debugoptimized \
    -D gtk_doc=true \
    -D man=true \
    -D trust_paths=/etc/ca-certificates/trust-source:/usr/share/ca-certificates/trust-source
  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  depends+=(libsystemd.so)

  DESTDIR="$pkgdir" meson install -C build
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 p11-kit/COPYING
  ln -sr "$pkgdir/usr/bin/update-ca-trust" "$pkgdir/usr/lib/p11-kit/trust-extract-compat"
}

# vim:set ts=2 sw=2 et:
