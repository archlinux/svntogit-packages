# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=adobe-source-sans-pro-fonts
pkgver=2.020ro+1.075it
_relver=2.020R-ro/1.075R-it
pkgrel=2
pkgdesc="Sans serif font family for user interface environments"
url="https://adobe-fonts.github.io/source-sans-pro/"
arch=(any)
license=(custom)
_tarname=source-sans-pro-${_relver//\//-}
source=("$_tarname.tar.gz::https://github.com/adobe-fonts/source-sans-pro/archive/$_relver.tar.gz")
sha256sums=('bb45283fa9979c4821c99da3b54313012821aba0246e2ec0057f2b03ca24912c')

package() {
  cd $_tarname
  install -d "$pkgdir/usr/share/fonts/${pkgname%-fonts}"
  install -t "$pkgdir/usr/share/fonts/${pkgname%-fonts}" -m644 OTF/*.otf
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
