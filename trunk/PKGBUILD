# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Travis Willard <travisw@wmpub.ca>
# Contributor: Georg Grabler <ggrabler@gmail.com>

pkgname=ktorrent
pkgver=5.0.1
pkgrel=2
pkgdesc="A powerful BitTorrent client for KDE"
arch=('i686' 'x86_64')
url='http://ktorrent.pwsp.net/'
license=('GPL2')
depends=('libktorrent' 'kdelibs4support' 'kcmutils' 'knotifyconfig' 'kross')
makedepends=('extra-cmake-modules' 'python' 'kdoctools' 'boost' 'plasma-workspace' 'kdnssd' 'taglib')
optdepends=('plasma-workspace: shutdown plugin' 'kdnssd: zeroconf plugin' 'taglib: mediaplayer plugin')
source=("http://download.kde.org/stable/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz")
sha1sums=('0f20f529ed08a7d13008945e961c637352aa43b6')

prepare() {
  mkdir -p build
}

build() {
	cd build
	cmake ../$pkgname-$pkgver \
	  -DCMAKE_BUILD_TYPE=Release \
	  -DCMAKE_INSTALL_PREFIX=/usr \
          -DKDE_INSTALL_LIBDIR=lib
	make
}

package() {
	cd build
	make DESTDIR="${pkgdir}" install
}
