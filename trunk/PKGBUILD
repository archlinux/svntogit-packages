# $Id$
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgname=syslinux
pkgver=4.02
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc="Collection of flexible boot loaders that boot from FAT, ext2/3/4 and btrfs filesystems, from CDs and via PXE"
url="http://syslinux.zytor.com/"
license=('GPL2')
depends=('perl' 'glibc')
optdepends=('perl-passwd-md5:  For md5pass'
            'perl-digest-sha1: For sha1pass'
            'mtools:           For mkdiskimage and syslinux'
           )
makedepends=('nasm')
source=(http://www.kernel.org/pub/linux/utils/boot/syslinux/$pkgname-${pkgver}.tar.bz2
        syslinux-dont-build-dos-windows-targets.patch)
sha256sums=('95cfc459954c3b3660e3c03f8cf9553955affaa1348bd9aa3dca65fb5435337f'
            '7e9301e37113d8bcddadc3a7d2fd6f3f433ef20ef6fa6df9fe9e256fa8868753')

build() {
  # Do not try to build syslinux with our default LDFLAGS, it will fail
  unset LDFLAGS
  cd "$srcdir"/$pkgname-${pkgver}
  # Do not try to build the Windows or DOS installers
  patch -p1 -i "$srcdir"/syslinux-dont-build-dos-windows-targets.patch
  # Fix FHS manpage path
  sed 's|/usr/man|/usr/share/man|g' -i MCONFIG
  make
}

package() {
  cd "$srcdir"/$pkgname-${pkgver}
  make INSTALLROOT="$pkgdir" AUXDIR=/usr/lib/syslinux install
}
