# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=accountsservice
pkgver=0.6.54+10+gd07621b
pkgrel=1
pkgdesc="D-Bus interface for user account query and manipulation"
url="https://gitlab.freedesktop.org/accountsservice/accountsservice"
arch=(x86_64)
license=(GPL3)
depends=(glib2 polkit systemd)
makedepends=(gtk-doc gobject-introspection git meson docbook-xsl xmlto)
_commit=d07621b0bbc3841a1fe3d18326e2fb54d1e9b349  # master
source=("git+https://gitlab.freedesktop.org/accountsservice/accountsservice.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}


prepare() {
  cd $pkgname
  sed -i '/dbus_conf_dir/s/sysconfdir/datadir/g' meson.build
}

build() {
  arch-meson $pkgname build \
    -D admin_group=wheel \
    -D systemd=true \
    -D docbook=true \
    -D gtk_doc=true \
    -D user_heuristics=false
  ninja -C build
}

check() {
  meson test -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}

# vim:set ts=2 sw=2 et:
