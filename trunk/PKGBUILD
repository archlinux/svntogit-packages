# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=wpebackend-fdo
pkgver=1.10.0
pkgrel=1
pkgdesc="Freedesktop.org backend for WPE WebKit"
url="https://wpewebkit.org"
arch=(x86_64)
license=(custom)
depends=(libwpe glib2 libegl wayland libepoxy)
makedepends=(mesa wayland-protocols git meson)
_commit=a908c630483bc99d0c87d4086af183c9a4cafde2  # tags/1.10.0^0
source=("git+https://github.com/Igalia/WPEBackend-fdo?signed#commit=$_commit")
sha256sums=('SKIP')
validpgpkeys=('5AA3BC334FD7E3369E7C77B291C559DBE4C9123B') # Adrián Pérez de Castro <aperez@igalia.com>

pkgver() {
  cd WPEBackend-fdo
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd WPEBackend-fdo
}

build() {
  arch-meson WPEBackend-fdo build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  depends+=(libwpe-1.0.so libg{lib,object,io}-2.0.so)
  provides+=(libWPEBackend-fdo-1.0.so)

  meson install -C build --destdir "$pkgdir"
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 WPEBackend-fdo/COPYING
}

# vim:set sw=2 et:
