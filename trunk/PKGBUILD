# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gmp
pkgver=5.0.1
pkgrel=1
pkgdesc="A free library for arbitrary precision arithmetic"
arch=('i686' 'x86_64')
url="http://gmplib.org/"
depends=('gcc-libs' 'sh')
license=('LGPL3')
options=(!libtool)
install=gmp.install
source=(ftp://ftp.gnu.org/gnu/gmp/gmp-${pkgver}.tar.bz2)
md5sums=('6bac6df75c192a13419dfd71d19240a7')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  [ "${CARCH}" = "i686" ] && export ABI="32"
  ./configure --build=$CHOST \
    --prefix=/usr --infodir=/usr/share/info \
    --enable-cxx || return 1
  make || return 1
  make check || return 1
  make DESTDIR="${pkgdir}" install || return 1
}
