# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Antonio Rojas
# Contributor: Alexey D. <lq07829icatm at rambler.ru>

pkgname=plasma-workspace
pkgver=5.2.0
pkgrel=1
pkgdesc='KDE Plasma Workspace'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/workspace/plasma-workspace'
license=('LGPL')
depends=('knewstuff' 'kjsembed' 'knotifyconfig' 'libxcursor' 'kwayland'
         'libksysguard' 'libkscreen-frameworks' 'ktexteditor' 'libqalculate'
         'qt5-tools' 'kded' 'kde-cli-tools' 'xorg-xrdb' 'xorg-xsetroot'
         'xorg-xmessage' 'xorg-xprop')
makedepends=('extra-cmake-modules' 'kdoctools' 'kwin' 'gpsd' 'baloo-frameworks')
optdepends=('plasma-workspace-wallpapers: additional wallpapers'
            'gpsd: GPS support for geolocation')
conflicts=('kdebase-workspace')
groups=('plasma')
source=("http://download.kde.org/stable/plasma/${pkgver}/$pkgname-$pkgver.tar.xz"
        'kde.pam')
md5sums=('4ffdb4c7e310dff222abd452fd67e74d'
         '929b182dec8a096206ad493477c09d2c')

prepare() {
  mkdir build

  cd ${pkgname}-${pkgver}
  # be sure to use the Qt5 version of qtpaths
  sed -i 's:qtpaths:qtpaths-qt5:' startkde/startkde.cmake
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib \
    -DLIBEXEC_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install

  install -D "${srcdir}"/kde.pam \
    "${pkgdir}"/etc/pam.d/kde
}
