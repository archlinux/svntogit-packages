# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: dracorp aka Piotr Rogoza <piotr.r.public at gmail.com>

pkgname=kimageannotator
pkgver=0.4.0
pkgrel=1
pkgdesc='Tool for annotating images'
arch=(x86_64)
url='https://github.com/DamirPorobic/kImageAnnotator'
license=(GPL)
depends=(qt5-svg kcolorpicker)
makedepends=(cmake qt5-tools)
source=($pkgname-$pkgver.tar.gz::https://github.com/ksnip/kImageAnnotator/archive/v$pkgver.tar.gz)
sha256sums=('4ce8b9853e7c55cffd5eb5fe42ca89ead55db5f432be1b715e6f2804f7e8287d')

build() {
  cmake -B build -S kImageAnnotator-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_SHARED_LIBS=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}

