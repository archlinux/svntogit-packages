# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=knotifications
pkgver=5.42.0
pkgrel=2
pkgdesc='Abstraction for system notifications'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(phonon-qt5 libdbusmenu-qt5 kwindowsystem kconfig kcodecs kcoreaddons qt5-speech)
makedepends=(extra-cmake-modules qt5-tools python doxygen)
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"{,.sig}
        kdebug-389284.patch::"https://cgit.kde.org/knotifications.git/patch/?id=26904527")
sha256sums=('ccb969d47cb41cc9dcaf86ae4a9a11b25317ed40336d2115dc048456afe7952b'
            'SKIP'
            'b6a7f9e0cb70e0bcbf220bce53f199693fbfc3fb3ecbdcbaec2836d43413dc7a')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../kdebug-389284.patch # Fix running commands on notification events
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
