# $Id$
# Maintainer: Laurent Carlier <lordheavym@gmail.com>

pkgname=spirv-tools
pkgver=1.1.1
_pkgver=1.1-rev1
pkgrel=1
pkgdesc="API and commands for processing SPIR-V modules"
arch=('i686' 'x86_64')
url="https://www.khronos.org/vulkan/"
license=('custom')
groups=(vulkan-devel)
depends=(gcc-libs)
makedepends=(cmake python)
source=("https://github.com/KhronosGroup/SPIRV-Tools/archive/spirv-${_pkgver}.tar.gz")
sha256sums=('c843441dd994b589ac23eac4b83826fe55c5823a7fcbbde6d61826494f6843b5')

build() {
  cd SPIRV-Tools-spirv-"${_pkgver}"
  
  rm -rf build ; mkdir build ; cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    ../
  make
}

package() {
  cd SPIRV-Tools-spirv-"${_pkgver}"/build
  
  make DESTDIR="${pkgdir}" install

  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 ../LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/"
}
