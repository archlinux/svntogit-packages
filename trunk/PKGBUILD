# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libdazzle
pkgver=3.26.2
pkgrel=1
pkgdesc="A library to delight your users with fancy features"
url="https://git.gnome.org/browse/libdazzle"
arch=(x86_64)
license=(LGPL)
depends=(gtk3)
makedepends=(git meson vala gobject-introspection gtk-doc)
checkdepends=(xorg-server-xvfb)
_commit=a078dd5013a624619c54a3373759471eef647169  # tags/3.26.2^0
source=("git+https://gitlab.gnome.org/GNOME/libdazzle.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build -Denable_gtk_doc=true
  ninja -C build
}

check() {
  cd build
  xvfb-run meson test
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}
