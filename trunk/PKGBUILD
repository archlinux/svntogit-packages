# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=iproute2
pkgver=2.6.38
pkgrel=1
pkgdesc="IP Routing Utilities"
arch=('i686' 'x86_64')
license=('GPL2')
url="http://www.linux-foundation.org/en/Net:Iproute2"
depends=('perl')
makedepends=('linux-atm')
optdepends=('linux-atm: ATM support')
provides=('iproute')
conflicts=('iproute')
replaces=('iproute')
options=('!makeflags')
backup=('etc/iproute2/ematch_map' 'etc/iproute2/rt_dsfield' 'etc/iproute2/rt_protos' \
	'etc/iproute2/rt_realms' 'etc/iproute2/rt_scopes' 'etc/iproute2/rt_tables')
source=(http://devresources.linux-foundation.org/dev/iproute2/download/iproute2-${pkgver}.tar.bz2
	'iproute2-fhs.patch')
sha1sums=('e9f6d457a06866a2a20a6cba6b3a039b2ec3e14a'
          '2416b11252364d7a6c742eabb4a6924a75637a46')

build() {
  cd $srcdir/iproute2-${pkgver}
  
  # set correct fhs structure
  patch -Np1 -i ${srcdir}/iproute2-fhs.patch

  ./configure

  make
}

package() {
  cd $srcdir/iproute2-${pkgver}

  make DESTDIR=$pkgdir install

  # libnetlink isn't installed, install it FS#19385
  install -Dm644 include/libnetlink.h ${pkgdir}/usr/include/libnetlink.h
  install -Dm644 lib/libnetlink.a ${pkgdir}/usr/lib/libnetlink.a
}
