# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=audacious
pkgver=4.1
pkgrel=1
pkgdesc="Lightweight, advanced audio player focused on audio quality"
arch=('x86_64')
url="https://audacious-media-player.org/"
license=('BSD')
depends=('qt5-base' 'glib2' 'libguess' 'libsm' 'audacious-plugins'
         'hicolor-icon-theme' 'desktop-file-utils')
makedepends=('python') # for gdbus-codegen
optdepends=('unzip: zipped skins support')
source=(https://distfiles.audacious-media-player.org/$pkgname-$pkgver.tar.bz2)
sha256sums=('1f58858f9789e867c513b5272987f13bdfb09332b03c2814ad4c6e29f525e35c')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --disable-gtk \
    --with-buildstamp='Arch Linux'
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
