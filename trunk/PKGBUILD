# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=patch
pkgver=2.6.1
pkgrel=3
pkgdesc="A utility to apply patch files to original sources"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/patch/patch.html"
license=('GPL')
groups=('base-devel')
depends=('glibc')
makedepends=('ed')
optdepends=('ed: Interpret the patch as an ed script; for patch -e functionality(deprecated)')
source=(ftp://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz{,.sig}
        patch-2.6.1-get-arg.patch)
md5sums=('d758eb96d3f75047efc004a720d33daf'
         'f7953f3e472fae5118815e6138372e22'
         '45cd5709adbbcdcb3ab95dc43559c07c')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # Fixed argument type for --get
  # see https://bugzilla.redhat.com/show_bug.cgi?id=553624
  patch -Np1 -i $srcdir/patch-2.6.1-get-arg.patch
  ./configure --prefix=/usr --mandir=/usr/share/man
  make
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make prefix=${pkgdir}/usr mandir=${pkgdir}/usr/share/man install
}
