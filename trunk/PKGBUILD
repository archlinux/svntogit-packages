# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgbase=totem
pkgname=('totem' 'totem-plugin')
pkgver=3.6.3
pkgrel=1
pkgdesc="GNOME3 movie player based on GStreamer"
url="http://www.gnome.org"
arch=(i686 x86_64)
license=(GPL2 custom)
depends=(gst-plugins-base gst-plugins-good totem-plparser desktop-file-utils iso-codes libpeas
         hicolor-icon-theme gnome-icon-theme-symbolic gsettings-desktop-schemas dconf clutter-gst
         clutter-gtk mx python2-gobject gst-plugins-bad)
makedepends=(intltool gnome-doc-utils nautilus lirc-utils vala grilo pylint)
options=('!libtool' '!emptydirs')
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgbase/${pkgver%.*}/$pkgbase-$pkgver.tar.xz
        browser-plugins.ini)
sha256sums=('ad9f2ff207c8a965975670e262394953b37b2dc25cff7ee59f138f961f88ce60'
            'a50a3bbf35f0535f7e8e20af1893446a2e5711015484f9ae6d1ff91af3b23c4e')

build() {
  cd "$pkgbase-$pkgver"
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/totem \
    --localstatedir=/var \
    --disable-static \
    --enable-python \
    --enable-nautilus
  make
}

package_totem() {
  groups=(gnome-extra)
  install=totem.install
  optdepends=('gst-plugins-ugly: Extra media codecs'
              'gst-ffmpeg: Extra media codecs'
              'lirc-utils: Infrared Remote Control plugin'
              'grilo-plugins: Browse sidebar (remote media)'
              'pyxdg: opensubtitles plugin')

  cd "$pkgbase-$pkgver"
  make DESTDIR="${pkgdir}" install

  rm -r "$pkgdir/usr/lib/mozilla"
  rm "$pkgdir/usr/lib/totem/totem-plugin-viewer"

  sed -i "s|#!/usr/bin/python$|#!/usr/bin/python2|" \
    $pkgdir/usr/lib/totem/totem/totem-bugreport.py

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/totem/COPYING"
}

package_totem-plugin() {
  pkgdesc="Totem plugin for web browsers"
  depends=("totem=$pkgver")
  backup=(etc/totem/browser-plugins.ini)

  cd "$pkgbase-$pkgver"
  make -C browser-plugin \
    plugindir=/usr/lib/mozilla/plugins \
    xptdir=/usr/lib/mozilla/plugins \
    DESTDIR="$pkgdir" install
  
  install -Dm644 ../browser-plugins.ini "$pkgdir/etc/totem/browser-plugins.ini"

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/totem-plugin/COPYING"
}
