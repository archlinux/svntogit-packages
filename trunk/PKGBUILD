# Maintainer: damir <damir@archlinux.org>
# Packager: Maksim Sipos (maxsipos at gmail dot com)

pkgname=plotutils
pkgver=2.6
pkgrel=10
arch=('x86_64')
pkgdesc="Set of utilities and libraries for plotting."
url="https://directory.fsf.org/graphics/plotutils.html"
license=("GPL")
depends=("libpng" "gcc-libs" "libxaw>=1.0.5")
source=(https://ftp.gnu.org/pub/gnu/plotutils/$pkgname-$pkgver.tar.gz{,.sig} 
        plotutils-2.6-libpng-1.5.patch)
sha1sums=('7921301d9dfe8991e3df2829bd733df6b2a70838'
          'SKIP'
          '492f0e04f8265ab50d9ba0905a0f5adf3ec06ab8')
sha512sums=('4e5982605e5c1fbdb7325069c3ab2edd736e37a2eb30a1ec01fdc3541fcee6ed49937d4814c6baf79dab5b6fa42f909e4800c851d0ace3c7cf6d5a8681d0f0e7'
            'SKIP'
            'f05fd9cc7731d5167b112dae489f8adc69a2a478d85610998711c4aea011afe5af434c9ac2889aa5162dfc63dd7d4807ee0c1c55ad50d3605685b74ce6021030')
validpgpkeys=('C7823604DFEA27BC29DD4F179DEB46C0D679F6CF') # Karl Berry

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p0 -i ../plotutils-2.6-libpng-1.5.patch
  ./configure --prefix=/usr \
	--with-gnu-ld \
	--with-x \
	--enable-libplotter
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
