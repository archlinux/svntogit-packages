# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kig
pkgver=15.08.3
pkgrel=1
pkgdesc="Interactive Geometry"
url="http://kde.org/applications/education/kig/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdeedu')
depends=('ktexteditor' 'boost-libs') # kemoticons, kitemmodels are only requires for tests
makedepends=('extra-cmake-modules' 'kdoctools' 'python' 'python2' 'boost' 'qt5-xmlpatterns')
optdepends=('qt5-xmlpatterns: support for GeoGebra files')
replaces=('kdeedu-kig')
conflicts=('kdeedu-kig')
install=${pkgname}.install
source=("http://download.kde.org/stable/applications/${pkgver}/src/kig-${pkgver}.tar.xz")
sha1sums=('e7e3eb25bc8c3ceac28fae503fee8ed6dbe4c330')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kig-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install

  # Use the python2 executable
  sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|' "${pkgdir}"/usr/bin/pykig.py
}
