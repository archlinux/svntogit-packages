# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Karol Babioch <karol@babioch.de>

pkgname=libfido2
pkgver=1.4.0
pkgrel=4
pkgdesc='Library functionality for FIDO 2.0, including communication with a device over USB'
url='https://developers.yubico.com/libfido2/'
arch=('x86_64')
license=('BSD')
depends=('glibc' 'openssl' 'libcbor' 'libcbor.so' 'hidapi' 'systemd-libs'  'libudev.so')
makedepends=('cmake' 'systemd')
provides=('libfido2.so')
source=(https://developers.yubico.com/libfido2/Releases/libfido2-${pkgver}.tar.gz{,.sig}
        gzip-timestamp.patch::https://github.com/Yubico/libfido2/commit/e79f7d7996e70d6b2ae9826fce81d61659cab4f6.patch)
sha512sums=('5cf2f2d70bdba893fd33bf3ca91940c7eded5ed1728b517ff3fc46cbde58bf64f16da4104138b20dcea1d9a1cec730e532bc4938cdcba4ad86343e51a1c3c513'
            'SKIP'
            'c5433485b45944f4dfbf7e6be13b1d9b3ac3a8fcb2427e03e41ffa2247f6b9c55a3a478a8df7ebedfaecb4c2981570d68964e484113a4089d3e035d04339d4b4')
b2sums=('d45ff3e1a5605c5ea4c5bddbe423bb69e6237e7a359fcc58469a86c503c934ce4491b98fdd673130d419cabe09acab7e201592f923ffe3908568b2b0888ae32a'
        'SKIP'
        '7f8025bacbf48536edd473edca0eaced6a416d0bb6467aa4a925e952a8fa0b345fa2345efd32759a0f249180fef76d28b223cb9e2ed73bebd94b8a065fbec39c')
validpgpkeys=(
  'EE90AE0D19774C8386628FAAB428949EF7914718' # pedro martelletto <pedro@yubico.com>
  '1D7308B0055F5AEF36944A8F27A9C24D9588EA0F' # Aveen Ismail <aveen.ismail@yubico.com>
  '7FBB6186957496D58C751AC20E777DD85755AA4A' # Konstantinos Georgantas <kostas@yubico.com>
)

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i ${srcdir}/gzip-timestamp.patch
}

build() {
  cd ${pkgname}-${pkgver}
  cmake -B build \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_C_FLAGS="${CFLAGS} ${CPPFLAGS}"
  make -C build VERBOSE=1
}

package() {
  cd ${pkgname}-${pkgver}
  make -C build DESTDIR="${pkgdir}" install
  install -Dm 644 NEWS README.adoc -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
