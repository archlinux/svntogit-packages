# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kidentitymanagement
pkgver=20.12.1
pkgrel=1
pkgdesc="KDE PIM libraries"
arch=(x86_64)
url="https://kontact.kde.org"
license=(LGPL)
depends=(kpimtextedit)
makedepends=(extra-cmake-modules)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('c9d3a6aa4bef1b3864e7c5dd8def3f323c086321d9cedd142c131fcf574714b9'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
