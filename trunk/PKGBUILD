# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: François Charette <firmicus ατ gmx δοτ net>

pkgname=perl-xml-namespacesupport
pkgver=1.12
pkgrel=2
pkgdesc="Generic namespace helpers (ported from SAX2)"
arch=('any')
url="http://search.cpan.org/dist/XML-NamespaceSupport"
license=('GPL')
depends=('perl')
options=('!emptydirs')
source=("https://www.cpan.org/authors/id/P/PE/PERIGRIN/XML-NamespaceSupport-${pkgver}.tar.gz")
md5sums=('a8916c6d095bcf073e1108af02e78c97')

build() {
  cd XML-NamespaceSupport-$pkgver

  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd XML-NamespaceSupport-$pkgver

  make install DESTDIR="$pkgdir"

  find "$pkgdir" -name '.packlist' -delete
  find "$pkgdir" -name '*.pod' -delete
}
