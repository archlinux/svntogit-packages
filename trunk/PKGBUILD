# $$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

# Temporary package since Pidgin doesn't yet support farstream 0.2 and
# gstreamer 1.0.

pkgname=farstream-0.1
pkgver=0.1.2
pkgrel=1
pkgdesc="Farstream (formerly Farsight) - Audio/Video Communications Framework"
arch=(i686 x86_64)
url="http://www.freedesktop.org/wiki/Software/Farstream"
license=('LGPL2.1')
depends=('libnice' 'gstreamer0.10-base' 'gstreamer0.10-bad-plugins' 'gstreamer0.10-good' 'gstreamer0.10-ffmpeg')
makedepends=('gobject-introspection' 'gstreamer0.10-python' 'python2-gobject2')
conflicts=('farsight2')
replaces=('farsight2')
options=(!libtool)
source=(http://freedesktop.org/software/$pkgname/releases/$pkgname/$pkgname-$pkgver.tar.gz
        vp8.patch)
md5sums=('5d6e561b3688d0d0c8906fec4f356df3'
         'c4e76d03d678d29b872b610f9cff42cd')

build() {
  cd  "$pkgname-$pkgver"
  patch -Np1 -i "$srcdir/vp8.patch"
  ./configure --prefix=/usr \
    --with-package-name='Arch Linux farstream package' \
    --with-package-origin='http://archlinux.org' \
    --disable-static
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
