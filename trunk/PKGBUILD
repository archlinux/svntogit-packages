# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-xmodmap
pkgver=1.0.9
pkgrel=2
pkgdesc="Utility for modifying keymaps and button mappings"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libx11')
makedepends=('xorg-util-macros')
groups=('xorg-apps' 'xorg')
source=(https://xorg.freedesktop.org/archive/individual/app/xmodmap-${pkgver}.tar.bz2{,.sig})
sha512sums=('6e50f2ba072ec171bd3fc34f75b21c63f9834f3c0f1608c857f80a4b2c8a9696b03dde79f8342bf67504b759297bc24a64056a625d295a4f89dd31eb938400ac'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # Alan Coopersmith <alan.coopersmith@oracle.com>

build() {
  cd xmodmap-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd xmodmap-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
