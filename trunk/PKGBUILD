# $Id$
# Maintainer:
# Contributor: damir <damir@archlinux.org>

pkgname=libcdio
pkgver=0.90
pkgrel=1
pkgdesc="GNU Compact Disc Input and Control Library"
arch=('i686' 'x86_64')
license=('GPL3')
url="http://www.gnu.org/software/libcdio/"
depends=('libcddb' 'ncurses' 'gcc-libs')
options=('!libtool')
install=libcdio.install
source=(http://ftp.gnu.org/gnu/libcdio/${pkgname}-${pkgver}.tar.gz{,.sig}
        libcdio-0.83-linking.patch)
md5sums=('1b245b023fb03a58d030fd2800db3247'
         '45100d90ea49d6f8481eddf709655c0f'
         '5a7f50209c03d5919d5b932f07871af7')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i $srcdir/libcdio-0.83-linking.patch
  ./configure --prefix=/usr --disable-vcd-info --enable-cpp-progs --disable-static
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make -j1 DESTDIR="${pkgdir}" install
  
  sed -i "/LIBCDIO_SOURCE_PATH/s|.*|/* #undef LIBCDIO_SOURCE_PATH */|" \
    $pkgdir/usr/include/cdio/cdio_config.h
}
