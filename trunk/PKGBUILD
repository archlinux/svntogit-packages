# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com
# Contributor: Charles Mauch <cmauch@gmail.com>

pkgname=perl-file-which
_realname=File-Which
pkgver=1.22
pkgrel=2
pkgdesc="Portable implementation of which"
arch=(any)
url="http://search.cpan.org/dist/File-Which"
license=(GPL PerlArtistic)
depends=(perl)
options=('!emptydirs')
_author=PLICEASE
source=("https://www.cpan.org/authors/id/${_author:0:1}/${_author:0:2}/$_author/${_realname}-${pkgver}.tar.gz")
md5sums=('face60fafd220dc83fa581ef6f96d480')

build() {
  cd "$srcdir/${_realname}-${pkgver}"
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd "$srcdir/${_realname}-${pkgver}"
  make install DESTDIR="$pkgdir"
}
