# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgbase=sof-bin
pkgname=(sof-firmware sof-tools)
pkgver=2.2
pkgrel=1
pkgdesc="Sound Open Firmware"
url="https://www.sofproject.org/"
arch=(x86_64)
license=(custom:BSD custom:ISC)
makedepends=(rsync)
_binver=v$pkgver
source=(https://github.com/thesofproject/sof-bin/releases/download/$_binver/sof-bin-$_binver.tar.gz)
sha256sums=('f0a22018aa02266e3713f1551b698847ca303859be4be8c711dd3685de55ad15')

package_sof-firmware() {
  options=(!strip)

  cd sof-bin-$_binver

  (
    export FW_DEST="$pkgdir/usr/lib/firmware/intel"
    export TOOLS_DEST="$srcdir/tools/usr/bin"

    mkdir -p "$FW_DEST" "$TOOLS_DEST"
    ./install.sh $_binver
  )

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 LICENCE* Notice*
}

package_sof-tools() {
  pkgdesc+=" - tools"
  depends=(alsa-lib)

  cd sof-bin-$_binver

  mv "$srcdir"/tools/* "$pkgdir"

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 LICENCE* Notice*
}

# vim:set sw=2 et:
