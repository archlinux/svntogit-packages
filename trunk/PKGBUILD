# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-lazy-object-proxy
pkgver=1.6.0
pkgrel=1
pkgdesc='A fast and thorough lazy object proxy'
arch=('x86_64')
url="https://github.com/ionelmc/python-lazy-object-proxy"
license=('BSD')
depends=("python")
makedepends=('python-setuptools-scm')
checkdepends=('python-pytest-benchmark' 'python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ionelmc/python-lazy-object-proxy/archive/v$pkgver.tar.gz")
sha512sums=('08fd5e76a6e892a9390ae3bc3025c2fe9255437823fba9aa804f2c7cac09c5f9b57c791d3c9b137aa96c4c78fc2c494eca629cc60d21d23d59713f7a418491a1')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd python-lazy-object-proxy-$pkgver
  python setup.py build
}

check() {
  cd python-lazy-object-proxy-$pkgver
  python setup.py pytest --addopts --ignore=src
}

package() {
  cd python-lazy-object-proxy-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
