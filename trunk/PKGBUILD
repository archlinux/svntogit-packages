# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>

pkgname=prometheus
pkgver=2.6.0
pkgrel=2

pkgdesc='An open-source systems monitoring and alerting toolkit'
url='https://prometheus.io'
arch=('x86_64')
license=('Apache')

depends=('glibc')
makedepends=('go-pie' 'git' 'bzr')

backup=('etc/prometheus/prometheus.yml')

source=("https://github.com/prometheus/prometheus/archive/v$pkgver.tar.gz"
        prometheus.service
        prometheus.sysusers)

sha256sums=('e0d3e77e45466fd055726268354a02834968a3275791be6cbd17513ec7860c1d'
            'a1484a9c97e51c02e61ba8dfbd21aed34a8315fd111391ac0d13196cde7503a9'
            '2747fabb4e56b808361eb7dd7acf9729ab8973d1ebe2f857dd56f6c71f71e45f')

build() {
  cd prometheus-$pkgver

  go build \
    -gcflags "all=-trimpath=${PWD}" \
    -asmflags "all=-trimpath=${PWD}" \
    -ldflags "-extldflags ${LDFLAGS}" \
    ./cmd/prometheus
  go build \
    -gcflags "all=-trimpath=${PWD}" \
    -asmflags "all=-trimpath=${PWD}" \
    -ldflags "-extldflags ${LDFLAGS}" \
    ./cmd/promtool
}

package() {
  install -Dm644 prometheus.service "$pkgdir"/usr/lib/systemd/system/prometheus.service
  install -Dm644 prometheus.sysusers "$pkgdir"/usr/lib/sysusers.d/prometheus.conf

  cd prometheus-$pkgver

  install -Dm755 -t "$pkgdir"/usr/bin prometheus promtool
  install -Dm755 -t "$pkgdir"/etc/prometheus documentation/examples/prometheus.yml
  install -dm755 "$pkgdir"/etc/prometheus/{consoles,console_libraries}
  install -dm755 -o210 -g210 "$pkgdir"/var/lib/prometheus

  # Web
  install -dm755 "$pkgdir"/usr/share/prometheus/
  cp -R web/ui "$pkgdir"/usr/share/prometheus/web

  # Examples
  install -Dm644 -t "$pkgdir"/usr/share/doc/prometheus/examples documentation/examples/prometheus*.yml
  cp -R consoles console_libraries "$pkgdir"/usr/share/doc/prometheus/examples

}
