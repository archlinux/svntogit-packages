# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Thomas Baechler <thomas@archlinux.org>

pkgname=iptables
pkgver=1.4.21
pkgrel=3
pkgdesc='Linux kernel packet control tool'
arch=('i686' 'x86_64')
license=('GPL2')
url='http://www.netfilter.org/projects/iptables/index.html'
depends=('glibc' 'bash')
makedepends=('linux-api-headers')
source=(http://www.netfilter.org/projects/iptables/files/${pkgname}-${pkgver}.tar.bz2{,.sig} \
        iptables_upstream940.patch
        empty.rules
        simple_firewall.rules
        empty-filter.rules
        empty-mangle.rules
        empty-nat.rules
        empty-raw.rules
        empty-security.rules
        iptables.service
        ip6tables.service
        iptables-flush)
sha1sums=('85d4160537546a23a7e42bc26dd7ee62a0ede4c8'
          'SKIP'
          '4914485dd940840849a9ddbefef0aa87e1505019'
          '83b3363878e3660ce23b2ad325b53cbd6c796ecf'
          'f085a71f467e4d7cb2cf094d9369b0bcc4bab6ec'
          'd9f9f06b46b4187648e860afa0552335aafe3ce4'
          'c45b738b5ec4cfb11611b984c21a83b91a2d58f3'
          '1694d79b3e6e9d9d543f6a6e75fed06066c9a6c6'
          '7db53bb882f62f6c677cc8559cff83d8bae2ef73'
          'ebbd1424a1564fd45f455a81c61ce348f0a14c2e'
          '9306cba67dbeaa004af084a816f66920a6a10faf'
          '38fa2ffe7965e63b494d333f69193029c1258c28'
          'e7abda09c61142121b6695928d3b71ccd8fdf73a')
validpgpkeys=('57FF5E9C9AA67A860B557AF7A4111F89BB5F58CC') # Netfilter Core Team

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # use system one
  rm include/linux/types.h
  
  # https://bugzilla.netfilter.org/show_bug.cgi?id=940
  patch -Np1 -i "${srcdir}/iptables_upstream940.patch"
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

 ./configure --prefix=/usr \
     --libexecdir=/usr/lib/iptables --sysconfdir=/etc \
     --with-xtlibdir=/usr/lib/iptables \
     --enable-devel --enable-shared \
     --sbindir=/usr/bin
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  cd "${srcdir}"
  install -D -m644 empty.rules "${pkgdir}"/etc/iptables/empty.rules
  install -D -m644 simple_firewall.rules "${pkgdir}"/etc/iptables/simple_firewall.rules

  mkdir -p "${pkgdir}"/var/lib/{iptables,ip6tables}
  install -m644 empty-{filter,mangle,nat,raw,security}.rules "${pkgdir}"/var/lib/iptables
  install -m644 empty-{filter,mangle,nat,raw,security}.rules "${pkgdir}"/var/lib/ip6tables

  # install systemd files
  install -Dm644 ${srcdir}/iptables.service ${pkgdir}/usr/lib/systemd/system/iptables.service
  install -Dm644 ${srcdir}/ip6tables.service ${pkgdir}/usr/lib/systemd/system/ip6tables.service
  install -Dm755 ${srcdir}/iptables-flush ${pkgdir}/usr/lib/systemd/scripts/iptables-flush
}

