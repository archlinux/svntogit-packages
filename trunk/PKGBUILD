# $Id$
# Maintainer: Christian Hesse <mail@eworm.de>
# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=htop
pkgver=2.0.2
pkgrel=2
pkgdesc="Interactive process viewer"
arch=('i686' 'x86_64')
url="http://hisham.hm/htop/"
license=('GPL')
depends=('ncurses')
makedepends=('python')
optdepends=('lsof: show files opened by a process'
            'strace: attach to a running process')
options=('!emptydirs')
validpgpkeys=('8460980B2B79786DE0C7FCC83FD8F43C2BB3C478') # Hisham Muhammad <h@hisham.hm>
source=("https://hisham.hm/$pkgname/releases/$pkgver/$pkgname-$pkgver.tar.gz"{,.asc})
sha256sums=('179be9dccb80cee0c5e1a1f58c8f72ce7b2328ede30fb71dcdf336539be2f487'
            'SKIP')

build() {
  cd "$pkgname-$pkgver"

  ./configure \
      --prefix=/usr \
      --sysconfdir=/etc \
      --enable-unicode \
      --enable-openvz \
      --enable-vserver \
      --enable-cgroup

  make
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
}
