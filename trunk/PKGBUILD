# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: megadriver <megadriver at gmx dot com>
# Contributor: Luca Bennati <lucak3 AT gmail DOT com>

pkgname=libcdr
pkgver=0.1.1
pkgrel=2
pkgdesc="CorelDraw file format importer library for LibreOffice"
arch=('i686' 'x86_64')
url="https://wiki.documentfoundation.org/DLP/Libraries/libcdr"
license=('GPL2' 'LGPL2.1' 'MPL')
depends=('libwpd' 'lcms2' 'icu' 'librevenge')
makedepends=('libwpg' 'boost' 'doxygen')
source=(http://dev-www.libreoffice.org/src/$pkgname/$pkgname-$pkgver.tar.xz
        boost159.patch)
md5sums=('72ed4d2f900eac95061207d026d5324c'
         'dd38f0c2324920b577b9b5725a8fa3f2')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../boost159.patch
  autoreconf -vi
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

check() {
  cd $pkgname-$pkgver
  make -k check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
