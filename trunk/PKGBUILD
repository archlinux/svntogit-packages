# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=gnome-online-accounts
pkgver=3.18.0
pkgrel=2
pkgdesc="GNOME service to access online accounts"
arch=(i686 x86_64)
url="http://www.gnome.org"
license=('GPL')
depends=('webkit2gtk' 'json-glib' 'libnotify' 'rest' 'libsecret' 'telepathy-glib' 'krb5' 'gcr')
makedepends=('intltool' 'libxslt' 'gobject-introspection' 'docbook-xsl')
optdepends=('gvfs-goa: Virtual file systems (e.g. OwnCloud)'
            'gvfs-google: Google Drive')
install=$pkgname.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('fc2dac96551746576759bd14f9b322bae1dd0aeedc0e755065ddf5eaaefacd34')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --libexecdir=/usr/lib/$pkgname --disable-static \
    --enable-media-server --enable-kerberos --enable-lastfm
  make
}

package() {
  cd $pkgname-$pkgver
  make -j1 DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
