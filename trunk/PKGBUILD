# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Sean Middleditch <elanthis@awesomeplay.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=docbook-xsl
pkgver=1.79.2
pkgrel=4
pkgdesc='XML stylesheets for Docbook-xml transformations'
arch=(any)
license=(custom)
url='http://docbook.org/'
depends=(libxml2 libxslt docbook-xml sed)
install="$pkgname.install"
source=("https://github.com/docbook/xslt10-stylesheets/releases/download/release%2F$pkgver/docbook-xsl-nons-$pkgver.tar.gz"
        765567_non-recursive_string_subst.patch)
sha256sums=('f89425b44e48aad24319a2f0d38e0cb6059fdc7dbaf31787c8346c748175ca8e'
            '193ec26dcb37bdf12037ed4ea98d68bd550500c8e96b719685d76d7096c3f9b3')

prepare() {
  cd $pkgname-nons-$pkgver
  patch -Np2 -i ../765567_non-recursive_string_subst.patch
}

package() {
  cd $pkgname-nons-$pkgver

  _pkgroot="$pkgdir"/usr/share/xml/docbook/xsl-stylesheets-$pkgver

  install -dm755 ${_pkgroot}
  install -m644 VERSION VERSION.xsl ${_pkgroot}

  for fn in assembly common eclipse epub epub3 fo highlighting html htmlhelp javahelp lib \
      manpages params profiling roundtrip template website xhtml xhtml-1_1 xhtml5; do
          install -dm755 ${_pkgroot}/${fn}
	  install -m644 ${fn}/*.{xml,xsl,dtd,ent} ${_pkgroot}/${fn} || true  # ignore missing files
  done

  install -dm755 "$pkgdir"/etc/xml
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
