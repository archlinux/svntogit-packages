# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

pkgname=libxinerama
pkgver=1.1.4
pkgrel=1
pkgdesc="X11 Xinerama extension library"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libxext' 'xineramaproto')
makedepends=('xorg-util-macros')
source=("${url}/releases/individual/lib/libXinerama-${pkgver}.tar.bz2")
sha512sums=('cff777ad942614fbf6bc6d8529f399e62debf3ecbf6cc0694a94e38c022bf929ffd5636fb59f55533c394d89b23af3ea51fa5128927f12a85787e16239330f14')

build() {
  cd libXinerama-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd libXinerama-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
