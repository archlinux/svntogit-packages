# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Wieland Hoffmann <the_mineo@web.de>

pkgname=chromaprint
pkgver=1.5.1
pkgrel=5
pkgdesc="Library for extracting fingerprints from any audio source"
url="https://acoustid.org/chromaprint"
arch=('x86_64')
license=('LGPL2.1' 'MIT')
depends=('gcc-libs' 'glibc')
makedepends=('cmake' 'ffmpeg' 'gtest')
provides=('libchromaprint.so')
# upstream signs with DSA key: https://github.com/acoustid/chromaprint/issues/81
source=(https://github.com/acoustid/${pkgname}/archive/v$pkgver/$pkgname-$pkgver.tar.gz
        8ccad693.patch)
sha512sums=('ea16e4d2b879c15b1d9b9ec93878da8b893f1834c70942663e1d2d106c2e0a661094fe2dd3bae7a6c2a1f9d5d8fab5e0b0ba493561090cf57b2228606fad1e66'
            '9eb62b6b165fb2a48fe6399025a51161347cb56b8b7d28819a90be38425d145d2997aecb203dbadf40bcb469b0757ad8f836dca7dbb1db95f923a427f3c2d9ee')
b2sums=('9f7f030e97d3114cf679df298d313ea826c0fb05e7e7d8a10090d0a27ed0811b380b81b29fce973e0493826c478964367396311fd0484619cb2fc4c2d8e0d4c0'
        '085dd1c64347a76cf912d327d8782fafd392777aae9dd565b1e84860e12a1262d9b25f908c54b96147abf74c82824529c6e100f5943ddff725358bb7aa19da47')

prepare() {
  patch -d $pkgname-$pkgver -p1 < 8ccad693.patch # Port to ffmpeg 5
}

build() {
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE='None' \
        -DBUILD_TESTS=ON \
        -DBUILD_TOOLS=ON \
        -DGTEST_SOURCE_DIR=/usr/src/googletest/ \
        -Wno-dev \
        -B build \
        -S $pkgname-$pkgver
  make VERBOSE=1 -C build
}

check() {
  make VERBOSE=1 -C build check
}

package() {
  depends+=('libavcodec.so' 'libavformat.so' 'libavutil.so' 'libswresample.so')
  make VERBOSE=1 DESTDIR="$pkgdir" install -C build
  install -vDm 644 $pkgname-$pkgver/LICENSE.md -t "$pkgdir/usr/share/licenses/$pkgname"
  install -vDm 644 $pkgname-$pkgver/{NEWS.txt,README.md} -t "$pkgdir/usr/share/doc/$pkgname"
}
