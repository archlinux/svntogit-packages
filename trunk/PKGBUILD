# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-documents
pkgver=3.10.0
pkgrel=2
pkgdesc="Documents Manager for GNOME"
arch=(i686 x86_64)
url="http://www.gnome.org"
license=(GPL)
groups=(gnome-extra)
depends=(evince gjs gtk3 gnome-desktop gnome-online-accounts libgdata
         tracker libzapojit webkitgtk gnome-online-miners)
makedepends=(intltool gobject-introspection itstool docbook-xsl)
optdepends=('unoconv: Support for Libreoffice document types')
options=('!libtool')
install=gnome-documents.install
source=(http://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('93365aee61c2777d1fe70fa8f55f6e3ce9e7e130dd2869d74eb85787825be9f6')

build() {
  cd $pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/$pkgname
  make
}


check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
