# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdelibs4support
pkgver=5.15.0
pkgrel=1
pkgdesc='Porting aid from KDELibs4'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kdelibs4support'
license=('LGPL')
depends=('kunitconversion' 'kdesignerplugin' 'kinit' 'kitemmodels' 'kemoticons')
makedepends=('extra-cmake-modules' 'kdoctools' 'qt5-tools' 'networkmanager' 'perl-uri')
groups=('kf5-aids')
replaces=('kde4support')
conflicts=('kde4support')
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/portingAids/${pkgname}-${pkgver}.tar.xz")
md5sums=('a7e41bbb268da562cd5c58b47717afa6')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DLIBEXEC_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DHTML_INSTAL_DIR=/usr/share/doc/kdelibs4support/html \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install

# cert bundle seems to be hardcoded
# link it to the one from ca-certificates
  rm -f "$pkgdir"/usr/share/kf5/kssl/ca-bundle.crt
  ln -sf /etc/ssl/certs/ca-certificates.crt "${pkgdir}"/usr/share/kf5/kssl/ca-bundle.crt
}
