# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=gjs
pkgver=1.41.91
pkgrel=1
pkgdesc="Javascript Bindings for GNOME"
arch=(i686 x86_64)
url="http://live.gnome.org/Gjs"
license=(GPL)
depends=(cairo gobject-introspection 'js>=17.0.0')
makedepends=('python2')
source=(http://ftp.gnome.org/pub/gnome/sources/gjs/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('ca0b861cce674e757b746f28d331b3419a71e2ada3cacf31fb778cbff3edde62')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-static --libexecdir=/usr/lib
  make
}

check() {
  cd $pkgname-$pkgver
  # Needs a display
  make -k check || :
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
