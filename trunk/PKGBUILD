# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=gjs
pkgver=1.56.1
pkgrel=1
epoch=2
pkgdesc="Javascript Bindings for GNOME"
url="https://wiki.gnome.org/Projects/Gjs"
arch=(x86_64)
license=(GPL)
depends=(cairo gobject-introspection-runtime js60 gtk3)
makedepends=(gobject-introspection git autoconf-archive)
checkdepends=(valgrind xorg-server-xvfb)
_commit=2b17ce9519cdac6dc39d04b02900f47384621ac5  # tags/1.56.1^0
source=("git+https://gitlab.gnome.org/GNOME/gjs.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --libexecdir=/usr/lib \
    --disable-static \
    --enable-compile-warnings=yes \
    $(type -P Xvfb >/dev/null && echo --with-xvfb-tests)
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd $pkgname
  make -j1 check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
