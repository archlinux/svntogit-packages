# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=libsoup
pkgname=('libsoup' 'libsoup-gnome')
pkgver=2.30.1
pkgrel=1
arch=(i686 x86_64)
license=('LGPL')
makedepends=('glib2>=2.24.0' 'gnutls>=2.8.6' 'libxml2>=2.7.7' 'libproxy>=0.2.3' 'sqlite3>=3.6.23' 'gconf>=2.28.1' 'libgnome-keyring>=2.30.1' 'intltool' 'pkgconfig' 'gobject-introspection>=0.6.10')
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgbase}/2.30/${pkgbase}-${pkgver}.tar.bz2)
options=('!libtool' '!emptydirs')
sha256sums=('6395c6e0bbcf4e822f1c5f3dd6f844116c571ed13cdf611d5c8f1dd4fe5777ed')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static || return 1
  make || return 1
}

package_libsoup() {
  pkgdesc="GNOME HTTP Library - base library"
  depends=('glib2>=2.24.0' 'libxml2>=2.7.7' 'gnutls>=2.8.6')

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make DESTDIR="${pkgdir}" install || return 1
  rm -f "${pkgdir}"/usr/lib/libsoup-gnome-2.4.*
  rm -f "${pkgdir}/usr/lib/pkgconfig/libsoup-gnome-2.4.pc"
  rm -rf "${pkgdir}/usr/include/libsoup-gnome-2.4"
}

package_libsoup-gnome() {
  pkgdesc="GNOME HTTP Library - GNOME libraries"
  depends=("libsoup=${pkgver}" 'libproxy>=0.2.3' 'libgnome-keyring>=2.30.1')

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make DESTDIR="${pkgdir}"  install || return 1
  rm -f "${pkgdir}"/usr/lib/libsoup-2.4.*
  rm -f "${pkgdir}/usr/lib/pkgconfig/libsoup-2.4.pc"
  rm -rf "${pkgdir}/usr/include/libsoup-2.4"
  rm -rf "${pkgdir}/usr/share"
}
