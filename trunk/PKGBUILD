# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: tinywrkb <tinywrkb@gmail.com>

pkgname=power-profiles-daemon
pkgver=0.11.1
pkgrel=1
pkgdesc='Makes power profiles handling available over D-Bus'
url='https://gitlab.freedesktop.org/hadess/power-profiles-daemon'
license=(GPL3)
arch=(x86_64)
depends=(upower polkit)
optdepends=('python-gobject: for powerprofilesctl')
makedepends=(meson)
checkdepends=(python-dbusmock python-isort python-mccabe umockdev)
source=(https://gitlab.freedesktop.org/hadess/power-profiles-daemon/-/archive/$pkgver/$pkgname-$pkgver.tar.gz)
b2sums=('53514a3de851da349b39e0261a1ce4cdd7e3e62ed08d1ed7374518132e1f5ce2230ae869c34c7461bcdaf4eb89b9d78360526ac7e21c525c09de31bd19503ab4')

build() {
  meson $pkgname-$pkgver build \
    --prefix /usr \
    --libexec lib \
    --sysconfdir /usr/share
  meson compile -C build
}

check() {
  meson test -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}
