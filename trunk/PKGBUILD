# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=mtools
pkgver=4.0.36
pkgrel=1
epoch=1
pkgdesc="A collection of utilities to access MS-DOS disks"
arch=(x86_64)
url="https://www.gnu.org/software/mtools/"
license=(GPL3)
depends=(bash glibc)
makedepends=(libx11 texinfo)
optdepends=('libx11: required by floppyd'
            'libxau: required by floppyd')
backup=("etc/${pkgname}.conf")
source=("https://ftp.gnu.org/gnu/mtools/${pkgname}-${pkgver}.tar.bz2"{,.sig})
sha512sums=('2425ee0e39c06ea201a5303ab09a0aa9f0c59341522e2c15abc4f079320fd3d67d28afeb2870ee508d472bba6904c542e11c02b92652c8eda1bf338bab372d07'
            'SKIP')
b2sums=('753ee3e412452572b36d15805982255f28bc277764ce66a0fe8483e19a3488fbb282b873a6be85e87e82a71bf7fda517e1234e1911f0b7c3f3992e25f8d18322'
        'SKIP')
validpgpkeys=('51A23D9D7C4DF41AA48FA893E99CF5537790B839') # Alain KNAFF <alain@knaff.lu>

prepare() {
  cd "${pkgname}-${pkgver}"
  # comment entry in default config
  sed -e '/^SAMPLE FILE$/s:^:# :' -i mtools.conf
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --mandir=/usr/share/man \
              --infodir=/usr/share/info \
              --sysconfdir=/etc
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -vDm 644 mtools.conf -t "${pkgdir}/etc/"
  install -vDm 644 {README,NEWS} -t "${pkgdir}/usr/share/doc/${pkgname}"
}
