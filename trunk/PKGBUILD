# Maintainer: anex <assassin.anex[@]gmail.com

pkgname=signon-kwallet-extension
pkgver=16.04.1
pkgrel=1
pkgdesc="KWallet signon extension"
arch=('i686' 'x86_64')
url="http://kde.org"
license=('LGPL')
groups=('kde-applications' 'kdenetwork')
depends=('signond' 'kwallet')
makedepends=('extra-cmake-modules')
source=("http://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
md5sums=('f1aa5570b681cbbde5cf5dcd9dc25e50')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
