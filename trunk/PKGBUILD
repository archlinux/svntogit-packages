# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=logrotate
pkgver=3.8.2
pkgrel=2
pkgdesc="Rotates system logs automatically"
arch=('i686' 'x86_64')
url="https://fedorahosted.org/logrotate/"
license=('GPL')
groups=('base')
depends=('popt' 'cron' 'gzip')
optdepends=('cron: scheduled log rotation')
backup=('etc/logrotate.conf')
source=("https://fedorahosted.org/releases/l/o/logrotate/logrotate-${pkgver}.tar.gz"
        'noasprintf.patch'
        'paths.patch'
        'logrotate.conf'
        'logrotate.cron.daily')
md5sums=('ddd4dcf28c38b3ac6bc6ff4e0148308a'
         'cd76976b5ce37d328b452c806b55a015'
         'e76526bcd6fc33c9d921e1cb1eff1ffb'
         '86209d257c8b8bc0ae34d6f6ef057c0f'
         'aa8ac8283908b6114483a293adcb650f')

build() {
  cd "$srcdir/${pkgname}-${pkgver}"

  patch -p0 -i "$srcdir/noasprintf.patch"
  patch -p0 -i "$srcdir/paths.patch"

  make RPM_OPT_FLAGS="$CFLAGS" EXTRA_LDFLAGS="$LDFLAGS"
}

check() {
  cd "$srcdir/${pkgname}-${pkgver}"
  make test
}

package() {
  cd "$srcdir/${pkgname}-${pkgver}"
  make PREFIX="$pkgdir" MANDIR="/usr/share/man" install

  install -Dm644 "$srcdir/logrotate.conf" "$pkgdir/etc/logrotate.conf"
  install -Dm744 "$srcdir/logrotate.cron.daily" "$pkgdir/etc/cron.daily/logrotate"
}
