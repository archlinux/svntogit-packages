# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: bzklrm <bzklrm@gmail.com>
# Additional contributors Senjin, Xavier, dundee

pkgname=glpk
pkgver=4.64
pkgrel=1
pkgdesc="GNU Linear Programming Kit : solve LP, MIP and other problems."
arch=('x86_64')
url="http://www.gnu.org/software/glpk/glpk.html"
license=('GPL')
depends=('gmp')
source=(http://ftp.gnu.org/gnu/glpk/${pkgname}-${pkgver}.tar.gz{,.sig})
sha1sums=('b7828320ad02530a47a3a65813b75626c2a308da'
          'SKIP')
validpgpkeys=('A2B56CCB42220C06481335BAD17BF2305981E818') # Andrew Makhorin

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr --with-gmp
  make
}

package(){
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 doc/glpk.pdf "${pkgdir}/usr/share/doc/glpk/glpk.pdf"
  install -Dm644 doc/gmpl.pdf "${pkgdir}/usr/share/doc/glpk/gmpl.pdf"
}
