# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-xman
pkgver=1.1.5
pkgrel=3
pkgdesc="Unix manual page viewer"
arch=(x86_64)
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libxaw' 'libxt')
makedepends=('xorg-util-macros' 'xorgproto' 'libxmu')
source=(https://xorg.freedesktop.org/archive/individual/app/xman-${pkgver}.tar.bz2{,.sig})
sha512sums=('f6b2be424a79f298bf1a81edce4172572195c9efc9454e5b7b7046f344ed08154c77ccaa763ccdf3f1a30b64aa4802783bf813ee637437ccb1fba434fc5d4400'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # Alan Coopersmith <alan.coopersmith@oracle.com>

build() {
  cd xman-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd xman-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
