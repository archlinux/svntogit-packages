# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
pkgname=libva
pkgver=1.0.4
pkgrel=1
pkgdesc="libva with patches to extend VA API with data needed for VDPAU and XvBA backends."
arch=('i686' 'x86_64')
url="http://freedesktop.org/wiki/Software/vaapi"
license=('MIT')
depends=('libgl' 'libdrm' 'libxfixes')
makedepends=('mesa')
optdepends=('vdpau-video: VDPAU backend for VA API')
options=('!libtool')
source=(http://cgit.freedesktop.org/libva/snapshot/${pkgname}-${pkgver}.tar.bz2)
md5sums=('3a16a9339dcc3c27234ba9f28fb38a63')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  autoreconf -fi
  ./configure --prefix=/usr  \
    --enable-i965-driver \
    --enable-glx
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -m644 -D COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}

# vim:set ts=2 sw=2 et:
