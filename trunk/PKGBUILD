# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=analitza
pkgver=16.04.2
pkgrel=1
pkgdesc="A library to add mathematical features to your program"
url="https://edu.kde.org/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('qt5-svg' 'qt5-declarative')
makedepends=('extra-cmake-modules' 'kdoctools' 'python' 'eigen')
replaces=('kdebase-analitza')
conflicts=('kdebase-analitza')
source=("http://download.kde.org/stable/applications/${pkgver}/src/analitza-${pkgver}.tar.xz")
sha1sums=('b1df104db53040aa873960cf498fbdae599cb269')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
