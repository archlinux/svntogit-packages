# $Id$
# Contributor: Dale Blount <dale@archlinux.org>
# Contributor: Gregor Ibic <gregor.ibic@intelicom.si>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=clamav
pkgver=0.99.2
pkgrel=7
pkgdesc='Anti-virus toolkit for Unix'
url='http://www.clamav.net/'
license=('GPL')
arch=('i686' 'x86_64')
makedepends=('libmilter')
depends=('bzip2' 'libltdl' 'libxml2' 'curl' 'pcre')
validpgpkeys=('B964E6D7BC7D7C82CCB8D45840B8EA2364221D53'
              'F79FB2D08751574C5D3FDFFBB3D5342C260429A0')
source=("http://www.clamav.net/downloads/production/${pkgname}-${pkgver}.tar.gz"{,.sig}
        'logrotate'
        'tmpfiles.d'
        'clamd.conf'
        'freshclam.conf'
        'clamd.service'
        'freshclamd.service'
        'make_it_compile_against_openssl_1_1_0.patch')
sha256sums=('167bd6a13e05ece326b968fdb539b05c2ffcfef6018a274a10aeda85c2c0027a'
            'SKIP'
            'ce4b9b8c300614641af600c9a73b52a00ee8e47ccc9f91b2428a113b0ecff21b'
            '0a61abee3b9bba94126afe3344e7d8e82da5120ca6dbd2b413b10f75da5b0b0d'
            'afdb95f93f7e11e163d368caccd5f6814206c6f0d74816b4f712c0267b50572a'
            '127b39e13525ffb4242198cfb76f99d4d517e5f2fd9fa8dcad3f31fc9f82f952'
            'e376ab0cefeefa5ac5f1cd611718452ea8646198e854aca3cc0026f5ffe58fb4'
            'dd5ff6c79ee360da5f2221c4d9110a2a8886d86293f6c93c16bf74fdb126593c'
            '0a8e02a91bc3f2c99bd52dc475592637376baa991fe3f899b7745b840fc586c5')

backup=('etc/clamav/clamd.conf'
        'etc/clamav/freshclam.conf'
        'etc/logrotate.d/clamav')

install=install

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	patch -Np1 -i ../make_it_compile_against_openssl_1_1_0.patch
	autoreconf -fi
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	# --disable-zlib-vcheck because the configure script thinks that
	# zlib 1.2.11 is older than 1.2.2
	./configure \
		--prefix=/usr \
		--sbindir=/usr/bin \
		--sysconfdir=/etc/clamav \
		--with-dbdir=/var/lib/clamav \
		--disable-clamav \
		--disable-llvm \
		--disable-zlib-vcheck \
		--enable-milter

        sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	install -Dm644 ../clamd.conf "${pkgdir}"/etc/clamav/clamd.conf
	install -Dm644 ../freshclam.conf "${pkgdir}"/etc/clamav/freshclam.conf
	install -Dm644 ../freshclamd.service "${pkgdir}"/usr/lib/systemd/system/freshclamd.service
	install -Dm644 ../clamd.service "${pkgdir}"/usr/lib/systemd/system/clamd.service
	install -Dm644 ../tmpfiles.d "${pkgdir}"/usr/lib/tmpfiles.d/clamav.conf
	install -Dm644 ../logrotate "${pkgdir}"/etc/logrotate.d/clamav

	install -d -o 64 -g 64 "${pkgdir}"/var/log/clamav
	install -d -o 64 -g 64 "${pkgdir}"/var/lib/clamav
}
