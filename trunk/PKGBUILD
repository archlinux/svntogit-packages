# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ndctl
pkgver=67
pkgrel=1
pkgdesc='Utility library for managing the libnvdimm (non-volatile memory device) sub-system in the Linux kernel'
arch=('x86_64')
url="https://github.com/pmem/ndctl"
license=('LGPL')
depends=('json-c' 'kmod' 'systemd-libs' 'libutil-linux')
makedepends=('asciidoctor' 'xmlto' 'bash-completion')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pmem/ndctl/archive/v$pkgver.tar.gz")
sha512sums=('a923510468f540ac84fca5c562096b02eb3827c57e87ac36641f5ce129114ba583ce5ac5d71b45d7be876e880ad7e4fe38678350480d8974745120c7e0161435')

prepare() {
  # fix location of modprobe.d conf
  sed -i 's|^modprobedir =.*|modprobedir = /usr/lib/modprobe.d/|' $pkgname-$pkgver/Makefile.am
}

build() {
  cd "$srcdir"/$pkgname-$pkgver
  ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
