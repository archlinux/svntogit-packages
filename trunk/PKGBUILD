# $Id$
# Maintainer: andyrtr <andyrtr funnychar archlinux.org>
# Contributor: Andrew Simmons <andrew.simmons@gmail.com>

pkgname=thunar
pkgver=1.1.6
pkgrel=4
pkgdesc="modern file manager for Xfce"
arch=('i686' 'x86_64')
license=('GPL2' 'LGPL2.1')
url="http://thunar.xfce.org"
groups=('xfce4')
depends=('desktop-file-utils' 'libexif' 'hicolor-icon-theme' 'libnotify' 'udev'
         'gtk2' 'exo>=0.5.4' 'libxfce4util>=4.7.3' 'libxfce4ui' 'libpng' 'udisks')
makedepends=('intltool' 'gtk-doc' 'xfce4-panel>=4.7.4')
optdepends=('gvfs: for trash support, mounting with udisk and remote filesystems'
            'xfce4-panel: for trash applet'
            'tumbler: for thumbnail previews'
            'thunar-volman: manages removable devices'
            'thunar-archive-plugin: create and deflate archives'
            'thunar-media-tags-plugin: view/edit id3/ogg tags')
options=('!libtool')
install=${pkgname}.install
backup=('etc/polkit-1/localauthority/50-local.d/org.freedesktop.udisks.pkla')
source=(http://archive.xfce.org/src/xfce/${pkgname}/1.1/Thunar-${pkgver}.tar.bz2
	org.freedesktop.udisks.pkla)
md5sums=('a96fdbc0b3d422f864d50374b5d6be41'
         'a7ddb5eec02d9a8e91a2997862e73cd8')

build() {
  cd ${srcdir}/Thunar-${pkgver}
  ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--libexecdir=/usr/lib \
	--localstatedir=/var \
	--disable-static \
	--enable-gio-unix \
	--enable-dbus \
	--enable-startup-notification \
	--enable-gudev \
	--enable-notifications \
	--enable-exif \
	--enable-pcre \
	--enable-gtk-doc \
	--disable-debug
  make
}

package() {
  cd ${srcdir}/Thunar-${pkgver}
  make DESTDIR=${pkgdir} install
  sed -i 's:x-directory/gnome-default-handler;::' \
    ${pkgdir}/usr/share/applications/Thunar-folder-handler.desktop

  # install udisks permission file
  install -dm700 ${pkgdir}/etc/polkit-1/localauthority
  install -dm755 ${pkgdir}/etc/polkit-1/localauthority/50-local.d
  install -m644 ${srcdir}/org.freedesktop.udisks.pkla ${pkgdir}/etc/polkit-1/localauthority/50-local.d/
}
