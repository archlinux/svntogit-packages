# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: Link Dupont <link@subpop.net>

pkgname=mpc
pkgver=0.26
pkgrel=1
pkgdesc='Minimalist command line interface to MPD'
url='http://www.musicpd.org/clients/mpc/'
license=('GPL2')
arch=('i686' 'x86_64')
depends=('libmpdclient')
source=("http://www.musicpd.org/download/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha1sums=('dd1577cfff1d8324bc27c97fb26d88b845118800' 'SKIP')

options=('!emptydirs')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr 
	make 
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install 
	install -Dm644 doc/mpc-completion.bash "${pkgdir}/usr/share/bash-completion/completions/mpc" 
}
