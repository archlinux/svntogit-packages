# $Id$
# Maintainer: Paul Mattal <paul@archlinux.org>

pkgname=lirc
pkgver=0.8.6
pkgrel=7
_kernver=2.6.35-ARCH
pkgdesc="Linux Infrared Remote Control kernel modules for stock arch kernel"
arch=('i686' 'x86_64')
url="http://www.lirc.org/"
license=('GPL')
depends=('lirc-utils=0.8.6' 'kernel26>=2.6.35' 'kernel26<2.6.36')
makedepends=('help2man' 'kernel26-headers>=2.6.35' 'kernel26-headers<2.6.36')
replaces=('lirc+pctv')
options=('!makeflags')
install=$pkgname.install
source=("http://prdownloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.bz2"
        'kernel-2.6.33.patch'
        'kernel-2.6.35.patch')

build() {
	# configure
	cd ${srcdir}/lirc-${pkgver}
	# see http://bugs.gentoo.org/301321
	patch -p0 -i ../kernel-2.6.33.patch
        patch -Np0 -i ../kernel-2.6.35.patch
	./configure --enable-sandboxed --prefix=/usr \
		--with-driver=all --with-kerneldir=/usr/src/linux-${_kernver}/ \
		--with-moduledir=/lib/modules/${_kernver}/kernel/drivers/misc \
	        --with-transmitter

	# disable parallel and bt829
        # because of incompatibility with smp systems
        sed -i -e "s:lirc_parallel::" -e "s:lirc_bt829::" \
		Makefile drivers/Makefile drivers/*/Makefile tools/Makefile

	# disable lirc_gpio due to brokeness of kernel 2.6.23
	sed -i -e "s:lirc_gpio::" \
		Makefile drivers/Makefile drivers/*/Makefile tools/Makefile

	# build
	cd drivers
	make
	make DESTDIR=${pkgdir} install

	# set the kernel we've built for inside the install script
	sed -i -e "s/KERNEL_VERSION=.*/KERNEL_VERSION=${_kernver}/g" \
		${startdir}/lirc.install
}
md5sums=('4ca24da6f5e7c2dcea74878c27a4a3f7'
         'f029698154cf32bc51e5e69879d53a12'
         'fdc37867570915123acc8daec03f7174')
