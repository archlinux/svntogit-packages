# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libdrm
pkgver=2.4.33
pkgrel=1
pkgdesc="Userspace interface to kernel DRM services"
arch=(i686 x86_64)
license=('custom')
depends=('glibc' 'libpciaccess')
makedepends=('cairo' 'valgrind')
options=('!libtool')
url="http://dri.freedesktop.org/"
source=(http://dri.freedesktop.org/${pkgname}/${pkgname}-${pkgver}.tar.bz2
        no-pthread-stubs.patch
        COPYING
#	git_fixes.diff
)
sha1sums=('4da2c635491724e44326871e6a49ccfec0b6b5a6'
          '825ff5e0c4238b31bdea52f104bfec8949270e25'
          'ba3dcd636997ee0d30df14b03dae05c24ae5d094')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/no-pthread-stubs.patch"

  # git fixes - currently none
#  patch -Np1 -i ${srcdir}/git_fixes.diff
  
  #libtoolize --force
  autoreconf --force --install
  ./configure --prefix=/usr \
      --enable-udev \
      --enable-intel \
      --enable-radeon \
      --enable-vmwgfx-experimental-api \
      --enable-nouveau-experimental-api
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make -k check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 "${srcdir}/COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/"
}
