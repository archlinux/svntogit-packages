# Maintainer: Antonio Rojas <arojas@archlinux.org> 
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=umbrello
pkgver=15.08.0
pkgrel=2
pkgdesc='UML modeller'
arch=(i686 x86_64)
url='http://kde.org/applications/development/umbrello'
license=(GPL)
depends=(hicolor-icon-theme ktexteditor kinit)
makedepends=(extra-cmake-modules kdoctools python)
conflicts=(kdesdk-umbrello)
replaces=(kdesdk-umbrello)
groups=(kde-applications kdesdk)
source=("http://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz" 'qwindowtitle.patch')
install=$pkgname.install
md5sums=('8eab51485322ddbc69543e0b9ab0624f')

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
# Fix launching umbrello from the desktop file
  patch -p1 -i ../qwindowtitle.patch
}

build() { 
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_KF5=ON \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}

