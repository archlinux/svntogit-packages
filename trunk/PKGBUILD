# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=libmbim
pkgver=1.22.0
pkgrel=1
pkgdesc="MBIM modem protocol helper library"
arch=(x86_64)
url="https://www.freedesktop.org/wiki/Software/libmbim/"
license=(GPL2)
depends=(glib2 bash systemd libgudev)
makedepends=(gtk-doc python git help2man autoconf-archive)
_commit=697465da7e976d15b13061368d37f76d8e0c33c4  # tags/1.22.0
source=("git+https://gitlab.freedesktop.org/mobile-broadband/libmbim.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib --disable-static --enable-gtk-doc
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
