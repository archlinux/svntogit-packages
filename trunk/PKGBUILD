# $Id$
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>

pkgname=rtkit
pkgver=0.10
pkgrel=2
pkgdesc="Realtime Policy and Watchdog Daemon"
arch=('i686' 'x86_64')
url="http://git.0pointer.de/?p=rtkit.git"
license=('GPL' 'custom:BSD')
depends=('dbus' 'polkit')
makedepends=('pkgconfig')
install=rtkit.install
source=(http://0pointer.de/public/$pkgname-$pkgver.tar.gz)
md5sums=('9ab7f2a25ddf05584ea2216dfe4cefd4')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --with-systemdsystemunitdir=/lib/systemd/system

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  # DBus configuration
  mkdir -p "$pkgdir/usr/share/dbus-1/interfaces"
  ./rtkit-daemon --introspect >"$pkgdir/usr/share/dbus-1/interfaces/org.freedesktop.RealtimeKit1.xml"

  # Client license
  mkdir -p "$pkgdir/usr/share/licenses/rtkit"
  sed -ne '4,25p' rtkit.c >"$pkgdir/usr/share/licenses/rtkit/COPYING"
}
