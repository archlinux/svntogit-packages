# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-alien-libxml2
pkgver=0.12
pkgrel=1
pkgdesc="Install the C libxml2 library on your system"
arch=('any')
license=('PerlArtistic')
url="https://metacpan.org/release/Alien-Libxml2"
depends=('libxml2' 'perl-alien-build')
makedepends=('perl-html-parser' 'perl-sort-versions' 'perl-test2-suite' 'perl-uri')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/P/PL/PLICEASE/Alien-Libxml2-$pkgver.tar.gz")
sha512sums=('664e746592f7e5045b012cc26d0fb70057f4c31d6ac32e8e5381b3b338a9e63e51ab03033e0ef2781aaaca734f6ddc1670e1160e76f4c3950eb5998c2c5aac9a')

build() {
  cd Alien-Libxml2-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Alien-Libxml2-$pkgver
  make test
}

package() {
  cd Alien-Libxml2-$pkgver
  make DESTDIR="$pkgdir" install
}
