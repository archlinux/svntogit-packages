# Maintainer: Antonio Rojas <arojas@archlinux.org> 

pkgname=kaccounts-integration
pkgver=19.12.2
pkgrel=2
pkgdesc='Small system to administer web accounts for the sites and services across the KDE desktop, including: Google, Facebook, Owncloud, IMAP, 
Jabber and others'
arch=(x86_64)
url='https://www.kde.org/applications/internet/'
license=(GPL)
groups=(kde-applications kdenetwork)
depends=(kcmutils signon-kwallet-extension signon-plugin-oauth2 signon-ui)
makedepends=(extra-cmake-modules kdoctools)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
         kdebug-415267.patch::"https://cgit.kde.org/kaccounts-integration.git/patch/?id=2420b04e")
sha256sums=('fefc25a7bfad2bb00462df0797ce18e28ff0a5a536ba465f856bdcfe6c8504d4'
            'SKIP'
            '5df8f84b9b8d5f18a352f31af5421b9baa0daa070e123c3fc08ed3c0dcc466b8')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../kdebug-415267.patch # Unbreak accounts KCM
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
