# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=pango-perl
pkgver=1.226
pkgrel=1
pkgdesc="Perl bindings for Pango"
arch=(i686 x86_64)
license=('LGPL')
url="http://gtk2-perl.sourceforge.net/"
makedepends=('perl-extutils-pkgconfig' 'perl-extutils-depends')
checkdepends=('gtk2-perl' 'xorg-server-xvfb')
depends=('pango' 'glib-perl' 'cairo-perl')
options=('!emptydirs')
source=(http://downloads.sourceforge.net/sourceforge/gtk2-perl/Pango-${pkgver}.tar.gz)
md5sums=('9ff2dee3cc1d37563ea98b91a45e6ba1')

build() {
  cd Pango-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Pango-$pkgver
  xvfb-run -a -s "-extension GLX -screen 0 1280x1024x24" make test
}

package() {
  cd Pango-$pkgver
  make install DESTDIR="${pkgdir}"
}
