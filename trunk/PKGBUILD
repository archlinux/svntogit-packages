# $Id$
# Maintainer: Dale Blount <dale@archlinux.org>
# Contributor: Gregor Ibic <gregor.ibic@intelicom.si>

pkgname=clamav
pkgver=0.96.5
pkgrel=1
pkgdesc='Anti-virus toolkit for Unix'
arch=('i686' 'x86_64')
depends=('bzip2' 'zlib' 'libtool')
options=(!libtool)
install="$pkgname.install"
license=('GPL')
backup=('etc/clamav/clamd.conf' 'etc/clamav/freshclam.conf' 'etc/conf.d/clamav')
url='http://www.clamav.net/'
source=("http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz"
        'clamav'
        'clamav.confd'
        'clamav.logrotate'
        'config.patch')

sha1sums=('a7253e30d40e0c03964a60ed589b356a115b26c5'
          '7f15f0b13a1c11235bc99ef0add01efd8a442f07'
          'cb116cdab49a810381a515cbcfb6a6c148547f07'
          'be3310d2b41a68ce06e33c84ab68ffe59fdce104'
          '701a61571788d10ff7af01597785835c6bfea918')

build() {
	cd "$srcdir/$pkgname-$pkgver"
	patch -p1 < ../config.patch
	./configure --prefix=/usr --sysconfdir=/etc/clamav \
		--with-dbdir=/var/lib/clamav --disable-clamav
	make
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install

	# make sure conf files get installed, cause make install
	# doesn't do that if clamav is already installed upon building.
	install -D -m644 etc/clamd.conf "$pkgdir/etc/clamav/clamd.conf"
	install -D -m644 etc/freshclam.conf "$pkgdir/etc/clamav/freshclam.conf"

	install -D -m644 ../clamav.logrotate "$pkgdir/etc/logrotate.d/clamav"
	install -D -m644 ../clamav.confd "$pkgdir/etc/conf.d/clamav"
	install -D -m755 ../clamav "$pkgdir/etc/rc.d/clamav"

	# un-distribute databases to require freshclam
	rm "$pkgdir"/var/lib/clamav/*.cvd
}
