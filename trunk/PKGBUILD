# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kolourpaint
pkgver=17.04.1
pkgrel=1
pkgdesc="Paint Program"
url="https://kde.org/applications/graphics/kolourpaint/"
arch=(i686 x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kdegraphics)
depends=(kdelibs4support libksane hicolor-icon-theme)
makedepends=(extra-cmake-modules kdesignerplugin kdoctools)
conflicts=(kdegraphics-kolourpaint)
replaces=(kdegraphics-kolourpaint)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('6ec834dae99bac4fbd7a9ab504006f766c4b97f11c134f56fb857842cb06f7d7'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
