# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=evolution-data-server
pkgver=3.8.0
pkgrel=1
pkgdesc="Centralized access to appointments and contacts"
arch=('i686' 'x86_64')
depends=('gnome-online-accounts' 'nss' 'krb5' 'libgweather' 'libical' 'db' 'libgdata')
makedepends=('intltool' 'gperf' 'gobject-introspection' 'vala')
options=('!libtool')
install=$pkgname.install
url="http://www.gnome.org"
license=('GPL')
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('fff7dca02abc52caa62ddbd93a9135c15da3f21c36c25774f3399fb7a6c1d41e')

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --with-openldap=yes \
      --libexecdir=/usr/lib/evolution-data-server \
      --with-krb5=/usr --with-libdb=/usr \
      --enable-vala-bindings --disable-uoa
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' -e 's/    if test "$export_dynamic" = yes && test -n "$export_dynamic_flag_spec"; then/      func_append compile_command " -Wl,-O1,--as-needed"\n      func_append finalize_command " -Wl,-O1,--as-needed"\n\0/' libtool
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
