# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=upower
pkgver=0.99.9
pkgrel=1
pkgdesc="Abstraction for enumerating power devices, listening to device events and querying history and statistics"
url="https://upower.freedesktop.org"
arch=(x86_64)
license=(GPL)
depends=(systemd libusb libimobiledevice libgudev)
makedepends=(intltool docbook-xsl gobject-introspection python2 git gtk-doc)
backup=(etc/UPower/UPower.conf)
_commit=59faabc3c2634b6677ae0e1f3d94a23bbca21b12  # tags/UPOWER_0_99_9^0
source=("git+https://anongit.freedesktop.org/git/upower#commit=$_commit")
md5sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed -e 's/UPOWER_//' -e 's/_/\./g' -e 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib \
    --disable-static
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
