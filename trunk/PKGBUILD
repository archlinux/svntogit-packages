# $Id$
# Contributor: dorphell <dorphell@archlinux.org>
# Contributor: Travis Willard <travis@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>

pkgname=libpng
pkgver=1.4.1
pkgrel=1
pkgdesc="A collection of routines used to create PNG format graphics files"
arch=('i686' 'x86_64')
url="http://www.libpng.org/pub/png/libpng.html"
license=('custom')
depends=('zlib')
options=('!libtool')
source=("http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.xz"
        "http://downloads.sourceforge.net/sourceforge/libpng-apng/libpng-${pkgver}-apng.patch.gz"
	'libpng14-enable-read-dither.patch')
md5sums=('d4cb0236cce9ce8ff49a22994a01f9e0'
         'cdf7cde368a4e0de8d5400a99a79957f'
         'd4696db4af61b175b705f51aeb9c8d7e')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Add animated PNG (apng) support
  # see http://sourceforge.net/projects/libpng-apng/
  patch -p1 -i "${srcdir}/libpng-${pkgver}-apng.patch" || return 1
  # Enabling read dither support - needed for xfig and transfig
  patch -p1 -i "${srcdir}/libpng14-enable-read-dither.patch" || return 1

  libtoolize --force --copy || return 1
  aclocal || return 1
  autoconf || return 1
  automake --add-missing || return 1

  ./configure --prefix=/usr || return 1
  make ECHO=echo || return 1
  make ECHO=echo DESTDIR="${pkgdir}" install || return 1

  cd contrib/pngminus
  make ECHO=echo PNGLIB="-L${pkgdir}/usr/lib -lpng" -f makefile.std png2pnm pnm2png
  install -m755 png2pnm pnm2png "${pkgdir}/usr/bin/"
  install -D -m644 ../../LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE" || return 1
}
