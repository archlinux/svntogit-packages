# $Id$
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=meld
pkgver=1.4.0
pkgrel=3
pkgdesc='Visual diff and merge tool'
arch=('any')
url='http://meld.sourceforge.net/'
license=('GPL')
depends=('pygtk')
makedepends=('intltool' 'gnome-doc-utils')
optdepends=('pygtksourceview2: syntax highlighting'
            'gnome-python: gnome integration')
install=meld.install
source=("http://ftp.gnome.org/pub/gnome/sources/${pkgname}/1.4/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('0edb78d9da2bfe7e630742111eb6d8edc476c8cc91b1f6403d940d072dd75469')

options=('force')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make PYTHON=python2 prefix=/usr
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make PYTHON=python2 prefix=/usr DESTDIR="${pkgdir}" install
    sed -i -e 's#env python#env python2#' "${pkgdir}"/usr/bin/meld
}
