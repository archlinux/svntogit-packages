# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Roman Kyrylych <Roman.Kyrylych@gmail.com>
# Contributor: Roman Kyrylych <Roman.Kyrylych@gmail.com>

pkgname=iniparser
pkgver=4.1
pkgrel=1
pkgdesc="A free stand-alone ini file parsing library written in portable ANSI C"
arch=('x86_64')
url="http://ndevilla.free.fr/iniparser/"
license=('MIT')
depends=('glibc')
makedepends=('chrpath')
source=($pkgname-$pkgver.tar.gz::https://github.com/ndevilla/iniparser/archive/v$pkgver.tar.gz)
md5sums=('e43b722c71b399ab17c329c04dbdf1d7')

build() {
  cd $srcdir/$pkgname-$pkgver
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  install -Dm644 src/iniparser.h $pkgdir/usr/include/iniparser.h
  install -Dm644 src/dictionary.h $pkgdir/usr/include/dictionary.h
  install -Dm644 libiniparser.a $pkgdir/usr/lib/libiniparser.a
  install -Dm644 libiniparser.so.1 $pkgdir/usr/lib/libiniparser.so.1
  install -Dm644 LICENSE $pkgdir/usr/share/licenses/iniparser/LICENSE
  ln -sf libiniparser.so.1 $pkgdir/usr/lib/libiniparser.so
  chrpath -d $pkgdir/usr/lib/libiniparser.so
}
