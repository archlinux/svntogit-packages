# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=markdownpart
pkgver=20.12.3
pkgrel=1
pkgdesc='KPart for rendering Markdown content'
arch=(x86_64)
url='https://kde.org/'
license=(GPL)
depends=(kparts)
makedepends=(extra-cmake-modules)
groups=(kde-applications kde-utilities)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('74e598e1ae9331f4f442ca7cc3c988f0ec569a6579996bee92f72b22dd0179bc'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
