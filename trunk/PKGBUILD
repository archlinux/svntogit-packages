# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=bluedevil
pkgver=5.6.5
pkgrel=1
epoch=1
pkgdesc='Integrate the Bluetooth technology within KDE workspace and applications'
arch=('i686' 'x86_64')
url='https://www.kde.org/workspaces/plasmadesktop/'
license=('GPL2')
depends=('bluez-qt' 'kio')
makedepends=('extra-cmake-modules' 'plasma-framework' 'kded' 'python')
conflicts=('bluedevil-frameworks')
replaces=('bluedevil-frameworks')
groups=('plasma')
source=("http://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('1ddac54394a5eb94c1c0be03a2f04a9839676d354546f52f132f0d5005a8c97d'
            'SKIP')
validpgpkeys=('13C16D03EDE728514473AA73A506E6D4DD4D5088') # Jonathan Riddell

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
