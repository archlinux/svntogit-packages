# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgbase=mesa
pkgname=('mesa' 'libgl' 'ati-dri' 'intel-dri' 'unichrome-dri' 'mach64-dri' 'mga-dri' 'r128-dri' 'savage-dri' 'sis-dri' 'tdfx-dri' 'nouveau-dri')
pkgver=7.9.99.git20101222
pkgrel=1
arch=('i686' 'x86_64')
makedepends=('glproto>=1.4.12' 'pkgconfig' 'libdrm>=2.4.23' 'libxxf86vm>=1.1.0' 'libxdamage>=1.1.3' 'expat>=2.0.1' 'libx11>=1.3.5' 'libxt>=1.0.8' 
             'gcc-libs>=4.5' 'dri2proto=2.3' 'python2' 'talloc' 'libxml2' 'imake')
url="http://mesa3d.sourceforge.net"
license=('custom')
source=( # ftp://ftp.freedesktop.org/pub/mesa/${pkgver}/MesaLib-${pkgver}.tar.bz2
	# mesa git shot from 7.10 branch - see for state: http://cgit.freedesktop.org/mesa/mesa/commit/?h=7.10&id=b7c187df9ac9bb778cf0f2abb3ad86775e42b07b
	ftp://ftp.archlinux.org/other/mesa/mesa-b7c187df9ac9bb778cf0f2abb3ad86775e42b07b.tar.bz2
        ftp://ftp.archlinux.org/other/mesa/gl-manpages-1.0.1.tar.bz2
        LICENSE)
md5sums=('66eabc6f14e6e2c090024d99d17d3444'
         '6ae05158e678f4594343f32c2ca50515'
         '5c65a0fe315dd347e09b1f2826a1df5a')

build() {
#  cd "${srcdir}/Mesa-${pkgver}"
  cd ${srcdir}/mesa-*

  # required for git build
#  autoreconf -vfi

  # python2 build fixes
  sed -i -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
         -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" $(find $srcdir -name '*.py')
  sed -i -e "s|PYTHON2 = python|PYTHON2 = python2|" configs/{default,autoconf.in}
  sed -i -e "s|python|python2|" src/gallium/auxiliary/Makefile

#  ./configure --prefix=/usr \
  ./autogen.sh --prefix=/usr \
    --with-dri-driverdir=/usr/lib/xorg/modules/dri \
    --disable-egl \
    --enable-gallium-radeon \
    --enable-gallium-nouveau \
    --enable-glx-tls \
    --with-driver=dri \
    --enable-xcb \
    --with-state-trackers=dri,glx \
    --disable-glut
  make

  cd "${srcdir}/gl-manpages-1.0.1"
  ./configure --prefix=/usr
  make
}

package_libgl() {
  depends=('libdrm>=2.4.22' 'libxxf86vm>=1.1.0' 'libxdamage>=1.1.3' 'expat>=2.0.1')
  pkgdesc="Mesa 3-D graphics library and DRI software rasterizer"

#  cd "${srcdir}/Mesa-${pkgver}"
  cd ${srcdir}/mesa-*
  install -m755 -d "${pkgdir}/usr/lib"
  install -m755 -d "${pkgdir}/usr/lib/xorg/modules/extensions"

  bin/minstall lib/libGL.so* "${pkgdir}/usr/lib/"

  cd src/mesa/drivers/dri
  make -C swrast DESTDIR="${pkgdir}" install
  ln -s libglx.xorg "${pkgdir}/usr/lib/xorg/modules/extensions/libglx.so"

  install -m755 -d "${pkgdir}/usr/share/licenses/libgl"
  install -m755 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/libgl/"
}

package_mesa() {
  depends=('libgl' 'libx11>=1.3.5' 'libxt>=1.0.8' 'gcc-libs>=4.5' 'dri2proto=2.3' 'libdrm>=2.4.22' 'glproto>=1.4.12')
  pkgdesc="Mesa 3-D graphics libraries and include files"

#  cd "${srcdir}/Mesa-${pkgver}"
  cd ${srcdir}/mesa-*
  make DESTDIR="${pkgdir}" install

  rm -f "${pkgdir}/usr/lib/libGL.so"*
  rm -rf "${pkgdir}/usr/lib/xorg"
  rm -f "${pkgdir}/usr/include/GL/glew.h"
  rm -f "${pkgdir}/usr/include/GL/glxew.h"
  rm -f "${pkgdir}/usr/include/GL/wglew.h"
  rm -f "${pkgdir}/usr/include/GL/glut.h"

  cd "${srcdir}/gl-manpages-1.0.1"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/mesa"
  install -m755 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/mesa/"
}

package_ati-dri() {
  depends=("libgl=${pkgver}")
  pkgdesc="Mesa DRI + Gallium3D r300 drivers for AMD/ATI Radeon"
  conflicts=('xf86-video-ati<6.9.0-6')

#  cd "${srcdir}/Mesa-${pkgver}/src/mesa/drivers/dri"
  cd ${srcdir}/mesa-*/src/mesa/drivers/dri
  make -C radeon DESTDIR="${pkgdir}" install
  make -C r200 DESTDIR="${pkgdir}" install
  # classic mesa driver for R300 r300_dri.so
  #make -C r300 DESTDIR="${pkgdir}" install  <------- depricated
  # gallium3D driver for R300 r300_dri.so
#  make -C ${srcdir}/Mesa-${pkgver}/src/gallium/targets/dri-r300 DESTDIR="${pkgdir}" install
  make -C ${srcdir}/mesa-*/src/gallium/targets/dri-r300 DESTDIR="${pkgdir}" install
  make -C r600 DESTDIR="${pkgdir}" install
}

package_intel-dri() {
  depends=("libgl=${pkgver}")
  pkgdesc="Mesa DRI drivers for Intel"

#  cd "${srcdir}/Mesa-${pkgver}/src/mesa/drivers/dri"
  cd ${srcdir}/mesa-*/src/mesa/drivers/dri
  make -C i810 DESTDIR="${pkgdir}" install
  make -C i915 DESTDIR="${pkgdir}" install
  make -C i965 DESTDIR="${pkgdir}" install
}

package_unichrome-dri() {
  depends=("libgl=${pkgver}")
  pkgdesc="Mesa DRI drivers for S3 Graphics/VIA Unichrome"

#  cd "${srcdir}/Mesa-${pkgver}/src/mesa/drivers/dri"
  cd ${srcdir}/mesa-*/src/mesa/drivers/dri
  make -C unichrome DESTDIR="${pkgdir}" install
}

package_mach64-dri() {
  depends=("libgl=${pkgver}")
  pkgdesc="Mesa DRI drivers for ATI Mach64"
  conflicts=('xf86-video-mach64<6.8.2')

#  cd "${srcdir}/Mesa-${pkgver}/src/mesa/drivers/dri"
  cd ${srcdir}/mesa-*/src/mesa/drivers/dri
  make -C mach64 DESTDIR="${pkgdir}" install
}

package_mga-dri() {
  depends=("libgl=${pkgver}")
  pkgdesc="Mesa DRI drivers for Matrox"
  conflicts=('xf86-video-mga<1.4.11')

#  cd "${srcdir}/Mesa-${pkgver}/src/mesa/drivers/dri"
  cd ${srcdir}/mesa-*/src/mesa/drivers/dri
  make -C mga DESTDIR="${pkgdir}" install
}

package_r128-dri() {
  depends=("libgl=${pkgver}")
  pkgdesc="Mesa DRI drivers for ATI Rage128"
  conflicts=('xf86-video-r128<6.8.1')

#  cd "${srcdir}/Mesa-${pkgver}/src/mesa/drivers/dri"
  cd ${srcdir}/mesa-*/src/mesa/drivers/dri
  make -C r128 DESTDIR="${pkgdir}" install
}

package_savage-dri() {
  depends=("libgl=${pkgver}")
  pkgdesc="Mesa DRI drivers for S3 Sraphics/VIA Savage"
  conflicts=('xf86-video-savage<2.3.1')

#  cd "${srcdir}/Mesa-${pkgver}/src/mesa/drivers/dri"
  cd ${srcdir}/mesa-*/src/mesa/drivers/dri
  make -C savage DESTDIR="${pkgdir}" install
}

package_sis-dri() {
  depends=("libgl=${pkgver}")
  pkgdesc="Mesa DRI drivers for SiS"
  conflicts=('xf86-video-sis<0.10.2')

#  cd "${srcdir}/Mesa-${pkgver}/src/mesa/drivers/dri"
  cd ${srcdir}/mesa-*/src/mesa/drivers/dri
  make -C sis DESTDIR="${pkgdir}" install
}

package_tdfx-dri() {
  depends=("libgl=${pkgver}")
  pkgdesc="Mesa DRI drivers for 3dfx"
  conflicts=('xf86-video-tdfx<1.4.3')

#  cd "${srcdir}/Mesa-${pkgver}/src/mesa/drivers/dri"
  cd ${srcdir}/mesa-*/src/mesa/drivers/dri
  make -C tdfx DESTDIR="${pkgdir}" install
}

package_nouveau-dri() {
  depends=("libgl=${pkgver}")
  pkgdesc="Mesa classic DRI + Gallium3D drivers for Nouveau"

#  cd "${srcdir}/Mesa-${pkgver}/src/mesa/drivers/dri"
  cd ${srcdir}/mesa-*/src/mesa/drivers/dri
  # classic mesa driver for nv10 , nv20 nouveau_vieux_dri.so
  make -C nouveau DESTDIR="${pkgdir}" install
  # gallium3D driver for nv30 - nv40 - nv50 nouveau_dri.so
#  make -C ${srcdir}/Mesa-${pkgver}/src/gallium/targets/dri-nouveau DESTDIR="${pkgdir}" install
  make -C ${srcdir}/mesa-*/src/gallium/targets/dri-nouveau DESTDIR="${pkgdir}" install
}

