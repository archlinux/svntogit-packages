# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: G_Syme <demichan(at)mail(dot)upb(dot)de>

pkgname=libass
pkgver=0.9.13
pkgrel=1
pkgdesc="A portable library for SSA/ASS subtitles rendering"
arch=('i686' 'x86_64')
url="http://code.google.com/p/libass/"
license=('BSD')
depends=('enca' 'fontconfig' 'libpng')
makedepends=('pkgconfig')
options=(!libtool)
source=("http://libass.googlecode.com/files/${pkgname}-${pkgver}.tar.xz")
md5sums=('d99381922dcbeb7a766d2e7825cca193')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
