# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gom
pkgver=0.3.1
pkgrel=1
pkgdesc="A GObject to SQLite object mapper"
arch=(i686 x86_64)
url="http://www.gnome.org"
license=(LGPL2.1)
depends=(glib2 sqlite)
makedepends=(intltool gtk-doc gobject-introspection gdk-pixbuf2)
source=(http://download.gnome.org/sources/$pkgname/${pkgver:0:3}/$pkgname-$pkgver.tar.xz)
sha256sums=('7951eb46ee784cbdbee6e3f2da084ffbf776c11ca1c904404b05feafe37e38f5')
 
build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
    --disable-static --enable-gtk-doc
  make
}

check() {
  cd $pkgname-$pkgver
  make -k check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
