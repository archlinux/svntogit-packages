# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>

pkgname=libxslt
pkgver=1.1.29
pkgrel=1
pkgdesc="XML stylesheet transformation library"
arch=('i686' 'x86_64')
url="http://xmlsoft.org/XSLT/"
license=('custom')
depends=('libxml2' 'libgcrypt')
makedepends=('python2')
checkdepends=('docbook-xml' 'python')
source=(http://xmlsoft.org/sources/${pkgname}-${pkgver}.tar.gz)
md5sums=('a129d3c44c022de3b9dcf6d6f288d72e')

prepare() {
  cd $pkgname-$pkgver
  sed -e 's|/usr/bin/python -u|/usr/bin/python2 -u|g' -e 's|/usr/bin/python$|/usr/bin/python2|g' -i python/tests/*.py
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --with-python=/usr/bin/python2

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="${pkgdir}" install

  install -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"

  rm -f "${pkgdir}"/usr/lib/python*/site-packages/*.a
}
