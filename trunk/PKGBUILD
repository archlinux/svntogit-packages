# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-maps
pkgver=43.3
pkgrel=1
pkgdesc="A simple GNOME 3 maps application"
url="https://wiki.gnome.org/Apps/Maps"
arch=(x86_64)
license=(GPL)
depends=(gjs gtk4 libadwaita geocode-glib-2 geoclue libshumate librest libxml2
         libgweather-4)
makedepends=(gobject-introspection git meson yelp-tools)
groups=(gnome)
options=(debug)
_commit=1573a92044c05fbf1622958944372e3bfab55461  # tags/v43.3^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-maps.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
