# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Sergej Pupykin <arch+pub@sergej.pp.ru>
# Contributor: EVorster <evorster@gmail.com>

pkgname=vid.stab
pkgver=1.1
pkgrel=2
pkgdesc='Video stabilization library'
arch=('x86_64')
url='http://public.hronopik.de/vid.stab'
license=('GPL')
depends=('gcc-libs' 'glibc')
makedepends=('cmake' 'git')
provides=('libvidstab.so')
_hash='2d82492533bf5004bc2c4f8213a1b622c45f6a44'
source=("vid.stab-${pkgver}::git+https://github.com/georgmartius/vid.stab.git#commit=${_hash}")
sha256sums=('SKIP')

prepare() {
  cd vid.stab-${pkgver}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd vid.stab-${pkgver}/build

  cmake .. \
    -DCMAKE_INSTALL_PREFIX='/usr'
  make
}

package() {
  cd vid.stab-${pkgver}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
