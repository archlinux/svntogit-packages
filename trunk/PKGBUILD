# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kcontacts
epoch=1
pkgver=5.64.0
pkgrel=1
pkgdesc="Address book API for KDE"
arch=(x86_64)
url="https://community.kde.org/Frameworks"
license=(LGPL)
depends=(kcoreaddons kconfig ki18n kcodecs iso-codes)
makedepends=(extra-cmake-modules doxygen qt5-tools qt5-doc)
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('0bf0a1ba6ebedd400bed7a490093962cde6a2b26c49627d6770a71524db63058'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
