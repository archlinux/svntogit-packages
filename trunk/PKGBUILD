# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
pkgname=gdm
pkgver=2.30.2
pkgrel=2
pkgdesc="Gnome Display Manager (a reimplementation of xdm)"
arch=(i686 x86_64)
license=('GPL')
url="http://www.gnome.org"
backup=('etc/pam.d/gdm' 'etc/pam.d/gdm-autologin' 'etc/gdm/custom.conf')
groups=('gnome-extra')
options=('!libtool')
depends=('libcanberra>=0.23' 'libxklavier>=5.0' 'tcp_wrappers' 'gnome-session>=2.30.0' 'upower>=0.9.2')
makedepends=('intltool' 'gnome-doc-utils>=0.20.1' 'xorg-server' 'gnome-panel>=2.30.0')
optdepends=('gnome-panel: needed for user switch applet')
conflicts=('fast-user-switch-applet')
replaces=('fast-user-switch-applet')
install=gdm.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.30/${pkgname}-${pkgver}.tar.bz2
        04_fix_external_program_directories.patch
        gdm.pam
        gdm-autologin.pam
        gdm)
sha256sums=('884f4816a78f8f1eece8f1468f19dc01c5350d4a3513d3c0e854f45f870c1ec5'
            'cb585c39c6671387b5a25f21a99c97e3a20eaf4188fea1b62871681a1ebf8bff'
            'f1dfa4d88288d4b0a631a68a51b46c2da537bee8fe5a99f9f288c8ff75a50b19'
            '3daff680ff6b7ea56f84f40843e46e72477c81e9e405028203c942af04d07ae5'
            '272c08d8e8b50bf424d0705ac864d4c18c47ec4f6893b1af732c2efbc86c9550')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/04_fix_external_program_directories.patch" || return 1
  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/gdm --localstatedir=/var --disable-static \
      --with-at-spi-registryd-directory=/usr/lib/at-spi \
      --disable-scrollkeeper || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1
  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gdm ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -r "${pkgdir}/etc/gconf/schemas/"

  install -m644 "${srcdir}/gdm.pam" "${pkgdir}/etc/pam.d/gdm" || return 1
  install -m644 "${srcdir}/gdm-autologin.pam" "${pkgdir}/etc/pam.d/gdm-autologin" || return 1

  install -m755 -d "${pkgdir}/etc/rc.d"
  install -m755 "${srcdir}/gdm" "${pkgdir}/etc/rc.d/" || return 1

  rmdir "${pkgdir}/var/gdm"
  chmod 1770 "${pkgdir}/var/log/gdm"
}
