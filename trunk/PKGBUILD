# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-fonts-100dpi
pkgver=1.0.4
pkgrel=1
pkgdesc="X.org 100dpi fonts"
arch=('any')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('xorg-fonts-alias-100dpi')
makedepends=('xorg-util-macros' 'xorg-mkfontscale' 'xorg-bdftopcf' 'xorg-font-util')
groups=('xorg')
source=(${url}/releases/individual/font/font-adobe-100dpi-1.0.4.tar.xz
        ${url}/releases/individual/font/font-adobe-utopia-100dpi-1.0.4.tar.bz2
        ${url}/releases/individual/font/font-bh-100dpi-1.0.3.tar.bz2
        ${url}/releases/individual/font/font-bh-lucidatypewriter-100dpi-1.0.3.tar.bz2
        ${url}/releases/individual/font/font-bitstream-100dpi-1.0.3.tar.bz2)
sha512sums=('fedb86672c3b9cb624ee5d74d1d710f21d92c2d48a1fcf0a45b54b3f992f6f5599fc271d38a7e25ef123b64d405fa60a3d46883fe7a9d71ebad83c9b418f8383'
            'fecb9a9bde99a82787d12779ea579c8696dcce168acd74b478a879ae24d421d5978d5f69da651e8ff3b25dca355960aaa19f69f2bf61e540464887e546a7b94b'
            '60532fb3bd25bad08b1db65f96fdd1cdb2ae5fd580729d7851f5b619f1ed6bdef4fec30111ec408cec9ae5e94f92ad9cf214214c01ac684f3a4bd8d43bafd8a2'
            '1862003c20aa7709ea07487a4326c619e2beb33b7069d644f438d64182b41a06039c69e2867921b4d12939612a4b8e1e1727fd15851aeab9a0361060183e3b0f'
            '10fd920d46d2cb1b314e8c2f05c202e9ffa74a4e5315f34790eba8bd8fcda865a6932eb712a7538624e69367647bcd6891e7015099e65463aeef772d0ba58bfd')

build() {
  cd "${srcdir}"
  for dir in font-*-100dpi*; do
    if [ -d "${dir}" ]; then
      pushd ${dir}
      ./configure --prefix=/usr \
          --with-fontdir=/usr/share/fonts/100dpi
      make
      popd
    fi
  done
}

package() {
  cd "${srcdir}"
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  for dir in font-*-100dpi*; do
    if [ -d "${dir}" ]; then
      pushd ${dir}
      make DESTDIR="${pkgdir}" install
      install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.${dir%-100dpi-*}"
      popd
    fi
  done
  rm -f "${pkgdir}"/usr/share/fonts/100dpi/fonts.*
}
