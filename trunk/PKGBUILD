# Maintainer:
# Contributor: eric <eric@archlinux.org>
# Contributor: Markus Meissner <markus@meissna.de>

pkgname=tidy
pkgver=5.7.16
_commit=5f7e367cb54563dabda4bf4e3c11c6ecc68a0fa3
pkgrel=1
pkgdesc="A tool to tidy down your HTML code to a clean style"
arch=(x86_64)
url="http://www.html-tidy.org/"
license=(custom)
depends=(glibc)
makedepends=(cmake libxslt)
conflicts=(tidyhtml)
provides=(tidyhtml)
replaces=(tidyhtml)
source=("$pkgname-$_commit.tar.gz::https://github.com/htacg/tidy-html5/archive/$_commit.tar.gz")
sha512sums=('2854f81a4dcc5f0a995360b85a6169a4320a823e1982c12fba5fbe5d10afca442719e8a4ed719d038e7cf723a43523dc4294b2c751a29a8dfd5f471d96079767')

prepare() {
  mv tidy-html5-{$_commit,$pkgver}
  mkdir -p build
}

build() {
  cd build
  cmake ../tidy-html5-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

# Compatibility symlinks until everything is ported
  ln -s tidybuffio.h "$pkgdir"/usr/include/buffio.h
  ln -s tidyplatform.h "$pkgdir"/usr/include/platform.h

  install -Dm644 "$srcdir"/$pkgname-html5-$pkgver/README/LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
