#$Id$
# Maintainer: Jan De Groot <jgc@archlinux.org>

pkgname=gnome-keyring
pkgver=3.2.2
pkgrel=3
pkgdesc="GNOME Password Management daemon"
arch=(i686 x86_64)
license=('GPL' 'LGPL')
depends=('gtk3' 'dconf' 'libgcrypt' 'libcap-ng' 'p11-kit' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('intltool' 'python2')
groups=('gnome')
options=('!libtool' '!emptydirs')
url="http://www.gnome.org"
install=gnome-keyring.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz
        silence_no_socket_to_connect.patch)
sha256sums=('f4cdc2c492a9b0157d59439310093e611e1f718a16f7ee2391ac03aadacfaaa3'
            '4cf3965e9beea747a7916682d37f36bc481bee10247788d4dd53f6e27fe5c041')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -i "$srcdir/silence_no_socket_to_connect.patch"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --libexecdir=/usr/lib/gnome-keyring \
      --with-pam-dir=/lib/security --with-root-certs=/etc/ssl/certs \
      --disable-schemas-compile --disable-update-mime
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
