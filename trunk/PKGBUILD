# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=dmidecode
pkgver=2.12
pkgrel=2
pkgdesc="Desktop Management Interface table related utilities"
arch=('i686' 'x86_64')
url="http://www.nongnu.org/dmidecode"
license=('GPL')
depends=('glibc')
source=("http://download.savannah.nongnu.org/releases/${pkgname}/${pkgname}-${pkgver}.tar.bz2")
md5sums=('a406f3cbb27736491698697beeddb781')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i "s:sbin:bin:g" Makefile
  make prefix=/usr
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make prefix=/usr DESTDIR="${pkgdir}" install
}
