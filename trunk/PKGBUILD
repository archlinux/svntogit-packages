# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=libgssglue
pkgver=0.7
pkgrel=1
pkgdesc="Library exporting a gssapi interface to call gssapi routines in other libraries"
arch=(x86_64)
url="https://gitlab.com/gsasl/libgssglue/"
license=(BSD)
depends=(glibc)
provides=(libgssglue.so)
backup=(etc/gssapi_mech.conf)
source=(https://gitlab.com/gsasl/$pkgname/-/archive/$pkgname-$pkgver/$pkgname-$pkgname-$pkgver.tar.gz)
sha512sums=('6f3b2bec37fec376fcdbf804593c01d6ba35541e482956f454583e6cd9e623f4d0f85ee75302621c38abe67c7675a39312bc25522985253482aa7012b8b6688e')
b2sums=('ade4c5eaf642e79090d84688f4c4e2abd07b1cdc955fafaf439c1453dc64e25fd2a7433b5330dd70e8a1559b1270af5f2029ab3027f53b1d01c05e6a7532489c')

prepare() {
  cd $pkgname-$pkgname-$pkgver
  touch ChangeLog
  autoreconf -fiv
}

build() {
  cd $pkgname-$pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  make DESTDIR="$pkgdir/" install -C $pkgname-$pkgname-$pkgver
  install -vDm 644 $pkgname-$pkgname-$pkgver/doc/gssapi_mech.conf -t "$pkgdir/etc/"
  install -vDm 644 $pkgname-$pkgname-$pkgver/COPYING -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 $pkgname-$pkgname-$pkgver/{AUTHORS,README,NEWS} -t "$pkgdir/usr/share/doc/$pkgname/"
}
