# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Antonio Rojas
# Contributor: Alexey D. <lq07829icatm at rambler.ru>

pkgbase=plasma-workspace
pkgname=('plasma-workspace' 'drkonqi')
pkgver=5.2.2
pkgrel=1
pkgdesc='KDE Plasma Workspace'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/workspace/plasma-workspace'
license=('LGPL')
# note on libxdamage:
# not detected by namcap because libgl depends on it
# but nvidia providing libgl does not depend on libxdamage
depends=('knewstuff' 'kjsembed' 'knotifyconfig' 'libxdamage' 'kwayland'
         'libksysguard' 'libkscreen-frameworks' 'ktexteditor' 'libqalculate'
         'qt5-tools' 'kded' 'kde-cli-tools' 'xorg-xrdb' 'xorg-xsetroot'
         'xorg-xmessage' 'xorg-xprop' 'milou')
makedepends=('extra-cmake-modules' 'kdoctools' 'kwin' 'gpsd' 'baloo-frameworks'
             'krunner')
groups=('plasma')
source=("http://download.kde.org/stable/plasma/${pkgver}/${pkgbase}-${pkgver}.tar.xz"
        'kde.pam' 'lockscreen-focus.patch')
md5sums=('93b4b7e187035635982d3099ba2c8d79'
         '929b182dec8a096206ad493477c09d2c'
         '0dcc042d4188b2f664f9daf4a5923778')

prepare() {
  mkdir build

  cd ${pkgbase}-${pkgver}
  # be sure to use the Qt5 version of qtpaths
  sed -i 's:qtpaths:qtpaths-qt5:' startkde/startkde.cmake

# fix password field not getting focus in lock screen (upstream bug #344823)
  patch -p1 -i "$srcdir"/lockscreen-focus.patch
}

build() {
  cd build
  cmake ../${pkgbase}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIB_INSTALL_DIR=lib \
    -DLIBEXEC_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package_plasma-workspace() {
  optdepends=('plasma-workspace-wallpapers: additional wallpapers'
              'gpsd: GPS support for geolocation')
  conflicts=('kdebase-workspace')

  cd build
  make DESTDIR="${pkgdir}" install

  install -D "${srcdir}"/kde.pam \
    "${pkgdir}"/etc/pam.d/kde

  # Remove conflicts with drkonqi
  rm "${pkgdir}"/usr/lib/drkonqi
  rm -r "${pkgdir}"/usr/share/drkonqi
  rm "${pkgdir}"/usr/lib/libKF5XmlRpcClientPrivate.*
}

package_drkonqi() {
  pkgdesc='KDE crash handler'
  depends=('kdewebkit')

  cd build/drkonqi
  make DESTDIR="${pkgdir}" install
}
