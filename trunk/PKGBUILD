# $Id$
# Maintainer: 
# Contributor: Judd <jvinet@zeroflux.org>

pkgname=libevent
pkgver=2.0.11
pkgrel=1
pkgdesc="An event notification library"
arch=('i686' 'x86_64')
url="http://www.monkey.org/~provos/libevent/"
license=('GPL2')
depends=('openssl')
options=('!libtool')
source=("http://www.monkey.org/~provos/libevent-$pkgver-stable.tar.gz")
md5sums=('bd7ef33c08aa6401c8d67dbc88679ded')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}-stable"
  sed -i 's#python#python2#' event_rpcgen.py
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}-stable"
  make DESTDIR="${pkgdir}" install
}
