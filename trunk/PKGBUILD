# $Id$
# Maintainer: Jan de Groot <jan@archlinux.org>
# Contributor: Tom K <tomk@runbox.com>
pkgname=opal
pkgver=3.10.2
pkgrel=3
pkgdesc="Open Phone Abstraction Library"
arch=(i686 x86_64)
url="http://www.ekiga.org"
license=('GPL')
depends=('ptlib' 'libtheora' 'x264' 'speex')
makedepends=('ffmpeg')
options=(!makeflags)
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('8e227978263c07502b2bc4c9de0e71be1b95d673fe858bfd907910adfb2eb205')

build() {
  cd "${srcdir}/opal-${pkgver}"
  CXXFLAGS="$CXXFLAGS -fpermissive" \
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd "${srcdir}/opal-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
