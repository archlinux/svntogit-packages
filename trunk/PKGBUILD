# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: kevin <kevin@archlinux.org>
# Contributor: Manolis Tzanidakis

pkgname=perl-digest-sha1
pkgver=2.13
pkgrel=11
pkgdesc="Perl interface to the SHA-1 Algorithm"
arch=(x86_64)
license=('PerlArtistic')
url="http://search.cpan.org/dist/Digest-SHA1/"
depends=('perl')
options=('!emptydirs')
source=("https://www.cpan.org/authors/id/G/GA/GAAS/Digest-SHA1-$pkgver.tar.gz")
md5sums=('bd22388f268434f2b24f64e28bf1aa35')

build() {
  cd Digest-SHA1-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Digest-SHA1-$pkgver
  make test
}

package() {
  cd Digest-SHA1-$pkgver
  make install DESTDIR="$pkgdir"
}
# vim: ts=2 sw=2 et ft=sh
