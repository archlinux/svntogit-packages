# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Tobias Kieslich <tobias funnychar archlinux.org>
# Contributor: Alois Nespor alois.nespor@gmail.com

pkgname=xfburn
pkgver=0.5.4
pkgrel=2
pkgdesc="A simple CD/DVD burning tool based on libburnia libraries"
arch=('i686' 'x86_64')
url="http://goodies.xfce.org/projects/applications/xfburn"
license=('GPL')
depends=('libburn' 'libisofs' 'gtk2' 'libxfce4ui' 'exo' 'gst-plugins-base-libs'
         'libgudev' 'desktop-file-utils')
makedepends=('intltool')
groups=('xfce4-goodies')
install=xfburn.install
source=(http://archive.xfce.org/src/apps/xfburn/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=('9a0053ff106ae475871a2bed328d5d7c5a2fc4280a24e956ca85b6a13a9fc1f6')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --enable-gstreamer \
    --disable-debug
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
