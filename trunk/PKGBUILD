# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux,org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=krfb
pkgver=16.12.1
pkgrel=1
pkgdesc='Desktop Sharing'
url='http://www.kde.org/applications/system/krfb'
arch=(i686 x86_64)
license=(LGPL)
depends=(kdnssd kxmlgui kwallet libvncserver libxdamage)
makedepends=(extra-cmake-modules kdoctools python)
conflicts=(kdenetwork-krfb)
replaces=(kdenetwork-krfb)
groups=(kde-applications kdenetwork)
source=("http://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig} krfb-gcc6.patch)
md5sums=('a0a415ad3bb0f61baeb58f7ded1aec4e'
         'SKIP'
         'fc342abdbbac31a98c9839b2f61b88e7')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build

# Fix build with GCC6 (Fedora)
  cd $pkgname-$pkgver
  patch -p1 -i ../krfb-gcc6.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
