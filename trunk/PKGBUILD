# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=gitg
pkgver=3.32.1+r23+g33bb5d2d
pkgrel=1
epoch=2
pkgdesc="GNOME GUI client to view git repositories"
url="https://wiki.gnome.org/Apps/Gitg"
arch=(x86_64)
license=(GPL)
depends=(gtksourceview3 git libgit2-glib libgee webkit2gtk libpeas gtkspell3
         python-gobject libdazzle)
makedepends=(gobject-introspection git meson vala)
_commit=33bb5d2d896fcf2fc9a2ed50b236b727c5364895  # gnome-3-32
source=("git+https://gitlab.gnome.org/GNOME/gitg.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd gitg
  git describe --tags | sed 's/^v//;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd gitg

  # fix tests
  git cherry-pick -n b9f102c760adaee8185567007bcbe3b6604754f2
}

build() {
  arch-meson gitg build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  python -m compileall -d /usr/lib "$pkgdir/usr/lib"
  python -O -m compileall -d /usr/lib "$pkgdir/usr/lib"
}
