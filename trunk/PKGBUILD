# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: Eduardo Romero <eduardo@archlinux.org>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>

pkgname=libgphoto2
pkgver=2.5.23
pkgrel=1
pkgdesc="The core library of gphoto2, designed to allow access to digital camera by external programs."
url="http://www.gphoto.org/"
arch=(x86_64)
license=(LGPL)
depends=(libexif libjpeg gd libltdl libusb libxml2)
makedepends=(autoconf-archive git)
_commit=276c1a403f07aac74224a65079c51ef600c6837b  # tags/libgphoto2-2_5_23-release
source=("git+https://github.com/gphoto/libgphoto2#commit=$_commit")
sha256sums=('SKIP')
validpgpkeys=('7C4AFD61D8AAE7570796A5172209D6902F969C95') # Marcus Meissner

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^libgphoto2-//;s/-release//;s/_/./g;s/-/+/g'
}

prepare() {
  cd $pkgname
  git cherry-pick -n 236cd97b617972c38ac8e6780590b3049c0afa2f
  autoreconf -fvi
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --disable-rpath
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' -e 's/    if test "$export_dynamic" = yes && test -n "$export_dynamic_flag_spec"; then/      func_append compile_command " -Wl,-O1,--as-needed"\n      func_append finalize_command " -Wl,-O1,--as-needed"\n\0/' libtool
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install

  # Remove unused udev helper
  rm -r "$pkgdir/usr/lib/udev"

  # Remove recursive symlink
  rm "$pkgdir/usr/include/gphoto2/gphoto2"

  _genudev
}

_genudev() (
  cd "$pkgdir/usr/lib/libgphoto2"

  export LD_LIBRARY_PATH="$pkgdir/usr/lib${LD_LIBRARY_PATH:+:}$LD_LIBRARY_PATH"
  export CAMLIBS="$PWD/$pkgver"

  ./print-camera-list hwdb \
    | install -Dm644 /dev/stdin "$pkgdir/usr/lib/udev/hwdb.d/20-gphoto.hwdb"
  ./print-camera-list udev-rules version 201 \
    | install -Dm644 /dev/stdin "$pkgdir/usr/lib/udev/rules.d/40-gphoto.rules"
)

# vim:set sw=2 et:
