# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>

pkgname=kile
pkgver=2.9.93
pkgrel=3
pkgdesc='A user friendly TeX/LaTeX frontend for KDE'
arch=(x86_64)
license=(GPL2)
url='https://apps.kde.org/kile/'
depends=(ktexteditor kinit okular khtml texlive-core qt5-script)
makedepends=(extra-cmake-modules kdoctools)
optdepends=('konsole: embedded terminal' 'imagemagick: for some file type conversions')
source=(https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.bz2)
sha256sums=('04499212ffcb24fb3a6829149a7cae4c6ad5d795985f080800d6df72f88c5df0')

build() {
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
