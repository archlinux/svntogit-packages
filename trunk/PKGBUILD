# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=gsasl
pkgver=1.8.0
pkgrel=8
pkgdesc="Simple Authentication and Security Layer framework and a few common SASL mechanisms"
arch=('x86_64')
url="http://josefsson.org/gsasl/"
license=('GPL')
depends=('gnutls' 'libidn' 'krb5' 'libgcrypt')
source=(ftp://ftp.gnu.org/gnu/gsasl/${pkgname}-${pkgver}.tar.gz)
sha1sums=('343fd97ae924dc406986c02fb9b889f4114239ae')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -i 's|error too old libgcrypt|/*error too old libgcrypt*/|' lib/configure
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --with-gssapi-impl=mit
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
