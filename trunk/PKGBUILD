# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Jameson Pugh <imntreal@gmail.com>
# Contributor: Tim Jester-Pfadt <t.jp<at>gmx.de>

pkgbase=appstream
pkgname=(appstream appstream-qt)
pkgver=0.11.8
pkgrel=3
pkgdesc="Provides a standard for creating app stores across distributions"
arch=(x86_64)
url="http://distributions.freedesktop.org/wiki/AppStream"
license=(GPL)
depends=(libyaml libxml2 libstemmer glib2)
makedepends=(meson xmlto gobject-introspection python2 gtk-doc qt5-tools itstool vala)
source=("https://www.freedesktop.org/software/appstream/releases/AppStream-$pkgver.tar.xz"{,.asc}
        update-appstream-cache.hook
        https://github.com/ximion/appstream/commit/3e58f9c9.patch
        https://github.com/ximion/appstream/commit/bef5af5.patch
        https://github.com/ximion/appstream/commit/643b74f.patch
        )
sha256sums=('54a6c443e51c2e016fe5031f8f1865500bb7a7807b8514355f4841298b85edf3'
            'SKIP'
            'a6c42e4607ffd7d7dc931d608f1856e2ba7689d4caac0783e7100d86de0f951b'
            'e6aeca8a58187c92298fd42aa83eb29dea2db6810b0f55c34671e03de4ed8002'
            '1567ddcc5162ecdbf91f9518541d9ef537cd6740c4fc6bbdda07eb3815116a91'
            '8c4355f8c3eea7629a07bfd0a70dd2b1548476b74718c92ca1e63686fbf1a6c6')
validpgpkeys=(D33A3F0CA16B0ACC51A60738494C8A5FBF4DECEB) # Matthias Klumpp <matthias@tenstral.net>

prepare() {
  mkdir -p build

  cd AppStream-$pkgver
  patch -p1 -i ../3e58f9c9.patch # Fix build of packages against appstream-qt
  patch -p1 -i ../bef5af5.patch 
  patch -p1 -i ../643b74f.patch # Fix showing translated metadata
}

build() {
  cd build

  meson ../AppStream-$pkgver \
    --prefix=/usr \
    -Dqt=true \
    -Dvapi=true
  ninja
}

package_appstream() {
  cd build

  DESTDIR="$pkgdir" ninja install

# provided by -qt subpackage
  rm -r "$pkgdir"/usr/{include/AppStreamQt,lib/cmake,lib/libAppStreamQt.*}

  install -Dm644 "$srcdir"/update-appstream-cache.hook "$pkgdir"/usr/share/libalpm/hooks/update-appstream-cache.hook
}

package_appstream-qt() {
  pkgdesc='Qt5 interface for AppStream'
  depends=(appstream qt5-base)

  cd build
  DESTDIR="$pkgdir" ninja install

# provided by appstream
  rm -r "$pkgdir"{/etc,/usr/{bin,include/appstream,lib/{girepository-1.0,libappstream*,pkgconfig},share}}
}
