# $Id$
# Maintainer: Jan de Groot <jan@archlinux.org>

pkgname=gnome-menus
pkgver=3.5.92
pkgrel=1
pkgdesc="GNOME menu specifications"
arch=('i686' 'x86_64')
depends=('glib2' 'python2-gobject')
makedepends=('intltool' 'gobject-introspection')
options=('!libtool')
license=('GPL' 'LGPL')
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz
        menus.patch)
sha256sums=('e56022b4329e97fdcadf0c70212dba04c4da9c51be2036e6c931ab0fd48fef0f'
            'df2779777906b038fa911dc745534564ede9524a64ab368e2f6f8e38d54e7acc')

build() {
  cd "$pkgname-$pkgver"

  patch -Np0 -i "$srcdir/menus.patch"

  PYTHON=/usr/bin/python2 ./configure \
      --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var  --disable-static
  make
}

package(){
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  sed -i "1s|#!/usr/bin/env python$|&2|" \
    "$pkgdir/usr/lib/python2.7/site-packages/GMenuSimpleEditor/config.py"
}
