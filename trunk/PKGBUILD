# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgbase=hspell
pkgname=('hspell' 'hunspell-he')
pkgver=1.4
pkgrel=1
arch=('x86_64')
license=('GPL')
url="http://www.ivrix.org.il/projects/spell-checker/"
makedepends=('glibc' 'zlib' 'perl' 'hunspell' 'gawk')
options=('!makeflags')
source=(http://hspell.ivrix.org.il/${pkgname}-${pkgver}.tar.gz{,.sig})
sha256sums=('7310f5d58740d21d6d215c1179658602ef7da97a816bc1497c8764be97aabea3'
            'SKIP')
validpgpkeys=('996512CD85DC510BD45EAD46415E26D84D4E37DB')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  export PERLLIB="$PWD"
  ./configure --prefix=/usr --mandir=/usr/share/man \
      --enable-linginfo --enable-fatverb --enable-shared
  make 
  make hunspell
}

package_hspell() {

  pkgdesc="Hebrew spell-checker"
  depends=('glibc' 'zlib' 'perl')

  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  rm -f ${pkgdir}/usr/lib/libhspell.a
}

package_hunspell-he() {

  pkgdesc="Hebrew hunspell dictionary"
  
  cd "${srcdir}/${pkgbase}-${pkgver}"

  install -dm755 ${pkgdir}/usr/share/hunspell
  install -m644 he.dic ${pkgdir}/usr/share/hunspell/he_IL.dic
  install -m644 he.aff ${pkgdir}/usr/share/hunspell/he_IL.aff

  # the symlinks
  install -dm755 ${pkgdir}/usr/share/myspell/dicts
  pushd $pkgdir/usr/share/myspell/dicts
    for file in $pkgdir/usr/share/hunspell/*; do
      ln -sv /usr/share/hunspell/$(basename $file) .
    done
  popd
}
