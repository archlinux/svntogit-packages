# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kongress
pkgver=23.04.0
pkgrel=1
pkgdesc='Companion application for conferences'
url='https://apps.kde.org/kongress/'
arch=(x86_64)
license=(GPL LGPL)
groups=(kde-applications kde-utilities)
depends=(kirigami2 kcalendarcore qt5-svg ki18n kconfig kcoreaddons knotifications kdbusaddons)
makedepends=(extra-cmake-modules)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('69f992ab1c5fde33c892646106b6de6ba2f0763c2b010f8a810909f06f729a86'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
