# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: kusakata <shohei atmark kusakata period com>

pkgname=libixion
pkgver=0.11.1
pkgrel=5
pkgdesc="A general purpose formula parser & interpreter"
arch=('i686' 'x86_64')
url="https://gitlab.com/ixion/ixion/blob/master/README.md"
license=('custom')
depends=('boost-libs' 'python')
makedepends=('boost' 'mdds')
source=("https://kohei.us/files/ixion/src/libixion-${pkgver}.tar.xz"
        LICENSE)
sha256sums=('c9e9f52580d618fa969fc0293f55af21a9c74bfb802e655c6bf239202f95bede'
            '2bfdca60adf803108d4c7f009000bea76ad00e621e163197881b0eaae91b530e')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr \
    --disable-static
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make -k check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  # LICENSE file is missing in tarball
  install -Dm644 $srcdir/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

