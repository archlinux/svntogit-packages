# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kitinerary
pkgver=20.08.2
pkgrel=1
pkgdesc="Data model and extraction system for travel reservation information"
arch=(x86_64)
url="https://kontact.kde.org"
license=(LGPL)
depends=(kmime kpkpass kcalendarcore kcontacts poppler libphonenumber zxing-cpp)
makedepends=(extra-cmake-modules)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('69d8b5f64774860e5098c1dbfb06d274da4379c04383c4f0f0e412481c48fa27'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
