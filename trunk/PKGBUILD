# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=plasma-wayland-protocols
pkgver=1.1.0
pkgrel=2
pkgdesc="Plasma Specific Protocols for Wayland"
arch=(any)
url="https://kde.org/plasma-desktop"
license=(LGPL)
depends=()
makedepends=(qt5-base extra-cmake-modules)
source=("https://download.kde.org/stable/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
         kdebug-424780.patch::"https://invent.kde.org/libraries/plasma-wayland-protocols/-/commit/404ae95b.patch")
sha256sums=('d7bcf780b764f013613870ff2366486f052827857ca0bb79981c97b436601d31'
            'SKIP'
            '7ba2947aa7398ff09defd4f1bdc84675788a64d6a8e93b4d0458e7a7c57a3271')
validpgpkeys=(CA1B4E348D22755A67EF4E1A6A4F2C4CDC8BC77F) # Aleix Pol Gonzalez <aleixpol@kde.org>

prepare() {
  patch -d $pkgname-$pkgver -p1 -i ../kdebug-424780.patch # Fix plasma-wayland crash
}

build() {
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
