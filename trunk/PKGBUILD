# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=libmcrypt
pkgver=2.5.8
pkgrel=5
pkgdesc="A library which provides a uniform interface to several symmetric encryption algorithms"
url="http://mcrypt.sourceforge.net/"
arch=('x86_64')
license=('GPL')
depends=('glibc')
options=('!emptydirs')
source=("http://downloads.sourceforge.net/mcrypt/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('6c05c42767401d66af67b0922b207d17cdb1c3efdbfdfb5b0c2e651821c48a8a6c6749debfa0206091b8a801f538fabe9f7d95ebc86d82c6b84c8001031d50fe')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr --mandir=/usr/share/man
  make
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
