# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Maxime de Roucy <maxime.deroucy@gmail.com>
# Contributor: Darwin Bautista <djclue917@gmail.com>

pkgname=gsm
pkgver=1.0.20
pkgrel=1
pkgdesc="Shared libraries for GSM 06.10 lossy speech compression"
arch=('x86_64')
url="http://www.quut.com/gsm/"
license=('custom')
depends=('glibc')
source=("http://www.quut.com/$pkgname/$pkgname-$pkgver.tar.gz" gsm-shared.patch)
sha512sums=('8a57395cd22ff1e0340619aefc279693cbed7c4616b6e6ddc33fffca35361ddfe26b6c5a64ddcb5a9bb455797ca79aa1b7b8a31671dab1a003c544da78fc2ec5'
            '2d5a0233d5daa964c328cec2c240277443dc1c56ba5b347b5ed0a65d974c2d7da628d76a61c7b432dc5957dd07c68bee79cf90c67dedd8a04ef6f5c1ba795e7f')

prepare() {
  cd $pkgname-${pkgver%.*}-pl${pkgver##*.}

  patch -p0 -i ../gsm-shared.patch
}

build() {
  cd $pkgname-${pkgver%.*}-pl${pkgver##*.}

  make CCFLAGS="-c ${CFLAGS} -fPIC"
}

package() {
  cd $pkgname-${pkgver%.*}-pl${pkgver##*.}

  # Prepare directories
  install -m755 -d "$pkgdir"/usr/{bin,lib,include/gsm,share/{licenses/$pkgname,man/man{1,3}}}

  make -j1 INSTALL_ROOT="$pkgdir"/usr \
    GSM_INSTALL_INC="$pkgdir"/usr/include/gsm \
    GSM_INSTALL_MAN="$pkgdir"/usr/share/man/man3 \
    TOAST_INSTALL_MAN="$pkgdir"/usr/share/man/man1 \
    install

  # Install license
  install -m644 COPYRIGHT "$pkgdir"/usr/share/licenses/$pkgname/license.txt
}
