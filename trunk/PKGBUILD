# Contributor: Jan de Groot <jgc@archlinux.org>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=xorg-xfontsel
pkgver=1.0.6
pkgrel=1
pkgdesc='Point and click selection of X11 font names'
url='https://xorg.freedesktop.org/'
arch=('x86_64')
license=('custom')
makedepends=('xorg-util-macros')
depends=('libxaw' 'libxmu' 'libxt' 'libx11')
source=(https://xorg.freedesktop.org/archive/individual/app/xfontsel-${pkgver}.tar.bz2{,.sig})
sha512sums=('2f6bc51549a302404cdaf9a5a825add383953fcbb2a0c7076691cefd4dd7e4a3c772c047be74101c8486f75d03ce0bd3e65ede14e91732cf2a932810441c9099'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # "Alan Coopersmith <alan.coopersmith@oracle.com>"

build() {
	cd "${srcdir}/xfontsel-${pkgver}"
	./configure --prefix=/usr
	make
}

package() {
	cd "${srcdir}/xfontsel-${pkgver}"
	make DESTDIR="${pkgdir}" install
	install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
