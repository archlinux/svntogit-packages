# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=yelp
pkgver=42.2
pkgrel=1
pkgdesc="Get help with GNOME"
url="https://wiki.gnome.org/Apps/Yelp"
license=(GPL)
arch=(x86_64)
depends=(webkit2gtk-4.1 yelp-xsl libxslt xz bzip2 libhandy)
makedepends=(itstool autoconf-archive git appstream-glib)
provides=(libyelp.so)
groups=(gnome)
_commit=9fd1a84e1eae23db21939ff76c7e96d5422d3b1a  # tags/42.2^0
source=("git+https://gitlab.gnome.org/GNOME/yelp.git#commit=$_commit"
        no-gtk-doc.diff)
sha256sums=('SKIP'
            '795b8b6ee7cbbf6ccdbeba70ac8335becaaa6a44abe82f90c1c95d8113e4bf28')

pkgver() {
  cd yelp
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd yelp

  # Remove vestiges of docs build to allow building without gtk-doc
  git apply -3 ../no-gtk-doc.diff

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd yelp
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --enable-compile-warnings=minimum
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd yelp
  make DESTDIR="$pkgdir" install
}
