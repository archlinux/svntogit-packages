# $Id$
# Maintainer:  Ionut Biru <ibiru@archlinux.org>
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Paul Mattal <paul@archlinux.org>

pkgname=x264
pkgver=20140311
pkgrel=1
pkgdesc='Library for encoding H264/AVC video streams'
arch=('i686' 'x86_64')
url='http://www.videolan.org/developers/x264.html'
license=('GPL')
depends=('glibc')
makedepends=('yasm' 'git')
source=(git://git.videolan.org/x264.git#commit=aff928d2)
md5sums=('SKIP')

pkgver() {
  cd $pkgname
  git log -1 --format="%cd" --date=short | tr -d -
}

build() {
  cd $pkgname

  ./configure --prefix=/usr \
    --enable-shared \
    --enable-pic

  make
}

package() {
  make -C $pkgname DESTDIR="$pkgdir" install
}
