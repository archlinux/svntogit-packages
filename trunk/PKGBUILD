# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Troy C < rstrox -ta yahoo -tod com >

pkgbase=python-wrapt
pkgname=(python-wrapt python2-wrapt)
pkgver=1.10.10
pkgrel=1
pkgdesc="A Python module for decorators, wrappers and monkey patching"
arch=("i686" "x86_64")
url="https://pypi.python.org/pypi/wrapt"
license=("BSD")
makedepends=('python' 'python2')
checkdepends=('python-pytest' 'python2-pytest')
source=("$pkgname-$pkgver.tar.gz::https://github.com/GrahamDumpleton/wrapt/archive/$pkgver.tar.gz")
sha256sums=('ded853e421bbc7bb668b085391f2e2be9bd83f8e3565f8efddc937284446e3aa')

prepare() {
  cp -a wrapt-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/wrapt-$pkgver
  python setup.py build

  cd "$srcdir"/wrapt-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/wrapt-$pkgver
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-3.6:$PYTHONPATH" py.test

  cd "$srcdir"/wrapt-$pkgver-py2
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-2.7:$PYTHONPATH" py.test2
}

package_python-wrapt() {
  depends=('python')

  cd wrapt-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-wrapt() {
  depends=('python2')

  cd wrapt-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
