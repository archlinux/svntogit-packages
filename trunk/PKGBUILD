# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Kritoke <kritoke@nospam.gmail.com>

pkgname=gnome-nettool
pkgver=42.0
pkgrel=1
pkgdesc="Graphical interface for various networking tools"
url="https://gitlab.gnome.org/GNOME/gnome-nettool"
arch=(x86_64)
license=(GPL)
depends=(gtk3 libgtop iputils whois)
makedepends=(nmap bind net-tools git meson yelp-tools)
optdepends=('nmap: Port scanning'
            'bind: DNS lookup'
            'net-tools: Network statistics')
groups=(gnome-extra)
_commit=5748c0fa632c9caad9e46377ca1aa330dec4b757  # tags/gnome-nettool-42-0^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-nettool.git#commit=$_commit"
        fix-build.diff)
sha256sums=('SKIP'
            '95437116039a453c7c15eb25b90e3818338d6b6a87437f384b3d6aded4a845d7')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^gnome-nettool-//;s/-/./;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname

  # Fix build with recent Meson
  git apply -3 ../fix-build.diff
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
