# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>

pkgname=xfce4-notifyd
pkgver=0.3.0
pkgrel=1
pkgdesc="Notification daemon for the Xfce desktop"
arch=('i686' 'x86_64')
url="http://goodies.xfce.org/projects/applications/xfce4-notifyd"
license=('GPL2')
groups=('xfce4-goodies')
depends=('libxfce4ui' 'libnotify' 'hicolor-icon-theme')
makedepends=('intltool' 'python')
provides=('notification-daemon')
source=(http://archive.xfce.org/src/apps/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=('7076081424932b40d03366a7518daf495f81eb780f376819e59e76d65d811100')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
