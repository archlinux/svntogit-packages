# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgbase=phonon
pkgname=(phonon-qt5)
pkgdesc="The multimedia framework by KDE"
pkgver=4.11.0
pkgrel=1
arch=(x86_64)
url='http://phonon.kde.org/'
license=(LGPL)
depends=(libpulse qt5-base)
optdepends=('pulseaudio: PulseAudio support' 'qt5-tools: Designer plugin')
makedepends=(extra-cmake-modules qt5-tools)
source=("http://download.kde.org/stable/$pkgbase/$pkgver/$pkgbase-$pkgver.tar.xz"{,.sig})
sha256sums=('91250a38feb0adb62385ad9bacc0ea0e67bbba3e0ceb3c2111d83803fb52582e'
            'SKIP')
validpgpkeys=(CB9387521E1EE0127DA804843FDBB55084CC5D84) # Harald Sitter <sitter@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgbase-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package_phonon-qt5(){
  depends+=(phonon-qt5-backend)
  cd build
  make DESTDIR="$pkgdir" install
}
