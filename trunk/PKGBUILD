# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=grilo
pkgver=0.2.0
pkgrel=1
pkgdesc="Framework that provides access to various sources of multimedia content"
url="http://www.gnome.org"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('gtk3' 'libxml2' 'libsoup')
makedepends=('gobject-introspection' 'gtk-doc' 'vala')
optdepends=('grilo-plugins: Plugins for grilo')
options=('!libtool' '!emptydirs')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('daab3104684dd1a347aa3f3bc8e5a435f57829f594d951890692b9035c993d1f')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static --disable-debug
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
