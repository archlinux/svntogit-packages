# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=libxp
pkgver=1.0.4
pkgrel=1
pkgdesc="X11 X Print Library"
arch=('x86_64')
license=('custom')
url="https://xorg.freedesktop.org/"
depends=('libxext')
makedepends=('xorg-util-macros' 'xorgproto')
source=(${url}/releases/individual/lib/libXp-${pkgver}.tar.xz{,.sig}
        add-proto-files.patch)
license=('custom')
sha512sums=('50e82e7ee7222db13a04f6223ae8653eb783593c3b28825d7fc233af188960bec53d2b11ced281e01140d283d840d0e3822d71f95f5045d594fb704485fd0f89'
            'SKIP'
            'bd9c667cb51bb50b0ca4a50c85f32fcf2d26b642a15cfa1980cca49decf236ebc37fcca45fa4c979d02bb5ece4c43eee78cd952cc90c5419331f69b94e590737')
#validpgpkeys=('3BB639E56F861FA2E86505690FDD682D974CA72A') #  "Matt Turner <mattst88@gmail.com>"
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # Alan Coopersmith <alan.coopersmith@oracle.com>

prepare() {
  cd libXp-${pkgver}
  patch -Np1 -i ../add-proto-files.patch
  autoreconf -vfi
}

build() {
  cd libXp-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd libXp-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
