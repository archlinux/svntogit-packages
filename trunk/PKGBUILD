# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-video-effects
pkgver=0.5.0+r18+g5f4c801
pkgrel=1
epoch=1
pkgdesc="Collection of GStreamer effects for GNOME"
url="https://gitlab.gnome.org/GNOME/gnome-video-effects"
arch=(any)
license=(GPL2)
depends=(
  frei0r-plugins
  gst-plugins-bad
  gst-plugins-good
)
makedepends=(
  git
  meson
)
_commit=5f4c801ceff9e10f7ae6c1b0b3ff907f56f945e3  # master
source=("git+$url.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
