# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=gitg
pkgver=41
pkgrel=1
epoch=2
pkgdesc="GNOME GUI client to view git repositories"
url="https://wiki.gnome.org/Apps/Gitg"
arch=(x86_64)
license=(GPL)
depends=(glib2 gtk3 gtksourceview4 gobject-introspection-runtime libgit2-glib
         libgee json-glib gsettings-desktop-schemas libpeas gspell libdazzle
         libsecret libxml2)
makedepends=(git meson gobject-introspection vala python-gobject)
_commit=1adffb6b3ce97626f2ed23885d6d0e44a2764cbe  # tags/v41^0
source=("git+https://gitlab.gnome.org/GNOME/gitg.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd gitg
  git describe --tags | sed 's/^v//;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd gitg
}

build() {
  arch-meson gitg build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  python -m compileall -d /usr/lib "$pkgdir/usr/lib"
  python -O -m compileall -d /usr/lib "$pkgdir/usr/lib"
}
