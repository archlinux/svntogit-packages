# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Zuf <kontakt.zuf@gmail.com>
# Contributor: Darwin Bautista <djclue917@gmail.com>
# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=kdenlive
pkgver=17.04.1
pkgrel=1
pkgdesc="A non-linear video editor for Linux using the MLT video framework"
arch=(i686 x86_64)
url="https://www.kdenlive.org/"
license=(GPL)
groups=(kde-applications kdemultimedia)
depends=(knewstuff knotifyconfig kded kfilemetadata qt5-webkit mlt glu sdl_image hicolor-icon-theme qt5-quickcontrols)
makedepends=(extra-cmake-modules kdoctools v4l-utils python)
optdepends=('ffmpeg: for FFmpeg plugin'
            'cdrtools: for creation of DVD ISO images'
            'dvdauthor: for creation of DVD'
            'dvgrab: for firewire capture'
            'libdv: for webcam capture (if FFmpeg is not installed)'
            'recordmydesktop: for screen capture'
            'xine-ui: for DVD preview'
            'plasma-desktop: theme configuration')
source=("https://download.kde.org/stable/applications/${pkgver}/src/$pkgname-${pkgver}.tar.xz"{,.sig})
sha256sums=('1b1c79d5d25f6bab52f8999572f5df5b5cbfd3297f6ca18e84af96c7bf4aed08'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  make -C build DESTDIR="${pkgdir}" install
}

