# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=libkgapi
pkgver=4.80.0
pkgrel=1
pkgdesc="A KDE-based library for accessing various Google services via their public API"
url='https://projects.kde.org/projects/extragear/libs/libkgapi'
arch=(i686 x86_64)
license=(GPL LGPL FDL)
depends=(kcalcore kcontacts)
makedepends=(extra-cmake-modules kdoctools)
source=("http://download.kde.org/stable/$pkgname/$pkgver/src/$pkgname-$pkgver.tar.xz")
md5sums=('4602f8ded81bbd62f033e589fbd85203')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
