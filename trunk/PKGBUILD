# $Id$

pkgname=perl-io-html
pkgver=1.00
pkgrel=2
pkgdesc="Open an HTML file with automatic charset detection"
arch=('any')
url='http://search.cpan.org/dist/IO-HTML'
depends=('perl')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/C/CJ/CJM/IO-HTML-$pkgver.tar.gz)
sha1sums=('408784f567d52f26ee4ed7c10026e2c8b8e86178')

build() {
  export PERL_MM_USE_DEFAULT=1 PERL_MM_OPT="INSTALLDIRS=vendor" \
    PERL_MB_OPT="--installdirs vendor"
  cd IO-HTML-$pkgver
  perl Makefile.PL
  make 
}

check() {
  cd IO-HTML-$pkgver
  make test
}

package() {
  cd IO-HTML-$pkgver
  make DESTDIR="$pkgdir" install
}
