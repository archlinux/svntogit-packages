# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=adobe-source-code-pro-fonts
pkgver=2.010ro+1.030it
_relver=2.010R-ro/1.030R-it
_tarver=${_relver//\//-}
pkgrel=1
pkgdesc=" Monospaced font family for user interface and coding environments"
arch=(any)
url="http://adobe-fonts.github.io/source-code-pro/"
license=(custom)
depends=(fontconfig)
install=fonts.install
source=("${_tarver}.tar.gz::https://github.com/adobe-fonts/source-code-pro/archive/${_relver}.tar.gz")
sha256sums=('fcb82b3becff6dbc6440d6e03c1edca410a912d616088205bf1dff96fb93918b')

package() {
  cd source-code-pro-$_tarver
  install -d "$pkgdir/usr/share/fonts/adobe-source-code-pro"
  install -t "$pkgdir/usr/share/fonts/adobe-source-code-pro" -m644 OTF/*.otf
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
