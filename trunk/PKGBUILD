# $Id$
# Maintainer: Jürgen Hötzel <juergen@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>
# Contributor: Kevin Piche <kevin@archlinux.org>

pkgname=bigloo
_pkgver=3.5a
pkgver=${_pkgver/-/_}
pkgrel=1
pkgdesc="Fast scheme compiler"
arch=('i686' 'x86_64')
url="http://www-sop.inria.fr/mimosa/fp/Bigloo/"
license=('GPL' 'LGPL')
depends=('openssl' 'sqlite3')
makedepends=('jdk' 'emacs')
optdepends=('emacs')
options=('!makeflags')
install=bigloo.install
source=(ftp://ftp-sop.inria.fr/indes/fp/Bigloo/${pkgname}${_pkgver}.tar.gz)
md5sums=('73cacd923c391d4c53537901844f84b4')

build() {
  cd "${srcdir}/${pkgname}${_pkgver}"
  ./configure --prefix=/usr \
    --enable-ssl \
    --enable-sqlite \
    --disable-gstreamer \
    --mandir=/usr/share/man \
    --infodir=/usr/share/info \
    --docdir=/usr/share/doc/bigloo \
    --dotnet=no \
    --javaprefix=/opt/java/bin
  make build compile-bee
}

package() {
  cd "${srcdir}/${pkgname}${_pkgver}"
  make DESTDIR="${pkgdir}" install install-bee
  make -C manuals DESTDIR="${pkgdir}" install-bee
  chmod 644 "${pkgdir}"/usr/lib/bigloo/${_pkgver%-*}/*.a
}
