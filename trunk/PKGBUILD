# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-xinput
pkgver=1.6.2
pkgrel=2
pkgdesc="Small commandline tool to configure devices"
arch=(x86_64)
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libx11' 'libxi>=1.5.99.2' 'xorg-xrandr' 'libxinerama')
makedepends=('xorg-util-macros' 'inputproto' 'libxfixes')
groups=('xorg-apps' 'xorg')
source=(https://xorg.freedesktop.org/archive/individual/app/xinput-${pkgver}.tar.bz2{,.sig})
sha512sums=('dbef3a87dd47bb8c593f1305bea7978697a12434a4992584e4124d745bb366812366b44fc205159137a3db6f7b8338effd1b90641a194497b19159b9fd91939e'
            'SKIP')
validpgpkeys=('3C2C43D9447D5938EF4551EBE23B7E70B467F0BF') # "Peter Hutterer (Who-T) <office@who-t.net>"

build() {
  cd xinput-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd xinput-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
