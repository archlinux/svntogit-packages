# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=appstream-glib
pkgver=0.7.4
pkgrel=1
pkgdesc="Objects and methods for reading and writing AppStream metadata"
url="https://people.freedesktop.org/~hughsient/appstream-glib/"
arch=(x86_64)
license=(LGPL)
depends=(gtk3 libyaml pacman gcab libsoup libstemmer)
makedepends=(gobject-introspection gtk-doc autoconf-archive git gperf meson)
provides=(appdata-tools)
conflicts=(appdata-tools)
replaces=(appdata-tools)
_commit=c113a6b6591bbdf84dc8f5000f5f98db1174443d  # tags/appstream_glib_0_7_4^0
source=("git+https://github.com/hughsie/appstream-glib#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^appstream_glib_//;s/_/./g;s/-/+/g'
}

prepare() {
  mkdir build
  cd $pkgname
}

build() {
  cd build
  arch-meson ../$pkgname \
    -Denable-alpm=true \
    -Denable-gtk-doc=true \
    -Denable-rpm=false
  ninja
}

check() {
  cd build
  meson test
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}
