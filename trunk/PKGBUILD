# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-characters
pkgver=43.1
pkgrel=1
pkgdesc="A character map application"
arch=(x86_64)
url="https://gitlab.gnome.org/GNOME/gnome-characters"
license=(GPL2)
depends=(gjs gtk4 libunistring gnome-desktop-4 emoji-font libadwaita)
makedepends=(meson gobject-introspection appstream-glib git gperf)
groups=(gnome)
_commit=7c7b8b03596c3d64276da6522649ac5a81fe4724  # tags/43.1^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-characters.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
