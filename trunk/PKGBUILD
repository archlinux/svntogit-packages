# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=gwenview
pkgver=15.08.1
pkgrel=1
pkgdesc="A fast and easy to use image viewer for KDE"
url='http://kde.org/applications/graphics/gwenview/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegraphics')
depends=('kded' 'kactivities-frameworks' 'baloo' 'kdelibs4support' 'hicolor-icon-theme')
optdepends=('qt5-imageformats: support for tiff, webp, and more image formats'
            'kimageformats: support for dds, xcf, exr, psd, and more image formats')
makedepends=('extra-cmake-modules' 'kdoctools' 'python')
replaces=('kdegraphics-gwenview')
conflicts=('kdegraphics-gwenview')
install=$pkgname.install
source=("http://download.kde.org/stable/applications/${pkgver}/src/gwenview-${pkgver}.tar.xz")
sha1sums=('31efb06b4d328561d58929e64df8cf008f1194ae')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DGWENVIEW_SEMANTICINFO_BACKEND="Baloo" \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install

# Fix missing menu items https://bugs.kde.org/show_bug.cgi?id=351431
  mv "$pkgdir"/usr/share/kxmlgui5/gwenview/{gwenviewui.rc,org.kde.gwenviewui.rc}
  mv "$pkgdir"/usr/share/kxmlgui5/{gwenview,org.kde.gwenview}
}
