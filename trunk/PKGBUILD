# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Firmicus <francois.archlinux.org>
# Contributor: Tom Killian <tomk@runbox.com>
# Contributor: FJ <joostef@gmail.com>

pkgname=perl-template-toolkit
pkgver=3.100
pkgrel=1
pkgdesc="Perl template processing system"
arch=('x86_64')
url="https://search.cpan.org/dist/Template-Toolkit/"
license=('PerlArtistic')
depends=('perl-appconfig' 'perl')
checkdepends=('perl-cgi' 'perl-image-info' 'perl-test-leaktrace')
options=('!emptydirs')
source=(https://search.cpan.org/CPAN/authors/id/T/TO/TODDR/Template-Toolkit-$pkgver.tar.gz)
sha512sums=('7a23254450662a71761f7f8f6d774f71695ce975ef5638d04e45858a39003f82453cefdfed4528840094126a6148fe68d98a39fd5dde4908a08b6128576b3767')

build() {
  cd Template-Toolkit-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor TT_ACCEPT=y
  make
}

check() {
  cd Template-Toolkit-$pkgver
  make test
}

package() {
  cd Template-Toolkit-$pkgver
  make install DESTDIR="${pkgdir}"
}
