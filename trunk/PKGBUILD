# Maintainer:
# Contributor: Daniel Bermond < yahoo-com: danielbermond >

pkgname=libraqm
pkgver=0.7.0
pkgrel=1
pkgdesc='A library that encapsulates the logic for complex text layout'
arch=(x86_64)
url='https://github.com/HOST-Oman/libraqm/'
license=(MIT)
depends=(freetype2 fribidi)
makedepends=(gtk-doc)
source=(https://github.com/HOST-Oman/libraqm/releases/download/v$pkgver/raqm-$pkgver.tar.gz)
sha256sums=('e28575ecdd4e8a1d277d9be8268bb663ce1e476aaf55eb0456787821ddf0f941')

build() {
    cd raqm-$pkgver
    
    ./configure \
        --prefix=/usr \
        --enable-static=no \
        --enable-shared=yes \
        --enable-fast-install=yes \
        --enable-gtk-doc=yes \
        --enable-gtk-doc-html=yes \
        --enable-gtk-doc-pdf=no
    make
}

package() {
    cd raqm-$pkgver
    
    make DESTDIR="$pkgdir" install
    
    install -D -m644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
