# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: dracorp aka Piotr Rogoza <piotr.r.public at gmail.com>

pkgname=kimageannotator
pkgver=0.6.0
pkgrel=1
pkgdesc='Tool for annotating images'
arch=(x86_64)
url='https://github.com/ksnip/kImageAnnotator'
license=(GPL)
depends=(qt5-svg kcolorpicker)
makedepends=(cmake qt5-tools)
source=(https://github.com/ksnip/kImageAnnotator/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('148ef703ed5535a0c23d11629b588d9cb163598798261aa760d5b351756646a7')

build() {
  cmake -B build -S kImageAnnotator-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_SHARED_LIBS=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}

