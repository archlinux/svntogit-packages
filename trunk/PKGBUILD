# $Id$
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=pavucontrol
pkgver=1.0
pkgrel=1
pkgdesc="A GTK volume control tool for PulseAudio"
arch=(i686 x86_64)
url="http://freedesktop.org/software/pulseaudio/pavucontrol/"
license=(GPL)
depends=(gnome-icon-theme libcanberra-pulse gtkmm3 libsigc++)
makedepends=(intltool lynx)
source=($url/$pkgname-$pkgver.tar.xz pavucontrol.desktop)
sha256sums=('3dbe2865b6e3a195a8951967d9234f62cd254c30b08bdea1ecf8997b58f68cde'
            '5eab8c78e92c39a71ac1ca4c4464a8915d3a29d91e1703fd51c24652b747eebe')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --enable-gtk3
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir/pavucontrol.desktop" \
    "$pkgdir/usr/share/applications/pavucontrol.desktop"
}
