# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=eog
pkgver=2.30.0
pkgrel=1
pkgdesc="Eye of Gnome: An image viewing and cataloging program"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gnome-desktop>=2.29.91' 'libexif>=0.6.19' 'lcms>=1.18-3' 'desktop-file-utils' 'gnome-icon-theme>=2.28.0' 'exempi>=2.1.1')
makedepends=('pkgconfig' 'gnome-doc-utils>=0.20.0' 'intltool')
install=eog.install
groups=('gnome-extra')
options=('!emptydirs' '!libtool')
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.30/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('9fbda0995416eeec70b41b0b2db4b38fd5256ac39415cdac852b32def52a06b2')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-scrollkeeper || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1
  
  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain eog ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
