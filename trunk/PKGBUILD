# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kasts
pkgver=23.04.0
pkgrel=1
pkgdesc='Kirigami-based podcast player'
url='https://apps.kde.org/kasts/'
arch=(x86_64)
license=(GPL LGPL)
groups=(kde-applications kde-multimedia)
depends=(kirigami-addons qt5-svg qtkeychain-qt5 syndication threadweaver ki18n kcoreaddons kconfig networkmanager-qt taglib qt5-multimedia vlc)
makedepends=(extra-cmake-modules)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('985f3617caf75866adfb92e10d67fc22f69ea70ac577e6671a01708f62e9134f'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
