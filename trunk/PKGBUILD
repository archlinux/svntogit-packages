# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gtk-doc
pkgver=1.18
pkgrel=1
pkgdesc="Documentation tool for public library API"
arch=('any')
license=('GPL' 'FDL')
depends=('docbook-xsl' 'gnome-doc-utils' 'perl')
makedepends=('pkgconfig' 'jade')
optdepends=('jade: SGML support')
url="http://www.gtk.org/gtk-doc/"
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('785b591c561be1c8206f0d2575dc63558ea6199c3afe49cbc4efde94a6629a20')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  export PYTHON=/usr/bin/python2
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
