# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-xwininfo
pkgver=1.1.6
pkgrel=1
pkgdesc="Command-line utility to print information about windows on an X server"
arch=(x86_64)
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libxcb')
makedepends=('xorg-util-macros' 'libx11')
groups=('xorg-apps' 'xorg')
source=(https://xorg.freedesktop.org/archive/individual/app/xwininfo-${pkgver}.tar.xz{,.sig})
sha512sums=('c6df4ed3dcf97772450612f2786ecab8313a43bb78e0b8020cc9ec228b3f466f55090313ad02c8159a73a6892d117322ad85e28d9d76e38bb84c9750712c20bc'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # "Alan Coopersmith <alan.coopersmith@oracle.com>"

build() {
  cd xwininfo-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd xwininfo-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
