# $Id$
# Maintainer: tobias <tobias@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>

pkgname=perl-event-execflow
_realname=Event-ExecFlow
pkgver=0.64
pkgrel=2
pkgdesc="Framework for perl-events"
arch=(any)
license=('PerlArtistic')
url="http://www.exit1.org/${_realname}/"
depends=('perl-anyevent' 'perl-libintl-perl' 'perl>=5.10.0')
options=(!emptydirs)
source=(http://www.exit1.org/packages/${_realname}/dist/${_realname}-${pkgver}.tar.gz)
md5sums=('7370ea61607a200239cdd491755efee3')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  # install module in vendor directories.
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
  make test
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make install DESTDIR=${pkgdir}/

  # remove perllocal.pod and .packlist
  find ${pkgdir} -name perllocal.pod -delete
  find ${pkgdir} -name .packlist -delete
}
