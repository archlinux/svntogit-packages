# Maintainer: Guillaume ALAUX <guillaume@archlinux.org>
_libname=hamcrest
pkgname=java-${_libname}
pkgver=1.3
pkgrel=4
pkgdesc='Java matcher objects library'
arch=('any')
url='https://code.google.com/p/hamcrest'
license=('BSD')
depends=('java-runtime-headless')
_url='https://storage.googleapis.com/google-code-archive-downloads/v2/code.google.com/hamcrest'
source=(${_url}/${_libname}-core-${pkgver}.jar
        ${_url}/${_libname}-library-${pkgver}.jar
        ${_url}/${_libname}-generator-${pkgver}.jar
        ${_url}/${_libname}-integration-${pkgver}.jar
        LICENSE.txt)
noextract=(${_libname}-core-${pkgver}.jar
           ${_libname}-library-${pkgver}.jar
           ${_libname}-generator-${pkgver}.jar
           ${_libname}-integration-${pkgver}.jar)
sha256sums=('66fdef91e9739348df7a096aa384a5685f4e875584cce89386a7a47251c4d8e9'
            '711d64522f9ec410983bd310934296da134be4254a125080a0416ec178dfad1c'
            '84c810cafabd046d0e5599f9b5fc1514b2a6ce45313997e020a47076ba27d035'
            '70f418efbb506c5155da5f9a5a33262ea08a9e4d7fea186aa9015c41a7224ac2'
            '1cde867cab5c8e842929da5391cef98b4017314822270e934e8e2eef3767c666')

package() {
  cd "${srcdir}"

  install -dm755 ${pkgdir}/usr/share/java/${_libname}
  for j in core library generator integration; do
    cp ${_libname}-${j}-${pkgver}.jar ${pkgdir}/usr/share/java/${_libname}/${j}-${pkgver}.jar
    ln -s ${j}-${pkgver}.jar ${pkgdir}/usr/share/java/${_libname}/${j}.jar
    ln -s ${_libname}/${j}-${pkgver}.jar ${pkgdir}/usr/share/java/${_libname}-${j}.jar
  done

  install -Dm644 LICENSE.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
