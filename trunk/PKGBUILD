# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=five-or-more
pkgver=3.10.2
pkgrel=1
pkgdesc="Remove colored balls from the board by forming lines"
arch=('i686' 'x86_64')
license=('GPL')
depends=('desktop-file-utils' 'hicolor-icon-theme' 'gtk3' 'librsvg')
makedepends=('intltool' 'itstool' 'gobject-introspection')
conflicts=('gnome-games')
replaces=('gnome-games')
options=('!emptydirs')
install=five-or-more.install
url="https://live.gnome.org/Five%20or%20more"
groups=('gnome-extra')
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('e2e6834df8be22db1feb1ef41f4d3c4b2f41ab6b3bcbc7f4ba61b8b70bfd89a9')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --disable-schemas-compile \
      --with-scores-user=root --with-scores-group=games
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  # Remove all scores, we generate them from postinstall
  rm -rf "$pkgdir/var"
}
