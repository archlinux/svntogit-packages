# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xdg-utils
pkgver=1.1.0rc1
pkgrel=2
pkgdesc='Command line tools that assist applications with a variety of desktop integration tasks.'
arch=('any')
url='http://portland.freedesktop.org/'
depends=('sh' 'xorg-xset')
makedepends=('docbook-xsl' 'lynx' 'xmlto')
optdepends=('xorg-utils: needed if using Xfce')
license=('custom')
source=( #"ftp://ftp.archlinux.org/other/${pkgname}/${pkgname}-${pkgver}.tar.xz"
	http://portland.freedesktop.org/download/${pkgname}-${pkgver/rc1/-rc1}.tar.gz)
md5sums=('fadf5e7a08e0526fc60dbe3e5b7ef8d6')

build() {
	#cd ${srcdir}/${pkgname}-${pkgver}
	cd ${srcdir}/${pkgname}-${pkgver/rc1/-rc1}
	./configure --prefix=/usr --mandir=/usr/share/man
	make -C scripts
	make
}

package() {
	#cd ${srcdir}/${pkgname}-${pkgver}
	cd ${srcdir}/${pkgname}-${pkgver/rc1/-rc1}
	make DESTDIR=${pkgdir} install
	install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

_source() {

	# outdated! see now http://cgit.freedesktop.org/xdg/xdg-utils/
	local _current=$(pwd)
	local _tmp=$(mktemp -d)
	cd $_tmp

	cvs -d:pserver:anoncvs@anoncvs.freedesktop.org:/cvs/portland co -d ${pkgname}-${pkgver} portland/xdg-utils
	find ${pkgname}-${pkgver} -name 'CVS' -type d -exec rm -rf {} \;
	bsdtar cJf ${pkgname}-${pkgver}.tar.xz ${pkgname}-${pkgver}
	mv ${pkgname}-${pkgver}.tar.xz $_current
	cd $_current
	rm -rf $_tmp
}
