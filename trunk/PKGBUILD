# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=freetype2
pkgver=2.4.9
pkgrel=2
pkgdesc="TrueType font rendering library"
arch=(i686 x86_64)
license=('GPL')
url="http://freetype.sourceforge.net"
depends=('zlib' 'bzip2' 'sh')
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/freetype/freetype-${pkgver}.tar.bz2
        freetype-2.3.0-enable-spr.patch
        freetype-2.2.1-enable-valid.patch
        bug35833.patch
        bug35847.patch)
md5sums=('77a893dae81fd5b896632715ca041179'
         '816dc8619a6904a7385769433c0a8653'
         '214119610444c9b02766ccee5e220680'
         '69bde731a06448c7e4828e96c74d45ee'
         '41f9c0a5362bf2cd9a4d7557ddf98098')

build() {
  cd "${srcdir}/freetype-${pkgver}"
  patch -Np1 -i "${srcdir}/freetype-2.3.0-enable-spr.patch"
  patch -Np1 -i "${srcdir}/freetype-2.2.1-enable-valid.patch"

  #Upstream bugs fixed in git. Requested in FS#29034
  patch -Np1 -i "${srcdir}/bug35833.patch"
  patch -Np1 -i "${srcdir}/bug35847.patch"

  ./configure --prefix=/usr
  make
}

check() {
  cd "${srcdir}/freetype-${pkgver}"
  make -k check
}

package() {
  cd "${srcdir}/freetype-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
