# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=libmbim
pkgver=1.26.2
pkgrel=1
pkgdesc="MBIM modem protocol helper library"
arch=(x86_64)
url="https://www.freedesktop.org/wiki/Software/libmbim/"
license=(GPL2)
depends=(glib2 bash systemd)
makedepends=(gtk-doc meson git help2man gobject-introspection bash-completion)
provides=(libmbim-glib.so)
_commit=55d51a8a76921b478a75b79a8695b37d989e4868  # tags/1.26.2
source=("git+https://gitlab.freedesktop.org/mobile-broadband/libmbim.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-rc/rc/;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build -D gtk_doc=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
