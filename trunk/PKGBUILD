# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-error
pkgver=0.17026
pkgrel=2
pkgdesc="Perl/CPAN Error module - Error/exception handling in an OO-ish way"
url="http://search.cpan.org/dist/Error/"
arch=('any')
license=('PerlArtistic' 'GPL')
depends=('perl')
checkdepends=('perl-test-pod' 'perl-test-pod-coverage')
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/S/SH/SHLOMIF/Error-${pkgver}.tar.gz)
sha512sums=('58d43a133fb74d94d726ae923cdfb7df8e1faf156247bb07cb7308218e21a627485f031d029b3ee3179d92e756dca7f1e6dd1e2f4378f420c72b171aefe2f410')

build() {
  cd Error-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make 
}

check() {
  cd Error-${pkgver}
  make test
}

package() {
  cd Error-${pkgver}
  make DESTDIR="${pkgdir}" install
}

