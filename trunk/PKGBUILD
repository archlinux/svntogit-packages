# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=libnotify
pkgver=0.7.12
pkgrel=1
pkgdesc="Library for sending desktop notifications"
url="https://gitlab.gnome.org/GNOME/libnotify"
arch=(x86_64)
license=(LGPL)
depends=(gdk-pixbuf2)
makedepends=(gtk3 gobject-introspection git meson docbook-xsl xmlto gtk-doc)
options=(debug)
_commit=8162b1d397deb77e7b615dab36437cd43e96fbc6  # tags/0.7.12^0
source=("git+$url.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  depends+=(libg{lib,object,io}-2.0.so libgdk_pixbuf-2.0.so)
  provides+=(libnotify.so)

  meson install -C build --destdir "$pkgdir"
}
