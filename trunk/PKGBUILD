# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=enchant
pkgver=1.6.0
pkgrel=1
pkgdesc="A wrapper library for generic spell checking"
arch=('i686' 'x86_64')
url="http://www.abisource.com/enchant/"
license=('LGPL')
depends=('aspell' 'dbus-glib' 'hunspell')
makedepends=('hspell')
options=('!libtool')
source=("http://www.abisource.com/downloads/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
md5sums=('de11011aff801dc61042828041fb59c7')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
    --disable-static \
    --disable-ispell \
    --with-myspell-dir=/usr/share/myspell || return 1
  make || return 1
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install || return 1
}
