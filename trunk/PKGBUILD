# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: 03/08/04 <lefungus@altern.org>

pkgname=libebml
pkgver=1.3.6
pkgrel=1
pkgdesc='Extensible Binary Meta Language library'
arch=('x86_64')
url='https://github.com/Matroska-Org/libebml'
license=('LGPL')
depends=('gcc-libs')
makedepends=('cmake')
provides=('libebml.so')
source=("libebml-${pkgver}.tar.gz::https://github.com/Matroska-Org/libebml/archive/release-${pkgver}.tar.gz")
sha256sums=('57ee73eacca8dc13d3c2c064fb757950450da9185e02e863cac34674681de1a8')

prepare() {
  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd build

  cmake ../libebml-release-${pkgver} \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_INSTALL_LIBDIR='/usr/lib' \
    -DBUILD_SHARED_LIBS='ON'
  make
}

package() {
  make DESTDIR="${pkgdir}" -C build install
}

# vim: ts=2 sw=2 et:
