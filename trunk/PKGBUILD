# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>
pkgname=v4l-utils
pkgver=1.14.2
pkgrel=1
pkgdesc="Userspace tools and conversion library for Video 4 Linux"
arch=('x86_64')
url="https://linuxtv.org/"
provides=("libv4l=$pkgver")
replaces=('libv4l')
conflicts=('libv4l')
backup=(etc/rc_maps.cfg)
license=('LGPL')
makedepends=('qt5-base' 'alsa-lib')
optdepends=('qt5-base' 'alsa-lib')
depends=('hicolor-icon-theme' 'gcc-libs' 'sysfsutils' 'libjpeg-turbo')
source=(https://linuxtv.org/downloads/v4l-utils/${pkgname}-${pkgver}.tar.bz2{,.asc})
sha256sums=('e6b962c4b1253cf852c31da13fd6b5bb7cbe5aa9e182881aec55123bae680692'
            'SKIP')
validpgpkeys=('05D0169C26E41593418129DF199A64FADFB500FF') # Gregor Jasny <gjasny@googlemail.com>

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr --sysconfdir=/etc --sbindir=/usr/bin
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  MAKEFLAGS="-j1" make install DESTDIR="${pkgdir}/"
  rm "${pkgdir}/usr/bin/ivtv-ctl"
}
