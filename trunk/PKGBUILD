# $Id$
# Maintainer: Alexander Baldeck <alexander@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
pkgname=xorg-xinit
pkgver=1.2.1
pkgrel=1
pkgdesc="X.Org initialisation program "
arch=(i686 x86_64)
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('libx11' 'xorg-xauth')
makedepends=('pkgconfig')
optdepends=('xorg-twm' 'xterm')
backup=('etc/skel/.xinitrc'
        'etc/skel/.xsession'
        'etc/X11/xinit/xserverrc'
        'etc/X11/xinit/xinitrc')
groups=('xorg')
source=(${url}/releases/individual/app/xinit-${pkgver}.tar.bz2
        06_move_serverauthfile_into_tmp.diff
        xinitrc
        xsession
        xserverrc)
options=(!emptydirs)
md5sums=('56f2d202b3dc10fcd21931a67bb270f7'
         'abd072ec435ce084cf4e89c58f83d45c'
         'cee2d70748a089fbc7db64a4a6e8fdb1'
         '7873d97db30d6c279ed37e3559e7c59d'
         'bb550d3b9a2d2b6cbe6e2667a3a7cd03')

build() {
  cd "${srcdir}/xinit-${pkgver}"
  patch -Np1 -i "${srcdir}/06_move_serverauthfile_into_tmp.diff" || return 1
  ./configure --prefix=/usr || return 1
  make XINITDIR=/etc/X11/xinit || return 1
  make XINITDIR=/etc/X11/xinit DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/etc/skel"
  install -m755 -d "${pkgdir}/etc/X11/xinit"
  install -m644 "${srcdir}/xinitrc" "${pkgdir}/etc/skel/.xinitrc" || return 1
  install -m644 "${srcdir}/xsession" "${pkgdir}/etc/skel/.xsession" || return 1
  install -m644 "${srcdir}/xserverrc" "${pkgdir}/etc/X11/xinit/xserverrc" || return 1
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/" || return 1
}
