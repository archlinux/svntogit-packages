# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=telepathy-kde-call-ui
_pkgname=ktp-call-ui
pkgver=22.04.3
pkgrel=1
pkgdesc='Voice/Video Call UI for Telepathy'
arch=(x86_64)
url='https://community.kde.org/KTp'
license=(GPL)
groups=(kde-applications kde-network telepathy-kde)
depends=(telepathy-kde-common-internals qt-gstreamer phonon-qt5)
makedepends=(extra-cmake-modules boost)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$_pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('f3d512babd050cfaee912d3dd51d6d0e3ca26618ed7b49346e4997a86582efcd'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
options=(debug)

build() {
  cmake -B build -S $_pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
