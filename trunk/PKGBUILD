# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdnssd
pkgver=4.100.0
pkgrel=1
pkgdesc='KDNSSD Framework'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kdnssd-framework'
license=('LGPL')
depends=('qt5-base' 'avahi')
makedepends=('extra-cmake-modules' 'qt5-tools')
groups=('kf5')
replaces=('kdnssd-framework')
conflicts=('kdnssd-framework')
source=("http://download.kde.org/unstable/frameworks/${pkgver}/${pkgname}-${pkgver}.tar.xz")
md5sums=('b0f088b9bb3949369efa26af21e42c8d')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DECM_MKSPECS_INSTALL_DIR=/usr/share/qt/mkspecs/modules \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
