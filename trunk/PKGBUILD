# $Id$
# Maintainer: judd <jvinet@zeroflux.org>
pkgname=man-pages
pkgver=3.00
_posixver=2003-a
pkgrel=1
pkgdesc="Linux man pages"
arch=(i686 x86_64)
license=('GPL')
url="http://www.kernel.org/doc/man-pages/"
groups=('base')
depends=()
source=(http://www.kernel.org/pub/linux/docs/man-pages/$pkgname-$pkgver.tar.gz
	http://www.kernel.org/pub/linux/docs/man-pages/man-pages-posix/$pkgname-posix-${_posixver}.tar.bz2)
md5sums=('cfd848e71f40ef7e12a7460f584946f7'
         '7c78aff03c0a6767ba483d34f19e4b09')

build() {
  cd $startdir/src/$pkgname-$pkgver
  make prefix=$startdir/pkg/usr install || return 1

  cd $startdir/src/$pkgname-posix-${_posixver}
  make prefix=$startdir/pkg/usr install || return 1

  cd $startdir/pkg/usr/share/man
  # these are included in coreutils || return 1
  rm -f man1/{chgrp,chmod,chown,cp,dir,dd}.1 || return 1
  rm -f man1/{df,dircolors,du,install,ln,ls}.1 || return 1
  rm -f man1/{mkdir,mkfifo,mknod,mv,rm,rmdir}.1 || return 1
  rm -f man1/{touch,vdir}.1 || return 1
  # this is included in shadow
  rm -f man5/passwd.5 || return 1
  rm -f man3/getspnam.3 || return 1
  # this is included in diffutils
  rm -f man1/diff.1 || return 1
  # this is included in xf86-input-mouse
  rm -f man4/mouse.4 || return 1
  # these are included in module-init-tools
  rm -f man2/{create_module,delete_module,get_kernel_syms}.2 || return 1
  rm -f man2/{init_module,query_module}.2 || return 1
}
