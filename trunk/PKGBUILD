# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=libkgapi
pkgver=5.1.0
pkgrel=3
pkgdesc="A KDE-based library for accessing various Google services via their public API"
url='https://projects.kde.org/projects/extragear/libs/libkgapi'
arch=(i686 x86_64)
license=(GPL LGPL FDL)
depends=(kcalcore kcontacts)
makedepends=(extra-cmake-modules kdoctools qt5-tools)
source=("http://download.kde.org/stable/$pkgname/$pkgver/src/$pkgname-$pkgver.tar.xz"
         bug355908.patch::"https://quickgit.kde.org/?p=libkgapi.git&a=commitdiff&h=46b9c59d&o=plain")
md5sums=('a65d1d637f18655b9b9bd22acf3813ec'
         '03314cf2f8241fe6b4881c62614fe1c4')

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
# Fix syncing Google Calendar events https://bugs.kde.org/show_bug.cgi?id=355908
  patch -p1 -i ../bug355908.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
