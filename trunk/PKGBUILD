# $Id$
# Maintainer: tobias <tobias@archlinux.org>
# Contributor: Sarah Hay <sarah@archlinux.org>

pkgname=xvidcore
pkgver=1.3.2
pkgrel=2
pkgdesc="XviD is an open source MPEG-4 video codec"
arch=('i686' 'x86_64')
url="http://www.xvid.org/"
license=('GPL')
depends=('glibc')
makedepends=('nasm')
source=(http://downloads.xvid.org/downloads/${pkgname}-${pkgver}.tar.bz2)
md5sums=('34389e980d4f849688f8b1e168f4e2f9')
sha1sums=('74f9ce9c1bebcc9806bf21cb53274b8102700309')

build() {
  cd "${srcdir}/${pkgname}/build/generic"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}/build/generic"
  make DESTDIR="${pkgdir}" install

  #Fix dynamic libraries
  cd "${pkgdir}/usr/lib"
  _mylib=$(basename libxvidcore.so.*)
  ln -sf ${_mylib} libxvidcore.so.4
  ln -sf ${_mylib} libxvidcore.so
}
