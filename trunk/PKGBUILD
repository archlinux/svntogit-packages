# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=fping
pkgver=3.5
pkgrel=1
pkgdesc="A utility to ping multiple hosts at once"
arch=('i686' 'x86_64')
url="http://www.fping.org/"
license=('custom') 
depends=('glibc')
source=("http://www.fping.org/dist/${pkgname}-${pkgver}.tar.gz")
md5sums=('2e17cb655aa4eb59b5a4a38a89e746ed')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr \
              --sbindir=/usr/bin \
              --mandir=/usr/share/man \
              --enable-ipv4 \
              --enable-ipv6
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="${pkgdir}" install

  # install license
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/$pkgname/LICENSE"
  chmod 4555 ${pkgdir}/usr/bin/fping
}
