# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Gabriel Martinez < reitaka at gmail dot com >

pkgname=libimobiledevice
pkgver=1.2.0+87+g92c5462
pkgrel=6
pkgdesc="Library that talks the protocols to support iPhone and iPod Touch devices on Linux"
url="https://libimobiledevice.org/"
arch=('x86_64')
license=('GPL2' 'LGPL2.1')
depends=('libusbmuxd' 'usbmuxd' 'gnutls')
makedepends=('python' 'cython' 'libplist' 'autoconf-archive'
             'git' 'python-setuptools')
_commit=92c5462adef87b1e577b8557b6b9c64d5a089544  # master
source=("git+https://github.com/libimobiledevice/libimobiledevice#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  # Update ac_python_devel.m4 to newer ax_python_devel.m4
  git cherry-pick -n eea4f1be91
  NOCONFIGURE=1 ./autogen.sh
}

build() (
  cd $pkgname
  ./configure --prefix=/usr --disable-openssl
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
)

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  DESTDIR="$pkgdir" make install
}

# vim:set sw=2 et:
