# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: eric <eric@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
 
pkgname=psutils
pkgver=1.17
pkgrel=6
arch=('x86_64')
pkgdesc="A set of postscript utilities"
url="http://knackered.org/angus/psutils/"
license=('custom')
depends=('glibc' 'ghostscript')
provides=('psutils-lprng')
replaces=('psutils-lprng')
options=('!makeflags')
source=("https://sources.archlinux.org/other/psutils/${pkgname}-${pkgver}.tar.gz"
        'psutils-1.17.diff')
sha512sums=('91feff88c885173ab1c4ba6b5ffd0d9e90bfa6105479b074065584fc2a87e78fbe71e4bc90d2e7079d86b467caf092b6744b0ea4234880ba78cad8dd7820de77'
            '795f45c6315e3c02485d1d5243c1541992b669539257249a185f57136e623c489a9f3635d3f171f698428aeb9710332cf36bb0aa0ade8ea351514044630f12f0')

build() {
  cd "${srcdir}/${pkgname}"
  patch -Np1 -i "${srcdir}/psutils-1.17.diff"
  make -f Makefile.unix BINDIR=/usr/bin \
    INCLUDEDIR=/usr/include/psutils PERL=/usr/bin/perl \
    MANDIR=/usr/share/man/man1 \
    CFLAGS="$CFLAGS -DPAPER=\\\"letter\\\" -DUNIX -Wall" \
    CCFLAGS="$LDFLAGS"
}

package() {
  cd "${srcdir}/${pkgname}"
  install -d -m755 "${pkgdir}/usr/include"
  install -d -m755 "${pkgdir}/usr/share/man"
  make -f Makefile.unix BINDIR="${pkgdir}/usr/bin" \
    INCLUDEDIR="${pkgdir}/usr/include/psutils" PERL=/usr/bin/perl \
    MANDIR="${pkgdir}/usr/share/man/man1" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/"
}
