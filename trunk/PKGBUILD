# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: Aleksey Filippov <sarum9in@gmail.com>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Thomas S Hatch <thatch45@gmail.com>
# Contributor: Geoffroy Carrier <geoffroy@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgbase='protobuf'
pkgname=('protobuf' 'python-protobuf')
pkgver=21.10
pkgrel=1
pkgdesc="Protocol Buffers - Google's data interchange format"
arch=('x86_64')
url='https://developers.google.com/protocol-buffers/'
license=('BSD')
depends=('gcc-libs' 'glibc' 'zlib')
makedepends=('python-setuptools' 'cmake' 'gtest')
source=(https://github.com/protocolbuffers/protobuf/archive/v$pkgver/$pkgname-$pkgver.tar.gz
        https://github.com/protocolbuffers/protobuf/commit/101b6199.patch)
sha512sums=('83ab712c610da011d73fc5162f67163016ced3f242f4126e49e8ebb203b9d70fb9e7e6afff1ac05712ceca251aaa6e33f4ea656083161d0c458ba826508a42ea'
            '63a56d6ba2bd452ef5380eca4ec5e294a395a6dae95fbe3ae28ee6bceb601450e234a2052b1df010c9cea008b8d03176ffd08fa62a66bf5e7af64c9829e788c8')

prepare() {
  patch -d $pkgbase-$pkgver -Rp1 < 101b6199.patch # Revert ABI break
}

build() {
  cmake -B build -S $pkgbase-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -Dprotobuf_USE_EXTERNAL_GTEST=ON \
    -Dprotobuf_BUILD_SHARED_LIBS=ON
  cmake --build build

  ln -rs build $pkgbase-$pkgver/src/.libs
  cd $pkgbase-$pkgver/python
  PROTOC="$srcdir"/build/protoc \
  python setup.py build --cpp_implementation
}

check() {
  cmake --build build --target check

  cd $pkgbase-$pkgver/python
  python setup.py test
}

package_protobuf() {
  replaces=('protobuf-cpp')
  provides=('libprotoc.so' 'libprotobuf.so' 'libprotobuf-lite.so')

  DESTDIR="$pkgdir" cmake --install build

  cd $pkgbase-$pkgver
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -vDm 644 editors/protobuf-mode.el \
    -t "$pkgdir/usr/share/emacs/site-lisp/"
  install -vDm 644 editors/proto.vim \
    -t "${pkgdir}/usr/share/vim/vimfiles/syntax"
}

package_python-protobuf() {
  pkgdesc='Python 3 bindings for Google Protocol Buffers'
  depends=('python' 'python-six' "protobuf=$pkgver")

  local python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')
  cd "$pkgbase-$pkgver"
  (
    cd python
    PYTHONPATH="$PWD/build/lib.linux-$CARCH-${python_version}:PYTHONPATH" \
    python setup.py install --skip-build \
                            --cpp_implementation \
                            --optimize=1 \
                            --root="$pkgdir"
  )
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
