# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=tinycdb
pkgver=0.78
pkgrel=6
pkgdesc='Very fast and simple package for creating and reading constant data bases'
arch=(x86_64)
url="https://www.corpit.ru/mjt/tinycdb.html"
license=(custom)
depends=(glibc)
provides=(libcdb.so)
# we don't want libcdb.a and would need to modify the Makefile otherwise
options=(!staticlibs)
source=(https://www.corpit.ru/mjt/tinycdb/$pkgname-$pkgver.tar.gz)
sha512sums=('8930086b8e7fddcd4dbd3354c5f5ee05171df68fde1cc222b6c402430042b6e761efbad7e5fa8de18e1d36390f1526cc3e605c5086fe1c363ba1df6c03201553')
b2sums=('41f4256840645acd28296c5473087c7cf4c026f068310375b3c305430c030b1c946f53fd3e09967d77b592ba84e7e56518fdb18e54d1e0f5e7dd552a1664a0ec')

build() {
  local make_options=(
    CFLAGS="$CFLAGS"
    LDFLAGS="$LDFLAGS"
    all
    shared
    -C $pkgname-$pkgver
  )

  make "${make_options[@]}"
}

package() {
  local make_options=(
    DESTDIR="$pkgdir"
    prefix=/usr
    mandir=/usr/share/man
    INSTALLPROG=cdb-shared
    install-{all,sharedlib}
    -C $pkgname-$pkgver
  )

  make "${make_options[@]}"
  install -vDm 644 $pkgname-$pkgver/debian/copyright -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 $pkgname-$pkgver/{NEWS,ChangeLog} -t "$pkgdir/usr/share/doc/$pkgname/"
}
