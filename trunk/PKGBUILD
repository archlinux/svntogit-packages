# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=squashfuse
pkgver=0.1.103
pkgrel=2
pkgdesc="FUSE filesystem to mount squashfs archives"
arch=(x86_64)
url="https://github.com/vasi/squashfuse"
license=(custom)
depends=(fuse2 zstd)
makedepends=()
source=("https://github.com/vasi/squashfuse/releases/download/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('42d4dfd17ed186745117cfd427023eb81effff3832bab09067823492b6b982e7')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname

# install missing headers
  install -Dm644 *.h -t "$pkgdir"/usr/include/squashfuse
  sed -e 's|\#include \"|\#include \"squashfuse\/|g' -i "$pkgdir"/usr/include/squashfuse.h
}
