# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=alsa-ucm-conf
pkgver=1.2.4
pkgrel=1
pkgdesc="ALSA Use Case Manager configuration (and topologies)"
url="https://alsa-project.org/"
arch=('any')
license=('BSD')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/alsa-project/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('6e218dbf51d498ce965f64a897f1ed22c625e4e02f6768eea4f2ecee31659dc50dc92b6544a205a4fd5f5b7b2553db81e12b0ece3e3ec11fcbeb4dade779a52d')
b2sums=('cc7242ab6b2db7951d26c0536684c60a724afe64624f05ce38de348e9824b508f0cca13ea44af50a948d7cc2263bef3ba28b7a49b673212f0d32979ce88b79aa')

package() {
  cd "${pkgname}-${pkgver}"
  find ucm2 -type f -iname "*.conf" -exec install -vDm 644 {} "${pkgdir}/usr/share/alsa/"{} \;
  find ucm2 -type l -iname "*.conf" -exec cp -dv {} "${pkgdir}/usr/share/alsa/"{} \;
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -vDm 644 ucm2/README.md -t "$pkgdir/usr/share/doc/$pkgname/ucm2"
}
