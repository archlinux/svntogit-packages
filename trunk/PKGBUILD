# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=avidemux
pkgver=2.4.4
pkgrel=3
pkgdesc="A graphical tool to edit video (filter/re-encode/split)"
arch=('i686' 'x86_64')
license=('GPL')
url="http://fixounet.free.fr/avidemux/"
depends=('libxv' 'libvorbis' 'libxml2' 'alsa-lib' 'lame' 'xvidcore' 'faad2>=2.7' 'faac' 'sdl' 'x264>=20090416')
makedepends=('cmake' 'libxslt' 'gtk2' 'qt')
optdepends=('gtk2: for using the GTK GUI' 'qt: for using the QT4 GUI')
options=('!makeflags')
source=(http://download.berlios.de/avidemux/${pkgname}_${pkgver}.tar.gz avidemux.desktop \
        avidemux-2.4-i18n.patch avidemux-2.4-cmake264.patch avidemux-2.4.4-gcc-4.4.patch)
md5sums=('503dfd25842ef81be7c769811e7bc54e' 'a9cf864c209782307afda5bc6a33a0cd'\
         'c76ab4a1434689b5be2084c4af66298c' 'bc1e21d62ed9b46f5727d692b1130adb'\
         'f54b663879ae8331883a2bb2d252d837')
sha1sums=('c50a00caf13a8ea1053e5994edeb59354192e18e'
          'e575817d59cd992c2b056c1ff978c23f7fa0e50b'
          '32557519caeed3b236f48b40d5ee8953b2d03197'
          '387804ad24a816a24a036b57692cee9ff13e728f'
          '63de9ce7999af1b9f94c01b14ccd049c2b27f32a')

build() {
  cd ${srcdir}/${pkgname}_${pkgver}
  patch -p1 < ../avidemux-2.4-i18n.patch || return 1
  patch -p1 < ../avidemux-2.4-cmake264.patch || return 1
  patch -p0 < ../avidemux-2.4.4-gcc-4.4.patch || return 1

  mkdir build
  cd build
  cmake -D CMAKE_INSTALL_PREFIX=/usr -D CMAKE_BUILD_TYPE=Release -D NO_GTK=0 -D NO_QT4=0 .. || return 1
  make || return 1
  make DESTDIR=${pkgdir} install || return 1

  install -D -m644 ../avidemux_icon.png ${pkgdir}/usr/share/pixmaps/avidemux.png || return 1
  install -D -m644 ${srcdir}/avidemux.desktop ${pkgdir}/usr/share/applications/avidemux.desktop || return 1
  install -D -m644 ../man/avidemux.1 ${pkgdir}/usr/share/man/man1/avidemux.1 || return 1
}
