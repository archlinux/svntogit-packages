# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=alsa-ucm-conf
pkgver=1.2.5
pkgrel=2
pkgdesc="ALSA Use Case Manager configuration (and topologies)"
url="https://alsa-project.org/"
arch=('any')
license=('BSD')
source=("https://www.alsa-project.org/files/pub/lib/${pkgname}-${pkgver}.tar.bz2"{,.sig}
        "${pkgname}-1.2.5-hda_intel_lookups.patch::https://github.com/alsa-project/alsa-ucm-conf/commit/3f34021beffba4e39f064a14c5faceeaa224b766.patch"
)
sha512sums=('f16e8f04018c65de5e24f2a3eb0b3edff26cbce78cc1820c42c46907c903913c571797b59e803ae36b8ec3136487d0a5aacd034e2a8df3e15ed998b7a70152b7'
            'SKIP'
            '17f599150f628f02f43aa654026d4cf359594099fb3643702d928aa3e00519ee4dd0b424329fd2419a0efa2c1e74376c6faec42163d48a1116244e8d43b01b4a')
b2sums=('8ecd5adee6c9f429ab2791dfd57b0f9b1d58272b04d2c6cd3ad9aba36b53b380cfe626cae1e5c40f703eb7f891fb53b0bf70a78f9e5d2d8ae6b2dfb30fd6f659'
        'SKIP'
        '80bcd7bea55f1c1200d4b4af804ed4b42c1afddf36dc63e199e7d47d04ad978787b049b960df99add78c695e931f51107dae21c6c48d14b2a08d298d0960d75e')
validpgpkeys=('F04DF50737AC1A884C4B3D718380596DA6E59C91') # ALSA Release Team (Package Signing Key v1) <release@alsa-project.org>

prepare() {
  # upstream creates tarball contents that don't match the version
  # https://github.com/alsa-project/alsa-ucm-conf/issues/95
  mv -v "${pkgname}-"*/ "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  # fix issue with missing lookup:
  # https://github.com/alsa-project/alsa-lib/issues/148
  # https://bugs.archlinux.org/task/71180
  patch -Np1 -i ../"${pkgname}-1.2.5-hda_intel_lookups.patch"
}

package() {
  cd "${pkgname}-${pkgver}"
  find ucm2 -type f -iname "*.conf" -exec install -vDm 644 {} "${pkgdir}/usr/share/alsa/"{} \;
  find ucm2 -type l -iname "*.conf" -exec cp -dv {} "${pkgdir}/usr/share/alsa/"{} \;
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -vDm 644 ucm2/README.md -t "$pkgdir/usr/share/doc/$pkgname/ucm2"
}
