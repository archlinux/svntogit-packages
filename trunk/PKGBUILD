# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>

pkgbase=python-geoip
pkgname=('python-geoip' 'python2-geoip')
pkgver=1.3.2
pkgrel=5
pkgdesc="Python bindings for the GeoIP IP-to-country resolver library"
arch=('x86_64')
url="https://pypi.python.org/pypi/GeoIP/"
license=('LGPL')
makedepends=('python' 'python2' 'geoip')
source=(https://pypi.python.org/packages/source/G/GeoIP/GeoIP-${pkgver}.tar.gz)
md5sums=('6d1a82798b9f5f8448fca50296d974ba')

prepare() {
  cp -a "GeoIP-$pkgver"{,-py2}
}

build() {
  cd "GeoIP-$pkgver"
  python setup.py build

  cd "../GeoIP-$pkgver-py2"
  python2 setup.py build
}

package_python-geoip() {
  depends=('python' 'geoip')

  cd "GeoIP-$pkgver"
  python setup.py install --root="$pkgdir" -O1
}

package_python2-geoip() {
  depends=('python2' 'geoip')
  conflicts=('python-geoip<1.2.8')

  cd "GeoIP-$pkgver-py2"
  python2 setup.py install --root="$pkgdir" -O1
}

# vim:set ts=2 sw=2 et:
