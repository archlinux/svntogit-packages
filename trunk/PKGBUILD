# $Id$
# Maintainer: Dave Reisner <dreisner@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org> 
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: ice-man <icemanf@gmail.com>

pkgname=libssh2
pkgver=1.4.3
pkgrel=1
pkgdesc="A library implementing the SSH2 protocol as defined by Internet Drafts"
url="http://www.libssh2.org/"
arch=('i686' 'x86_64')
license=('BSD')
depends=('openssl')
makedepends=('zlib')
provides=('libssh2.so')
options=('!libtool')
source=("http://www.libssh2.org/download/$pkgname-$pkgver.tar.gz"{,.asc})
md5sums=('071004c60c5d6f90354ad1b701013a0b'
         'bd01eca1e58c15233d69dae411dd2656')

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr
  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
