# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gom
pkgver=0.3.3
pkgrel=2
pkgdesc="A GObject to SQLite object mapper"
arch=(x86_64)
url="https://wiki.gnome.org/Projects/Gom"
license=(LGPL2.1)
depends=(glib2 sqlite)
makedepends=(gtk-doc gobject-introspection gdk-pixbuf2 git meson python-gobject)
_commit=dafdf432ac78f500dcd045092d5d5c28f8e35c60  # tags/0.3.3^0
source=("git+https://git.gnome.org/browse/gom#commit=$_commit")
sha256sums=('SKIP')
 
pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  mkdir build
  cd $pkgname
}

build() {
  cd build
  meson setup --prefix=/usr --buildtype=release -Denable-gtk-doc=true ../$pkgname
  ninja
}

check() {
  cd build
  meson test
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}

# vim:set ts=2 sw=2 et:
