# Maintainer:

pkgname=perl-http-cookies
pkgver=6.04
pkgrel=3
pkgdesc="HTTP cookie jars"
arch=('any')
url="http://search.cpan.org/dist/HTTP-Cookies"
license=('PerlArtistic' 'GPL')
depends=('perl' 'perl-http-date' 'perl-http-message')
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/O/OA/OALDERS/HTTP-Cookies-$pkgver.tar.gz)
sha512sums=('e8b24b96e6630f1238e828edfbf7074fb9186a391fd1d129fb65a82748a4cf7d71669fa658c31957560a6faac3e638c8dc7aa3182acaa6fd14f0d56f3968f5e4')

build() {
  cd HTTP-Cookies-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd HTTP-Cookies-$pkgver
  make test
}

package() {
  cd HTTP-Cookies-$pkgver
  make DESTDIR="$pkgdir" install
}
