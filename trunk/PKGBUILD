# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=oxygen-icons
pkgname=('oxygen-icons'
         'oxygen-icons-svg')
pkgver=15.04.3
pkgrel=1
pkgdesc="The Oxygen Icon Theme"
arch=('any')
url='http://www.oxygen-icons.org/'
license=('LGPL')
makedepends=('cmake' 'automoc4')
source=("http://download.kde.org/stable/applications/${pkgver}/src/${pkgname}-${pkgver}.tar.xz")
sha1sums=('22c4259a797c32a87970046d2ffb4f9d7615bb2e')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package_oxygen-icons() {
  cd build
  make DESTDIR="$pkgdir" install
}

package_oxygen-icons-svg() {
  pkgdesc="The Oxygen Icon Theme (Scalable Vector Graphics)"

  cd ${pkgbase}-${pkgver}
  find scalable -type f ! -name '*.sh' -exec \
    install -D -m644 "{}" "${pkgdir}/usr/share/icons/oxygen/{}" \;
}
