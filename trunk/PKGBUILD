# $Id$
#Maintainer: Jan de Groot <jgc@archlinux.org>
pkgname=libpst
pkgver=0.6.41
pkgrel=5
arch=('i686' 'x86_64')
pkgdesc="Outlook .pst file converter"
url="http://www.five-ten-sg.com/libpst/"
license=('GPL')
depends=('gcc-libs>=4.5.0')
makedepends=('python' 'boost')
options=('!libtool')
source=(http://www.five-ten-sg.com/libpst/packages/${pkgname}-${pkgver}.tar.gz
        fix-linking.patch)
md5sums=('d2cae1a3ae960eccb74b59f13b9c1744'
         '0845d46e73f3fbbaa114b874cbd1079f')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/fix-linking.patch" || return 1
  libtoolize --force || return 1
  aclocal -I m4 || return 1
  autoconf || return 1
  automake || return 1
  ./configure --prefix=/usr --mandir=/usr/share/man \
      --disable-static --enable-libpst-shared --disable-dii || return 1
  make || return 1
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install || return 1
}
