# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=kscreen
pkgver=5.6.5
pkgrel=1
pkgdesc="KDE's screen management software"
arch=('i686' 'x86_64')
url='https://www.kde.org/workspaces/plasmadesktop/'
license=('LGPL')
depends=('kxmlgui' 'libkscreen' 'qt5-graphicaleffects' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'python')
conflicts=('kscreen-frameworks')
replaces=('kscreen-frameworks')
groups=('plasma')
source=("http://download.kde.org/stable/plasma/${pkgver}/kscreen-$pkgver.tar.xz"{,.sig})
sha256sums=('f9f301bde43c1171b7064834fad144bdc5df9b5882133f5a94c7dc5d27709699'
            'SKIP')
validpgpkeys=('13C16D03EDE728514473AA73A506E6D4DD4D5088') # Jonathan Riddell

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kscreen-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
