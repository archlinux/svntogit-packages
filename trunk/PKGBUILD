# Maintainer: Paul Mattal <paul@archlinux.org>

pkgname=hdparm
pkgver=9.58
pkgrel=2
pkgdesc="A shell utility for manipulating Linux IDE drive/driver parameters"
arch=(x86_64)
depends=('glibc')
optdepends=('sh: required by idectl and ultrabayd')
source=(https://downloads.sourceforge.net/sourceforge/hdparm/${pkgname}-${pkgver}.tar.gz
        wiper.sh.2_6.max-ranges.patch pass-ldflags.patch)
license=('BSD')
url="https://sourceforge.net/projects/hdparm/"
optdepends=('bash: for wiper.sh script')
options=('emptydirs')
sha512sums=('42fb70f494aae4cc205ca68c3e883e1f4fc33e8ced1914e280e9f3faa7233d3f839e59178ec09ddb9d32e9d2bca122ce324645ecb5ac73defa284030ac7d3cea'
            'ddb9eff6e4c0b67da72104a9ddcb6fde2b6ad543174e56a7e065453beb88f4c16b977718b1db4beaed41aff477a0ed955303172d019efbe9eacef2a397f27499'
            '21adb4a515e0b7ea9535bf2201e0b9a668c7f912f010d3efc6f8440344f107169daf0d94d77602b2162a6619b6a02bb0da24f2418ff3c7e0160395ae8889e5bc')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}
  # Fix Range input/output error when wiping Intel G2 and OCZ drives
  patch -Np1 -i $srcdir/wiper.sh.2_6.max-ranges.patch
  # Enable FULL RELRO
  patch -Np1 -i $srcdir/pass-ldflags.patch
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  # install
  mkdir -p ${pkgdir}/usr/bin
  make DESTDIR=${pkgdir} sbindir=/usr/bin install
  install -m755 contrib/idectl ${pkgdir}/usr/bin
  install -m755 contrib/ultrabayd ${pkgdir}/usr/bin

  install -D -m 0644 $srcdir/$pkgname-$pkgver/wiper/README.txt $pkgdir/usr/share/doc/wiper/README.txt
  install -D -m 0755 $srcdir/$pkgname-$pkgver/wiper/wiper.sh $pkgdir/usr/bin/wiper.sh

  #install license file
  install -D -m 644 LICENSE.TXT $pkgdir/usr/share/licenses/hdparm/LICENSE.TXT
}
