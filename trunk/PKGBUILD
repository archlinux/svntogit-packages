# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=dleyna-server
pkgver=0.5.0
pkgrel=1
pkgdesc="Library to discover, browse and manipulate Digital Media Servers"
arch=(i686 x86_64)
url="http://01.org/dleyna"
license=(LGPL2.1)
depends=(dleyna-connector-dbus gupnp-av gupnp-dlna libsoup)
source=(https://01.org/sites/default/files/downloads/dleyna/$pkgname-$pkgver.tar.gz
        proptype.patch)
sha256sums=('65aea97df031ac2ceeae04bf8473d2a260db58ced737992f882fe331ec3f19ae'
            '10017d4f7bcfceca92a9ea06144e4ebe2197548d4c8a0c8368ee35ebac5c01a6')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../proptype.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --libexecdir=/usr/lib/$pkgname
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
