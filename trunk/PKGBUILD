# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Lucien Immink <l.immink@student.fnt.hvu.nl>

pkgname=curl
pkgver=7.19.6
pkgrel=2
pkgdesc="An URL retrival utility and library"
arch=('i686' 'x86_64')
url="http://curl.haxx.se"
license=('MIT')
depends=('zlib' 'openssl>=0.9.8b' 'bash' 'ca-certificates')
options=('!libtool')
source=(http://curl.haxx.se/download/${pkgname}-${pkgver}.tar.bz2)
md5sums=('8402c1f654c51ad7287aad57c3aa79be')
sha1sums=('b7cb98c7a6577de7073d0b0c23eecdb8ed993e8e')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Thanks Debian and Gentoo =)
  ./configure \
  	--with-random=/dev/urandom \
	--prefix=/usr \
	--mandir=/usr/share/man \
	--disable-dependency-tracking \
	--enable-ipv6 \
	--disable-ldaps \
	--disable-ldap \
	--enable-manual \
	--enable-versioned-symbols \
	--with-ca-path=/etc/ssl/certs \
	--without-libidn || return 1

  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING" || return 1
  install -D -m644 docs/libcurl/libcurl.m4 "${pkgdir}/usr/share/aclocal/libcurl.m4" || return 1
}
