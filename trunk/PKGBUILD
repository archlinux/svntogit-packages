# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Mihai Bi≈üog <mihai.bisog@gmail.com>

pkgname=fmt
pkgver=6.2.1
pkgrel=3
pkgdesc='Open-source formatting library for C++'
arch=(x86_64)
url=https://fmt.dev
license=(BSD)
depends=(gcc-libs)
makedepends=(
  cmake
  doxygen
  git
  ninja
  npm
  python-pip
  python-virtualenv
)
provides=(libfmt.so)
source=(git+https://github.com/fmtlib/fmt.git#tag=19bd751020a1f3c3363b2eb67a039852f139a8d3
        dont-install-doctrees.patch) # https://github.com/fmtlib/fmt/commit/8f2b5fe74d34ba8bb20a362a4daa6cc5a99a2010


pkgver() {
  cd fmt

  git describe --tags
}

prepare() {
  npm install less less-plugin-clean-css
  sed "s/'lessc',/'npx', 'lessc',/" -i fmt/doc/build.py

  # Don't install sphinx cache doctrees directory
  cd fmt
  patch -Np1 -i ${srcdir}/dont-install-doctrees.patch
}

build() {
  cmake -S fmt -B build \
    -G Ninja \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DBUILD_SHARED_LIBS=1
  cmake --build build
  cmake --build build --target doc
}

check() {
  cmake --build build --target test
}

package() {
  DESTDIR="${pkgdir}" cmake --build build --target install
  install -Dm 644 fmt/LICENSE.rst -t "${pkgdir}"/usr/share/licenses/fmt
}

# vim: ts=2 sw=2 et:
md5sums=('SKIP'
         'f29b10877482313f98456cc2674c0c37')
