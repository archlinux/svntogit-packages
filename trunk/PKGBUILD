# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Aaron Griffin <aaron@archlinux.org>
# Original TU: Jeff Mickey <j@codemac.net>
# Contributor: ciccio.a

pkgname=squashfs-tools
pkgver=4.5.1
pkgrel=1
pkgdesc="Tools for squashfs, a highly compressed read-only filesystem for Linux"
arch=(x86_64)
url="https://github.com/plougher/squashfs-tools"
license=(GPL2)
depends=(lz4 lzo xz zlib zstd)
makedepends=(help2man)
options=(debug)
source=($pkgname-$pkgver.tar.gz::https://github.com/plougher/$pkgname/archive/refs/tags/$pkgver.tar.gz)
sha512sums=('b3934ea1e26c7508110312711465644a6d9674b6b5332a7d011e191fa3c1d4b8be694214794a0f6005263d0f4e18bab96af2f7ed66a178f8e3bb3a781cd44896')
b2sums=('cbd35daf582d284b021911bb754ed548763f2773e1bc02516ca18bab7034e352780c11eb7dfed1f047b70974dcfb716bb1a1d3729e5b25662203cfbf93553b73')

build() {
  local _params=(GZIP_SUPPORT=1 XZ_SUPPORT=1 LZO_SUPPORT=1 LZMA_XZ_SUPPORT=1 LZ4_SUPPORT=1 ZSTD_SUPPORT=1 XATTR_SUPPORT=1)
  make ${_params[@]} -C $pkgname-$pkgver/$pkgname
}

package() {
  make -C $pkgname-$pkgver/$pkgname install INSTALL_PREFIX="$pkgdir/usr"
  install -vDm 644 $pkgname-$pkgver/{ACTIONS-README,CHANGES,USAGE} $pkgname-$pkgver/README-$pkgver -t "$pkgdir/usr/share/doc/$pkgname/"
}
