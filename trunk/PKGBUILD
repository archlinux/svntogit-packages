# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Daniel Isenmann <daniel@archlinux.org>

pkgname=libgdiplus
pkgver=5.6
pkgrel=1
pkgdesc='Open Source Implementation of the GDI+ API'
arch=('x86_64')
license=('MPL' 'LGPL')
url='http://www.mono-project.com/docs/gui/libgdiplus/'
depends=('libtiff' 'libjpeg' 'libpng' 'cairo' 'giflib' 'glib2' 'libexif' 'zlib' 'freetype2')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/mono/libgdiplus/archive/${pkgver}.tar.gz)
sha256sums=('6a75e4a476695cd6a1475fd6b989423ecf73978fd757673669771d8a6e13f756')
sha512sums=('15487b170e3dcca63573da2360788c5d235fc214bfbfa38b041fd3f075354589d5dcf93ee1cc9f242515f3430af43324f39aa1b51827d3f004b526fc524507df')

prepare() {
  cd ${pkgname}-${pkgver}
  autoreconf -fiv
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --with-cairo=system
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 NEWS README TODO AUTHORS ChangeLog -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
