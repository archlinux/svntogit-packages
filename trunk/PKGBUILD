# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Gabriel Martinez < reitaka at gmail dot com >

pkgname=libimobiledevice
pkgver=1.2.0+87+g92c5462
pkgrel=1
pkgdesc="Library that talks the protocols to support iPhone and iPod Touch devices on Linux"
url="http://libimobiledevice.org/"
arch=('x86_64')
license=('GPL2' 'LGPL2.1')
depends=('libusbmuxd' 'usbmuxd' 'gnutls')
makedepends=('python2' 'cython2' 'python' 'cython' 'libplist' 'autoconf-archive'
             'git' 'python2-setuptools' 'python-setuptools')
_commit=92c5462adef87b1e577b8557b6b9c64d5a089544  # master
source=("git+https://github.com/libimobiledevice/libimobiledevice#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

_build() (
  mkdir build-py$1
  cd build-py$1
  PYTHON=/usr/bin/python$1 CYTHON=/usr/bin/cython$1 \
    ../$pkgname/configure --prefix=/usr --disable-openssl
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
)

build() {
  _build 2
  _build 3
}

check() {
  make check -C build-py2
  make check -C build-py3
}

package() {
  DESTDIR="$pkgdir" make install -C build-py2
  DESTDIR="$pkgdir" make install -C build-py3/cython
}

# vim:set sw=2 et:
