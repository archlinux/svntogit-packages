# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Paul Mattal <paul@archlinux.org>

pkgname=ddrescue
pkgver=1.24
pkgrel=1
pkgdesc="GNU data recovery tool"
arch=('x86_64')
url="http://www.gnu.org/software/ddrescue/ddrescue.html"
license=('GPL3')
depends=('gcc-libs')
source=("http://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.lz")
        #"http://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.lz.sig")
sha256sums=('4b5d3feede70e3657ca6b3c7844f23131851cbb6af0cecc9721500f7d7021087')
         #'SKIP')
#validpgpkeys=('1D41C14B272A2219A739FA4F8FE99503132D7742') # Antonio Diaz Diaz

prepare() {
  sed -e '/CPPFLAGS=/d' -e '/CXXFLAGS=/d' -e '/LDFLAGS=/d' -i $pkgname-$pkgver/configure # Honor system CFLAGS
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install install-man
  rm "$pkgdir"/usr/share/info/dir
}
