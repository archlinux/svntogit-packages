# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-xeyes
pkgver=1.1.2
pkgrel=1
pkgdesc="Follow the mouse/SHAPE extension X demo"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libx11' 'libxt' 'libxext' 'libxmu' 'libxrender')
makedepends=('xorg-util-macros')
source=(https://xorg.freedesktop.org/archive/individual/app/xeyes-${pkgver}.tar.bz2{,.sig})
sha512sums=('008d76f03ddabd3d86ad9e52a540504553b94cb7a6743b20cf08897549eb27505667365ab9af21956a1b5ec7297acca8903a326536e6195a30e643cd93297ee0'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # "Alan Coopersmith <alan.coopersmith@oracle.com>"

build() {
  cd "${srcdir}/xeyes-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/xeyes-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
