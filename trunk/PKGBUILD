# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=elisa
pkgver=20.08.3
pkgrel=2
pkgdesc='A simple music player aiming to provide a nice experience for its users'
url='https://community.kde.org/Elisa'
arch=(x86_64)
license=(LGPL3)
groups=(kde-applications kde-multimedia)
depends=(baloo kirigami2 kdeclarative vlc) # upnp-player-qt
makedepends=(extra-cmake-modules kdoctools)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
         elisa-qt5.15.1.patch::"https://invent.kde.org/multimedia/elisa/-/commit/2ccef9fb.patch")
sha256sums=('4c84c4960c90a5eb2051d7fabfa536989e2a6630e9b2049ab3576909e4b22321'
            'SKIP'
            '55d032a9e7912ec61f183cda477e9b96f74f32c014372d2e7b021fa0de3dce61')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  patch -d $pkgname-$pkgver -p1 -i ../elisa-qt5.15.1.patch # Fix crash at startup with Qt 5.15.1
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF 
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
