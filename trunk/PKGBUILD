# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=libwpg
pkgver=0.3.3
pkgrel=4
pkgdesc="Library for importing and converting Corel WordPerfect(tm) Graphics images."
arch=('x86_64')
url="https://libwpg.sourceforge.net/"
license=('LGPL')
depends=('libwpd' 'perl' 'librevenge')
makedepends=('doxygen')
source=("https://downloads.sourceforge.net/libwpg/${pkgname}-${pkgver}.tar.xz")
sha1sums=('20096bd99681c719164cefafc9046271de255acb')
sha256sums=('99b3f7f8832385748582ab8130fbb9e5607bd5179bebf9751ac1d51a53099d1c')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
