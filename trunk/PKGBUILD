# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=atk
pkgver=2.20.0
pkgrel=1
pkgdesc="A library providing a set of interfaces for accessibility"
arch=(i686 x86_64)
license=('LGPL')
depends=('glib2')
makedepends=('gobject-introspection')
source=(https://download.gnome.org/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
url='https://developer.gnome.org/atk/'
sha256sums=('493a50f6c4a025f588d380a551ec277e070b28a82e63ef8e3c06b3ee7c1238f0')

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
