# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=l-smash
pkgver=2.14.5
pkgrel=1
pkgdesc='MP4 muxer and other tools'
arch=('x86_64')
url='https://github.com/l-smash/l-smash'
license=('custom')
depends=('glibc')
provides=('liblsmash.so')
source=("l-smash-${pkgver}.tar.gz::https://github.com/l-smash/l-smash/archive/v${pkgver}.tar.gz")
sha256sums=('e6f7c31de684f4b89ee27e5cd6262bf96f2a5b117ba938d2d606cf6220f05935')

build() {
  cd l-smash-${pkgver}

  ./configure \
    --prefix='/usr' \
    --enable-shared \
    --disable-static \
    --extra-cflags="$CFLAGS" \
    --extra-ldflags="$LDFLAGS"
  make
}

package() {
  cd l-smash-${pkgver}

  make DESTDIR="${pkgdir}" install

  install -dm 755 "${pkgdir}"/usr/share/licenses/l-smash
  install -m 644 LICENSE "${pkgdir}"/usr/share/licenses/l-smash/
}

# vim: ts=2 sw=2 et:
