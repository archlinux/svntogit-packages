# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kdiagram
pkgver=2.6.0
pkgrel=1
pkgdesc="Powerful libraries for creating business diagrams"
arch=(i686 x86_64)
url="http://www.kde.org/"
license=(GPL2)
depends=(qt5-svg)
makedepends=(extra-cmake-modules qt5-tools)
source=("http://download.kde.org/stable/$pkgname/$pkgver/src/$pkgname-$pkgver.tar.xz")
sha1sums=('e0a94210c295990536465ab1d3fb0d0805382a67')


prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF \
    -DKDE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
