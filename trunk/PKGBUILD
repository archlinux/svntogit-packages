# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=baobab
pkgver=3.28.0
pkgrel=1
pkgdesc="A graphical directory tree analyzer"
url="https://wiki.gnome.org/Apps/Baobab"
arch=(x86_64)
license=(GPL2)
depends=(dconf gtk3 gsettings-desktop-schemas librsvg)
makedepends=(intltool itstool vala gobject-introspection yelp-tools appstream-glib
             git meson)
groups=(gnome)
_commit=9b17875c2ea649efd414f1c8f56d8b4abb69ed2b  # tags/3.28.0^0
source=("git+https://git.gnome.org/browse/baobab#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install

  # https://bugs.archlinux.org/task/31861
  sed -i '/MimeType/d' "$pkgdir/usr/share/applications/org.gnome.baobab.desktop"
}
