# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libxaw
pkgver=1.0.13
pkgrel=2
pkgdesc="X11 Athena Widget library"
arch=('x86_64')
license=('custom')
url="https://xorg.freedesktop.org/"
depends=('libxmu' 'libxpm')
makedepends=('xorg-util-macros')
source=(${url}/releases/individual/lib/libXaw-${pkgver}.tar.bz2{,.sig})
sha512sums=('d768a39f7111802493fa1df1b80d858e4139ceeb883d45ff13ce3b7a0e775a7d2834b7ad157c8330117f04c32f38979795332dd7a119adb2344fcb1aa9cf1e2f'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') #Alan Coopersmith <alan.coopersmith@oracle.com>


build() {
  cd libXaw-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static
  make
}

package() {
  cd libXaw-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
