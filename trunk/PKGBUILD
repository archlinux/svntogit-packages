# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=libdmapsharing
pkgver=2.9.41
pkgrel=2
pkgdesc="A library that implements the DMAP family of protocols"
url="https://www.flyn.org/projects/libdmapsharing/index.html"
arch=(x86_64)
license=(LGPL2.1)
depends=(libsoup avahi gst-plugins-base-libs gdk-pixbuf2)
makedepends=(git gtk-doc gobject-introspection vala)
_commit=ecee3cc737d748d45ef4524b2c19a0fdf7c73804  # tags/LIBDMAPSHARING_2_9_41^0
source=("git+https://gitlab.gnome.org/GNOME/libdmapsharing.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^LIBDMAPSHARING_//;s/_/./g;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  local configure_args=(
    --prefix=/usr
    --sysconfdir=/etc
    --localstatedir=/var
    --with-mdns=avahi

    # Disable tests; get built into the library, adding a dep on libcheck
    --disable-tests

    # GTK-docs are broken when tests are disabled
    --disable-gtk-doc
  )


  cd $pkgname
  ./configure "${configure_args[@]}"
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
