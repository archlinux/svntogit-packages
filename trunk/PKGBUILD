# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=libpeas
pkgver=1.30.0
pkgrel=3
pkgdesc="A GObject plugins library"
arch=(x86_64)
url="https://wiki.gnome.org/Projects/Libpeas"
license=(GPL2)
depends=(gtk3 gobject-introspection-runtime)
makedepends=(gtk-doc python-gobject glade gobject-introspection git meson vala)
optdepends=('python-gobject: Python loader')
provides=(libpeas{,-gtk}-1.0.so)
_commit=1d50694f770b12e1e7447d2c606a1e46533e34b7  # tags/libpeas-1.30.0^0
source=("git+https://gitlab.gnome.org/GNOME/libpeas.git#commit=$_commit")
sha256sums=('SKIP')


pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^libpeas-//;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build -D vapi=true -D gtk_doc=true
  meson compile -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}

# vim:set ts=2 sw=2 et:
