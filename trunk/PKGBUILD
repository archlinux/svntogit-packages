# $Id$
# Maintainer: Paul Mattal <paul@archlinux.org>

pkgname=lirc
pkgver=0.8.6
pkgrel=2
_kernver=2.6.32-ARCH
pkgdesc="Linux Infrared Remote Control kernel modules for stock arch kernel"
arch=('i686' 'x86_64')
url="http://www.lirc.org/"
license=('GPL')
depends=('lirc-utils=0.8.6' 'kernel26>=2.6.32' 'kernel26<2.6.33')
makedepends=('help2man' 'kernel26-headers>=2.6.32' 'kernel26-headers<2.6.33')
replaces=('lirc+pctv')
options=('!makeflags')
install=$pkgname.install
source=(http://prdownloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.bz2
        kernel-2.6.32.patch)

build() {
	# configure
	cd ${srcdir}/lirc-${pkgver} || return 1
	patch -Np1 -i ../kernel-2.6.32.patch || return 1
	./configure --enable-sandboxed --prefix=/usr \
		--with-driver=all --with-kerneldir=/usr/src/linux-${_kernver}/ \
		--with-moduledir=/lib/modules/${_kernver}/kernel/drivers/misc \
	        --with-transmitter \
		|| return 1

	# disable parallel and bt829
        # because of incompatibility with smp systems
        sed -i -e "s:lirc_parallel::" -e "s:lirc_bt829::" \
		Makefile drivers/Makefile drivers/*/Makefile tools/Makefile \
                || return 1

	# disable lirc_gpio due to brokeness of kernel 2.6.23
	sed -i -e "s:lirc_gpio::" \
		Makefile drivers/Makefile drivers/*/Makefile tools/Makefile \
                || return 1

	# build
	cd drivers || return 1
	make || return 1
	make DESTDIR=${pkgdir} install || return 1

	# set the kernel we've built for inside the install script
	sed -i -e "s/KERNEL_VERSION=.*/KERNEL_VERSION=${_kernver}/g" \
		${startdir}/lirc.install || return 1
}
md5sums=('4ca24da6f5e7c2dcea74878c27a4a3f7'
         '4e6dbbab02f82a0be2e6d9ca234416e2')
