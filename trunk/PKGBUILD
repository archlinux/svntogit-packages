# $Id$
# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
pkgname=serf
pkgver=1.2.0
pkgrel=1
pkgdesc="High-performance asynchronous HTTP client library"
url="http://code.google.com/p/serf/"
arch=('i686' 'x86_64')
license=('Apache')
depends=('apr' 'apr-util' 'openssl' 'zlib')
source=(http://serf.googlecode.com/files/${pkgname}-${pkgver}.tar.bz2)
sha1sums=('30b29bd9214d50887abcc20cf82096aaaf5d1d61')
options=('!libtool')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr --with-apr=/usr --with-apr-util=/usr --with-openssl=/usr
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  rm "${pkgdir}"/usr/lib/libserf-1.a
}
