# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>

pkgbase=python-service-identity
pkgname=('python-service-identity' 'python2-service-identity')
pkgver=0.2
pkgrel=1
pkgdesc="Service identity verification for pyOpenSSL"
arch=('any')
license=('MIT')
url="http://pypi.python.org/pypi/service_identity"
makedepends=('python-pyopenssl' 'python2-pyopenssl' 'python-pyasn1' 'python2-pyasn1'
             'python-setuptools' 'python2-setuptools' 'python-pyasn1-modules' 'python2-pyasn1-modules')
source=("http://pypi.python.org/packages/source/s/service_identity/service_identity-${pkgver}.tar.gz")
md5sums=('5fd2b5103291e71b3afe23aaeefb89f2')

prepare() {
   cp -a service_identity-${pkgver}{,-python2}
}

build() {
   # Build python 3 module
   cd service_identity-${pkgver}
   python3 setup.py build
 
   # Build python 2 module
   cd ../service_identity-${pkgver}-python2
   python2 setup.py build
}

check() {
   # Check python3 module
   cd "${srcdir}"/service_identity-${pkgver}
   python3 setup.py test

   # Check python2 module
   cd "${srcdir}"/service_identity-${pkgver}-python2
   python2 setup.py test
}
 
package_python-service-identity() {
   depends=('python' 'python-pyopenssl' 'python-pyasn1' 'python-pyasn1-modules')
 
   cd service_identity-${pkgver}
   python3 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
   install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
 
package_python2-service-identity() {
   depends=('python2' 'python2-pyopenssl' 'python2-pyasn1' 'python2-pyasn1-modules')
 
   cd service_identity-${pkgver}-python2
   python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
   install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
