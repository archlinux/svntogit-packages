# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Anton Leontiev <bunder@t-25.ru>

pkgname=graphicsmagick
pkgver=1.3.18
pkgrel=6
pkgdesc='Image processing system'
url='http://www.graphicsmagick.org/'
arch=('i686' 'x86_64')
license=('MIT')
optdepends=('jasper: jp2 module'
            'libpng: png module'
            'libwmf: wmf module'
            'libxml2: msl, svg, url modules'
            'libtiff: ps2, ps3, tiff modules'
            'ghostscript: pdf, ps modules')
makedepends=('perl' 'jasper' 'libpng' 'libwmf' 'libxml2' 'libtiff')
depends=('freetype2' 'libxext' 'libsm' 'lcms2' 'libltdl' 'bzip2' 'xz')
source=("http://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgver}/GraphicsMagick-${pkgver}.tar.xz"
        'benign.patch')
sha1sums=('085c23666adcf88585119cb6aea7efe5c58481d4'
          '04e16b9fe255e87cf16d4c61e313e8cdf7b2e24e')

options=('!emptydirs' 'libtool')

prepare() {
	cd "${srcdir}/GraphicsMagick-${pkgver}"
	patch -p1 -i ../benign.patch
}

build() {
	cd "${srcdir}/GraphicsMagick-${pkgver}"

	./configure \
		--prefix=/usr \
		--enable-shared \
		--with-modules \
		--with-perl \
		--with-gs-font-dir=/usr/share/fonts/Type1 \
		--with-quantum-depth=32 \
		--with-openmp \
		--with-threads
	make
}

package() {
	cd "${srcdir}/GraphicsMagick-${pkgver}"

	make DESTDIR="${pkgdir}" install

	# Install MIT license
	install -Dm644 "Copyright.txt" "${pkgdir}/usr/share/licenses/${pkgname}/Copyright.txt"

	# Install perl bindings
	# The patching was introduced in order to build perl module without installing package itself and
	# not to introduce unnecessary path into LD_RUN_PATH
	cd PerlMagick
	sed -i -e "s:'LDDLFLAGS'  => \"\(.*\)\":'LDDLFLAGS'  => \"-L${pkgdir}/usr/lib \1\":" Makefile.PL
	perl Makefile.PL INSTALLDIRS=vendor PREFIX=/usr DESTDIR="${pkgdir}"
	sed -i -e "s/LDLOADLIBS =/LDLOADLIBS = -lGraphicsMagick/" Makefile
	make
	make install

	# Remove perllocal.pod and .packlist
	rm -rf "${pkgdir}/usr/lib/perl5/core_perl"
	rm "${pkgdir}/usr/lib/perl5/vendor_perl/auto/Graphics/Magick/.packlist"
}
