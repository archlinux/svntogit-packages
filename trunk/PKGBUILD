# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-iceauth
pkgver=1.0.8
pkgrel=1
pkgdesc="ICE authority file utility"
arch=(x86_64)
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libice')
makedepends=('xorg-util-macros' 'xproto')
groups=('xorg-apps' 'xorg')
source=(https://xorg.freedesktop.org/archive/individual/app/iceauth-${pkgver}.tar.bz2{,.sig})
sha512sums=('9d4520adf951b16a3e784349dbb70d5d8176b74b956f8adc63abf55d049745c113b03ccfa60a281fc39b487db3742302dc6287c9985ce83a0157bf4674df2af1'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # Alan Coopersmith <alan.coopersmith@oracle.com>

build() {
  cd "${srcdir}/iceauth-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/iceauth-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
