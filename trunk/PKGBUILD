# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-clocks
pkgver=3.26.1
pkgrel=1
pkgdesc="Clocks applications for GNOME"
url="https://wiki.gnome.org/Apps/Clocks"
arch=(x86_64)
license=(GPL)
depends=(gtk3 libcanberra libgweather gnome-desktop geoclue2 geocode-glib gsound)
makedepends=(vala gobject-introspection itstool yelp-tools git meson)
groups=(gnome-extra)
_commit=ba217e6d81d446ee88abc016b428c63f6face55e  # tags/3.26.1^0
source=("git+https://git.gnome.org/browse/gnome-clocks#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  mkdir build
  cd $pkgname
}

build() {
  cd build
  meson setup --prefix=/usr --buildtype=release ../$pkgname
  ninja
}

check() {
  cd build
  meson test
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}
