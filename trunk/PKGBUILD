# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=k3b
pkgver=2.0.3a
pkgrel=2
epoch=1
pkgdesc="Feature-rich and easy to handle CD burning application"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/k3b/"
license=('GPL')
depends=('kdebase-runtime' 'libkcddb' 'libsamplerate' 'libmad'
         'ffmpeg' 'taglib' 'libmpcdec' 'libdvdread' 'cdrkit' 'libxft')
makedepends=('cmake' 'automoc4' 'docbook-xml')
optdepends=('dvd+rw-tools: for dvd burning support'
            'vcdimager: for vcd burning support'
            'transcode: for advanced mpeg conversion support'
            'emovix: for bootable multimedia cd/dvd support'
            'cdrdao: for disk-at-once (DAO) mode support'
            'cdparanoia: for cd ripping support')
install=k3b.install
source=(http://download.kde.org/stable/${pkgname}/${pkgname}-${pkgver}.tar.xz)
sha1sums=('f1001db3b5b32ef6bdb827742f1ca357c82b216f')

prepare() {
  cd $pkgname-2.0.3
# Workaround build failure with cmake 3.4
  sed -e "s|^cmake_minimum_required|#cmake_minimum_required|" -i CMakeLists.txt
}

build() {
  mkdir build
  cd build
  cmake ../${pkgname}-2.0.3 \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
