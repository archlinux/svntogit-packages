# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=pyside6
pkgname=(shiboken6 pyside6)
_qtver=6.3.0
_clangver=13.0.1
pkgver=${_qtver/-/}
pkgrel=1
arch=(x86_64)
url='https://www.qt.io'
license=(LGPL)
makedepends=(cmake ninja clang llvm python-setuptools vulkan-headers python-numpy
             qt6-tools qt6-svg qt6-3d qt6-scxml qt6-datavis3d qt6-charts qt6-multimedia qt6-quick3d
             qt6-positioning qt6-networkauth qt6-remoteobjects qt6-sensors qt6-serialport qt6-connectivity
             qt6-webchannel qt6-webengine qt6-websockets qt6-shadertools)
_pkgfn=pyside-setup-opensource-src-$_qtver
source=(https://download.qt.io/official_releases/QtForPython/pyside6/PySide6-$pkgver-src/${_pkgfn}.tar.xz
        designer-plugin-install-dir.patch
        fix-build.patch)
sha256sums=('9d808d617c8daa2fe074f9a481478dc923a9799b5c89f6c5af38ece111ed57e2'
            '66e895e07d5b01c64a94092353854c946fd7fc445b6181068dca290b5a3887e0'
            '34ea90b4576991bffa0a79349c9f0d80f9d6eafd6f519d86f5dc92dfe4f8ceb4')
options=(debug)

prepare() {
  patch -d $_pkgfn -p1 < designer-plugin-install-dir.patch # Fix designer plugin install dir
  patch -d $_pkgfn -p1 < fix-build.patch # https://bugreports.qt.io/browse/PYSIDE-1890
}

build() {
  cmake -B build -S $_pkgfn -G Ninja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DBUILD_TESTS=OFF \
    -DPYTHON_EXECUTABLE=/usr/bin/python
  PYTHONPATH="$PWD"/build/sources \
  cmake --build build
}

package_shiboken6() {
  pkgdesc='Generates bindings for C++ libraries using CPython source code'
  depends=(clang=$_clangver llvm libxslt qt6-base)
  optdepends=('python: Python bindings')

  DESTDIR="$pkgdir" cmake --install build/sources/shiboken6

# Install egg-info
  export PATH="/usr/lib/qt6/bin:$PATH"
  cd $_pkgfn
  python setup.py egg_info --build-type=shiboken6
  _pythonpath=`python -c "from sysconfig import get_path; print(get_path('platlib'))"`
  cp -r shiboken6.egg-info "$pkgdir"/$_pythonpath
}

package_pyside6() {
  pkgdesc='Enables the use of Qt6 APIs in Python applications'
  depends=(qt6-declarative shiboken6)
  optdepends=('qt6-tools: QtHelp, QtUiTools bindings'
              'qt6-svg: QtSvg bindings'
              'qt6-3d: Qt3D bindings'
              'qt6-quick3d: QtQuick3D bindings'
              'qt6-scxml: QtScxml bindings'
              'qt6-datavis3d: QtDataVisualization bindings'
              'qt6-charts: QtCharts bindings'
              'qt6-multimedia: QtMultimedia bindings'
              'qt6-serialport: QtSerialPort bindings'
              'qt6-webengine: QtWebEngine bindings'
              'qt6-remoteobjects: QtRemoteObjects bindings'
              'qt6-sensors: QtSensors bindings'
              'qt6-websockets: QtWebSockets bindings'
              'qt6-networkauth: QtNetworkAuth bindings')

  DESTDIR="$pkgdir" cmake --install build/sources/pyside6

# Install egg-info
  export PATH="/usr/lib/qt6/bin:$PATH"
  cd $_pkgfn
  python setup.py egg_info --build-type=pyside6
  _pythonpath=`python -c "from sysconfig import get_path; print(get_path('platlib'))"`
  cp -r PySide6.egg-info "$pkgdir"/$_pythonpath

# Install pyi files
  install -Dm644 "$srcdir"/build/sources/pyside6/PySide6/*.pyi -t "$pkgdir"/usr/lib/python*/site-packages/PySide6
}
