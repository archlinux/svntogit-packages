# $Id$
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>

pkgname=clutter-gst2
pkgver=2.0.16
pkgrel=1
pkgdesc="GStreamer bindings for clutter"
arch=(i686 x86_64)
url="http://www.clutter-project.org/"
license=(LGPL)
depends=(clutter gst-plugins-base-libs libxdamage)
makedepends=(gobject-introspection)
conflicts=('clutter-gst<=2.0.14')
source=(http://download.gnome.org/sources/clutter-gst/${pkgver:0:3}/clutter-gst-$pkgver.tar.xz)
sha256sums=('a944f84186937021aba6329c23954f6015d33bf151ba8994e13c59cefd742039')

build() {
  cd clutter-gst-$pkgver
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

check() {
  cd clutter-gst-$pkgver
  make check
}

package() {
  cd clutter-gst-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
