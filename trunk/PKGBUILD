# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Justin "juster" Davis <jrcd83@gmail.com>
# Generator : pbjam 0.01

pkgname=perl-http-message
pkgver=6.06
pkgrel=2
pkgdesc="HTTP style messages"
arch=(any)
url=http://search.cpan.org/dist/HTTP-Message
license=(PerlArtistic GPL)
options=('!emptydirs')
depends=('perl' 'perl-encode-locale' 'perl-html-parser'
         'perl-http-date' 'perl-lwp-mediatypes' 'perl-uri')
checkdepends=('perl-io-html')
conflicts=('perl-libwww<6')
source=(http://search.cpan.org/CPAN/authors/id/G/GA/GAAS/HTTP-Message-$pkgver.tar.gz)
md5sums=('e563ac516e28d33f65bc0f295f625f85')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor"     \
      PERL_MB_OPT="--installdirs vendor" \
      MODULEBUILDRC=/dev/null

    cd HTTP-Message-$pkgver
    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    cd HTTP-Message-$pkgver
    make test
  )
}

package() {
    cd HTTP-Message-$pkgver
    make DESTDIR="$pkgdir" install
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
