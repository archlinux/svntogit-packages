# $Id$
# Maintainer: SÃ©bastien Luttringer <seblu@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=grep
pkgver=2.21
pkgrel=2
pkgdesc='A string search utility'
arch=('i686' 'x86_64')
license=('GPL3')
url='http://www.gnu.org/software/grep/grep.html'
groups=('base' 'base-devel')
depends=('glibc' 'pcre')
makedepends=('texinfo')
install=$pkgname.install
validpgpkeys=('155D3FC500C834486D1EEA677FD9FCCB000BEEEE') # Jim Meyering
source=("ftp://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.xz"{,.sig}
        'CVE-2015-1345.patch')
md5sums=('43c48064d6409862b8a850db83c8038a'
         'SKIP'
         'f9c8e95efcc1bd52d4af42cb4bff03aa')

 prepare() {
  cd $pkgname-$pkgver
  # fix CVE-2015-1345
  patch -Np1 < "${srcdir}/CVE-2015-1345.patch"
 }

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --without-included-regex
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR=$pkgdir install
}
