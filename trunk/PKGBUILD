# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-idna
pkgname=('python-idna' 'python2-idna')
pkgver=1.1
pkgrel=1
pkgdesc="Internationalized Domain Names in Applications (IDNA)"
arch=('any')
license=('BSD')
url="https://github.com/kjd/idna"
makedepends=('python-setuptools' 'python2-setuptools')
source=("http://pypi.python.org/packages/source/i/idna/idna-${pkgver}.tar.gz")
md5sums=('9f5bfff6b317763c5a6f674707744dbe')

prepare() {
   cp -a idna-${pkgver}{,-python2}
}

build() {
   # Build python 3 module
   cd idna-${pkgver}
   python setup.py build
 
   # Build python 2 module
   cd ../idna-${pkgver}-python2
   python2 setup.py build
}

check() {
   # Check python3 module
   cd "${srcdir}"/idna-${pkgver}
   python setup.py test

   # Check python2 module
   cd "${srcdir}"/idna-${pkgver}-python2
   python2 setup.py test
}
 
package_python-idna() {
   depends=('python')
 
   cd idna-${pkgver}
   python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
   install -Dm644 LICENSE.rst "$pkgdir/usr/share/licenses/$pkgname/LICENSE.rst"
}
 
package_python2-idna() {
   depends=('python2')
 
   cd idna-${pkgver}-python2
   python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
   install -Dm644 LICENSE.rst "$pkgdir/usr/share/licenses/$pkgname/LICENSE.rst"
}
