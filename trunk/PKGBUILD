# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias [tobias at archlinux.org]

pkgname=mousepad
pkgver=0.4.0
pkgrel=3
pkgdesc="Simple text editor for Xfce"
arch=('x86_64')
url="http://www.xfce.org/"
license=('GPL2')
groups=('xfce4-goodies')
depends=('gtksourceview3' 'dbus-glib' 'desktop-file-utils')
makedepends=('intltool' 'perl')
source=(https://archive.xfce.org/src/apps/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=('60114431eac8db6bb6ce18bd38f1630cccb684375b97a445a1b6fd619848d132')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/xfce4 \
    --localstatedir=/var \
    --disable-static \
    --disable-debug \
    --enable-gtk3
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
