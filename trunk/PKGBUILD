# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgbase=libpst
pkgname=(libpst libpst-docs)
pkgver=0.6.76
pkgrel=3
pkgdesc="Outlook .pst file converter"
url="https://www.five-ten-sg.com/libpst/"
arch=(x86_64)
license=(GPL)
depends=(gcc-libs libgsf)
makedepends=(python boost)
source=(https://www.five-ten-sg.com/libpst/packages/${pkgbase}-${pkgver}.tar.gz)
sha256sums=('3d291beebbdb48d2b934608bc06195b641da63d2a8f5e0d386f2e9d6d05a0b42')

build() {
  cd $pkgbase-$pkgver
  ./configure --prefix=/usr --mandir=/usr/share/man \
      --disable-static --enable-libpst-shared --disable-dii
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

package_libpst() {
  optdepends=('boost-libs: for libpst python interface')
  provides=(libpst.so)

  cd $pkgbase-$pkgver
  make DESTDIR="$pkgdir" install

  cd "$pkgdir"

  _pick docs usr/share/doc
}

package_libpst-docs() {
  pkgdesc+=" (documentation)"
  depends=()
  mv docs/* "$pkgdir"
}
