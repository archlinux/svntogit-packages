# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kapidox
pkgver=5.42.0
pkgrel=1
pkgdesc='Frameworks API Documentation Tools'
arch=('any')
url='https://community.kde.org/Frameworks'
license=('LGPL')
depends=('python-jinja' 'python-yaml')
makedepends=('extra-cmake-modules' 'qt5-base')
groups=('kf5')
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha256sums=('ff0602f9a5f20380aee259fa4f2c092bad7a92ff391e9a007d0390728d69fe47'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
