# $Id: $
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdeplasma-addons
pkgver=4.2.4
pkgrel=1
pkgdesc="KDE Plasma addons"
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL')
groups=('kde')
depends=('kdebase-workspace' 'kdegraphics')
replaces=('kdeaddons' 'lancelot')
makedepends=('pkgconfig' 'cmake' 'automoc4')
options=('docs')
install='kdeplasma-addons.install'
source=("http://download.kde.org/stable/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2" 'lancelot.patch')
md5sums=('8cd684126de488203f644bb59af60ac2'
         'fa07578e1781c61b62c818955c5c5087')

build() {
	cd $srcdir/${pkgname}-${pkgver}
	# workaround for Lancelot when using Qt 4.5
	# see http://ivan.fomentgroup.org/blog/2009/03/09/patch-for-lancelot-and-qt4/
	patch -p0 -i $srcdir/lancelot.patch
	cd ../
	mkdir build
	cd build
	cmake ../${pkgname}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_{SHARED,MODULE,EXE}_LINKER_FLAGS='-Wl,--no-undefined -Wl,--as-needed' \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
	make DESTDIR=$pkgdir install
}
