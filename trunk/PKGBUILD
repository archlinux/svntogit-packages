# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: riai <riai@bigfoot.com>, Ben <ben@benmazer.net>

pkgname=sip
pkgver=4.10.1
_pkgver=4.10.1
pkgrel=1
pkgdesc="A tool that makes it easy to create Python bindings for C and C++ libraries"
arch=('i686' 'x86_64')
url="http://www.riverbankcomputing.com/software/sip/"
license=('custom:"sip"')
depends=('python' 'gcc-libs')
source=(http://riverbankcomputing.com/static/Downloads/${pkgname}4/${pkgname}-${_pkgver}.tar.gz)
md5sums=('9fa0b0d17ad355bde004317f67c819f9')

build() {
  cd ${srcdir}/${pkgname}-${_pkgver}

  python configure.py -b /usr/bin \
    -d /usr/lib/python2.6/site-packages \
    -e /usr/include/python2.6 \
    -v /usr/share/sip || return 1

  make || return 1
}

package() {
  cd ${srcdir}/${pkgname}-${_pkgver}
  make DESTDIR=${pkgdir} install

  install -Dm644 LICENSE \
    ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE || return 1
}
