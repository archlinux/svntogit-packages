# $Id: PKGBUILD 260933 2016-03-05 09:31:35Z andyrtr $
# Maintainer: AndyRTR <andyrtr@archlinux.org>

pkgname=libcmis
pkgver=0.5.1
pkgrel=1
pkgdesc="a C/C++ client library for the CMIS protocol"
arch=('x86_64' 'i686')
url="https://github.com/tdf/libcmis"
license=('GPL2' 'LGPL2.1' 'MPL')
depends=('boost-libs' 'curl' 'libxml2')
makedepends=('docbook2x' 'cppunit' 'boost')
source=("https://github.com/tdf/libcmis/releases/download/v${pkgver}/$pkgname-$pkgver.tar.gz")
md5sums=('3270154f0f40d86fce849b161f914101')
validpgpkeys=()

build() {
	cd "$pkgname-$pkgver"
	./configure --prefix=/usr DOCBOOK2MAN='docbook2man'
	make
}

check() {
	cd "$pkgname-$pkgver"
	make check
}

package() {
	cd "$pkgname-$pkgver"
	make DESTDIR="$pkgdir/" install
}
