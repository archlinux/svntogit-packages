# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=farstream
pkgver=0.2.9
pkgrel=1
pkgdesc="Farstream (formerly Farsight) - Audio/Video Communications Framework"
arch=('x86_64')
url="https://www.freedesktop.org/wiki/Software/Farstream"
license=('LGPL')
depends=('gst-plugins-base-libs' 'libnice')
makedepends=('gobject-introspection' 'python' 'gtk-doc')
optdepends=('gst-plugins-good' 'gst-plugins-bad')
conflicts=('farsight2')
replaces=('farsight2')
source=(https://freedesktop.org/software/$pkgname/releases/$pkgname/$pkgname-$pkgver.tar.gz{,.asc}
        farstream-make-4.3.patch::"https://gitlab.freedesktop.org/farstream/farstream/-/commit/54987d44.patch")
sha256sums=('cb7d112433cf7c2e37a8ec918fb24f0ea5cb293cfa1002488e431de26482f47b'
            'SKIP'
            '39485c842498c57a0b88ced4897e4ac216426cdd8a8416231513af1513208f5c')
validpgpkeys=('DB30B086FAF86CE7296FADC51D388E5A4ED9A2BB')  # Olivier CrÃªte

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../farstream-make-4.3.patch # Fix build with make 4.3
  ./autogen.sh
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
    --enable-gtk-doc \
    --with-package-name='Arch Linux farstream package' \
    --with-package-origin='http://archlinux.org'
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
