# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=falkon
pkgver=3.1.0
pkgrel=10
pkgdesc="Cross-platform QtWebEngine browser"
arch=(x86_64)
url="https://www.falkon.org/"
license=(GPL3)
depends=(qt5-webengine qt5-x11extras qt5-svg)
makedepends=(extra-cmake-modules qt5-tools kio purpose libgnome-keyring pyside2 shiboken2)
optdepends=('kio: KDE integration' 'purpose: KDE integration' 'libgnome-keyring: gnome keyring integration'
            'pyside2: Python plugins')
replaces=(qupzilla)
source=(https://download.kde.org/stable/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz{,.sig}
        falkon-pyside-5.12.2.patch::"https://cgit.kde.org/falkon.git/patch/?id=479933e0"
        falkon-qt-5.14.patch::"https://cgit.kde.org/falkon.git/patch/?id=bbde5c69")
sha256sums=('ce743cd80c0e2d525a784e29c9b487f73480119b0567f9ce8ef1f44cca527587'
            'SKIP'
            'ab7b8c1a8e0e53cb3db2722498f55cb39103d2138f4cf71859384549544074d6'
            'd5a8bb8b77087ffd7ca73c004afe396333966145744d021395b196e020b1f0ae')
validpgpkeys=(3CB7B8416EF6778B409FDC43EBC3FC294452C6D8) # David Rosca <nowrep@gmail.com>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../falkon-pyside-5.12.2.patch # Fix build with PySide 5.12.2
  patch -p1 -i ../falkon-qt-5.14.patch # Fix build with Qt 5.14
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
