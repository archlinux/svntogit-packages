# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=colord
pkgver=1.0.2
pkgrel=2
pkgdesc="System daemon for managing color devices"
arch=(i686 x86_64)
url="http://www.freedesktop.org/software/colord"
license=(GPL2)
depends=(lcms2 libgusb polkit shared-color-profiles sqlite udev dconf dbus)
makedepends=(intltool gobject-introspection vala docbook2x sane bash-completion)
optdepends=('sane: UseSANE support [/etc/colord.conf]')
install=colord.install
backup=(etc/colord.conf)
source=($url/releases/$pkgname-$pkgver.tar.xz
        udev-systemd.patch)
sha1sums=('081ce4457e9752c726e68820b1b079ab314093a0'
          '3b467b9674f767c804943d191beaf6bd1ba3a606')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../udev-systemd.patch
}

build() {
  cd $pkgname-$pkgver
  
  # put udev files in /usr/lib
  sed -i "/slashlibdir=/s#/lib#/usr/lib#" configure
  
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib/$pkgname --disable-static \
    --with-systemdsystemunitdir=/usr/lib/systemd/system \
    --enable-vala --enable-sane --disable-volume-search
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
