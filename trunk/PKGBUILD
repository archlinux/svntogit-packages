# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=adobe-source-sans-fonts
pkgver=3.052
pkgrel=1
pkgdesc="Sans-serif font family for user interface environments"
url="https://adobe-fonts.github.io/source-sans/"
arch=(any)
license=(custom)
provides=("adobe-source-sans-pro-fonts=$pkgver")
replaces=('adobe-source-sans-pro-fonts<=3.028-1')
_relver=${pkgver}R
_tarname=source-sans-$_relver
_oldver=3.006R
_oldname=source-sans-$_oldver
source=(
  "$_tarname.tar.gz::https://github.com/adobe-fonts/source-sans/archive/refs/tags/$_relver.tar.gz"
  "$_oldname.tar.gz::https://github.com/adobe-fonts/source-sans/archive/refs/tags/$_oldver.tar.gz"
)
b2sums=('7cb3965a4172c467f2fd536b1ea96d65bdb41c8546baa9f318adedca217601762cf506d050afd2e9f4e4fb840282734254cec484ac40178e62d9539308d56ddd'
        '26c700209ae1bb34bc4328e32e06437e078bd1ddeae771ff0f2ff1247dcca44b7fb39c8f2d1388913cc6c42507b37f71b398aa4c5b2a342aa206cbd044000a30')

package() {
  # "Source Sans Pro"
  cd $_oldname
  install -Dt "$pkgdir/usr/share/fonts/${pkgname%-fonts}" -m644 OTF/*.otf VAR/*.otf
  cd ..

  # "Source Sans 3"
  cd $_tarname
  install -Dt "$pkgdir/usr/share/fonts/${pkgname%-fonts}" -m644 OTF/*.otf VF/*.otf
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 LICENSE.md
}

# vim:set sw=2 sts=-1 et:
