# $Id$
# Maintainer:
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Allan McRae <mcrae_allan@hotmail.com>
# Contributor: lp76 <l.peduto@gmail.com>

pkgname=gtk-vnc
pkgver=0.4.4
pkgrel=1
pkgdesc="A VNC viewer widget for GTK"
arch=('i686' 'x86_64')
url="http://live.gnome.org/gtk-vnc"
license=('LGPL')
depends=('libgcrypt' 'gtk3')
optdepends=('gtk2: GTK2 widget')
makedepends=('intltool' 'pygtk' 'pygobject2-devel' 'gtk-doc' 'gtk2' 'vala' 'gobject-introspection')
options=('!libtool')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('c93fdb53c78b52a1cfa067c1a0be0b93d4adfc17ccea1215f20eeab2818308b7')

build() {
  cd "$srcdir"
  cp -a $pkgname-$pkgver gtk3-build

  ( cd $pkgname-$pkgver
    PYTHON=/usr/bin/python2 ./configure --prefix=/usr \
      --with-python --disable-static --with-gtk=2.0
    make )

  ( cd gtk3-build
    PYTHON=/usr/bin/python2 ./configure --prefix=/usr \
      --with-python --with-examples --disable-static --with-gtk=3.0
    make )
}

package() {
  cd "${srcdir}"
  make -C $pkgname-$pkgver -j1 DESTDIR="${pkgdir}" install
  make -C gtk3-build -j1 DESTDIR="${pkgdir}" install
}
