# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=('python-mako' 'python2-mako')
pkgver=1.0.3
pkgrel=1
pkgdesc="Hyperfast and lightweight templating for the Python2 platform"
arch=('any')
url="http://www.makotemplates.org/"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'python-markupsafe' 'python2-markupsafe'
             'python-beaker' 'python2-beaker')
checkdepends=('python-nose' 'python2-nose' 'python-mock' 'python2-mock'
              'python-babel' 'python2-babel' 'python-pygments' 'python2-pygments'
              'python-dogpile.cache' 'python2-dogpile.cache')
source=(https://pypi.python.org/packages/source/M/Mako/Mako-$pkgver.tar.gz{,.asc})
sha1sums=('d68636bc4b2d4c4030c936d88c4e05a8080b4463'
          'SKIP')
validpgpkeys=('83AF7ACE251C13E6BB7DEFBD330239C1C4DAFEE1')  # Michael Bayer

prepare() {
    cp -a Mako-$pkgver{,-py2}
}

build() {
    cd "$srcdir/Mako-$pkgver"
    python setup.py build

    cd "$srcdir/Mako-$pkgver-py2"
    python2 setup.py build
}

check() {
    cd "$srcdir/Mako-$pkgver"
    python setup.py test

    cd "$srcdir/Mako-$pkgver-py2"
    python2 setup.py test
}

package_python-mako() {
    depends=('python-markupsafe' 'python-beaker' 'python-setuptools')

    cd Mako-$pkgver
    python setup.py install --root="$pkgdir" --optimize=1
    install -D LICENSE "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

package_python2-mako() {
    depends=('python2-markupsafe' 'python2-beaker' 'python2-setuptools')

    cd Mako-$pkgver-py2
    python2 setup.py install --root="$pkgdir" --optimize=1
    install -D LICENSE "$pkgdir/usr/share/licenses/$pkgname/COPYING"

    mv "$pkgdir/usr/bin/mako-render"{,2}
}
