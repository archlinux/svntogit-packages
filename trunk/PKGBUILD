# $Id$
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=audacious
pkgver=2.3
pkgrel=1
pkgdesc="Media player based on BMP"
arch=('i686' 'x86_64')
url="http://audacious-media-player.org/"
license=('GPL3')
depends=('gtk2' 'libmcs' 'dbus-glib' 'libxml2' 'desktop-file-utils' 'audacious-plugins')
optdepends=('unzip: Zipped skins support')
provides=('audacious-player')
replaces=('audacious-player')
install=audacious.install
source=(http://distfiles.atheme.org/$pkgname-$pkgver.tgz)
sha256sums=('b19b3a75d4bac535231a77b6f993519b599a9619ac46c4d5627fd2bc8337416f')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  if [[ $CARCH == "i686" ]]; then
    ./configure --prefix=/usr \
      --disable-sse2 || return 1
  elif [[ $CARCH == "x86_64" ]]; then
    ./configure --prefix=/usr  || return 1
  fi

  make || return 1
  make DESTDIR="$pkgdir" install || return 1

  #fix for: tarball installs softlinks that point into buildroot
  rm -f "$pkgdir/usr/bin/audtool"
  ln -s /usr/bin/audtool2 "$pkgdir/usr/bin/audtool"

  rm -f "$pkgdir/usr/bin/audacious"
  ln -s /usr/bin/audacious2 "$pkgdir/usr/bin/audacious"
}
