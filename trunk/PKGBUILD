# $Id$
# Maintainer: Tom Gundersen <teg@jklm.no>
# Contributor : RÃ©my Oudompheng <remy@archlinux.org>
# Contributor: Morgan LEFIEUX <comete@archlinuxfr.org>

pkgname=hplip
pkgver=3.12.4
pkgrel=2
pkgdesc="Drivers for HP DeskJet, OfficeJet, Photosmart, Business Inkjet and some LaserJet"
arch=('i686' 'x86_64')
url="http://hplipopensource.com"
license=('GPL')
depends=('python2' 'ghostscript>=8.64-6' 'foomatic-db' 'foomatic-db-engine'
	 'net-snmp>=5.7.1')
makedepends=('python2-pyqt' 'python2-gobject2' 'sane' 'rpcbind' 'cups')
optdepends=('cups: for printing support'
            'dbus-python: for dbus support'
            'sane: for scanner support'
            'python-imaging: for commandline scanning support'
            'python-notify: for Desktop notification support'
            'rpcbind: for network support'
            'python2-pyqt: for running hp-toolbox'
            'python2-gobject2: for running hp-toolbox'
            'hplip-plugin: binary blobs for specific devices')
options=('!libtool')
install=hplip.install
source=(http://downloads.sourceforge.net/${pkgname}/$pkgname-$pkgver.tar.gz{,.asc})
md5sums=('a063f76aa47edab55a3f31ff2558df07'
         '2fe18f4091afb3dec200271dc7836128')

build() {
 cd "$srcdir/$pkgname-$pkgver"
 export PYTHON=python2
 find . -type f -exec sed -i 's~^#.*env python~#!/usr/bin/env python2~' {} +
 sed -i '1s|#!/usr/bin/python|#!/usr/bin/python2|' base/magic.py
 sed -i 's|python ./print.py|python2 ./print.py|' scan.py
 sed -i 's|python ./testpage.py|python2 ./testpage.py|' setup.py
 sed -i 's|python ./setup.py|python2 ./setup.py|' ui4/devmgr5.py ui4/nodevicesdialog.py 
 sed -i 's|python %HOME%|python2 %HOME%|' base/utils.py
 sed -i 's|python ./plugin.py|python2 ./plugin.py|' base/pkit.py

 export AUTOMAKE='automake --foreign'
 autoreconf --force --install

 ./configure --prefix=/usr \
             --enable-qt4 \
             --disable-foomatic-rip-hplip-install \
             --enable-foomatic-ppd-install \
             --enable-hpcups-install \
             --enable-new-hpcups \
             --enable-cups-ppd-install \
             --enable-cups-drv-install \
             --enable-hpijs-install \
             --enable-foomatic-drv-install \
             --enable-pp-build \
             --enable-udev-acl-rules

 make
}

package() {
 cd "$srcdir/$pkgname-$pkgver"
 make rulesdir=/lib/udev/rules.d DESTDIR="$pkgdir/" install
 # remove config provided by sane and autostart of hp-daemon
 rm -rf "$pkgdir"/etc/{sane.d,xdg}
 # remove HAL .fdi file because HAL is no longer used
 rm -rf "$pkgdir"/usr/share/hal
 # move to /usr
 mv "$pkgdir"/lib/udev "$pkgdir"/usr/lib/
 rm "$pkgdir"/lib -r
}
