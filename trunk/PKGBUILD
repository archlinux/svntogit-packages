# $Id$
# Maintainer: Paul Mattal <paul@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=dnsmasq
pkgver=2.48
pkgrel=1
pkgdesc="Lightweight, easy to configure DNS forwarder and DHCP server"
url="http://www.thekelleys.org.uk/dnsmasq/doc.html"
arch=('i686' 'x86_64')
license=('GPL')
depends=('glibc' 'bash')
makedepends=('libcap>=2.16')
backup=('etc/dnsmasq.conf')
install=$pkgname.install
source=("http://www.thekelleys.org.uk/$pkgname/$pkgname-$pkgver.tar.gz" \
        'rc.dnsmasq' "$pkgname-capability-config.patch")
md5sums=('8e12a386831b0b597c8a078f31d3fbd5' 'e956eee1ec24212a3254d2b168187495'\
         'ef61cd6c3ce1fd0767a98b02975c9c9c')
sha1sums=('472ef663b2b433d599f62cb6ca03eb6f7ce25bc2' \
          'd9884c231cc66513d9a478f67ec9a7ad3301e1f8' \
          '2db1fc4274dbcac6b711105ae598e4cb2f140d6b')

build() {
  cd $srcdir/$pkgname-$pkgver

  # Patch to add a Note about Capability Module in the Config File
  patch -Np0 -i ../$pkgname-capability-config.patch || return 1

  # Build
  make || return 1

  # Install
  install -d $pkgdir/etc/rc.d || return 1
  make PREFIX=/usr DESTDIR=$pkgdir install || return 1

  install -D -m755 \
    $srcdir/rc.dnsmasq $pkgdir/etc/rc.d/dnsmasq || return 1
  install -D -m644 \
    dnsmasq.conf.example $pkgdir/etc/dnsmasq.conf || return 1

  # Make the Required Directories for DHCP Server Default Directories
  install -d $pkgdir/var/db || return 1
  install -d $pkgdir/var/lib/misc || return 1
}

# vim: ts=2 sw=2 et ft=sh
