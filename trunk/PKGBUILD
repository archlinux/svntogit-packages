# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Sarah Hay <sarah@archlinux.org>
# Contributor: Arjan Timmerman <arjan@soulfly.nl>

pkgname=perl-extutils-depends
pkgver=0.8001
pkgrel=3
pkgdesc="The Perl depends module"
arch=('any')
url="https://search.cpan.org/dist/ExtUtils-Depends/"
license=('PerlArtistic')
depends=('perl')
options=('!emptydirs')
source=("https://www.cpan.org/authors/id/X/XA/XAOC/ExtUtils-Depends-$pkgver.tar.gz")
sha512sums=('80141f90fbde7c62b192a55d40d26ce4e442150287ae6fe200388dc84cedba5f5210ee6cd9a40b1a9cda0cc49815b1ba1ab35e75afaa7440de3c3577d01bb733')

build() {
  cd ExtUtils-Depends-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd ExtUtils-Depends-$pkgver
  make test
}

package() {
  cd ExtUtils-Depends-$pkgver
  make DESTDIR="$pkgdir" install
}
