# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-xcalc
pkgver=1.1.1
pkgrel=1
pkgdesc="Scientific calculator for X"
arch=('x86_64')
url="https://cgit.freedesktop.org/xorg/app/xcalc/"
license=('custom')
depends=('libxaw')
optdepends=('xorg-fonts-100dpi: for some symbols or'
           'xorg-fonts-75dpi: for some symbols')
makedepends=('xorg-util-macros')
source=(https://xorg.freedesktop.org/archive/individual/app/xcalc-${pkgver}.tar.xz{,.sig})
sha512sums=('a261e9798d62727f40b9aff07e68e63ba40aa7f4f9b1811d8efd8c3e73cd5912d6178c55ca3c059968191051bd20b889b06b5e92b3df70c7b5d09bd013b9ebdb'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # Alan Coopersmith <alan.coopersmith@oracle.com>

build() {
  cd xcalc-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd xcalc-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
