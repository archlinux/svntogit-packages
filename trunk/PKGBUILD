# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>

pkgname=attr
pkgver=2.5.0
pkgrel=1
pkgdesc='Extended attribute support library for ACL support'
arch=('x86_64')
url='https://savannah.nongnu.org/projects/attr'
license=('LGPL')
depends=('glibc')
makedepends=('gettext')
replaces=('xfsattr')
provides=('xfsattr' 'libattr.so')
conflicts=('xfsattr')
backup=('etc/xattr.conf')
validpgpkeys=('600CD204FBCEA418BD2CA74F154343260542DF34'  # Brandon Philips <brandon@ifup.co>
              'B902B5271325F892AC251AD441633B9FE837F581'  # Frysinger <vapier@gentoo.org>
              '259B3792B3D6D319212CC4DCD5BF9FEB0313653A') # Andreas Gruenbacher <andreas.gruenbacher@gmail.com>
source=("https://download.savannah.gnu.org/releases/${pkgname}/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha256sums=('af77acc2d4fc1346469071b9610d4454c66a4d4de92c1be6ea66b0908ec67ba5'
            'SKIP')

build() {
  cd $pkgname-$pkgver

  ./configure \
    --libdir=/usr/lib \
    --libexecdir=/usr/lib \
    --prefix=/usr \
    --sysconfdir=/etc
  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="${pkgdir}" install
}
