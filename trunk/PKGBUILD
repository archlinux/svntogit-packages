# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=libwmf
pkgver=0.2.12
pkgrel=3
pkgdesc="A library for reading vector images in Microsoft's native Windows Metafile Format (WMF)"
arch=('x86_64')
url="http://wvware.sourceforge.net/libwmf.html"
license=('LGPL')
depends=('libx11' 'libjpeg' 'gsfonts' 'freetype2' 'expat')
makedepends=('gtk2' 'libxt')
optdepends=('gdk-pixbuf2: for pixbuf loader')
options=('!docs' '!emptydirs')
source=($pkgname-$pkgver.tar.gz::https://github.com/caolanm/libwmf/archive/v$pkgver.tar.gz libwmf-freetype.patch)
sha256sums=('464ff63605d7eaf61a4a12dbd420f7a41a4d854675d8caf37729f5bc744820e2'
            'c3b25a70ba55893fdd7637893a3852894260ed94c029f2b3916fc3df3fde32b8')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i ../libwmf-freetype.patch # Port away from freetype-config, patch from openembedded.org
  autoreconf -vif -Ipatches
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr \
              --with-gsfontdir=/usr/share/fonts/Type1 \
	      --with-fontdir=/usr/share/fonts/Type1 \
	      --with-gsfontmap=/usr/share/ghostscript/9.10/Resource/Init/Fontmap.GS
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install -j1
  #Remove fonts, these are in gsfonts
  rm -rf "${pkgdir}/usr/share/fonts"
}
