# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josh Taylor <joshtaylor.mail@gmail.com>
# Contributor: simo <simo@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgbase=pyopengl
pkgname=('python2-opengl' 'python-opengl')
pkgver=3.1.5
pkgrel=1
pkgdesc="The cross platform Python binding to OpenGL and related APIs"
url="http://pyopengl.sourceforge.net/"
license=('BSD')
arch=('any')
makedepends=('freeglut' 'python2-setuptools' 'python-setuptools')
source=("https://pypi.python.org/packages/source/P/PyOpenGL/PyOpenGL-${pkgver}.tar.gz"
        'LICENSE')
sha512sums=('5d90fc68040b4886e46ff69a59be9e3a8e31e3e1295283756db86d50a1efe681986928e18eb55d20fd2660f863653301ea483425bbe24046f55085b44f4906eb'
            '84e8b6e984aa53ffdce33df2f4ee8993887bdb8752617d7f66b2ff5641ca16f9c3a13b383a9bb3236192b3af9518a3f3de1e7ac9f1496c8704911bb4238d091d')

prepare() {
    cp -a PyOpenGL-${pkgver} python-PyOpenGL-${pkgver}

    # Fix the shebang
    sed -i 's|#!/usr/bin/env python|#!/usr/bin/python2|' \
        PyOpenGL-${pkgver}/OpenGL/Tk/__init__.py
    sed -i 's|#! /usr/bin/env python|#!/usr/bin/python2|' \
        PyOpenGL-${pkgver}/OpenGL/arrays/{_,}buffers.py
}

package_python2-opengl() {
    depends=('python2' 'freeglut')
	conflicts=('python-pyopengl' 'python-opengl<=3.0.1-4')
	replaces=('python-pyopengl' 'python-opengl<=3.0.1-4')

    cd PyOpenGL-${pkgver}
	python2 setup.py install --root="${pkgdir}" --optimize=1
	install -Dm644 "${srcdir}/LICENSE" \
		"${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python-opengl() {
    depends=('python' 'freeglut')
 
    cd python-PyOpenGL-${pkgver}
 	python setup.py install --root="${pkgdir}" --optimize=1
	install -Dm644 "${srcdir}/LICENSE" \
		"${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

