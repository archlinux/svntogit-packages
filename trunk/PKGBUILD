# $Id$
# Maintainer:
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Paul Mattal <paul@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=ncftp
pkgver=3.2.5
pkgrel=5
pkgdesc="A set of free application programs implementing FTP"
url="http://www.ncftp.com/"
arch=('i686' 'x86_64')
license=('custom')
depends=('ncurses')
makedepends=('inetutils')
source=("ftp://ftp.ncftp.com/ncftp/$pkgname-$pkgver-src.tar.bz2"
        'fix-build.patch')
md5sums=('b05c7a6d5269c04891f02f43d4312b30'
         'f85fd4cc66b66952d67f864137f44029')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i "${srcdir}"/fix-build.patch
}

build() {
  # FS#39211
  unset CPPFLAGS

  cd ${pkgname}-${pkgver}
  CPP=/usr/bin/cpp ./configure --prefix=/usr \
    --sysconfdir=/etc
  make
}

package() {
  cd ${pkgname}-${pkgver}
  install -d "${pkgdir}/usr/share"
  make prefix="${pkgdir}/usr" mandir="${pkgdir}/usr/share/man" install

  # Install license
  install -Dm644 doc/LICENSE.txt \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
