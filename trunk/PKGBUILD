# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: dorphell <dorphell@gmx.net>

pkgname=moc
pkgver=2.5.0_alpha4
pkgrel=2
pkgdesc="An ncurses console audio player with support for the mp3, ogg, and wave formats"
arch=('i686' 'x86_64')
url="http://moc.daper.net/"
license=('GPL')
depends=('libmad' 'libid3tag' 'jack-audio-connection-kit' 'curl' 'libsamplerate' 'libtool')
makedepends=('speex' 'ffmpeg' 'taglib' 'libmpcdec' 'wavpack' 'libmodplug')
optdepends=('speex: for using the speex plugin' 'ffmpeg: for using the ffmpeg plugin' \
            'taglib: for using the musepack plugin' 'libmpcdec: for using the musepack plugin' \
            'wavpack: for using the wavpack plugin' 'libmodplug: for using the modplug plugin')
options=('!libtool')
source=(ftp://ftp.daper.net/pub/soft/moc/unstable/${pkgname}-${pkgver/_/-}.tar.bz2)
md5sums=('2dd0cb6481d77fda09c58ec3154a21c0')
sha1sums=('ca55b3bed0f3f3f352ca43078208ab63e20f40fc')

build() {
  cd "${srcdir}/${pkgname}-${pkgver/_/-}"
  sed -i 's|ffmpeg/avformat.h|libavformat/avformat.h|' decoder_plugins/ffmpeg/ffmpeg.c || return 1
# Disabling aac to use the external ffmpeg to play them (FS#13164)
  ./configure --prefix=/usr --without-rcc --without-aac \
    --with-oss  --with-alsa  --with-jack \
    --with-mp3 --with-musepack --with-vorbis --with-flac --with-wavpack --with-sndfile  \
    --with-modplug --with-ffmpeg --with-speex --with-samplerate --with-curl || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
}
