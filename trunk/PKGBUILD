# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=akonadi
pkgname=(akonadi libakonadi)
pkgver=19.04.3
pkgrel=2
pkgdesc="PIM layer, which provides an asynchronous API to access all kind of PIM data"
arch=(x86_64)
url='https://kontact.kde.org'
license=(LGPL)
makedepends=(extra-cmake-modules postgresql qt5-tools boost kdesignerplugin kio kitemmodels)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
         kdebug-409753.patch::"https://cgit.kde.org/akonadi.git/patch/?id=8b8db29d")
sha256sums=('7ac34dc7c3e2bf8e53ec9eebeecc8d96541c0b663dd71963e6bcd94e7ac41b07'
            'SKIP'
            'a09bc755c19a921ed80895da68325c7bf45f7e9c3ae0605c542111441d3dee22')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../kdebug-409753.patch # https://bugs.kde.org/show_bug.cgi?id=409753
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package_libakonadi() {
  pkgdesc='Libraries used by applications based on Akonadi'
  depends=(kio kitemmodels hicolor-icon-theme)

  cd build
  make DESTDIR="$pkgdir" install
  rm -r "$pkgdir"/usr/bin # Provided by akonadi
}

package_akonadi() {
  depends=(libakonadi mariadb)
  optdepends=('postgresql: PostgreSQL backend')

  cd build
  make DESTDIR="$pkgdir" install
  rm -r "$pkgdir"/{etc,usr/{include,lib,share}} # Provided by libakonadi
}
