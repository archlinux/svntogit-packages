# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=alsa-topology-conf
pkgver=1.2.4
pkgrel=1
pkgdesc="ALSA topology configuration files"
url="https://alsa-project.org/"
arch=('any')
license=('BSD')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/alsa-project/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('cfdfdb74e462450b97407b628b5bd607e4a32ecb278cf34d4f8bdbe2871e41ed8b466c8d3f76320fcd044917ae046e56be14754cc4f8013e0205673b11b43cd3')
b2sums=('7a932daf289c0ab479b111a58c5e4d5e6e896080bac818ad536646d320f9ac365343fa12c7e34d9331db4172415461942b0be2f4750e7d67892f0bc057d0cf53')

package() {
  cd "${pkgname}-${pkgver}"
  for dir in topology/*; do
    install -vDm 644 "${dir}"/*.conf -t "$pkgdir/usr/share/alsa/topology/$(basename $dir)"
  done
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
}
