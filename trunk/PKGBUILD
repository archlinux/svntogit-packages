# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gsound
pkgver=1.0.2
pkgrel=3
pkgdesc="Small library for playing system sounds"
url="https://wiki.gnome.org/Projects/GSound"
license=('LGPL')
arch=('x86_64')
depends=(glib2 libcanberra)
makedepends=(gobject-introspection vala)
source=(https://download.gnome.org/sources/$pkgname/${pkgver:0:3}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('bba8ff30eea815037e53bee727bbd5f0b6a2e74d452a7711b819a7c444e78e53')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-static

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
