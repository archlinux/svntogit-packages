# $Id$
# Contributer: Jan de Groot <jgc@archlinux.org>
# Contributer: Sean Middleditch <elanthis@awesomeplay.com>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=docbook-xsl
pkgver=1.75.2
pkgrel=1
pkgdesc='XML stylesheets for Docbook-xml transformations.'
arch=('any')
license=('custom')
url='http://scrollkeeper.sourceforge.net/docbook.shtml'
depends=('libxml2' 'libxslt' 'docbook-xml')
source=(http://downloads.sourceforge.net/sourceforge/docbook/docbook-xsl-${pkgver}.tar.bz2)
install=docbook-xsl.install
md5sums=('0c76a58a8e6cb5ab49f819e79917308f')

build() {
	/bin/true
}

package() {
	pkgroot=${pkgdir}/usr/share/xml/docbook/xsl-stylesheets-${pkgver}

	cd ${srcdir}/${pkgname}-${pkgver}
	install -dm755 ${pkgroot}/{common,params}
	install -m644 common/*.{xml,xsl,dtd,ent} ${pkgroot}/common/
	install -m644 params/*.xml ${pkgroot}/params/

	for fn in fo highlighting html roundtrip; do
		install -dm755 ${pkgroot}/${fn}
		install -m644 ${fn}/*.{xml,xsl} ${pkgroot}/${fn}/
	done

	for fn in eclipse htmlhelp javahelp lib manpages profiling template website xhtml; do
		install -dm755 ${pkgroot}/${fn}
		install -m644 ${fn}/*.xsl ${pkgroot}/${fn}/
	done

	install -dm755 ${pkgdir}/etc/xml
	install -m644 VERSION ${pkgroot}/
	install -Dm644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}
