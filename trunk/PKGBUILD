# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Kevin Piche <kevin@archlinux.org>
# Contributor: Manolis Tzanidakis

pkgname=perl-timedate
pkgver=2.30
pkgrel=5
pkgdesc="Date formating subroutines"
arch=('any')
license=('PerlArtistic')
url="http://search.cpan.org/dist/TimeDate/"
depends=('perl')
replaces=('timedate')
provides=('timedate')
options=('!emptydirs')
source=("https://www.cpan.org/authors/id/G/GB/GBARR/TimeDate-$pkgver.tar.gz")
md5sums=('b1d91153ac971347aee84292ed886c1c')

build() {
  cd TimeDate-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd TimeDate-$pkgver
  make test
}

package() {
  cd TimeDate-$pkgver
  make install DESTDIR="$pkgdir"
}
