# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Andrew Cook <ariscop@gmail.com>

pkgname=libbluray
pkgver=0.9.2
pkgrel=3
pkgdesc='Library to access Blu-Ray disks for video playback'
arch=('i686' 'x86_64')
url='http://www.videolan.org/developers/libbluray.html'
license=('LGPL2.1')
depends=('fontconfig' 'glibc' 'libfreetype.so' 'libxml2')
makedepends=('apache-ant')
optdepends=('java-environment: BD-J library')
provides=('libbluray.so')
source=("ftp://ftp.videolan.org/pub/videolan/libbluray/${pkgver}/libbluray-${pkgver}.tar.bz2"
        'libbluray-jdk8.patch')
sha256sums=('efc994f42d2bce6af2ce69d05ba89dbbd88bcec7aca065de094fb3a7880ce7ea'
            '11d848c931a0256989f8590b49739935ca29a63f8388af17e33379e5b839ced4')

prepare() {
  cd libbluray-${pkgver}

  patch -Np1 -i ../libbluray-jdk8.patch
}

build() {
  cd libbluray-${pkgver}

  ./configure \
    --prefix='/usr'
  make
}

package() {
  cd libbluray-${pkgver}

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
