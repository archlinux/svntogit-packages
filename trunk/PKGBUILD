# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Kevin Piche <kevin@archlinux.org>
# Contributor: Manolis Tzanidakis

pkgname=perl-timedate
_realname=TimeDate
pkgver=2.30
pkgrel=5
pkgdesc="Date formating subroutines"
arch=('any')
license=('PerlArtistic')
url="http://search.cpan.org/dist/${_realname}/"
depends=('perl')
replaces=('timedate')
provides=('timedate')
options=('!emptydirs')
source=("http://www.cpan.org/authors/id/G/GB/GBARR/${_realname}-${pkgver}.tar.gz")
md5sums=('b1d91153ac971347aee84292ed886c1c')

build() {
  cd ${_realname}-${pkgver}

  # install module in vendor directories.
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd ${_realname}-${pkgver}
  make test
}

package() {
  cd ${_realname}-${pkgver}
  make install DESTDIR="${pkgdir}"
}
