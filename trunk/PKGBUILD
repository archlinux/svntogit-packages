# $Id$
# Maintainer: judd <jvinet@zeroflux.org>
pkgname=gzip
pkgver=1.3.12
pkgrel=6
pkgdesc="GNU compression utility"
arch=(i686 x86_64)
url="http://www.gnu.org/software/gzip/"
license=('GPL')
groups=('base')
depends=('glibc' 'bash')
makedepends=('patch')
install=gzip.install
source=(ftp://ftp.gnu.org/pub/gnu/gzip/gzip-$pkgver.tar.gz
        gzip-fixutimens.patch)
md5sums=('b5bac2d21840ae077e0217bc5e4845b1'
         'cb592761476921018386031d91625153')

build() {
  cd $srcdir/$pkgname-$pkgver
  patch -Np1 -i ${startdir}/src/gzip-fixutimens.patch || return 1

  ./configure --prefix=/usr
  make || return 1
  mkdir -p $pkgdir/bin $pkgdir/usr/bin
  make prefix=$pkgdir/usr install
  cd $pkgdir/usr/bin
  mv $pkgname $pkgdir/bin
  mv gunzip zcat uncompress $pkgdir/bin
  cd $pkgdir/bin
  ln -sf $pkgname compress
  rm -f ${pkgdir}/usr/share/info/dir
}

