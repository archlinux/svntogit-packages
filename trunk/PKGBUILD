# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgbase=pygobject
pkgname=(python-gobject)
pkgver=3.44.0
pkgrel=1
pkgdesc="Python Bindings for GLib/GObject/GIO/GTK+"
url="https://wiki.gnome.org/Projects/PyGObject"
arch=(x86_64)
license=(LGPL)
depends=(
  gobject-introspection-runtime
  python
)
makedepends=(
  git
  gobject-introspection
  meson
  python-cairo
)
checkdepends=(
  gtk3
  python-pytest
  xorg-server-xvfb
)
optdepends=('cairo: Cairo bindings')
provides=("pygobject-devel=$pkgver")
conflicts=(pygobject-devel)
replaces=('pygobject-devel<=3.36.1-1')
_commit=894f8b1dd3246bad6edb75aa86f61f6d83531a33  # tags/3.44.0^0
source=(
  "git+https://gitlab.gnome.org/GNOME/pygobject.git#commit=$_commit"
  fix-test.diff
)
b2sums=('SKIP'
        '841a54a068a4a654337f84548db6c836bdb037a52bed3c45d0ba5d5d55d07b9edb5814fccaf482f78bd2a717adb654ff10e638c25dc71ead5c7307df4b5bc753')

pkgver() {
  cd pygobject
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd pygobject

  # fix tests failing due to various warnings being upgraded to criticals
  patch -Np1 -i ../fix-test.diff
}

build() {
  arch-meson pygobject build
  meson compile -C build
}

check() {
  PYTEST_ADDOPTS="-v" PYGI_TEST_VERBOSE=1 \
    xvfb-run -s '-nolisten local' \
    meson test -C build --print-errorlogs
}

package_python-gobject() {
  meson install -C build --destdir "$pkgdir"
  python -m compileall -d /usr/lib "$pkgdir/usr/lib"
  python -O -m compileall -d /usr/lib "$pkgdir/usr/lib"
}

# vim:set sw=2 sts=-1 et:
