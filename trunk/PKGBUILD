# $Id$
# Maintainer:  Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Johan Förberg <johan@forberg.se>

pkgname=zstd
pkgver=1.3.2
pkgrel=1
pkgdesc='Zstandard - Fast real-time compression algorithm'
arch=(x86_64)
url='http://www.zstd.net/'
license=(BSD GPL2)
depends=(zlib xz lz4)
makedepends=(gtest)
source=(zstd-$pkgver.tar.gz::https://github.com/facebook/zstd/archive/v${pkgver}.tar.gz)
sha1sums=('2478765996a25232abb5e38707fc77deeaaaab37')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  make
  make zstdmt
  make -C contrib/pzstd
}

check() {
  cd $pkgname-$pkgver
  make -C tests test-zstd
  make -C contrib/pzstd test
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make PREFIX=/usr DESTDIR="$pkgdir/" install
  install -Dm755 zstdmt "$pkgdir/usr/bin/zstdmt"
  install -Dm755 contrib/pzstd/pzstd "$pkgdir/usr/bin/pzstd"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
