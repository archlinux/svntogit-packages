# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=gnome-usage
pkgver=3.38.0
pkgrel=1
pkgdesc="GNOME application to view information about use of system resources"
url="https://wiki.gnome.org/Apps/Usage"
arch=(x86_64)
license=(GPL3)
depends=(gtk3 libgtop libdazzle tracker3 libhandy)
makedepends=(git meson vala)
optdepends=('accountsservice: show user tags for processes')
groups=(gnome-extra)
_commit=9f1a9b3c74ee42d2cd917aa0ef2e7938f4389003  # tags/3.38.0^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-usage.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
