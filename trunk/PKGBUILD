# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=mpfr
_pkgver=3.1.2
#_patchlevel=p1
#pkgver=${_pkgver}.${_patchlevel}
pkgver=${_pkgver}
pkgrel=1
pkgdesc="Multiple-precision floating-point library"
arch=('i686' 'x86_64')
url="http://www.mpfr.org/"
license=('LGPL')
depends=('gmp>=5.0')
options=('!libtool')
install=mpfr.install
source=(http://www.mpfr.org/mpfr-current/mpfr-${_pkgver}.tar.xz{,.asc})
md5sums=('e3d203d188b8fe60bb6578dd3152e05c'
         '63d1a00bedd4fed3717115297f70d029')

build() {
  cd "${srcdir}/${pkgname}-${_pkgver}"
  #patch -p1 -i $srcdir/mpfr-${_pkgver}-${_patchlevel}.patch
  ./configure --prefix=/usr --enable-thread-safe --enable-shared
  make
}

check() {
  cd "${srcdir}/${pkgname}-${_pkgver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${_pkgver}"
  make DESTDIR="${pkgdir}" install
}
