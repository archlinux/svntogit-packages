# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libmediaart
pkgver=1.9.4+1+ga704d0b
pkgrel=1
pkgdesc="Library tasked with managing, extracting and handling media art caches"
url="https://git.gnome.org/browse/libmediaart"
arch=(x86_64)
license=(LGPL)
depends=(gdk-pixbuf2)
makedepends=(vala gobject-introspection git gtk-doc meson)
_commit=a704d0b6cfea091274bd79aca6d15f19b4f6e5b5  # master
source=("git+https://git.gnome.org/browse/libmediaart#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  cd build
  meson test
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}
