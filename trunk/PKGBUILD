# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-xlsfonts
pkgver=1.0.5
pkgrel=1
pkgdesc='List available X fonts'
url='https://xorg.freedesktop.org/'
license=('custom')
arch=('i686' 'x86_64')
depends=('libx11')
makedepends=('xorg-util-macros')
source=(https://xorg.freedesktop.org/archive/individual/app/xlsfonts-${pkgver}.tar.bz2{,.sig})
sha256sums=('40295bec35077cee93be2e4c26cc00268ed6ace779ae6e97766a866c1e3927fa'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # Alan Coopersmith <alan.coopersmith@oracle.com>

build() {
	cd xlsfonts-${pkgver}
	./configure --prefix=/usr
	make
}

package() {
	cd xlsfonts-${pkgver}
	make DESTDIR="${pkgdir}" install
	install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
