# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgbase=mesa-demos
pkgname=('mesa-demos' 'mesa-utils')
pkgver=8.5.0
pkgrel=2
arch=('x86_64')
url="https://www.mesa3d.org/"
license=('custom')
makedepends=('meson' 'mesa' 'glu' 'freeglut' 'wayland-protocols')
source=(https://archive.mesa3d.org/demos/${pkgver}/mesa-demos-${pkgver}.tar.bz2{,.sig}
        'LICENSE'
        meson-demos_data_dir.patch)
sha512sums=('df6a7f09638ca389d6e8bc569e599dcc59c9aa280bcfb95976d83e5cd3edfe74bb3a31f0953956dc16faa540ee5b09990e3377e1341d836fc527e50ace6e60b8'
            'SKIP'
            '25da77914dded10c1f432ebcbf29941124138824ceecaf1367b3deedafaecabc082d463abcfa3d15abff59f177491472b505bcb5ba0c4a51bb6b93b4721a23c2'
            'fe5cedcb4ecee4d792ea1e3fa8724ca65b8528babd0e3207ee4a584aa27509cd7f65ed4ef08335c5e220cfb9216f2c20c3b1da1a3d7a9cce3f7192908430d8c5')
#validpgpkeys=('E390B9700582FAEA959ACAD41EEF53D38A3A9C67') # "Andreas Boll <andreas.boll.dev@gmail.com>"
validpgpkeys=('FF4CF638C624C3CF21ED2CF227EF763A2AA39F96') # Erik Faye-Lund <erik.faye-lund@collabora.com>

prepare() {
  cd ${pkgname}-${pkgver}
  # FS#75063 - https://gitlab.freedesktop.org/mesa/demos/-/issues/26
  patch -Np1 -i ../meson-demos_data_dir.patch
}

build() {

  arch-meson mesa-demos-$pkgver build \
    -D gles1=disabled \
    -D with-system-data-files=true

  # Print config
  meson configure build

  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_mesa-demos() {
  pkgdesc="Mesa demos"
  depends=('libgl' 'glu' 'freeglut')
  install=mesa-demos.install

  DESTDIR="$pkgdir" meson install -C build

  # add missing egl files
  install -m 0755 build/src/egl/opengl/{eglgears_wayland,eglgears_x11,eglkms,egltri_wayland,egltri_x11,peglgears,xeglgears,xeglthreads} "${pkgdir}/usr/bin/"

  # remove utils
  rm "${pkgdir}"/usr/bin/{glxinfo,glxgears,eglinfo}

  install -Dm 0644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_mesa-utils() {
  pkgdesc="Essential Mesa utilities"
  depends=('libgl')
  provides=('glxinfo' 'glxgears' 'eglinfo')


  install -Dm 0755 build/src/egl/opengl/eglinfo -t "${pkgdir}/usr/bin/"
  install -Dm 0755 build/src/xdemos/{glxinfo,glxgears} -t "${pkgdir}/usr/bin/"

  install -Dm 0644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
