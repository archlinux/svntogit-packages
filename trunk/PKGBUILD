# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-xbacklight
pkgver=1.2.2
pkgrel=1
pkgdesc="RandR-based backlight control application"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('xcb-util')
makedepends=('xorg-util-macros')
groups=('xorg-apps' 'xorg')
source=(https://xorg.freedesktop.org/archive/individual/app/xbacklight-${pkgver}.tar.bz2{,.sig})
sha512sums=('4dca70ef8e3494b19ea126c46395807cddc007b2b09a133810daeb77ae27fa36ac4eec7813828248fc195abe27f59700cfa58ee65ef3a020a51f73e5107678ec'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # "Alan Coopersmith <alan.coopersmith@oracle.com>"

build() {
  cd xbacklight-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd xbacklight-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
