# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=texinfo
pkgver=4.13a
pkgrel=8
pkgdesc="Utilities to work with and produce manuals, ASCII text, and on-line documentation from a single source file"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/texinfo/"
license=('GPL3')
groups=('base' 'base-devel')
depends=('ncurses' 'findutils' 'gzip' 'sh')
install=texinfo.install
source=(ftp://ftp.gnu.org/pub/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz{,.sig}
        texinfo-4.13a-data_types.patch)
md5sums=('71ba711519209b5fb583fed2b3d86fcb'
         'db2ca82c831684cd7cef181234017c46'
         'd8574782ff7def1e2dc5c291dbeaec18')

build() {
  cd ${srcdir}/${pkgname}-4.13
  patch -Np1 -i $srcdir/texinfo-4.13a-data_types.patch
  ./configure --prefix=/usr
  make
}

package() {
  cd ${srcdir}/${pkgname}-4.13
  make DESTDIR=${pkgdir} install

  # fix this here as it prevents make from trying to rebuild manual pages
  sed -i 's#A-z#A-Za-z#' ${pkgdir}/usr/bin/texi2dvi
}
