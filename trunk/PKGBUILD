# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=libwpe
pkgver=1.6.0
pkgrel=1
pkgdesc="General-purpose library for WPE WebKit"
url="https://wpewebkit.org"
arch=(x86_64)
license=(custom)
depends=(libxkbcommon gcc-libs)
makedepends=(cmake mesa)
provides=(libwpe-1.0.so)
source=($url/releases/$pkgname-$pkgver.tar.xz{,.asc})
sha256sums=('3587c6b8a807f4bb76b268ba74ca82c6b395b90235db41ad8252224456193c90'
            'SKIP')
validpgpkeys=('5AA3BC334FD7E3369E7C77B291C559DBE4C9123B') # Adrián Pérez de Castro <aperez@igalia.com>

prepare() {
  cd $pkgname-$pkgver
}

build() {
  cmake -H$pkgname-$pkgver -Bbuild \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --build build --target install
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 $pkgname-$pkgver/COPYING
}

# vim:set sw=2 et:
