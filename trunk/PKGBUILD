# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=links
pkgver=2.2
pkgrel=4
pkgdesc="A text WWW browser, similar to Lynx"
arch=('i686' 'x86_64')
url="http://links.twibright.com/"
license=('GPL')
depends=('bzip2' 'zlib' 'openssl' 'gpm')
makedepends=('pkgconfig')
changelog=ChangeLog
source=(http://links.twibright.com/download/${pkgname}-${pkgver}.tar.bz2 \
  links-2.1pre33-utf8.diff macroman.diff)
md5sums=('bf5b20529a2a811701c5af52b28ebdd4' '31ff332055d403315eb6e49a6c46aa02'\
         '78940e6106afb72b3d027af0f3765808')
sha1sums=('7588c151e98057f83a2e0b81b3f467e7eee9f824' 'f431eea4dcab0e3f3f53d06834e19c78e555b7ad'\
         '788368f04211d614a5a047e0a66a9d07998349a6')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 < ../links-2.1pre33-utf8.diff || return 1
  patch -p1 < ../macroman.diff || return 1
  (cd Unicode; ./gen) || return 1
  (cd intl; ./gen-intl) || return 1
  ./configure --prefix=/usr --mandir=/usr/share/man --enable-javascript --disable-graphics --without-x || return 1
  make || return 1
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install || return 1
}
