# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=gnome-font-viewer
pkgver=41.0
pkgrel=1
pkgdesc="A font viewer utility for GNOME"
url="https://gitlab.gnome.org/GNOME/gnome-font-viewer"
arch=(x86_64)
license=(GPL)
depends=(gtk3 gnome-desktop libhandy)
makedepends=(git meson)
groups=(gnome)
_commit=6723c64b9aef44b6c6e36e6a1c66f3d587e649ce  # tags/41.0^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-font-viewer.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
