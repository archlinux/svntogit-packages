# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: anex <assassin.anex[@]gmail.com

pkgname=signon-kwallet-extension
pkgver=20.08.2
pkgrel=1
pkgdesc="KWallet integration for signon framework"
arch=(x86_64)
url="https://kde.org"
license=(LGPL)
groups=(kde-applications kde-network)
depends=(signond kwallet)
makedepends=(extra-cmake-modules)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('a271f4d09511171dcf1ebcdaf0b7205f9ee6a75b20ef0801f36d136f7b4a70dd'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
