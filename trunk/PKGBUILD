# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Maxime Arthaud <maxime@arthaud.me>

pkgname=zxing-cpp
pkgver=1.0.4
pkgrel=1
pkgdesc="A command-line tool to decode QRCode"
arch=(x86_64)
url="https://github.com/nu-book/zxing-cpp"
license=(Apache)
depends=()
makedepends=(cmake)
optdepends=('opencv: support for opencv')
source=($pkgname-$pkgver.tar.gz::"https://github.com/nu-book/zxing-cpp/archive/v$pkgver.tar.gz")
sha256sums=('e3bb5c135523638ca875c242efb958742b4b8e02a1acdf6b182ecd85021463dd')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver/core \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_CXX_FLAGS="${CXXFLAGS} -fPIC"
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
