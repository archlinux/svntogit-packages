# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>

pkgname=bison
pkgver=3.6.2
pkgrel=1
pkgdesc="The GNU general-purpose parser generator"
arch=('x86_64')
license=('GPL3')
url="https://www.gnu.org/software/bison/bison.html"
depends=('glibc' 'm4' 'sh')
groups=('base-devel')
source=("https://ftp.gnu.org/gnu/bison/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha512sums=('69f5b6d9f28e0fe2d127a86e1315b06f3367c4d1cb667c0f0eee537fd5705d702d2a8db51204d7928db83efa83208cb5f738b27453eac6e2fd5037e8ffd0cd19'
            'SKIP')

validpgpkeys=('7DF84374B1EE1F9764BBE25D0DDCAA3278D5264E')  # Akim Demaille

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr --datadir=/usr/share
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make -j1 check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
