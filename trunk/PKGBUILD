# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=libgssglue
pkgver=0.6
pkgrel=1
pkgdesc="Library exporting a gssapi interface to call gssapi routines in other libraries"
arch=(x86_64)
url="https://gitlab.com/gsasl/libgssglue/"
license=(BSD)
depends=(glibc)
provides=(libgssglue.so)
backup=(etc/gssapi_mech.conf)
options=(debug)
source=(https://gitlab.com/gsasl/$pkgname/-/archive/$pkgname-$pkgver/$pkgname-$pkgname-$pkgver.tar.gz)
sha512sums=('b306f39b6a18047a2cacb7ba72d7445abfe3963893f7b39e992997e3c767ac574fd468431f86bafd6054217f67dbad2f4a25cc3065a956987fb1f4565be547af')
b2sums=('afdd14dff21ff742ab34991ceaec389d2fcccf78a78e89b18d3f3be64d4e85fde178ce40e5ee80c971f5126b73e2300a2f6ac73a7e3374ff0100b779262e6fdf')

prepare() {
  cd $pkgname-$pkgname-$pkgver
  touch ChangeLog
  autoreconf -fiv
}

build() {
  cd $pkgname-$pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  make DESTDIR="$pkgdir/" install -C $pkgname-$pkgname-$pkgver
  install -vDm 644 $pkgname-$pkgname-$pkgver/doc/gssapi_mech.conf -t "$pkgdir/etc/"
  install -vDm 644 $pkgname-$pkgname-$pkgver/COPYING -t "$pkgdir/usr/share/licenses/$pkgname/"
  install -vDm 644 $pkgname-$pkgname-$pkgver/{AUTHORS,README,NEWS} -t "$pkgdir/usr/share/doc/$pkgname/"
}
