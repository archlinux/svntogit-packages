# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-session
pkgver=3.24.1
pkgrel=1
pkgdesc="The GNOME Session Handler"
url="https://git.gnome.org/browse/gnome-session"
arch=(i686 x86_64)
license=(GPL LGPL)
depends=(systemd dconf gsettings-desktop-schemas gtk3 gnome-desktop
         json-glib libgl libsm libxtst systemd)
makedepends=(intltool mesa gtk-doc xtrans git gnome-common python xmlto)
groups=(gnome)
_commit=6fec14cb8802105e68116e167ad10ca68548767f  # tags/3.24.1^0
source=("git+https://git.gnome.org/browse/gnome-session#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}
  
build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
      --disable-schemas-compile --enable-systemd --disable-gconf
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
