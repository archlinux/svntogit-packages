# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=geocode-glib
pkgver=3.26.4
pkgrel=1
pkgdesc="Helper library for geocoding services"
url="https://gitlab.gnome.org/GNOME/geocode-glib"
arch=(x86_64)
license=(LGPL)
depends=(glib2 json-glib libsoup)
makedepends=(gobject-introspection git gtk-doc meson)
provides=(libgeocode-glib.so)
options=(debug)
_commit=210abe69d68e38947106f4680631c369b0c23189  # tags/3.26.4^0
source=("git+https://gitlab.gnome.org/GNOME/geocode-glib.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build -D enable-installed-tests=false
  meson compile -C build
}

check() {
  # Tests need locales we don't have: en_GB.UTF-8 fr_FR.UTF-8 cs_CZ.UTF-8
  meson test -C build --print-errorlogs || :
}

package() {
  meson install -C build --destdir "$pkgdir"
}

