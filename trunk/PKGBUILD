# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Travis Willard <travisw@wmpub.ca>
# Contributor: Georg Grabler <ggrabler@gmail.com>

pkgname=ktorrent
pkgver=4.1.2
pkgrel=1
pkgdesc="A powerful BitTorrent client for KDE"
arch=('i686' 'x86_64')
url="http://ktorrent.org"
license=('GPL2')
depends=('libktorrent' 'kdebase-workspace' 'taglib')
makedepends=('automoc4' 'cmake' 'boost')
source=("http://ktorrent.org/downloads/${pkgver}/${pkgname}-${pkgver}.tar.bz2"
        'httpserver.patch')
install=${pkgname}.install
options=('libtool')
md5sums=('8026d9f75257f2e52ac5ba3ce4720d55'
         '1f25b94615b0bbfa0457c99fb20de3e6')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	# build time patch
	patch -p0 -i "${srcdir}/httpserver.patch"

	cd "${srcdir}"
	mkdir build
	cd build
	cmake ../${pkgname}-${pkgver} \
	  -DCMAKE_BUILD_TYPE=Release \
	  -DCMAKE_INSTALL_PREFIX=/usr \
	  -DENABLE_MEDIAPLAYER_PLUGIN=true
	make
}

package() {
	cd "${srcdir}/build"
	make DESTDIR="${pkgdir}" install
}
