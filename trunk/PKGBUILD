# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=epiphany
pkgver=43.1
pkgrel=1
pkgdesc="A GNOME web browser based on the WebKit rendering engine"
url="https://wiki.gnome.org/Apps/Web"
arch=(x86_64)
license=(GPL)
depends=(
  gcr
  icu
  libarchive
  libdazzle
  libhandy
  libportal-gtk3
  libsoup3
  webkit2gtk-4.1
)
makedepends=(
  docbook-xml
  git
  gobject-introspection
  lsb-release
  meson
  startup-notification
  yelp-tools
)
checkdepends=(
  aspell
  hspell
  hunspell
  libvoikko
  nuspell
  xorg-server-xvfb
)
groups=(gnome)
_commit=e21c2e68f34fcf23ca560088479e13c9929a4c36  # tags/43.1^0
source=(
  "git+https://gitlab.gnome.org/GNOME/epiphany.git#commit=$_commit"
  fix-test.diff
)
b2sums=('SKIP'
        '05121ff5f759ec637a718c9b39406f9a384227a312d69f6dc74e73fde8351689d588a30dddea3cfe0a6edb094fdcf5382eb5ff725bb2adfccdff87ab3cf4c953')

pkgver() {
  cd epiphany
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd epiphany

  # Fix encoding test; perhaps caused by different glibc or locale env
  # https://gitlab.gnome.org/GNOME/epiphany/-/issues/1842
  git apply -3 ../fix-test.diff
}

build() {
  arch-meson epiphany build
  meson compile -C build
}

check() {
  dbus-run-session xvfb-run -s '-nolisten local' \
    meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
