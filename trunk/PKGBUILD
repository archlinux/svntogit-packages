# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: riai <riai@bigfoot.com>, Ben <ben@benmazer.net>

pkgname=sip
pkgver=4.12
pkgrel=1
pkgdesc="A tool that makes it easy to create Python bindings for C and C++ libraries"
arch=('i686' 'x86_64')
url="http://www.riverbankcomputing.com/software/sip/"
license=('custom:"sip"')
depends=('python2' 'gcc-libs')
source=("http://www.riverbankcomputing.com/static/Downloads/sip4/${pkgname}-${pkgver}.tar.gz")
md5sums=('8a0998fbaf34bdab2c15af3d4fa3bc0e')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  python2 configure.py
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
