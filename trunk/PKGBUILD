# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=ncurses
pkgver=6.0
pkgrel=1
pkgdesc='System V Release 4.0 curses emulation library'
arch=('i686' 'x86_64')
url='http://invisible-island.net/ncurses/ncurses.html'
license=('MIT')
depends=('glibc' 'gcc-libs' 'sh')
provides=('libmenu.so' 'libpanel.so' 'libform.so' 'libncurses.so' 'libncurses++w.so'
          'libformw.so' 'libmenuw.so' 'libpanelw.so' 'libncursesw.so')
source=(ftp://invisible-island.net/ncurses/ncurses-${pkgver/_/-}.tar.gz{,.asc})
md5sums=('ee13d052e1ead260d7c28071f46eefb1'
         'SKIP')
validpgpkeys=('C52048C0C0748FEE227D47A2702353E0F7E48EDB')  # Thomas Dickey

prepare() {
  mkdir ncurses{,w}-build
}

build() {
  cd ncursesw-build
  ../$pkgname-${pkgver/_/-}/configure --prefix=/usr --mandir=/usr/share/man \
     --with-shared --with-normal --without-debug --without-ada \
     --enable-widec --enable-pc-files --with-cxx-binding --with-cxx-shared \
     --enable-ext-colors --enable-ext-mouse
  make

  # libraries for external binary support
  cd ${srcdir}/ncurses-build
  [[ $CARCH = "x86_64" ]] && CONFIGFLAG="--with-chtype=long"
  ../$pkgname-${pkgver/_/-}/configure --prefix=/usr \
    --with-shared --with-normal --without-debug --without-ada $CONFIGFLAG \
    --with-cxx-binding --with-cxx-shared
  make
}

package() {
  cd ncursesw-build
  make DESTDIR="$pkgdir" install

  # fool packages looking to link to non-wide-character ncurses libraries
  for lib in ncurses ncurses++ form panel menu; do
    echo "INPUT(-l${lib}w)" > "$pkgdir"/usr/lib/lib${lib}.so
  done

  for lib in ncurses ncurses++ form panel menu; do
    ln -s ${lib}w.pc "$pkgdir"/usr/lib/pkgconfig/${lib}.pc
  done

  # some packages look for -lcurses during build
  echo "INPUT(-lncursesw)" > "$pkgdir"/usr/lib/libcursesw.so
  ln -s libncurses.so "$pkgdir"/usr/lib/libcurses.so

  # non-widec compatibility libraries
  cd "$srcdir"/ncurses-build
  for lib in ncurses form panel menu; do
    install -Dm755 lib/lib${lib}.so.${pkgver%_*} \
		"$pkgdir"/usr/lib/lib${lib}.so.${pkgver%_*}
    ln -s lib${lib}.so.${pkgver%_*} "$pkgdir"/usr/lib/lib${lib}.so.5
  done

  # install license, rip it from the readme
  cd "$srcdir"/$pkgname-${pkgver/_/-}
  install -dm755 "$pkgdir"/usr/share/licenses/$pkgname
  grep -B 100 '$Id' README > "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
