# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=libkgapi
pkgver=20.12.1
pkgrel=2
pkgdesc="A KDE-based library for accessing various Google services via their public API"
url='https://www.kde.org/'
arch=(x86_64)
license=(GPL LGPL FDL)
depends=(kcalendarcore kcontacts kwallet)
makedepends=(extra-cmake-modules qt5-tools)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
         kdebug-429406.patch::"https://invent.kde.org/pim/libkgapi/commit/d81d2470.patch")
sha256sums=('0df9a24e68d50f67e171ff5c6404891956c7fe675351bda8f8ab2a84ffa2a833'
            'SKIP'
            '28e88bd0862d7fe56f2b6a9f96b3454e73c1cafe36c813762ed963b78a89ec4f')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  patch -d $pkgname-$pkgver -p1 -i ../kdebug-429406.patch # Fix authentication prompts popping up every hour
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
