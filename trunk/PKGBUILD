# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Patryk Kowalczyk <patryk@kowalczyk.ws>
pkgname=libiscsi
pkgver=1.7.0
pkgrel=2
depends=('glibc' 'popt')
pkgdesc="Clientside library to implement the iSCSI protocol"
arch=(i686 x86_64)
url="https://github.com/sahlberg/libiscsi"
license=('LGPL')
source=(https://github.com/downloads/sahlberg/${pkgname}/${pkgname}-${pkgver}.tar.gz)
options=(!libtool)

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./autogen.sh
  ./configure --prefix=/usr --disable-static --libdir=/usr/lib 
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package () {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
md5sums=('4ba621f47d016d48ab93d5301ae5a363')
