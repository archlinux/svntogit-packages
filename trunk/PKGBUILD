# $Id$
# Maintainer: Kevin Piche <kevin@archlinux.org>
# Contributor: Aaron Griffin <aaron@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=easytag
pkgver=2.1.6
pkgrel=3
pkgdesc="Utility for viewing, editing and writing ID3 tags of your MP3 files"
arch=('i686' 'x86_64')
license=('GPL')
url="http://easytag.sourceforge.net/"
depends=('id3lib' 'libid3tag' 'gtk2' 'libvorbis' 'flac' 'libmp4v2' 'speex' 'wavpack')
install=${pkgname}.install
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.bz2"
	'mp4v2-1.9.patch')
md5sums=('6c5b9dc2bf1f3b0a11bd4efc81aaa9ee'
         '912d9baa362c4d9e5aee0436b4ccb5a2')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -Np1 -i ${srcdir}/mp4v2-1.9.patch
  ./configure --prefix=/usr
  make || return 1
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  # Remove directory mimetype
  sed '/^MimeType=/ s@;x-directory/normal;@;@' \
    -i ${pkgdir}/usr/share/applications/easytag.desktop
}
