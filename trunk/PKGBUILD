# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Justin Davis (juster) <jrcd83@gmail.com>

pkgname=perl-mozilla-ca
pkgver=20150826
pkgrel=1
pkgdesc="Mozilla's CA cert bundle in PEM format"
arch=(any)
license=(GPL2 LGPL2.1)
options=(!emptydirs)
depends=('perl')
url=https://metacpan.org/release/Mozilla-CA
source=("http://search.cpan.org/CPAN/authors/id/A/AB/ABH/Mozilla-CA-$pkgver.tar.gz")
md5sums=('52de059194004192345687f7a2f88224')

build() (
  cd Mozilla-CA-$pkgver
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  /usr/bin/perl Makefile.PL
  make
)

check() (
  cd Mozilla-CA-$pkgver
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
)

package() (
  cd Mozilla-CA-$pkgver
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
)

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
