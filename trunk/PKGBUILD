# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=libfontenc
pkgver=1.1.5
pkgrel=1
pkgdesc="X11 font encoding library"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('zlib' 'xorg-fonts-encodings')
makedepends=('pkgconfig' 'xorgproto' 'xorg-util-macros')
source=(${url}/releases/individual/lib/${pkgname}-${pkgver}.tar.xz{,.sig})
sha512sums=('1e285069012bee9742aa3608898c5c1677be42ced3b175f59de9830e626c7b7116f44da7deac5926b250145f4fdb4e8757c6901191c9f8f9e720d159d6a4c2b4'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') #Alan Coopersmith <alan.coopersmith@oracle.com>

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --with-encodingsdir=/usr/share/fonts/encodings      
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
