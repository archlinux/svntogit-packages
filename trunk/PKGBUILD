# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>

pkgname=libtorrent-rasterbar
pkgver=1.1.10
pkgrel=1
epoch=1
pkgdesc="A C++ BitTorrent library that aims to be a good alternative to all the other implementations around"
url="http://www.rasterbar.com/products/libtorrent/"
arch=('x86_64')
license=('BSD')
depends=('boost-libs')
makedepends=('boost' 'python2' 'python')
options=('!emptydirs')
_pkgver=${pkgver//./_}
source=(https://github.com/arvidn/libtorrent/archive/libtorrent-${_pkgver}/$pkgname-$pkgver.tar.gz)
sha512sums=('b811390faa99e53d1c60d7071c9e5665142ce9624fe64c82c995f4ca8944e10f3b7b1377ae40177ad8c6c81b09175183a9e110011aec0134d9ae82b4c7afdf43')

prepare() {
  mkdir py2 py3
  cd libtorrent-libtorrent-${_pkgver}

  # Avoid depending on newer processors
  sed -i 's/-msse4.2//' configure.ac

  ./autotool.sh
}

_build() (
  cd py$1

  # FS#50745
  _boost="boost_python"
  if [ $1 -eq 3 ]; then _boost="boost_python3"; fi

  # https://github.com/qbittorrent/qBittorrent/issues/5265#issuecomment-220007436
  CXXFLAGS="$CXXFLAGS -std=c++11" \
  PYTHON=/usr/bin/python$1 \
  ../libtorrent-libtorrent-${_pkgver}/configure \
    --prefix=/usr \
    --enable-python-binding \
    --enable-examples \
    --disable-static \
    --with-libiconv \
    --with-boost-python=$_boost
)

build() {
  _build 2
  _build 3
}

package() {
  make -C py2 DESTDIR="$pkgdir" install
  make -C py3 DESTDIR="$pkgdir" install
  install -Dm644 libtorrent-libtorrent-${_pkgver}/COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # Remove most example binaries
  rm "$pkgdir"/usr/bin/{*_test,*_tester,simple_client,stats_counters}
}
