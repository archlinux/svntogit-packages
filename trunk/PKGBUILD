# $Id$
# Maintainer: Dale Blount <dale@archlinux.org>
# Contributor: Manolis Tzanidakis

pkgname=spamassassin
pkgver=3.3.1
pkgrel=2
pkgdesc="A mail filter to identify spam."
arch=(i686 x86_64)
license=(APACHE)
url="http://spamassassin.apache.org"
depends=('perl>=5.10.0' 'perl-net-dns' 'perl-uri' 'perl-html-parser' \
		'perl-digest-sha1' 'perl-io-socket-ssl' 'openssl' \
		'perl-libwww' 'perl-io-zlib' 'perl-mail-spf' \
		'perl-archive-tar' 'zlib' 're2c')
makedepends=('razor' 'perl-dbi') 
optdepends=('razor: to identify collaborately-flagged spam'
	    'gnupg: gpg support, without this use --nogpg flag for sa-update'
            're2c: regexp compiler, for compiling rules with sa-compile')
backup=(etc/conf.d/spamd etc/mail/spamassassin/local.cf \
	etc/mail/spamassassin/init.pre \
	etc/mail/spamassassin/v310.pre \
	etc/mail/spamassassin/v312.pre \
	etc/mail/spamassassin/v320.pre)
install=${pkgname}.install
options=(!emptydirs)
source=(http://mirrors.devlib.org/apache/${pkgname}/source/Mail-SpamAssassin-${pkgver}.tar.gz \
		spamd.conf.d spamd)
md5sums=('5a93f81fda315411560ff5da099382d2'
         'af501c6b0bb937a2775b747cc4b6d150'
         'db5c79722722e56678401ae454b28ece')

build() {
  cd ${srcdir}/Mail-SpamAssassin-${pkgver}

  # install module in vendor directories.
  PERL_MM_USE_DEFAULT=1 /usr/bin/perl Makefile.PL INSTALLDIRS=vendor \
      CONTACT_ADDRESS=root@localhost ENABLE_SSL=yes PERL_TAINT=no
  /usr/bin/make || return 1
  /usr/bin/make DESTDIR=${pkgdir} install || return 1

  # remove perllocal.pod and .packlist
  find ${pkgdir} -name perllocal.pod -delete
  find ${pkgdir} -name .packlist -delete

  /bin/install -D -m644 ${srcdir}/spamd.conf.d ${pkgdir}/etc/conf.d/spamd
  /bin/install -D -m755 ${srcdir}/spamd ${pkgdir}/etc/rc.d/spamd
}
