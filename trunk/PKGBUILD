# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=claws-mail
pkgver=3.7.1
pkgrel=1
pkgdesc="A GTK+ based e-mail client."
arch=('i686' 'x86_64')
license=('GPL3')
url="http://www.claws-mail.org"
depends=('gtk2>=2.14.7' 'gnutls>=2.6.4' 'startup-notification>=0.9-1' 'pilot-link>=0.12.3-4' 'enchant>=1.4.2-1'
         'gpgme>=1.1.7-2' 'libetpan>=0.57-1' 'libsm>=1.1.0-' 'db>=4.7' 'curl>=7.19.4' 'dbus-glib>=0.78')
makedepends=('compface' 'spamassassin' 'bogofilter' 'valgrind')
optdepends=('python:            needed for some tools'
            'perl:              needed for some tools'
            'spamassassin:      adds support for spamfiltering'
            'bogofilter:        adds support for spamfiltering')
replaces=('sylpheed-claws')
provides=('claws')
options=(!libtool)
install=claws-mail.install
source=(http://downloads.sourceforge.net/sourceforge/sylpheed-claws/${pkgname}-${pkgver}.tar.bz2)
md5sums=('8351ba15b04c1388d772ba52e41a8643')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr --disable-static \
    --enable-enchant \
    --enable-gnutls \
    --enable-ldap \
    --disable-dillo-viewer-plugin \
    --enable-crash-dialog \
    --enable-pgpmime-plugin \
    --enable-spamassassin-plugin \
    --enable-bogofilter-plugin \
    --enable-jpilot

  make || return 1
  make DESTDIR=${pkgdir} install

  # build and install extra tools
  cd tools
  make
   # all executables and .conf files ; only top directory
  find -maxdepth 1 -type f -and -perm /111 -or -name '*.conf' | while read i ; do
      install -D -m755 ${i} \
        ${pkgdir}/usr/lib/claws-mail/tools/${i}
  done
}
