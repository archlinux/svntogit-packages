# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Patrick Griffis <tingping@tingping.se>

pkgname=xdg-desktop-portal-gtk
pkgver=1.7.1
pkgrel=1
pkgdesc="A GTK+ backend for xdg-desktop-portal"
url="https://github.com/flatpak/xdg-desktop-portal-gtk"
arch=(x86_64)
license=(LGPL2.1)
depends=(gtk3 gnome-desktop gnome-keyring)
makedepends=(xdg-desktop-portal python git evince)
optdepends=("evince: Print preview")
provides=(xdg-desktop-portal-impl)
_commit=3a40d4bbcac48fa6dec7bedcc0206074a0218884  # tags/1.7.1^0
source=("git+https://github.com/flatpak/xdg-desktop-portal-gtk#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $pkgname

  # Screencasting with mutter 3.36
  git cherry-pick -n adb72687ccda69b83509f05f80e4a226a2faaf1f

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --libexecdir=/usr/lib
  make 
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  DESTDIR="$pkgdir" make install
}
