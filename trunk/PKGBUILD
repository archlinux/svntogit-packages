# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libxkbfile
pkgver=1.0.9
pkgrel=2
pkgdesc="X11 keyboard file manipulation library"
arch=('x86_64')
license=('custom')
url="https://xorg.freedesktop.org/"
depends=('libx11')
makedepends=('xorg-util-macros')
source=(${url}/releases/individual/lib/${pkgname}-${pkgver}.tar.bz2{,.sig})
sha512sums=('5fa268f10d7c4bd7b1e0c9f12adaa53d86b149f193d228fc620b3b81d360b37e4ede0192f5a0dc715bf830a57bd1388af01399fb33609413fc64623ee91cb8d1'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') #Alan Coopersmith <alan.coopersmith@oracle.com>

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static 
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
