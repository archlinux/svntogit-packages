# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: George Brooke <george+arch.aur@george-brooke.co.uk>

pkgname=telepathy-logger-qt
pkgver=15.04.0
pkgrel=2
pkgdesc="Qt bindings for telepathy logger"
arch=('i686' 'x86_64')
url='https://www.kde.org/'
license=('GPL')
depends=('telepathy-qt' 'telepathy-logger')
makedepends=('extra-cmake-modules' 'doxygen' 'python2')
source=("http://download.kde.org/stable/$pkgname//${pkgver%.*}/src/$pkgname-$pkgver.tar.xz")
sha1sums=('3f71e607980b601b46ff45be6e72d066926fc912')

build() {
  mkdir build 
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DPYTHON_EXECUTABLE=/usr/bin/python2
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
