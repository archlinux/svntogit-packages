# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gspell
pkgver=1.12.0
pkgrel=2
pkgdesc="Spell-checking library for GTK applications"
url="https://gitlab.gnome.org/GNOME/gspell"
arch=(x86_64)
license=(LGPL)
depends=(glib2 gtk3 enchant icu)
makedepends=(gobject-introspection vala gtk-doc autoconf-archive git)
_commit=433999a26171030de17dcdceabeae3c4274352a4  # tags/1.12.0^0
source=("git+https://gitlab.gnome.org/GNOME/gspell.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd gspell
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd gspell
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd gspell
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --enable-gtk-doc
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package(){
  depends+=(libg{lib,object,io}-2.0.so libpango-1.0.so libgtk-3.so
            libenchant-2.so libicuuc.so)
  provides+=("libgspell-${pkgver%%.*}.so")

  cd gspell
  make DESTDIR="$pkgdir" install
}

# vim:set sw=2 sts=-1 et:
