# $Id$
# Maintainer: SÃ©bastien "Seblu" Luttringer <seblu@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=less
pkgver=468
pkgrel=1
pkgdesc='A terminal based program for viewing text files'
license=('GPL3')
arch=('i686' 'x86_64')
url='http://www.greenwoodsoftware.com/less'
groups=('base')
depends=('glibc' 'ncurses' 'pcre')
source=("http://www.greenwoodsoftware.com/$pkgname/$pkgname-$pkgver.tar.gz"{,.sig})
md5sums=('ebaeed479369ff5b8d331381363a9c13'
         'SKIP')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --with-regex=pcre
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
