# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Tom K <tomk@runbox.com>

pkgname=ekiga
pkgver=4.0.0
pkgrel=1
pkgdesc="VOIP/Videoconferencing app with full SIP and H.323 support (GnomeMeeting expanded and renamed)"
url="http://www.ekiga.org"
license=(GPL)
arch=(i686 x86_64)
depends=('opal' 'boost-libs' 'libxv' 'libnotify' 'hicolor-icon-theme' 'avahi' 'gtk2' 'gconf')
makedepends=('intltool' 'gnome-doc-utils' 'evolution-data-server' 'boost')
optdepends=('evolution-data-server: Evolution integration')
options=(!emptydirs)
groups=('gnome-extra')
install=ekiga.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('69c544fb0f2e9cb425b9dae131b01e256ad20fd597f1faf0c28f5930b0f1d998')

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-schemas-install \
      --disable-scrollkeeper --enable-dbus
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 install

  install -m755 -d "$pkgdir/usr/share/gconf/schemas"
  gconf-merge-schema "$pkgdir/usr/share/gconf/schemas/$pkgname.schemas" --domain ekiga "$pkgdir"/etc/gconf/schemas/*.schemas
  rm -f "$pkgdir"/etc/gconf/schemas/*.schemas
}
