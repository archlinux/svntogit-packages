# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: 03/08/04 <lefungus@altern.org>

pkgname=libebml
pkgver=1.3.9
pkgrel=1
pkgdesc='Extensible Binary Meta Language library'
arch=(x86_64)
url=https://github.com/Matroska-Org/libebml
license=(LGPL2.1)
depends=(gcc-libs)
makedepends=(
  cmake
  git
)
provides=(libebml.so)
source=(git+https://github.com/Matroska-Org/libebml.git#tag=release-${pkgver})
sha256sums=(SKIP)

prepare() {
  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd build

  cmake ../libebml \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DBUILD_SHARED_LIBS=ON
  make
}

package() {
  make DESTDIR="${pkgdir}" -C build install
}

# vim: ts=2 sw=2 et:
