# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: pressh <pressh@gmail.com>

pkgname=alacarte
pkgver=3.11.91+12+g1bb265f
pkgrel=2
pkgdesc="Menu editor for gnome"
arch=(any)
license=('LGPL')
url="http://www.gnome.org"
depends=('gnome-menus' 'hicolor-icon-theme' 'python2-gobject' 'gtk3')
makedepends=('intltool' 'libxslt' 'docbook-xsl' 'git' 'gnome-common')
source=('git+https://gitlab.gnome.org/GNOME/alacarte.git#commit=1bb265f7')
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --long | sed 's/-/+/g'
}

build() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var PYTHON=/usr/bin/python2
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
  python2 -m compileall "$pkgdir"/usr/lib/python2.7/site-packages/Alacarte/
}
