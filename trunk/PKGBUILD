# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=gupnp-igd
pkgver=0.2.5
pkgrel=1
pkgdesc="A library to handle UPnP IGD port mapping"
url="https://wiki.gnome.org/Projects/GUPnP"
arch=(x86_64)
license=(LGPL)
depends=(gupnp)
makedepends=(gobject-introspection python2-gobject2 git gnome-common)
checkdepends=(python2-gobject)
_commit=5d9c48f3de6277b56c86e7131d84a1b24f30d819  # tags/0.2.5^0
source=("git+https://git.gnome.org/browse/gupnp-igd#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  PYTHON=python2 ./configure --prefix=/usr --enable-python --enable-gtk-doc
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="${pkgdir}" install
}

