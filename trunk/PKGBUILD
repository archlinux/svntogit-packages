# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kapidox
pkgver=5.79.0
pkgrel=1
pkgdesc='Frameworks API Documentation Tools'
arch=(any)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(python-jinja python-yaml)
makedepends=(cmake qt5-base)
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('2752c83d9b72245c94d000b5568a457817dc5b6bfcd550daa5f046d5ba9e6462'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
