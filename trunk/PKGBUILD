# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kcompletion
pkgver=r5.3a450d5
pkgrel=1
pkgdesc='KCompletion'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kcompletion'
license=('LGPL')
depends=('kwidgetsaddons' 'kconfig')
makedepends=('extra-cmake-modules' 'git')
conflicts=('kcompletion')
provides=('kcompletion')
source=('git://anongit.kde.org/kcompletion.git')
md5sums=('SKIP')

pkgver() {
  cd kcompletion
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kcompletion \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
