# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: RÃ©my Oudompheng <remy@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Roman Kyrylych <Roman.Kyrylych@gmail.com>

pkgname=zziplib
pkgver=0.13.70
pkgrel=1
pkgdesc="A lightweight library that offers the ability to easily extract data from files archived in a single zip file"
arch=('x86_64')
url="http://zziplib.sourceforge.net"
license=('LGPL' 'MPL')
depends=('zlib')
makedepends=('python' 'xmlto' 'zip' 'cmake')
source=($pkgname-$pkgver.tar.gz::"https://github.com/gdraheim/zziplib/archive/v$pkgver.tar.gz"
        https://github.com/gdraheim/zziplib/commit/a46ac9630a46093eda88540a8ec705e6f53bcc4f.patch)
sha256sums=('a1457262d7a237dc50ce1f98ca57242bc714055ff81146f419ee53cdea1bf029'
            '6f24e770f858223f8321cc2a2df9fdab570290ac1a1279b6937988a0b460bca1')
sha512sums=('5e6bed4993cac880063a64b56db42f362898434e9c61916747377440f1325f649c086a3c1beeb05fb647b4f8af3fcb0407b087fc23604cd73fdd2373f132e9a5'
            '3e2e55bb7af0160dd0b1f0f10b9c41fbe68107c00a42bd6ab19a1a9b640371c751f5fc562f832668ba09b5cfc180b099e361c3747b0e22e80b496bff4f956b11')

prepare() {
  cd ${pkgname}-${pkgver}

  patch -Np1 -i "${srcdir}"/a46ac9630a46093eda88540a8ec705e6f53bcc4f.patch

  mkdir build
}

build() {
  cd ${pkgname}-${pkgver}/build
  cmake -DCMAKE_INSTALL_PREFIX=/usr ..
  make
}

package() {
  cd ${pkgname}-${pkgver}/build
  make DESTDIR="${pkgdir}" install
}

# vim: set ts=2 sw=2 et:
