# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=banshee
pkgver=1.0.0
pkgrel=1
pkgdesc="Music management and playback for GNOME"
arch=('i686' 'x86_64')
url="http://banshee-project.org/"
license=('MIT')
depends=('sqlite3' 'ndesk-dbus-glib>=0.4.1' 'mono-addins>=0.3.1' 'taglib-sharp>=2.0.3.0' 'gstreamer0.10-good-plugins' 'gstreamer0.10-base-plugins' 'gnome-sharp>=2.20.1' 'libmtp>=0.2.6.1' 'ipod-sharp>=0.8.0')
makedepends=('perlxml' 'pkgconfig')
options=('!libtool' '!makeflags' '!emptydirs')
install=banshee.install
source=(http://download.banshee-project.org/${pkgname}/${pkgname}-1-${pkgver}.tar.gz)
md5sums=('8825dc8b45b117f364d1769a28364928')

build() {
  export MONO_SHARED_DIR=${startdir}/src/.wabi
  mkdir -p "${MONO_SHARED_DIR}"
  cd ${startdir}/src/${pkgname}-1-${pkgver}
  LD_PRELOAD="" ./configure --prefix=/usr \
        --sysconfdir=/etc --localstatedir=/var \
	--disable-docs --disable-static || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR=${startdir}/pkg install || return 1

  install -m755 -d ${startdir}/pkg/usr/share/licenses/${pkgname}
  install -m644 COPYING ${startdir}/pkg/usr/share/licenses/${pkgname}/ || return 1

  install -d -m755 ${startdir}/pkg/usr/share/gconf/schemas
  gconf-merge-schema ${startdir}/pkg/usr/share/gconf/schemas/${pkgname}.schemas ${startdir}/pkg/etc/gconf/schemas/*.schemas || return 1
  rm -f ${startdir}/pkg/etc/gconf/schemas/*.schemas
}
