# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: PedsXing <pedsxing at gmx dot net>

pkgname=vc
pkgver=0.7.2
pkgrel=1
pkgdesc="A library to ease explicit vectorization of C++ code"
url='http://code.compeng.uni-frankfurt.de/projects/vc/'
arch=('x86_64' 'i686')
license=('LGPL3')
makedepends=('cmake')
source=("http://code.compeng.uni-frankfurt.de/attachments/download/164/Vc-${pkgver}.tar.gz")
md5sums=('f54dd2945f9e5a38945e50b4a3f7a901')
 
build() {
  mkdir build 
  cd build
  cmake ../Vc-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

check() {
  cd build
  make test
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
