# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname='mesa-demos'
pkgver=8.2.0
pkgrel=4
arch=('i686' 'x86_64')
pkgdesc="Mesa demos and tools"
url="http://mesa3d.sourceforge.net"
license=('custom')
depends=('libgl' 'glew' 'freeglut')
makedepends=('git')
source=("$pkgname::git://anongit.freedesktop.org/mesa/demos#tag=$pkgname-$pkgver"
        'LICENSE')
sha256sums=('SKIP'
            '7fdc119cf53c8ca65396ea73f6d10af641ba41ea1dd2bd44a824726e01c8b3f2')

prepare() {
  cd mesa-demos

  # https://bugs.freedesktop.org/show_bug.cgi?id=91643
  git cherry-pick -n 6f7ad3f24294 74443c6ee79f
}

build() {
  cd mesa-demos
  ./autogen.sh --prefix=/usr \
    --with-system-data-files=/usr/share/mesa-demos
  make
}

package() {
  cd mesa-demos
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/mesa-demos"
  install -m755 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/mesa-demos/"
}
