# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=libtiff
pkgver=3.9.1
pkgrel=1
pkgdesc="Library for manipulation of TIFF images"
arch=('i686' 'x86_64')
url="http://www.remotesensing.org/libtiff/"
license=('custom')
depends=('libjpeg>=7' 'zlib')
makedepends=('libgl' 'freeglut' 'libxmu' 'libxi')
optdepends=('freeglut: for using tiffgt')
options=('!libtool')
source=(ftp://ftp.remotesensing.org/pub/libtiff/tiff-${pkgver}.tar.gz \
        libtiff-CVE-2009-2285.patch tiff-3.9.0-CVE-2009-2347.patch)
md5sums=('63c59a44f34ae0787f2d71de3d256e20' 'ff61077408727a82281f77a94f555e2a'\
         '867fa3ec83e748abf247f9706ef13f9a')
sha1sums=('675ad1977023a89201b80cd5cd4abadea7ba0897' 'eadce8c8bd72ea9c74f35300bf299131813b0c8b'\
         'cf8b68ed8f218d2429ab296c2033dc0292efe299')

build() {
  cd "${srcdir}/tiff-${pkgver}"
  patch -p1 < ../libtiff-CVE-2009-2285.patch || return 1
  patch -p0 < ../tiff-3.9.0-CVE-2009-2347.patch || return 1
  ./configure --prefix=/usr --sysconfdir=/etc --mandir=/usr/share/man || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
  install -D -m644 COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE" || return 1
}
