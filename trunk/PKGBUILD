# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=bluez-qt
pkgver=5.25.0
pkgrel=2
pkgdesc='Qt wrapper for Bluez 5 DBus API'
arch=(i686 x86_64)
url='https://community.kde.org/Frameworks'
license=(GPL2)
depends=(qt5-declarative bluez)
makedepends=(extra-cmake-modules mesa)
conflicts=(libbluedevil-frameworks)
groups=(kf5)
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"
        bluez-qt-pri.patch::"https://quickgit.kde.org/?p=bluez-qt.git&a=commitdiff&h=b81b59bc&o=plain")
md5sums=('2e03661752de63494541649158d712fa'
         'ee25e5cfa71cdf21f8878d8a3741fdfe')

prepare() {
  mkdir -p build

# Fix include dir in pri file
  cd $pkgname-$pkgver
  patch -p1 -i ../bluez-qt-pri.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
