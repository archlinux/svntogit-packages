# Packager: Justin "juster" Davis <jrcd83@gmail.com>
# $Id$

pkgname=perl-lwp-mediatypes
pkgver=6.02
pkgrel=1
pkgdesc="guess media type for a file or a URL"
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=('perl>=5.6.2')
conflicts=('perl-libwww<6')
url=https://metacpan.org/release/LWP-MediaTypes
source=("http://search.cpan.org/CPAN/authors/id/G/GA/GAAS/LWP-MediaTypes-${pkgver}.tar.gz")
md5sums=(8c5f25fb64b974d22aff424476ba13c9)
sha512sums=(b2ad33fb2c6dd7afaa9b8dd2ed6a15b2ecc35d13bcdf5297ce1632ea77406def0840a24c5213187707b665f28cc9bbea5774bfd1a4070d1f6192e904f707b94e)
_distdir="${srcdir}/LWP-MediaTypes-${pkgver}"

build()
(
    export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor"     \
      PERL_MB_OPT="--installdirs vendor" \
      MODULEBUILDRC=/dev/null

    cd "$_distdir"
    /usr/bin/perl Makefile.PL
    make
)

check()
(
    export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    cd "$_distdir"
    make test
)

package() {
    cd "$_distdir"
    make DESTDIR="$pkgdir" install
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
