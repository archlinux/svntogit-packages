# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kalk
pkgver=23.04.0
pkgrel=2
pkgdesc='A powerful cross-platform calculator application built with the Kirigami framework'
url='https://apps.kde.org/kalk/'
arch=(x86_64)
license=(GPL LGPL)
groups=(kde-applications kde-utilities)
depends=(mpfr kirigami2 kunitconversion ki18n kconfig kcoreaddons qt5-feedback)
makedepends=(extra-cmake-modules)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('4bb3d1bddc029292785c251e01bf8e6b18d395b920e93eae796325e55f0c1ff6'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
