# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Arjan <arjan@archlinux.org>
# contributor Sarah Hay <sarahhay@mb.sympatico.ca>

pkgname=libdvdnav
pkgver=5.0.3
pkgrel=1
pkgdesc="The library for xine-dvdnav plugin."
arch=('i686' 'x86_64')
license=('GPL')
url="http://dvdnav.mplayerhq.hu/"
depends=('libdvdread')
makedepends=('git')
source=(git://git.videolan.org/libdvdnav.git#tag=$pkgver
        fix-crash-describe_title.patch)
sha256sums=('SKIP'
            '6a8e9c777dd9dbfa8a7f981796bd7a60412e3a5c7a8400253c567d956412c64b')

prepare() {
  cd $pkgname
  patch -Np1 -i ../fix-crash-describe_title.patch
  autoreconf -fi
}

build() {
  cd $pkgname
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname
  make DESTDIR="${pkgdir}" install
}
