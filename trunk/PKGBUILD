# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=eog-plugins
pkgver=42.1
pkgrel=1
pkgdesc="Plugins for Eye of Gnome"
arch=(x86_64)
url="https://wiki.gnome.org/Apps/EyeOfGnome/Plugins"
license=(GPL2)
depends=(eog libchamplain libgdata python-gobject)
makedepends=(gobject-introspection git meson)
options=(debug)
_commit=f7a02a5ee12d0a1166c0cd34472e8009960da644  # tags/42.1^0
source=("git+https://gitlab.gnome.org/GNOME/eog-plugins.git#commit=$_commit")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 et:
