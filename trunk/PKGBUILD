# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=imake
pkgver=1.0.6
pkgrel=2
pkgdesc="X.Org imake program and related utilities"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('glibc')
makedepends=('pkgconfig' 'xproto' 'perl' 'xorg-util-macros' 'xorg-font-util')
optdepends=('perl: for mkhtmlindex')
source=(http://xorg.freedesktop.org/releases/individual/util/imake-${pkgver}.tar.bz2
        http://xorg.freedesktop.org/releases/individual/util/gccmakedep-1.0.2.tar.bz2
        http://xorg.freedesktop.org/releases/individual/util/lndir-1.0.3.tar.bz2
        http://xorg.freedesktop.org/releases/individual/util/makedepend-1.0.5.tar.bz2
        http://xorg.freedesktop.org/releases/individual/util/xorg-cf-files-1.0.5.tar.bz2
        gccmakedep-cpp-4.8-compat.patch
        imake-cpp-4.8-compat.patch
        linuxconfig.patch
        LICENSE)
sha1sums=('a54c025d7ac9894b6bc919d13454c6adb12ae140'
          'a3d971a507a460233afd8ed46e9f06135fed634d'
          'fc71a6ff2cc0a1065cfb608796ffd6b4f0ce76fe'
          '2599afa039d2070bae9df6ce43da288b3a4adf97'
          'ae22eb81d56d018f0b3b149f70965ebfef2385fd'
          'ef7bb90e30ce35131d049f45b650cd0cf6dc0a20'
          '24f87a945307d4336ab2afd5cade683f0e94533c'
          '72f2d4cc4d4cc5f85ba6982718ae1bbb7e56e0d0'
          'c5ecffb25ad079ba3858e3aec047d046df603a89')


prepare() {
  cd xorg-cf-files-1.0.5
  patch -p1 -i ../linuxconfig.patch

  cd ../imake-1.0.6
  patch -Np1 -i ../imake-cpp-4.8-compat.patch

  cd ../gccmakedep-1.0.2
  patch -Np1 -i ../gccmakedep-cpp-4.8-compat.patch
}

build() {
  for i in *; do
    if [ -d "${i}" ]; then
      pushd "${i}"
      autoreconf -fi
      ./configure --prefix=/usr --mandir=/usr/share/man
      make
      popd
    fi
  done
}

package() {
  for i in *; do
    if [ -d "${i}" ]; then
      pushd "${i}"
      make DESTDIR="${pkgdir}" install
      popd
    fi
  done
  install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
