# $Id$
# Maintainer:
# Contributor: firmicus <francois@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>
# Contributor: herb <hrose@archlinux.org>

pkgname=perl-xml-parser
_realname=XML-Parser
pkgver=2.41
pkgrel=4
pkgdesc="Expat-based XML parser module for perl"
arch=('i686' 'x86_64')
license=('GPL' 'PerlArtistic')
url="http://search.cpan.org/dist/${_realname}/"
depends=('perl' 'expat')
replaces=('perlxml')
provides=("perlxml=${pkgver}")
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/T/TO/TODDR/${_realname}-${pkgver}.tar.gz")
md5sums=('c320d2ffa459e6cdc6f9f59c1185855e')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"

  # install module in vendor directories.
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make test
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make install DESTDIR="${pkgdir}"

  # remove perllocal.pod and .packlist.
  find "${pkgdir}" -name perllocal.pod -delete
  find "${pkgdir}" -name .packlist -delete
}
