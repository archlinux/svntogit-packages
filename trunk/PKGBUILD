# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: aurelien <aurelien@archlinux.org>
# Contributor: Tobias Kieslich <tobias (at) archlinux.org>

pkgname=xfce4-mpc-plugin
pkgver=0.4.0
pkgrel=1
pkgdesc="A plugin to control the music player daemon from the xfce4-panel"
arch=('i686' 'x86_64')
license=('BSD')
url="http://goodies.xfce.org/projects/panel-plugins/xfce4-mpc-plugin"
groups=('xfce4-goodies')
depends=('xfce4-panel' 'libmpd>=0.16.1')
makedepends=('intltool')
options=('!libtool')
source=(http://archive.xfce.org/src/panel-plugins/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2)
md5sums=('ae9b285e24385e27cfd797ab605a1b7f')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--libexecdir=/usr/lib \
	--localstatedir=/var \
	--disable-static \
	--disable-debug
  make
}

package(){
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
