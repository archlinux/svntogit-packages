# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: Damir Perisa <damir@archlinux.org>

pkgname=hddtemp
pkgver=0.3.beta15.46
_origver=0.3-beta15
_patchver=46
pkgrel=1
pkgdesc="Gives you the temperature of your hard drive by reading S.M.A.R.T. information"
arch=('i686' 'x86_64')
url="http://www.guzu.net/linux/hddtemp.php"
license=('GPL')
depends=('glibc')
backup=('etc/conf.d/hddtemp')
source=(http://www.guzu.net/files/hddtemp-${_origver}.tar.bz2
        http://ftp.debian.org/debian/pool/main/h/hddtemp/hddtemp_${_origver}-${_patchver}.diff.gz
        'hddtemp' 'hddtemp.confd' 'hddtemp-0.3-beta15-reg-eip.patch')
md5sums=('8b829339e1ae9df701684ec239021bb8'
         '040437845e28771c560b4bb40e41f933'
         'b7ebb86dfeef3bc4577ed1040307e36b'
         'fdca5c43900406f0be76534ef2a5a697'
         '526ef28549dd0da7e38d32cfc8523592')
options=('!libtool')

build() {
  cd "$srcdir/$pkgname-${_origver}"

  patch -Np1 -i "$srcdir/${pkgname}_${_origver}-${_patchver}.diff"
  patch -Np1 -i "$srcdir/hddtemp-0.3-beta15-reg-eip.patch"

  ./configure --prefix=/usr --mandir=/usr/share/man --with-db-path=/usr/share/$pkgname/hddtemp.db
  make
}

package() {
  cd "$srcdir/$pkgname-${_origver}"
  make DESTDIR=$pkgdir install

  install -D -m644 "$srcdir/$pkgname-${_origver}/debian/hddtemp.db" \
    "$pkgdir/usr/share/${pkgname}/hddtemp.db"
  install -D -m644 "$srcdir/hddtemp.confd" "$pkgdir/etc/conf.d/hddtemp"
  install -D -m755 "$srcdir/hddtemp" "$pkgdir/etc/rc.d/hddtemp"
}
