# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=nautilus
pkgver=2.24.1
pkgrel=1
pkgdesc="The GNOME shell and file manager"
arch=(i686 x86_64)
license=('GPL')
depends=('librsvg>=2.22.3' 'eel>=2.24.1' 'libbeagle>=0.3.5.1' 'libtracker>=0.6.6' 'libexif>=0.6.16' 'exempi>=2.0.1' 'gnome-icon-theme>=2.24.0' 'desktop-file-utils' 'gvfs>=1.0.2')
makedepends=('perlxml' 'pkgconfig')
url="http://www.gnome.org"
groups=('gnome')
options=('!libtool' '!emptydirs')
install=nautilus.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.24/${pkgname}-${pkgver}.tar.bz2)
md5sums=('ccb5497397dfba46569b6dd1d2f41cef')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var --disable-static \
	      --disable-update-mimedb || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1
  
  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
