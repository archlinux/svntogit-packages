# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

pkgname=libva-intel-driver
pkgver=1.8.0
pkgrel=1
pkgdesc='VA-API implementation for Intel G45 and HD Graphics family'
arch=(i686 x86_64)
url='https://01.org/linuxmedia/vaapi'
license=(MIT)
depends=(libva)
replaces=(libva-driver-intel)
source=(https://github.com/01org/intel-vaapi-driver/releases/download/$pkgver/intel-vaapi-driver-$pkgver.tar.bz2)
sha1sums=('9982122c8ef36aad01aa09c083974532afa8f8ca')

prepare() {
  cd intel-vaapi-driver-$pkgver

  # Only relevant if intel-gpu-tools is installed,
  # since then the shaders will be recompiled
  sed -i '1s/python$/&2/' src/shaders/gpp.py
}

build() {
  cd intel-vaapi-driver-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd intel-vaapi-driver-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
