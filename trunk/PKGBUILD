# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=xorg-font-alias
pkgname=('xorg-fonts-alias-100dpi' 'xorg-fonts-alias-75dpi' 'xorg-fonts-alias-cyrillic' 'xorg-fonts-alias-misc')
pkgver=1.0.3
pkgrel=4
arch=('any')
url="https://xorg.freedesktop.org/"
makedepends=('xorg-util-macros')
license=('custom')
source=("${url}/releases/individual/font/font-alias-${pkgver}.tar.bz2")
sha512sums=('05abfef4615722d8cf3f21ab1118494522553a75427140d9753b3d312f404735c20766ceacc1b53f9e783ea25ec8f2f6bdb8696525a4962afe29f524ed8b6552')

build() {
  cd "${srcdir}/font-alias-${pkgver}"
  ./configure --prefix=/usr --with-fontrootdir=/usr/share/fonts
  make
}

package_xorg-fonts-alias-100dpi() {

  pkgdesc="X.org font alias files - 100dpi font familiy"
  conflicts=(xorg-font-alias)

  cd "${srcdir}/font-alias-${pkgver}"
  make -C 100dpi DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

package_xorg-fonts-alias-75dpi() {

  pkgdesc="X.org font alias files - 75dpi font familiy"
  conflicts=(xorg-font-alias)

  cd "${srcdir}/font-alias-${pkgver}"
  make -C 75dpi DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
package_xorg-fonts-alias-cyrillic() {

  pkgdesc="X.org font alias files - cyrillic font familiy"
  conflicts=(xorg-font-alias)

  cd "${srcdir}/font-alias-${pkgver}"
  make -C cyrillic DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
package_xorg-fonts-alias-misc() {

  pkgdesc="X.org font alias files - misc font familiy"
  conflicts=(xorg-font-alias)

  cd "${srcdir}/font-alias-${pkgver}"
  make -C misc DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
