# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=freeglut
pkgver=2.8.1
pkgrel=1
pkgdesc="Provides functionality for small OpenGL programs"
arch=('i686' 'x86_64')
url="http://freeglut.sourceforge.net/"
license=('MIT')
depends=('libxi' 'libxrandr' 'libgl')
makedepends=('mesa' 'glu' 'libxxf86vm')
replaces=('glut')
provides=('glut')
conflicts=('glut')
options=('!libtool')
source=(http://downloads.sourceforge.net/freeglut/${pkgname}-${pkgver}.tar.gz)
md5sums=('918ffbddcffbac83c218bc52355b6d5a')
sha1sums=('7330b622481e2226c0c9f6d2e72febe96b03f9c4')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # smooth_opengl3 demo doesn't build with recent mesa, Gentoo bug #428382
  sed -i "s/smooth_opengl3 //" progs/demos/Makefile.*

  autoreconf -vfi  
  ./configure --prefix=/usr --disable-static
  make all
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
