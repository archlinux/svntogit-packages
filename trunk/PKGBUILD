# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgbase=phonon-vlc
pkgname=(phonon-qt5-vlc)
pkgdesc="Phonon VLC backend for Qt5"
pkgver=0.11.1
pkgrel=1
arch=(x86_64)
url='https://community.kde.org/Phonon'
license=(LGPL)
depends=(vlc phonon-qt5)
provides=(phonon-qt5-backend)
makedepends=(extra-cmake-modules qt5-tools)
source=("http://download.kde.org/stable/phonon/phonon-backend-vlc/$pkgver/phonon-backend-vlc-$pkgver.tar.xz"{,.sig})
sha256sums=('ccd91e967f943b9e2d7a1115cd1e2851b69f693d568d2174bf709a944a14e5ee'
            'SKIP')
validpgpkeys=(CB9387521E1EE0127DA804843FDBB55084CC5D84) # Harald Sitter <sitter@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../phonon-backend-vlc-$pkgver
  make
}

package_phonon-qt5-vlc() {
  cd build
  make DESTDIR="$pkgdir" install
}
