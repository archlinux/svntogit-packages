# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kio-extras
pkgver=16.12.1
pkgrel=1
pkgdesc="Additional components to increase the functionality of KIO"
arch=(i686 x86_64)
url='https://www.kde.org/applications/internet/'
license=(LGPL)
depends=(kdelibs4support khtml kdnssd libssh smbclient exiv2 openexr openslp libmtp kpty kactivities)
makedepends=(extra-cmake-modules kdoctools kdesignerplugin qt5-webengine)
optdepends=('qt5-webengine: HTML thumbnailer' 'qt5-imageformats: thumbnails for additional image formats'
            'kimageformats: thumbnails for additional image formats')
groups=(kde-applications kdenetwork)
source=("http://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
md5sums=('7afd2a4e1f645ef45450c1087e42206e'
         'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}

