# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-lazy-object-proxy
depends=("python")
pkgver=1.5.1
pkgrel=3
pkgdesc='A fast and thorough lazy object proxy'
arch=('x86_64')
url="https://github.com/ionelmc/python-lazy-object-proxy"
license=('BSD')
makedepends=('python-setuptools')
checkdepends=('python-pytest-benchmark' 'python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ionelmc/python-lazy-object-proxy/archive/v$pkgver.tar.gz")
sha512sums=('76b54265a7a85bcdb64879c7773f7be2e179494530c47fcaeecb2a16130e520163eff7095a94e6095e0b1d5479cc12d439c6b64ef6bc7aa293ebcc23fe846ab0')

build() {
  cd "$srcdir"/python-lazy-object-proxy-$pkgver
  python setup.py build
}

check() {
  cd python-lazy-object-proxy-$pkgver
  python setup.py pytest --addopts --ignore=src
}

package() {
  cd python-lazy-object-proxy-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
