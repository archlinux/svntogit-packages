# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-xdpyinfo
pkgver=1.3.1
pkgrel=1
pkgdesc="Display information utility for X"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libx11' 'libxext' 'libxtst' 'libxxf86vm' 'libxxf86dga' 'libxrender' 'libxcomposite' 'libxinerama' 'libdmx')
makedepends=('xorg-util-macros')
groups=('xorg-apps' 'xorg')
source=(http://xorg.freedesktop.org/archive/individual/app/xdpyinfo-${pkgver}.tar.bz2)
sha256sums=('aef9285069a517ed870e5d8a02d13f7d8a953d7f7220146da563e04c7f128b94')

build() {
  cd "${srcdir}/xdpyinfo-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/xdpyinfo-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
