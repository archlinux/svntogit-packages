# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xdg-utils
pkgver=1.1.3
pkgrel=3
pkgdesc="Command line tools that assist applications with a variety of desktop integration tasks"
arch=('any')
url="https://www.freedesktop.org/wiki/Software/xdg-utils/"
license=('MIT')
depends=('sh' 'xorg-xset') # xset needed inside xdg-screensaver
makedepends=('docbook-xsl' 'lynx' 'xmlto' 'git')
optdepends=('kde-cli-tools: for KDE Plasma5 support in xdg-open'
            'exo: for Xfce support in xdg-open'
            'xorg-xprop: for Xfce support in xdg-open'
            'pcmanfm: for LXDE support in xdg-open'
            'perl-file-mimeinfo: for generic support in xdg-open'
            'perl-net-dbus: Perl extension to dbus used in xdg-screensaver'
            'perl-x11-protocol: Perl X11 protocol used in xdg-screensaver')
source=(https://portland.freedesktop.org/download/$pkgname-$pkgver.tar.gz
        0001_better_pcmanfm_check.patch
        drop_obsolete_GNOME_support.patch) #{,.asc}
sha1sums=('98294cf332c341b85e481d98e9ea59357fe1efc7'
          'f5c09cc10d7b88c2033392efd4179da65a1f47b1'
          '32d4282c2e6c7345ddf04016c91f0defbf88b356')
#validpgpkeys=('8B75CA7811367175D05F3B03C43570F80CC295E6') # "Per Olofsson <pelle@pqz.se>"

prepare() {
	cd $pkgname-$pkgver
	# FS#58707
	patch -Np1 -i ../0001_better_pcmanfm_check.patch
	# drop Gnome2 support - FS58096/https://bugs.freedesktop.org/show_bug.cgi?id=90775
	patch -Np1 -i ../drop_obsolete_GNOME_support.patch
}

build() {
	cd $pkgname-$pkgver
	./configure --prefix=/usr --mandir=/usr/share/man
	make
}

package() {
	cd $pkgname-$pkgver
	make DESTDIR="${pkgdir}" install
	install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	
	# install empty directory to prevent xdg-desktop-menu install *.desktop to fail, see FS#33316
	install -dm755 $pkgdir/usr/share/desktop-directories
}
