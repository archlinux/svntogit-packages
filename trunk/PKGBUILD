# Maintainer: Kevin Piche <kevin@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=recode
pkgver=3.7
pkgrel=1
pkgdesc="Converts files between various character sets and usages"
arch=('x86_64')
url='https://github.com/rrthomas/recode'
license=('GPL3' 'LGPL3')
depends=('glibc')
makedepends=('python')
source=("https://github.com/rrthomas/recode/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('188ce229f01485ecd847b9c2828615c14611473efffbab04b5e2fe138f839e64')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man \
    --infodir=/usr/share/info \
    --without-included-gettext
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
}
