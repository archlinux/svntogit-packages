# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=breeze-gtk
pkgver=5.27.4
pkgrel=2
pkgdesc='Breeze widget theme for GTK 2 and 3'
arch=(any)
url='https://kde.org/plasma-desktop/'
license=(LGPL)
depends=()
makedepends=(extra-cmake-modules sassc python-cairo breeze)
groups=(plasma)
source=(https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz{,.sig}
        https://invent.kde.org/plasma/breeze-gtk/-/commit/c5e757a1.patch)
sha256sums=('cb967dcd258065f87a9cb43f8022378e7ead1db83590ca2eea4e77196226999f'
            'SKIP'
            '1f912c030bc3f40da6487e13a04ceb1834cf6568cd7857f894081c5ed1ef0fef')
validpgpkeys=('E0A3EB202F8E57528E13E72FD7574483BB57B18D'  # Jonathan Esk-Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  patch -d $pkgname-$pkgver -p1 < c5e757a1.patch # Fix oversized buttons
}

build(){
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
