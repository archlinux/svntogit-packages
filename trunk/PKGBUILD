# $Id$
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>
pkgname=iana-etc
pkgver=2.30
pkgrel=2
pkgdesc="/etc/protocols and /etc/services provided by IANA"
arch=('any')
url="http://sethwklein.net/iana-etc"
license=('custom')
makedepends=('gawk')
backup=(etc/{protocols,services})
source=(http://sethwklein.net/iana-etc-2.30.tar.bz2
        Makefile.patch)
sha256sums=('b9a6874fb20012836efef75452ef2acae624022d680feeb1994d73facba3f20d'
            '3e6db7adb8a67b765b8be65825f2a0e6c525eb42367385193c678af6b75d0321')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -p0 -i "$srcdir/Makefile.patch"
  make get
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"

  make test
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  install -D -m644 COPYING "$pkgdir/usr/share/licenses/iana-etc/LICENSE"
}

# vim:set ts=2 sw=2 et:
