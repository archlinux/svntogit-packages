# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-service-identity
pkgname=('python-service-identity' 'python2-service-identity')
pkgver=18.1.0
pkgrel=1
pkgdesc="Service identity verification for pyOpenSSL"
arch=('any')
license=('MIT')
url="http://pypi.python.org/pypi/service_identity"
makedepends=('python-pyopenssl' 'python2-pyopenssl' 'python-pyasn1' 'python2-pyasn1'
             'python-setuptools' 'python2-setuptools' 'python-pyasn1-modules' 'python2-pyasn1-modules'
             'python-attrs' 'python2-attrs' 'python-idna' 'python2-idna')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/pyca/service_identity/archive/$pkgver.tar.gz")
sha512sums=('0d1abf6ba1e725e923171fd3b809b2cf7ba757ca47f773e8c99a2e2265425b253549609c76955c4d67fcd2fd305d479a1d7541cb26811e94db26ab853d3d3441')

prepare() {
  cp -a service_identity-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/service_identity-$pkgver
  python setup.py build
 
  cd "$srcdir"/service_identity-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/service_identity-$pkgver
  python setup.py pytest

  cd "$srcdir"/service_identity-$pkgver-py2
  python2 setup.py pytest
}
 
package_python-service-identity() {
  depends=('python-pyopenssl' 'python-pyasn1' 'python-pyasn1-modules' 'python-attrs')
  optdepends=('python-idna: for Internationalized Domain Names support')
 
  cd service_identity-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
 
package_python2-service-identity() {
  depends=('python2-pyopenssl' 'python2-pyasn1' 'python2-pyasn1-modules' 'python2-attrs')
  optdepends=('python2-idna: for Internationalized Domain Names support')
 
  cd service_identity-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
