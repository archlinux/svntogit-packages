# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-bdftopcf
pkgver=1.1
pkgrel=1
pkgdesc="Convert X font from Bitmap Distribution Format to Portable Compiled Format"
arch=(x86_64)
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('glibc')
makedepends=('xorg-util-macros' 'xproto' 'fontsproto')
groups=('xorg' 'xorg-apps')
source=(https://xorg.freedesktop.org/archive/individual/app/bdftopcf-${pkgver}.tar.bz2{,.sig})
sha256sums=('4b4df05fc53f1e98993638d6f7e178d95b31745c4568cee407e167491fd311a2'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E' # "Alan Coopersmith <alan.coopersmith@oracle.com>"
	      '995ED5C8A6138EB0961F18474C09DD83CAAA50B2') # "Adam Jackson <ajax@nwnk.net>"

build() {
  cd "${srcdir}/bdftopcf-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/bdftopcf-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
