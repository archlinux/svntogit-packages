# $Id$
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>

pkgname=attr
pkgver=2.4.47
pkgrel=1
pkgdesc="Extended attribute support library for ACL support"
arch=('i686' 'x86_64')
url="http://savannah.nongnu.org/projects/attr"
license=('LGPL')
depends=('glibc') 
makedepends=('gettext')
replaces=('xfsattr')
provides=('xfsattr')
conflicts=('xfsattr')
source=("http://download.savannah.gnu.org/releases/$pkgname/$pkgname-$pkgver.src.tar.gz"{,.sig})
sha256sums=('25772f653ac5b2e3ceeb89df50e4688891e21f723c460636548971652af0a859'
            'SKIP')

build() {
  cd "$pkgname-$pkgver"

  export INSTALL_USER=root INSTALL_GROUP=root
  ./configure --prefix=/usr --libdir=/usr/lib --libexecdir=/usr/lib
  make
}

package() {
  make -C "$pkgname-$pkgver" DIST_ROOT="$pkgdir" install install-lib install-dev

  # tidy up
  rm -f "$pkgdir"/usr/lib/libattr.a
  chmod 0755 "$pkgdir"/usr/lib/libattr.so.*.*.*

  # remove conflicting manpages
  rm -rf "$pkgdir"/usr/share/man/man2
}
