# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=markdownpart
pkgver=20.12.1
pkgrel=1
pkgdesc='KPart for rendering Markdown content'
arch=(x86_64)
url='https://kde.org/'
license=(GPL)
depends=(kparts)
makedepends=(extra-cmake-modules)
groups=(kde-applications kde-utilities)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('93c3a7bd3a7175cc9d8fb3d1f7a492663b5062820598b57ca25a33b8b886098d'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
