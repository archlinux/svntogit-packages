# Maintainer:

pkgname=freetds
pkgver=1.2.21
pkgrel=1
pkgdesc='Library for accessing Sybase and MS SQL Server databases'
url='https://www.freetds.org'
arch=('x86_64')
license=('LGPL')
depends=('openssl' 'unixodbc')
backup=('etc/freetds/freetds.conf'
        'etc/freetds/locales.conf'
        'etc/freetds/pool.conf')
source=("ftp://ftp.freetds.org/pub/freetds/stable/$pkgname-$pkgver.tar.bz2")
sha256sums=('a5e6bbf6d6f3dc0816b19cbd08308d27a084b04926aaa05ac67f808ea07d3d8d')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc/freetds --enable-msdblib \
    --with-unixodbc=/usr --with-openssl
  make 
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
