# $Id: PKGBUILD 250288 2015-11-09 21:13:39Z arojas $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=artikulate
pkgver=16.04.1
pkgrel=1
pkgdesc="Improve your pronunciation by listening to native speakers"
url="https://edu.kde.org/applications/s/artikulate"
arch=(i686 x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kdeedu)
depends=(hicolor-icon-theme kdeclarative knewstuff qt5-gstreamer)
makedepends=(extra-cmake-modules kdoctools boost python)
conflicts=(kdeedu-artikulate)
replaces=(kdeedu-artikulate)
source=("http://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
sha1sums=('3a2a43c8b1118e7f0f038fec1eba798989d83987')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
