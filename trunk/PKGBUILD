# $Id$
# Maintainer:  Ionut Biru <ibiru@archlinux.org>
# Contributor: uastasi <uastasi@archlinux.us>

pkgname=gnote
pkgver=3.18.1
pkgrel=1
pkgdesc='A note taking application'
arch=('i686' 'x86_64')
url='http://live.gnome.org/Gnote'
license=('GPL3')
depends=('gtkmm3' 'libxslt' 'hicolor-icon-theme' 'desktop-file-utils' 'dconf'
         'libsecret')
makedepends=('intltool' 'itstool' 'boost')
install=gnote.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('5da4ec9e7852b09af1067311fc6ee18a2c64384f4f5943b71ac37665144be58d')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/gnote \
    --disable-scrollkeeper \
    --disable-schemas-compile \
    --disable-static
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
