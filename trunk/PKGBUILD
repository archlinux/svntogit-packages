# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=karchive
pkgver=5.1.0
pkgrel=1
pkgdesc='Qt 5 addon providing access to numerous types of archives'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/karchive'
license=('LGPL')
depends=('qt5-base')
makedepends=('extra-cmake-modules')
groups=('kf5')
source=("http://download.kde.org/stable/frameworks/${pkgver}/${pkgname}-${pkgver}.tar.xz")
md5sums=('d94ef0be567d7a3f89ccca7159b6c841')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DECM_MKSPECS_INSTALL_DIR=/usr/share/qt/mkspecs/modules \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
