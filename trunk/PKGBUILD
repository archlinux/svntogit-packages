# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgbase=eog
pkgname=(eog eog-docs)
pkgver=42.1
pkgrel=1
pkgdesc="Eye of Gnome: An image viewing and cataloging program"
url="https://wiki.gnome.org/Apps/EyeOfGnome"
arch=(x86_64)
license=(GPL)
depends=(gnome-desktop libexif lcms2 exempi libpeas librsvg libhandy)
makedepends=(gobject-introspection gi-docgen git meson yelp-tools systemd)
options=(debug)
_commit=7881760af1ef9b15f9f6c9bb636272aa23bfb4b0  # tags/42.1^0
source=("git+https://gitlab.gnome.org/GNOME/eog.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd eog
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

build() {
  arch-meson eog build -D gtk_doc=true -D libportal=false
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_eog() {
  optdepends=('eog-plugins: Additional features')
  groups=(gnome)

  meson install -C build --destdir "$pkgdir"

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/gtk-doc
}

package_eog-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}
