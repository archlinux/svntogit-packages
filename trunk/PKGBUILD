# $Id$
# Maintainer: tobias <tobias funnychar archlinux.org>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>

pkgname=xfce4-settings
pkgver=4.6.5
pkgrel=1
pkgdesc="Settings manager for xfce"
arch=('i686' 'x86_64')
license=('GPL2')
url="http://www.xfce.org/"
groups=('xfce4')
depends=('exo>=0.3.107' 'libnotify>=0.4.5' 'libxfcegui4>=4.6.4' 'libxklavier>=5.0' 'libwnck>=2.30.0' 'tango-icon-theme' 'gtk-engines' 'gtk-xfce-engine')
makedepends=('intltool')
optdepends=('libcanberra: for sound control')
conflicts=('xfce-mcs-manager')
replaces=('xfce-mcs-manager')
source=(http://archive.xfce.org/src/xfce/${pkgname}/4.6/${pkgname}-${pkgver}.tar.bz2
	xfce4-settings-4.6.0.patch)
md5sums=('f988cb732913b5fbc115ad7105d2231e'
         '3046732e5d73c9939e7674602eadb1e2')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  # enable Tango icon theme, clearlooks theme and font hinting by default(taken from Fedora)
  patch -Np1 -i ${srcdir}/xfce4-settings-4.6.0.patch || return 1
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --enable-libxklavier --enable-sound-settings || return 1
  make || return 1
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install || return 1
}
