# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgbase=glibmm
pkgname=(glibmm glibmm-docs)
pkgver=2.51.7
pkgrel=1
pkgdesc="C++ bindings for GLib"
url="http://www.gtkmm.org/"
arch=(i686 x86_64)
license=(LGPL)
depends=(glib2 libsigc++)
makedepends=(git mm-common perl-xml-parser clang)
_commit=9f3e0a8fba8ebab66698652c764ab208783c5d93  # tags/2.51.7^0
source=("git+https://git.gnome.org/browse/glibmm#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgbase
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgbase
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgbase
  ./configure --prefix=/usr --enable-maintainer-mode
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package_glibmm() {
  cd $pkgbase
  sed -i -e 's/^doc_subdirs/#doc_subdirs/' Makefile
  make DESTDIR="$pkgdir" install
}

package_glibmm-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  cd $pkgbase
  make -C docs DESTDIR="$pkgdir" install
}
