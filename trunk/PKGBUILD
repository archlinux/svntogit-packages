# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Juergen Hoetzel <juergen.archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=gsl
pkgver=2.5
pkgrel=1
pkgdesc="The GNU Scientific Library (GSL) is a modern numerical library for C and C++ programmers"
url="http://www.gnu.org/software/gsl/gsl.html"
source=("http://ftp.gnu.org/gnu/gsl/$pkgname-$pkgver.tar.gz"{,.sig})
license=('GPL')
arch=('x86_64')
depends=('glibc')
sha256sums=('0460ad7c2542caaddc6729762952d345374784100223995eb14d614861f2258d'
            'SKIP')
validpgpkeys=(DD61B7FC02790F978360F399245FB74BAE05B3E9) # Patrick Alken <alken@colorado.edu>

build() {
    cd ${pkgname}-${pkgver}
    ./configure --prefix=/usr
    make
}

check() {
    cd ${pkgname}-${pkgver}
    make check
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install
}
