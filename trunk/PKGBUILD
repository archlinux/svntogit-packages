# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>

pkgname=qwt
pkgver=6.1.3
pkgrel=1
pkgdesc="Qt Widgets for Technical Applications"
arch=('i686' 'x86_64')
url="http://qwt.sourceforge.net/"
depends=('qt4')
license=("custom:$pkgname")
source=("http://downloads.sourceforge.net/${pkgname}/$pkgname-$pkgver.tar.bz2" \
        "qwtconfig-archlinux.pri")
sha1sums=('90ec21bc42f7fae270482e1a0df3bc79cb10e5c7'
          '955f3702c5e8a847c545adf46745aade53626555')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # copy our config file to the right place
  cp ${srcdir}/qwtconfig-archlinux.pri qwtconfig.pri
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # build qwt:
  qmake-qt4 qwt.pro
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}

  make INSTALL_ROOT=${pkgdir} install

  # install web designer plugin
  install -d ${pkgdir}/usr/lib/qt4/plugins/designer/
  install -Dm755 designer/plugins/designer/libqwt_designer_plugin.so \
    ${pkgdir}/usr/lib/qt4/plugins/designer/

  # needed by qmake (FS#30407)
  install -d "${pkgdir}"/usr/share/qt4/mkspecs/features/
  install -m644 qwtconfig.pri qwt.prf qwtfunctions.pri \
    "${pkgdir}"/usr/share/qt4/mkspecs/features/

  # move man pages
  mv "${pkgdir}"/usr/share/doc/qwt/man/ "${pkgdir}"/usr/share/

  # install licence
  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/COPYING \
	${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
