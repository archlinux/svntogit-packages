# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libxcomposite
pkgver=0.4.4
pkgrel=3
pkgdesc="X11 Composite extension library"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libxfixes' 'compositeproto')
makedepends=('xorg-util-macros')
source=("${url}/releases/individual/lib/libXcomposite-${pkgver}.tar.bz2")
sha512sums=('b15f8fdbe4cb7488bdad161d73ef8e59ef603c5af92d7b573110ba136d7d4ce3749aac45972bce87662b0310b67e831cffad72c45c15bfff988b5b555d4c884b')

build() {
  cd libXcomposite-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static
  make
}

package() {
  cd libXcomposite-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
