# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=template-glib
pkgver=3.34.1
pkgrel=1
pkgdesc="A templating library for GLib"
url="https://gitlab.gnome.org/GNOME/template-glib"
arch=(x86_64)
license=(LGPL)
depends=(glib2 gobject-introspection-runtime)
makedepends=(git meson vala gobject-introspection gtk-doc)
provides=(libtemplate_glib-1.0.so)
options=(debug)
_commit=5d2b2e9c9fc26e687cb2b0720eaae620d66e54a3  # tags/3.34.1^0
source=("git+https://gitlab.gnome.org/GNOME/template-glib.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build -D enable_gtk_doc=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
