# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-session
pkgver=3.3.92
pkgrel=1
pkgdesc="The GNOME Session Handler"
arch=(i686 x86_64)
license=('GPL' 'LGPL')
depends=('libsystemd' 'dconf' 'gconf' 'gsettings-desktop-schemas' 'gtk3' 'hicolor-icon-theme'
         'json-glib' 'libgl' 'libsm' 'libxtst' 'polkit-gnome' 'startup-notification' 'upower')
makedepends=('intltool' 'mesa' 'xtrans')
optdepends=('consolekit: Seat management in absence of systemd')
options=('!emptydirs')
install=gnome-session.install
url="http://www.gnome.org"
groups=('gnome')
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('d7bc5a4d54981040bdd62cfec9ccef4ff266e24ba10d0015fb6591094039051f')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --libexecdir=/usr/lib/gnome-session \
      --disable-schemas-compile --with-systemd
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
