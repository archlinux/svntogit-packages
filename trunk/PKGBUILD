# $Id$
# Maintainer:  Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Adam Walters <abwalters@abwalters.us>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Rémy Oudompheng <remy@archlinux.org>

pkgname=libatomic_ops
pkgver=7.6.2
pkgrel=1
pkgdesc='Provides semi-portable access to hardware provided atomic memory operations'
arch=(x86_64)
url='https://github.com/ivmai/libatomic_ops'
depends=(glibc)
license=(GPL2 MIT)
options=(!docs)
source=($url/releases/download/v$pkgver/${pkgname}-${pkgver}.tar.gz)
sha256sums=('219724edad3d580d4d37b22e1d7cb52f0006d282d26a9b8681b560a625142ee6')

prepare() {
  cd $pkgname-$pkgver
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-static --enable-shared
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 doc/LICENSING.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
