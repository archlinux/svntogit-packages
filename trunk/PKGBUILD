# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Tobias Powalowski <t.powa@gmx.de>

pkgname=pstoedit
pkgver=3.74
pkgrel=1
pkgdesc="Translates PostScript and PDF graphics into other vector formats"
arch=('x86_64')
url="http://www.pstoedit.net/"
license=('GPL')
depends=('gcc-libs' 'plotutils' 'gd' 'libmagick')
makedepends=('ghostscript')
source=("https://downloads.sourceforge.net/sourceforge/pstoedit/pstoedit-${pkgver}.tar.gz")
sha512sums=('63dddb707a52c1d56f17da837ab7472b501723b032f21670455eb6bf1664fd783820b3496c29ecc19d7b8bc18c294dce5842a10ffed442ef14d1631ae35f18b8')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -e 's|ImageMagick++|Magick++|' -i configure.ac
  autoreconf -vi
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
