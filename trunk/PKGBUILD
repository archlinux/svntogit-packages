# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgbase=glibmm-2.68
pkgname=(glibmm-2.68 glibmm-2.68-docs)
pkgver=2.76.0
pkgrel=1
pkgdesc="C++ bindings for GLib"
url="https://www.gtkmm.org/"
arch=(x86_64)
license=(LGPL)
depends=(glib2 libsigc++-3.0)
makedepends=(git meson mm-common perl-xml-parser clang)
checkdepends=(glib-networking)
options=(!emptydirs)
_commit=d3874b1ec56d6db6b393e94daa1aded0f94d4b8b  # tags/2.76.0^0
source=("git+https://gitlab.gnome.org/GNOME/glibmm.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd glibmm
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd glibmm
}

build() {
  arch-meson glibmm build -D maintainer-mode=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_glibmm-2.68() {
  depends+=(libsigc-3.0.so)
  provides=(libglibmm-2.68.so)

  meson install -C build --destdir "$pkgdir"

  # Split -docs
  mkdir -p docs/usr/share
  mv -t docs/usr/share "$pkgdir"/usr/share/{devhelp,doc}
}

package_glibmm-2.68-docs() {
  pkgdesc+=" (documentation)"
  depends=()
  options=(!strip)

  mv -t "$pkgdir" docs/*
}

# vim:set sw=2 sts=-1 et:
