# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libxxf86vm
pkgver=1.1.4
pkgrel=2
pkgdesc="X11 XFree86 video mode extension library"
arch=('x86_64')
license=('custom')
url="https://xorg.freedesktop.org/"
depends=('libxext' 'xf86vidmodeproto')
makedepends=('xorg-util-macros')
source=(${url}/releases/individual/lib/libXxf86vm-${pkgver}.tar.bz2{,.sig})
sha512sums=('c5f92d86e143db02ebb36bcd25618acaa2cb2831f5a23800a06dd431cd73b6702d95de8fe7407ce626336bf614c288d5256f4d87ea7781fad2ab6c517cbf09e0'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # Alan Coopersmith <alan.coopersmith@oracle.com>

build() {
  cd libXxf86vm-${pkgver}
  ./configure --prefix=/usr --disable-static
  make
}

package()
{
  cd libXxf86vm-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
