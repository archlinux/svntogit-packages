# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Tom Killian <tom.archlinux.org>
# Contributor: Judd Vinet <jvinet.zeroflux.org>

pkgname=dhcpcd
pkgver=6.1.0
pkgrel=1
pkgdesc="RFC2131 compliant DHCP client daemon"
url="http://roy.marples.name/projects/dhcpcd/"
arch=('i686' 'x86_64')
license=('BSD')
groups=('base')
depends=('glibc' 'sh')
provides=('dhcp-client')
backup=('etc/dhcpcd.conf')
options=('emptydirs')  # We Need the Empty /var/lib/dhcpcd Directory
source=("http://roy.marples.name/downloads/$pkgname/$pkgname-$pkgver.tar.bz2"
        'dhcpcd_.service'
        'dhcpcd.service')
sha1sums=('dcd6970db0440398aaf3ae4b58cc262a67b2e472'
          '6f1633edde14d29b5cdc09c4f029a450ef2ebc96'
          '7f3e62908037b888df2f6b87af1c7611f462dd08')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # configure variables
  ./configure \
      --sbindir=/usr/bin \
      --libexecdir=/usr/lib/dhcpcd \
      --dbdir=/var/lib/dhcpcd \
      --rundir=/run

  # Build
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  # Install License
  install -d $pkgdir/usr/share/licenses/$pkgname
  sed 26q "$srcdir/$pkgname-$pkgver/control.h" \
      >>"$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # Set Options in /etc/dhcpcd.conf
  echo noipv4ll >> ${pkgdir}/etc/dhcpcd.conf # Disable ip4vall

  # install systemd files
  install -Dm644 ${srcdir}/dhcpcd_.service ${pkgdir}/usr/lib/systemd/system/dhcpcd@.service
  install -Dm644 ${srcdir}/dhcpcd.service ${pkgdir}/usr/lib/systemd/system/dhcpcd.service # FS#31543
}
