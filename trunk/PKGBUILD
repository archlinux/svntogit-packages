# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=libcdio-paranoia
pkgver=10.2+0.94+2
pkgrel=2
pkgdesc="CD paranoia libraries from libcdio"
arch=('x86_64')
license=('GPL' 'LGPL')
url="https://www.gnu.org/software/libcdio/"
depends=('libcdio')
source=(https://ftp.gnu.org/gnu/libcdio/${pkgname}-${pkgver}.tar.gz{,.sig}
        enable_symbol_versions.patch)
sha256sums=('d60f82ece97eeb92407a9ee03f3499c8983206672c28ae5e4e22179063c81941'
            'SKIP'
            'f84a2467ee7ffd747cea7c783d4d53d5439b69f49d01380d650bea8454ddf5f2')
validpgpkeys=('DAA63BC2582034A02B923D521A8DE5008275EC21') # R. Bernstein <rocky@panix.com>

prepare() {
  cd "${pkgname}-${pkgver}"
  patch -Np1 -i ../enable_symbol_versions.patch
  autoreconf -fi
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr --enable-cpp-progs --disable-static --disable-example-progs
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make -j1 DESTDIR="${pkgdir}" install
}
