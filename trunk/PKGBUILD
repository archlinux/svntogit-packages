# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-date-manip
pkgver=6.51
pkgrel=1
pkgdesc="Date::Manip - date manipulation routines"
arch=('any')
license=('PerlArtistic')
url="http://search.cpan.org/dist/Date-Manip/"
depends=('perl')
makedepends=('perl-module-build>0.4205')
checkdepends=('perl-test-inter')
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/S/SB/SBECK/Date-Manip-${pkgver}.tar.gz")
md5sums=('da85d48de8de695d67d0107a477fd501')

build() {
  cd Date-Manip-${pkgver}
  perl Build.PL installdirs=vendor
  perl Build
}

check() {
  cd Date-Manip-${pkgver}
  perl Build test
}

package() {
  cd Date-Manip-${pkgver}
  perl Build destdir="${pkgdir}" install
}
