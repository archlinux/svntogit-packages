# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-inc-latest
pkgver=0.500
pkgrel=4
pkgdesc="Build, test, and install Perl modules"
arch=('any')
url="https://github.com/dagolden/inc-latest"
license=('Apache')
depends=('perl')
checkdepends=('perl-cpan-meta')
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/D/DA/DAGOLDEN/inc-latest-$pkgver.tar.gz")
sha512sums=('b312d1dfce963322796bc0127f0ecd82c12baacf9e5df40d9acc093221edd80f3696ce6d9f185ed24b21983147363d1d0ff3e273b8b5ce7559a6f16983a1385c')

build() {
  cd inc-latest-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd inc-latest-$pkgver
  make test
}

package() {
  cd inc-latest-$pkgver
  make install DESTDIR="$pkgdir"
}
