# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=ptlib
pkgver=2.10.2
pkgrel=1
pkgdesc="Portable Windows Library"
arch=(i686 x86_64)
url="http://www.ekiga.org/"
license=('GPL')
depends=('gcc-libs' 'openssl' 'alsa-lib' 'sdl' 'expat' 'libpulse' 'v4l-utils')
replaces=('pwlib')
conflicts=('pwlib')
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('a0985848da2ea06a4df804a650fd800ff52a01f889c116595b9a67fc35537140')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --libdir=/usr/lib \
      --sysconfdir=/etc --localstatedir=/var \
      --enable-opal --enable-plugins \
      --enable-oss --enable-v4l2 \
      --disable-avc --enable-sdl \
      --enable-ipv6 --enable-v4l \
      --enable-dc --disable-odbc \
      --enable-expat --enable-httpforms
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
