# $Id$
# Maintainer: judd <jvinet@zeroflux.org>
pkgname=net-tools
pkgver=1.60
pkgrel=17
pkgdesc="Configuration tools for Linux networking"
arch=(i686 x86_64)
license=('GPL')
url="http://www.tazenda.demon.co.uk/phil/net-tools"
depends=('glibc')
source=(http://www.tazenda.demon.co.uk/phil/$pkgname/$pkgname-$pkgver.tar.bz2
  net-tools.patch gcc340.patch net-tools-1.60-2.6-compilefix.patch
  net-tools-1.60-miiioctl.patch
  net-tools-1.60-nameif.patch
  net-tools-1.60-nameif_strncpy.patch
  net-tools.install)
options=(!makeflags)

build() {
  cd $srcdir/$pkgname-$pkgver
  patch -Np1 -i ../net-tools.patch
  patch -Np1 -i ../net-tools-1.60-2.6-compilefix.patch
  patch -Np1 -i ../net-tools-1.60-miiioctl.patch
  patch -Np1 -i ../gcc340.patch
  patch -Np1 -i ${srcdir}/net-tools-1.60-nameif.patch
  patch -Np1 -i ${srcdir}/net-tools-1.60-nameif_strncpy.patch
  yes "" | make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make BASEDIR=$pkgdir update

  # the following is provided by yp-tools and coreutils
  rm "${pkgdir}"/bin/{{,nis,yp}domainname,hostname}
  rm "${pkgdir}"/usr/share/man/man1/{{,nis,yp}domainname,hostname}.1
}
md5sums=('888774accab40217dde927e21979c165'
         '7ef8d0c6818faa0fdeea94970a20e3fb'
         'b52d899cba9956bb0055150506f41ac1'
         '51de6eabe2d6d6dc860f72c41cee636b'
         'c16109863bc63f3dad4ef35305a340bb'
         '29a32617382fab1735acba4d920f1fcd'
         'e66466b9304dac85eb42b32f1ec3b284'
         '43ac5d55526f0b56ba792ee4d6b7ff51')
