# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=grml-zsh-config
pkgver=0.19.2
pkgrel=1
pkgdesc="grml's zsh setup"
arch=('any')
url='https://grml.org/zsh/'
license=('GPL2')
provides=('grmlzshrc')
depends=('zsh' 'coreutils' 'grep' 'sed' 'procps')
makedepends=('txt2tags')
source=("https://deb.grml.org/pool/main/g/grml-etc-core/grml-etc-core_${pkgver}.tar.gz")
sha256sums=('6103a9364eac997a1f3a207dc49929e314b3f855b13dcb5c9989b2af5253eea8')

build() {
	cd "${srcdir}/grml-etc-core-${pkgver}/doc"
	make
}

package() {
	cd "${srcdir}/grml-etc-core-${pkgver}"
	install -D -m644 etc/skel/.zshrc "${pkgdir}/etc/skel/.zshrc"
	install -D -m644 etc/zsh/keephack "${pkgdir}/etc/zsh/keephack"
	install -D -m644 etc/zsh/zshrc "${pkgdir}/etc/zsh/zshrc"

	install -D -m644 doc/grmlzshrc.5 "${pkgdir}/usr/share/man/man5/grmlzshrc.5"
	ln -sf grmlzshrc.5.gz "${pkgdir}/usr/share/man/man5/grml-zsh-config.5.gz"
}
