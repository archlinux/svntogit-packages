# $Id$
# Maintainer: Ronald van Haren <ronald@archlinux.org>
# Contributor: Gustavo Sverzut Barbieri <barbieri@gmail.com>

pkgname=evas_generic_loaders
pkgver=1.7.1
pkgrel=1
pkgdesc="Additional generic loaders for Evas"
arch=('i686' 'x86_64')
url="http://www.enlightenment.org"
license=('BSD')
depends=('librsvg' 'gstreamer0.10' 'poppler' 'libraw' 'libspectre' 'eina')
options=('!libtool' '!emptydirs')
source=(http://download.enlightenment.org/releases/$pkgname-$pkgver.tar.gz)
sha1sums=('f2c82792430c79bfcfbcb879d631c91a01f74f04')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  export CFLAGS="-fvisibility=hidden -ffunction-sections -fdata-sections ${CFLAGS}"
  export CXXFLAGS="-fvisibility=hidden -ffunction-sections -fdata-sections ${CXXFLAGS}"
  export LDFLAGS="-fvisibility=hidden -ffunction-sections -fdata-sections -Wl,--gc-sections -Wl,--as-needed  ${LDFLAGS}"

  ./configure \
    --prefix=/usr \
    --libexecdir=/usr/lib/evas \
    --enable-poppler \
    --enable-spectre \
    --enable-libraw \
    --enable-svg \
    --enable-gstreamer
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  # install license files
  install -Dm644 "$srcdir/$pkgname-$pkgver/COPYING" \
        "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
