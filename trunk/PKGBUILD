# Maintainer: Guillaume ALAUX <guillaume@archlinux.org>
_libname=jline
pkgname=("java-${_libname}")
pkgver=1.0
pkgrel=5
pkgdesc="Java library for handling console input"
arch=('any')
url="http://jline.sourceforge.net/"
license=('BSD')
makedepends=('maven' 'java-environment=8')
checkdepends=('maven')
source=(http://downloads.sourceforge.net/project/${_libname}/${_libname}/${pkgver}/${_libname}-${pkgver}.zip)
md5sums=('811dc1c8a351a2e928524bd79eb9e798')
_M2_REPO=$(mktemp -d)

build() {
  cd "${srcdir}/${_libname}-${pkgver}/src"

  mvn -Dmaven.repo.local=${_M2_REPO} \
    clean \
    package \
    -Dmaven.test.skip=true
}

check() {
  cd "${srcdir}/${_libname}-${pkgver}/src"

  mvn -Dmaven.repo.local=${_M2_REPO} \
    test
}

package() {
  depends=('java-runtime-headless')

  cd "${srcdir}/${_libname}-${pkgver}"

  install -Dm644 src/target/${_libname}-${pkgver}.jar \
                 "${pkgdir}"/usr/share/java/${_libname}-${pkgver}.jar
  ln -s ${_libname}-${pkgver}.jar "${pkgdir}"/usr/share/java/${_libname}.jar

  install -Dm644 LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
