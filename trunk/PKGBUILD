# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Kritoke <kritoke@gamebox.net>

pkgname=filelight
pkgver=1.9rc3
pkgrel=1
pkgdesc="A KDE program that creates a complex, but data-rich graphical representation of the files and directories on your computer"
arch=('i686' 'x86_64')
url="http://methylblue.com/filelight/"
license=('GPL2')
depends=('kdelibs' 'qimageblitz' 'hicolor-icon-theme')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://www.kde-apps.org/CONTENT/content-files/99561-$pkgname-${pkgver}.tgz")
md5sums=('a75cb50bc0b7238507952fb7d1b8d8a8')

build() {
  cd ${srcdir}
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_PREFIX=/usr
  make || return 1
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install || return 1
}
