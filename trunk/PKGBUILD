# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Manolis Tzanidakis

pkgname=perl-net-ssleay
_cpanname=Net-SSLeay
pkgver=1.55
pkgrel=1
pkgdesc="Perl extension for using OpenSSL"
arch=('i686' 'x86_64')
license=('custom:BSD')
url="http://search.cpan.org/dist/${_cpanname}/"
depends=('openssl')
options=(!emptydirs)
replaces=('net-ssleay')
provides=('net-ssleay')
source=("http://www.cpan.org/authors/id/M/MI/MIKEM/${_cpanname}-${pkgver}.tar.gz")
md5sums=('473b8d66ca69d5784bb0e428721f58e0')

build() {
  cd ${srcdir}/${_cpanname}-${pkgver}

  # install module in vendor directories.
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL -- INSTALLDIRS=vendor 
  make
}

package() {
  cd ${srcdir}/${_cpanname}-${pkgver}

  make install DESTDIR=${pkgdir} 

  # remove perllocal.pod and .packlist
  find ${pkgdir} -name perllocal.pod -delete
  find ${pkgdir} -name .packlist -delete

  install -D -m644 README "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
