# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=tk
pkgver=8.6.9
pkgrel=2
pkgdesc="A windowing toolkit for use with tcl"
arch=('x86_64')
url="http://tcl.sourceforge.net/"
license=('custom')
depends=("tcl=${pkgver}" 'libxss' 'libxft')
options=('staticlibs')
source=(http://downloads.sourceforge.net/sourceforge/tcl/tk${pkgver}-src.tar.gz)
sha256sums=('d3f9161e8ba0f107fe8d4df1f6d3a14c30cc3512dfc12a795daa367a27660dac')

build() {
  cd tk${pkgver}/unix
  ./configure --prefix=/usr --mandir=/usr/share/man --enable-threads --disable-rpath --enable-64bit
  make
}

check() {
  cd tk${pkgver}/unix
#  make test
}

package() {
  cd tk${pkgver}/unix
  make INSTALL_ROOT="${pkgdir}" install install-private-headers
  ln -sf wish${pkgver%.*} "${pkgdir}/usr/bin/wish"
  ln -sf libtk${pkgver%.*}.so "${pkgdir}/usr/lib/libtk.so"
  install -Dm644 license.terms "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # install private headers (FS#14388, FS#47616)
  cd ..
  for dir in compat generic generic/ttk unix; do
    install -dm755 "${pkgdir}"/usr/include/tk-private/$dir
    install -m644 -t "${pkgdir}"/usr/include/tk-private/$dir $dir/*.h
  done

  # remove buildroot traces
  sed -e "s#${srcdir}/tk${pkgver}/unix#/usr/lib#" \
      -e "s#${srcdir}/tk${pkgver}#/usr/include#" \
      -i "${pkgdir}/usr/lib/tkConfig.sh"
}
