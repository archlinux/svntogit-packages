# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kcontacts
pkgver=19.08.1
pkgrel=1
pkgdesc="Address book API for KDE"
arch=(x86_64)
url="https://kontact.kde.org"
license=(LGPL)
depends=(kcoreaddons kconfig ki18n kcodecs iso-codes)
makedepends=(extra-cmake-modules doxygen qt5-tools qt5-doc)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('020177eb155d3df44314e89da1824916d125aab48131fce76c2131b40eae8f39'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
