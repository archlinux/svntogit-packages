# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Kewl <xrjy@nygb.rh.bet(rot13)>
# Contributor: Justin Settle <jus10 at partlycloudy dot org>

pkgname=ttf-caladea
pkgver=20130214
pkgrel=2
pkgdesc="Google's Caladea font"
arch=(any)
license=(Apache)
url='https://code.google.com/p/chromium/issues/detail?id=168879'
provides=(google-crosextra-caladea-fonts)
source=("https://commondatastorage.googleapis.com/chromeos-localmirror/distfiles/crosextrafonts-$pkgver.tar.gz"
        62-google-crosextra-caladea.conf)
sha256sums=('c48d1c2fd613c9c06c959c34da7b8388059e2408d2bb19845dc3ed35f76e4d09'
            'cd61569289d7a188636df05addd3a744808de38b3d492b3e87c7a060bce4a2e6')

package() {
  cd crosextrafonts-$pkgver

  install -Dt "$pkgdir/usr/share/fonts/${pkgname#ttf-}" -m644 *.ttf

  install -Dt "$pkgdir/etc/fonts/conf.avail" -m644 ../*.conf

  mkdir "$pkgdir/etc/fonts/conf.d"
  ln -srt "$pkgdir/etc/fonts/conf.d" "$pkgdir"/etc/fonts/conf.avail/*
}
