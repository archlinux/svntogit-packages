# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgbase=pipewire
pkgname=(
  pipewire
  alsa-card-profiles
  pipewire-docs
  pipewire-audio
  pipewire-alsa
  pipewire-jack
  pipewire-pulse
  gst-plugin-pipewire
  pipewire-zeroconf
  pipewire-v4l2
  pipewire-x11-bell
)
_commit=5ff00f40b9d3ecfc38305edc27e9c2b01629802c  # tags/0.3.63
pkgver=0.3.63
pkgrel=6
epoch=1
pkgdesc="Low-latency audio/video router and processor"
url="https://pipewire.org"
arch=(x86_64)
license=(MIT)
makedepends=(
  alsa-lib
  avahi
  bluez-libs
  dbus
  doxygen
  git
  glib2
  graphviz
  gst-plugins-base
  libcamera
  libcanberra
  libfdk-aac
  libfreeaptx
  liblc3
  libldac
  libpulse
  libsndfile
  libusb
  libx11
  libxfixes
  lilv
  meson
  ncurses
  opus
  python-docutils
  readline
  rtkit
  sbc
  sdl2
  systemd
  valgrind
  webrtc-audio-processing
)
checkdepends=(desktop-file-utils)
options=(debug)
source=(git+https://gitlab.freedesktop.org/pipewire/pipewire.git#commit=$_commit)
sha256sums=('SKIP')

pkgver() {
  cd pipewire
  git describe --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd pipewire

  # https://gitlab.freedesktop.org/pipewire/pipewire/-/issues/2893
  git cherry-pick -n 89ac6b353521fb9a6d6eb6bb74724c4fa968f75c

  # https://gitlab.freedesktop.org/pipewire/pipewire/-/issues/2898
  git cherry-pick -n c7b3ef0d9ff16a1e69a299870860bebcb628e298

  # https://gitlab.freedesktop.org/pipewire/pipewire/-/issues/2925
  git cherry-pick -n 9f3237b74edc3df467cca50af7c09f1e3c89abea

  # https://gitlab.freedesktop.org/pipewire/pipewire/-/issues/2928
  git cherry-pick -n 624e265fd6e5027e11027822eb943c533feab85f

  # remove export of LD_LIBRARY_PATH for pw-jack as it would add /usr/lib
  sed -i '/LD_LIBRARY_PATH/d' pipewire-jack/src/pw-jack.in
}

build() {
  local meson_options=(
    -D bluez5-codec-lc3=enabled
    -D bluez5-codec-lc3plus=disabled
    -D docs=enabled
    -D jack-devel=true
    -D jack=disabled
    -D libjack-path=/usr/lib
    -D roc=disabled
    -D session-managers=[]
    -D udevrulesdir=/usr/lib/udev/rules.d
  )

  arch-meson pipewire build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

_ver=${pkgver:0:3}

package_pipewire() {
  license+=(LGPL)  # libspa-alsa
  depends=(
    libcamera-base.so
    libcamera.so
    libdbus-1.so
    libglib-2.0.so
    libncursesw.so
    libreadline.so
    libsystemd.so
    libudev.so
  )
  optdepends=(
    'gst-plugin-pipewire: GStreamer plugin'
    'pipewire-alsa: ALSA configuration'
    'pipewire-audio: Audio support'
    'pipewire-docs: Documentation'
    'pipewire-jack: JACK support'
    'pipewire-pulse: PulseAudio replacement'
    'pipewire-session-manager: Session manager'
    'pipewire-v4l2: V4L2 interceptor'
    'pipewire-x11-bell: X11 bell'
    'pipewire-zeroconf: Zeroconf support'
    'realtime-privileges: realtime privileges with rt module'
    'rtkit: realtime privileges with rtkit module'
  )
  provides=(libpipewire-$_ver.so)
  install=pipewire.install

  meson install -C build --destdir "$pkgdir"

  (
    cd "$pkgdir"

    # Replace copies with symlinks
    cmp usr/bin/pipewire usr/bin/pipewire-avb
    ln -sf pipewire usr/bin/pipewire-avb

    cmp usr/bin/pipewire usr/bin/pipewire-pulse
    ln -sf pipewire usr/bin/pipewire-pulse

    _pick acp usr/lib/udev
    _pick acp usr/share/alsa-card-profile

    _pick docs usr/share/doc

    _pick audio usr/bin/pw-{cat,play,record,midi{play,record},dsdplay}
    _pick audio usr/bin/pw-{loopback,mididump}
    _pick audio usr/bin/spa-{acp-tool,resample}
    _pick audio usr/lib/alsa-lib
    _pick audio usr/lib/pipewire-$_ver/libpipewire-module-echo-cancel.so
    _pick audio usr/lib/pipewire-$_ver/libpipewire-module-fallback-sink.so
    _pick audio usr/lib/pipewire-$_ver/libpipewire-module-filter-chain.so
    _pick audio usr/lib/pipewire-$_ver/libpipewire-module-loopback.so
    _pick audio usr/lib/pipewire-$_ver/libpipewire-module-pipe-tunnel.so
    _pick audio usr/lib/pipewire-$_ver/libpipewire-module-protocol-simple.so
    _pick audio usr/lib/spa-0.2/{aec,alsa,audio*,bluez5}
    _pick audio usr/lib/systemd/user/filter-chain.service
    _pick audio usr/share/alsa
    _pick audio usr/share/man/man1/pw-{cat,mididump}.1
    _pick audio usr/share/pipewire/filter-chain*
    _pick audio usr/share/spa-0.2/bluez5

    _pick jack usr/bin/pw-jack
    _pick jack usr/include/jack
    _pick jack usr/lib/libjack*
    _pick jack usr/lib/pkgconfig/jack.pc
    _pick jack usr/share/man/man1/pw-jack.1
    _pick jack usr/share/pipewire/jack.conf

    _pick pulse usr/bin/pipewire-pulse
    _pick pulse usr/lib/pipewire-$_ver/libpipewire-module-protocol-pulse.so
    _pick pulse usr/lib/pipewire-$_ver/libpipewire-module-pulse-tunnel.so
    _pick pulse usr/lib/systemd/user/pipewire-pulse.*
    _pick pulse usr/share/man/man1/pipewire-pulse.1
    _pick pulse usr/share/pipewire/pipewire-pulse.conf

    _pick gst usr/lib/gstreamer-1.0

    _pick zeroconf usr/lib/pipewire-$_ver/libpipewire-module-{raop,zeroconf}-*.so

    _pick v4l2 usr/bin/pw-v4l2 usr/lib/pipewire-$_ver/v4l2

    _pick x11-bell usr/lib/pipewire-$_ver/libpipewire-module-x11-bell.so

    # directories for overrides
    mkdir -p etc/pipewire/{client-rt,client,minimal,pipewire}.conf.d
  )

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 pipewire/COPYING
}

package_alsa-card-profiles() {
  pkgdesc+=" - ALSA card profiles"
  license=(LGPL)

  mv acp/* "$pkgdir"
}

package_pipewire-docs() {
  pkgdesc+=" - documentation"

  mv docs/* "$pkgdir"

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 pipewire/COPYING
}

package_pipewire-audio() {
  pkgdesc+=" - Audio support"
  depends=(
    alsa-card-profiles
    libasound.so
    libbluetooth.so
    libfdk-aac.so
    libfreeaptx.so
    liblc3.so
    libldacBT_enc.so
    liblilv-0.so
    libopus.so
    libpipewire-$_ver.so
    libsbc.so
    libsndfile.so
    libusb-1.0.so
    libwebrtc_audio_processing.so
    pipewire
  )

  mv audio/* "$pkgdir"

  mkdir -p "$pkgdir/etc/alsa/conf.d"
  ln -st "$pkgdir/etc/alsa/conf.d" \
    /usr/share/alsa/alsa.conf.d/50-pipewire.conf

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 pipewire/COPYING
}

package_pipewire-alsa() {
  pkgdesc+=" - ALSA configuration"
  depends=(
    pipewire-audio
    pipewire-session-manager
  )
  conflicts=(pulseaudio-alsa)
  provides=(pulseaudio-alsa)

  mkdir -p "$pkgdir/etc/alsa/conf.d"
  ln -st "$pkgdir/etc/alsa/conf.d" \
    /usr/share/alsa/alsa.conf.d/99-pipewire-default.conf

  install -Dm644 /dev/null \
    "$pkgdir/usr/share/pipewire/media-session.d/with-alsa"

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 pipewire/COPYING
}

package_pipewire-jack() {
  pkgdesc+=" - JACK support"
  license+=(GPL2)  # libjackserver
  depends=(
    libpipewire-$_ver.so
    pipewire-audio
    pipewire-session-manager
    sh
  )
  optdepends=('jack-example-tools: for official JACK example-clients and tools')
  conflicts=(jack jack2)
  provides=(jack libjack.so libjackserver.so libjacknet.so)

  mv jack/* "$pkgdir"

  install -Dm644 /dev/null \
    "$pkgdir/usr/share/pipewire/media-session.d/with-jack"

  # directories for overrides
  mkdir -p "$pkgdir/etc/pipewire/jack.conf.d"

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 pipewire/COPYING
}

package_pipewire-pulse() {
  pkgdesc+=" - PulseAudio replacement"
  depends=(
    libavahi-{client,common}.so
    libglib-2.0.so
    libpipewire-$_ver.so
    libpulse.so
    pipewire-audio
    pipewire-session-manager
  )
  provides=(pulseaudio pulseaudio-bluetooth)
  conflicts=(pulseaudio pulseaudio-bluetooth)
  install=pipewire-pulse.install

  mv pulse/* "$pkgdir"

  # directory for overrides
  mkdir -p "$pkgdir/etc/pipewire/pipewire-pulse.conf.d"

  install -Dm644 /dev/null \
    "$pkgdir/usr/share/pipewire/media-session.d/with-pulseaudio"

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 pipewire/COPYING
}

package_gst-plugin-pipewire() {
  pkgdesc="Multimedia graph framework - pipewire plugin"
  depends=(
    gst-plugins-base-libs
    libpipewire-$_ver.so
    pipewire-session-manager
  )

  mv gst/* "$pkgdir"

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 pipewire/COPYING
}

package_pipewire-zeroconf() {
  pkgdesc+=" - Zeroconf support"
  depends=(
    libavahi-{client,common}.so
    libpipewire-$_ver.so
    openssl
    pipewire
  )

  mv zeroconf/* "$pkgdir"

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 pipewire/COPYING
}

package_pipewire-v4l2() {
  pkgdesc+=" - V4L2 interceptor"
  depends=(
    libpipewire-$_ver.so
    pipewire-session-manager
    sh
  )

  mv v4l2/* "$pkgdir"

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 pipewire/COPYING
}

package_pipewire-x11-bell() {
  pkgdesc+=" - X11 bell"
  depends=(
    libcanberra.so
    libpipewire-$_ver.so
    libx11
    libxfixes
    pipewire
  )

  mv x11-bell/* "$pkgdir"

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 pipewire/COPYING
}

# vim:set sw=2 sts=-1 et:
