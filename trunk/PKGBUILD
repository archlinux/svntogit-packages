# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=messagelib
pkgver=21.04.1
pkgrel=1
pkgdesc='KDE PIM messaging library'
arch=(x86_64)
url='https://kontact.kde.org'
license=(LGPL)
depends=(kmailtransport libgravatar kidentitymanagement kmbox qca-qt5)
makedepends=(extra-cmake-modules boost)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        kdebug-282792.patch::https://invent.kde.org/pim/messagelib/commit/53daeb84.patch)
sha256sums=('9b6a97cc1348591d0dbdf7b01f3a6216aaa86ca5c9667f8ae2b1dc398178c7ec'
            'SKIP'
            'd4739f277fde5ec33625e47087d3cea6294331ae342b078368f2d7eac9861acc')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

prepare() {
  patch -d $pkgname-$pkgver -p1 < kdebug-282792.patch # Fix deletion of multiple attachments
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
