# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=xfce4-netload-plugin
pkgver=1.3.1
pkgrel=2
pkgdesc="A netload plugin for the Xfce panel"
arch=('x86_64')
url="http://goodies.xfce.org/projects/panel-plugins/xfce4-netload-plugin"
license=('GPL')
groups=('xfce4-goodies')
depends=('xfce4-panel')
makedepends=('intltool')
source=(https://archive.xfce.org/src/panel-plugins/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=('99762781099d1e0ab9aa6a7b30c2bd94d8f658dbe61c760410d5d42d0766391c')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
