# $Id$
# Maintainer: Jürgen Hötzel <juergen@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>
# Contributor: Kevin Piche <kevin@archlinux.org>

pkgname=bigloo
_pkgver=3.9a
pkgver=${_pkgver/-/_}
pkgrel=1
pkgdesc="Fast scheme compiler"
arch=('i686' 'x86_64')
url="http://www-sop.inria.fr/mimosa/fp/Bigloo/"
license=('GPL' 'LGPL')
depends=()
makedepends=('java-environment' 'emacs' 'zip' 'sqlite' 'alsa-lib' 'flac' 'avahi')
optdepends=('emacs' 'java-environment' 'zip' 'sqlite' 'alsa-lib' 'flac' 'avahi')
options=('!makeflags')
install=bigloo.install
source=(ftp://ftp-sop.inria.fr/indes/fp/Bigloo/${pkgname}${_pkgver}.tar.gz)
md5sums=('7091f6f54cb5f63768a8152085d1bd02')

build() {
  cd "${srcdir}/${pkgname}${_pkgver}"
  ./configure --prefix=/usr \
    --enable-ssl \
    --enable-sqlite \
    --enable-avahi \
    --enable-flac \
    --disable-gstreamer \
    --mandir=/usr/share/man \
    --infodir=/usr/share/info \
    --docdir=/usr/share/doc/bigloo \
    --jvm=yes
  make build compile-bee
}

check() {
  cd "${srcdir}/${pkgname}${_pkgver}"
  make test
}

package() {
  cd "${srcdir}/${pkgname}${_pkgver}"
  make DESTDIR="${pkgdir}" install install-bee
  make -C manuals DESTDIR="${pkgdir}" install-bee
  chmod 644 "${pkgdir}"/usr/lib/bigloo/${_pkgver%-*}/*.a
}
