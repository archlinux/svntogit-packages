# Contributor: Jan de Groot <jgc@archlinux.org>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=xorg-xev
pkgver=1.2.2
pkgrel=2
pkgdesc='Print contents of X events'
url='https://xorg.freedesktop.org/'
arch=('x86_64')
license=('custom')
depends=('libx11' 'libxrandr')
makedepends=('xorg-util-macros' 'xproto')
source=(https://xorg.freedesktop.org/archive/individual/app/xev-${pkgver}.tar.bz2{,.sig})
sha256sums=('d94ae62a6c1af56c2961d71f5782076ac4116f0fa4e401420ac7e0db33dc314f'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # Alan Coopersmith <alan.coopersmith@oracle.com>
groups=('xorg-apps' 'xorg')

build() {
	cd xev-${pkgver}
	./configure --prefix=/usr
	make
}

package() {
	cd xev-${pkgver}
	make DESTDIR="${pkgdir}" install
	install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
