# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=geocode-glib
pkgver=3.25.4.1
pkgrel=1
pkgdesc="Helper library for geocoding services"
url="https://git.gnome.org/browse/geocode-glib/"
arch=(i686 x86_64)
license=(LGPL)
depends=(glib2 json-glib libsoup)
makedepends=(intltool gobject-introspection git gtk-doc meson)
options=(!emptydirs)
_commit=a811227fce460f3ac319f689ff927ab37427b84b  # tags/3.25.4.1^0
source=("git+https://git.gnome.org/browse/geocode-glib#commit=$_commit")
sha256sums=('SKIP')

check() {
  cd build
  # Needs sv_SE.utf8, will not pass in chroot
  #meson test
}

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  mkdir build
  cd $pkgname
  sed -i 's/gnome/Adwaita/' icons/meson.build
}

build() {
  cd build
  meson setup --prefix=/usr --buildtype=release -Denable-installed-tests=false ../$pkgname
  ninja
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}

