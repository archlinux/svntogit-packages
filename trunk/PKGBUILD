# Packager: Justin "juster" Davis <jrcd83@gmail.com>
# $Id$

pkgname=perl-mp3-info
pkgver=1.24
pkgrel=3
pkgdesc="Manipulate / fetch info from MP3 audio files"
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=('perl')
url=https://metacpan.org/release/MP3-Info
source=("http://search.cpan.org/CPAN/authors/id/D/DA/DANIEL/MP3-Info-${pkgver}.tar.gz")
md5sums=(fb4bcc43764e3b46c3a7c74b50ed7a44)
sha512sums=(5cefb01b369921d49a5db9b5456e29054b993d8ef083dfa3a66b3a10b1a409162f7329f8dcf5ed96966651de5de093f4e5325f545559622d034e39ad33589701)
_distdir="${srcdir}/MP3-Info-${pkgver}"

build()
(
    export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor"     \
      PERL_MB_OPT="--installdirs vendor" \
      MODULEBUILDRC=/dev/null

    cd "$_distdir"
    /usr/bin/perl Makefile.PL
    make
)

check()
(
    export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    cd "$_distdir"
    make test
)

package() {
    cd "$_distdir"
    make DESTDIR="$pkgdir" install
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
