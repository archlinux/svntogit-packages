# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: riai <riai@bigfoot.com>, Ben <ben@benmazer.net>

pkgbase=sip
pkgname=('sip' 'python-sip' 'python2-sip')
pkgver=4.16.3
pkgrel=1
arch=('i686' 'x86_64')
url='http://www.riverbankcomputing.com/software/sip/intro'
license=('custom:"sip"')
makedepends=('python' 'python2')
source=("http://downloads.sourceforge.net/pyqt/${pkgbase}-${pkgver}.tar.gz")
md5sums=('884d60b6f17fc5b910da4665caf6fd85')

build() {
  cp -r ${pkgbase}-${pkgver} python2-${pkgbase}-${pkgver}

  cd ${pkgbase}-${pkgver}
  python configure.py CFLAGS="${CFLAGS}" LFLAGS="${LDFLAGS}"
  make

  ### Python2 version ###
  cd ../python2-${pkgbase}-${pkgver}
  python2 configure.py CFLAGS="${CFLAGS}" LFLAGS="${LDFLAGS}"
  make
}

package_sip() {
  pkgdesc="A tool that makes it easy to create Python bindings for C and C++ libraries"
  depends=('glibc')

  cd ${pkgbase}-${pkgver}
  make DESTDIR="${pkgdir}" install -C sipgen

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python-sip() {
  pkgdesc="Python 3.x SIP bindings for C and C++ libraries"
  depends=('sip' 'python')

  cd ${pkgbase}-${pkgver}
  make DESTDIR="${pkgdir}" install -C siplib

  install -Dm644 sipconfig.py "${pkgdir}"/usr/lib/python3.4/site-packages/sipconfig.py
  install -Dm644 sipdistutils.py "${pkgdir}"/usr/lib/python3.4/site-packages/sipdistutils.py

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-sip() {
  pkgdesc="Python 2.x SIP bindings for C and C++ libraries"
  depends=('sip' 'python2')

  cd python2-${pkgbase}-${pkgver}
  make DESTDIR="${pkgdir}" install -C siplib

  install -Dm644 sipconfig.py "${pkgdir}"/usr/lib/python2.7/site-packages/sipconfig.py
  install -Dm644 sipdistutils.py "${pkgdir}"/usr/lib/python2.7/site-packages/sipdistutils.py

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
