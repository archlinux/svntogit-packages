# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=tokodon
pkgver=23.02.0
pkgrel=1
pkgdesc='A Mastodon client for Plasma'
license=(GPL3)
arch=(x86_64)
url='https://apps.kde.org/tokodon'
depends=(qqc2-desktop-style kirigami-addons kdbusaddons knotifications kitemmodels kio qtkeychain-qt5 qt5-websockets hicolor-icon-theme)
makedepends=(extra-cmake-modules)
source=(https://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('33622ee42e81bcc716fe820d7da35cce40f1d621bdb4a9d2fa1c35b4e92f15a7'
            'SKIP')
validpgpkeys=(39FFA93CAE9C6AFC212AD00202325448204E452A  # Carl Schwan <carl@carlschwan.eu>
              B3ED0BAE371BEDA00717921D18F4795C1CE7042B  # Carl Schwan <carl@carlschwan.eu>
              0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D) # Bhushan Shah (mykolab address) <bshah@mykolab.com>

build() {
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
