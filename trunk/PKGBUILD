# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgbase=icedtea-web
pkgname=('icedtea-web' 'icedtea-web-doc')
pkgver=1.2.1
pkgrel=1
arch=('i686' 'x86_64')
url="http://icedtea.classpath.org/wiki/IcedTea-Web"
license=('GPL2')
makedepends=('openjdk6' 'zip' 'gtk2' 'npapi-sdk' 'rhino' 'junit')
source=(http://icedtea.classpath.org/download/source/$pkgname-$pkgver.tar.gz)
sha256sums=('134efcd429086a643ba03ec6e4da991527c3e5dfcd6ed6680a83824ad3f0cfd6')

  _javaver=6
  _jvmdir=/usr/lib/jvm/java-${_javaver}-openjdk

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr/lib/jvm/java-6-openjdk \
      --datarootdir=/usr/share
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  # as more tests have been added some are expectged to fail
  # see http://mail.openjdk.java.net/pipermail/distro-pkg-dev/2012-March/017566.html
  make -k check || /bin/true
}


package_icedtea-web() {

  pkgdesc="provides a Free Software web browser plugin running applets written in the Java programming language and an implementation of Java Web Start, originally based on the NetX project"
  depends=('openjdk6' 'gtk2' 'desktop-file-utils')
  install=$pkgname.install

  cd "$srcdir/$pkgname-$pkgver"
  # possible make target (see bottom of Makefile.am: install-exec-local install-data-local
  make DESTDIR="$pkgdir" install-exec-local install-data-local

  # Install desktop files.
  install -m755 -d ${pkgdir}/usr/share/{applications,pixmaps}
  install -m644 javaws.png ${pkgdir}/usr/share/pixmaps
  install -m644 {javaws,itweb-settings}.desktop ${pkgdir}/usr/share/applications
  # remove splitted doc files
  rm -rf ${pkgdir}/usr/share/doc

  # link binaries into /usr/bin + jre/bin
  install -m755 -d ${pkgdir}/usr/bin
  install -m755 -d ${pkgdir}/${_jvmdir}/jre/bin
  pushd ${pkgdir}/${_jvmdir}/bin
  for file in *; do
    ln -sf ${_jvmdir}/bin/${file} ${pkgdir}/usr/bin
    ln -sf ${_jvmdir}/bin/${file} ${pkgdir}/${_jvmdir}/jre/bin
  done
  popd

  # link the mozilla-plugin - test it here http://www.java.com/en/download/help/testvm.xml
  install -m755 -d ${pkgdir}/usr/lib/mozilla/plugins/
  ln -sf ${_jvmdir}/lib/IcedTeaPlugin.so ${pkgdir}/usr/lib/mozilla/plugins/
}

package_icedtea-web-doc() {

  pkgdesc="icedtea-web browser plugin + Java WebStart - documentation files"

  cd "$srcdir/$pkgbase-$pkgver"
  make DESTDIR="$pkgdir" install-data-local
  # remove javaws about and man page
  rm -rf ${pkgdir}/usr/lib
  rm -rf ${pkgdir}/usr/share/man
  rm -rf ${pkgdir}/usr/share/icedtea-web # conflicting and unneeded file it seems
}
