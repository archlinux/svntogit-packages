# $Id$
# Maintainer: Hugo Doria <hugo@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=mpg123
pkgver=1.12.3
pkgrel=1
pkgdesc="A console based real time MPEG Audio Player for Layer 1, 2 and 3"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/mpg123"
license=('GPL2' 'LGPL2.1')
depends=('glibc' 'libtool' 'alsa-lib')
makedepends=('esound' 'sdl')
optdepends=('esound: for additional audio support' 'sdl: for additional audio support')
conflicts=('mpg321')
provides=('mpg321')
replaces=('mpg321')
options=('libtool')
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.bz2)
md5sums=('c1568be2d1715786bd6b1a89ec6fa7ef')
sha1sums=('5e92d3c918f6095264089f711a9f38a5d2168b31')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --with-audio="alsa esd sdl"
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
