# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-music
pkgver=3.34.4
pkgrel=1
epoch=1
pkgdesc="Music player and management application"
url="https://wiki.gnome.org/Apps/Music"
arch=(x86_64)
license=(GPL)
depends=(grilo grilo-plugins tracker-miners libdmapsharing libmediaart gtk3 gvfs
         python-gobject python-cairo gst-plugins-base python-requests libdazzle)
makedepends=(gobject-introspection git meson yelp-tools appstream-glib)
optdepends=('gst-plugins-good: Extra media codecs'
            'gst-plugins-ugly: Extra media codecs'
            'gst-plugins-bad: Extra media codecs'
            'gst-libav: Extra media codecs')
groups=(gnome)
_commit=e73db52b3cc84d9f1135841b06218abbde62fc3f  # tags/3.34.4^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-music.git#commit=$_commit"
        "git+https://gitlab.gnome.org/GNOME/libgd.git"
        "git+https://gitlab.gnome.org/mschraal/gfm.git")
sha256sums=('SKIP'
            'SKIP'
            'SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname

  git submodule init
  git submodule set-url subprojects/libgd "$srcdir/libgd"
  git submodule set-url subprojects/gfm "$srcdir/gfm"
  git submodule update
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  # Screenshots too large?
  meson test -C build --print-errorlogs || :
}

package() {
  DESTDIR="$pkgdir" meson install -C build

  python -m compileall -d /usr/lib "$pkgdir/usr/lib"
  python -O -m compileall -d /usr/lib "$pkgdir/usr/lib"
}
