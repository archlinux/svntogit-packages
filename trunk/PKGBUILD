# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: dracorp aka Piotr Rogoza <piotr.r.public at gmail.com>

pkgname=kcolorpicker
pkgver=0.1.4
pkgrel=2
pkgdesc='Qt based Color Picker with popup menu'
arch=(x86_64)
url='https://github.com/DamirPorobic/kColorPicker'
license=(GPL)
makedepends=(extra-cmake-modules)
depends=(qt5-base)
source=($pkgname-$pkgver.tar.gz::"https://github.com/ksnip/kColorPicker/archive/v$pkgver.tar.gz")
sha256sums=('3036d00cce2a8cc6a0bc87a9a3ff629e21adbdaa509e41f23a2ef1661d537c72')

build() {
  cmake -B build -S kColorPicker-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_SHARED_LIBS=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
