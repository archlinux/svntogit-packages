# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kiten
pkgver=16.04.1
pkgrel=1
pkgdesc="Japanese Reference/Study Tool"
url="http://kde.org/applications/education/kiten/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdeedu')
depends=('khtml' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'python' 'kdoctools')
conflicts=('kdeedu-kiten')
replaces=('kdeedu-kiten')
source=("http://download.kde.org/stable/applications/${pkgver}/src/kiten-${pkgver}.tar.xz")
sha1sums=('2219e63521528fee69c6e5a6ee207a25bb577657')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
