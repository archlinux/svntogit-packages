# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>

pkgname=xfconf
pkgver=4.8.0
pkgrel=2
pkgdesc="A simple client-server configuration storage and query system"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('GPL2')
groups=('xfce4')
# http://www.xfce.org/documentation/requirements
# keep dbus and gtk2, see also http://bugs.archlinux.org/task/14536
depends=('libxfce4util' 'dbus-glib' 'dbus' 'gtk2')
makedepends=('pkgconfig' 'perl-extutils-depends' 'perl-extutils-pkgconfig'
             'glib-perl' 'intltool' 'gtk-doc' 'chrpath')
options=('!libtool' '!emptydirs')
source=(http://archive.xfce.org/src/xfce/$pkgname/4.8/$pkgname-$pkgver.tar.bz2)
sha1sums=('3f560b11d618171805bfb9e6a8290185c7ee5dcd')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/xfce4 \
    --localstatedir=/var \
    --disable-static \
    --enable-gtk-doc \
    --with-perl-options=INSTALLDIRS="vendor" \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  # remove unneeded dynloader bootstrap file
  rm "$pkgdir/usr/lib/perl5/vendor_perl/auto/Xfce4/Xfconf/Xfconf.bs"

  # fix insecure rpath, http://bugs.archlinux.org/task/19980
  chrpath -d "$pkgdir/usr/lib/perl5/vendor_perl/auto/Xfce4/Xfconf/Xfconf.so"
}

# vim:set ts=2 sw=2 et:
