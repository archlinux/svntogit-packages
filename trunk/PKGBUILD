# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan De Groot <jgc@archlinux.org>

pkgname=gucharmap
pkgver=14.0.1
pkgrel=1
pkgdesc="Gnome Unicode Charmap"
url="https://wiki.gnome.org/Apps/Gucharmap"
arch=(x86_64)
license=(GPL)
depends=(dconf gtk3)
makedepends=(gtk-doc gobject-introspection yelp-tools appdata-tools git meson
             unicode-character-database unzip vala)
provides=(libgucharmap_2_90.so)
_commit=3efb47b75b61d7c6db7e48d72f7aa8c4d4365011  # tags/14.0.1^0
source=("git+https://gitlab.gnome.org/GNOME/gucharmap.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build -D ucd_path=/usr/share/unicode
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
