# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=alsa-utils
pkgver=1.2.5
pkgrel=3
pkgdesc="Advanced Linux Sound Architecture - Utilities"
arch=('x86_64')
url="https://www.alsa-project.org"
license=('GPL2')
depends=('glibc' 'pciutils' 'psmisc')
makedepends=('alsa-lib' 'docbook-xsl' 'fftw' 'libsamplerate' 'ncurses'
'python-docutils' 'systemd' 'xmlto')
optdepends=('fftw: for alsabat')
# we require /var/lib/alsa for state file
options=(emptydirs)
source=("https://www.alsa-project.org/files/pub/utils/${pkgname}-${pkgver}.tar.bz2"{,.sig}
        "${pkgname}-1.2.5-nested_iteration.patch::https://github.com/alsa-project/alsa-utils/commit/78212445de4c8e07873cbc7dff2abcacd031f151.patch"
        "${pkgname}-1.2.5-alsactl_load_state1.patch::https://github.com/alsa-project/alsa-utils/commit/f138117a457944ca73776bd752693d64ec207e55.patch"
        "${pkgname}-1.2.5-alsactl_load_state2.patch::https://github.com/alsa-project/alsa-utils/commit/6018c2014ac24877b2aa58c2c57f2fd901e6c7b1.patch"
)
sha512sums=('0e319e553c9c209a46f5c83c50007bd82acf1c9cf78649978b3ab6fbb4349f51c1c7c6a975a8c24d25ba07ad5325e74d2c34dc5c89180998a0f63d1326fde8b1'
            'SKIP'
            '0cdc8c41b70be714515f355a630fc28b68b95cb7b5a5cbb58c1b288ee0edf3128eb8f80bb874844cf11268c89bb04e82f1fe8c4c1ebb7cd7334d28f2896d5a85'
            '1da95dd657ac66886b821edc6604833a1c35e68e41eb56419b3b68b10b6bb9d5b7177bc445b1cfd71bb1aa0bb434cff151affa1d5da47841c2c814368716dab2'
            'c67e2d0df9a1281f91f15dc0164c8fa123003248afd490707071516fb865aa34d0ba5de3024859bd1f449482563582869a568a85bbf63795b98791b0b2599c4c')
b2sums=('52cbe978390e1b5600bf7422e67ece1d24a946d99acddbc059bf6eb3a0b8dfe37cc573167013296e923bf1597973eb55b2580eaf849e088822ba7b2afc14279f'
        'SKIP'
        '5f5956c72a0e9605011b953f98ae29803f50e0c427c794331d1b99e7c1a4dc81379a074a1403ba71737cf8de03c0d749e4544a22a8e2f39984ed9dd6482b40dd'
        '3ddfb09bf63e75be3c97cf04b5010416dafab1b05d6326e45497546f5988aa4a10d811747e3219ae0c24188d38174d2c44e76868c2cb01a910f216f24efd292a'
        '7834c39053a074ae0e59bd11f9ff34c2147a0ebe87eafa7355af0bd42a39998ad62f0ccadc10442e5a29bea2a10111359833f06937144aea94443eb9eaf6cd91')
validpgpkeys=('F04DF50737AC1A884C4B3D718380596DA6E59C91') # ALSA Release Team (Package Signing Key v1) <release@alsa-project.org>

prepare() {
  cd "${pkgname}-${pkgver}"
  # fix alsactl soundcard detection:
  # https://github.com/alsa-project/alsa-lib/issues/142
  patch -Np1 -i ../"${pkgname}-1.2.5-nested_iteration.patch"
  # fix segfault on alsactl restore
  # https://bugs.archlinux.org/task/71175
  patch -Np1 -i ../"${pkgname}-1.2.5-alsactl_load_state1.patch"
  patch -Np1 -i ../"${pkgname}-1.2.5-alsactl_load_state2.patch"
  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --disable-alsaconf \
              --sbindir=/usr/bin \
              --with-udev-rules-dir=/usr/lib/udev/rules.d \
              --with-systemdsystemunitdir=/usr/lib/systemd/system
  make
}

package() {
  depends+=('libasound.so' 'libatopology.so' 'libformw.so' 'libmenuw.so'
  'libncursesw.so' 'libpanelw.so' 'libsamplerate.so')
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  # dir where to save ALSA state
  install -d "${pkgdir}/var/lib/alsa"
}
