# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>

pkgname=xfdesktop
pkgver=4.12.4
pkgrel=2
pkgdesc="A desktop manager for Xfce"
arch=('x86_64')
url="http://www.xfce.org/"
license=('GPL2')
groups=('xfce4')
# Re-add thunar once xfdesktop supports the new Thunarx-3 API
depends=('libxfce4ui' 'garcon' 'exo' 'hicolor-icon-theme' 'libwnck')
makedepends=('intltool')
conflicts=('xfce4-menueditor')
replaces=('xfce4-menueditor')
source=(https://archive.xfce.org/src/xfce/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=('098a35510562907e1431d5adbfa8307484a235c1dec6a43e2d58d2ac4241f1cb')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --enable-gio-unix \
    --enable-notifications \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
