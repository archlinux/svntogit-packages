# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kfilemetadata
pkgver=5.61.0
pkgrel=1
pkgdesc="A library for extracting file metadata"
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(ki18n karchive kconfig kcoreaddons exiv2 poppler-qt5 taglib ffmpeg ebook-tools)
optdepends=('catdoc: Extract text from Office 98 files')
makedepends=(extra-cmake-modules catdoc doxygen qt5-tools qt5-doc libappimage)
optdepends=('libappimage: AppImage extractor')
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('15f20af053c71c1e5ba6c6ade90b7cce27645b27ee30f1e6e73038e81a2c958e'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
