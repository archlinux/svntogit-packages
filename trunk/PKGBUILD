# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=(harfbuzz)
pkgname=(harfbuzz harfbuzz-icu)
pkgver=1.2.4
pkgrel=2
pkgdesc="OpenType text shaping engine"
arch=(i686 x86_64)
url="http://www.freedesktop.org/wiki/Software/HarfBuzz"
license=(MIT)
makedepends=(glib2 freetype2 graphite cairo icu gobject-introspection gtk-doc ragel)
source=(http://www.freedesktop.org/software/harfbuzz/release/${pkgbase}-${pkgver}.tar.bz2)
sha256sums=('449dbdf12a8f94aedbdefaac831d8b1bf45e7decaa9192eaa4f74104ef9de491')

prepare() {
  mkdir path
  ln -s /usr/bin/python2 path/python

  cd $pkgbase-$pkgver
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgbase-$pkgver
  CFLAGS+=' -DNDEBUG'
  CXXFLAGS+=' -DNDEBUG'
  ./configure --prefix=/usr \
    --with-glib --with-freetype --with-cairo --with-icu --with-graphite2 \
    --with-gobject --enable-doc
  make
}

check() {
  cd $pkgbase-$pkgver
  PATH="$srcdir/path:$PATH" make check
}

package_harfbuzz() {
  depends=(glib2 freetype2 graphite)
  optdepends=('cairo: hb-view program')

  cd $pkgbase-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/harfbuzz/COPYING"

# Split harfbuzz-icu
  mkdir -p ../hb-icu/usr/{include/harfbuzz,lib/pkgconfig}; cd ../hb-icu
  mv "$pkgdir"/usr/lib/libharfbuzz-icu* ./usr/lib
  mv "$pkgdir"/usr/lib/pkgconfig/harfbuzz-icu.pc ./usr/lib/pkgconfig
  mv "$pkgdir"/usr/include/harfbuzz/hb-icu.h ./usr/include/harfbuzz
}

package_harfbuzz-icu(){
  pkgdesc="$pkgdesc (ICU integration)"
  depends=(harfbuzz icu)

  mv hb-icu/* "$pkgdir"

  install -Dm644 $pkgbase-$pkgver/COPYING "$pkgdir/usr/share/licenses/harfbuzz-icu/COPYING"
}
