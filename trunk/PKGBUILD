# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Tobias Kieslich <tobias (at) archlinux.org>

pkgname=xfce4-screenshooter
pkgver=1.10.3
pkgrel=1
pkgdesc="An application to take screenshots"
arch=('x86_64')
url="https://docs.xfce.org/apps/xfce4-screenshooter/start"
license=('GPL2')
groups=('xfce4-goodies')
depends=('xfce4-panel' 'libsoup3' 'hicolor-icon-theme')
makedepends=('intltool' 'python')
source=(https://archive.xfce.org/src/apps/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=('a454159847becfeca274a5b58c0e5817d4a260a29345a37bbc3b4ff46f8f3818')

prepare() {
  cd $pkgname-$pkgver
}

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-debug
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
