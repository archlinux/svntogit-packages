# Maintainer: AndyRTR <andyrtr@archlinux.org>

pkgbase=hyphen
pkgname=('hyphen' 'hyphen-en')
pkgver=2.8.8
pkgrel=2
arch=('x86_64')
url="http://hunspell.sf.net"
license=('GPL2' 'LGPL2.1' 'MPL')
makedepends=('glibc' 'perl')
source=(http://downloads.sourceforge.net/hunspell/${pkgname}-${pkgver}.tar.gz)
sha1sums=('0556c392beb59433e577e3517575801212201df6')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-static
  make
}

package_hyphen() {
 pkgdesc="library for high quality hyphenation and justification"
 depends=('glibc' 'perl')

  cd $pkgbase-$pkgver
  make DESTDIR="$pkgdir/" install-libLTLIBRARIES
  make DESTDIR="$pkgdir/" install-binSCRIPTS
  make DESTDIR="$pkgdir/" install-includeHEADERS
  
  # license
  install -D -m644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

package_hyphen-en() {
 pkgdesc="English hyphenation rules"
 # arch=('any')
 optdepends=('hyphen: offers hyphenation library functions')

  cd $pkgbase-$pkgver
  make DESTDIR="$pkgdir/" install-hyphDATA

  pushd ${pkgdir}/usr/share/hyphen/
   en_US_aliases="en_AG en_AU en_BS en_BW en_BZ en_CA en_DK en_GB en_GH en_HK en_IE en_IN en_JM en_NA en_NZ en_PH en_SG en_TT en_ZA en_ZW"
   for lang in $en_US_aliases; do
        ln -s hyph_en_US.dic hyph_$lang.dic
   done
  popd
  
  # the symlinks
  install -dm755 ${pkgdir}/usr/share/myspell/dicts
  pushd $pkgdir/usr/share/myspell/dicts
    for file in $pkgdir/usr/share/hyphen/*; do
      ln -sv /usr/share/hyphen/$(basename $file) .
    done
  popd
}
