# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>

pkgname=glade
pkgver=3.22.2
pkgrel=1
pkgdesc="User Interface Builder for GTK+ applications"
url="https://glade.gnome.org/"
arch=(x86_64)
license=(GPL LGPL)
depends=(gtk3 libxml2)
makedepends=(intltool gtk-doc gobject-introspection python-gobject itstool docbook-xsl git
             gnome-common webkit2gtk)
optdepends=('devhelp: development help')
groups=('gnome-extra')
provides=(libgladeui-2.so)
_commit=1f0e1db7178c5038b2f61dc708b0939cb21d9254  # tags/GLADE_3_22_2^0
source=("git+https://gitlab.gnome.org/GNOME/glade.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^GLADE_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --enable-gtk-doc
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
