# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=gnome-shell-extensions
pkgver=42.0
pkgrel=1
pkgdesc="Extensions for GNOME shell, including classic mode"
url="https://wiki.gnome.org/Projects/GnomeShell/Extensions"
arch=(any)
license=(GPL)
depends=(gnome-shell)
makedepends=(meson git sassc)
optdepends=('gnome-menus: applications menu extension')
groups=(gnome)
_commit=227c900d1c037dd991cf74caafdc55ecf9139993  # tags/42.0^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-shell-extensions.git#commit=$_commit"
        "git+https://gitlab.gnome.org/GNOME/gnome-shell-sass.git")
sha256sums=('SKIP'
            'SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname

  git submodule init
  git submodule set-url data/gnome-shell-sass "$srcdir/gnome-shell-sass"
  git submodule update
}

build() {
  arch-meson $pkgname build -D extension_set=all -D classic_mode=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
