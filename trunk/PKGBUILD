# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdoctools
pkgver=5.56.0
pkgrel=2
pkgdesc='Documentation generation from docbook'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(karchive docbook-xsl)
makedepends=(extra-cmake-modules perl-uri ki18n doxygen qt5-tools)
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig}
        kdoctools-id.patch::"https://cgit.kde.org/kdoctools.git/patch/?id=3dc6d5f3")
sha256sums=('e4f83bfaeca9ef89711577b756a3c359eb8d45b60a180934ba82431b5c36c007'
            'SKIP'
            '1226a2f678b2605013edecfeceed3fa8ae731a1508a1d15df2867c0ccb8c1169')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../kdoctools-id.patch # Fix build of Indonesian translations
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
