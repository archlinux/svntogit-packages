# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=bluedevil
pkgver=2.0.git3a7a27e
pkgrel=1
pkgdesc='KDE bluetooth framework'
arch=('i686' 'x86_64')
url="https://projects.kde.org/projects/extragear/base/bluedevil"
license=('GPL')
depends=('kdebase-workspace' 'libbluedevil')
makedepends=('cmake' 'automoc4' 'git')
install="${pkgname}.install"
source=("git://anongit.kde.org/${pkgname}.git#branch=bluez5"
        'fix-build.patch')
md5sums=('SKIP'
         '4bc2613ab2b5d1125f63af615911d7f1')

pkgver() {
  cd ${pkgname}
  echo "2.0.git$(git log --pretty=format:'%h' -n 1)"
}

prepare() {
  mkdir build

  cd ${pkgname}
  patch -p1 -i "${srcdir}"/fix-build.patch
}

build(){
  cd build
  cmake ../${pkgname} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
