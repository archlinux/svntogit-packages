#Maintainer: Juergen Hoetzel <juergen@archlinux.org>

pkgname=lablgtk2
pkgver=2.18.11
pkgrel=10
pkgdesc="An Objective Caml interface to gtk2"
arch=(x86_64)
license=('LGPL')
url="http://lablgtk.forge.ocamlcore.org/"
depends=('gtk2' 'librsvg')
makedepends=('ocaml-compiler-libs>=4.12.0' 'camlp5>=7.12')
optdepends=('ocaml: for using the tools')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/garrigue/lablgtk/archive/${pkgver}.tar.gz" gcc10.patch)
sha512sums=('adc840df68aa5f9541db44f888f344b313e40c7d8f5cd35f2a45f1bc84e7136dde4b39f873de011db5d5fdfd904c0a097cd9d105458ea9f483cdab3954a2ee05'
            '0fcb9f333101c5ab04dde1729a9c9805e422f100f79ad8014a3cd8c5cb78d19344292e334d4cc70d3614a41a4e143a30cd707b67a8443fa8b6f8c8fc9d7f27d9')
options=(!makeflags staticlibs !lto)

prepare() {
  cd "${srcdir}/lablgtk-${pkgver}/src"
  patch -p0 -i "${srcdir}/gcc10.patch"
}

build() {
  cd "${srcdir}/lablgtk-${pkgver}"
  ./configure --prefix=/usr \
    --without-gnomeui \
    --without-gnomecanvas \
    --without-panel \
    --without-gtkspell \
    --without-gtksourceview \
    --without-gtksourceview2 \
    --without-glade
  sed -e "s|-O|$CFLAGS|" \
      -e "s|-shared|& -ccopt \"$LDFLAGS\"|" \
      -e "s|(CAMLMKLIB)|& -ldopt \"$LDFLAGS\"|" \
      -i src/Makefile
  make world
  make opt
}

package() {
  cd "${srcdir}/lablgtk-${pkgver}"
  make DESTDIR="${pkgdir}" old-install
  install -m644 META "${pkgdir}/usr/lib/ocaml/lablgtk2/"
}
