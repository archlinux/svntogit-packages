# Maintainer: RÃ©my Oudompheng <remy@archlinux.org>
# Contributor: Piotr Rogoza <piotr.r.public at gmail dot com>
# Contributor: Alessandro Sagratini <ale_sagra at hotmail dot com>

pkgname=perl-sort-key
pkgver=1.33
pkgrel=5
_author="S/SA/SALVA"
_perlmod="Sort-Key"
pkgdesc="Sort::Key - the fastest way to sort anything in Perl"
arch=("x86_64")
url=http://search.cpan.org/dist/sort-sort/
license=('GPL' 'PerlArtistic')
depends=(perl)
options=(!emptydirs)
source=("http://search.cpan.org/CPAN/authors/id/$_author/$_perlmod-$pkgver.tar.gz")
sha256sums=('ed6a4ccfab094c9cd164f564024e98bd21d94f4312ccac4d6246d22b34081acf')

build() {
    cd "${srcdir}/${_perlmod}-${pkgver}"

    # install module in vendor directories.
    PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
    make
}

check() {
    cd "${srcdir}/${_perlmod}-${pkgver}"
    unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
    export PERL_MM_USE_DEFAULT=1
    make test
}

package() {
    cd "${srcdir}/${_perlmod}-${pkgver}"
    unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
    make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
}

