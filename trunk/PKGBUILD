# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: AndyRTR <andyrtr@archlinux.org>
# Contributor: Tobias Kieslich <tobias (at) archlinux.org>

pkgname=xfce4-xkb-plugin
pkgver=0.8.1
pkgrel=1
pkgdesc="Plugin to switch keyboard layouts for the Xfce4 panel"
arch=('x86_64')
url="http://goodies.xfce.org/projects/panel-plugins/xfce4-xkb-plugin"
license=('custom')
groups=('xfce4-goodies')
depends=('xfce4-panel' 'libxklavier' 'librsvg' 'libwnck3')
makedepends=('intltool')
install=$pkgname.install
source=(https://archive.xfce.org/src/panel-plugins/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=('c19ecf126201deb6148741c521124771ad396adc874471512ab5ffe1946567a1')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
