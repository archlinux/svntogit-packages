# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan De Groot <jgc@archlinux.org>

pkgname=gucharmap
pkgver=14.0.3
pkgrel=1
pkgdesc="Gnome Unicode Charmap"
url="https://wiki.gnome.org/Apps/Gucharmap"
arch=(x86_64)
license=(GPL3)
depends=(gtk3)
makedepends=(gtk-doc gobject-introspection yelp-tools appdata-tools git meson
             unicode-character-database unzip vala)
provides=(libgucharmap_2_90.so)
options=(debug)
_commit=c5ed6328814952e7a6e7997d02697119dc52eb90  # tags/14.0.3^0
source=("git+https://gitlab.gnome.org/GNOME/gucharmap.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build -D ucd_path=/usr/share/unicode
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
