# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=libphonenumber
pkgver=8.12.20
pkgrel=1
pkgdesc="Google's common library for parsing, formatting, and validating international phone numbers"
url="https://github.com/googlei18n/libphonenumber"
arch=(x86_64)
license=(Apache)
depends=(icu protobuf boost-libs)
makedepends=(boost cmake gtest git 'java-environment>=9')
provides=(libgeocoding.so libphonenumber.so)
_commit=7b2fa013f2630c8aba6eb7675211b779f2562de3  # tags/v8.12.20^0
source=("git+$url#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^libphonenumber-\|^v//;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  CXXFLAGS+=" -Wno-error=deprecated-declarations"  # readdir_r deprecation
  CXXFLAGS+=" -Wno-error=unused-variable"  # this is nuts
  cmake -S $pkgname/cpp -B build \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  cmake --build build
}

check() {
  cmake --build build --target tests
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
