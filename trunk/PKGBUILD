# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ffmpegthumbs
pkgver=15.08_r91_c06602f
pkgrel=2
pkgdesc='FFmpeg-based thumbnail creator for video files'
url='https://projects.kde.org/ffmpegthumbs'
arch=(i686 x86_64)
license=(GPL LGPL FDL)
#groups=(kde-applications kdemultimedia)
depends=(kio ffmpeg)
makedepends=(extra-cmake-modules git)
source=("git://git.kde.org/ffmpegthumbs#commit=c06602ff")
sha1sums=('SKIP')

pkgver() { 
  cd $pkgname
  printf "15.08_r%s_%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
