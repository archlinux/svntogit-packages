# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: schuay <jakob.gruber@gmail.com>

pkgname=ksshaskpass
pkgver=5.2.0
pkgrel=1
pkgdesc='ssh-add helper that uses kwallet and kpassworddialog'
url='https://projects.kde.org/projects/kde/workspace/ksshaskpass'
license=('GPL')
arch=('i686' 'x86_64')
depends=('kwallet')
makedepends=('extra-cmake-modules')
provides=('x11-ssh-askpass')
conflicts=('x11-ssh-askpass')
groups=('plasma')
source=("http://download.kde.org/stable/plasma/${pkgver}/${pkgname}-${pkgver}.tar.xz"
        "$pkgname.sh")
md5sums=('1a95a3e40ad05ea977c836df109fd7ca'
         '70396ba019ae05cc91502456f548caff')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../ksshaskpass-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}
package() {
  cd build
  make DESTDIR="$pkgdir" install

  install -Dm755 "$srcdir"/ksshaskpass.sh \
    "${pkgdir}"/etc/profile.d/ksshaskpass.sh
}
