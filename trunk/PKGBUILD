# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kpat
pkgver=22.12.1
pkgrel=1
pkgdesc='Offers a selection of solitaire card games'
url='https://apps.kde.org/kpat/'
arch=(x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kde-games)
depends=(libkdegames freecell-solver black-hole-solver hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools)
conflicts=(kpatience)
replaces=(kpatience)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('749c5cd1b38f62c1d3ef961ad6676865917b390c9d2c09b3df184eed29c81a30'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
options=(debug)

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
