# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Matheus Gabriel Werny de Lima <matheusgwdl@protonmail.com>

pkgname=keysmith
pkgver=23.01.0
pkgrel=1
pkgdesc='OTP client for Plasma Mobile and Desktop'
license=(GPL3)
arch=(x86_64)
url='https://apps.kde.org/keysmith'
depends=(kirigami2 ki18n kdbusaddons kwindowsystem libsodium qt5-svg hicolor-icon-theme)
makedepends=(extra-cmake-modules)
source=(https://download.kde.org/stable/plasma-mobile/$pkgver/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('6eb9aacb013721a3ba8d0a85f304b5659b6bc43925c3ab4ac786069e875baf54'
            'SKIP')
validpgpkeys=(39FFA93CAE9C6AFC212AD00202325448204E452A  # Carl Schwan <carl@carlschwan.eu>
              0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D) # Bhushan Shah (mykolab address) <bshah@mykolab.com>

build() {
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
