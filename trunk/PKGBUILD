# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgbase=kate
pkgname=('kwrite'
         'kate')
pkgver=14.12.1
pkgrel=2
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('knewstuff' 'ktexteditor' 'plasma-framework' 'threadweaver' 'kded' 'kitemmodels')
makedepends=('extra-cmake-modules' 'kdoctools' 'python')
source=("http://download.kde.org/stable/applications/${pkgver}/src/kate-${pkgver}.tar.xz")
sha1sums=('e7097f07f9876c44e482a88d7fdae0db92aa2359')

prepare() {
  mkdir build

  cd kate-${pkgver}
}

build() {
  cd build
  cmake ../kate-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DSYSCONF_INSTALL_DIR=/etc \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package_kwrite() {
  pkgdesc="Text Editor"
  groups=('kde' 'kdebase')
  url="http://www.kde.org/applications/utilities/kwrite/"
  install='kwrite.install'
  replaces=('kdebase-kwrite')
  conflicts=('kdebase-kwrite')
  provides=('kdebase-kwrite')

  cd build/kwrite
  make DESTDIR="${pkgdir}" install

  cd ../../build/doc/kwrite
  make DESTDIR="${pkgdir}" install
}

package_kate() {
  pkgdesc="Advanced Text Editor"
  groups=('kde' 'kdesdk')
  url="http://www.kde.org/applications/utilities/kate/"
  install='kate.install'
  replaces=('kdesdk-kate')
  conflicts=('kdesdk-kate')
  provides=('kdesdk-kate')
  optdepends=('konsole: open a terminal in Kate')

  cd build/kate
  make DESTDIR="${pkgdir}" install

  cd ../../build/doc/kate
  make DESTDIR="${pkgdir}" install

  cd ../../../build/addons
  make DESTDIR="${pkgdir}" install
}
