# $Id$
# Maintainer: Juergen Hoetzel <juergen@archlinux.org>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>

pkgname=lua 
pkgver=5.1.4
pkgrel=6
pkgdesc="A powerful light-weight programming language designed for extending applications." 
arch=('i686' 'x86_64')
url="http://www.lua.org/" 
depends=('readline' 'ncurses') 
license=('MIT')
options=('!makeflags')
source=(http://www.lua.org/ftp/${pkgname}-${pkgver}.tar.gz http://www.lua.org/ftp/patch-lua-5.1.4-2 lua-arch.patch lua-5.1-cflags.diff)
md5sums=('d0870f2de55d59c1c8419f36e8fac150' '1239310e0c4a581c7831e596f95cc6cd'\
         '6c5953f63904bf20a0183cdab05b80de' '249582bf1fd861ccf492d2c35a9fe732')

build() { 
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i "${srcdir}/lua-arch.patch"
  patch -i ${srcdir}/patch-lua-5.1.4-2 -d src
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  [ "$CARCH" == "x86_64" ] && patch -Np1 -i ../lua-5.1-cflags.diff
  [ "$CARCH" == "x86_64" ] && export CFLAGS="$CFLAGS -fPIC"
  make INSTALL_DATA="cp -d" TO_LIB="liblua.a liblua.so liblua.so.5.1" LUA_SO=liblua.so  INSTALL_TOP="${pkgdir}/usr" INSTALL_MAN="${pkgdir}/usr/share/man/man1" \
    linux install  || return 1
  install -D -m 644 etc/lua.pc "${pkgdir}/usr/lib/pkgconfig/lua.pc"
  install -D -m644 COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgname}/COPYRIGHT"

  # Install the documentation
  mkdir -p "${pkgdir}/usr/share/doc/lua"
  cp -R doc/* "${pkgdir}/usr/share/doc/lua"
}
