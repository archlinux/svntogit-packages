# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: trya <tryagainprod@gmail.com>

pkgname=quazip
pkgver=0.7.6
pkgrel=1
pkgdesc="C++ wrapper for the Gilles Vollant's ZIP/UNZIP C package"
url="https://stachenov.github.io/quazip/"
license=('LGPL')
arch=('x86_64')
depends=('qt5-base')
makedepends=('cmake')
provides=('quazip-qt5')
source=($pkgname-$pkgver.tar.gz::https://github.com/stachenov/$pkgname/archive/$pkgver.tar.gz)
sha256sums=('4118a830a375a81211956611cc34b1b5b4ddc108c126287b91b40c2493046b70')

prepare() {
  # Fix cmake module install dir
  sed -e 's|${CMAKE_ROOT}/Modules|${CMAKE_INSTALL_PREFIX}/lib/cmake/QuaZip|' -i $pkgname-$pkgver/CMakeLists.txt
}

build() {
  cd "${srcdir}"

  install -d build
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_WITH_QT4:BOOL=OFF \
    -DCMAKE_CXX_FLAGS="$CFLAGS -fPIC"
  make
}

package() {
  cd "${srcdir}"

  cd build
  make DESTDIR="${pkgdir}" install
}
