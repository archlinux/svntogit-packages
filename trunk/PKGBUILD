# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: jlvsimoes
# Contributor: damir <damir@archlinux.org>

pkgname=k3b
pkgver=1.93.0rc4
pkgrel=1
depends=('kdebase-runtime' 'kdemultimedia-kioslave' 'libsamplerate' 'libmad'
         'ffmpeg>=0.5' 'taglib>=1.4' 'libmpcdec>=1.2.5' 'libdvdread>=4.1.3'
         'cdrdao' 'cdrkit' 'shared-mime-info' 'libxft')
makedepends=('cmake' 'pkgconfig' 'automoc4' )
pkgdesc="Feature-rich and easy to handle CD burning application"
arch=("i686" "x86_64")
license=('GPL')
install=$pkgname.install
source=(http://downloads.sourceforge.net/sourceforge/k3b/${pkgname}-${pkgver}.tar.bz2)
url="http://k3b.sourceforge.net/"
optdepends=('dvd+rw-tools: for dvd burning support'
            'vcdimager:    for vcd burning support'
            'transcode:    for advanced mpeg conversion support'
            'emovix:       for bootable multimedia cd/dvd support')
options=(!libtool)

build() {
  cd ${srcdir}/
  mkdir build
  cd build
  cmake ../${pkgname}-1.93.0 \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make || return 1
  make DESTDIR=${pkgdir} install || return 1
}
md5sums=('78ce03b8a7d4f9872b4dd0963b77307c')
