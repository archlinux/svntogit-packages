# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=python-dbusmock
pkgver=0.28.6
pkgrel=3
pkgdesc="Mock D-Bus objects for tests"
url="https://github.com/martinpitt/python-dbusmock"
arch=(any)
license=(LGPL3)
depends=(python-dbus python-gobject)
makedepends=(python-setuptools python-setuptools-scm python-build
             python-installer python-wheel git)
_commit=276f54de0f653e8a37b0d0392450df3f57b0a746  # tags/0.28.6^0
source=("git+https://github.com/martinpitt/python-dbusmock#commit=$_commit"
        0001-Fix-OBEX-PullAll-after-pathlib-conversion.patch)
sha256sums=('SKIP'
            'd259cbecb464ad5ea782f43ca630062ffa03feb6017825a3b7ac235d910ba798')

# Suppress local version
export SETUPTOOLS_SCM_PRETEND_VERSION="${pkgver%%+*}"

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
  git apply -3 ../0001-Fix-OBEX-PullAll-after-pathlib-conversion.patch
}

build() {
  cd $pkgname
  python -m build --wheel --no-isolation
}

package() {
  cd $pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl
}

# vim:set sw=2 sts=-1 et:
