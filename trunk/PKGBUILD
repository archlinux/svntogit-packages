# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegraphics-mobipocket
pkgver=20.08.3
pkgrel=1
pkgdesc="A collection of plugins to handle mobipocket files"
url="https://www.kde.org/applications/graphics/"
arch=(x86_64)
license=(GPL LGPL FDL)
depends=(kio)
makedepends=(extra-cmake-modules)
groups=(kde-applications kde-graphics)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('1876ef0993214c97ddd31105a230c7f68835c050279898893fb9d77b2c5fdd45'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
