# $Id$
# Maintainer: Alexander Baldeck <alexander@archlinux.org>
# Contributor Jan de Groot <jgc@archlinux.org>
pkgname=libx11
pkgver=1.1.5
pkgrel=2
pkgdesc="X11 client-side library"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
depends=('libxcb>=1.1.90.1' 'kbproto>=1.0.3')
makedepends=('xproto>=7.0.13' 'xextproto>=7.0.3' 'xf86bigfontproto>=1.1.2' 'xtrans>=1.2.1' 'kbproto>=1.0.3' 'inputproto>=1.4.4')
options=('!libtool')
license=('custom:XFREE86')
source=(${url}/releases/individual/lib/libX11-${pkgver}.tar.bz2
	012_ru_RU_UTF-8_XLC_LOCALE.diff
	xorg.sh)
md5sums=('d1512d65dadd4f48c779d4749e7753a8'
         'c7e38a68b932ed557bc71979eed2a8a1'
         '942ff006e69e041f0307c1316e0dec5d')

build() {
  cd ${srcdir}/libX11-${pkgver}
  patch -Np1 -i ${srcdir}/012_ru_RU_UTF-8_XLC_LOCALE.diff || return 1

  libtoolize --force --copy || return 1
  aclocal || return 1
  autoconf || return 1
  automake --add-missing || return 1

  ./configure --prefix=/usr \
              --with-xcb || return 1
  make || return 1
  make DESTDIR=${pkgdir} install || return 1

  install -m755 -d ${pkgdir}/etc/profile.d 
  install -m755 ${srcdir}/xorg.sh \
                ${pkgdir}/etc/profile.d/ || return 1

  install -d -m755 ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/ || return 1
}
