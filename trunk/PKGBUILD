# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=dmidecode
pkgver=3.5
pkgrel=1
pkgdesc="Desktop Management Interface table related utilities"
arch=('x86_64')
url="https://www.nongnu.org/dmidecode"
license=('GPL')
depends=('glibc')
source=("https://sources.archlinux.org/other/${pkgname}/${pkgname}-${pkgver}.tar.xz"{,.sig})
# source=("https://download.savannah.nongnu.org/${pkgname}/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha256sums=('79d76735ee8e25196e2a722964cf9683f5a09581503537884b256b01389cc073'
            'SKIP')
validpgpkeys=('7CA69F4460F1BDC41FD2C858A5526B9BB3CD4E6A') # Jean Delvare (kernel.org) <jdelvare@suse.de>

build() {
  cd ${pkgname}-${pkgver}

  sed -i "s:sbin:bin:g" Makefile
  make prefix=/usr CFLAGS="${CFLAGS}" LDFLAGS="${LDFLAGS}"
}

package() {
  cd ${pkgname}-${pkgver}

  make prefix=/usr DESTDIR="${pkgdir}" install
}
