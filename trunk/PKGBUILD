# Maintainer: Allan McRae <allan@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=brasero
pkgver=3.12.2
pkgrel=2
pkgdesc="CD/DVD mastering tool"
url="https://wiki.gnome.org/Apps/Brasero"
arch=(x86_64)
license=(GPL)
depends=(gtk3 gst-plugins-good totem-plparser cdrtools cdrdao shared-mime-info libcanberra
         dvd+rw-tools dconf libsm tracker libnotify gvfs)
makedepends=(gtk-doc yelp-tools gnome-common intltool libburn libisofs libnautilus-extension
             gobject-introspection itstool docbook-xsl git)
optdepends=('libburn: alternative back-end'
            'libisofs: libburn back-end'
            'dvdauthor: video project'
            'vcdimager: video project')
_commit=f0c7ac25f7e01a608f74111e64ff196fb470ed9f  # tags/3.12.2^0
source=("git+https://git.gnome.org/browse/brasero#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-caches --disable-schemas-compile --enable-compile-warnings=minimum \
    --enable-gtk-doc
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname
  make DESTDIR=$pkgdir install
}

