# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=python-asgiref
pkgver=3.2.7
pkgrel=1
pkgdesc="Reference ASGI adapters and channel layers"
arch=(any)
url="http://github.com/django/asgiref"
license=('BSD')
makedepends=('python-setuptools')
checkdepends=(python-pytest python-pytest-asyncio)
depends=('python')
source=("https://pypi.io/packages/source/a/asgiref/asgiref-${pkgver}.tar.gz")
sha256sums=('8036f90603c54e93521e5777b2b9a39ba1bad05773fcf2d208f0299d1df58ce5')

build() {
    cd "asgiref-${pkgver}"
    python setup.py build
}

check() {
    cd "asgiref-${pkgver}"
    PYTHONPATH=. pytest .
}

package() {
    cd "asgiref-${pkgver}"
    python setup.py install --root=${pkgdir} --optimize=1

    install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
