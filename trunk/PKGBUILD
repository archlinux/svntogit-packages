# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: simo <simo@archlinux.org> 
# Contributor: Manolis Tzanidakis

pkgname=compface
pkgver=1.5.2
pkgrel=11
pkgdesc="Utils & libs to convert from/to X-Face format."
arch=('x86_64')
url="http://ftp.xemacs.org/pub/xemacs/aux/"
license=('custom')
depends=('glibc')
source=(#$url/$pkgname-$pkgver.tar.gz
        http://mirrors.ibiblio.org/xemacs/aux/$pkgname-$pkgver.tar.gz
        compface-1.5.2-build.patch
        LICENSE)
md5sums=('62f4f79c0861ad292ba3cf77b4c48319'
         '25874b31f97a53f66ceed54f6b422515'
         '27a0d5a4d695e68b4d3eebe17db77189')
sha256sums=('a6998245f530217b800f33e01656be8d1f0445632295afa100e5c1611e4f6825'
            '5002ad6bd405af37515314b33184d5539603ffad7ad97e50e76e5d00d67eebca'
            '6f44301a736c5a5ff047d78f6a5e5b32dd6f05974ce0380ec4437ad73912168d')

prepare() {
  cd $pkgname-$pkgver
  patch -Np0 -i ${srcdir}/compface-1.5.2-build.patch
}

build() {
  cd $pkgname-$pkgver
  CFLAGS="$CFLAGS -fPIC"
  ./configure --prefix=/usr --mandir=/usr/share/man
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" STRIP=/bin/true install

  install -D -m644 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
