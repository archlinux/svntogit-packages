# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=gjs
pkgver=1.43.3
pkgrel=1
pkgdesc="Javascript Bindings for GNOME"
arch=(i686 x86_64)
url="http://live.gnome.org/Gjs"
license=(GPL)
depends=(cairo gobject-introspection-runtime 'js>=24.2.0' 'gtk3')
makedepends=('gobject-introspection')
source=(http://ftp.gnome.org/pub/gnome/sources/gjs/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('82b54dd4590ea1622d19b2e27978bcb9c2c5b46922e1e1780ea79b6a28aa1c4e')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-static --libexecdir=/usr/lib
  make
}

check() {
  cd $pkgname-$pkgver
  # Needs a display
  make -k check || :
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
