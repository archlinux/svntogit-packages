# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: MaÃ«l Lavault <moimael@neuf.fr>

pkgbase=abiword
pkgname=('abiword' 'abiword-plugins')
pkgver=2.8.6
pkgrel=9
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.abisource.com"
makedepends=('pkgconfig' 'asio' 'boost' 'fribidi' 'goffice0.8' 'libwmf' 'wv' 'link-grammar' 'gtkmathview' 
             'aiksaurus' 'libxslt' 'enchant' 'libots' 'libwpg' 'librsvg' 'loudmouth' 'libsoup' 'psiconv')
options=('!makeflags')
source=(http://www.abisource.com/downloads/${pkgbase}/${pkgver}/source/${pkgbase}-${pkgver}.tar.gz
        compat_libwpg_0_9.patch
	abiword-2.8.6-no-undefined.patch
        abiword-2.8.6-libpng15.patch
	abiword-2.8.6-glib-2.31.patch)
md5sums=('f883b0a7f26229a9c66fd6a1a94381aa'
         '09446a2ebba8288fc611e88f7cf17298'
         '42e2614b6e0aad457d91f6ce20b6f225'
         '40e370e7e88c5437140ef18037abfbd3'
         '6ec338a27a6fb590c348724ddb545849')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"

  patch -Np1 -i "${srcdir}/compat_libwpg_0_9.patch"
  # fix a gcc 4.6.x build issue, patch from Fedora
  patch -Np1 -i "${srcdir}/abiword-2.8.6-no-undefined.patch"
  patch -Np0 -i "${srcdir}/abiword-2.8.6-libpng15.patch"

  # Fix build with recent glib, patch from Gentoo
  patch -Np0 -i "${srcdir}/abiword-2.8.6-glib-2.31.patch"

  autoreconf -vfi

  ./configure --prefix=/usr \
              --enable-clipart \
              --enable-templates \
              --enable-collab-backend-xmpp \
              --enable-collab-backend-tcp \
              --enable-collab-backend-service \
              --disable-collab-backend-sugar \
              --enable-plugins --without-gnomevfs \
              --with-gio \
              --with-goffice \
              --disable-static \
              --with-psiconv-config=/nothere
  make
}

package_abiword() {
  pkgdesc="A fully-featured word processor"
  depends=('fribidi' 'wv' 'goffice0.8' 'librsvg' 'enchant' 'desktop-file-utils')
  install=$pkgname.install
  optdepends=('abiword-plugins')
  conflicts=("abiword-plugins<${pkgver}-${pkgrel}")

  cd "${srcdir}/${pkgbase}-${pkgver}"
  sed -i plugins/Makefile \
      -e 's/ collab / /' \
      -e 's/ wpg / /' \
      -e 's/ wmf / /' \
      -e 's/ grammar / /' \
      -e 's/ mathview / /' \
      -e 's/ aiksaurus / /' \
      -e 's/ latex / /' \
      -e 's/ ots / /' \
      -e 's/ wordperfect / /'
  make DESTDIR="${pkgdir}" install
}

package_abiword-plugins() {
  pkgdesc="Additional plugins for Abiword"
  depends=("abiword=${pkgver}-${pkgrel}" 'loudmouth' 'libwpg' 'libwmf' 'link-grammar' 
           'gtkmathview' 'aiksaurus' 'libxslt' 'libsoup' 'libots' 'libgsf')

  cd "${srcdir}/${pkgbase}-${pkgver}/plugins"
  for dir in collab wpg wmf grammar mathview aiksaurus latex ots wordperfect; do
    make -C ${dir} DESTDIR="${pkgdir}" install
  done
}
