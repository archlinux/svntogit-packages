# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: tobias <tobias funnychar archlinux.org>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>

pkgname=xfce4-settings
pkgver=4.7.7
pkgrel=1
pkgdesc="Settings manager for xfce"
arch=('i686' 'x86_64')
license=('GPL2')
url="http://www.xfce.org/"
groups=('xfce4')
depends=('exo>=0.5.4' 'libxfce4ui' 'libnotify' 'libxklavier' 'tango-icon-theme' 'gtk-engines' 'gtk-xfce-engine') # 'libwnck' 
makedepends=('intltool') #'libcanberra')
optdepends=('libcanberra: for sound control')
source=(http://archive.xfce.org/src/xfce/${pkgname}/4.7/${pkgname}-${pkgver}.tar.bz2
	xfce4-settings-4.6.0.patch)
md5sums=('e18fb70781bc45d60b7ec9031fa3bf35'
         '3046732e5d73c9939e7674602eadb1e2')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  # enable Tango icon theme, clearlooks theme and font hinting by default(taken from Fedora)
  patch -Np1 -i ${srcdir}/xfce4-settings-4.6.0.patch || return 1
  # git fixes
  
  ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--localstatedir=/var \
	--disable-static \
	--enable-xrandr \
	--enable-xcursor \
	--enable-libnotify \
	--enable-libxklavier \
	--enable-pluggable-dialogs \
	--enable-sound-settings \
	--disable-debug
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
