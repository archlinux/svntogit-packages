# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Gerardo Exequiel Pozzi <vmlinuz386@yahoo.com.ar>

pkgname=libbsd
pkgver=0.11.2
pkgrel=1
pkgdesc='Provides useful functions commonly found on BSD systems like strlcpy()'
arch=('x86_64')
url="https://libbsd.freedesktop.org"
license=('custom')
depends=('glibc' 'libmd')
options=('staticlibs')
source=("https://libbsd.freedesktop.org/releases/$pkgname-$pkgver.tar.xz"{,.asc})
sha512sums=('b0e7bad6d1b8fcbc6f457c704d29d23d6c1af3a88d8c3d8922a470b1da757210dfe9ae04fdcb8e6f27ea020180b17046640463f42e1b9dfc7bde68edac3472f5'
            'SKIP')
validpgpkeys=('4F3E74F436050C10F5696574B972BF3EA4AE57A3') # Guillem Jover

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr
  make
}

check() {
  cd "$pkgname-$pkgver"
  make check
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  rm "${pkgdir}"/usr/lib/libbsd.a
  install -D -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # included now in man-pages, FS#53351
  rm -f "${pkgdir}"/usr/share/man/man3/explicit_bzero.3
}
