# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Andrew Simmons <andrew.simmons@gmail.com>

pkgname=thunar
pkgver=1.8.4
pkgrel=1
pkgdesc="Modern file manager for Xfce"
arch=('x86_64')
url="https://docs.xfce.org/xfce/thunar/start"
license=('GPL2' 'LGPL2.1')
groups=('xfce4')
depends=('desktop-file-utils' 'libexif' 'hicolor-icon-theme' 'libnotify'
         'libgudev' 'exo' 'libxfce4util' 'libxfce4ui' 'libpng')
makedepends=('intltool' 'xfce4-panel')
optdepends=('gvfs: for trash support, mounting with udisk and remote filesystems'
            'xfce4-panel: for trash applet'
            'tumbler: for thumbnail previews'
            'thunar-volman: manages removable devices'
            'thunar-archive-plugin: create and deflate archives'
            'thunar-media-tags-plugin: view/edit id3/ogg tags')
source=(https://archive.xfce.org/src/xfce/$pkgname/${pkgver%.*}/Thunar-$pkgver.tar.bz2
        thunar-bz14946.patch::https://github.com/xfce-mirror/thunar/commit/4c0e17d7fc.patch)
sha256sums=('c2bf51da8ddec8f642d67818184ada2ca6e143151e6a45563991db73387a93b0'
            '23497c2472807b5268b5df3e707940326565f15c480fde755d4fd2f9a86bed8d')

prepare() {
  cd "$srcdir/Thunar-$pkgver"

  # https://bugzilla.xfce.org/show_bug.cgi?id=14946
  patch -Np1 -i ../thunar-bz14946.patch
}

build() {
  cd "$srcdir/Thunar-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --enable-gio-unix \
    --enable-gudev \
    --enable-notifications \
    --enable-exif \
    --enable-pcre \
    --disable-debug
  make
}

package() {
  cd "$srcdir/Thunar-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
