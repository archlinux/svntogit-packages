# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-service-identity
pkgver=18.1.0
pkgrel=7
pkgdesc="Service identity verification for pyOpenSSL"
arch=('any')
license=('MIT')
url="https://pypi.python.org/pypi/service_identity"
depends=('python-pyopenssl' 'python-pyasn1' 'python-pyasn1-modules' 'python-attrs')
makedepends=('python-setuptools' 'python-idna')
checkdepends=('python-pytest-runner')
optdepends=('python-idna: for Internationalized Domain Names support')
source=("https://github.com/pyca/service-identity/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('6b6da48c543bfbe0d9da9f0d03dbd735ce97e64a76cbf9eae2b615ced7b91c5fcca1f25353e53858c48da9c4e0c0be4e33fa4559adb50899a79689cbb372f49b')

build() {
  cd service-identity-$pkgver
  python setup.py build
}

check() {
  cd service-identity-$pkgver
  python setup.py pytest
}
 
package() {
  cd service-identity-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
