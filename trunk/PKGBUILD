# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgbase=pango
pkgname=(pango pango-docs)
pkgver=1.48.6
pkgrel=1
epoch=1
pkgdesc="A library for layout and rendering of text"
url="https://www.pango.org/"
arch=(x86_64)
license=(LGPL)
depends=(libthai cairo libxft harfbuzz fribidi)
makedepends=(gobject-introspection help2man git meson gi-docgen)
checkdepends=(ttf-dejavu cantarell-fonts noto-fonts noto-fonts-emoji)
_commit=61763238b548d717d0772f31467ad7e9b6a557ae  # tags/1.48.6^0
source=("git+https://gitlab.gnome.org/GNOME/pango.git#commit=$_commit"
        fix-test.diff)
sha256sums=('SKIP'
            'd74df5b325007d02c3054a9e2a623327d0d195adf18d0ee41689672c27657cdb')

pkgver() {
  cd pango
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd pango

  # Test assumes no bitmap fonts?
  git apply -3 ../fix-test.diff
}

build() {
  arch-meson pango build -D gtk_doc=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_pango() {
  provides=(libpango{,cairo,ft2,xft}-1.0.so)

  meson install -C build --destdir "$pkgdir"

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc
}

package_pango-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}

# vim:set sw=2 et:
