# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgbase=gtkmm3
_pkgbasename=gtkmm
pkgname=('gtkmm3' 'gtkmm3-docs')
pkgver=3.2.0
pkgrel=1
arch=('i686' 'x86_64')
makedepends=('gtk3' 'pangomm' 'atkmm' 'glibmm-docs' 'mm-common')
license=('LGPL')
options=('!libtool' '!emptydirs')
url="http://gtkmm.sourceforge.net/"
source=(http://ftp.gnome.org/pub/GNOME/sources/${_pkgbasename}/3.2/${_pkgbasename}-${pkgver}.tar.xz)
sha256sums=('6195dac58454e287ae17d380c028827f0a49669a3b93f85e2d4f6eb9459674f2')

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package_gtkmm3() {
  pkgdesc="C++ bindings for gtk3"
  depends=('gtk3' 'pangomm' 'atkmm')
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  sed -i -e 's/^doc_subdirs/#doc_subdirs/' Makefile

  make DESTDIR="${pkgdir}" install
}

package_gtkmm3-docs() {
  pkgdesc="Developer documentation for gtkmm v3"

  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make -C docs DESTDIR="${pkgdir}" install
}
