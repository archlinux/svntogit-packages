# $Id$
# Maintainer:
# Contributor: John Proctor <jproctor@prium.net>
# Contributor: Kevin Piche <kevin@archlinux.org>

pkgname=bigloo
_pkgver=3.4a-2
pkgver=${_pkgver/-/_}
pkgrel=1
pkgdesc="Fast scheme compiler"
arch=('i686' 'x86_64')
url="http://www-sop.inria.fr/mimosa/fp/Bigloo/"
license=('GPL' 'LGPL')
depends=('openssl' 'sqlite3')
makedepends=('jdk')
options=('!makeflags')
install=bigloo.install
source=(ftp://ftp-sop.inria.fr/indes/fp/Bigloo/${pkgname}${_pkgver}.tar.gz)
md5sums=('56a14f2dc903a59f8628f0d21018c8d2')
sha1sums=('e2e55a1af5d7af74f11347d0575e3592a87d4090')

build() {
  cd "${srcdir}/${pkgname}${_pkgver}"
  ./configure --prefix=/usr \
    --mandir=/usr/share/man \
    --infodir=/usr/share/info \
    --docdir=/usr/share/doc/bigloo \
    --dotnet=no \
    --javaprefix=/opt/java/bin
  make build compile-bee
}

package() {
  cd "${srcdir}/${pkgname}${_pkgver}"
  make DESTDIR="${pkgdir}" install install-bee
  make -C manuals DESTDIR="${pkgdir}" install-bee
  chmod 644 "${pkgdir}"/usr/lib/bigloo/${_pkgver%-*}/*.a
}
