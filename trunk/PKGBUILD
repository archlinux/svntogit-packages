# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>

pkgname=source-highlight
pkgver=3.1.9
pkgrel=1
pkgdesc="Convert source code to syntax highlighted document"
arch=('x86_64')
url="https://www.gnu.org/software/src-highlite/"
license=('GPL')
depends=('bash' 'boost-libs')
makedepends=('ctags' 'boost')
source=("ftp://ftp.gnu.org/gnu/src-highlite/${pkgname}-${pkgver}.tar.gz")
sha512sums=('d8e154e9a5d62c77807e4e5d36c0faed5ce2964291be5f8b83e2968a6de52229503689a4ca2109a717ae2632a14b63ec937ca0430c91684c72063f6bc0294195')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --with-bash-completion=/usr/share/bash-completion/completions
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make prefix="${pkgdir}/usr" bash_completiondir="${pkgdir}/usr/share/bash-completion/completions" install

  # PKGBUILD syntax highlighting
  echo -e "\npkgbuild = sh.lang" >> "${pkgdir}"/usr/share/${pkgname}/lang.map
}
