# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-robots
pkgver=3.22.0+2+g293319a
pkgrel=1
pkgdesc="Avoid the robots and make them crash into each other"
url="https://wiki.gnome.org/Apps/Robots"
arch=(i686 x86_64)
license=(GPL)
depends=(gtk3 libcanberra librsvg libgnome-games-support)
makedepends=(intltool itstool gobject-introspection yelp-tools appstream-glib git)
groups=(gnome-extra)
_commit=293319ad335bf7314a5e24de5bcc47a4bd3dd948  # master
source=("git://git.gnome.org/gnome-robots#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --disable-schemas-compile
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
