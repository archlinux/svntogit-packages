# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgbase=colord
pkgname=(colord colord-sane)
pkgver=1.4.2
pkgrel=3
pkgdesc="System daemon for managing color devices"
url="https://www.freedesktop.org/software/colord"
arch=(x86_64)
license=(GPL2)
depends=(lcms2 libgusb polkit sqlite systemd dconf dbus libgudev shared-mime-info)
makedepends=(gobject-introspection vala sane bash-completion argyllcms git docbook-utils
             docbook-sgml perl-sgmls meson gtk-doc)
options=(!emptydirs)
_commit=5b9aa8de432579a2636f13ad6895928f42511081  # tags/1.4.2^0
source=("git+https://github.com/hughsie/colord#commit=$_commit"
        0001-Make-cd_color_get_blackbody_rgb_full-safer.patch)
sha256sums=('SKIP'
            '73689805d46705c8680d9ae979a2f5203f661741e56aa07dc627b36059ab404b')
validpgpkeys=('163EB50119225DB3DF8F49EA17ACBA8DFA970E17')  # Richard Hughes

pkgver() {
  cd colord
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd colord
  patch -Np1 -i ../0001-Make-cd_color_get_blackbody_rgb_full-safer.patch
}

build() {
  arch-meson colord build \
    -Denable-libcolordcompat=true \
    -Denable-sane=true \
    -Denable-vala=true \
    -Denable-print-profiles=true \
    -Dwith-daemon-user=colord
  ninja -C build
}

check() {
  cd build
  meson test
}

package_colord() {
  optdepends=('argyllcms: color profiling'
              'colord-sane: SANE support')
  replaces=(shared-color-profiles)

  DESTDIR="$pkgdir" ninja -C build install

  echo 'u colord - "Color management daemon" /var/lib/colord' |
    install -Dm644 /dev/stdin "$pkgdir/usr/lib/sysusers.d/colord.conf"

### Split colord-sane
  mkdir -p colord-sane/usr/lib/colord-plugins
  mv {"$pkgdir",colord-sane}/usr/lib/colord-sane
  mv {"$pkgdir",colord-sane}/usr/lib/colord-plugins/libcolord_sensor_sane.so
}

package_colord-sane() {
  pkgdesc+=" (SANE support)"
  depends=("colord=$pkgver-$pkgrel" sane)
  mv colord-sane/* "$pkgdir"
}

# vim:set ts=2 sw=2 et:
