# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Sergej Pupykin <sergej@aur.archlinux.org>

pkgname=man-db
pkgver=2.5.5
pkgrel=2
pkgdesc="A utility for reading man pages"
arch=('i686' 'x86_64')
url="http://www.nongnu.org/man-db/"
license=('GPL' 'LGPL')
groups=('base')
depends=( 'bash' 'gdbm' 'zlib' 'groff')
optdepends=('less' 'gzip')
backup=(etc/man_db.conf)
conflicts=('man')
provides=('man')
replaces=('man')
install=${pkgname}.install
source=(#http://savannah.nongnu.org/download/man-db/$pkgname-$pkgver.tar.gz
	http://launchpad.net/man-db/main/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz
        convert-mans man-db.cron.daily
	rev1051.diff
	fix-14467.patch)
md5sums=('ca382dd934fc8b9e9a64d13354be48cf'
         '2b7662a7d5b33fe91f9f3e034361a2f6'
         'acdff891e224511cf26c92de77347db8'
         '7dc2e8324cbc2ae777fe966ef7a7d0c8'
         '90554646622ffc1258648bd79a2f8e4c')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  
  # fix missing sections from posix man-pages
  # see http://bugs.archlinux.org/task/13734 and http://bugs.debian.org/519807 ; solved in 
  # http://bzr.savannah.gnu.org/lh/man-db/trunk/revision/1051?start_revid=1055&filter_file_id=x_Arch_Librarian_<arch%40canonical.com>_Tue_Mar_22_20%3A41%3A03_2005_29634.0
  patch -Np0 -i ${srcdir}/rev1051.diff || return 1
  # libdb/db_lookup.c (dblookup): In exact mode, make sure extensions                                                                                                   
  # match exactly, rather than merely that the extension found is a                                                                                                     
  # prefix of that which was requested (http://bugs.archlinux.org/task/14467).      
  patch -Np0 -i ${srcdir}/fix-14467.patch || return 1
  
  autoreconf -fv
  
  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib \
	--with-db=gdbm --disable-setuid --enable-mandirs=GNU \
	--with-sections="1 n l 8 3 0 2 5 4 9 6 7"
  make || return 1
  make DESTDIR=${pkgdir} install || return 1

  # part of groff pkg
  rm -f ${pkgdir}/usr/bin/zsoelim

  # script from LFS to convert manpages, see
  # http://www.linuxfromscratch.org/lfs/view/6.4/chapter06/man-db.html
  install -D -m755 ${srcdir}/convert-mans  ${pkgdir}/usr/bin/convert-mans 

  #install whatis cron script
  install -D -m744 ${srcdir}/man-db.cron.daily ${pkgdir}/etc/cron.daily/man-db
}

