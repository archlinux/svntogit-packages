# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kpimtextedit
pkgver=19.08.3
pkgrel=1
pkgdesc="A textedit with PIM-specific features"
arch=(x86_64)
url="https://kontact.kde.org"
license=(LGPL)
depends=(kio kemoticons grantlee syntax-highlighting)
makedepends=(extra-cmake-modules kdesignerplugin qt5-tools)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('75ff2300e771a1fe02a19ef57e2a28489179a92dc184c924be495e878b4444ab'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
