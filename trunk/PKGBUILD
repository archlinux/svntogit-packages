# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
pkgname=empathy
pkgver=2.30.3
pkgrel=1
pkgdesc="A GNOME instant messaging client using the Telepathy framework."
arch=('i686' 'x86_64')
url="http://live.gnome.org/Empathy"
license=('GPL2')
depends=('iso-codes' 'telepathy-mission-control>=5.4.1' 'telepathy-farsight>=0.0.14' 'libcanberra' 'libnotify' 'libunique>=1.1.6' 'libwebkit>=1.2.3' 'evolution-data-server>=2.30.3')
makedepends=('pkg-config' 'intltool' 'gnome-doc-utils>=0.20.1' 'nautilus-sendto')
optdepends=('telepathy-gabble: XMPP/Jabber support'
            'telepathy-butterfly: MSN support'
            'telepathy-idle: IRC support'
            'telepathy-salut: Link-local XMPP support'
            'telepathy-sofiasip: SIP support'
            'telepathy-haze: libpurple support')
options=('!libtool')
groups=('gnome-extra')
install=empathy.install
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/2.30/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('5965434fcc05f7ec3cd32d9128b03ac20e96624b0035d329abf72decd70c5def')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
      --sysconfdir=/etc  \
      --libexecdir=/usr/lib/empathy \
      --disable-schemas-install
  make
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 \
      DESTDIR="${pkgdir}" install

  # Merge schemas file in a single file and remove trash
  mkdir -p "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" \
      --domain empathy "${pkgdir}/etc/gconf/schemas/"*.schemas
  rm -rf "${pkgdir}/etc/gconf"
}
