# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-session
pkgver=3.26.1
pkgrel=2
pkgdesc="The GNOME Session Handler"
url="https://git.gnome.org/browse/gnome-session"
arch=(x86_64)
license=(GPL LGPL)
depends=(systemd dconf gsettings-desktop-schemas gtk3 gnome-desktop
         json-glib libgl libsm libxtst systemd)
makedepends=(intltool mesa gtk-doc xtrans git gnome-common python xmlto)
groups=(gnome)
_commit=26514ecb0844b7f3acd9fd8c70343450ecf7afca  # tags/3.26.1^0
source=("git+https://git.gnome.org/browse/gnome-session#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}
  
build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib \
      --disable-schemas-compile --enable-systemd --disable-gconf
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
