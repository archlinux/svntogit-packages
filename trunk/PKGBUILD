# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Jameson Pugh <imntreal@gmail.com>

pkgname=ell
_commit=ebc021cee3b2d20ca2af5e3ed02495da616b2fd3 # tags/0.17
pkgver=0.17
pkgrel=1
pkgdesc="Embedded Linux library"
arch=(x86_64)
url="https://01.org/ell"
#url="https://git.kernel.org/pub/scm/libs/ell/ell.git/"
license=('LGPL2.1')
depends=('glibc')
makedepends=('git')
source=("git+https://git.kernel.org/pub/scm/libs/ell/ell.git/#commit=$_commit")
sha256sums=('SKIP')

prepare() {
  cd "$pkgname"
  ./bootstrap
}

build() {
  cd "$pkgname"
  ./configure --prefix=/usr
  make
}

check() {
  cd "$pkgname"
  make -k check
}

package() {
  cd "$pkgname"
  make DESTDIR="$pkgdir/" install
}
