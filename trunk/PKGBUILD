# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Paul Mattal <paul@archlinux.org>

pkgname=lirc
_pkgver=0.9.4c
[[ $_pkgver =~ [a-z]$ ]] && pkgver="${_pkgver:0:-1}.${_pkgver: -1}" || pkgver="$_pkgver"
pkgrel=4
epoch=1
pkgdesc="Linux Infrared Remote Control utilities"
arch=('i686' 'x86_64')
url="http://www.lirc.org/"
license=('GPL')
depends=('alsa-lib' 'libx11' 'libftdi' 'libusb-compat')
makedepends=('help2man' 'alsa-lib' 'libx11' 'libxslt' 'python')
optdepends=('python: for lirc-setup, irdb-get and pronto2lirc')
provides=('lirc-utils')
conflicts=('lirc-utils')
replaces=('lirc-utils')
backup=('etc/lirc/lirc_options.conf' 'etc/lirc/lircd.conf' 'etc/lirc/lircmd.conf')
source=("http://prdownloads.sourceforge.net/${pkgname}/${pkgname}-${_pkgver}.tar.bz2"
        0005-lib-curl_poll.h-Ensure-build-on-unconfiguredclients.patch
        0006-lirc.pc-Fix-bad-library-specification-236.patch
        0007-Build-Use-HAVE_UINPUT-1-to-force-building-uinput-cod.patch
        lirc.logrotate
        lirc.tmpfiles)
sha1sums=('e6a04fa7447b2c3bc5bac7658fa2cc377ae133ed'
          '1f8e8f206c7f2b8275c8a55d15e1520ec35602ac'
          '03ddd50f59ce9c44b51a5855a0673cb15ded5fa4'
          '0b4d5dfdc940a0233e6f81a07d910a6c84732577'
          '4342b004eb53d51fcbb9af2cf136bb4990874608'
          '5cd3f206e6e60632d9bea2ce9d22dbe5283eb129')

prepare() {
  cd "${srcdir}/lirc-${_pkgver}"

  # Downstream build problems
  patch -Np1 -i ../0005-lib-curl_poll.h-Ensure-build-on-unconfiguredclients.patch
  patch -Np1 -i ../0006-lirc.pc-Fix-bad-library-specification-236.patch

  # devtools chroots have no /dev/uinput
  patch -Np1 -i ../0007-Build-Use-HAVE_UINPUT-1-to-force-building-uinput-cod.patch

  autoreconf -fi
}

build() {
  cd "${srcdir}/lirc-${_pkgver}"

  HAVE_UINPUT=1 ./configure --prefix=/usr --sbindir=/usr/bin --sysconfdir=/etc --localstatedir=/var
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd "${srcdir}/lirc-${_pkgver}"

  make DESTDIR="${pkgdir}" -j1 install

  install -Dm644 "${srcdir}"/lirc.tmpfiles "${pkgdir}"/usr/lib/tmpfiles.d/lirc.conf
  install -Dm644 "${srcdir}"/lirc.logrotate "${pkgdir}"/etc/logrotate.d/lirc

  rmdir "${pkgdir}"/var/{run/lirc/,run/}
}
