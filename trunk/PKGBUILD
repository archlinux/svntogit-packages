# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=plasma-firewall
pkgver=5.27.4
_dirver=$(echo $pkgver | cut -d. -f1-3)
pkgrel=1
pkgdesc='Control Panel for your system firewall'
arch=(x86_64)
url='https://kde.org/plasma-desktop/'
license=(GPL LGPL)
source=(https://download.kde.org/stable/plasma/$_dirver/$pkgname-$pkgver.tar.xz{,.sig})
depends=(systemsettings)
makedepends=(extra-cmake-modules)
optdepends=('iproute2: netstat backend'
            'firewalld: firewalld backend'
            'ufw: ufw backend')
groups=(plasma)
sha256sums=('349db4a0908d7a6565eb8b80971eb2ed4f6d4f1730ee65de2fd23f2b1343a3ac'
            'SKIP')
validpgpkeys=('E0A3EB202F8E57528E13E72FD7574483BB57B18D'  # Jonathan Esk-Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DLIBEXEC_INSTALL_DIR=lib
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
