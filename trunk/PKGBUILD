# $Id$
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>

pkgname=attr
pkgver=2.4.46
pkgrel=2
pkgdesc="Extended attribute support library for ACL support"
arch=('i686' 'x86_64')
url="http://savannah.nongnu.org/projects/attr"
license=('LGPL')
depends=('glibc') 
makedepends=('gettext')
replaces=('xfsattr')
provides=('xfsattr')
conflicts=('xfsattr')
options=('!libtool')
source=("http://download.savannah.gnu.org/releases/$pkgname/$pkgname-$pkgver.src.tar.gz"{,.sig})
sha256sums=('dcd69bdca7ff166bc45141eddbcf21967999a6b66b0544be12a1cc2fd6340e1f'
            'a8ee37a0d02c0365049b8a06dc6963e6698a448ff2c0a54ed040f0c60f27bca3')

build() {
  cd "$pkgname-$pkgver"

  export INSTALL_USER=root INSTALL_GROUP=root
  ./configure --prefix=/usr --libdir=/usr/lib --libexecdir=/usr/lib
  make
}

package() {
  make -C "$pkgname-$pkgver" DIST_ROOT="$pkgdir" install install-lib install-dev

  # tidy up
  rm -f "$pkgdir"/usr/lib/libattr.a
  chmod 0755 "$pkgdir"/usr/lib/libattr.so.*.*.*

  # remove conflicting manpages
  rm -rf "$pkgdir"/usr/share/man/man2
}
