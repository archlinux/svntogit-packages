# $Id$
# Maintainer: Aaron Griffin <aaron@archlinux.org>
# Maintainer: Allan McRae <allan@archlinux.org>

pkgname=bash
_patchlevel=009 #prepare for some patches
pkgver=4.1.$_patchlevel
pkgrel=3
pkgdesc="The GNU Bourne Again shell"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnu.org/software/bash/bash.html"
groups=('base')
backup=(etc/skel/.bashrc etc/skel/.bash_profile)
depends=('readline>=6.1' 'glibc')
provides=('sh')
install=bash.install
source=(http://ftp.gnu.org/gnu/bash/bash-4.1.tar.gz
        bashrc
        system.bashrc)
if [ $_patchlevel -gt 000 ]; then
    for (( p=1; p<=$((10#${_patchlevel})); p++ )); do
        source=(${source[@]} http://ftp.gnu.org/gnu/bash/bash-4.1-patches/bash41-$(printf "%03d" $p))
    done
fi

build() {
  cd ${srcdir}/${pkgname}-4.1
  for p in ../bash41-*; do
    [ -e "$p" ] || continue
    msg "applying patch ${p}"
    patch -Np0 -i ${p}
  done

  _bashconfig=(-DDEFAULT_PATH_VALUE=\'\"/usr/local/bin:/usr/bin:/bin:/usr/local/sbin:/usr/sbin:/sbin\"\'
               -DSTANDARD_UTILS_PATH=\'\"/usr/bin:/bin:/usr/sbin:/sbin\"\'
               -DSYS_BASHRC=\'\"/etc/bash.bashrc\"\'
               -DSYS_BASH_LOGOUT=\'\"/etc/bash.bash_logout\"\'
               -DNON_INTERACTIVE_LOGIN_SHELLS)
  export CFLAGS="${CFLAGS} ${_bashconfig[@]}"

  ./configure --prefix=/usr --with-curses --enable-readline \
    --without-bash-malloc --with-installed-readline \
    --bindir=/bin --mandir=/usr/share/man --infodir=/usr/share/info
  make
  #make check
}

package() {
  cd ${srcdir}/${pkgname}-4.1
  make DESTDIR=${pkgdir} install

  install -Dm644 ${srcdir}/system.bashrc ${pkgdir}/etc/bash.bashrc

  # for now, bash is our default /bin/sh
  cd ${pkgdir}/bin
  ln -s bash sh

  mkdir -p ${pkgdir}/etc/skel/
  install -D -m644 ${srcdir}/bashrc ${pkgdir}/etc/skel/.bashrc
  echo ". \$HOME/.bashrc" >${pkgdir}/etc/skel/.bash_profile
}

md5sums=('9800d8724815fd84994d9be65ab5e7b8'
         '3d2837e84645eac9bde399b58014bafa'
         '196697769f1667a8a1aed608811129c6'
         '582dea5671b557f783e18629c2f77b68'
         '118d465095d4a4706eb1d34696a2666a'
         '120f7cf039a40d35fe375e59d6f17adc'
         '336ee037fc2cc1e2350b05097fbdc87c'
         '9471e666797f0b03eb2175ed752a9550'
         'fb80ccd58cb1e34940f3adf4ce6e4a1e'
         '192a8b161d419a1d0d211169f1d1046e'
         '1dc5a0253827decbe6aa529cde83e154'
         'ea361b842b7405a7f5ebbabbdf74372f')
