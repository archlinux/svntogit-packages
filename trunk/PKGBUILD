# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=rcs
pkgver=5.9.3
pkgrel=1
pkgdesc='Revision Control System: manages multiple revisions of files'
url='http://www.gnu.org/software/rcs/'
license=('GPL3')
arch=('i686' 'x86_64')
depends=('ed')
source=("ftp://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.xz"{,.sig})
sha1sums=('e4a9549678618f5d69968b10c6c9b92b29519813' 'SKIP')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr
	make
}

check() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make check
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make prefix="${pkgdir}/usr" install
}
