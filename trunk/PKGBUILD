# Maintainer: Eric BÃ©langer <eric@archlinux.org>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>

pkgname=geeqie
pkgver=1.4
pkgrel=3
pkgdesc="A lightweight image browser and viewer"
arch=('x86_64')
url="http://www.geeqie.org/"
license=('GPL2')
depends=('exiv2' 'gtk2' 'ffmpegthumbnailer')
makedepends=('intltool' 'doxygen' 'gnome-doc-utils' 'python')
optdepends=('librsvg: SVG rendering'
	    'fbida: for jpeg rotation')
validpgpkeys=('85D4CA42952C949B175362B379D0B06F4E20AF1C')
source=(http://www.geeqie.org/${pkgname}-${pkgver}.tar.xz{,.asc} geeqie-no-changelog.patch)
sha256sums=('5c583a165573ec37874c278f9dc57e73df356b30e09a9ccac3179dd5d97e3e32'
            'SKIP'
            'f6eb2725a47fc750d95455d4bbf4be5a0ae45307d3ada571937212a25b2b4301')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../geeqie-no-changelog.patch # Workaround missing changelog (Fedora)
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package(){
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
