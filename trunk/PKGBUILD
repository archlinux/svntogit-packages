# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor:  <mathieu.clabaut@gmail.com>

pkgname=xdg-user-dirs
pkgver=0.15
pkgrel=2
pkgdesc="Manage user directories like ~/Desktop and ~/Music"
arch=(i686 x86_64)
url="http://www.freedesktop.org/wiki/Software/xdg-user-dirs"
license=(GPL)
depends=(sh)
makedepends=(docbook-xsl)
backup=(etc/xdg/user-dirs.conf etc/xdg/user-dirs.defaults)
source=(http://user-dirs.freedesktop.org/releases/$pkgname-$pkgver.tar.gz
        60-xdg-user-dirs.sh)
sha256sums=('20b4a751f41d0554bce3e0ce5e8d934be98cc62d48f0b90a894c3e1916552786'
            '328ae6908bff2842ea0960730b7d4802bdce66473768e38ebc9368ce78da7156')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install 
  install -D ../60-xdg-user-dirs.sh "$pkgdir/etc/X11/xinit/xinitrc.d/60-xdg-user-dirs.sh"
}
