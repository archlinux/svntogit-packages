# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>

pkgname=wireless-regdb
pkgver=2022.04.08
pkgrel=1
pkgdesc="Central Regulatory Domain Database"
arch=('any')
url="https://wireless.wiki.kernel.org/en/developers/regulatory/wireless-regdb"
license=('custom')
depends=('bash' 'iw')
replaces=('crda')
provides=('crda')
conflicts=('crda')
backup=(etc/conf.d/wireless-regdom)
source=(https://www.kernel.org/pub/software/network/wireless-regdb/${pkgname}-${pkgver}.tar.{xz,sign}
        wireless-regdom
        set-wireless-regdom
        85-regulatory.rules)
sha256sums=('8828c25a4ee25020044004f57374bb9deac852809fad70f8d3d01770bf9ac97f'
            'SKIP'
            '192428fd959806705356107bffc97b8b379854e79bd013c4ee140e5202326e2b'
            '603ce97da5cce3f5337e99007ce04e2f295bb33a36b308794884011f7bcabaf3'
            'f5055dfe7584757a11b419836320bcdcd1d3d1e5caf26e20c63a0c64779f3376')
validpgpkeys=('2ABCA7498D83E1D32D51D3B5AB4800A62DB9F73A') #Seth Forshee <seth.forshee@canonical.com>

package() {
  install -Dm644 wireless-regdom -t "${pkgdir}"/etc/conf.d
  install -Dm644 85-regulatory.rules -t "${pkgdir}"/usr/lib/udev/rules.d
  install -Dm755 set-wireless-regdom -t "${pkgdir}"/usr/bin

  cd ${pkgname}-${pkgver}
  make install DESTDIR="${pkgdir}" MANDIR=usr/share/man FIRMWARE_PATH=usr/lib/firmware

  for dom in $(grep ^country db.txt | cut -d' ' -f2 | sed 's|:||g'); do
    echo "#WIRELESS_REGDOM=\"${dom}\"" >> "${pkgdir}"/etc/conf.d/wireless-regdom.tmp
  done
  sort -u "${pkgdir}"/etc/conf.d/wireless-regdom.tmp >> "${pkgdir}"/etc/conf.d/wireless-regdom
  rm "${pkgdir}"/etc/conf.d/wireless-regdom.tmp

  install -D -m644 "${srcdir}"/${pkgname}-${pkgver}/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
