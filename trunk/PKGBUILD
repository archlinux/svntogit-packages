# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=run-parts
pkgver=4.5.1
pkgrel=1
pkgdesc='run scripts or programs in a directory'
arch=('i686' 'x86_64')
url='http://packages.qa.debian.org/d/debianutils.html'
license=('GPL')
depends=('glibc')
source=("https://sources.archlinux.org/other/run-parts/debianutils_${pkgver}.tar.xz")
sha256sums=('a531c23e0105fe01cfa928457a8343a1e947e2621b3cd4d05f4e9656020c63b7')

build() {
	cd $srcdir/debianutils
	./configure --prefix=/usr
	make run-parts
}

package() {
	cd $srcdir/debianutils
	install -D -m755 run-parts $pkgdir/usr/bin/run-parts
	install -D -m644 run-parts.8 $pkgdir/usr/share/man/man8/run-parts.8
	for l in po4a/*/run-parts.8; do
		install -D -m644  ${l} $pkgdir/usr/share/man/${l:5:2}/man8/run-parts.8
	done
}
