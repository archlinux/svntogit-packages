# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=four-in-a-row
pkgver=3.13.92
pkgrel=1
pkgdesc="Make lines of the same color to win"
arch=('i686' 'x86_64')
license=('GPL3')
depends=('hicolor-icon-theme' 'gtk3' 'libcanberra' 'librsvg')
makedepends=('intltool' 'itstool' 'gobject-introspection' 'python2')
conflicts=('gnome-games')
replaces=('gnome-games')
options=('!emptydirs')
install=four-in-a-row.install
url="https://live.gnome.org/Four-in-a-row"
groups=('gnome-extra')
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('b93bce57257af34a51d92c9558079b2b9b8d81f7a4d15ce67045373b293c245d')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
