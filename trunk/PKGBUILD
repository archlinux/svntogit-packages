# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=liferea
pkgver=1.14.3
pkgrel=2
pkgdesc="A desktop news aggregator for online news feeds and weblogs"
arch=('x86_64')
url="https://lzone.de/liferea"
license=('GPL')
depends=('webkit2gtk' 'libpeas' 'python-gobject')
makedepends=('intltool' 'gobject-introspection')
optdepends=('libsecret: for keyring plugin support'
            'python-cairo: for the tray icon plugin'
	    'gst-plugins-base: for media player plugin support'
	    'gst-plugins-good: for media player plugin support'
	    'gst-plugins-bad: for media player plugin support')
options=('!emptydirs')
source=(https://github.com/lwindolf/liferea/releases/download/v$pkgver/$pkgname-${pkgver}.tar.bz2
        https://github.com/lwindolf/liferea/commit/91241bb0f0abb00ac82c31ebe07e98808a7f86c3.patch)
sha256sums=('566132197ade3dd9d1cd195d48e39f881e79281bbbc3dc7863dbc0f20b9fa2de'
            'c5b9ac711ad7648e3c214b3776c266408d6ec0b7c583a7fc0836f9f83a1d107c')

prepare() {
  cd $pkgname-${pkgver}

  patch -p1 -i "${srcdir}"/91241bb0f0abb00ac82c31ebe07e98808a7f86c3.patch
}

build() {
  cd $pkgname-${pkgver}
  
  # https://github.com/lwindolf/liferea/issues/767
  export WEBKIT_DISABLE_COMPOSITING_MODE=1
  
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-schemas-compile
  make
}

package() {
  cd $pkgname-${pkgver}
  make DESTDIR="${pkgdir}" install
}
