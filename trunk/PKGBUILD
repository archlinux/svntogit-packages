# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=cairo-perl
pkgver=1.081
pkgrel=1
pkgdesc="Perl wrappers for cairo"
arch=(i686 x86_64)
license=('LGPL')
url="http://gtk2-perl.sourceforge.net/"
options=('!emptydirs')
depends=('cairo' 'perl')
makedepends=('perl-extutils-depends' 'perl-extutils-pkgconfig')
source=(http://downloads.sourceforge.net/sourceforge/gtk2-perl/Cairo-${pkgver}.tar.gz)
md5sums=('52c712b0f749f2fe232ae6e18880fe7d')

build() {
  cd "${srcdir}/Cairo-${pkgver}"
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "${srcdir}/Cairo-${pkgver}"
  make test
}

package() {
  cd "${srcdir}/Cairo-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
