# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libx11
pkgver=1.3.5
pkgrel=2
pkgdesc="X11 client-side library"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
depends=('libxcb>=1.7' 'kbproto>=1.0.5')
makedepends=('xproto>=7.0.18' 'xextproto>=7.1.2' 'xf86bigfontproto>=1.2.0' 'xtrans>=1.2.5' 'inputproto>=2.0' 'xorg-util-macros>=1.10.0')
options=('!libtool')
license=('custom:XFREE86')
source=(${url}/releases/individual/lib/libX11-${pkgver}.tar.bz2
        xorg.sh)
sha1sums=('c5dda2b6279f3e3267646576138fcd091a7f3c54'
          '4b756387426afb2a5bb7a155631ec42348abf5c1')

build() {
  cd "${srcdir}/libX11-${pkgver}"
  ./configure --prefix=/usr \
      --with-xcb --disable-static
  make
  make check
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/etc/profile.d"
  install -m755 "${srcdir}/xorg.sh" "${pkgdir}/etc/profile.d/"

  install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
