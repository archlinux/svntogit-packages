# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Tobias Powalowski <t.powa@gmx.de>

pkgname=pstoedit
pkgver=3.73
pkgrel=1
pkgdesc="Translates PostScript and PDF graphics into other vector formats"
arch=('x86_64')
url="http://www.pstoedit.net/"
license=('GPL')
depends=('gcc-libs' 'plotutils' 'gd' 'libmagick')
makedepends=('ghostscript')
source=("https://downloads.sourceforge.net/sourceforge/pstoedit/pstoedit-${pkgver}.tar.gz")
sha512sums=('77823d77dcec8604decab03cee86edb1ef0a6c56b8dd01a65ab6431d9345bc6a2140076f5d5572fd5221b7f381a07d14b2a45e66cff2a6e12ab7417cd95113b6')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -e 's|ImageMagick++|Magick++|' -i configure.ac
  sed -i 's/-pedantic//' configure.ac
  autoreconf -vi
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
