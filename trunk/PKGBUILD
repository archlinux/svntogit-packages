# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=ktextaddons
pkgver=1.1.0
pkgrel=2
pkgdesc='Various text handling addons'
arch=(x86_64)
license=(GPL)
url='https://invent.kde.org/libraries/ktextaddons'
depends=(kxmlgui qtkeychain-qt5 qt5-speech)
makedepends=(extra-cmake-modules doxygen qt5-tools)
optdepends=('languagetool: Grammar checking'
            'grammalecte: French grammar checking'
            'libreoffice: Use autocorrection data from LibreOffice')
source=(https://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz)
sha256sums=('055d6d1c20fa906239663f0f4597046a72e2d4eee1b92faa5228e3b5e3c3be29')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON \
    -DBUILD_DESIGNERPLUGIN=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
