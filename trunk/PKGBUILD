# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-data
pkgver=15.08.3
pkgrel=1
pkgdesc="Common data for KDE Edu applications"
url="http://kde.org/applications/education"
arch=('any')
license=('GPL' 'LGPL' 'FDL')
makedepends=('extra-cmake-modules')
conflicts=('libkdeedu<14')
source=("http://download.kde.org/stable/applications/${pkgver}/src/$pkgname-${pkgver}.tar.xz")
sha1sums=('b2e23a54477ac23ed7d7a2ebaf94ececa84a705d')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
