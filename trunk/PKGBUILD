# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Hussam Al-Tayeb <ht990332@gmail.com>

pkgname=xmlsec
pkgver=1.2.35
pkgrel=1
pkgdesc="XML Security Library is a C library based on LibXML2"
license=('custom')
arch=('x86_64')
url="https://www.aleksey.com/xmlsec/index.html"
depends=('libxslt' 'openssl' 'gnutls' 'nss' 'libltdl')
provides=('libxmlsec1.so' 'libxmlsec1-openssl.so' 'libxmlsec1-nss.so' 'libxmlsec1-gnutls.so' 'libxmlsec1-gcrypt.so')
options=('debug')
#source=(https://www.aleksey.com/xmlsec/download/${pkgname}1-${pkgver}.{tar.gz,sig}) # signature file not compatible
source=(https://www.aleksey.com/xmlsec/download/${pkgname}1-${pkgver}.tar.gz
        0001_xmlsec_1.2.35-buildfix.patch::https://patch-diff.githubusercontent.com/raw/lsh123/xmlsec/pull/412.patch
        #0002_remove_test_nssdb.patch::https://patch-diff.githubusercontent.com/raw/lsh123/xmlsec/pull/418.patch
        0002_remove_test_nssdb.patch
)
sha256sums=('a39f86f6fe8e8cdc998b9a911ef1fae03f57a9ef99c2b704cd492007a52ac223'
            'bf62a51337a3bad0b1a81311edf1dc0d74cb78b05ed0e49ed4c0d9507fed1b03'
            '693737c12b2c95c437816d8b6f0c95a81772100a5b43f918ff842b5bd89138e9')
#validpgpkeys=('') #

prepare() {
  cd ${pkgname}1-${pkgver}
  # build fix / https://github.com/lsh123/xmlsec/issues/411
  patch -Np1 -i ../0001_xmlsec_1.2.35-buildfix.patch
  # https://github.com/lsh123/xmlsec/issues/415
  rm tests/nssdb/cert8.db tests/nssdb/key3.db tests/nssdb/secmod.db
  patch -Np1 -i ../0002_remove_test_nssdb.patch
}

build() {
  cd ${pkgname}1-${pkgver}
  ./configure --prefix=/usr --disable-static
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd ${pkgname}1-$pkgver
  # tests run in fakeroot fail expected in xmlsec-gcrypt
  # https://github.com/lsh123/xmlsec/issues/415
  make -k check || /bin/true
}

package() {
  cd ${pkgname}1-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
