# $Id$
# Maintainer: 
# Contributor:	Jesse Jaara	<gmail.com: jesse.jaara>
# Contributor:	Jussi Timperi	<gmail.com: jussi.timperi>

pkgname=libvoikko
pkgver=4.0.2
pkgrel=2
pkgdesc="A spelling and grammar checker, hyphenator and collection of related linguistic data for Finnish language"
arch=(i686 x86_64)
url="http://voikko.sourceforge.net"
license=(GPL2)
depends=(gcc-libs)
makedepends=(python)
source=(http://www.puimula.org/voikko-sources/libvoikko/$pkgname-$pkgver.tar.gz)
md5sums=('67f168ac77c552945380a06a9e73bbda')

build() {
  cd $pkgname-$pkgver

# Workaround encoding error
  export LANG=en_US.UTF-8
  ./configure --prefix=/usr --enable-hfst=false --with-dictionary-path=/usr/share/voikko
  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install
  mkdir -p "$pkgdir"/usr/lib/python3.6/site-packages
  cp python/libvoikko.py "$pkgdir"/usr/lib/python3.6/site-packages/libvoikko.py
}

