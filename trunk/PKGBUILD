# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=libgcrypt
pkgver=1.4.6
pkgrel=1
pkgdesc="a general purpose crypto library based on the code used"
arch=(i686 x86_64)
url="http://www.gnupg.org"
license=('LGPL')
depends=('libgpg-error>=1.7')
options=(!libtool)
source=(ftp://ftp.gnupg.org/gcrypt/${pkgname}/${pkgname}-${pkgver}.tar.bz2
	#ftp://ftp.franken.de/pub/crypt/mirror/ftp.gnupg.org/gcrypt/libgcrypt/${pkgname}-${pkgver}.tar.bz2
)
md5sums=('dbf99425a4fe9217c84ce3a35d938634')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  # keep static library for , needed for cryptsetup
  ./configure --prefix=/usr --disable-padlock-support
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  # Move dynamic libraries to /lib
  install -d -m755 "${pkgdir}"/lib/ || return 1
  mv "${pkgdir}"/usr/lib/libgcrypt.so* "${pkgdir}"/lib/ || return 1
  ln -sf /lib/libgcrypt.so "${pkgdir}"/usr/lib/libgcrypt.so || return 1
}
