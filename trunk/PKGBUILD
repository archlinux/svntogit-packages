# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgname=openexr
pkgver=1.7.1
pkgrel=2
pkgdesc="An high dynamic-range image file format library"
url="http://www.openexr.com/"
arch=('i686' 'x86_64')
license=('BSD')
depends=('zlib' 'ilmbase')
options=('!libtool')
source=("https://github.com/downloads/${pkgname}/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        'gcc43.patch')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p0 -i "${srcdir}/gcc43.patch"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

md5sums=('a9e0369dfd68941c50cdaa93e9d7a8d1'
         'd91fae759053a70bcedcf02eb33ca4a2')
