# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=gnome-online-accounts
pkgver=3.14.2
pkgrel=1
pkgdesc="GNOME service to access online accounts"
arch=(i686 x86_64)
url="http://www.gnome.org"
license=('GPL')
depends=('webkitgtk' 'json-glib' 'libnotify' 'rest' 'libsecret' 'telepathy-glib' 'krb5' 'gcr')
makedepends=('intltool' 'libxslt' 'gobject-introspection' 'docbook-xsl' 'python2')
install=$pkgname.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('341a2c79cd26770ccbf4476f7ce7222cfe026f442cfbd05dc7f7612f7b30dae9')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --libexecdir=/usr/lib/$pkgname --disable-static \
    --enable-exchange --enable-facebook --enable-flickr \
    --enable-google --enable-imap-smtp --enable-kerberos \
    --enable-owncloud --enable-telepathy --enable-windows-live \
    --enable-media-server
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
