# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-oclock
pkgver=1.0.4
pkgrel=1
pkgdesc="Round X clock"
arch=(x86_64)
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libx11' 'libxmu' 'libxext' 'libxkbfile')
makedepends=('xorg-util-macros')
source=(https://xorg.freedesktop.org/archive/individual/app/oclock-${pkgver}.tar.bz2{,.sig})
sha512sums=('6e0d36a84572a153226b2929f2253cb7adc3c9e5e3af168aeaf8c487ade2e43517b271a8e9cdb2a2c4a67ca56ed0b99c194ca63a4a822b68bfc72ca6518335e7'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # Alan Coopersmith <alan.coopersmith@oracle.com>

build() {
  cd "${srcdir}/oclock-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/oclock-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
