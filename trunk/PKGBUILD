# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=run-parts
pkgver=3.4.4
pkgrel=1
pkgdesc='run scripts or programs in a directory'
arch=('i686' 'x86_64')
url='http://packages.qa.debian.org/d/debianutils.html'
license=('GPL')
depends=('glibc')
source=("http://ftp.de.debian.org/debian/pool/main/d/debianutils/debianutils_${pkgver}.tar.gz")
sha256sums=('f5d6f6116bb2b474399c07f519c3a30bcda5c85994b43195fdf09508031c72fe')

build() {
	cd $srcdir/debianutils-$pkgver
	./configure --prefix=/usr
	make run-parts
}

package() {
	cd $srcdir/debianutils-$pkgver
	install -D -m755 run-parts $pkgdir/usr/bin/run-parts
	install -D -m644 run-parts.8 $pkgdir/usr/share/man/man8/run-parts.8
}
