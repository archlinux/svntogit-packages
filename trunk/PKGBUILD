# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Miguel Revilla <yo at miguelrevilla.com>
# Contributor: David Sotelo <dvsotelo at gmail.com>
# Contributor: Nuno Araujo <nuno.araujo@russo79.com>

pkgname=qpdf
pkgver=5.2.0
pkgrel=1
pkgdesc="QPDF: A Content-Preserving PDF Transformation System"
arch=('i686' 'x86_64')
url="http://qpdf.sourceforge.net/"
license=('custom:Artistic-2.0')
depends=('pcre' 'perl')
makedepends=('make')
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz)
sha1sums=('0f4cfbb090979f8173f3588da522693967af4600')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --disable-static
  make
}

check() {
  cd $pkgname-$pkgver
  make check # passes all
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}/" install

  mkdir -m755 -p ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 Artistic-2.0 ${pkgdir}/usr/share/licenses/${pkgname}/
}
