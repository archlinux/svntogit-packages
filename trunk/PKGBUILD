# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

pkgname=libxau
pkgver=1.0.10
pkgrel=2
pkgdesc="X11 authorisation library"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('glibc')
makedepends=('xorgproto')
provides=('libXau.so')
options=('debug')
source=(${url}/releases/individual/lib/libXau-${pkgver}.tar.xz{,.sig})
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # "Alan Coopersmith <alan.coopersmith@oracle.com>"
sha512sums=('e56a862a7bbccb108c9683eeb8b643d2587a121dd96982e1e7edc5bd0b676302d244bfaa750afc7fb2ad31df8b825095d779b6e2ab2c76f94e47095d90071ebb'
            'SKIP')

build() {
  cd libXau-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd libXau-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
