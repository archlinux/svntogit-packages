# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-xwininfo
pkgver=1.1.4
pkgrel=1
pkgdesc="Command-line utility to print information about windows on an X server"
arch=(x86_64)
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libxcb')
makedepends=('xorg-util-macros' 'libx11')
groups=('xorg-apps' 'xorg')
source=(https://xorg.freedesktop.org/archive/individual/app/xwininfo-${pkgver}.tar.bz2{,.sig})
sha512sums=('d3ab103a0a311e08d1eabc4346750c21eb7d2fbcdc27cab0989a2624b9cd488911614fcf9832603353450114b1231a8f8262cab16e090a0d458d83ec7b6b959b'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # "Alan Coopersmith <alan.coopersmith@oracle.com>"

build() {
  cd xwininfo-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd xwininfo-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
