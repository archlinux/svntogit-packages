# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: George Brooke <george+arch.aur@george-brooke.co.uk>

pkgname=telepathy-logger-qt
pkgver=17.08.0
pkgrel=1
pkgdesc="Qt bindings for telepathy logger"
arch=(x86_64)
url='https://www.kde.org/'
license=(GPL)
depends=(telepathy-qt telepathy-logger)
makedepends=(extra-cmake-modules doxygen python2)
source=("http://download.kde.org/stable/$pkgname//${pkgver%.*}/src/$pkgname-$pkgver.tar.xz")
sha256sums=('d098a77de2a58381d9ebcd64c8ae54c773b5206ed7888576653d3b505fe5032a')
validpgpkeys=(D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heirecka@exherbo.org>

prepare () {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DPYTHON_EXECUTABLE=/usr/bin/python2
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
