# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Dan McGee <dan@archlinux.org>
# Contributor: Florian Zeitz <florob at babelmonkeys dot de>

pkgname=numactl
pkgver=2.0.15
pkgrel=1
pkgdesc="Simple NUMA policy support"
arch=(x86_64)
url="https://github.com/numactl/numactl"
license=(LGPL2.1 GPL2)
depends=(glibc)
provides=(libnuma.so)
options=(debug)
source=($pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz)
sha512sums=('dcc76b8d36698a740e04206471352013b39bd61357d6090fc062681fa2f31e5f0b40c8582c06fd1a97919f494002bef1fa8330b6a6784b516c39961caa2a4320')
b2sums=('8525d7d1bc05ce47fadde0b64ad55c9b78caddc277297baf1f74666626ec418502c14e9640e36b74dc15ad435d5b77225167fb8ab6bc25a2344f7b037acc61a3')

prepare(){
  cd $pkgname-$pkgver
  autoreconf -fiv
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

check() {
  make -k test -C $pkgname-$pkgver || echo "Tests known to fail depending on system load."
}

package() {
  make DESTDIR="$pkgdir" install -C $pkgname-$pkgver
  install -vDm 644 $pkgname-$pkgver/README.md -t "$pkgdir/usr/share/doc/$pkgname/"
  # move_pages(2) is provided by man-pages and is more up-to-date there anyway
  rm -rf "$pkgdir/usr/share/man/man2"
}
