# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kaccounts-providers
pkgver=19.12.1
pkgrel=2
pkgdesc='Small system to administer web accounts for the sites and services across the KDE desktop, including: Google, Facebook, Owncloud, IMAP, Jabber and others'
arch=(x86_64)
url='https://www.kde.org/applications/internet/'
license=(GPL)
groups=(kde-applications kdenetwork)
depends=(kaccounts-integration)
makedepends=(extra-cmake-modules intltool)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
         kaccounts-google-login.patch::"https://cgit.kde.org/kaccounts-providers.git/patch/?id=0a71da4e"
         kaccounts-google-login-fix.patch::"https://cgit.kde.org/kaccounts-providers.git/patch/?id=5952bf")
sha256sums=('abcd1fa9f63248f3ce7f9c98d940b124ff5c70c1a3381a6cfad6ce7012b23c69'
            'SKIP'
            '853949eca50427f98b319aba13d6b6453326be1ca59fbada1387348585c5d4cd'
            '23a5ae6460b7ac11b4669bd911ef52a8d2b1f5f52588bead95a320530c8d1c50')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../kaccounts-google-login.patch # Fix login to Google
  patch -p1 -i ../kaccounts-google-login-fix.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
