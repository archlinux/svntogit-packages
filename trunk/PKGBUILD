# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kwallet
pkgver=5.97.0
pkgrel=4
pkgdesc='Secure and unified container for user passwords'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(knotifications kiconthemes kservice gpgme qca-qt5)
makedepends=(extra-cmake-modules kdoctools boost doxygen qt5-tools qt5-doc)
optdepends=('kwalletmanager: Configuration GUI')
provides=(org.freedesktop.secrets)
replaces=(kwallet-secrets)
groups=(kf5)
source=(https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz{,.sig}
        kwallet-dont-register-dummy-service.patch::https://invent.kde.org/frameworks/kwallet/-/merge_requests/33.patch)
sha256sums=('ab9b78b0c6aba461de6247974b7e60239cd2a52a1c2c6d67b92e466c7597d821'
            'SKIP'
            '99e8295bbce2f5afc3695c82d2f1c940cbe5a26be745daf078a2073e4d4762a8')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>
options=(debug)

prepare() {
  patch -d $pkgname-$pkgver -p1 < kwallet-dont-register-dummy-service.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
