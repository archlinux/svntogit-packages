# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=archlinux-appstream-data
pkgver=20180129
pkgrel=1
pkgdesc="Arch Linux application database for AppStream-based software centers"
arch=(any)
url="http://www.archlinux.org"
license=(GPL)
depends=()
makedepends=()
source=()
noextract=()
for _repo in core extra community; do
 source+=($_repo.xml.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/Components-x86_64.xml.gz
          $_repo-icons-64x64.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-64x64.tar.gz
          $_repo-icons-128x128.tar.gz::https://sources.archlinux.org/other/packages/$pkgname/$pkgver/$_repo/icons-128x128.tar.gz)
 noextract+=($_repo.xml.gz $_repo-icons-{64x64,128x128}.tar.gz)
done
sha256sums=('4eada9278c7f65e43bf12059dea0ca0d1f7ce4a318d75991f571dc8efbefa0ce'
            '7989bb311baa38ef545250282aa065d23281c46dfb8faabe4c653487bdbded5c'
            '7989bb311baa38ef545250282aa065d23281c46dfb8faabe4c653487bdbded5c'
            '7966ca12d0505d19e773146d618f217116e8d8b07357897c800868233a48a4de'
            '4f615f30096f8076efe748bfb230109a48be7b4eb722f3d322df958085a3e643'
            'ae4b25414f4c32e33af5913f7e90116441ad85e3ed80b6d3ae8efc11e9eb1dc1'
            '6a8dbf41c8205e00fe783f1ded710ddc28cfe5797116066013434bcb3d9a430f'
            '77ad1068c2b4d503904c6a3c7ff73ff67ff59da67baab94572dfc1cc9c9129ea'
            'a09479c2b70e2304cb6925abe12a41317bb80a0160881641f6b3903b4ae06a31')

package() {
  mkdir -p "$pkgdir"/usr/share/app-info/{icons/archlinux-arch-{core,extra,community}/{64x64,128x128},xmls}
  for _repo in core extra community; do
   tar -xzf $_repo-icons-64x64.tar.gz -C "$pkgdir"/usr/share/app-info/icons/archlinux-arch-$_repo/64x64
   tar -xzf $_repo-icons-128x128.tar.gz -C "$pkgdir"/usr/share/app-info/icons/archlinux-arch-$_repo/128x128
  done
  cp *.xml.gz "$pkgdir"/usr/share/app-info/xmls/
}
