# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=enchant
pkgver=2.3.1
pkgrel=1
pkgdesc="A wrapper library for generic spell checking"
arch=('x86_64')
url="https://abiword.github.io/enchant/"
license=('LGPL')
depends=('glib2')
makedepends=('aspell' 'hunspell' 'hspell' 'nuspell' 'libvoikko')
checkdepends=('unittestpp')
optdepends=('aspell: for aspell based spell checking support'
            'hunspell: for hunspell based spell checking support'
            'libvoikko: for libvoikko based spell checking support'
            'hspell: for hspell based spell checking support'
            'nuspell: for nuspell based spell checking support')
provides=('libenchant-2.so') # enchant_voikko.so enchant_nuspell.so enchant_hunspell.so enchant_hspell.so enchant_aspell.so)
source=("https://github.com/AbiWord/enchant/releases/download/v$pkgver/enchant-$pkgver.tar.gz")
sha512sums=('c6fb8498d34a55040c2791124c2975952497e5a1b8999958691279b1a60a37473a1d2e3e11504f7cc877370be7772c2db621022040ea7035b21e40e8367606f5')

prepare() {
  cd $pkgname-$pkgver
  autoreconf -vfi
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
    --disable-static \
    --enable-relocatable
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
