# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>

pkgname=ctags
pkgver=5.8
pkgrel=5
pkgdesc="Generates an index file of language objects found in source files"
arch=('i686' 'x86_64')
license=('GPL')
depends=('glibc')
url="http://ctags.sourceforge.net/"
source=("http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        "CVE-2014-7204.patch")
md5sums=('c00f82ecdcc357434731913e5b48630d'
         '5fd1a8abb0e1e2d16fd4b8b870e03249')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}

  patch -Np1 <../CVE-2014-7204.patch
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  ./configure --prefix=/usr \
              --disable-external-sort
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}

  make prefix=${pkgdir}/usr install
}
