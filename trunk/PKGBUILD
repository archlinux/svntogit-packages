# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
#Contributor: Urs Wolfer <uwolfer @ fwo.ch>

pkgname=dmraid
pkgver=1.0.0.rc16.3
pkgrel=7
pkgdesc="Device mapper RAID interface"
url="http://people.redhat.com/~heinzm/sw/dmraid/"
conflicts=('mkinitcpio<0.7')
depends=('device-mapper>=2.0.54')
arch=('i686' 'x86_64')
license=('GPL')
source=(#ftp://ftp.archlinux.org/other/dmraid/$pkgname-$pkgver.tar.bz2
        http://people.redhat.com/~heinzm/sw/dmraid/src/$pkgname-1.0.0.rc16-3.tar.bz2
        dmraid_install
        dmraid_hook
        dmraid_tmpfiles
        dmraid.service)
install=dmraid.install
md5sums=('819338fcef98e8e25819f0516722beeb'
         '2297d23cee1aef23ec6ad8d6d1870356'
         'faec669dc85f87187b45b5d3968efe2c'
         '56a8bb0ece8d206cd8efb504ee072ddd'
         '1cd201496e52bd05c1c3048bf6dd809f')

build() {
  cd "$pkgname/1.0.0.rc16-3/$pkgname"
  ./configure --enable-led --enable-intel_led
  make
}

package() {
  cd "$pkgname/1.0.0.rc16-3/$pkgname"
  make DESTDIR="$pkgdir" prefix=/usr libdir=/usr/lib mandir=/usr/share/man includedir=/usr/include install
  install -D -m644 "$srcdir"/dmraid_install "$pkgdir"/usr/lib/initcpio/install/dmraid
  install -D -m644 "$srcdir"/dmraid_hook "$pkgdir"/usr/lib/initcpio/hooks/dmraid
  install -D -m644 "$srcdir"/dmraid_tmpfiles "$pkgdir"/usr/lib/tmpfiles.d/dmraid.conf

  # fix permissions
  chmod 644 "$pkgdir"/usr/include/dmraid/* "$pkgdir"/usr/lib/libdmraid.a

  install -Dm644 "$srcdir/dmraid.service" "$pkgdir/usr/lib/systemd/system/dmraid.service"
}
