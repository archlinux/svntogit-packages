# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgbase=libpeas
pkgname=(libpeas libpeas-docs)
pkgver=1.34.0
pkgrel=1
pkgdesc="A GObject plugins library"
url="https://wiki.gnome.org/Projects/Libpeas"
arch=(x86_64)
license=(LGPL)
depends=(gtk3 gobject-introspection-runtime)
makedepends=(python-gobject glade gobject-introspection git meson vala gi-docgen)
checkdepends=(xorg-server-xvfb)
optdepends=('python-gobject: Python loader')
_commit=744e16bdada0eb79c37abac328cb4c3dc6dd42df  # tags/libpeas-1.34.0^0
source=("git+https://gitlab.gnome.org/GNOME/libpeas.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd libpeas
  git describe --tags | sed 's/^libpeas-//;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd libpeas
}

build() {
  arch-meson libpeas build -D vapi=true -D gtk_doc=true
  meson compile -C build
}

check() {
  xvfb-run -s '-nolisten local' \
    meson test -C build --print-errorlogs
}

package_libpeas() {
  provides=(libpeas{,-gtk}-1.0.so)

  meson install -C build --destdir "$pkgdir"

  mkdir -p doc/usr/share
  mv {"$pkgdir",doc}/usr/share/doc
}

package_libpeas-docs() {
  pkgdesc+=" (documentation)"
  depends=()

  mv doc/* "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
