# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>

pkgname=prometheus
pkgver=2.13.1
pkgrel=1

pkgdesc='An open-source systems monitoring and alerting toolkit'
url='https://prometheus.io'
arch=('x86_64')
license=('Apache')

depends=('glibc')
makedepends=('go-pie' 'git' 'bzr')

backup=('etc/prometheus/prometheus.yml')

source=("https://github.com/prometheus/prometheus/archive/v$pkgver.tar.gz"
        prometheus.service
        prometheus.sysusers)

sha256sums=('5624c16728679362cfa46b76ec1d247018106989f2260d35583c42c49c5142b5'
            '116a5444d7da1f96446a9801f0c9df9c45e7d6af497c9fabf36f0dd26c31c025'
            '2747fabb4e56b808361eb7dd7acf9729ab8973d1ebe2f857dd56f6c71f71e45f')

build() {
  cd prometheus-$pkgver

  LDFLAGS="-extldflags $LDFLAGS \
    -X github.com/prometheus/common/version.Version=$pkgver \
    -X github.com/prometheus/common/version.Revision=$pkgver \
    -X github.com/prometheus/common/version.Branch=tarball \
    -X github.com/prometheus/common/version.BuildUser=someone@builder \
    -X github.com/prometheus/common/version.BuildDate=$(date -d@"$SOURCE_DATE_EPOCH" +%Y%m%d-%H:%M:%S)"

  go build \
    -gcflags "all=-trimpath=$PWD" \
    -asmflags "all=-trimpath=$PWD" \
    -ldflags "$LDFLAGS" \
    ./cmd/prometheus
  go build \
    -gcflags "all=-trimpath=$PWD" \
    -asmflags "all=-trimpath=$PWD" \
    -ldflags "$LDFLAGS" \
    ./cmd/promtool
}

check() {
  cd prometheus-$pkgver

  go test ./...
}

package() {
  install -Dm644 prometheus.service "$pkgdir"/usr/lib/systemd/system/prometheus.service
  install -Dm644 prometheus.sysusers "$pkgdir"/usr/lib/sysusers.d/prometheus.conf

  cd prometheus-$pkgver

  install -Dm755 -t "$pkgdir"/usr/bin prometheus promtool
  install -Dm640 -g210 -t "$pkgdir"/etc/prometheus documentation/examples/prometheus.yml
  install -dm755 -o210 -g210 "$pkgdir"/var/lib/prometheus

  # Web
  install -dm755 "$pkgdir"/usr/share/prometheus/
  cp -R web/ui "$pkgdir"/usr/share/prometheus/web

  # Examples
  install -Dm644 -t "$pkgdir"/usr/share/doc/prometheus/examples documentation/examples/prometheus*.yml
  cp -R consoles console_libraries "$pkgdir"/usr/share/doc/prometheus/examples

}
