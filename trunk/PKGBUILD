# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-color-manager
pkgver=3.26.0+3+g3620f9b
pkgrel=1
pkgdesc="GNOME Color Profile Tools"
arch=(x86_64)
url="https://git.gnome.org/browse/gnome-color-manager"
license=(GPL2)
depends=(libcanberra vte3 exiv2 shared-color-targets colord-gtk libexif)
makedepends=(itstool git appstream-glib meson)
optdepends=('argyllcms: color calibration')
groups=(gnome)
_commit=3620f9bc7e5f4f3cae856eaeac1046a41da69164  # master
source=("git+https://git.gnome.org/browse/gnome-color-manager#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^GNOME_COLOR_MANAGER_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd $pkgname
}
  

build() {
  arch-meson $pkgname build
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}
