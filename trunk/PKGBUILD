# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Charles-Henri d'Adhemar <cdadhemar@gmail.com>

pkgname=gegl
pkgver=0.1.2
pkgrel=1
pkgdesc="Graph based image processing framework"
arch=('i686' 'x86_64')
url="http://www.gegl.org/"
license=('GPL3' 'LGPL3')
depends=('babl' 'libpng' 'libjpeg' 'gtk2')
makedepends=('ruby' 'lua' 'openexr' 'ffmpeg' 'librsvg')
optdepends=('openexr: for using the openexr plugin' \
            'ffmpeg: for using the ffmpeg plugin' \
            'librsvg: for using the svg plugin')
options=('!libtool')
source=(ftp://ftp.gimp.org/pub/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2)
md5sums=('d35ffe17dc042652eca3205488ece262')
sha1sums=('34b4a6be9e472324d6dd1233713a1f8851fcdc04')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure  --prefix=/usr  --with-sdl --with-openexr --with-librsvg \
    --with-libavformat --disable-docs || return 1
  make  || return 1
  make DESTDIR="${pkgdir}" install || return 1
}
