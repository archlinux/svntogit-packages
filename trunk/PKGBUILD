# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Gour <Gour <gour@mail.inet.hr>

pkgname=libisoburn
pkgver=1.3.8 #.pl01
pkgrel=1
pkgdesc="frontend for libraries libburn and libisofs"
url="http://libburnia-project.org"
arch=('i686' 'x86_64')
license=('GPL2')
depends=('libburn>=1.3.8' 'libisofs>=1.3.8' 'readline')
optdepends=('tk:      for xorriso-tcltk frontend'
            'bwidget: for xorriso-tcltk frontend')
provides=('xorriso' 'xorriso-tcltk')
install=${pkgname}.install
source=(http://files.libburnia-project.org/releases/${pkgname}-${pkgver}.tar.gz{,.sig})
md5sums=('4f1c2290a2d02bdb709c3707fd9f1ae6'
         'SKIP')

build() {
  cd ${pkgname}-${pkgver/.pl01/}
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver/.pl01/}
  make DESTDIR=${pkgdir} install
}
