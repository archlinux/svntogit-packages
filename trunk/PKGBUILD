# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-control-center
pkgver=2.24.0.1
pkgrel=3
pkgdesc="The Control Center for GNOME"
arch=(i686 x86_64)
depends=('metacity>=2.24.0' 'eel>=2.24.0' 'gstreamer0.10-base>=0.10.20' 'libxss' 'gnome-panel>=2.24.0' 'desktop-file-utils' 'gnome-settings-daemon>=2.24.0' 'libgnomekbd>=2.24.0' 'libcanberra>=0.10' 'sound-theme-freedesktop')
makedepends=('perlxml' 'gnome-doc-utils>=0.14.0' 'pkgconfig' 'intltool')
url="http://www.gnome.org"
groups=('gnome')
replaces=('control-center')
conflicts=('control-center')
provides=('control-center')
install=gnome-control-center.install
license=('GPL')
options=('!libtool' '!emptydirs')
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/2.24/${pkgname}-${pkgver}.tar.bz2
	ta-schema.patch)
md5sums=('dcaf6e91fddbb1d57d045a4de34ec25c' 'b8438f6eb8825a440b49fc81a6621705')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/ta-schema.patch" || return 1
  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var --disable-static \
	      --enable-gstreamer=0.10 --enable-aboutme \
	      --disable-scrollkeeper --disable-update-mimedb || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
