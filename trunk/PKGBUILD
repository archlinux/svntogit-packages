# Contributor: Kaiting Chen <kaiting.chen@kiwilight.com>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname='cronie'
pkgver=1.4.7
pkgrel=7
pkgdesc='Daemon that runs specified programs at scheduled times and related tools'
url='https://fedorahosted.org/cronie/'
license=('custom:BSD')
arch=('i686' 'x86_64')
depends=('pam' 'bash' 'run-parts')

source=("https://fedorahosted.org/releases/c/r/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        'cron.deny'
        'crontab'
        'pam.d'
        'rc.d')
sha1sums=('c6644ba0e58bcb14e0bb3f925e3e8cc3f0d47a7f'
          '0f279b8fb820340267d578dc85511c980715f91e'
          '4059bc4ccb75f08b0d4970940799e5d9722b339f'
          '6d8aef6880935b3dcc3e28481111d036544eeae5'
          'c08c040ed5cb12bc4fd15639a5242d31ec247ef5')

backup=('etc/crontab'
        'etc/anacrontab'
        'etc/conf.d/crond'
        'etc/pam.d/crond'
        'etc/cron.deny')

conflicts=('cron')
provides=('cron')
groups=('base')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--enable-anacron \
		--with-inotify \
		--with-pam \

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make DESTDIR="${pkgdir}" install

	install -d "${pkgdir}"/etc/cron.{d,hourly,daily,weekly,monthly}
	install -d "${pkgdir}"/var/spool/{ana,}cron
	chmod u+s "${pkgdir}"/usr/bin/crontab

	install -Dm755 ../rc.d "${pkgdir}"/etc/rc.d/crond
	install -Dm644 ../pam.d "${pkgdir}"/etc/pam.d/crond
	install -Dm644 ../crontab "${pkgdir}"/etc/crontab
	install -Dm644 ../cron.deny "${pkgdir}"/etc/cron.deny
	install -Dm644 crond.sysconfig "${pkgdir}"/etc/conf.d/crond
	install -Dm644 contrib/0hourly "${pkgdir}"/etc/cron.d/0hourly
	install -Dm755 contrib/0anacron "${pkgdir}"/etc/cron.hourly/0anacron
	install -Dm644 contrib/anacrontab "${pkgdir}"/etc/anacrontab

	install -Dm644 COPYING "${pkgdir}"/usr/share/licenses/cronie/COPYING
}
