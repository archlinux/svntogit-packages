# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-sudoku
pkgver=40.2
pkgrel=1
pkgdesc="Test your logic skills in this number grid puzzle"
url="https://wiki.gnome.org/Apps/Sudoku"
arch=(x86_64)
license=(GPL)
depends=(gtk3 qqwing libgee json-glib)
makedepends=(vala yelp-tools git meson)
groups=(gnome-extra)
_commit=614d6d8e3eb28fcb6ef82e6f197a1c97a1cb8bc7  # tags/40.2^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-sudoku.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
