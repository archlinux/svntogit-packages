# $Id$
# Maintainer:
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>

pkgbase=qscintilla
pkgname=('qscintilla' 'python-qscintilla' 'python2-qscintilla')
pkgver=2.6
pkgrel=1
license=('GPL')
arch=('i686' 'x86_64')
url="http://www.riverbankcomputing.co.uk/software/qscintilla/intro"
makedepends=('python2-pyqt' 'chrpath')
source=("http://riverbankcomputing.com/static/Downloads/QScintilla2/QScintilla-gpl-${pkgver}.tar.gz"
        'configure.py-objdir-support.diff')
md5sums=('0605a8006ea752ec2d1d7fc4791d1c75'
         '8cf9c06252e2f11ab00e62848e322fd3')


build() {
    cd "${srcdir}/QScintilla-gpl-${pkgver}" 
    patch -Np1 -i "${srcdir}/configure.py-objdir-support.diff"

    cd Qt4
    qmake qscintilla.pro
    make
    
    cd ../designer-Qt4
    qmake designer.pro INCLUDEPATH+=../Qt4 QMAKE_LIBDIR+=../Qt4
    make

    cd ../
    cp -rf Python Python2
    cd Python
    python configure.py -n ../Qt4/ -o ../Qt4 -c -p 4
    make

    cd ../Python2
    python2 configure.py -n ../Qt4/ -o ../Qt4 -c -p 4
    make    
}

package_qscintilla() {
    pkgdesc="A port to Qt4 of Neil Hodgson's Scintilla C++ editor class"
    depends=('qt')

    cd "${srcdir}/QScintilla-gpl-${pkgver}/Qt4"
    make DESTDIR="${pkgdir}" INSTALL_ROOT="${pkgdir}" install

    cd "${srcdir}/QScintilla-gpl-${pkgver}/designer-Qt4"
    make DESTDIR="${pkgdir}" INSTALL_ROOT="${pkgdir}" install
}

package_python-qscintilla() {
    pkgdesc="Python 3 bindings for QScintilla2"
    depends=('pyqt' 'qscintilla')

    cd "${srcdir}/QScintilla-gpl-${pkgver}/Python"
    install -Dm755 Qsci.so \
      "${pkgdir}/usr/lib/python3.2/site-packages/PyQt4/Qsci.so"

    # Fix RPATH
    chrpath -r /usr/lib/ "${pkgdir}"/usr/lib/python3.2/site-packages/PyQt4/Qsci.so
}

package_python2-qscintilla() {
    pkgdesc="Python 2 bindings for QScintilla2"
    depends=('python-qscintilla' 'python2-pyqt')

    cd "${srcdir}/QScintilla-gpl-${pkgver}/Python2"
    make DESTDIR="${pkgdir}" INSTALL_ROOT="${pkgdir}" install

    # Fix RPATH
    chrpath -r /usr/lib/ "${pkgdir}"/usr/lib/python2.7/site-packages/PyQt4/Qsci.so
}
