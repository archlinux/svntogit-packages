# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Simon Perry <aur [at] sanxion [dot] net>
# Contributor: Jonas Nyr√©n <jonas.nyren*mindkiller.com>

pkgname=libsidplayfp
pkgver=1.8.2
pkgrel=1
pkgdesc='Library to play Commodore 64 music derived from libsidplay2'
url='http://sourceforge.net/projects/sidplay-residfp/'
arch=('i686' 'x86_64')
license=('GPL')
depends=('gcc-libs')
makedepends=('vice' 'xa')
optdepends=('vice: better SID support')
source=("http://downloads.sourceforge.net/project/sidplay-residfp/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.gz")
sha1sums=('41eb3b3f3f5689f5e42b244d9782163e12498fd3')

build() {
	cd "${pkgname}-${pkgver}"
	./configure --prefix=/usr
	make
}

package() {
	cd "${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	install -d "${pkgdir}/usr/share/sidplayfp"
	ln -t "${pkgdir}/usr/share/sidplayfp" -s /usr/lib/vice/C64/{kernal,basic,chargen}
}
