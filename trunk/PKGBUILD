# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=eglexternalplatform
pkgver=1.1
pkgrel=2
pkgdesc="EGL External Platform interface"
url=https://github.com/NVIDIA/eglexternalplatform
arch=(any)
license=(MIT)
depends=(libegl)
makedepends=(git)
_commit=7c8f8e2218e46b1a4aa9538520919747f1184d86  # tags/1.1
source=("git+$url#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

package() {
  cd $pkgname
  install -Dt "$pkgdir/usr/include/EGL" -m644 interface/*
  install -Dt "$pkgdir/usr/share/pkgconfig" -m644 *.pc
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 COPYING
}

# vim:set sw=2 et:
