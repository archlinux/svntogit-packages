# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-alien-build
pkgver=2.66
pkgrel=1
pkgdesc="Build external dependencies for use in CPAN"
arch=('any')
license=('PerlArtistic')
url="https://metacpan.org/release/Alien-Build"
depends=('perl-capture-tiny' 'perl-ffi-checklib' 'perl-file-chdir' 'perl-file-which'
         'perl-path-tiny')
makedepends=('perl-test2-suite')
checkdepends=('perl-alien-base-modulebuild' 'perl-alien-cmake3' 'perl-env-shellwords'
              'perl-pkgconfig' 'perl-pkgconfig-libpkgconf' 'perl-readonly' 'perl-sort-versions')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/P/PL/PLICEASE/Alien-Build-$pkgver.tar.gz")
sha512sums=('8893eb6a9a2b9744402dc6abb723cae93f07afe15e0da9f23e508b83bea552dd2d0c8c29d07ba6a5a98ed6c6bae7e952a5a935c6b4ba0ed9155bb6b89c4817ea')

build() {
  cd Alien-Build-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Alien-Build-$pkgver
  make test
}

package() {
  cd Alien-Build-$pkgver
  make DESTDIR="$pkgdir" install
}
