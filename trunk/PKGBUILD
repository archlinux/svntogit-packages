# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-xclock
pkgver=1.0.8
pkgrel=1
pkgdesc="X clock"
arch=(x86_64)
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libx11' 'libxmu' 'libxaw' 'libxrender' 'libxft' 'libxkbfile')
makedepends=('xorg-util-macros')
source=(https://xorg.freedesktop.org/archive/individual/app/xclock-${pkgver}.tar.bz2{,.sig})
sha512sums=('1272221ace3657505bae1b22f181e9ecbf1710900c89a860c8ceae47ae31ca5e75b6557c37dc0a881a0a24c9597b9a07f46200926feb1013f1e2e995111a7646'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # "Alan Coopersmith <alan.coopersmith@oracle.com>"

build() {
  cd "xclock-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "xclock-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
