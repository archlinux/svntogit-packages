# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=solid
pkgver=r5.6038b79
pkgrel=1
pkgdesc='Solid'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/solid'
license=('LGPL')
depends=('qt5-declarative' 'media-player-info')
makedepends=('extra-cmake-modules' 'git')
groups=('kf5')
source=('git://anongit.kde.org/solid.git')
md5sums=('SKIP')

pkgver() {
  cd solid
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../solid \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF \
    -DHUPNP_ENABLED=FALSE
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
