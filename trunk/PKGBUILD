# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: György Balló <ballogy@freestart.hu>

pkgname=geoclue
pkgver=0.12.0
pkgrel=5
pkgdesc="Modular geoinformation service built on the D-Bus messaging system."
arch=('i686' 'x86_64')
url="http://geoclue.freedesktop.org/"
license=('LGPL')
depends=('dbus-glib' 'libxml2')
optdepends=('networkmanager: geolocation'
            'gconf: geolocation'
            'libsoup-gnome: Skyhook location provider')
makedepends=('gconf' 'networkmanager' 'libsoup-gnome' 'gtk-doc')
options=('!libtool')
source=(https://launchpad.net/geoclue/trunk/${pkgver%.*}/+download/$pkgname-$pkgver.tar.gz
        geoclue-gcc46.patch)
md5sums=('33af8307f332e0065af056ecba65fec2'
         '0cf8ecc350be702f623ed9a9715818c5')

build() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../geoclue-gcc46.patch

  sed -i 's/libnm_glib/libnm-glib/g' configure.ac
  sed -i '/libsoup-gnome/s/$/ gthread-2.0/' configure.ac
  autoreconf -f -i

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
