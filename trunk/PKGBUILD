# Maintainer: AndyRTR <andyrtr@archlinux.org>

pkgbase=ghostscript
pkgname=(ghostscript ghostxps ghostpcl)
pkgver=9.56.0
pkgrel=1
pkgdesc="An interpreter for the PostScript language"
url="https://www.ghostscript.com/"
arch=('x86_64')
license=('AGPL3' 'custom')
depends=('libxt' 'libcups' 'fontconfig' 'zlib' 'libpng' 'libjpeg' 'jbig2dec'
         'libtiff' 'lcms2' 'dbus' 'libpaper' 'ijs' 'openjpeg2' 'libidn')
makedepends=('gtk3' 'gnutls' 'glu' 'freeglut')
options=('debug')
# https://github.com/ArtifexSoftware/ghostpdl-downloads/releases
source=(https://github.com/ArtifexSoftware/ghostpdl-downloads/releases/download/gs${pkgver//./}/ghostpdl-${pkgver}.tar.xz
        #https://github.com/ArtifexSoftware/ghostpdl-downloads/releases/download/ghostpdl-${pkgver/.0//}/ghostpdl-${pkgver}.tar.xz
        2010_add_build_timestamp_setting.patch
        https://github.com/ArtifexSoftware/ghostpdl/commit/9aae213ff3ae971942b427062799b009c07ea713.patch)
sha512sums=('213aaa844533fe28ffde0008ff6e8ae10096efddf480c236407668a6445ef2c146557af8da05d9664f44ff53bd31809e89178dfe464763365a1af9de49edf507'
            'cd7794ee4f28b11177021b950196385200b610127ed6cb94a45e3202b690b721a0dfcc0276ff39448d4dab64c1b31a76e6c323696a8315aad9edc22077f18a3d'
            '530a03f3d333a6c45cc024b5ebd57138b4f7fbff8069e8a81f3015aed9d553b1294e529f57e4123d20442e896dde780c120f438e61c1e3a6436d90c03580e982')

### update jbig2dec first! ###

### make sure to rebuild core/groff on version updates - https://bugs.archlinux.org/task/67751 ###

prepare() {
  cd ghostpdl-${pkgver}

  # *** remove after final decision ***
  # new in 9.54.0: 
  # https://www.ghostscript.com/doc/9.54.0/News.htm

  # 1) inclusion of the tesseract/leptonica sources for OCR capabilities
  # at the moment we do not support linking with tesseract/leptonica shared libraries. 
  # As is normal with such included libraries, deleting those directories and (re)running
  # configure (on Unix like systems) will automatically build without the OCR functionality.
  # increases package size ghostpcl 2.7->4.9MB | ghostscript 18->23MB | ghostxps 2,7->4.9MB
  # https://www.ghostscript.com/doc/9.54.0/VectorDevices.htm#UseOCR - 
  # this doesn't seem to be worth to keep enabled until linking with shared libs is supported
  rm -r tesseract leptonica

  # 2) new directory addition in the source tree: "extract/". 
  # It contains the implementation for the writing of docx format files used by the
  # new "docxwrite" device. This is *not* a "thirdparty library". 
  # For distribution package maintainers, if you want your packaged Ghostscript to include
  # the "docxwrite" device, do not delete this directory.
  # ^ this one doesn't affect package size - so let's keep it

  # force it to use system-libs
  rm -r cups/libs expat ijs jbig2dec jpeg lcms2mt libpng openjpeg tiff zlib
  # using tree freetype because of https://bugs.archlinux.org/task/56849
  # lcms2mt is the new lcms2 fork aimed to replace lcms2 in a thread safe way
  
  # http://git.ghostscript.com/?p=ghostpdl.git;a=commit;h=40dc5b409c6262b18b4bf5386b5482ead4c511e3
  # libs link unwanted to libgpdl that isn't installed
  rm -rf gpdl

  # https://bugs.ghostscript.com/show_bug.cgi?id=704405
  sed -i "s/gscms_transformm_color_const/gscms_transform_color_const/" base/gsicc_lcms2.c

  # Debian: # allow the build timestamp to be externally set
  patch -Np1 -i ../2010_add_build_timestamp_setting.patch

  # Revert patch breaking build of GTK gsx
  patch -Rp1 -i ../9aae213ff3ae971942b427062799b009c07ea713.patch
}

build() {
  cd ghostpdl-${pkgver}
  ./configure --prefix=/usr \
              --enable-dynamic \
              --with-ijs \
              --with-jbig2dec \
              --with-x \
              --with-drivers=ALL \
              --with-fontpath=/usr/share/fonts/gsfonts \
              --enable-fontconfig \
              --enable-freetype \
              --enable-openjpeg \
              --with-system-libtiff \
              --with-libpaper \
              --disable-compile-inits #--help # needed for linking with system-zlib
  make
}

package_ghostscript() {
  optdepends=('texlive-core:      needed for dvipdf'
              'gtk3:              needed for gsx')

  cd ghostpdl-${pkgver}
  make DESTDIR="${pkgdir}" install-gs install-so-gs

  # replace statically linked gs binary with symlink to dynamically linked gsc
  rm -v "${pkgdir}"/usr/bin/gs
  ln -s gsc "${pkgdir}"/usr/bin/gs

  # remove unwanted localized manpages
  rm -rv "${pkgdir}"/usr/share/man/de

  install -Dt "${pkgdir}"/usr/share/licenses/${pkgname} -m644 LICENSE
}

package_ghostxps() {
  pkgdesc="${pkgdesc/PostScript/XPS document}"
  depends=("ghostscript=${pkgver}-${pkgrel}")

  cd ghostpdl-${pkgver}
  make DESTDIR="${pkgdir}" install-gxps install-so-gxps
  rm -r "${pkgdir}"/usr/include

  # fix file conflict - FS#70238
  rm -r "${pkgdir}"/usr/lib/libgxps.so
  
  install -Dt "${pkgdir}"/usr/share/licenses/${pkgname} -m644 LICENSE
}

package_ghostpcl() {
  pkgdesc="${pkgdesc/PostScript/PCL 6}"
  depends=("ghostscript=${pkgver}-${pkgrel}")

  cd ghostpdl-${pkgver}
  make DESTDIR="${pkgdir}" install-gpcl6 install-so-gpcl6
  rm -r "${pkgdir}"/usr/include

  install -Dt "${pkgdir}"/usr/share/licenses/${pkgname} -m644 LICENSE
}
