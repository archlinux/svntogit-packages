# $Id$
# Maintainer:
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=('python2-geoip')
pkgbase=python-geoip
pkgver=1.2.8
pkgrel=1
pkgdesc="Python bindings for the GeoIP IP-to-country resolver library"
arch=('i686' 'x86_64')
url="http://dev.maxmind.com/geoip/legacy/downloadable#Python-10"
license=('LGPL')
depends=('python2' 'geoip')
source=(http://www.maxmind.com/download/geoip/api/python/GeoIP-Python-$pkgver.tar.gz)
sha256sums=('8b946307355b60cb0f2b0be8ac90c1231286e0e79917509763267fce01a50e73')

build() {
  cd "$srcdir/GeoIP-Python-$pkgver"
  python2 setup.py build
}

package_python2-geoip() {
  cd "$srcdir/GeoIP-Python-$pkgver"
  python2 setup.py install --root="$pkgdir" -O1
}

# vim:set ts=2 sw=2 et:
