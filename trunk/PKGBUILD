# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=vi
pkgver=050325
pkgrel=4
epoch=1
pkgdesc="The original ex/vi text editor"
arch=('i686' 'x86_64')
url="http://ex-vi.sourceforge.net/"
license=('custom:ex')
depends=('ncurses')
optdepends=('heirloom-mailx: used by the preserve command for notification')
groups=('base')
source=(http://downloads.sourceforge.net/sourceforge/ex-vi/ex-${pkgver}.tar.bz2
        navkeys.patch
        increase-tube.patch
        preserve_dir.patch)
sha1sums=('573501d15fa4be59f136641957c7f893e86bac82'
          '9ff3e79dd5f4ba29bd439f2c86afbc748c95ec3d'
          '7039755bf1dd27b4aa5384e76ee52e31d06f9972'
          'ff399edd106eb0b61cc58f66812c2a2c8ea7670d')

prepare() {
  cd ex-${pkgver}
  patch -Np1 -i  ../navkeys.patch
  patch -Np0 -i ../increase-tube.patch
  # FS#20653
  patch -Np1 -i ../preserve_dir.patch
}

build() {
  cd ex-${pkgver}
  make PREFIX=/usr LIBEXECDIR=/usr/lib/ex PRESERVEDIR=/var/lib/ex \
      'FEATURES=-DCHDIR -DFASTTAG -DUCVISUAL -DMB -DBIT8' TERMLIB=ncurses
}

package() {
  cd ex-${pkgver}
  make PREFIX=/usr LIBEXECDIR=/usr/lib/ex PRESERVEDIR=/var/lib/ex INSTALL=/usr/bin/install \
      DESTDIR="${pkgdir}" install
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
