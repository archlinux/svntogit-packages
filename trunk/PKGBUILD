# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=imake
pkgver=1.0.3
pkgrel=1
pkgdesc="X.Org imake program and related utilities"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=(glibc)
makedepends=(pkgconfig xproto perl)
optdepends=('perl:	for mkhtmlindex')
source=(http://xorg.freedesktop.org/releases/individual/util/imake-1.0.1.tar.bz2
	http://xorg.freedesktop.org/releases/individual/util/gccmakedep-1.0.2.tar.bz2
	http://xorg.freedesktop.org/releases/individual/util/lndir-1.0.1.tar.bz2
	http://xorg.freedesktop.org/releases/individual/util/makedepend-1.0.2.tar.bz2
	http://xorg.freedesktop.org/releases/individual/util/xorg-cf-files-1.0.3.tar.bz2
	linuxconfig.patch
	xorg-cf-files-1.0.2-xprint.patch
	LICENSE)
md5sums=('8c859fdddd57e91132ceab1344bfe728'
         'b533c0771dbbaf9b041ff35bb941d3a2'
         'e274ea9f55dfd62afa0a7b1e1ab4ba96'
         '62e58330fe8d1e3e28c7a45779833a48'
         '10546b5ddda3cdda7950bb56bf98e0ea'
         'b3385d8efbbe7c7fc50ed3a96ce37a16'
         '82356cfc8a761c85b0b37736fc91f604'
         'f5767133ef7a98a0bfa15139e0e3d32e')

build() {
  cd ${srcdir}/xorg-cf-files-1.0.3*
  patch -Np1 -i "${srcdir}/linuxconfig.patch" || return 1
  patch -Np1 -i "${srcdir}/xorg-cf-files-1.0.2-xprint.patch" || return 1
  cd "${srcdir}"
  for i in *; do
    if [ -d "${i}" ]; then
      pushd "${i}"
      ./configure --prefix=/usr --mandir=/usr/share/man || return 1
      make || return 1
      make DESTDIR="${pkgdir}" install || return 1
      popd
    fi
  done
  install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE" || return 1
}
