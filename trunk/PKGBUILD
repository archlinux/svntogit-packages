# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>

pkgname=db
pkgver=4.7.25.4
_basever=4.7.25
pkgrel=1
pkgdesc="The Berkeley DB embedded database system"
arch=('i686' 'x86_64')
url="http://www.oracle.com/technology/software/products/berkeley-db/index.html"
license=('custom')
groups=('base')
depends=('gcc-libs' 'sh')
options=('!libtool')
install=db.install
source=(http://download-uk.oracle.com/berkeley-db/db-${_basever}.tar.gz
	#post release upstream patches
	http://www.oracle.com/technology/products/berkeley-db/db/update/4.7.25/patch.4.7.25.1
	http://www.oracle.com/technology/products/berkeley-db/db/update/4.7.25/patch.4.7.25.2
	http://www.oracle.com/technology/products/berkeley-db/db/update/4.7.25/patch.4.7.25.3
	http://www.oracle.com/technology/products/berkeley-db/db/update/4.7.25/patch.4.7.25.4)
md5sums=('ec2b87e833779681a0c3a814aa71359e'
         '5fdf101259e5164dea1c8c86214fde38'
         'bd410a11c71fee52fddb6aa2d8d4f80c'
         '6fcd69f64f5b34bfe8f0a63cc2e402c1'
         '26f2a29b3f5c5c56b39c6c414fef98e5')

build() {
  cd ${srcdir}/$pkgname-${_basever}
  patch -Np0 -i ${srcdir}/patch.4.7.25.1 || return 1
  patch -Np0 -i ${srcdir}/patch.4.7.25.2 || return 1
  patch -Np0 -i ${srcdir}/patch.4.7.25.3 || return 1
  patch -Np1 -i ${srcdir}/patch.4.7.25.4 || return 1
  cd ${srcdir}/$pkgname-${_basever}/build_unix
  ../dist/configure --prefix=/usr --enable-compat185 \
    --enable-shared --enable-static --enable-cxx
  make LIBSO_LIBS=-lpthread || return 1
  make DESTDIR=${pkgdir} install
  rm -rf ${pkgdir}/usr/docs
  install -Dm644 ${srcdir}/${pkgname}-${_basever}/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
