# Contributor: judd <jvinet@zeroflux.org>

pkgname=alsa-lib
pkgver=1.1.7
pkgrel=2
pkgdesc="An alternative implementation of Linux sound support"
arch=('x86_64')
url="http://www.alsa-project.org"
depends=('glibc')
optdepends=('python2: for python smixer plugin')
makedepends=('python2')
license=('LGPL')
source=(ftp://ftp.alsa-project.org/pub/lib/$pkgname-$pkgver.tar.bz2
        fix_streams.patch::'http://git.alsa-project.org/?p=alsa-lib.git;a=patch;h=b420056604f06117c967b65d43d01536c5ffcbc9')
sha1sums=('eca62790517220297ec82ba8a5258bcb6e708866'
          '92da3c05177c5ac8ab0892f56a4360b60c444939')

prepare() {
  cd $srcdir/$pkgname-$pkgver
  patch -p1 < ../fix_streams.patch  # FS60591
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --without-debug --prefix=/usr --with-pythonlibs="-lpthread -lm -ldl -lpython2.7" --with-pythonincludes=-I/usr/include/python2.7
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
