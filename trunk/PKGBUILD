# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgbase=gtkmm-4.0
pkgname=(gtkmm-4.0 gtkmm-4.0-docs)
pkgver=4.8.0
pkgrel=1
pkgdesc="C++ bindings for GTK 4"
url="https://www.gtkmm.org/"
arch=(x86_64)
license=(LGPL)
depends=(gtk4 pangomm-2.48)
makedepends=(git meson mm-common glibmm-2.68-docs cairomm-1.16-docs
             pangomm-2.48-docs)
checkdepends=(xorg-server-xvfb)
_commit=17f869d1b0b3e820884f5fe079488bfc3d6c62c7  # tags/4.8.0^0
source=("git+https://gitlab.gnome.org/GNOME/gtkmm.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd gtkmm
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd gtkmm
}

build() {
  arch-meson gtkmm build -D maintainer-mode=true
  meson compile -C build
}

check() {
  xvfb-run -s '-nolisten local' \
    meson test -C build --print-errorlogs
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

package_gtkmm-4.0() {
  depends+=(libsigc-3.0.so libglibmm-2.68.so libcairomm-1.16.so
            libpangomm-2.48.so)
  provides=(libgtkmm-4.0.so)

  meson install -C build --destdir "$pkgdir"

  cd "$pkgdir"
  _pick docs usr/share/{devhelp,doc}
}

package_gtkmm-4.0-docs() {
  pkgdesc+=" (documentation)"
  depends=()
  options=(!strip)

  mv docs/* "$pkgdir"
}

# vim:set sw=2 et:
