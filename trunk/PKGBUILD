# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Ju Liu <liuju86 at gmail dot com>
# Contributor: Bjorn Lindeijer <bjorn lindeijer nl>
# Contributor: Andreas Zwinkau <beza1e1@web.de>

pkgname=telepathy-mission-control
pkgver=5.16.4
pkgrel=3
pkgdesc="A Telepathy component providing abstraction of some of the details of connection managers."
arch=(x86_64)
url="http://telepathy.freedesktop.org/wiki/Mission Control"
license=(LGPL2.1)
depends=(telepathy-glib dconf libnm)
makedepends=(libxslt python2 gtk-doc)
source=(https://telepathy.freedesktop.org/releases/$pkgname/$pkgname-$pkgver.tar.gz{,.asc})
sha256sums=('9769ddac7ad8aad21f6db854016792162b57e6fa0b0aed8d823d76a71fe7e6cb'
            'SKIP')
validpgpkeys=(38FDAD7D73CE1AE7A16200291F3845DD1432A9FB) # Georgios Kiagiadakis <gkiagia@tolabaki.gr>

build() {
    cd $pkgname-$pkgver
    PYTHON=python2 ./configure --prefix=/usr --libexecdir=/usr/lib/telepathy \
        --with-connectivity=nm \
        --enable-gtk-doc \
        --disable-upower \
        --disable-static \
        --disable-schemas-compile
    make
}

package() {
    cd $pkgname-$pkgver
    make DESTDIR="$pkgdir" install
}
