# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=grilo-plugins
pkgver=0.2.9
pkgrel=3
pkgdesc="Plugins for Grilo"
url="http://www.gnome.org"
arch=(i686 x86_64)
license=(LGPL)
depends=(grilo sqlite libgcrypt)
makedepends=(gupnp-av libgdata gmime rest libtracker-sparql totem-plparser libdmapsharing
             json-glib intltool)
optdepends=('gupnp-av: uPnP plugin'
            'libgdata: Youtube plugin'
            'gmime: Podcasts plugin'
            'rest: Blip.tv plugin'
            'libtracker-sparql: Tracker plugin'
            'totem-plparser: Youtube plugin, Vimeo plugin, Optical media plugin'
            'libdmapsharing: DMAP sharing plugin'
            'json-glib: TMDb plugin')
options=('!emptydirs')
groups=('gnome')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('902b839bd1cdc27613d9e44af2afe00d1ef3b7471ae72c66c622560007e1023e')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static \
    --enable-shoutcast
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
