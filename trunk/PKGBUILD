# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=xcb-util-wm
pkgver=0.4.0
pkgrel=1
pkgdesc="Utility libraries for XC Binding - client and window-manager helpers for ICCCM"
arch=('i686' 'x86_64')
url="http://xcb.freedesktop.org"
license=('custom')
depends=('libxcb>=1.7')
makedepends=('xorg-util-macros')
source=("http://xcb.freedesktop.org/dist/${pkgname}-${pkgver}.tar.bz2"
        'LICENSE')
sha256sums=('66f97c18f7d98d3c873fe5db5842d2fca34519274c31ee668551ab8995a2f51f'
            'ded299aa179dcf0d885bf89274a4db77a530e03f9f5e7cf1c3c4ef1d60e914b9')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --disable-static
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() { 
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -D -m644 "${srcdir}/LICENSE" \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
