# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=libappimage
pkgver=1.0.2
pkgrel=3
pkgdesc="Reference implementation of the AppImage specification"
arch=(x86_64)
url="https://appimage.org/"
license=(GPL)
depends=(libarchive squashfuse boost-libs librsvg)
makedepends=(cmake wget vim desktop-file-utils boost xdg-utils-cxx)
source=($pkgname-$pkgver.tar.gz::"https://github.com/AppImage/libappimage/archive/v$pkgver.tar.gz")
sha256sums=('250230db5b1cbace039ec0d70ea020a0f37c4fe867656d88d330017117da37eb')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DUSE_SYSTEM_XZ=ON \
    -DUSE_SYSTEM_SQUASHFUSE=ON \
    -DUSE_SYSTEM_LIBARCHIVE=ON \
    -DUSE_SYSTEM_BOOST=ON \
    -DUSE_SYSTEM_XDGUTILS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
