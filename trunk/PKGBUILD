# Maintainer: Antonio Rojas <arojas@archlinux,org>

pkgname=krita
_pkgver=5.1.4
pkgver=${_pkgver/-/}
pkgrel=4
pkgdesc='Edit and paint images'
arch=(x86_64)
url='https://krita.org'
license=(GPL3)
depends=(kitemviews kitemmodels ki18n kcompletion kguiaddons kcrash qt5-svg qt5-multimedia quazip
         gsl libraw exiv2 openexr fftw openjpeg2 opencolorio libwebp hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools boost eigen poppler-qt5 python-pyqt5 libheif
             qt5-tools sip kseexpr libmypaint libjxl xsimd)
optdepends=('poppler-qt5: PDF filter' 'ffmpeg: to save animations'
            'python-pyqt5: for the Python plugins' 'libheif: HEIF filter'
            'kseexpr: SeExpr generator layer' 'kimageformats: PSD support' 'libmypaint: support for MyPaint brushes'
            'krita-plugin-gmic: GMic plugin' 'libjxl: JPEG-XL filter')
source=(https://download.kde.org/stable/krita/$_pkgver/$pkgname-$_pkgver.tar.gz{,.sig}
        find-xsimd.patch
        krita-libraw-0.21.patch
        https://invent.kde.org/graphics/krita/-/commit/b388330b.patch)
sha256sums=('c22b020862596eb2fdd911e1b179dbbce7b081be111457a2eacaf6ae1cca2dcb'
            'SKIP'
            '622c9ffbbfef72dd024b391b879c79ae34da11d52b2f3488dfd8ee42bf7b453b'
            '583559875b48d099f120703663e229bd7df8d55b2de45b4d3ce1ca36d3572baa'
            '01faa00abf5b1291bc6b98b6bd49d277c3f3986824915fe3bd5ea13abd62abbe')
validpgpkeys=('05D00A8B73A686789E0A156858B9596C722EA3BD'  # Boudewijn Rempt <foundation@krita.org>
              'E9FB29E74ADEACC5E3035B8AB69EB4CF7468332F'  # Dmitry Kazakov (main key) <dimula73@gmail.com>
              '064182440C674D9F8D0F6F8B4DA79EDA231C852B') # Stichting Krita Foundation <foundation@krita.org>
options=(debug)

prepare() {
  patch -d $pkgname-$_pkgver -p1 < find-xsimd.patch
  patch -d $pkgname-$_pkgver -p1 < krita-libraw-0.21.patch # Fix build with libraw 0.21
  patch -d $pkgname-$_pkgver -p1 < b388330b.patch # Fix artifacts with xsimd 10
}

build() {
  cmake -B build -S $pkgname-$_pkgver \
    -DBUILD_KRITA_QT_DESIGNER_PLUGINS=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
