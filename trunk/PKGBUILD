# Maintainer: RÃ©my Oudompheng <remy@archlinux.org>

pkgname=texlive-science
pkgver=2023.66461
_revnr=${pkgver#2023.}
pkgrel=1
pkgdesc="TeX Live - Typesetting for mathematics, natural and computer sciences"
license=('GPL')
arch=(any)
depends=('texlive-core')
optdepends=(
  'texlive-latexextra: required for algorithm2e package'
)
groups=('texlive-most')
url='http://tug.org/texlive/'
source=("https://sources.archlinux.org/other/texlive/$pkgname-$pkgver-src.zip"
        "$pkgname.maps"
        "$pkgname.fmts")
options=('!emptydirs')
sha256sums=('9c5e36cdc932007bb151bd61bacdc216d1d1d682758555a4d6e0e85c51005098'
            '1d9151c9cabc3521b4406731b0147d80e0bf600bdedaf55a1a49b8775165daa8'
            '1cbd5339f8cfa32214a749a408cf7136fd5acffaffcde6500a337d332c70c5c3')

build() {
   cd "$srcdir"
   for p in *.tar.xz; do
	   bsdtar -xf $p
   done
   rm -rf {tlpkg,doc,source} || true
}

package() {
   cd "$srcdir"
   install -m755 -d "$pkgdir"/var/lib/texmf/arch/installedpkgs
   sed -i '/^#/d' CONTENTS
   install -m644 CONTENTS "$pkgdir"/var/lib/texmf/arch/installedpkgs/${pkgname}_${_revnr}.pkgs
   install -m644 $pkgname.maps "$pkgdir"/var/lib/texmf/arch/installedpkgs/
   install -m644 $pkgname.fmts "$pkgdir"/var/lib/texmf/arch/installedpkgs/
   install -m755 -d "$pkgdir"/usr/share
   wanteddirs=$(for d in *; do test -d $d && [[ $d != texmf* ]] && echo $d; done) || true
   for dir in $wanteddirs; do
     find $dir -type d -exec install -d -m755 "$pkgdir"/usr/share/texmf-dist/'{}' \;
     find $dir -type f -exec install -m644 '{}' "$pkgdir"/usr/share/texmf-dist/'{}' \;
   done
   if [[ -d texmf-dist ]]; then
     find texmf-dist -type d -exec install -d -m755 "$pkgdir"/usr/share/'{}' \;
     find texmf-dist -type f -exec install -m644 '{}' "$pkgdir"/usr/share/'{}' \;
   fi
   if [[ -d "$pkgdir"/usr/share/texmf-dist/scripts ]]; then
     find "$pkgdir"/usr/share/texmf-dist/scripts -type f -exec chmod a+x '{}' \;
   fi
   install -m755 -d "$pkgdir"/usr/bin
   ln -s /usr/share/texmf-dist/scripts/ulqda/ulqda.pl "$pkgdir"/usr/bin/ulqda
}
