# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gtk-doc
pkgver=1.15
pkgrel=3
pkgdesc="Documentation tool for public library API"
arch=('any')
license=('GPL' 'FDL')
depends=('docbook-xsl' 'gnome-doc-utils' 'perl')
makedepends=('pkgconfig' 'jade')
optdepends=('jade: SGML support')
url="http://www.gtk.org/gtk-doc/"
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.bz2
        fixxref_tweak_the_vim_invocation.patch)
sha256sums=('59cc73babf0db3a89d1a71f7ef5394a42ec1d3447d3a0a1539057a7b0ce44831'
            '3fb764d102aa75ea2e51e016e2dc1e1da7bec86b93e7f7dbb7ce66e775996d48')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/fixxref_tweak_the_vim_invocation.patch"
  export PYTHON=/usr/bin/python2
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
