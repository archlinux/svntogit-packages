# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Ronald van Haren <ronald@archlinux.org>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Jason Taylor <jftaylor21@gmail.com>
# Contributor: Darwin Bautista <djclue917@gmail.com>

pkgname=qhull
pkgver=2015.2
pkgrel=3
pkgdesc="A general dimension code for computing convex hulls and related structures"
arch=(x86_64)
url="http://www.qhull.org/"
license=(custom)
depends=(glibc)
makedepends=(cmake)
source=("http://www.qhull.org/download/qhull-2015-src-7.2.0.tgz")
sha256sums=('78b010925c3b577adc3d58278787d7df08f7c8fb02c3490e375eab91bb58a436')

build() {
  cd $pkgname-$pkgver

  if [ "${CARCH}" = "x86_64" ]; then
    EXTRAOPTS="-DCMAKE_CXX_FLAGS=-fPIC -DCMAKE_C_FLAGS=-fPIC"
  fi

  mkdir srcbuild
  cd srcbuild
  cmake -DCMAKE_INSTALL_PREFIX=/usr $EXTRAOPTS ..
  make
}

package() { 
  cd $pkgname-$pkgver/srcbuild

  make DESTDIR="$pkgdir" install

  # Install license
  install -Dm644 "$srcdir/$pkgname-$pkgver/COPYING.txt" "$pkgdir/usr/share/licenses/$pkgname/license.txt"
}
