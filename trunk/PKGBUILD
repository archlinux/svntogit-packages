# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Tom Gundersen <teg@jklm.no>
# Contributor: Thomas Baechler <thomas@archlinux.org>
# Contributor: Sarah Hay <sarahhay@mb.sympatico.ca>

pkgname=a52dec
pkgver=0.7.4
pkgrel=12
pkgdesc="A free library for decoding ATSC A/52 streams"
url="http://liba52.sourceforge.net/"
arch=('x86_64')
license=('GPL2')
depends=('glibc')
source=("http://liba52.sourceforge.net/files/${pkgname}-${pkgver}.tar.gz"
        'a52dec-0.7.4-build.patch'
        'stderr.patch')
sha256sums=('a21d724ab3b3933330194353687df82c475b5dfb997513eef4c25de6c865ec33'
            '00a094d6178cd21bfc2303ff2ac91ce656810e6fef50e77ab30e18d0f4d4a650'
            '5151ccb8b727f3985042011f0e5c4891dd5f2a9e6f177a23d41b44bebbdd1947')

prepare() {
  cd "${pkgname}-${pkgver}"
  patch -Np1 -i ../a52dec-0.7.4-build.patch
  patch -Np1 -i ../stderr.patch
  sed -i 's/AM_CONFIG_HEADER/AC_CONFIG_HEADERS/' configure.in
  mv configure.in configure.ac
  ./bootstrap
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --enable-shared
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m644 liba52/a52_internal.h "${pkgdir}/usr/include/a52dec/"
}
