# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Gabriel Martinez < reitaka at gmail dot com >

pkgname=libplist
pkgver=1.3
pkgrel=2
pkgdesc="A library to handle Apple Property List format whereas it's binary or XML"
url="http://libimobiledevice.org/"
arch=('i686' 'x86_64')
license=('GPL2' 'LGPL2.1')
depends=('glib2>=2.24.1' 'libxml2>=2.7.7')
makedepends=('cmake' 'swig' 'python2')
source=(http://cloud.github.com/downloads/JonathanBeck/${pkgname}/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('982c8aac59cdc3fafc925a407a29b6cf367c5ec9bad6ad509fe5ea25d3e5b6b0')

build() {
  # CMake is not patched yet
  cd "${srcdir}/${pkgname}-${pkgver}/cmake/modules"
  cp /usr/share/cmake-2.8/Modules/FindPython*.cmake .
  sed -i -e 's#2.6#2.7 2.6#' FindPythonLibs.cmake
  sed -i -e 's#python2.6#python2.7 python2.6#' FindPythonInterp.cmake

  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../"${pkgname}-${pkgver}" \
      -DCMAKE_SKIP_RPATH=ON \
      -DCMAKE_INSTALL_PREFIX:PATH=/usr
  make || return 1
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install || return 1
}
