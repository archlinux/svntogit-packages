# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gst-plugins-bad
pkgver=1.14.4+22+ge87fb02c1
pkgrel=1
pkgdesc="GStreamer Multimedia Framework Bad Plugins"
url="https://gstreamer.freedesktop.org/"
arch=(x86_64)
license=(LGPL)
depends=(mjpegtools gst-plugins-base-libs curl chromaprint libmms faad2 celt libdca libdvdnav
         libmodplug libgme wayland libofa openjpeg2 libwebp libsrtp gnutls sbc rtmpdump libgudev
         libexif libdvdread libvdpau libmpeg2 wildmidi ladspa openal libusb vulkan-icd-loader
         libfdk-aac faac soundtouch spandsp neon webrtc-audio-processing libdc1394 libmpcdec zvbi
         openexr libbs2b libnice lcms2 bluez-libs glu srt aom x265)
makedepends=(python gtk-doc autoconf-archive git gobject-introspection gtk3 vulkan-headers
             vulkan-validation-layers librsvg libtiger zbar fluidsynth lilv)
_commit=e87fb02c151aaf6da0a3a97928c531240e8800a5  # 1.14
source=("git+https://gitlab.freedesktop.org/gstreamer/gst-plugins-bad.git#commit=$_commit"
        'gst-common::git+https://gitlab.freedesktop.org/gstreamer/common.git')
sha256sums=('SKIP'
            'SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname

  # libfdk-aac 2.0.0
  git cherry-pick -n \
    f4fdb9770c76113f38515245fecc5f11b3ace20d \
    19d34f6b5e1633d5ec4bb2832c58470f0c829cab

  git submodule init
  git config --local submodule.common.url "$srcdir/gst-common"
  git submodule update

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib \
    --with-package-name="GStreamer Bad Plugins (Arch Linux)" \
    --with-package-origin="https://www.archlinux.org/" \
    --with-gtk=3.0 \
    --enable-experimental \
    --enable-gtk-doc \
    --disable-static
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd $pkgname
  # bad tests are bad
  #make -k check || :
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
