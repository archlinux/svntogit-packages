# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: François Charette <firmicus ατ gmx δοτ net>

pkgname=perl-xml-sax
pkgver=1.00
pkgrel=1
pkgdesc="Simple API for XML"
arch=('any')
url="http://search.cpan.org/dist/XML-SAX"
license=('GPL' 'PerlArtistic')
depends=('perl' 'perl-xml-sax-base' 'perl-xml-namespacesupport')
options=('!emptydirs')
install=perl-xml-sax.install
source=("https://www.cpan.org/authors/id/G/GR/GRANTM/XML-SAX-$pkgver.tar.gz") 
sha512sums=('95553c9693d8a310747b4b86d4e80a58d4056b18cfc334f6979c98d0e9034a34253bfb17daecfc5bb28b1bd65e6baf52136f5135a969fadba86aa4148ea9bc1d')

build() {
  cd XML-SAX-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd XML-SAX-$pkgver
  make test
}

package() {
  cd XML-SAX-$pkgver
  PERLLIB=blib/lib make install DESTDIR="$pkgdir"

  find "$pkgdir" -name '.packlist' -delete
  find "$pkgdir" -name '*.pod' -delete
}
