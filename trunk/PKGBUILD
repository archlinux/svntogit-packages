# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gpaste
pkgver=3.20.3
pkgrel=1
pkgdesc="Clipboard management system"
url="http://www.imagination-land.org/tags/GPaste.html"
license=(GPL3)
arch=(i686 x86_64)
depends=(gtk3)
makedepends=(intltool vala appstream-glib gobject-introspection gnome-shell gnome-control-center)
optdepends=("wgetpaste: Upload clipboard contents")
options=(!emptydirs)
install=gpaste.install
source=("http://www.imagination-land.org/files/$pkgname/$pkgname-$pkgver.tar.xz")
sha256sums=('0bbd7f86597690095b5c00c5d5932f6246e5d4925222549dbdb4394b83542ab4')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
    --libexecdir=/usr/lib \
    --sysconfdir=/etc \
    --enable-vala \
    --enable-applet
  make
}

check () {
  cd $pkgname-$pkgver
  #make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  install -Dm644 data/completions/gpaste-client \
    "$pkgdir/usr/share/bash-completion/completions/gpaste-client"
  install -Dm644 data/completions/_gpaste-client \
    "$pkgdir/usr/share/zsh/site-functions/_gpaste-client"

  # Don't autostart the applet, ever
  rm "$pkgdir/etc/xdg/autostart/org.gnome.GPaste.Applet.desktop"
}
