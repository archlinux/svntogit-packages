# $Id$
# Maintainer :
# Contributor: Hugo Doria <hugo@archlinux.org>

pkgname=bzr
pkgver=2.6.0
pkgrel=1
pkgdesc="A decentralized revision control system (bazaar)"
arch=('i686' 'x86_64')
url="http://bazaar.canonical.com/en/"
license=('GPL')
depends=('python2')
optdepends=('python2-paramiko: for sftp support')
source=("http://launchpad.net/${pkgname}/${pkgver%.*}/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz"{,.sig})
sha1sums=('5eb4d0367c6d83396250165da5bb2c8a9f378293'
          'SKIP')
validpgpkeys=('84736900600FF95EF23513F52975A150DEF6218F')  # Vincent Ladeuil

prepare() {
  cd ${pkgname}-${pkgver}
  sed -i 's|man/man1|share/man/man1|' setup.py
  sed -i 's|/usr/bin/env python|/usr/bin/env python2|' \
    bzrlib/{plugins/bash_completion/bashcomp.py,tests/ssl_certs/create_ssls.py,patiencediff.py,_patiencediff_py.py}
}

build() {
  cd ${pkgname}-${pkgver}
  python2 setup.py build
}

package() {
  cd ${pkgname}-${pkgver}
  python2 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1

  # bash-completion
  install -D -m644 contrib/bash/bzr \
    "${pkgdir}/usr/share/bash-completion/completions/bzr"
}
