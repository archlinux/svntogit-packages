# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Link Dupont <link@subpop.net>
#
pkgname=dbus
pkgver=1.2.24
pkgrel=1
pkgdesc="Freedesktop.org message bus system"
url="http://www.freedesktop.org/Software/dbus"
arch=(i686 x86_64)
license=('GPL' 'custom')
depends=("dbus-core>=${pkgver}" 'libx11')
makedepends=('libsm' 'libice')
source=(http://dbus.freedesktop.org/releases/${pkgname}/${pkgname}-${pkgver}.tar.gz
        30-dbus)
md5sums=('565346cecd9cfecf1463540c6086cc2c'
         'd14e59575f04e55d21a04907b6fd9f3c')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/usr/lib/dbus-1.0 --with-dbus-user=81 \
      --with-system-pid-file=/var/run/dbus.pid \
      --disable-verbose-mode --disable-static \
      --enable-inotify --disable-dnotify \
      --disable-tests --disable-asserts || return 1
  cd tools || return 1
  make dbus-launch || return 1
  install -m755 -d "${pkgdir}/usr/bin"
  install -m755 -d "${pkgdir}/usr/share/man/man1"
  install -m755 dbus-launch "${pkgdir}/usr/bin/" || return 1
  install -m644 dbus-launch.1 "${pkgdir}/usr/share/man/man1/" || return 1

  install -m755 -d "${pkgdir}/etc/X11/xinit/xinitrc.d"
  install -m755 "${srcdir}/30-dbus" "${pkgdir}/etc/X11/xinit/xinitrc.d/" || return 1

  install -d -m755 "${pkgdir}/usr/share/licenses/dbus"
  install -m644 ../COPYING "${pkgdir}/usr/share/licenses/dbus/" || return 1
}
