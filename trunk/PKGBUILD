# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: G_Syme <demichan@mail.upb.de>

pkgname=libass
pkgver=0.14.0
pkgrel=1
pkgdesc='A portable library for SSA/ASS subtitles rendering'
arch=('x86_64')
url='https://github.com/libass/libass/'
license=('BSD')
depends=('fontconfig' 'fribidi' 'glib2' 'glibc' 'harfbuzz'
         'libfreetype.so')
makedepends=('nasm')
provides=('libass.so')
source=("https://github.com/libass/libass/releases/download/${pkgver}/libass-${pkgver}.tar.xz")
sha256sums=('881f2382af48aead75b7a0e02e65d88c5ebd369fe46bc77d9270a94aa8fd38a2')

build() {
  cd libass-${pkgver}

  ./configure \
    --prefix='/usr' \
    --enable-harfbuzz \
    --enable-fontconfig
  make
}

package() {
  cd libass-${pkgver}

  make DESTDIR="${pkgdir}" install
  install -Dm 644 COPYING -t "${pkgdir}"/usr/share/licenses/libass/
}

# vim: ts=2 sw=2 et:
