# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgbase=sof-bin
pkgname=(
  sof-firmware
  sof-tools
)
pkgver=2.2.5
pkgrel=1
pkgdesc="Sound Open Firmware"
url="https://www.sofproject.org/"
arch=(x86_64)
license=(
  custom:BSD
  custom:ISC
)
_binver=v${pkgver}
source=(https://github.com/thesofproject/sof-bin/releases/download/$_binver/sof-bin-$_binver.tar.gz)
b2sums=('681ffbc16d983b795556b00881d59d2a0ba258f89482d6e136ddb61345718802d1b3f7fcc494091337f42f9377a8831d890e2c5de140f083e223f29cc5936742')

package_sof-firmware() {
  options+=(!strip)

  local fwdir="$pkgdir/usr/lib/firmware/intel"
  mkdir -p "$fwdir"

  cd sof-bin-$_binver

  cp -at "$fwdir" sof-$_binver
  ln -sr "$fwdir"/{sof-$_binver,sof}

  cp -at "$fwdir" sof-tplg-$_binver
  ln -sr "$fwdir"/{sof-tplg-$_binver,sof-tplg}

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 LICENCE* Notice*
}

package_sof-tools() {
  pkgdesc+=" - tools"
  depends=(alsa-lib)

  cd sof-bin-$_binver

  install -Dt "$pkgdir/usr/bin" tools-$_binver/*
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 LICENCE* Notice*
}

# vim:set sw=2 sts=-1 et:
