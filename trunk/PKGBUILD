# $Id$
# Maintainer: Kevin Piche <kevin@archlinux.org>
# Contributor: Manolis Tzanidakis
# Contributor: Francois Charette <francois.archlinux.org>

pkgname=perl-io-socket-ssl
_cpanname=IO-Socket-SSL
pkgver=1.59
pkgrel=1
pkgdesc="Nearly transparent SSL encapsulation for IO::Socket::INET"
arch=('any')
license=('GPL' 'PerlArtistic')
url="http://search.cpan.org/dist/${_cpanname}/"
depends=('perl-net-ssleay' 'perl>=5.10.0')
options=(!emptydirs)
source=(http://www.cpan.org/authors/id/S/SU/SULLR/${_cpanname}-${pkgver}.tar.gz)
md5sums=('2da0994ed9b23fc3c33175372494e2b1')

build() {
  cd "${srcdir}/${_cpanname}-${pkgver}"
  # install module in vendor directories.
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "${srcdir}/${_cpanname}-${pkgver}"
  make test
}

package() {
  cd "${srcdir}/${_cpanname}-${pkgver}"
  make install DESTDIR="${pkgdir}"
}
