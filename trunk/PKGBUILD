# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=skanpage
pkgver=22.12.2
pkgrel=1
pkgdesc='Utility to scan images and multi-page documents'
arch=(x86_64)
url='https://apps.kde.org/skanpage/'
license=(GPL)
depends=(ksanecore kirigami2 purpose tesseract)
makedepends=(extra-cmake-modules)
groups=(kde-applications kde-utilities)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('4524ef39c73e8f21a155aab8af3cdf1d2e37caa75fd33282afe75047f31b907a'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
