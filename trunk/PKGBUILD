# Packager: Justin "juster" Davis <jrcd83@gmail.com>
# $Id$

pkgname=perl-net-http
pkgver=6.06
pkgrel=1
pkgdesc="Low-level HTTP connection (client)"
arch=(any)
url=http://search.cpan.org/dist/Net-HTTP
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=('perl>=5.6.2')
conflicts=('perl-libwww<6')
source=("http://search.cpan.org/CPAN/authors/id/G/GA/GAAS/Net-HTTP-${pkgver}.tar.gz")
md5sums=('6e74d76205f168775de68347266793a7')

build() {
    export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor"     \
      PERL_MB_OPT="--installdirs vendor" \
      MODULEBUILDRC=/dev/null

    cd Net-HTTP-${pkgver}
    /usr/bin/perl Makefile.PL
    make
}

check() {
    export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    cd Net-HTTP-${pkgver}
    make test
}

package() {
    cd Net-HTTP-${pkgver}
    make DESTDIR="$pkgdir" install
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
