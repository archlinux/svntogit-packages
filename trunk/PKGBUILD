# $Id$
# Maintainer: Dale Blount <dale@archlinux.org>
# Contributor: Dale Blount <dale@archlinux.org>
# Contributor: Francois Charette <francois.archlinux.org>

pkgname=perl-netaddr-ip
_realname=NetAddr-IP
pkgver=4.037
pkgrel=1
pkgdesc="Perl module to manage IP addresses and subnets"
arch=(i686 x86_64)
license=('PerlArtistic' 'GPL')
depends=('perl>=5.10.0')
options=(!emptydirs)
url="http://search.cpan.org/dist/${_realname}/"
source=(http://search.cpan.org/CPAN/authors/id/M/MI/MIKER/${_realname}-${pkgver}.tar.gz)
md5sums=('d966eac6b5941af3f7c1fad839569cb4')

build() {
  cd ${srcdir}/${_realname}-${pkgver}
  # install module in vendor directories.
  perl Makefile.PL INSTALLDIRS=vendor
  make
  make test
}

package() {
  cd ${srcdir}/${_realname}-${pkgver}
  make install DESTDIR=${pkgdir}

  # remove perllocal.pod and .packlist
  find ${pkgdir} -name perllocal.pod -delete
  find ${pkgdir} -name .packlist -delete
}
