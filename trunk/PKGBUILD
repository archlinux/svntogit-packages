# $Id$
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=audacious-plugins
pkgver=2.3
pkgrel=1
pkgdesc="Plugins for Audacious"
arch=('i686' 'x86_64')
url="http://audacious-media-player.org/"
license=('GPL')
depends=('libmad' 'libvorbis')
makedepends=('jack' 'lame' 'fluidsynth' 'libcdio' 'libsidplay' 'wavpack'
             'libmpcdec' 'taglib' 'lirc-utils' 'curl' 'libmtp' 'sdl' 'projectm' 'imlib2'
             'neon' 'libmms' 'gtkglext' "audacious>=${pkgver}" 'ffmpeg' 'libsamplerate')
optdepends=('jack: JACK output plugin'
	    'libsamplerate: for JACK output'
            'lame: FileWriter output plugin'
            'fluidsynth: FluidSynth backend for AMIDI-Plug input plugin'
            'libcdio: CD Audio input plugin'
            'libsidplay: SID input plugin'
            'wavpack: WavPack input plugin'
            'libmpcdec: Musepack input plugin'
            'taglib: Musepack input plugin'
            'lirc-utils: LIRC plugin'
            'curl: Audioscrobbler plugin'
            'libmtp: MTP upload plugin'
            'sdl: Paranormal and projectM visualization plugins'
            'projectm: projectM visualization plugin'
            'imlib2: RootVis visualization plugin'
            'neon: HTTP transport plugin'
            'libmms: MMS transport plugin'
	    'ffmpeg: for MPEG 4 Audio AAC')
source=(http://distfiles.atheme.org/$pkgname-$pkgver.tgz)

build() {
  cd "$srcdir/$pkgname-$pkgver"

  if [[ $CARCH = "i686" ]]; then
    ./configure --prefix=/usr --disable-esd --disable-pulse \
      --enable-amidiplug --disable-adplug --enable-chardet \
      --disable-sse2 || return 1
  elif [[ $CARCH = "x86_64" ]]; then
    ./configure --prefix=/usr --disable-esd --disable-pulse \
      --enable-amidiplug --disable-adplug --enable-chardet || return 1
  fi

  make || return 1
  make DESTDIR="$pkgdir" install || return 1
}
sha256sums=('7da44d2aaccceea6ad838a8b677abd5f3e5e10ad168f6f478d3e5e2c4aacaa41')
