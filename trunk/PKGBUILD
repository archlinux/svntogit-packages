# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: hauptmech, figo.zhang, chubtuff, lubosz
#
# Matlab bindings are not built by default to reduce dependencies.

pkgname=gl2ps
pkgver=1.3.8
pkgrel=3
pkgdesc="an OpenGL to PostScript printing library"
arch=('i686' 'x86_64')
url='http://geuz.org/gl2ps/'
license=('LGPL2')
makedepends=('cmake' 'texlive-core')
source=("http://geuz.org/gl2ps/src/gl2ps-${pkgver}.tgz")
sha512sums=('76e90d675764196d249d87c6041088736a8b41d9b93620c6171a40362a259d50e34d5efc06e4ea17e6c147bc26b6a3a7356d95ea5e204193ef631fb48e0c0a4e')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../gl2ps-$pkgver-source \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_EXE_LINKER_FLAGS=-lm
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir/" install
}

