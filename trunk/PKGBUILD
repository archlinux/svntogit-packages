# $Id$
# Maintainer: Evangelos Foutras <foutrelis@gmail.com>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Hokum <hokum_at_mail_dot_ru>

pkgname=gptfdisk
pkgver=0.7.1
pkgrel=2
pkgdesc="A text-mode partitioning tool that works on Globally Unique Identifier (GUID) Partition Table (GPT) disks"
arch=('i686' 'x86_64')
url="http://www.rodsbooks.com/gdisk/"
license=('GPL2')
depends=('gcc-libs' 'util-linux' 'popt' 'icu')
provides=('gdisk')
conflicts=('gdisk')
replaces=('gdisk')
source=(http://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgver/$pkgname-$pkgver.tgz)
md5sums=('7c8d810df61e81c821bef399b832e89e')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  
  make
}

package () {
  cd "$srcdir/$pkgname-$pkgver"
  
  install -d "$pkgdir"/{sbin,usr/share/{man/man8,gdisk}}
  install -t "$pkgdir/sbin" gdisk sgdisk fixparts
  install -m644 -t "$pkgdir/usr/share/man/man8" {gdisk,sgdisk}.8
  install -m644 -t "$pkgdir/usr/share/gdisk" README NEWS
}

# vim:set ts=2 sw=2 et:
