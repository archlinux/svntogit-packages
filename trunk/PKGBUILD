# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=gupnp-igd
pkgver=1.2.0
pkgrel=3
pkgdesc="A library to handle UPnP IGD port mapping"
url="https://wiki.gnome.org/Projects/GUPnP"
arch=(x86_64)
license=(LGPL)
depends=(gupnp)
makedepends=(gobject-introspection git gtk-doc meson)
_commit=a0aafc79ac8e258f849cdb2924cb11ec9162d0c5  # tags/1.2.0-correct^0
source=("git+https://gitlab.gnome.org/GNOME/gupnp-igd.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-correct//;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname

  # GUPnP 1.6
  git cherry-pick -n 649b7100339c57531a8e31f69220f8e17f0860e0 \
                     79a1e4cf8c256132978a1d8ab718c8ad132386de
}

build() {
  arch-meson $pkgname build -D gtk_doc=true
  meson compile -C build
}

check() {
  # No multicast in our containers?
  meson test -C build --print-errorlogs || :
}

package() {
  depends+=(libg{lib,object,io}-2.0.so libg{ssdp,upnp}-1.6.so)
  provides+=(libgupnp-igd-1.0.so)

  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
