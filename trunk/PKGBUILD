# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=networkmanager-pptp
pkgver=0.9.8.4
pkgrel=3
pkgdesc="NetworkManager VPN plugin for pptp "
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.gnome.org/projects/NetworkManager/"
_pppver=2.4.6
depends=("networkmanager>=${pkgver}" 'pptpclient' 'gtk3' 'libgnome-keyring' "ppp=$_pppver")
makedepends=('intltool')
optdepends=('network-manager-applet: GNOME frontends to NetWorkmanager')
install=networkmanager-pptp.install
source=(http://ftp.gnome.org/pub/GNOME/sources/NetworkManager-pptp/0.9/NetworkManager-pptp-${pkgver}.tar.xz
        gtk_table_to_gtk_grid.patch)
sha256sums=('97ba8f6f726056c3b8e0e1c8d2ab3d32791cadd7c3a38e0478fb2bd3ecafdce8'
            '17c194c605bca780bf657711e9cff910c63ab27eae70c11cb6c53e9c75ad3215')

prepare() {
  cd NetworkManager-pptp-${pkgver}
  patch -Np1 -i ../gtk_table_to_gtk_grid.patch
}

build() {
  cd NetworkManager-pptp-${pkgver}
  ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --libexecdir=/usr/lib/networkmanager \
        --disable-static \
        --enable-more-warnings=yes \
        --with-pppd-plugin-dir=/usr/lib/pppd/$_pppver
  make
}

package() {
  cd NetworkManager-pptp-${pkgver}
  make DESTDIR="${pkgdir}" install
}
