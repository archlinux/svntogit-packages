# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-file-mimeinfo
pkgver=0.29
pkgrel=1
pkgdesc='Determine file type, includes mimeopen and mimetype'
arch=('any')
url="https://metacpan.org/release/File-MimeInfo"
license=('GPL' 'PerlArtistic')
depends=('perl-file-basedir' 'perl-file-desktopentry' 'shared-mime-info')
checkdepends=('perl-test-pod' 'perl-test-pod-coverage')
options=('!emptydirs')
source=(http://cpan.metacpan.org/authors/id/M/MI/MICHIELB/File-MimeInfo-${pkgver}.tar.gz)
sha512sums=('3b012843a0934e2df78a28e52efec3cbe10b06fd2be7f3d63f64309752240af2e7ef9440a839aa170197f1037baee5fdcf1c1303fd57349841f37ee7001b68dd')

build() {
  cd File-MimeInfo-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd File-MimeInfo-${pkgver}
  make test
}

package() {
  cd File-MimeInfo-${pkgver}
  make DESTDIR="${pkgdir}" install
}
