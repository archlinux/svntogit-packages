# $Id$
# Maintainer: Anatol Pomozov
# Contributor: JSpaces <jspace -aT- unseen -d0T- is>
# Contributor: Triode <triode1 -aT- btinternet -d0t- com>

pkgname=libsoxr
pkgver=0.1.1
pkgrel=3
pkgdesc='The SoX Resampler library that aims to give fast and high quality results for any constant resampling ratio'
arch=(i686 x86_64) 
url='http://sourceforge.net/p/soxr/wiki/Home/'
license=(GPL)
depends=(gcc-libs)
makedepends=(cmake)
options=(!emptydirs)
source=(http://downloads.sourceforge.net/project/soxr/soxr-$pkgver-Source.tar.xz
        0001-fix-ineffectual-SOXR_MINIMUM_PHASE-and-SOXR_INTERMED.patch)
sha1sums=('f5d90e375db3914a522fef477898bde8c70243e7'
          '73e6a3418001ce01aa16c3d8e29e2535d0158111')

prepare() {
  cd soxr-$pkgver-Source
  patch -p1 < "$srcdir"/0001-fix-ineffectual-SOXR_MINIMUM_PHASE-and-SOXR_INTERMED.patch
}

build() {
  cd soxr-$pkgver-Source
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_EXAMPLES=OFF \
    -DBUILD_SHARED_LIBS=ON \
    -DCMAKE_BUILD_TYPE=Release \
    -DVISIBILITY_HIDDENL=ON \
    -DWITH_AVFFT=ON \
    -DWITH_DOUBLE_PRECISION=ON \
    -DWITH_LSR_BINDINGS=ON \
    -DWITH_OPENMP=ON \
    -DWITH_PFFFT=ON \
    -DWITH_SIMD=ON \
    .
  make
}

check() {
  cd soxr-$pkgver-Source
  make test
}

package() {
  cd soxr-$pkgver-Source
  
  make DESTDIR="$pkgdir" install
  install -Dm644 LICENCE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  rm -r "$pkgdir"/usr/share/doc/$pkgname/
}
