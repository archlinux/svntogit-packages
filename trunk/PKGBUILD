# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kig
pkgver=16.04.2
pkgrel=1
pkgdesc="Interactive Geometry"
url="http://kde.org/applications/education/kig/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdeedu')
depends=('ktexteditor' 'boost-libs' 'python2' 'hicolor-icon-theme') # kemoticons, kitemmodels are only required for tests
makedepends=('extra-cmake-modules' 'kdoctools' 'boost' 'qt5-xmlpatterns')
optdepends=('qt5-xmlpatterns: support for GeoGebra files')
replaces=('kdeedu-kig')
conflicts=('kdeedu-kig')
source=("http://download.kde.org/stable/applications/${pkgver}/src/kig-${pkgver}.tar.xz")
sha1sums=('a935ce854f1511b62e085dc02642f480c010ecf4')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install

  # Use the python2 executable
  sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|' "${pkgdir}"/usr/bin/pykig.py
}
