# $Id$
# Jan de Groot <jgc@archlinux.org>

pkgname=eog
pkgver=2.28.1
pkgrel=1
pkgdesc="Eye of Gnome: An image viewing and cataloging program"
arch=(i686 x86_64)
license=('GPL')
depends=('gnome-desktop>=2.28.0' 'libexif>=0.6.17' 'lcms>=1.18-2' 'desktop-file-utils' 'gnome-icon-theme>=2.28.0' 'exempi>=2.1.0')
makedepends=('pkgconfig' 'gnome-doc-utils>=0.18.0' 'intltool')
install=eog.install
groups=('gnome-extra')
options=(!emptydirs)
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.28/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('e30b2f8f05d74acd715f1a93467cc3200850033338ddad05b6b06db8386ad27e')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-scrollkeeper || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1
  
  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain eog ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
