# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgbase=oxygen
pkgname=(oxygen oxygen-kde4)
pkgver=5.13.3
pkgrel=1
arch=(x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
makedepends=(frameworkintegration kcmutils kdecoration extra-cmake-modules kdoctools kdelibs automoc4 kwayland)
source=("https://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('7f322a3d409dcf89dc643662e31ae06dfd253134d9121e6db9739413e71a0281'
            'SKIP')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E'  # Jonathan Riddell
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  mkdir -p build{,-kde4}
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
  cd ..

  cd build-kde4
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF \
    -DUSE_KDE4=ON
  make
}

package_oxygen() {
  pkgdesc='KDE Oxygen style'
  depends=(frameworkintegration kdecoration kcmutils kwayland hicolor-icon-theme)
  conflicts=(oxygen-cursors)
  replaces=(oxygen-cursors)
  groups=(plasma)

  cd build
  make DESTDIR="$pkgdir" install
}

package_oxygen-kde4() {
  pkgdesc='KDE Oxygen style for KDE4 applications'
  depends=(kdelibs)

  cd build-kde4
  make DESTDIR="$pkgdir" install

  # needed so that pure Qt4 apps are correctly themed
  install -d -m755 "$pkgdir"/usr/lib/qt4/plugins/styles
  ln -s /usr/lib/kde4/plugins/styles/oxygen.so "$pkgdir"/usr/lib/qt4/plugins/styles
}
