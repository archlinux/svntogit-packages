# $Id$
# Maintainer: Daniel Isenmann <daniel@archlinux.org>

pkgname=libgdiplus
pkgver=3.12
pkgrel=1
pkgdesc="An Open Source Implementation of the GDI+ API"
arch=(i686 x86_64)
license=('MPL' 'LGPL')
url="http://www.mono-project.com"
depends=('libtiff' 'cairo' 'giflib' 'glib2' 'libexif')
source=(http://download.mono-project.com/sources/${pkgname}/${pkgname}-${pkgver}.tar.gz
	libgdiplus0-giflib5.patch)
md5sums=('b8fd78c2adce16498354c2476a248d98'
         'fa26245d5bf28607730192386d66fca7')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -Np1 -i ../libgdiplus0-giflib5.patch

  sed -i -e 's/-L\${libjpeg_prefix}\/lib -ljpeg/-ljpeg/' configure
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --with-cairo=system

  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
