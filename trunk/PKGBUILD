# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgbase=pyqt6-webengine
pkgname=(python-pyqt6-webengine)
pkgver=6.4.0
pkgrel=1
pkgdesc='Python bindings for QtWebEngine'
arch=(x86_64)
url='https://www.riverbankcomputing.com/software/pyqtwebengine/intro'
license=(GPL3)
groups=(pyqt6)
depends=(qt6-webengine python-pyqt6)
makedepends=(sip pyqt-builder)
source=(https://pypi.python.org/packages/source/P/PyQt6-WebEngine/PyQt6_WebEngine-$pkgver.tar.gz)
sha256sums=('4c71c130860abcd11e04cafb22e33983fa9a3aee8323c51909b15a1701828e21')

build() {
  cd PyQt6_WebEngine-$pkgver
  sip-build \
    --no-make \
    --qmake=/usr/bin/qmake6
  cd build
  make
}

package_python-pyqt6-webengine() {
  cd PyQt6_WebEngine-$pkgver/build
  make INSTALL_ROOT="$pkgdir" install
}
