# Maintainer: Justin Davis (juster) <jrcd83@gmail.com>

pkgname=perl-net-server
pkgver=2.014
pkgrel=1
pkgdesc='Extensible, general Perl server engine'
arch=('any')
url="https://metacpan.org/release/Net-Server"
license=('PerlArtistic' 'GPL')
depends=('perl')
options=('!emptydirs')
source=("https://search.cpan.org/CPAN/authors/id/R/RH/RHANDOM/Net-Server-$pkgver.tar.gz")
sha512sums=('6b1bbfeed13af5f9dba76873899e162497234b5cc8fc08bf749fd86e0377bd9184a9807df61f27645634d0ff8d8e6c8c14b700b3d283fbbb7d1d844e6608c9c2')

build() {
  cd Net-Server-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Net-Server-$pkgver
  make test
}

package() {
  cd Net-Server-$pkgver
  make install DESTDIR="$pkgdir"
}
