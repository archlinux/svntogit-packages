# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=dbus-glib
pkgver=0.112
pkgrel=2
pkgdesc="GLib bindings for DBUS"
url="https://www.freedesktop.org/wiki/Software/DBusBindings"
arch=(x86_64)
license=('GPL')
depends=('dbus' 'glib2')
makedepends=('python' 'gtk-doc')
source=(https://dbus.freedesktop.org/releases/${pkgname}/${pkgname}-${pkgver}.tar.gz{,.asc}
        dbus-glib-bash-completion.patch::https://gitlab.freedesktop.org/dbus/dbus-glib/-/merge_requests/5.patch)
sha256sums=('7d550dccdfcd286e33895501829ed971eeb65c614e73aadb4a08aeef719b143a'
            'SKIP'
            'edbdd4fb5fce358b427da1a1c68347ce09ca444da6ac67ff1fcd6cc67fc92f99')
validpgpkeys=('DA98F25C0871C49A59EAFF2C4DE8FF2A63C7CC90')

prepare() {
  patch -d $pkgname-$pkgver -p1 < dbus-glib-bash-completion.patch # Fix bash completion
}

build() {
  cd $pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib \
    --enable-gtk-doc \
    --disable-static
  make
}

package() {
  cd $pkgname-$pkgver
  _c=/usr/share/bash-completion/completions
  make DESTDIR="$pkgdir" install completiondir=$_c
  mv "$pkgdir$_c"/{dbus-bash-completion.sh,dbus-send}
}
