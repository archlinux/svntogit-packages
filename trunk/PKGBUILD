# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=gnome-screenshot
pkgver=3.26.0
pkgrel=1
pkgdesc="Take pictures of your screen"
url="https://git.gnome.org/browse/gnome-screenshot"
arch=(x86_64)
license=(GPL2)
depends=(dconf gtk3 libcanberra)
makedepends=(git meson appstream-glib)
groups=(gnome)
_commit=03d32c3726fa9ac8f5346953a33a6053ca059fb1  # tags/3.26.0^0
source=("git+https://git.gnome.org/browse/gnome-screenshot#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  mkdir build
  cd $pkgname
}

build() {
  cd build
  arch-meson ../$pkgname
  ninja
}

check() {
  cd build
  meson test
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}

# vim:set ts=2 sw=2 et:
