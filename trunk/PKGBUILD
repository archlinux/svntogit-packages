# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: AndyRTR <andyrtr@archlinux.org>
# Contributor: Tobias Kieslich <tobias (at) archlinux.org>

pkgname=xfce4-verve-plugin
pkgver=2.0.0
pkgrel=1
pkgdesc="Command line plugin Xfce4 panel"
arch=('x86_64')
license=('GPL2')
url="http://goodies.xfce.org/projects/panel-plugins/xfce4-verve-plugin"
groups=('xfce4-goodies')
depends=('xfce4-panel')
makedepends=('intltool')
conflicts=('verve-plugin')
replaces=('verve-plugin')
source=(https://archive.xfce.org/src/panel-plugins/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=('9e6510ba6c48fcfc60d1c6ab14613dd7b6ff095cabbf4746f82bf788d8ab4cd2')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
