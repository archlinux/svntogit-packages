# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Thomas Baechler <thomas@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=libdvdread
pkgver=6.0.0
pkgrel=1
pkgdesc="Provides a simple foundation for reading DVD video disks"
arch=(x86_64)
url="https://www.videolan.org/developers/libdvdnav.html"
license=('GPL')
depends=('glibc')
makedepends=('libdvdcss' 'git')
optdepends=('libdvdcss: for decoding encrypted DVDs')
_commit=95fdbe8337d2ff31dcfb68f35f3e4441dc27d92f  # tags/6.0.0^0
source=("git+https://code.videolan.org/videolan/libdvdread.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed -e 's/-/+/g'
}

prepare() {
  cd $pkgname
  autoreconf -fi
}

build() {
  cd $pkgname
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname
  make DESTDIR="${pkgdir}" install
}
