# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=polari
pkgver=3.26.2
pkgrel=1
pkgdesc="An IRC Client for GNOME"
url="https://wiki.gnome.org/Apps/Polari"
arch=(x86_64)
license=(GPL)
depends=(gjs gtk3 telepathy-glib telepathy-idle telepathy-mission-control telepathy-logger gspell)
makedepends=(gobject-introspection appdata-tools yelp-tools appstream-glib git meson)
groups=(gnome-extra)
_commit=148f5740691064c0aa0533ad393588648139c278  # tags/3.26.2^0
source=("git+https://gitlab.gnome.org/GNOME/polari.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  mkdir build
  cd $pkgname
}

build() {
  cd build
  arch-meson ../$pkgname
  ninja
}

check() {
  cd build
  meson test
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}
