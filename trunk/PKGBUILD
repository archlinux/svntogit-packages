# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-xclipboard
pkgver=1.1.3
pkgrel=2
pkgdesc="X clipboard manager"
arch=(x86_64)
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libxaw' 'libxmu' 'libxt' 'libx11')
makedepends=('xorg-util-macros')
source=(https://xorg.freedesktop.org/archive/individual/app/xclipboard-${pkgver}.tar.bz2)
sha512sums=('84a51d11851756422eb23e67b2721d2bcb2ac248fa32c0837423b6169144dff76717c22611ad3c93633e09a44dc70c847a2470d3988eff0b24d77e252a4e147e')

build() {
  cd "${srcdir}/xclipboard-${pkgver}"
  ./configure --prefix=/usr
  make
}

package(){
  cd "${srcdir}/xclipboard-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
