# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Federico Quagliata (quaqo) <quaqo@despammed.com>

pkgname=cracklib
pkgver=2.8.18
pkgrel=2
pkgdesc="Password Checking Library"
arch=('i686' 'x86_64')
license=('GPL')
url="http://sourceforge.net/projects/cracklib"
depends=('glibc' 'zlib')
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz)
md5sums=('79053ad8bc714a44cd660cb12116211b')

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure --prefix=/usr --without-python
  make 
}

check() {
  cd $srcdir/$pkgname-$pkgver
  make check
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
  install -Dm644 dicts/cracklib-small $pkgdir/usr/share/dict/cracklib-small
  sh ./util/cracklib-format dicts/cracklib-small \
    | sh ./util/cracklib-packer $pkgdir/usr/share/cracklib/pw_dict
}
