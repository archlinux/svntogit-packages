# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=syndication
pkgver=5.61.0
pkgrel=1
pkgdesc="RSS/Atom parser library"
arch=(x86_64)
url="https://community.kde.org/Frameworks"
license=(LGPL)
depends=(kcodecs)
makedepends=(extra-cmake-modules doxygen qt5-tools qt5-doc)
groups=(kf5)
conflicts=(kde-syndication)
provides=(kde-syndication)
replaces=(kde-syndication)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('2803b2960dd23492ad002e0f23563c9f06500ddc144dd0be2e3e0ef2f6c1f576'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
