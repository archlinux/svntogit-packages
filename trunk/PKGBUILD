# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=koko
pkgver=23.04.0
pkgrel=1
pkgdesc='Image gallery application'
url='https://apps.kde.org/koko/'
arch=(x86_64)
license=(GPL LGPL)
groups=(kde-applications kde-graphics)
depends=(kirigami2 kdeclarative qt5-location kfilemetadata kquickimageeditor)
makedepends=(extra-cmake-modules)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        https://download.geonames.org/export/dump/cities1000.zip
        https://download.geonames.org/export/dump/admin1CodesASCII.txt
        https://download.geonames.org/export/dump/admin2Codes.txt)
sha256sums=('939a6d71bf0a9cfb1e897b8b314b58b352ba4322ca5a57952f16b3886caa67e6'
            'SKIP'
            'c2d5211aa34a32143955788cccc0b7acf06d167830cb6769fff87dfc955e8a7c'
            'b3b3e6b587776ff0ee9b14f56fc27af6cf14a32535a8c14197b5a29d83127cf1'
            'cc85944be54766a24f703a57c2b006375d8a8d86432d3765f8e262be15e19758')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

prepare() {
  cp -L cities1000.zip admin1CodesASCII.txt admin2Codes.txt $pkgname-$pkgver/src
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
