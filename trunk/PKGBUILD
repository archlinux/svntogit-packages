# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Jameson Pugh <imntreal@gmail.com>
# Contributor: Tim Jester-Pfadt <t.jp<at>gmx.de>

pkgbase=appstream
pkgname=(appstream appstream-qt)
pkgver=0.12.2
pkgrel=2
pkgdesc="Provides a standard for creating app stores across distributions"
arch=(x86_64)
url="http://distributions.freedesktop.org/wiki/AppStream"
license=(GPL)
depends=(libyaml libxml2 libstemmer glib2)
makedepends=(meson xmlto gobject-introspection python2 gtk-doc qt5-tools itstool vala gperf)
source=("https://www.freedesktop.org/software/appstream/releases/AppStream-$pkgver.tar.xz"{,.asc}
        update-appstream-cache.hook
        https://github.com/ximion/appstream/commit/734e9da6.patch)
sha256sums=('8b232fdac3e2bc46b0adb5e9bd462fdc99f52de3afc2f17a67b35c122b6e4c05'
            'SKIP'
            'a6c42e4607ffd7d7dc931d608f1856e2ba7689d4caac0783e7100d86de0f951b'
            'c4a49320bc8296361b8b869ce4d07ea1b24f417aa9f68c77f10dc105d494434d')
validpgpkeys=(D33A3F0CA16B0ACC51A60738494C8A5FBF4DECEB) # Matthias Klumpp <matthias@tenstral.net>

prepare() {
  mkdir -p build

  cd AppStream-$pkgver
  patch -p1 -i ../734e9da6.patch # Fix errors with Gnome 3.30 metadata
}

build() {
  cd build

  meson ../AppStream-$pkgver \
    --prefix=/usr \
    -Dqt=true \
    -Dvapi=true
  ninja
}

package_appstream() {
  cd build

  DESTDIR="$pkgdir" ninja install

# provided by -qt subpackage
  rm -r "$pkgdir"/usr/{include/AppStreamQt,lib/cmake,lib/libAppStreamQt.*}

  install -Dm644 "$srcdir"/update-appstream-cache.hook "$pkgdir"/usr/share/libalpm/hooks/update-appstream-cache.hook
}

package_appstream-qt() {
  pkgdesc='Qt5 interface for AppStream'
  depends=(appstream qt5-base)

  cd build
  DESTDIR="$pkgdir" ninja install

# provided by appstream
  rm -r "$pkgdir"{/etc,/usr/{bin,include/appstream,lib/{girepository-1.0,libappstream*,pkgconfig},share}}
}
