# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=vorbis-tools
pkgver=1.4.0
pkgrel=7
pkgdesc='Extra tools for Ogg-Vorbis'
arch=('x86_64')
url='https://www.xiph.org/vorbis/'
license=('GPL2')
depends=('libao' 'libvorbis' 'curl' 'flac')
source=("https://downloads.xiph.org/releases/vorbis/${pkgname}-${pkgver}.tar.gz"
        'vorbis-tools-cve9638-cve9639.patch'
        'vorbis-tools-cve9640.patch'
        'vorbis-tools-CVE-2015-6749.patch')
sha256sums=('a389395baa43f8e5a796c99daf62397e435a7e73531c9f44d9084055a05d22bc'
            '5a6d74de56e67613c4887e3fe9102aadfce4cd4a9a477fb2c1ed1a26f11fdff6'
            '903e159f141dcb7308a7070e58d4434b09e2a8d4f6b76ebf20751eda201127a9'
            '52f1d083c16b9caa2bc7bd5315210bac810dd6a0565e4c270650177a4c815fa7')

prepare() {
  cd ${pkgname}-${pkgver}

  patch -p1 -i "${srcdir}/vorbis-tools-cve9638-cve9639.patch"
  patch -p3 -i "${srcdir}/vorbis-tools-cve9640.patch"
  patch -p1 -i "${srcdir}/vorbis-tools-CVE-2015-6749.patch"
}

build() {
  cd ${pkgname}-${pkgver}

  ./configure \
    --prefix=/usr \
    --without-speex \
    --enable-vcut
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install
}
