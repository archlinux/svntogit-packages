# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>

pkgname=cloog
pkgver=0.16.0
pkgrel=1
pkgdesc="Library that generates loops for scanning polyhedra"
arch=('i686' 'x86_64')
url="http://www.bastoul.net/cloog/"
license=('GPL')
depends=('isl' 'gmp')
conflicts=('cloog-ppl>0.15.10-2')
options=('!libtool')
source=(http://www.bastoul.net/cloog/pages/download/$pkgname-$pkgver.tar.gz)
md5sums=('9292e98031b5a47c6f4ab9c12a249c1d')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --with-isl=system --with-gmp=system
  make
  make check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
