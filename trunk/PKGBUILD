# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kdebugsettings
pkgver=16.04.1
pkgrel=1
pkgdesc='An application to enable/disable qCDebug'
arch=(i686 x86_64)
url='https://utils.kde.org/'
license=(GPL)
depends=(kdbusaddons ki18n kitemviews kcompletion kcoreaddons)
makedepends=(extra-cmake-modules python)
groups=(kde-applications kdeutils)
source=("http://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
sha256sums=('2c3f8599b3d6bed67b2bc7e7636025711acecaaf731566859a2856b06c0d1795')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_LIBEXECDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}

