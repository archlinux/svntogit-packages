# $Id$
# Maintainer: Hugo Doria <hugo@archlinux.org>

pkgname=nmap
pkgver=4.75
pkgrel=1
pkgdesc="A network exploration tool and security/port scanner"
arch=('i686' 'x86_64')
url="http://nmap.org"
license=('custom:GPL')
depends=('pcre' 'openssl' 'libpcap>=0.9.8' 'lua')
# python is needed for zenmap
makedepends=('python')
optdepends=('pygtk: gui for nmap')
options=('!makeflags')
source=(http://download.insecure.org/nmap/dist/${pkgname}-${pkgver}.tgz)
md5sums=('85d160ccaaa848fcf415681e3349d848')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  ./configure --prefix=/usr --mandir=/usr/share/man \
    --libexecdir=/usr/lib
  make || return 1
  make DESTDIR="${pkgdir}" install

  # remove zenmap uninstall script
  rm "${pkgdir}/usr/bin/uninstall_zenmap"

  # install custom GPL2 license
  install -D -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
