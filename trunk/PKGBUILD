# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-smproxy
pkgver=1.0.7
pkgrel=1
pkgdesc="Allows X applications that do not support X11R6 session management to participate in an X11R6 session"
arch=(x86_64)
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libsm' 'libxt' 'libxmu')
makedepends=('xorg-util-macros')
groups=('xorg-apps' 'xorg')
source=(https://xorg.freedesktop.org/archive/individual/app/smproxy-${pkgver}.tar.xz{,.sig})
sha512sums=('cd6a6933b74f76ae65fb16504e57871724bc1102eae1d02e801e99c3fe73fe44e27f87cef3fbf9f75f369d18197b53bb677d637d94cc8185b55acae0cbd44f21'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # Alan Coopersmith <alan.coopersmith@oracle.com>

build() {
  cd smproxy-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd smproxy-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
