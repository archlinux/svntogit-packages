# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Aaron Griffin <aaron@archlinux.org>
# Original TU: Jeff Mickey <j@codemac.net>
# Contributor: ciccio.a

pkgname=squashfs-tools
pkgver=4.5.1
pkgrel=2
pkgdesc="Tools for squashfs, a highly compressed read-only filesystem for Linux"
arch=(x86_64)
url="https://github.com/plougher/squashfs-tools"
license=(GPL2)
depends=(lz4 lzo xz zlib zstd)
makedepends=(help2man)
source=($pkgname-$pkgver.tar.gz::https://github.com/plougher/$pkgname/archive/refs/tags/$pkgver.tar.gz
	gzip-reproducible.patch::https://github.com/plougher/squashfs-tools/commit/6100e82c7e7f18f503c003c67c87791025d5f01b.patch)
sha512sums=('b3934ea1e26c7508110312711465644a6d9674b6b5332a7d011e191fa3c1d4b8be694214794a0f6005263d0f4e18bab96af2f7ed66a178f8e3bb3a781cd44896'
            '867eee58b06235f3c78ff62dbfe7e516ad295f57640123d6da0484b38d28568e6267f9679ebf16d3349531deddbd958cb88c2bec2ff1de2016532757fa54f179')
b2sums=('cbd35daf582d284b021911bb754ed548763f2773e1bc02516ca18bab7034e352780c11eb7dfed1f047b70974dcfb716bb1a1d3729e5b25662203cfbf93553b73'
        'ddeacebebb0cb4d0853d81d5b60886a64fe14fb0b5850bc61b2fb1e6c6d428e4633f85ed6c9507aa4350d32bb947c94d5d75064913a5e6d1e7cadeffa0a5caa7')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ${srcdir}/gzip-reproducible.patch
}

build() {
  local _params=(GZIP_SUPPORT=1 XZ_SUPPORT=1 LZO_SUPPORT=1 LZMA_XZ_SUPPORT=1 LZ4_SUPPORT=1 ZSTD_SUPPORT=1 XATTR_SUPPORT=1)
  make ${_params[@]} -C $pkgname-$pkgver/$pkgname
}

package() {
  make -C $pkgname-$pkgver/$pkgname install INSTALL_PREFIX="$pkgdir/usr"
  install -vDm 644 $pkgname-$pkgver/{ACTIONS-README,CHANGES,USAGE} $pkgname-$pkgver/README-$pkgver -t "$pkgdir/usr/share/doc/$pkgname/"
}
