# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgname=dmapi
pkgver=2.2.12
pkgrel=5
pkgdesc="Data migration API"
arch=('x86_64')
url="http://oss.sgi.com/projects/xfs/"
license=('GPL')
depends=('glibc')
makedepends=('xfsprogs')
provides=('xfsdmapi')
conflicts=('xfsdmapi')
replaces=('xfsdmapi')
#source=("ftp://oss.sgi.com/projects/xfs/previous/cmd_tars/$pkgname-$pkgver.tar.gz" dmapi-headers.patch)
source=(https://sources.archlinux.org/other/packages/dmapi/dmapi-$pkgver.tar.gz dmapi-headers.patch)

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../dmapi-headers.patch # Fix build (Gentoo)
}

build() {
  cd dmapi-$pkgver
  [ "$CARCH" = "x86_64" ] && export CFLAGS="$CLFAGS -fPIC"
  export INSTALL_USER=root
  export INSTALL_GROUP=root
  export DEBUG=-DNDEBUG
  autoconf
  ./configure --prefix=/usr
  make
}

package() {
  cd dmapi-$pkgver
  make DIST_ROOT="$pkgdir" install install-dev
  # the static lib
  install -D -m644 libdm/.libs/libdm.a "$pkgdir"/usr/lib/libdm.a
  # and the header file, please
  install -D -m644 include/dmapi.h "$pkgdir"/usr/include/xfs/dmapi.h
  # kill libexec dir
  rm -rf "$pkgdir"/usr/libexec
}
sha256sums=('b18e34f47374f6adf7c164993c26df36986a009b86aa004ef9444102653aea69'
            'c5935312dd4d77156da023776c93036fa7989ce78ff8b9f6d9421c31a5535647')
