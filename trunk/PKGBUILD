# $Id$
# Maintainer: Dan McGee <dan@archlinux.org>

pkgbase=doxygen
pkgname=('doxygen' 'doxygen-docs')
pkgver=1.8.6
pkgrel=1
license=('GPL')
arch=(i686 x86_64)
url="http://www.doxygen.org/"
makedepends=('gcc-libs' 'flex' 'qt4' 'texlive-core' 'ghostscript'
             'texlive-latexextra' 'graphviz' 'python2')
source=(http://ftp.stack.nl/pub/users/dimitri/${pkgname}-${pkgver}.src.tar.gz
        doxygen-configure-python2.patch)
md5sums=('9385dc52f0627875f8fa758e754ec674'
         'a16ef06cd207292eda73801ea3835f1e')
sha1sums=('204f1b3695c73efac44a521609c5360241d64045'
          '98f9f473f906c5cd363b29c65b54bbe998c0793e')

build() {
  export PATH=$PATH:/usr/lib/qt4/bin
  cd "${srcdir}/${pkgbase}-${pkgver}"
  patch -Np1 < ../doxygen-configure-python2.patch
  QTDIR=/usr/lib/qt4 ./configure --prefix /usr --with-doxywizard
  make
}

package_doxygen() {
  pkgdesc="A documentation system for C++, C, Java, IDL and PHP"
  depends=('gcc-libs')
  optdepends=('graphviz: for caller/callee graph generation'
              'qt4: for doxywizard')

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make INSTALL="${pkgdir}"/usr MAN1DIR=share/man/man1 install
}

package_doxygen-docs() {
  pkgdesc="Developer documentation for doxygen"

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make INSTALL="${pkgdir}"/usr MAN1DIR=share/man/man1 install_docs
}
