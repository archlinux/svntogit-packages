# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=apr
pkgver=1.5.2
pkgrel=1
pkgdesc='The Apache Portable Runtime'
arch=('i686' 'x86_64')
url='http://apr.apache.org/'
depends=('util-linux')
license=('APACHE')
source=(https://www.apache.org/dist/apr/apr-$pkgver.tar.bz2{,.asc})
sha1ums=('5486180ec5a23efb5cae6d4292b300ab'
         'SKIP')
validpgpkeys=('5B5181C2C0AB13E59DA3F7A3EC582EB639FF092C') # Jeff Trawick

build() {
  cd apr-$pkgver
  ./configure --prefix=/usr --includedir=/usr/include/apr-1 \
    --with-installbuilddir=/usr/share/apr-1/build \
    --enable-nonportable-atomics \
    --with-devrandom=/dev/urandom --disable-static
  make
}

check() {
  cd apr-$pkgver
  make -j1 check
}

package() {
  cd apr-$pkgver
  make DESTDIR="$pkgdir" install
}
md5sums=('4e9769f3349fe11fc0a5e1b224c236aa'
         'SKIP')
