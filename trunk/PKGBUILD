# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=calendarsupport
pkgver=16.04.1
pkgrel=1
pkgdesc="Calendar support library"
arch=(i686 x86_64)
url="https://community.kde.org/KDE_PIM"
license=(LGPL)
depends=(kdepim-apps-libs kholidays akonadi-calendar)
makedepends=(extra-cmake-modules kdoctools python boost qt5-tools)
conflicts=('libkdepim<16')
source=("http://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
md5sums=('3af4ed5077aa3e2504ac081d0ea70609')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
