# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=link-grammar
pkgver=5.11.0
pkgrel=1
pkgdesc="A Grammar Checking library"
arch=('x86_64')
url="https://www.abisource.com/projects/link-grammar/"
license=('LGPL')
depends=('hunspell' 'sqlite' 'libedit')
makedepends=('python' 'swig' 'apache-ant' 'java-environment=11')
options=('!makeflags')
source=(https://www.abisource.com/downloads/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz{,.asc})
validpgpkeys=('6407453C98BECC19ADB03D82EB6AA534E0C0651C'
              '8305252160000B5E89843F5464A99A8201045933') # Linas Vepstas <linasvepstas@gmail.com>
sha256sums=('bdb9a359f877ff95d60f44d1780387324fa3763de5084ba1817dbf561a0ebed4'
            'SKIP')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install
}
