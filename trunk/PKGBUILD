# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Troy C < rstrox -ta yahoo -tod com >

pkgbase=python-wrapt
pkgname=(python-wrapt python2-wrapt)
pkgver=1.10.8
pkgrel=2
pkgdesc="A Python module for decorators, wrappers and monkey patching"
arch=("i686" "x86_64")
url="https://pypi.python.org/pypi/wrapt"
license=("BSD")
makedepends=('python' 'python2' 'git')
checkdepends=('python-pytest' 'python2-pytest')
source=("git+https://github.com/GrahamDumpleton/wrapt.git#tag=$pkgver")
sha256sums=('SKIP')

prepare() {
  cp -a wrapt{,-py2}
}

build() {
  cd "$srcdir"/wrapt
  python setup.py build

  cd "$srcdir"/wrapt-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/wrapt
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-3.6:$PYTHONPATH" py.test

  cd "$srcdir"/wrapt-py2
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-2.7:$PYTHONPATH" py.test2
}

package_python-wrapt() {
  depends=('python')

  cd wrapt
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-wrapt() {
  depends=('python2')

  cd wrapt-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
