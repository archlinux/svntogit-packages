# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=gnome-online-accounts
pkgver=3.8.3
pkgrel=1
pkgdesc="GNOME service to access online accounts"
arch=(i686 x86_64)
url="http://www.gnome.org"
license=('GPL')
depends=('webkitgtk3' 'json-glib' 'libnotify' 'rest' 'hicolor-icon-theme' 'libsecret')
makedepends=('intltool' 'libxslt' 'gobject-introspection' 'docbook-xsl')
options=(!libtool)
install=$pkgname.install
source=(http://download.gnome.org/sources/$pkgname/${pkgver:0:3}/$pkgname-$pkgver.tar.xz
        drop-google-password-interface-1.patch
        drop-google-password-interface-2.patch
        drop-google-password-interface-3.patch)
sha256sums=('9e5e55c5097b8d7c32c0dfb7d94448795c627723b133e8edd0b846adde63a44b'
            '33435c10aa1ba3fbb6fd633dc098cd1da50629f1d602521ca83be5379f2ebe48'
            'c837f9be4e5e072a06ea44076e6b9a1519c66c95b16d7a55b1dba105fded317f'
            '18dfad8ed978af1068fd057bbe323006c349ac4ea77c742aa06f1011dc92326e')

prepare() {
  cd $pkgname-$pkgver
  # Backport from Ubuntu
  patch -Np1 -i ../drop-google-password-interface-1.patch
  patch -Np1 -i ../drop-google-password-interface-2.patch
  patch -Np1 -i ../drop-google-password-interface-3.patch
}
 
build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --libexec=/usr/lib/gnome-online-accounts
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
