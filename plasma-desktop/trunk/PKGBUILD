# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=plasma-desktop
pkgver=5.6.4
pkgrel=1
pkgdesc='KDE Plasma Desktop'
arch=('i686' 'x86_64')
url='https://www.kde.org/workspaces/plasmadesktop/'
license=('LGPL')
source=("http://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz" plasma-desktop-gcc6.patch)
depends=('powerdevil' 'polkit-kde-agent' 'libcanberra' 'libxkbfile' 'kmenuedit'
         'systemsettings' 'ksysguard' 'kpeople' 'baloo' 'qt5-graphicaleffects')
makedepends=('extra-cmake-modules' 'kdoctools' 'boost' 'xf86-input-evdev' 'xf86-input-synaptics' 'xorg-server-devel'
             'libibus' 'scim' 'python')
optdepends=('plasma-nm: Network manager applet'
            'ibus: kimpanel IBUS support'
            'scim: kimpanel SCIM support')
conflicts=('kdebase-workspace' 'kcm-touchpad-frameworks' 'kdeplasma-addons<5.5.95' 'kactivities<5.19.0-3')
replaces=('kcm-touchpad-frameworks')
groups=('plasma')
md5sums=('f6083c7f337c721337783cdd03c16ab2'
         '82d1604912c9d846676d8a72d270ac04')

prepare() {
  mkdir -p build

# Fix build with GCC 6
  cd $pkgname-$pkgver
  patch -p1 -i ../plasma-desktop-gcc6.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
