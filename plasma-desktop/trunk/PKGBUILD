# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Antonio Rojas

pkgname=plasma-desktop
pkgver=5.5.0
pkgrel=3
pkgdesc='KDE Plasma Desktop'
arch=('i686' 'x86_64')
url='https://www.kde.org/workspaces/plasmadesktop/'
license=('LGPL')
depends=('powerdevil' 'polkit-kde-agent' 'libcanberra' 'kwin' 'libxkbfile' 'kmenuedit'
         'systemsettings' 'ksysguard' 'kpeople' 'baloo' 'qt5-graphicaleffects')
makedepends=('extra-cmake-modules' 'kdoctools' 'boost' 'xf86-input-evdev' 'xf86-input-synaptics' 'xorg-server-devel')
optdepends=('plasma-nm: Network manager applet')
conflicts=('kdebase-workspace' 'kcm-touchpad-frameworks')
replaces=('kcm-touchpad-frameworks')
install="${pkgname}.install"
groups=('plasma')
source=("http://download.kde.org/stable/plasma/${pkgver}/$pkgname-$pkgver.tar.xz"
fix-favorites-migration.patch::"https://quickgit.kde.org/?p=plasma-desktop.git&a=commitdiff&h=97ced66d&o=plain")
md5sums=('4039d29843c5a5dc71b2f2f787f634d3'
         '07fec581bf676a090c3fd033f482fbfc')

prepare() {
  mkdir -p build

# Fix favorites migration
  cd $pkgname-$pkgver
  patch -p1 -i ../fix-favorites-migration.patch
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DLIBEXEC_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
