# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Antonio Rojas

pkgname=plasma-desktop
pkgver=5.2.1
pkgrel=2
pkgdesc='KDE Plasma Desktop'
arch=('i686' 'x86_64')
url='https://www.kde.org/workspaces/plasmadesktop/'
license=('LGPL')
depends=('qt5-graphicaleffects' 'powerdevil' 'polkit-kde-frameworks'
         'libcanberra' 'kwin' 'libusb-compat' 'libxkbfile' 'kmenuedit'
         'systemsettings' 'ksysguard' 'breeze' 'krunner')
makedepends=('extra-cmake-modules' 'kdoctools' 'boost' 'baloo-frameworks')
optdepends=('baloo-frameworks: system indexing'
           'plasma-nm: Network manager applet')
conflicts=('kdebase-workspace' 'kdebase-kdepasswd')
install="${pkgname}.install"
groups=('plasma')
source=("http://download.kde.org/stable/plasma/${pkgver}/$pkgname-$pkgver.tar.xz"
        336094f43d82d83f374116862a0f568fe9092224.patch)
md5sums=('8a9f8e2914ca6ec653565a7eeb65f91f'
         'eb6dee2f30642ac1c808e227c1b7bea4')

prepare() {
  mkdir build

  cd $pkgname-$pkgver
  patch -p1 -i ../336094f43d82d83f374116862a0f568fe9092224.patch
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DLIBEXEC_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
