# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Allan McRae <mcrae_allan@hotmail.com>
# Contributor: lp76 <l.peduto@gmail.com>

pkgname=gtk-vnc
pkgver=0.5.3
pkgrel=2
pkgdesc="A VNC viewer widget for GTK"
arch=('i686' 'x86_64')
url="https://wiki.gnome.org/Projects/gtk-vnc"
license=('LGPL')
depends=('libgcrypt' 'gtk3')
optdepends=('gtk2: GTK2 widget')
makedepends=('intltool' 'pygtk' 'pygobject2-devel' 'gtk-doc' 'gtk2' 'vala' 'gobject-introspection')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('d490a6c8f40e550d43aef0043ce3bec498ec220d5eb3dc5cdcff8fd561149caf')

build() {
  rm -rf gtk2-build gtk3-build
  cp -a $pkgname-$pkgver gtk2-build
  mv $pkgname-$pkgver gtk3-build

  ( cd gtk2-build
    PYTHON=/usr/bin/python2 ./configure --prefix=/usr \
      --with-python --disable-static --with-gtk=2.0
    make )

  ( cd gtk3-build
    PYTHON=/usr/bin/python2 ./configure --prefix=/usr \
      --with-python --with-examples --disable-static --with-gtk=3.0
    make )
}

package() {
  make -C gtk2-build DESTDIR="${pkgdir}" install
  make -C gtk3-build DESTDIR="${pkgdir}" install
}
