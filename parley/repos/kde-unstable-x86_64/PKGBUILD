# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=parley
pkgver=22.07.80
pkgrel=1
pkgdesc='Vocabulary Trainer'
url='https://apps.kde.org/parley/'
arch=(x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kde-education)
depends=(knewstuff libkeduvocdocument qt5-webengine)
makedepends=(extra-cmake-modules kdoctools)
optdepends=('translate-shell: Google Translate integration')
source=(https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        no-kcmutils.patch)
sha256sums=('664039945544231a2335814ce92b8923025d184d0dcd034736ec0892e4dbc591'
            'SKIP'
            '3c63c609e73676304e0266278d50012a3fbc445379a4f3198faa2bbf08b1b0e9')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
options=(debug)

prepare() {
  patch -d $pkgname-$pkgver -p1 < no-kcmutils.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
