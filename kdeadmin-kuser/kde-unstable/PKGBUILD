# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeadmin-kuser
pkgver=15.11.80
pkgrel=1
pkgdesc='User Manager'
url='http://kde.org/applications/system/kuser/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdeadmin')
depends=('kdebase-runtime' 'kdepimlibs4')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/unstable/applications/${pkgver}/src/kuser-${pkgver}.tar.xz"
	kuser-cmake-3.4.patch::"https://quickgit.kde.org/?p=kuser.git&a=commitdiff&h=0bf52af&o=plain")
sha1sums=('ea9a823fe5678cb7b923cd3480cc8c40877674ab'
          '19f9b3b9bcfe7db167affb5509e5eb09c4eddf7c')

prepare() {
  mkdir -p build

# Fix build with cmake 3.4
  cd kuser-$pkgver
  patch -p1 -i ../kuser-cmake-3.4.patch
}

build() {
  cd build
  cmake ../kuser-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
