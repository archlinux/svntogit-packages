# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=kde-gtk-config
pkgver=5.6.5
pkgrel=1
pkgdesc='GTK2 and GTK3 Configurator for KDE'
arch=('i686' 'x86_64')
url='https://www.kde.org/workspaces/plasmadesktop/'
license=('LGPL')
depends=('knewstuff' 'kde-cli-tools')
makedepends=('extra-cmake-modules' 'gtk2' 'gtk3' 'python')
optdepends=('gtk2: GTK2 apps support'
            'gtk3: GTK3 apps support')
conflicts=('kde-gtk-config-frameworks')
replaces=('kde-gtk-config-frameworks')
groups=('plasma')
source=("http://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('9dc2d144295eb0a1a890f135d39f18d9605daf94aeda0bf7689e47acc22fd31a'
            'SKIP')
validpgpkeys=('13C16D03EDE728514473AA73A506E6D4DD4D5088') # Jonathan Riddell

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_LIBEXECDIR=lib \
    -DSYSCONF_INSTALL_DIR=/etc \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
