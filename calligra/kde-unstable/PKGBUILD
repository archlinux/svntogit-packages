# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgbase=calligra
pkgname=('calligra-filters'
         'calligra-interfaces'
         'calligra-libs'
         'calligra-plugins'
         'calligra-pics'
         'calligra-servicetypes'
         'calligra-templates'
         'calligra-tools'
         'calligra-karbon'
         'calligra-karbon-doc'
         'calligra-kexi'
         'calligra-kexi-doc'
         'calligra-kformula'
         'calligra-kformula-doc'
         'calligra-kounavail'
         'calligra-kplato'
         'calligra-stage'
         'calligra-stage-doc'
         'calligra-krita'
         'calligra-tables'
         'calligra-tables-doc'
         'calligra-words'
         'calligra-handbook'
         'calligra-thesaurus-doc'
         'calligra-braindump'
         'calligra-flow')
pkgver=2.3.71
pkgrel=1
arch=('i686' 'x86_64')
url='http://www.calligra-suite.org'
license=('FDL1.2' 'GPL2' 'LGPL')
makedepends=('kdelibs' 'kdepimlibs' 'eigen' 'lcms' 'postgresql-libs' 'freetds'
             'kdegraphics-okular' 'libgsf' 'libwpd' 'libwpg' 'pstoedit'
             'cmake' 'automoc4' 'boost')
groups=('calligra')
source=("http://download.kde.org/unstable/${pkgbase}-${pkgver}/${pkgbase}-${pkgver}.tar.bz2")
md5sums=('f280add8d1d57cc701dc105fc92d2504')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgbase}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package_calligra-filters() {
  pkgdesc="Filters for the Calligra office suite"
  optdepends=('libgsf: Microsoft Word document importer'
              'libwpd: WordPerfect document importer'
              'libwpg: Corel WordPerfect Graphics image importer'
              'pstoedit: Karbon EPS import filter'
              'kdegraphics-libs: support for raw images')
  conflicts=('koffice-filters')
  replaces=('koffice-filters')
  install=calligra-filters.install

  cd "${srcdir}"/build/filters
  make DESTDIR="${pkgdir}" install
}

package_calligra-interfaces() {
  pkgdesc="Interfaces for the Calligra office suite"
  depends=('calligra-libs')
  conflicts=('koffice-interfaces')
  replaces=('koffice-interfaces')

  cd "${srcdir}"/build/interfaces
  make DESTDIR="${pkgdir}" install
}

package_calligra-libs() {
  pkgdesc="Libraries for the Calligra office suite"
  depends=('kdepimlibs' 'lcms' 'calligra-servicetypes')
  conflicts=('koffice-libs')
  replaces=('koffice-libs')

  cd "${srcdir}"/build/libs
  make DESTDIR="${pkgdir}" install
}

package_calligra-plugins() {
  pkgdesc="Plugins for the Calligra office suite"
  depends=('calligra-libs')
  conflicts=('koffice-plugins' 'koffice-kchart')
  replaces=('koffice-plugins' 'koffice-kchart')

  cd "${srcdir}"/build/plugins
  make DESTDIR="${pkgdir}" install
}

package_calligra-pics() {
  pkgdesc="Icons for the Calligra office suite"
  depends=('calligra-libs')
  conflicts=('koffice-pics')
  replaces=('koffice-pics')
  install=calligra.install

  cd "${srcdir}"/build/pics
  make DESTDIR="${pkgdir}" install
}

package_calligra-servicetypes() {
  pkgdesc="Servicetypes for the Calligra office suite"
  conflicts=('koffice-servicetypes')
  replaces=('koffice-servicetypes')
  install=calligra.install

  cd "${srcdir}"/build/servicetypes
  make DESTDIR="${pkgdir}" install
}

package_calligra-templates() {
  pkgdesc="Templates for the Calligra office suite"
  conflicts=('koffice-templates')
  replaces=('koffice-templates')

  cd "${srcdir}"/build/templates
  make DESTDIR="${pkgdir}" install
}

package_calligra-tools() {
  pkgdesc="Tools for the Calligra office suite"
  depends=('calligra-libs')
  conflicts=('koffice-tools')
  replaces=('koffice-tools')
  install=calligra-tools.install

  cd "${srcdir}"/build/tools
  make DESTDIR="${pkgdir}" install
}

#### package common files done #####

#### package apps and their docs #####

package_calligra-karbon() {
  pkgdesc="Create scalable vector drawings"
  depends=('calligra-libs' 'calligra-templates')
  conflicts=('koffice-karbon')
  replaces=('koffice-karbon')
  install=calligra-karbon.install

  cd "${srcdir}"/build/karbon
  make DESTDIR="${pkgdir}" install
}

package_calligra-karbon-doc() {
  pkgdesc="Documentation for Karbon"
  depends=('calligra-karbon')
  conflicts=('koffice-karbon-doc')
  replaces=('koffice-karbon-doc')

  cd "${srcdir}"/build/doc/karbon
  make DESTDIR="${pkgdir}" install
}

package_calligra-kexi() {
  pkgdesc="Develop desktop database applications"
  depends=('calligra-libs' 'calligra-templates')
  conflicts=('koffice-kexi')
  replaces=('koffice-kexi')
  install=calligra-kexi.install

  cd "${srcdir}"/build/kexi
  make DESTDIR="${pkgdir}" install
}

package_calligra-kexi-doc() {
  pkgdesc="Documentation for Kexi"
  depends=('calligra-kexi')
  conflicts=('koffice-kexi-doc')
  replaces=('koffice-kexi-doc')

  cd "${srcdir}"/build/doc/kexi
  make DESTDIR="${pkgdir}" install
}

package_calligra-kformula() {
  pkgdesc="Formula editor"
  depends=('calligra-libs' 'calligra-templates')
  conflicts=('koffice-kformula')
  replaces=('koffice-kformula')
  install=calligra-kformula.install

  cd "${srcdir}"/build/kformula
  make DESTDIR="${pkgdir}" install
}

package_calligra-kformula-doc() {
  pkgdesc="Documentation for KFormula"
  depends=('calligra-kformula')
  conflicts=('koffice-kformula-doc')
  replaces=('koffice-kformula-doc')

  cd "${srcdir}"/build/doc/kformula
  make DESTDIR="${pkgdir}" install
}

package_calligra-kounavail() {
  pkgdesc="Unavailable Calligra Document"
  depends=('calligra-libs')
  conflicts=('koffice-kounavail')
  replaces=('koffice-kounavail')

  cd "${srcdir}"/build/kounavail
  make DESTDIR="${pkgdir}" install
}

package_calligra-kplato() {
  pkgdesc="Calligra Project Management Component"
  depends=('calligra-words')
  conflicts=('koffice-kplato')
  replaces=('koffice-kplato')
  install=calligra-kplato.install

  cd "${srcdir}"/build/kplato
  make DESTDIR="${pkgdir}" install
}

package_calligra-stage() {
  pkgdesc="Write presentation documents"
  depends=('calligra-libs' 'calligra-templates')
  optdepends=('koffice-filters: import/export filters')
  conflicts=('koffice-kpresenter')
  replaces=('koffice-kpresenter')
  install=calligra-stage.install

  cd "${srcdir}"/build/kpresenter
  make DESTDIR="${pkgdir}" install
}

package_calligra-stage-doc() {
  pkgdesc="Documentation for Stage"
  depends=('calligra-stage')
  conflicts=('koffice-kpresenter-doc')
  replaces=('koffice-kpresenter-doc')

  cd "${srcdir}"/build/doc/stage
  make DESTDIR="${pkgdir}" install
}

package_calligra-krita() {
  pkgdesc="Edit and paint images"
  depends=('calligra-libs' 'calligra-templates' 'calligra-plugins' 'glew'
           'qimageblitz' 'poppler-qt' 'openexr' 'opengtl')
  optdepends=('koffice-filters: import/export filters')
  conflicts=('koffice-krita')
  replaces=('koffice-krita')
  install=calligra-krita.install

  cd "${srcdir}"/build/krita
  make DESTDIR="${pkgdir}" install
}

package_calligra-tables() {
  pkgdesc="Write spreadsheet documents"
  depends=('calligra-libs' 'calligra-templates' 'calligra-plugins' 'gsl')
  optdepends=('koffice-filters: import/export filters')
  conflicts=('koffice-kspread')
  replaces=('koffice-kspread')
  install=calligra.install

  cd "${srcdir}"/build/tables
  make DESTDIR="${pkgdir}" install
}

package_calligra-tables-doc() {
  pkgdesc="Documentation for Tables"
  depends=('calligra-tables')
  conflicts=('koffice-kspread-doc')
  replaces=('koffice-kspread-doc')

  cd "${srcdir}"/build/doc/tables
  make DESTDIR="${pkgdir}" install
}

package_calligra-words() {
  pkgdesc="Word Processor"
  depends=('calligra-libs' 'calligra-templates' 'calligra-plugins' 'calligra-pics'
           'calligra-kounavail')
  optdepends=('koffice-filters: import/export filters')
  conflicts=('koffice-kword')
  replaces=('koffice-kword')
  install=calligra-words.install

  cd "${srcdir}"/build/words
  make DESTDIR="${pkgdir}" install
}

package_calligra-handbook() {
  pkgdesc="Documentation for Calligra"
  conflicts=('koffice-handbook')
  replaces=('koffice-handbook')

  cd "${srcdir}"/build/doc/calligra
  make DESTDIR="${pkgdir}" install
}

package_calligra-thesaurus-doc() {
  pkgdesc="Documentation for Thesaurus"
  conflicts=('koffice-thesaurus-doc')
  replaces=('koffice-thesaurus-doc')

  cd "${srcdir}"/build/doc/thesaurus
  make DESTDIR="${pkgdir}" install
}

package_calligra-braindump() {
  pkgdesc="Notes and idea gathering"
  install=calligra-braindump.install

  cd "${srcdir}"/build/braindump
  make DESTDIR="${pkgdir}" install
}

package_calligra-flow() {
  pkgdesc="Flowchart & Diagram Editing"
  install=calligra-flow.install

  cd "${srcdir}"/build/flow
  make DESTDIR="${pkgdir}" install
}

package_calligra-flow-doc() {
  pkgdesc="Documentation for Flow"
  depends=('calligra-flow')

  cd "${srcdir}"/build/doc/flow
  make DESTDIR="${pkgdir}" install
}

##### package apps done #####
