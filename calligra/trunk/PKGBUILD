# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Ronald van Haren <ronald.archlinux.org>

pkgname=calligra
pkgdesc="A set of applications for productivity and creative usage"
pkgver=3.0.0.1
pkgrel=5
arch=('i686' 'x86_64')
url='http://www.calligra-suite.org/'
license=('FDL1.2' 'GPL2' 'LGPL')
depends=('kcmutils' 'knotifyconfig' 'kross' 'ktexteditor' 'threadweaver' 'kactivities' 'kdiagram' 'kreport' 'libspnav'
         'gsl' 'openexr' 'kcontacts' 'kcalcore' 'qca-qt5' 'poppler-qt5' 'libodfgen' 'qt5-webkit' 'cauchy')
makedepends=('extra-cmake-modules' 'kdoctools' 'python' 'kdesignerplugin' 'khtml' 'boost' 'openjpeg'
             'libwpg' 'okular' 'eigen' 'marble-common' 'pstoedit' 'libvisio' 'libetonyek' 'libodfgen' 'libwps' 'vc')
optdepends=('libwpg: Corel WordPerfect Graphics image importer'
            'libwps: Microsoft Works file word processor format import'
            'libvisio: Microsoft Visio import filter'
            'libetonyek: Apple Keynote import filter'
            'pstoedit: EPS to SVG filter'
            'poppler: PDF to SVG filter'
            'khtml: HTML to ODS filter'
            'openjpeg: PDF to SVG filer')
conflicts=('calligra-devtools' 'calligra-extras' 'calligra-filters' 'calligra-karbon' 'karbon' 'calligra-libs' 'calligra-plan'
          'calligra-plugins' 'calligra-sheets' 'calligra-stage' 'calligra-words' 'calligra-l10n' 'calligra-meta'
          'calligra-flow' 'calligra-braindump' 'calligra-gemini' 'calligra-handbook')
replaces=('calligra-devtools' 'calligra-extras' 'calligra-filters' 'calligra-karbon' 'karbon' 'calligra-libs' 'calligra-plan'
          'calligra-plugins' 'calligra-sheets' 'calligra-stage' 'calligra-words' 'calligra-l10n' 'calligra-meta')
for _lang in bs ca ca@valencia cs da de el en_gb es et fi fr gl hu it ja kk nb nl \
             pl pt pt_br ru sk sv uk zh_cn zh_tw; do
  conflicts+=(calligra-l10n-$_lang)
  replaces+=(calligra-l10n-$_lang)
done
source=("http://download.kde.org/stable/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('dedc51efc42f7dda37514d450cb772d9db37a658e0abbcf0f513712a04c1f011'
            'SKIP')
validpgpkeys=(05D00A8B73A686789E0A156858B9596C722EA3BD) # Boudewijn Rempt <foundation@krita.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgbase}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
