# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kanagram
pkgver=16.04.2
pkgrel=1
pkgdesc="Letter Order Game"
url="http://kde.org/applications/education/kanagram/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdeedu')
depends=('libkeduvocdocument' 'kdeclarative' 'knewstuff')
makedepends=('extra-cmake-modules' 'kdoctools' 'python')
replaces=('kdeedu-kanagram')
conflicts=('kdeedu-kanagram')
source=("http://download.kde.org/stable/applications/${pkgver}/src/kanagram-${pkgver}.tar.xz")
sha1sums=('300eff6f026c1e392a61bab3fc581b92aabc0de6')

build() {
  cd "${srcdir}"
  mkdir -p build
  cd build
  cmake ../kanagram-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DSYSCONF_INSTALL_DIR=/etc
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
