# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgbase=marble
pkgname=(marble libastro marble-data)
pkgver=16.08.2
pkgrel=1
pkgdesc="Desktop Globe"
arch=(i686 x86_64)
url="https://www.kde.org/applications/system/marble"
license=(GPL)
makedepends=(extra-cmake-modules gpsd quazip libwlocate phonon-qt5 qt5-webkit krunner
             kparts knewstuff qt5-tools kdoctools qt5-serialport)
source=("http://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
md5sums=('c8bd34ca0f43b704a7cc890444c8e03b')

prepare() {
  mkdir -p build

  # only build the KDE app
  sed -e '/mobile/d' \
      -e '/qt/d'  \
      -e '/Quick/d' \
      -e '/touch/d' \
      -i $pkgname-$pkgver/src/apps/CMakeLists.txt
}

build() {
  cd build
  cmake ../$pkgbase-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_SYSCONFDIR=/etc \
    -DQT_PLUGINS_DIR=lib/qt/plugins \
    -DBUILD_TESTING=OFF \
    -DBUILD_MARBLE_EXAMPLES=OFF \
    -DBUILD_MARBLE_TESTS=OFF \
    -DMOBILE=OFF
  make
}

package_libastro() {
  pkgdesc='Marble astronomy library'
  depends=(gcc-libs)
  conflicts=('kdeedu-marble<15.07' marble-qt)

  cd build/src/lib/astro
  make DESTDIR="$pkgdir" install
}

package_marble() {
  depends=(libastro marble-data qt5-webkit kparts knewstuff quazip)
  optdepends=('gpsd: GPS based geolocation'
            'qt5-serialport: APRS plugin'
            'libwlocate: WLAN based geolocation'
            'krunner: Krunner plugin')
  conflicts=('kdeedu-marble<15.04.3-3' marble-qt)
  replaces=(kdeedu-marble 'marble-qt<15.07')
  groups=(kde-applications kdeedu)

  cd build
  make DESTDIR="$pkgdir" install

# provided by libastro
  rm -r "$pkgdir"/usr/include/astro
  rm -r "$pkgdir"/usr/lib/{libastro.*,cmake/Astro}

# provided by marble-data
  rm -r "$pkgdir"/usr/share/{icons,marble/data,mime}
}

package_marble-data() {
  pkgdesc='Data for Marble'
  depends=(hicolor-icon-theme)
  conflicts=('kdeedu-marble<15.07' 'marble<15.07.80-3' marble-qt)

  cd build/data
  make DESTDIR="$pkgdir" install
}
