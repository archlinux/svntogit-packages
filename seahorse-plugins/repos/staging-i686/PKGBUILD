# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=seahorse-plugins
pkgver=2.30.1
pkgrel=3
pkgdesc="Seahorse plugins"
arch=(i686 x86_64)
license=('GPL')
depends=('seahorse>=2.30.1' 'desktop-file-utils')
makedepends=('intltool' 'gnome-doc-utils>=0.20.0' 'pkgconfig' 'gedit>=2.30.2' 'nautilus>=2.30.1' 'gnome-panel-bonobo>=2.32.0')
optdepends=('gnome-panel-bonobo: applet support')
groups=('gnome-extra')
url="http://www.gnome.org"
options=('!libtool' '!emptydirs')
install=seahorse-plugins.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.30/${pkgname}-${pkgver}.tar.bz2
        gpgme-init.patch
        seahorse-plugins-2.30.1-libnotify-0.7.patch)
sha256sums=('1fdfed1aef2c22a29a9abc00eae67752854e466a66cee2a8c1c278277e832d66'
            '418d67892b348d02885e968c5299416baf545b12cf8f58b23579f79e87776ab2'
            '9da8c5552217a56f4b7d12e28a425b3980b20953d8312c3017378e5a71536905')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/gpgme-init.patch"
  patch -Np0 -i "${srcdir}/seahorse-plugins-2.30.1-libnotify-0.7.patch"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-scrollkeeper \
      --disable-update-mime-database \
      --disable-static
  make
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain seahorse-plugins ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
