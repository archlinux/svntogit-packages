# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Alan Berndt <alan@eatabrick.org>

pkgname=libgfshare
pkgver=2.0.0
pkgrel=2
pkgdesc='Utilities for multi-way secret-sharing'
url='https://www.digital-scurf.org/software/libgfshare'
arch=('x86_64')
license=('MIT')
depends=('glibc')
provides=('libgfshare.so')
source=(https://www.digital-scurf.org/files/libgfshare/libgfshare-${pkgver}.tar.bz2)
sha512sums=('d6fd60a743825df85cb429b1dee583ab0a6d998fd5c79ee45b3da4c83a55970aee896337514c3686abfd3ff63099232e0f4af4203c32fa042500bfa8bc8d3495')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make -k check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 COPYRIGHT -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README doc/theory.tex -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
