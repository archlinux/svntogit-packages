# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Kevin Piche <kevin@archlinux.org>
# Contributor: Manolis Tzanidakis

pkgname=perl-timedate
pkgver=2.32
pkgrel=1
pkgdesc="Date formating subroutines"
arch=('any')
license=('PerlArtistic')
url="https://search.cpan.org/dist/TimeDate/"
depends=('perl')
replaces=('timedate')
provides=('timedate')
options=('!emptydirs')
source=("https://www.cpan.org/authors/id/A/AT/ATOOMIC/TimeDate-$pkgver.tar.gz")
sha512sums=('7c451c3a69b32db1ec8ec21462eef2ac5e3a63f8eb6521260362a396f39c0c0f93efaf6f2903ade82175b391f09b7d6f6121b68fbf9f5dde421a8b5dd65476bf')

build() {
  cd TimeDate-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd TimeDate-$pkgver
  make test
}

package() {
  cd TimeDate-$pkgver
  make install DESTDIR="$pkgdir"
}
