# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: Lukas Sabota <punkrockguy318@cocmast.net> (Timidity Patch)

pkgname=sdl_mixer
pkgver=1.2.12
pkgrel=3
pkgdesc="A simple multi-channel audio mixer"
arch=('i686' 'x86_64')
url="http://www.libsdl.org/projects/SDL_mixer/"
license=('custom')
depends=('sdl>=1.2.12' 'libvorbis' 'libmikmod' 'smpeg')
makedepends=('fluidsynth')
optdepends=('fluidsynth: MIDI software synth, replaces built-in timidity')
options=('!libtool')
source=(http://www.libsdl.org/projects/SDL_mixer/release/SDL_mixer-$pkgver.tar.gz
        mikmod1.patch mikmod2.patch fluidsynth-volume.patch double-free-crash.patch)
md5sums=('e03ff73d77a55e3572ad0217131dc4a1'
         '95a6d58686fe3017d58c39e3e1dd40f7'
         'd823994c3961f2ff48644478f27a27d1'
         'eed8b61defde36ac2077ef96d868ea3e'
         '4b4835c63297f016a198cacb7dd68ec1')

build() {
  cd "$srcdir/SDL_mixer-$pkgver"

  patch -Np1 -i ../mikmod1.patch
  patch -Np1 -i ../mikmod2.patch
  patch -Np1 -i ../fluidsynth-volume.patch
  patch -Np1 -i ../double-free-crash.patch

  sed -e "/CONFIG_FILE_ETC/s|/etc/timidity.cfg|/etc/timidity++/timidity.cfg|" \
      -e "/DEFAULT_PATH/s|/etc/timidity|/etc/timidity++|" \
      -e "/DEFAULT_PATH2/s|/usr/local/lib/timidity|/usr/lib/timidity|" \
      -i timidity/config.h

  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd "$srcdir/SDL_mixer-$pkgver"
  make DESTDIR="$pkgdir" install

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
