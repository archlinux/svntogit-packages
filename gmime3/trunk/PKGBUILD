# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Ben <ben@benmazer.net>

pkgname=gmime3
pkgver=3.2.0
pkgrel=2
pkgdesc="A C/C++ MIME creation and parser library with support for S/MIME, PGP, and Unix mbox spools"
arch=(x86_64)
license=(GPL)
url="https://github.com/jstedfast/gmime"
depends=(glib2 gpgme zlib)
makedepends=(gobject-introspection gtk-doc git vala docbook-utils)
_commit=7bae7f1dddf5e017548841a9803f8b0f505caa9f  # tags/3.2.0^0
source=("git+https://github.com/jstedfast/gmime#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd gmime
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd gmime
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd gmime

  ./configure --prefix=/usr \
    --program-prefix=$pkgname \
    --enable-gtk-doc \
    --enable-smime \
    --disable-static

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd gmime
  make DESTDIR="$pkgdir" install
}
