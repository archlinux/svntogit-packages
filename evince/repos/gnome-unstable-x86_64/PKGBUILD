# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=evince
pkgver=3.28.0
pkgrel=1
pkgdesc="Document viewer (PDF, Postscript, djvu, tiff, dvi, XPS, SyncTex support with gedit, comics books (cbr,cbz,cb7 and cbt))"
url="https://wiki.gnome.org/Apps/Evince"
arch=(x86_64)
license=(GPL)
depends=(gtk3 libgxps libspectre gsfonts poppler-glib djvulibre t1lib dconf
         libsecret gsettings-desktop-schemas gnome-desktop libarchive
         gst-plugins-base-libs)
makedepends=(itstool libnautilus-extension texlive-bin gobject-introspection
             intltool docbook-xsl python gtk-doc git gnome-common)
optdepends=('texlive-bin: DVI support'
	    'gvfs: bookmark support and session saving')
groups=(gnome)
options=('!emptydirs')
_commit=db31945d903a747e258eede6b9d2282167fb4e39  # tags/3.28.0^0
source=("git+https://git.gnome.org/browse/evince#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}
  

build() {
  cd $pkgname

  BROWSER_PLUGIN_DIR=/usr/lib/epiphany/plugins \
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib/$pkgname \
    --disable-static \
    --enable-compile-warnings=minimum \
    --enable-introspection \
    --enable-nautilus \
    --enable-pdf \
    --enable-tiff \
    --enable-djvu \
    --enable-dvi \
    --enable-t1lib \
    --enable-comics \
    --enable-gtk-doc \
    --enable-multimedia \
    --disable-schemas-compile

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
