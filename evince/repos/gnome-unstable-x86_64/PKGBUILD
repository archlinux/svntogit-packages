# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=evince
pkgver=2.30.0
pkgrel=1
pkgdesc="Simply a document viewer"
url="http://www.gnome.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libspectre>=0.2.3' 'gsfonts' 'poppler-glib>=0.12.4' 'libdjvu>=3.5.22' 'gnome-icon-theme>=2.29.3' 't1lib' 'libgnome-keyring>=2.29.4' 'desktop-file-utils')
makedepends=('gnome-doc-utils>=0.20.0' 'nautilus>=2.30.0' 'pkgconfig' 'texlive-bin' 'intltool' 'gobject-introspection')
optdepends=('texlive-bin: DVI support')
replaces=('gpdf')
groups=('gnome-extra')
install=evince.install
options=('!libtool' '!emptydirs')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.30/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('36dfee4e973421f15cb51c9b1c84784523592dc81feb6465fb59f87b8f7f8bf7')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --libexecdir=/usr/lib/evince \
      --disable-static --enable-nautilus \
      --enable-pdf --enable-tiff \
      --enable-djvu --enable-dvi \
      --enable-t1lib --enable-pixbuf \
      --enable-comics --enable-impress \
      --enable-introspection \
      --disable-scrollkeeper || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain evince ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
