# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=evince
pkgver=3.14.0
pkgrel=1
pkgdesc="Document viewer (PDF, Postscript, djvu, tiff, dvi, XPS, SyncTex support with gedit, comics books (cbr,cbz,cb7 and cbt))"
url="https://wiki.gnome.org/Apps/Evince"
arch=(i686 x86_64)
license=(GPL)
depends=(gtk3 libgxps libspectre gsfonts poppler-glib djvulibre t1lib libsecret desktop-file-utils dconf gsettings-desktop-schemas gnome-desktop)
makedepends=(itstool libnautilus-extension texlive-bin intltool gobject-introspection docbook-xsl python python2)
optdepends=('texlive-bin: DVI support'
	    'gvfs: bookmark support and session saving'
            'p7zip: cbz and cb7 compressed comic books'
            'tar: cbt compressed comic books'
            'unrar: cbr compressed comic books')
groups=(gnome)
install=evince.install
options=('!emptydirs')
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('f4a4b0e393b1fdb097bbf8ff69c658ff1a9eac5798d62ca3c9f52e5ca553de93')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr   --sysconfdir=/etc \
      --localstatedir=/var    --libexecdir=/usr/lib/evince \
      --disable-static        --enable-nautilus \
      --enable-pdf            --enable-tiff \
      --enable-djvu           --enable-dvi \
      --enable-t1lib          --enable-comics \
      --disable-scrollkeeper  --disable-schemas-compile \
      --enable-introspection
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
