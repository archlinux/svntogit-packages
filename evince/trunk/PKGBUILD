# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=evince
pkgver=2.24.0
pkgrel=1
pkgdesc="Simply a document viewer"
url="http://www.gnome.org"
arch=(i686 x86_64)
license=('GPL')
depends=('libspectre>=0.2.0' 'gsfonts' 'poppler-glib>=0.8.6' 'libdjvu>=3.5.20' 'gnome-icon-theme>=2.24.0' 't1lib' 'libglade>=2.6.3' 'gnome-keyring>=2.24.0' 'shared-mime-info' 'desktop-file-utils')
makedepends=('perlxml' 'gnome-doc-utils>=0.14.0' 'nautilus>=2.24.0' 'pkgconfig' 'texlive-bin')
optdepends=('texlive-bin: DVI support')
install=evince.install
options=('!libtool' '!emptydirs')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.24/${pkgname}-${pkgver}.tar.bz2)
replaces=('gpdf')
groups=('gnome-extra')
md5sums=('7d0f9850b0f33267d3977532f027ac95')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var --disable-static \
	      --with-print=gtk --enable-nautilus \
	      --enable-pdf --enable-tiff \
	      --enable-djvu --enable-dvi \
	      --enable-t1lib --enable-pixbuf \
	      --enable-comics --enable-impress \
	      --disable-scrollkeeper --without-libgnome || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1
  
  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
