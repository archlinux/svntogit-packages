# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=knetwalk
pkgver=16.04.1
pkgrel=1
pkgdesc="Connect all the terminals to the server, in as few turns as possible"
url="http://kde.org/applications/games/knetwalk/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegames')
depends=('libkdegames' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'python' 'kdoctools')
conflicts=('kdegames-knetwalk')
replaces=('kdegames-knetwalk')
source=("http://download.kde.org/stable/applications/${pkgver}/src/knetwalk-${pkgver}.tar.xz")
sha1sums=('405368708d925eedb6446c04a68f1a1d71984610')

build() {
  mkdir -p build
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
