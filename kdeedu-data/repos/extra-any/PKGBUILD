# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-data
pkgver=16.12.2
pkgrel=1
pkgdesc="Common data for KDE Edu applications"
url="http://kde.org/applications/education"
arch=('any')
license=('GPL' 'LGPL' 'FDL')
depends=('hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'qt5-base')
conflicts=('libkdeedu<14')
source=("http://download.kde.org/stable/applications/${pkgver}/src/$pkgname-${pkgver}.tar.xz"{,.sig})
sha1sums=('4275f2ab5d972121bd14c3f1e36bb4c913346749'
          'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
