# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=iproute2
pkgver=2.6.35
pkgrel=2
pkgdesc="IP Routing Utilities"
arch=('i686' 'x86_64')
license=('GPL2')
url="http://www.linux-foundation.org/en/Net:Iproute2"
depends=('linux-atm' 'perl')
provides=('iproute')
conflicts=('iproute')
replaces=('iproute')
options=('!makeflags')
backup=('etc/iproute2/ematch_map' 'etc/iproute2/rt_dsfield' 'etc/iproute2/rt_protos' \
	'etc/iproute2/rt_realms' 'etc/iproute2/rt_scopes' 'etc/iproute2/rt_tables')
source=(http://devresources.linux-foundation.org/dev/iproute2/download/iproute2-${pkgver}.tar.bz2
	'iproute2-fhs.patch' 'iproutegetfix.patch')
sha1sums=('862b86ac73ea696867675765d119654553a22aeb'
          '2416b11252364d7a6c742eabb4a6924a75637a46'
          '5d8aa3c01642ef3d7893ae03457c6891e3c14f1a')

build() {
  cd $srcdir/iproute2-${pkgver}
  
  # set correct fhs structure
  patch -Np1 -i ${srcdir}/iproute2-fhs.patch

  # fix ip route get breakage
  patch -Np1 -i ${srcdir}/iproutegetfix.patch

  ./configure

  make
}

package() {
  cd $srcdir/iproute2-${pkgver}

  make DESTDIR=$pkgdir install

  # libnetlink isn't installed, install it FS#19385
  install -Dm644 include/libnetlink.h ${pkgdir}/usr/include/libnetlink.h
  install -Dm644 lib/libnetlink.a ${pkgdir}/usr/lib/libnetlink.a
}
