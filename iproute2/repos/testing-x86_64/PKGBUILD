# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=iproute2
pkgver=3.17.0
pkgrel=1
pkgdesc="IP Routing Utilities"
arch=('i686' 'x86_64')
license=('GPL2')
url="http://www.linuxfoundation.org/collaborate/workgroups/networking/iproute2"
depends=('glibc' 'iptables')
makedepends=('linux-atm' 'git')
optdepends=('linux-atm: ATM support')
groups=('base')
provides=('iproute')
conflicts=('iproute')
replaces=('iproute')
options=('staticlibs' '!makeflags')
backup=('etc/iproute2/ematch_map' 'etc/iproute2/rt_dsfield' 'etc/iproute2/rt_protos' \
	'etc/iproute2/rt_realms' 'etc/iproute2/rt_scopes' 'etc/iproute2/rt_tables')
#source=(http://www.kernel.org/pub/linux/utils/net/$pkgname/$pkgname-$pkgver.tar.xz
source=(git://git.kernel.org/pub/scm/linux/kernel/git/shemminger/iproute2.git#tag=v$pkgver
        iproute2-fhs.patch
        unwanted-link-help.patch)
sha1sums=('SKIP'
          '35b8cf2dc94b73eccad427235c07596146cd6f6c'
          '3b1335f4025f657f388fbf4e5a740871e3129c2a')

prepare() {
  cd "$srcdir/$pkgname"

  # set correct fhs structure
  patch -Np1 -i "$srcdir/iproute2-fhs.patch"

  # allow operations on links called "h", "he", "hel", "help"
  patch -Np1 -i "$srcdir/unwanted-link-help.patch"

  # do not treat warnings as errors
  sed -i 's/-Werror//' Makefile
}

build() {
  cd "$srcdir/$pkgname"

  ./configure
  make
}

package() {
  cd "$srcdir/$pkgname"

  make DESTDIR="$pkgdir" install

  # libnetlink isn't installed, install it FS#19385
  install -Dm644 include/libnetlink.h "$pkgdir/usr/include/libnetlink.h"
  install -Dm644 lib/libnetlink.a "$pkgdir/usr/lib/libnetlink.a"

  # usrmove
  cd "$pkgdir"
  mv usr/sbin usr/bin
}
