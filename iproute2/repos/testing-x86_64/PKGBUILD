# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=iproute2
pkgver=2.6.29
pkgrel=1
pkgdesc="IP Routing Utilities"
arch=('i686' 'x86_64')
license=('GPL2')
url="http://www.linux-foundation.org/en/Net:Iproute2"
depends=('linux-atm' 'perl')

provides=('iproute')
conflicts=('iproute')
replaces=('iproute')

options=('!makeflags')
backup=('etc/iproute2/ematch_map' 'etc/iproute2/rt_dsfield' 'etc/iproute2/rt_protos' \
	'etc/iproute2/rt_realms' 'etc/iproute2/rt_scopes' 'etc/iproute2/rt_tables')

source=(http://devresources.linux-foundation.org/dev/iproute2/download/iproute2-${pkgver}.tar.bz2
	'iftunnel.patch')
md5sums=('a3ecfaa091289656fae786b027b44eda'
         '8b3dab19a03d8d521c690be7bb7b5c2e')

build() {
  cd $srcdir/iproute2-${pkgver}
  
  sed -i 's|/usr/local/lib/iptables|/usr/lib/iptables|' include/iptables.h || return 1
  sed -i 's|=/share|=/usr/share|' Makefile || return 1
  sed -i 's|=/sbin|=/usr/sbin|' Makefile || return 1

  # fix missing #include
  patch -Np0 -R -i ${srcdir}/iftunnel.patch || return 1

  ./configure || return 1
  make || return 1
  make DESTDIR=$pkgdir install || return 1

#  chmod 755 $pkgdir/usr/sbin/ifcfg || return 1

}
