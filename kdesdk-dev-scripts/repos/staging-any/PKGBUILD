# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdesdk-dev-scripts
pkgver=15.04.0
pkgrel=1
pkgdesc='Scripts and setting files useful during development of KDE software'
url='https://projects.kde.org/projects/kde/kdesdk/kde-dev-scripts'
arch=('any')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdesdk')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
source=("http://download.kde.org/stable/applications/${pkgver}/src/kde-dev-scripts-${pkgver}.tar.xz")
sha1sums=('e32b495c82e4377fe2e32e98aee5eec6c3cbe21f')

build() {
  mkdir build
  cd build
  cmake ../kde-dev-scripts-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install

  # Fix python 2 path
  sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|' \
    "${pkgdir}"/usr/bin/{zonetab2pot,kde-systemsettings-tree}.py
  sed -i 's|#! /usr/bin/env python|#!/usr/bin/env python2|' \
    "${pkgdir}"/usr/bin/{kdelnk2desktop.py,kde_generate_export_header}
}
