# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-kajongg
pkgver=4.13.97
pkgrel=1
pkgdesc="The ancient Chinese board game for 4 players"
url="http://kde.org/applications/games/kajongg/"
arch=('any')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'twisted' 'kdebindings-python2' 'libkmahjongg')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/unstable/${pkgver}/src/kajongg-${pkgver}.tar.xz")
sha1sums=('0dc761863f3b436d165d2e6165262c5848bb06fb')

prepare() {
  mkdir build

  cd kajongg-${pkgver}
  sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|' \
    src/{deferredutil,kajongg,kajonggserver,server}.py
}

build() {
  cd build
  cmake ../kajongg-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DPYTHON_EXECUTABLE=/usr/bin/python2
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
