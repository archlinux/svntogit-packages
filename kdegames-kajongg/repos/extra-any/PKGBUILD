# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-kajongg
pkgver=16.12.1
pkgrel=1
pkgdesc="The ancient Chinese board game for 4 players"
url="http://kde.org/applications/games/kajongg/"
arch=(any)
license=(GPL LGPL FDL)
groups=(kde-applications kdegames)
depends=(kdebase-runtime python2-twisted python2-pyqt4 libkmahjongg)
makedepends=()
optdepends=('vorbis-tools: for playing sounds')
source=("http://download.kde.org/stable/applications/$pkgver/src/kajongg-$pkgver.tar.xz"{,.sig})
sha1sums=('dac51b69c65f9dcd2cba5576c7f7857b0c59cd1f'
          'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  cd kajongg-$pkgver
# Fix shebang
  find . -type f | xargs sed -e 's|\/usr\/bin\/env python|/usr/bin/env python2|' -i   
}

build() {
  cd kajongg-$pkgver
  python2 setup.py build
}

package() {
  cd kajongg-$pkgver
  python2 setup.py install --root="$pkgdir"
}
