# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=libkipi4
_pkgname=libkipi
pkgver=15.08.3
pkgrel=2
pkgdesc="An interface to use kipi-plugins from a KDE application"
url="https://projects.kde.org/projects/kde/kdegraphics/libs/libkipi"
arch=(i686 x86_64)
license=(GPL LGPL FDL)
depends=(kdelibs)
makedepends=(cmake automoc4)
replaces=('libkipi<15.11')
conflicts=('libkipi<15.11')
install=$pkgname.install
source=("http://download.kde.org/stable/applications/$pkgver/src/$_pkgname-$pkgver.tar.xz")
sha1sums=('e8d260f58d3287eec629fc8237f411831190f475')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$_pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

# Remove icons, provided with KF5 libkipi
  rm -r "$pkgdir"/usr/share/icons
}
