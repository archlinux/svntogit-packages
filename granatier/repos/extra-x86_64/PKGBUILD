# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=granatier
pkgver=15.04.2
pkgrel=1
pkgdesc="A clone of the classic Bomberman game"
url="http://kde.org/applications/games/granatier/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegames')
depends=('libkdegames' 'knotifyconfig')
makedepends=('extra-cmake-modules' 'python' 'kdoctools')
install=${pkgname}.install
conflicts=('kdegames-granatier')
replaces=('kdegames-granatier')
source=("http://download.kde.org/stable/applications/${pkgver}/src/granatier-${pkgver}.tar.xz")
sha1sums=('b7ca4c2e140ac42525a0a7ef88ec7624faa7fa64')

build() {
  mkdir build
  cd build
  cmake ../granatier-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
