# $Id$
# Maintainer:
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=fontforge
pkgver=20100501
pkgrel=1
pkgdesc="An outline and bitmap font editor"
arch=('i686' 'x86_64')
url="http://fontforge.sourceforge.net"
license=('BSD')
depends=('libxkbui' 'libxi' 'libxml2' 'pango' 'giflib' 'libtiff' 'python')
options=('!libtool' '!makeflags')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}_full-${pkgver}.tar.bz2")
md5sums=('5f3d20d645ec1aa2b7b4876386df8717')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
    --mandir=/usr/share/man \
    --enable-type3 \
    --enable-double \
    --enable-devicetables \
    --with-regular-link \
    --with-python \
    --enable-pyextension || return 1
  make || return 1
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install || return 1
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE" || return 1
}
