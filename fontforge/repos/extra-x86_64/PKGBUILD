# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=fontforge
pkgver=20190317
pkgrel=1
pkgdesc='Outline and bitmap font editor'
url='https://fontforge.github.io/'
arch=('x86_64')
license=('BSD')
makedepends=('git')
depends=('libtool' 'pango' 'giflib' 'libtiff' 'libxml2' 'libspiro' 'python'
         'potrace' 'woff2' 'gtk3')
source=("https://github.com/fontforge/fontforge/archive/${pkgver}.tar.gz"
        'https://fontforge.github.io/cidmaps.tgz'
	'git+https://github.com/coreutils/gnulib.git')
sha256sums=('0f2dd80ea99f9ba432ceba94fe81227e23b9c1489a17dce6fde98458b4d529d5'
            '1bf9c7eb8835e6ed94e62cb49f1141bc046c562849e52e6c3c7f1d7cfc95c7b3'
            'SKIP')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	ln -s ../gnulib .
	./bootstrap --force
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure \
		--prefix=/usr \
		--mandir=/usr/share/man \
		--enable-python-extension \
		--enable-woff2 \
		--enable-gdk \

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	install -d "${pkgdir}/usr/share/fontforge"
	install -m644 ../Adobe-* "${pkgdir}/usr/share/fontforge"
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
