# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=four-in-a-row
pkgver=3.28.0+3+g52208eb
pkgrel=1
pkgdesc="Make lines of the same color to win"
url="https://wiki.gnome.org/Apps/Four-in-a-row"
arch=(x86_64)
license=(GPL3)
depends=(gtk3 libcanberra librsvg)
makedepends=(itstool gobject-introspection yelp-tools appstream-glib vala git)
groups=(gnome-extra)
_commit=52208eb9835edc5c595ef024a09f648bcee23475  # master
source=("git+https://git.gnome.org/browse/four-in-a-row#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --disable-schemas-compile
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
