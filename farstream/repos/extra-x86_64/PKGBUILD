# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=farstream
pkgver=0.2.4
pkgrel=1
pkgdesc="Farstream (formerly Farsight) - Audio/Video Communications Framework"
arch=('i686' 'x86_64')
url="http://www.freedesktop.org/wiki/Software/Farstream"
license=('LGPL')
depends=('gst-plugins-base-libs' 'libnice')
makedepends=('gobject-introspection')
optdepends=('gst-plugins-good' 'gst-plugins-bad')
conflicts=('farsight2')
replaces=('farsight2')
source=(http://freedesktop.org/software/$pkgname/releases/$pkgname/$pkgname-$pkgver.tar.gz{,.asc})
md5sums=('edcc75cbf7c811cfe3de9b00b9808682'
         'SKIP')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
    --with-package-name='Arch Linux farstream package' \
    --with-package-origin='http://archlinux.org'
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
