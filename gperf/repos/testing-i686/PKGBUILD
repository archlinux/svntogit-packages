# $Id$
# Maintainer:
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gperf
pkgver=3.0.4
pkgrel=6
pkgdesc="Perfect hash function generator"
arch=('x86_64' 'i686')
url="http://www.gnu.org/software/gperf/"
license=('GPL3')
depends=('gcc-libs' 'texinfo')
install=gperf.install
source=("http://ftp.gnu.org/pub/gnu/gperf/${pkgname}-${pkgver}.tar.gz"{,.sig})
sha1sums=('e32d4aff8f0c730c9a56554377b2c6d82d0951b8'
          '85b8c44964a70a63d751afa5906796a0417e64ce')
options=('!makeflags')
validpgpkeys=('A47F5EB8FEF0E8AB1E613F2045E59068F91BB79F') # Brendan Kehoe

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  ./configure --prefix=/usr \
    --docdir=/usr/share/gperf/doc/
  make
}

check() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  make check
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
