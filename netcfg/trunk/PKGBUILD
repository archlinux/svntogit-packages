# $Id$
# Maintainer: Jouke Witteveen <j.witteveen@gmail.com>

pkgname=netcfg
pkgver=2.7.1
pkgrel=1
pkgdesc="Network configuration and profile scripts"
url="http://archlinux.org"
license=("BSD")
backup=(etc/iftab etc/conf.d/netcfg)
depends=("coreutils" "dhcpcd>=4.0" "iproute2")
#makedepends=('asciidoc')  # The source tarball includes pre-built documentation.
optdepends=('dialog: Required for menu based profile and wifi selectors'
            'bridge-utils: To set up bridge connections'
            'wpa_supplicant: required for wireless network support'
            'wpa_actiond: Required for automatic wireless connection with rc.d/net-auto-wireless'
            'ifplugd: Required for automatic wired connection with rc.d/net-auto-wired'
            'wireless_tools: Required for net-rename'
           )
source=(ftp://ftp.archlinux.org/other/netcfg/netcfg-${pkgver}.tar.xz)
arch=(any)
sha256sums=('5f97f3eeff50fdc3c5fd2aaa251f50a73acb08116d97bddfa35adf600bc427bb')

package() {
  cd "$srcdir/netcfg-${pkgver}"
  make DESTDIR="$pkgdir" install
  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/netcfg/LICENSE"

  # Shell Completion
  install -D -m644 contrib/bash-completion "$pkgdir/etc/bash_completion.d/netcfg"
  install -D -m644 contrib/zsh-completion "$pkgdir/usr/share/zsh/site-functions/_netcfg"
}
