# $Id$
# Contributor: tobias <tobias@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=inkscape
pkgver=0.48.1
pkgrel=3
pkgdesc='Vector graphics editor using the SVG file format'
url='http://inkscape.sourceforge.net/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL')
makedepends=('boost' 'pkg-config' 'intltool')
depends=('gc' 'gtkmm' 'poppler-glib' 'pyxml' 'libxslt' 'gsl' 'popt' 'python2'
         'gtkspell' 'imagemagick' 'desktop-file-utils' 'hicolor-icon-theme')
optdepends=('python2-numpy: some extensions'
            'python-lxml: some extensions and filters'
            'uniconvertor: reading/writing to some proprietary formats')
options=('!libtool')
source=("http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        'gcc46.patch')
sha1sums=('a43467119925aee70d1d63d746f9846abb4dea91'
          '7163f35978538a6e223005737e8707b6f847fab3')

install=install

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	patch -p1 -i ../gcc46.patch
	sed -i 's|/usr/bin/python\>|/usr/bin/python2|g' cxxtest/*.py
	sed -i 's|/usr/bin/env python\>|/usr/bin/env python2|g' share/*/{test/,}*.py
	sed -i 's|"python" },|"python2" },|g' src/extension/implementation/script.cpp
	sed -i 's|python -c|python2 -c|g' configure share/extensions/uniconv*.py
	sed -i 's|"python"|"python2"|g' src/main.cpp

	./configure --prefix=/usr \
		--with-python \
		--with-perl \
		--without-gnome-vfs \
		--with-xft \
		--enable-lcms \
		--enable-poppler-cairo \
		--disable-dependency-tracking \

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make DESTDIR="${pkgdir}" install
}
