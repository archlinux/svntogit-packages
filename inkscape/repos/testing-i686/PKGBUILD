# $Id$
# Maintainer: tobias <tobias@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>

pkgname=inkscape
pkgver=0.47
pkgrel=1
pkgdesc="A vector-based drawing program - svg compliant"
url="http://inkscape.sourceforge.net/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL')
depends=('poppler-glib>=0.12.2' 'imagemagick>=6.5.8.5' 'gnome-vfs>=2.24.2' 'gtkspell>=2.0.16' 'gc>=7.1' 'gtkmm>=2.18.2' 'libxslt>=1.1.26' 'gsl>=1.13' 'pyxml>=0.8.4' 'desktop-file-utils')
makedepends=('boost' 'pkgconfig' 'intltool')
options=('!libtool')
install=inkscape.install
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.gz
        50-poppler-API.dpatch)
md5sums=('916c85b501bcfb0ae61fcf7318f36348'
         'b447272c9b282b49e86dca24c2581065')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/50-poppler-API.dpatch" || return 1
  ./configure --prefix=/usr \
    --with-python \
    --with-perl \
    --with-gnome-vfs \
    --with-xft \
    --enable-lcms \
    --enable-poppler-cairo \
    --disable-dependency-tracking || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
}
