# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>
# Contributor: tobias <tobias@archlinux.org>

pkgname=inkscape
pkgver=0.91
pkgrel=12
pkgdesc='Vector graphics editor using the SVG file format'
url='http://inkscape.sourceforge.net/'
license=('GPL' 'LGPL')
arch=('i686' 'x86_64')
makedepends=('boost' 'intltool')
depends=('gc' 'gsl' 'gtkmm' 'gtkspell' 'imagemagick' 'libxslt' 'poppler-glib' 'popt'
         'ttf-dejavu' 'python2' 'desktop-file-utils' 'hicolor-icon-theme')
optdepends=('pstoedit: latex formulas'
            'texlive-core: latex formulas'
            'python2-numpy: some extensions'
            'python2-lxml: some extensions and filters'
            'uniconvertor: reading/writing to some proprietary formats')
#source=("http://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.bz2"
source=("https://inkscape.global.ssl.fastly.net/media/resources/file/inkscape-0.91.tar.bz2"
        'http://bazaar.launchpad.net/~inkscape.dev/inkscape/RELEASE_0_91_BRANCH/download/head:/markers_strokepaint.-20091128124040-aej0x7yhxng1m6ly-8339/markers_strokepaint.inx.disabled')
sha1sums=('9941ee467af570ac71a70f965cd9c48d4993b8f3'
          'f61909a58c185ed9249b19f71242af2edd32ef9a')

install=install

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	sed -i 's|/usr/bin/python\>|/usr/bin/python2|g' cxxtest/*.py
	sed -i 's|/usr/bin/env python\>|/usr/bin/env python2|g' share/*/{test/,}*.py
	sed -i 's|"python" },|"python2" },|g' src/extension/implementation/script.cpp
	sed -i 's|python -c|python2 -c|g' configure share/extensions/uniconv*.py
	sed -i 's|"python"|"python2"|g' src/main.cpp

	# FS#43744
	mv ../markers_strokepaint.inx.disabled share/extensions/markers_strokepaint.inx
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	# glibmm 2.46 uses C++11 features
	CXXFLAGS+=' -std=c++11'

	./configure \
		--prefix=/usr \
		--with-python \
		--with-perl \
		--enable-lcms \
		--enable-poppler-cairo \
		--disable-dependency-tracking
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
