# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>
# Contributor: tobias <tobias@archlinux.org>

pkgname=inkscape
pkgver=1.0.2
_commit=e86c870879310bfb4a9abee3e7fd65aef0408fd7
pkgrel=4
pkgdesc='Professional vector graphics editor'
url='https://inkscape.org/'
license=('GPL' 'LGPL')
arch=('x86_64')
makedepends=('cmake' 'boost' 'git')

depends=(
	'aspell'
	'dbus-glib'
	'double-conversion'
	'gc'
	'gdl'
	'gsl'
	'gtkmm3'
	'gtkspell3'
	'libcdr'
	'libjpeg-turbo'
	'graphicsmagick'
	'libvisio'
	'libxslt'
	'poppler-glib'
	'potrace'
	'python'
	'ttf-font'
)

optdepends=(
	'fig2dev: xfig input'
	'gvfs: import clip art'
	'pstoedit: latex formulas'
	'python-lxml: some extensions'
	'python-numpy: some extensions'
	'scour: optimized SVG output, some extensions'
	'texlive-core: latex formulas'
)

#source=("https://media.inkscape.org/dl/resources/file/${pkgname}-${pkgver}.tar.bz2")
source=("git+https://gitlab.com/inkscape/inkscape.git#commit=${_commit}")
sha256sums=('SKIP')

prepare() {
	cd "${pkgname}"
	git submodule init
	git submodule update
}

build() {
	mkdir -p build
	export PKG_CONFIG_PATH='/usr/lib/imagemagick6/pkgconfig'
	cmake -H${pkgname} -Bbuild \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX:PATH=/usr \
		-DWITH_DBUS=ON \

	cmake --build build
}

package() {
	DESTDIR="${pkgdir}" cmake --build build --target install
}
