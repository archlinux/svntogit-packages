# $Id$
# Maintainer: tobias <tobias@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>

pkgname=inkscape
pkgver=0.46
pkgrel=9
pkgdesc="A vector-based drawing program - svg compliant"
url="http://inkscape.sourceforge.net/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL')
depends=('gtkmm>=2.12.6' 'gc>=7.0' 'desktop-file-utils' 'libxslt>=1.1.22' 'perl>=5.10.0' 'pyxml' 'openssl>=0.9.8d' 'lcms' 'gtkspell' 'poppler-glib>=0.10.0' 'imagemagick>=6.4.0.2' 'popt' 'gnome-vfs>=2.24.0')
makedepends=('perlxml' 'pkgconfig' 'boost')
options=('!libtool')
install=inkscape.install
source=("http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        perl-5.10.patch
	inkscape-0.46-poppler-0.8.3.patch
	gtk-clist.patch
	eqtexsvg.py_linux_shell_apply.patch)
md5sums=('3bae9034047379012127e52f9b138d32'
         'da1009efea12f6512e69cc3ec8604f4f'
         'fffe7ed73bdadcf0f90282d5afc4df30'
         'c68e508b390257d5a79364ce998be95d'
         '8c8f32a44b9cc1ea829027b3197d18cb')

build() {
  cd ${startdir}/src/${pkgname}-${pkgver}
  patch -Np1 -i ../perl-5.10.patch || return 1
  patch -Np1 -i ../inkscape-0.46-poppler-0.8.3.patch || return 1
  patch -Np0 -i ../gtk-clist.patch || return 1
  patch -Np0 -i ../eqtexsvg.py_linux_shell_apply.patch || return 1
  CFLAGS="${CFLAGS} -I/usr/include/ImageMagick" \
  CXXFLAGS="${CXXFLAGS} -I/usr/include/ImageMagick" \
   ./configure --prefix=/usr \
    --enable-inkboard \
    --enable-lcms \
    --with-gnome-print \
    --with-xft \
    --with-python \
    --with-perl \
    --with-gnome-vfs || return 1
  make || return 1
  make DESTDIR=${startdir}/pkg install || return 1
}
