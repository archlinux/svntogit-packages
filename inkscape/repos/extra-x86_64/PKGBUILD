# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>
# Contributor: tobias <tobias@archlinux.org>

pkgname=inkscape
pkgver=0.92.5
pkgrel=1
pkgdesc='Professional vector graphics editor'
url='https://inkscape.org/'
license=('GPL' 'LGPL')
arch=('x86_64')
depends=(
	'gc' 'gsl' 'gtkmm' 'gtkspell' 'libmagick6' 'libxslt' 'poppler-glib' 'popt'
	'potrace' 'ttf-dejavu' 'python2' 'libcdr' 'desktop-file-utils' 'hicolor-icon-theme'
)
# python is needed for glib-genmarshal
makedepends=('boost' 'intltool' 'python' 'cmake')
optdepends=(
	'gvfs: import clip art'
	'pstoedit: latex formulas'
	'python2-scour: optimized SVG output, some extensions'
	'texlive-core: latex formulas'
	'python2-numpy: some extensions'
	'python2-lxml: some extensions'
)
source=(
	"https://media.inkscape.org/dl/resources/file/${pkgname}-${pkgver}.tar.bz2"
	python2.diff
)
sha256sums=('81ee7f69e2bd64a97343d8395a4a7a7905b21b861566ed5e5d9179178d519a0b'
            '3b9697236c17ed36756f344ace23e71c1b73537fb9e394b6448b2c13fcca0cc8')

# NB: Some CDN nodes appear to be distributing an outdated version of this file.

prepare() {
	cd ${pkgname}-${pkgver}

    # Fix python2 shebangs
	sed -e '1s|python$|&2|' \
	    -i cxxtest/*.py share/*/{test/,}*.py

	# Non-shebang python2 changes
	patch -Np1 -i ../python2.diff
}

build() {
	export PKG_CONFIG_PATH='/usr/lib/imagemagick6/pkgconfig'

	cmake -H${pkgbase}-${pkgver} -Bbuild \
		-DCMAKE_BUILD_TYPE=None \
		-DCMAKE_INSTALL_PREFIX:PATH=/usr \
		-DINCLUDE_INSTALL_DIR:PATH=/usr/include \
		-DLIB_INSTALL_DIR:PATH=/usr/lib \
		-DSYSCONF_INSTALL_DIR:PATH=/etc \
		-DSHARE_INSTALL_PREFIX:PATH=/usr/share
	cmake --build build
}

package() {
	DESTDIR="${pkgdir}" cmake --build build --target install

	rm "${pkgdir}"/usr/lib/inkscape/*_LIB.a
}

# vim:set sw=0 noet:
