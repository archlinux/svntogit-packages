# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=make
pkgver=4.2.1
pkgrel=4
pkgdesc="GNU make utility to maintain groups of programs"
arch=('x86_64')
url="https://www.gnu.org/software/make"
license=('GPL3')
groups=('base-devel')
depends=('glibc' 'guile')
source=(https://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.bz2{,.sig}
        guile-2.2.patch
        make-4.2.1-glob1.patch
        make-4.2.1-glob2.patch
        make-4.2.1-test-driver.patch)
md5sums=('15b012617e7c44c0ed482721629577ac'
         'SKIP'
         '89bbbe3f806f208608e117665feb562b'
         '05a3b10e0da01d84cd078905a11437ed'
         '6f5dbcb368728582b34bac197f0d3197'
         'af63eff24206e1fa06af64b8ec7b07b9')
validpgpkeys=('3D2554F0A15338AB9AF1BB9D96B047156338B6D4')   # Paul Smith

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i ${srcdir}/guile-2.2.patch

  #https://git.savannah.gnu.org/cgit/make.git/patch/?id=193f1e81edd6b1b56b0eb0ff8aa4b41c7b4257b4
  patch -p1 -i ${srcdir}/make-4.2.1-glob1.patch
  #https://git.savannah.gnu.org/cgit/make.git/patch/?id=48c8a116a914a325a0497721f5d8b58d5bba34d4
  patch -p1 -i ${srcdir}/make-4.2.1-glob2.patch

  patch -p1 -i ${srcdir}/make-4.2.1-test-driver.patch

  autoreconf -fi
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make -k check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
