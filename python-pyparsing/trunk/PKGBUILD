# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Chris Brannon <cmbrannon79@gmail.com>
# Contributor: Geoffroy Carrier <geoffroy.carrier@aur.archlinux.org>
# Contributor: Arvid Ephraim Picciani <aep@exys.org>
# Contributor: Michael Krauss <hippodriver@gmx.net>

pkgname=python-pyparsing
pkgname=('python-pyparsing' 'python2-pyparsing')
pkgver=2.2.0
pkgrel=2
pkgdesc='General parsing module for Python'
arch=('any')
url='http://pyparsing.wikispaces.com/'
makedepends=('python-setuptools' 'python2-setuptools' 'subversion')
license=('MIT')
source=("svn://svn.code.sf.net/p/pyparsing/code/tags/pyparsing_$pkgver")
sha512sums=('SKIP')

prepare() {
  cp -a pyparsing_$pkgver{,-py2}

  # :/
  sed -i '1i#coding=utf-8' pyparsing_$pkgver-py2/src/unitTests.py
}

build() {
  cd "$srcdir"/pyparsing_$pkgver/src
  python setup.py build

  cd "$srcdir"/pyparsing_$pkgver-py2/src
  python2 setup.py build
}

check() {
  export LC_CTYPE=en_US.UTF-8

  cd "$srcdir"/pyparsing_$pkgver/src
  python unitTests.py

  cd "$srcdir"/pyparsing_$pkgver-py2/src
  python2 unitTests.py
}

package_python-pyparsing() {
  depends=('python')

  cd pyparsing_$pkgver/src

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-pyparsing() {
  depends=('python2')

  cd pyparsing_$pkgver-py2/src

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
