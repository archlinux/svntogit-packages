# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Chris Brannon <cmbrannon79@gmail.com>
# Contributor: Geoffroy Carrier <geoffroy.carrier@aur.archlinux.org>
# Contributor: Arvid Ephraim Picciani <aep@exys.org>
# Contributor: Michael Krauss <hippodriver@gmx.net>

pkgname=python-pyparsing
pkgname=('python-pyparsing' 'python2-pyparsing')
pkgver=2.4.0
pkgrel=1
pkgdesc='General parsing module for Python'
arch=('any')
url='https://github.com/pyparsing/pyparsing/'
makedepends=('python-setuptools' 'python2-setuptools')
license=('MIT')
source=("https://github.com/pyparsing/pyparsing/archive/pyparsing_$pkgver.tar.gz")
sha512sums=('2888b60c2518b19979e00b01ea499c45cd0a98affe0551f80ea7985acd649d62b4e231e142d3efd65cdcd9e3a0182680bfe1c4d98e310fa2ac476f37ac4ff344')

build() {
  cd pyparsing-pyparsing_$pkgver
  python setup.py build
  python2 setup.py build
}

check() {
  cd pyparsing-pyparsing_$pkgver
  python unitTests.py
  python2 unitTests.py
}

package_python-pyparsing() {
  depends=('python')

  cd pyparsing-pyparsing_$pkgver

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-pyparsing() {
  depends=('python2')

  cd pyparsing-pyparsing_$pkgver

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
