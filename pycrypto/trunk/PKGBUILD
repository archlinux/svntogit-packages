# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Kritoke <kritoke@gamebox.net>

pkgbase=pycrypto
pkgname=('python2-crypto' 'python-crypto')
pkgver=2.6.1
pkgrel=4
arch=('i686' 'x86_64')
makedepends=('python2' 'python' 'gmp')
url="http://www.dlitz.net/software/pycrypto/"
license=('custom')
source=(http://ftp.dlitz.net/pub/dlitz/crypto/${pkgbase}/${pkgbase}-${pkgver}.tar.gz{,.asc})
sha256sums=('f2ce1e989b272cfcb677616763e0a2e7ec659effa67a88aa92b3a65528f60a3c'
            'c2ab0516cc55321e6543ae75e2aa6f6e56e97432870f32a7799f3b89f467dc1b')
validpgpkeys=('19E11FE8B3CFF273ED174A24928CEC1339C25CF7')  # Dwayne Litzenberger

prepare() {
  find ${pkgbase}-${pkgver}/LEGAL -type f -exec chmod 644 {} \;
  find ${pkgbase}-${pkgver}/LEGAL -type d -exec chmod 755 {} \;
 
  cp -r ${pkgbase}-${pkgver} ${pkgbase}-${pkgver}-py3
}

build() {
  cd ${pkgbase}-${pkgver}
  python2 setup.py build

  cd ../${pkgbase}-${pkgver}-py3
  python setup.py build
}

package_python2-crypto() {
  pkgdesc="Collection of cryptographic algorithms and protocols, implemented for use from Python 2."
  depends=('python2' 'gmp')
  replaces=('pycrypto')
  conflicts=('pycrypto')
  provides=("pycrypto=${pkgver}")

  cd ${pkgbase}-${pkgver}
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgname}/"
  cp -r LEGAL "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_python-crypto() {
  pkgdesc="Collection of cryptographic algorithms and protocols, implemented for use from Python 3."
  depends=('python' 'gmp')

  cd ${pkgbase}-${pkgver}-py3
  python setup.py install --root="${pkgdir}" --optimize=1
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgname}/"
  cp -r LEGAL "${pkgdir}/usr/share/licenses/${pkgname}/"
}

check() {
  cd ${pkgbase}-${pkgver}
  python2 setup.py test

  cd ../${pkgbase}-${pkgver}-py3
  python setup.py test
}
