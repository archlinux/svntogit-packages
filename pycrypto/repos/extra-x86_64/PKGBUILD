# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Kritoke <kritoke@gamebox.net>

pkgbase=pycrypto
pkgname=('python2-crypto' 'python-crypto')
pkgver=2.6
pkgrel=1
arch=('i686' 'x86_64')
makedepends=('python2' 'python')
url="http://www.dlitz.net/software/pycrypto/"
license=('custom')
source=("http://ftp.dlitz.net/pub/dlitz/crypto/${pkgbase}/${pkgbase}-${pkgver}.tar.gz")
sha256sums=('7293c9d7e8af2e44a82f86eb9c3b058880f4bcc884bf3ad6c8a34b64986edde8')

build() {
  cd "${srcdir}"
  find ${pkgbase}-${pkgver}/LEGAL -type f -exec chmod 644 {} \;
  find ${pkgbase}-${pkgver}/LEGAL -type d -exec chmod 755 {} \;
 
  cp -r ${pkgbase}-${pkgver} ${pkgbase}-${pkgver}-py3
  cd "${srcdir}/${pkgbase}-${pkgver}"
  python2 setup.py build

  cd "${srcdir}/${pkgbase}-${pkgver}-py3"
  python setup.py build
}

package_python2-crypto() {
  pkgdesc="Collection of cryptographic algorithms and protocols, implemented for use from Python 2."
  depends=('python2')
  replaces=('pycrypto')
  conflicts=('pycrypto')
  provides=("pycrypto=${pkgver}")

  cd "${srcdir}/${pkgbase}-${pkgver}"
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgname}/"
  cp -r LEGAL "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_python-crypto() {
  pkgdesc="Collection of cryptographic algorithms and protocols, implemented for use from Pythoni 3."
  depends=('python')
  replaces=('pycrypto')
  conflicts=('pycrypto')
  provides=("pycrypto=${pkgver}")

  cd "${srcdir}/${pkgbase}-${pkgver}-py3"
  python setup.py install --root="${pkgdir}" --optimize=1
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgname}/"
  cp -r LEGAL "${pkgdir}/usr/share/licenses/${pkgname}/"
}
