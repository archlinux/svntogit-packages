# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: arjan <arjan@archlinux.org>
# Contributor: Jason Chu <jchu@xentac.net>

pkgname=libshout
pkgver=2.3.0
pkgrel=1
epoch=1
pkgdesc="Library for accessing a shoutcast/icecast server"
arch=('i686' 'x86_64')
depends=('libvorbis' 'libtheora' 'speex')
url="http://www.icecast.org/"
options=('!libtool' '!emptydirs')
license=('LGPL')
source=("http://downloads.xiph.org/releases/${pkgname}/${pkgname}-${pkgver}.tar.gz")
md5sums=('976a5979505f3de4026ae7fffb10754b')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make LDFLAGS+=-lspeex
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
