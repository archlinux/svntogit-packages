# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-vfs
pkgver=2.24.0
pkgrel=1
pkgdesc="The GNOME Virtual File System"
arch=(i686 x86_64)
license=('LGPL')
depends=('fam' 'gconf>=2.24' 'hal>=0.5.11' 'bzip2' 'avahi>=0.6.23' 'smbclient>=3.0.31' 'gnome-mime-data>=2.18.0-2' 'heimdal>=1.2' 'gnutls>=2.4.1')
makedepends=('perlxml' 'pkgconfig')
options=('!libtool' '!emptydirs')
conflicts=(gnome-vfs-samba)
provides=(gnome-vfs-samba)
replaces=(gnome-vfs-samba gnome-vfs-extras)
url="http://www.gnome.org"
install=gnome-vfs.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.24/gnome-vfs-${pkgver}.tar.bz2
	hal-show-volume-names.patch)
md5sums=('870ee8861bfcaeb852934e95657ef83e' '109d07aa1dc8cb6523c47c4984047dce')

build() {
  cd ${startdir}/src/${pkgname}-${pkgver}
  #Archlinux patch (b.g.o #321498)
  patch -Np1 -i ${startdir}/src/hal-show-volume-names.patch || return 1

  ./configure --prefix=/usr --sysconfdir=/etc \
        --localstatedir=/var --disable-static \
	--libexecdir=/usr/lib/gnome-vfs-2.0 \
  	--enable-samba --enable-hal \
        --enable-avahi --disable-howl \
	--disable-openssl --enable-gnutls
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR=${startdir}/pkg install || return 1

  mkdir -p ${startdir}/pkg/usr/share/gconf/schemas
  gconf-merge-schema ${startdir}/pkg/usr/share/gconf/schemas/${pkgname}.schemas ${startdir}/pkg/etc/gconf/schemas/*.schemas
  rm -f ${startdir}/pkg/etc/gconf/schemas/*.schemas
}
