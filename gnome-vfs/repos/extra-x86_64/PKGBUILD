# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-vfs
pkgver=2.24.3
pkgrel=2
pkgdesc="The GNOME Virtual File System"
arch=(i686 x86_64)
license=('LGPL')
depends=('fam' 'gconf>=2.28.0' 'hal>=0.5.13' 'bzip2' 'avahi>=0.6.25' 'smbclient>=3.3.7' 'gnome-mime-data>=2.18.0-2' 'heimdal>=1.3.1' 'gnutls>=2.8.3')
makedepends=('pkgconfig' 'intltool' 'gtk-doc' 'gnome-common')
options=('!libtool' '!emptydirs' '!makeflags')
conflicts=(gnome-vfs-samba)
provides=(gnome-vfs-samba)
replaces=(gnome-vfs-samba gnome-vfs-extras)
url="http://www.gnome.org"
install=gnome-vfs.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.24/gnome-vfs-${pkgver}.tar.bz2
        hal-show-volume-names.patch
        gnutls-config.patch)
sha256sums=('3b0f342e79813bfc11d8e636db96b3ffd16c9ca48b07306410e09a3fd1065b4a'
            'b38b86d6c966dcf64f9140d800aebe8a14845bb8ce7c045f52d4df483cacd23b'
            '66c7cfb12995c0dd94a2caea95c7e3c55981993f05a79c585d60915ff131955d')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  #Archlinux patch (b.g.o #321498)
  patch -Np1 -i "${srcdir}/hal-show-volume-names.patch" || return 1

  #Fix build with new gnutls
  patch -Np1 -i "${srcdir}/gnutls-config.patch" || return 1
  libtoolize --force || return 1
  gtkdocize || return 1
  aclocal || return 1
  autoconf || return 1
  automake || return 1
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --libexecdir=/usr/lib/gnome-vfs-2.0 \
      --enable-samba --enable-hal \
      --enable-avahi --disable-howl \
      --disable-openssl --enable-gnutls || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -d -m755 "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gnome-vfs-2.0 ${startdir}/pkg/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
