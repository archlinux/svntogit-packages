# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-vfs
pkgver=2.24.4
pkgrel=1
pkgdesc="The GNOME Virtual File System"
arch=(i686 x86_64)
license=('LGPL')
depends=('fam' 'gconf>=2.32.0' 'hal>=0.5.13' 'bzip2' 'avahi>=0.6.27' 'smbclient>=3.5.5' 'gnome-mime-data>=2.18.0-2' 'heimdal>=1.3.3' 'gnutls>=2.8.6')
makedepends=('pkgconfig' 'intltool' 'gtk-doc' 'gnome-common')
options=('!libtool' '!emptydirs' '!makeflags')
conflicts=(gnome-vfs-samba)
provides=(gnome-vfs-samba)
replaces=(gnome-vfs-samba gnome-vfs-extras)
url="http://www.gnome.org"
install=gnome-vfs.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.24/gnome-vfs-${pkgver}.tar.bz2
        hal-show-volume-names.patch
        gnutls-config.patch)
sha256sums=('62de64b5b804eb04104ff98fcd6a8b7276d510a49fbd9c0feb568f8996444faa'
            'b38b86d6c966dcf64f9140d800aebe8a14845bb8ce7c045f52d4df483cacd23b'
            '66c7cfb12995c0dd94a2caea95c7e3c55981993f05a79c585d60915ff131955d')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  #Archlinux patch (b.g.o #321498)
  patch -Np1 -i "${srcdir}/hal-show-volume-names.patch"

  #Fix build with new gnutls
  patch -Np1 -i "${srcdir}/gnutls-config.patch"
  libtoolize --force
  gtkdocize
  aclocal
  autoconf
  automake
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --libexecdir=/usr/lib/gnome-vfs-2.0 \
      --enable-samba --enable-hal \
      --enable-avahi --disable-howl \
      --disable-openssl --enable-gnutls
  make
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

  install -d -m755 "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gnome-vfs-2.0 ${startdir}/pkg/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
