# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=adobe-source-serif-pro-fonts
pkgver=2.007ro+1.007it
_relver=2.007R-ro/1.007R-it
pkgrel=1
pkgdesc="Serif typeface for setting text. Designed to complement Source Sans Pro"
url="https://adobe-fonts.github.io/source-serif-pro/"
arch=(any)
license=(custom)
_tarname=source-serif-pro-${_relver//\//-}
source=("$_tarname.tar.gz::https://github.com/adobe-fonts/source-serif-pro/archive/$_relver.tar.gz")
sha256sums=('8cc3c342fd783096711b7b73d9d4a63d6086f60ff3458dcb1913376ac44c9aeb')

package() {
  cd $_tarname
  install -d "$pkgdir/usr/share/fonts/${pkgname%-fonts}"
  install -t "$pkgdir/usr/share/fonts/${pkgname%-fonts}" -m644 OTF/*.otf
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
