# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=libkgapi
pkgver=2.2.0
pkgrel=1
pkgdesc="A KDE-based library for accessing various Google services via their public API"
url='https://projects.kde.org/projects/extragear/libs/libkgapi'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdepimlibs')
makedepends=('cmake' 'automoc4' 'boost')
replaces=('akonadi-google' 'libkgoogle')
conflicts=('akonadi-google' 'libkgoogle')
source=("http://download.kde.org/stable/${pkgname}/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2")
md5sums=('f3f2e4e68bb8baf849935f2f2eecf781')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE4_BUILD_TESTS=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
