# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=libkgapi
pkgver=20.11.90
pkgrel=1
pkgdesc="A KDE-based library for accessing various Google services via their public API"
url='https://www.kde.org/'
arch=(x86_64)
license=(GPL LGPL FDL)
depends=(kcalendarcore kcontacts kwallet)
makedepends=(extra-cmake-modules qt5-tools)
source=("https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('da559d88eaa4c5139db13ea0fd9ce846d45e925f03fd3fc80206d0e513714700'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
