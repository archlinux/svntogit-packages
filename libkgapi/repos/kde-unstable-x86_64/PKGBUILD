# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=libkgapi
pkgver=22.03.90
pkgrel=1
pkgdesc='A KDE-based library for accessing various Google services via their public API'
url='https://www.kde.org/'
arch=(x86_64)
license=(GPL LGPL FDL)
depends=(kcalendarcore kcontacts kwallet)
makedepends=(extra-cmake-modules qt5-tools)
source=(https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('0fee31ce2ab246abc27e47b2ac0102cdd0f6754b447eff6674364cef86c6e25a'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
options=(debug !lto) # https://bugs.kde.org/show_bug.cgi?id=411425

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
