# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=libkgapi
pkgver=17.12.1
pkgrel=2
pkgdesc="A KDE-based library for accessing various Google services via their public API"
url='https://www.kde.org/'
arch=(x86_64)
license=(GPL LGPL FDL)
depends=(kcalcore kcontacts qt5-webengine kio)
makedepends=(extra-cmake-modules qt5-tools python)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
        kdebug-388483.patch::"https://cgit.kde.org/libkgapi.git/patch/?id=2980ace5")
sha256sums=('d4569bd7c8f76c5e33fd16557ab9283f77a6ab87fe9fe870583472eda408f59c'
            'SKIP'
            '193867c7ec72283614fb7795724dc50730d001b41892ba0a730aeb75e98f86a0')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../kdebug-388483.patch # Fix Google authentication
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
