# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Ben <ben@benmazer.net>

pkgname=liferea
pkgver=1.4.19
pkgrel=1
pkgdesc="A GTK desktop news aggregator for online news feeds and weblogs"
arch=('i686' 'x86_64')
url="http://liferea.sourceforge.net/"
license=('GPL')
depends=('gconf>=2.22.0' 'xulrunner>=1.9' 'libnotify>=0.4.4' 'libnetworkmanager' 'libxslt>=1.1.20' \
         'libglade>=2.6.2' 'sqlite3' 'gnutls>=2.4.1')
makedepends=('perlxml' 'pkgconfig' 'diffutils' 'intltool')
options=('!libtool' '!emptydirs')
install=liferea.install
source=(http://downloads.sourceforge.net/sourceforge/liferea/liferea-${pkgver}.tar.gz liferea-1.4.17-xulrunner-1.9.patch)
md5sums=('e0e1e0071d39364ba8c2c95c07a7203a' '1384062e8e10e79cee0bbf468cf4497b')
sha1sums=('f57d7ad947583ee9bc33ef862b97aaf642de6b25' '17eb5ee13f8e56e1437ba44920db821b5a4316d0')

build() {
  cd ${srcdir}/liferea-${pkgver}
  patch -Np1 -i ${srcdir}/liferea-1.4.17-xulrunner-1.9.patch || return 1
  autoconf || return 1
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --enable-xulrunner --disable-gtkhtml2 \
	      --disable-webkit --disable-gecko || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR=${pkgdir} install || return 1
  
  mkdir -p ${pkgdir}/usr/share/gconf/schemas
  gconf-merge-schema ${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
