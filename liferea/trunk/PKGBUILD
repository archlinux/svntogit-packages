# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Ben <ben@benmazer.net>

pkgname=liferea
pkgver=1.4.28
pkgrel=1
pkgdesc="A GTK desktop news aggregator for online news feeds and weblogs"
arch=('i686' 'x86_64')
url="http://liferea.sourceforge.net/"
license=('GPL')
depends=('gconf>=2.22.0' 'xulrunner>=1.9' 'libnotify>=0.4.4' 'libnetworkmanager' 'libxslt>=1.1.20' \
         'libglade>=2.6.2' 'sqlite3' 'gnutls>=2.4.1')
makedepends=('perlxml' 'pkgconfig' 'diffutils' 'intltool')
options=('!libtool' '!emptydirs')
install=liferea.install
source=(http://downloads.sourceforge.net/sourceforge/liferea/liferea-${pkgver}.tar.gz)
md5sums=('411bd9e56a648056cdce96dbea104c3e')
sha1sums=('5c40c9a20eb94d5e0bc5e9b41af51389bc364daf')

build() {
  cd ${srcdir}/liferea-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --with-gecko=libxul --disable-gtkhtml2 \
	      --disable-webkit || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR=${pkgdir} install || return 1
  
  mkdir -p ${pkgdir}/usr/share/gconf/schemas
  gconf-merge-schema ${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
