# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=liferea
pkgver=1.12.5
pkgrel=3
pkgdesc="A desktop news aggregator for online news feeds and weblogs"
arch=('x86_64')
url="https://lzone.de/liferea"
license=('GPL')
depends=('webkit2gtk' 'libpeas' 'python-gobject')
makedepends=('intltool' 'gobject-introspection')
optdepends=('libsecret: for keyring plugin support'
            'python-cairo: for the tray icon plugin'
	    'gst-plugins-base: for media player plugin support'
	    'gst-plugins-good: for media player plugin support'
	    'gst-plugins-bad: for media player plugin support')
options=('!emptydirs')
source=(https://github.com/lwindolf/liferea/releases/download/v$pkgver/$pkgname-${pkgver}a.tar.bz2)
sha256sums=('566b17524e8573f26f733161bf7d452069c9a534219e882a5a79b36f69a59877')

build() {
  cd $pkgname-${pkgver}a
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --disable-schemas-compile
  make
}

package() {
  cd $pkgname-${pkgver}a
  make DESTDIR="${pkgdir}" install
}
