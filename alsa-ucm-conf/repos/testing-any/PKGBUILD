# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=alsa-ucm-conf
pkgver=1.2.7.1
pkgrel=1
pkgdesc="ALSA Use Case Manager configuration (and topologies)"
arch=(any)
url="https://alsa-project.org/"
license=(BSD)
source=(https://www.alsa-project.org/files/pub/lib/$pkgname-$pkgver.tar.bz2{,.sig})
sha512sums=('e2ade1b4f4d26cfb77fb423b82b7eea1b51bbb4857b771acb845da3f3f99c846ec28a415eb184c7d79ffbe1edf21f026951778f905e23658d78deaebb1315759'
            'SKIP')
b2sums=('72a58b3eb927549ca1433d1249cb43723dd26bf6ce2e72713859c81d2b56388c99d03e028f0e27952dad6296e7efe7e94744cb37150b56b9985d99cee8d53388'
        'SKIP')
validpgpkeys=('F04DF50737AC1A884C4B3D718380596DA6E59C91') # ALSA Release Team (Package Signing Key v1) <release@alsa-project.org>

package() {
  cd $pkgname-$pkgver
  install -vdm 755 "$pkgdir/usr/share/alsa/"
  cp -av ucm2 "$pkgdir/usr/share/alsa/"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -vDm 644 ucm2/README.md -t "$pkgdir/usr/share/doc/$pkgname/ucm2"
}
