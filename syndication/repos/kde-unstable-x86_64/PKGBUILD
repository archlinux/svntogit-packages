# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=syndication
pkgver=5.100.0
pkgrel=1
pkgdesc='RSS/Atom parser library'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(kcodecs)
makedepends=(extra-cmake-modules doxygen qt5-tools qt5-doc)
groups=(kf5)
conflicts=(kde-syndication)
provides=(kde-syndication)
replaces=(kde-syndication)
source=(https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('c548045b97da030ebf0cab8c6b0e32d3508384fa5aedee3c794d7fe60b9df38d'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>
options=(debug)

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
