# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=syndication
pkgver=5.90.0
pkgrel=1
pkgdesc='RSS/Atom parser library'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(kcodecs)
makedepends=(extra-cmake-modules doxygen qt5-tools qt5-doc)
groups=(kf5)
conflicts=(kde-syndication)
provides=(kde-syndication)
replaces=(kde-syndication)
source=(https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('bdfe30c05bbdd8e10903fe69a37cf859f25984edae16283fdd5c0ac207213229'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
