# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=rygel
pkgver=0.30.1
pkgrel=1
pkgdesc="UPnP AV MediaServer and MediaRenderer that allows you to easily share audio, video and pictures, and control of media player on your home network"
arch=(i686 x86_64)
url="http://live.gnome.org/Rygel"
license=(LGPL)
depends=(gupnp-av gupnp-dlna libgee gtk3 libunistring libtracker-sparql hicolor-icon-theme xdg-utils libmediaart)
makedepends=(vala intltool python2 gobject-introspection)
optdepends=('gst-plugins-base: Extra media codecs'
            'gst-plugins-good: Extra media codecs'
            'gst-plugins-bad: Extra media codecs'            
            'gst-plugins-ugly: Extra media codecs'
            'gst-libav: Extra media codecs'
            'tracker: Share indexed media files')
backup=(etc/rygel.conf)
install=rygel.install
groups=('gnome-extra')
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz
        org.gnome.Rygel1.service rygel.service)
sha256sums=('f4e188008f450c40d79bd5bb41c53c2db97301183dce414ef83eeb80f445c376'
            '0997df280adf182772e30613986c6fdbb4b76626182aa7ac03e4315f745499fe'
            '5fdb7b439e5b69407d0d1a24ee2543657a76ec39eb5d8a8271405b74a1fa9061')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib \
              --disable-static --enable-mediathek-plugin --enable-gst-launch-plugin
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 ../rygel.service "$pkgdir/usr/lib/systemd/user/rygel.service"
  install -m644 ../org.gnome.Rygel1.service "$pkgdir/usr/share/dbus-1/services/"
}
