# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=rygel
pkgver=0.40.2
pkgrel=2
epoch=1
pkgdesc="UPnP AV MediaServer and MediaRenderer"
url="https://wiki.gnome.org/Projects/Rygel"
arch=(x86_64)
license=(LGPL)
depends=(gupnp gupnp-av gupnp-dlna libgee gtk3 libunistring tracker3
         libmediaart gst-editing-services)
makedepends=(vala gobject-introspection git meson)
optdepends=('gst-plugins-base: Extra media codecs'
            'gst-plugins-good: Extra media codecs'
            'gst-plugins-bad: Extra media codecs'
            'gst-plugins-ugly: Extra media codecs'
            'gst-libav: Extra media codecs'
            'tracker3-miners: Share indexed media files'
            'tumbler: Thumbnailing service')
backup=(etc/rygel.conf)
groups=(gnome)
_commit=4cc71529e53acaae16957d38a6565396a7ebf6b4  # tags/rygel-0.40.2^0
source=("git+https://gitlab.gnome.org/GNOME/rygel.git#commit=$_commit"
        0001-tracker3-Don-t-create-connection-in-static-construct.patch
        0002-tracker3-Correct-graph-name-for-video-items.patch)
sha256sums=('SKIP'
            '888f5e0c23a677521074523b262e19de6f859f8bf44d614429fb75d2f960c4c3'
            '17ef15116014dda19f6887b8d84b55876f998ed9f660f381f2636a92b63ada46')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^RYGEL//;s/^rygel-//;s/_/./g;s/-/+/g'
}

prepare() {
  cd $pkgname
  git cherry-pick -n d0410cccf6af55e16d3ba9400ad5e9f703313739
  git apply -3 ../0001-tracker3-Don-t-create-connection-in-static-construct.patch
  git apply -3 ../0002-tracker3-Correct-graph-name-for-video-items.patch
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
