# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: ponsfoot <cabezon dot hashimoto at gmail dot com>
# Contributor: philacs

pkgname=libvisio
pkgver=0.1.3
pkgrel=1
pkgdesc='Library providing ability to interpret and import visio diagrams'
arch=('i686' 'x86_64')
url="https://wiki.documentfoundation.org/DLP/Libraries/libvisio"
license=('LGPL')
depends=('libwpd' 'libwpg' 'libxml2' 'icu' 'librevenge')
makedepends=('boost' 'gperf' 'doxygen' 'cppunit')
source=(http://dev-www.libreoffice.org/src/$pkgname/$pkgname-$pkgver.tar.xz
        boost159.patch
        fix_test.diff)
md5sums=('fda521a18bbfccc5f034ea02119e7998'
         'd197fba862d0a176fd2e68c448c96844'
         'f7f3d18896d7e76536267238bf85be42')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../boost159.patch
  # https://gerrit.libreoffice.org/gitweb?p=libvisio.git;a=commit;h=75a02c586f6ec49d4316f79004834250ef42b0e0
  patch -Np1 -i ../fix_test.diff
  autoreconf -vfi
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

check() {
  cd $pkgname-$pkgver
  # Need to set timezone to CET to work around a test failure
  # https://bugs.documentfoundation.org/show_bug.cgi?id=92396
  make -k check TZ=CET
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
