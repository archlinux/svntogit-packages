# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=libopenmpt
pkgver=0.6.8
pkgrel=1
pkgdesc="A library to render tracker music to a PCM audio stream"
arch=(x86_64)
url="https://lib.openmpt.org/libopenmpt/"
license=(BSD)
depends=(
  flac libFLAC.so
  gcc-libs
  glibc
  libogg libogg.so
  libpulse libpulse.so libpulse-simple.so
  libsndfile libsndfile.so
  libvorbis libvorbis.so libvorbisfile.so
  mpg123 libmpg123.so
  portaudio libportaudio.so
)
makedepends=(autoconf-archive doxygen help2man)
provides=(libopenmpt.so)
source=($pkgname-$pkgver.tar.gz::https://lib.openmpt.org/files/libopenmpt/src/$pkgname-$pkgver+release.autotools.tar.gz)
sha512sums=('254affd878c8139631b14a74a3bcb7d4af47eb94542507830b82fe6b29d3c1db6f5a1a921c3190304b82886969d49a61a33f260bb2e53dd0c8d58c7a91e34e8e')
b2sums=('207df582a1a23c5da617ad4f9a26f3cebdf7468cc3c3a26c9962a2bff209c678711c7ec443711e0b3b38c0bd7ff08daba51b0b69191b8b188fe7513519758ba0')

prepare() {
  cd $pkgname-$pkgver+release.autotools
  autoreconf -fiv
}

build() {
  cd $pkgname-$pkgver+release.autotools
  ./configure --prefix=/usr
  # prevent excessive overlinking due to libtool
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  make -k check -C $pkgname-$pkgver+release.autotools
}

package() {
  cd $pkgname-$pkgver+release.autotools
  make DESTDIR="$pkgdir" install
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
