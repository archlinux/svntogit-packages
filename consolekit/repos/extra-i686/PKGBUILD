# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: onestep_ua <onestep@ukr.net>

pkgname=consolekit
pkgver=0.4.3
pkgrel=3
pkgdesc="A framework for defining and tracking users, login sessions, and seats"
arch=('i686' 'x86_64')
url="http://www.freedesktop.org/wiki/Software/ConsoleKit"
license=('GPL')
depends=('polkit' 'zlib' 'libx11' 'dbus-glib')
makedepends=('pkgconfig' 'xmlto' 'docbook-xsl')
options=(!libtool)
source=(http://www.freedesktop.org/software/ConsoleKit/dist/ConsoleKit-${pkgver}.tar.bz2
        pam-foreground-compat.ck consolekit.logrotate)
md5sums=('4c39c6eacc75334b890f21eead1d2945'
         'a8a4de71d9b0549b8143e5f6c2a36fc7'
         '6fefa451d9fe2fc6d6269629d3529793')

build() {
  cd "${srcdir}/ConsoleKit-${pkgver}"
  ./configure --prefix=/usr \
      --sysconfdir=/etc \
      --localstatedir=/var \
      --libexecdir=/usr/lib/ConsoleKit \
      --enable-pam-module \
      --enable-docbook-docs
  make
}

package() {
  cd "${srcdir}/ConsoleKit-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 "${srcdir}/pam-foreground-compat.ck" "${pkgdir}/usr/lib/ConsoleKit/run-session.d/"

  # install the logrotate config
  install -D -m644 ${srcdir}/consolekit.logrotate ${pkgdir}/etc/logrotate.d/consolekit

  rm -rf "${pkgdir}/var/run"
}
