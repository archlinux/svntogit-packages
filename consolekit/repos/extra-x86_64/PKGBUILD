# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: onestep_ua <onestep@ukr.net>

pkgname=consolekit
pkgver=0.4.3
pkgrel=1
pkgdesc="A framework for defining and tracking users, login sessions, and seats"
arch=('i686' 'x86_64')
url="http://www.freedesktop.org/wiki/Software/ConsoleKit"
license=('GPL')
depends=('polkit>=0.99' 'zlib' 'libx11>=1.4.1' 'dbus-glib>=0.92')
makedepends=('pkgconfig' 'xmlto' 'docbook-xsl')
options=(!libtool)
source=(http://www.freedesktop.org/software/ConsoleKit/dist/ConsoleKit-${pkgver}.tar.bz2
        pam-foreground-compat.ck)
md5sums=('4c39c6eacc75334b890f21eead1d2945'
         'a8a4de71d9b0549b8143e5f6c2a36fc7')

build() {
  cd "${srcdir}/ConsoleKit-${pkgver}"
  ./configure --prefix=/usr \
      --sysconfdir=/etc \
      --localstatedir=/var \
      --libexecdir=/usr/lib/ConsoleKit \
      --enable-pam-module \
      --enable-docbook-docs
  make
  make DESTDIR="${pkgdir}" install
  install -m755 "${srcdir}/pam-foreground-compat.ck" "${pkgdir}/usr/lib/ConsoleKit/run-session.d/"
}
