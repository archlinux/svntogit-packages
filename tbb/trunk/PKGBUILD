# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Denis Martinez <deuns.martinez AT gmail.com>
# Contributor: Bogdan Burlacu <bogdan.burlacu AT pm.me>

pkgname=intel-tbb
pkgver=2020.2
pkgrel=1
pkgdesc='High level abstract threading library'
arch=(x86_64)
url='https://www.threadingbuildingblocks.org/'
license=(Apache)
depends=(gcc-libs)
makedepends=(cmake inetutils)
source=($pkgname-$pkgver.tar.gz::https://github.com/intel/tbb/archive/v$pkgver.tar.gz)
sha256sums=('4804320e1e6cbe3a5421997b52199e3c1a3829b2ecb6489641da4b8e32faf500')

build() {
  cd oneTBB-$pkgver
  make
}

package() {
  cd oneTBB-$pkgver
  install -d "$pkgdir"/usr/lib
  install -m755 build/linux_*/*.so* "$pkgdir"/usr/lib
  install -d "$pkgdir"/usr/include
  cp -a include/tbb "$pkgdir"/usr/include
  cmake \
    -DINSTALL_DIR="$pkgdir"/usr/lib/cmake/TBB \
    -DSYSTEM_NAME=Linux -DTBB_VERSION_FILE="$pkgdir"/usr/include/tbb/tbb_stddef.h \
    -P cmake/tbb_config_installer.cmake
}
