# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=poppler-qt
pkgver=0.8.4
pkgrel=1
pkgdesc="Poppler Qt bindings"
arch=(i686 x86_64)
license=('GPL')
depends=('qt>=4.3.3' 'poppler>=0.8.2')
makedepends=('pkgconfig')
options=('!libtool')
url="http://poppler.freedesktop.org/"
source=(http://poppler.freedesktop.org/poppler-${pkgver}.tar.gz
	poppler-0.6-bindings.patch)
md5sums=('29d341f295d01bff64c63a29a6abb274'
         '4255424fcf2c29fe3cae9cb14a671da6')

build() {
  cd ${startdir}/src/poppler-${pkgver}

  patch -Np1 -i ${startdir}/src/poppler-0.6-bindings.patch || return 1
  libtoolize --force --copy || return 1
  AT_M4DIR="m4" autoreconf -i || return 1
  ./configure --prefix=/usr --sysconfdir=/etc \
              --localstatedir=/var --disable-static \
	      --enable-zlib \
	      --enable-libjpeg \
	      --enable-cairo-output \
	      --enable-poppler-qt4 \
	      --disable-poppler-glib \
	      --disable-gtk-test \
	      --disable-poppler-qt
  sed -i -e 's|^LDFLAGS =|LDFLAGS = -lqt-mt|' qt/Makefile
  make -C goo libgoo.la || return 1
  make -C splash libsplash.la || return 1
  make -C fofi libfofi.la || return 1
  make -C poppler libpoppler.la libpoppler-cairo.la libpoppler-arthur.la || return 1
  make || return 1
  make DESTDIR=${startdir}/pkg install || return 1
  rm -f ${startdir}/pkg/usr/lib/pkgconfig/poppler-cairo.pc || return 1
}
