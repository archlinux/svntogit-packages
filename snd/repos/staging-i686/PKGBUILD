# $Id$
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>
# Contributor: see .contrib

pkgname=snd
pkgver=16.3
pkgrel=1
pkgdesc="An advanced sound editor"
arch=('i686' 'x86_64')
url="http://ccrma.stanford.edu/software/snd/"
license=('custom:free')
depends=('jack' 'gamin' 'gsl' 'fftw' 'gtk2' 'libmpc')
makedepends=('ladspa' 'wavpack' 'flac' 'speex' 'gmp'
             'timidity++' 'mpg123' 'vorbis-tools')
optdepends=('wavpack' 'flac' 'speex' 'mpg123'
            'timidity++' 'vorbis-tools')
source=("ftp://ccrma-ftp.stanford.edu/pub/Lisp/snd-16.3.tar.gz")
md5sums=('d7ade48a312186769ab7cbfd08d72b47')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  LDFLAGS+=" -Wl,-z,noexecstack"

  ./configure --prefix=/usr \
              --with-alsa \
              --with-jack \
              --with-gtk \
              --with-ladspa \
              --with-gmp
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make prefix="$pkgdir/usr" install

  install -Dm644 COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
