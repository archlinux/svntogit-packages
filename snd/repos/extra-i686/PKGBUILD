# $Id$
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=snd
pkgver=12.5
pkgrel=1
pkgdesc="An advanced sound editor"
arch=('i686' 'x86_64')
url="http://ccrma.stanford.edu/software/snd/"
license=('custom')
depends=('lesstif' 'jack' 'gamin' 'gsl' 'fftw')
makedepends=('ladspa' 'wavpack' 'flac' 'speex' 'libxpm'
             'timidity++' 'mpg123' 'vorbis-tools' 'gmp')
optdepends=('ladspa' 'wavpack' 'flac' 'speex'
            'timidity++' 'mpg123' 'vorbis-tools')
source=("http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz")
md5sums=('53bed2048fe9287f9a0e6a6d63536729')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr \
              --with-alsa \
              --with-jack
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make prefix="$pkgdir/usr" install

  install -Dm644 COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
