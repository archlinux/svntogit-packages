# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgname=lensfun
pkgver=0.2.8
pkgrel=1
pkgdesc="Database of photographic lenses and a library that allows advanced access to the database"
arch=(i686 x86_64)
url="http://lensfun.sourceforge.net"
license=('LGPL3')
depends=('glibc' 'glib2')
makedepends=('python2' 'libpng' 'doxygen')
source=("http://sourceforge.net/projects/lensfun/files/0.2.8/${pkgname}-${pkgver}.tar.bz2"
         lensfun-0.2.8-make-4.0.patch)

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    sed -i 's|#!/usr/bin/python|#!/usr/bin/python2|' configure
    # fix configure script for make 4.0
    patch -Np0 -i "${srcdir}/lensfun-0.2.8-make-4.0.patch"
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
   ./configure \
        --prefix=/usr \
        --libdir=/usr/lib
    make all
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make INSTALL_PREFIX="$pkgdir" install
}
md5sums=('db2988505e7432c6b331aa597789c639'
         '9464d933a5e4ccefed7fc38656a9ec18')
