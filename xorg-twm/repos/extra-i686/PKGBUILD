# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-twm
pkgver=1.0.8
pkgrel=3
pkgdesc="Tab Window Manager for the X Window System"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
backup=(usr/share/X11/twm/system.twmrc)
license=('custom')
depends=('libxmu')
makedepends=('xorg-util-macros')
options=('!emptydirs')
source=(${url}/releases/individual/app/twm-${pkgver}.tar.bz2
        manpage.patch)
sha256sums=('7660352353d632127ff50390991706aa660b28a4ada816c2582ac02720722e44'
            '0d08dacdb94bb0b2de1a80cc7275082e8f168e6309252c5250e507dd36950b9f')

prepare() {
  cd twm-${pkgver}
  patch -Np1 -i ../manpage.patch
  autoreconf -fiv
}

build() {
  cd twm-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd twm-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
