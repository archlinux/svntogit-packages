# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>

pkgname=libtorrent-rasterbar
pkgver=1.0.8
pkgrel=2
epoch=1
pkgdesc="A C++ BitTorrent library that aims to be a good alternative to all the other implementations around"
url="http://www.rasterbar.com/products/libtorrent/"
arch=('i686' 'x86_64')
license=('BSD')
depends=('boost-libs' 'geoip' 'python2')
makedepends=('boost')
options=('!emptydirs')
source=(https://github.com/arvidn/libtorrent/archive/libtorrent-${pkgver//./_}/$pkgname-$pkgver.tar.gz)
sha1sums=('7b0cc26207e47d833f6e97b356ed33e7d7859184')

build() {
  cd libtorrent-libtorrent-${pkgver//./_}
  ./autotool.sh
  PYTHON=/usr/bin/python2 \
  ./configure \
    --prefix=/usr \
    --enable-python-binding \
    --with-libgeoip=system \
    --with-libiconv
  make
}

package() {
  cd libtorrent-libtorrent-${pkgver//./_}
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
