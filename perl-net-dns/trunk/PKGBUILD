# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-net-dns
pkgver=0.83
pkgrel=2
pkgdesc="Perl Module: Interface to the DNS resolver"
arch=('i686' 'x86_64')
license=('PerlArtistic')
url="http://search.cpan.org/dist/Net-DNS/"
depends=('perl-digest-hmac' 'perl-net-ip')
checkdepends=('perl-test-pod')
optdepends=('perl-io-socket-inet6: IPv6 support'
            'perl-socket6: IPv6 support')
options=('!emptydirs')
source=(http://www.cpan.org/authors/id/N/NL/NLNETLABS/Net-DNS-$pkgver.tar.gz)
sha1sums=('1e0f7a3640125c5d7511324e516620ae25cac99f')

build() {
  cd Net-DNS-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Net-DNS-${pkgver}
  make test
}

package() {
  cd Net-DNS-${pkgver}
  make DESTDIR="${pkgdir}" install
# template start; name=perl-binary-module-dependency; version=1;
if [[ $(find "$pkgdir/usr/lib/perl5/" -name "*.so") ]]; then
	_perlver_min=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]);')
	_perlver_max=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]+1);')
	depends+=("perl>=$_perlver_min" "perl<$_perlver_max")
fi
# template end;
}
