# $Id$
# Maintainer Tobias Powalowski <tpowa@archlinux.org>
# Contributed: eliott <eliott@solarblue.net>, Andre Naumann <anaumann@SPARCed.org>
pkgname=nx-common
pkgver=3.4.0
pkgrel=4
pkgdesc="NoMachine NX common package for client and server"
arch=('i686' 'x86_64')
license=('GPL')
url="http://nomachine.com/"
depends=('libjpeg>=8' 'libpng>=1.4.0' 'openssl>=1.0.0' 'gcc-libs' 'audiofile' 'alsa-lib' 'bash')
source=(\
#Compression libs and proxy sources
http://64.34.161.181/download/$pkgver/sources/nxcomp-$pkgver-7.tar.gz \
http://64.34.161.181/download/$pkgver/sources/nxssh-$pkgver-2.tar.gz \
# ESD Support
http://64.34.161.181/download/$pkgver/sources/nxesd-$pkgver-2.tar.gz
# 64bit fixes
NXproto.h.64bit.diff
nx-gcc44.patch)
options=(!libtool)
md5sums=('cba926f2b855231a8fc3e0dabff52855'
         '18b81d850223ea84de244f8a9509c4b1'
         '65d55f2a86baa59343432261cabd52ec'
         '58341ba70dfab92ff38570071fbbf88a'
         'c805442f1500d8d6de15ec224f63277e')

build() {
  cd ${srcdir}
#  patch -Np1 -i ${srcdir}/nx-gcc44.patch
 
  mkdir -p ${pkgdir}/opt/NX/bin
  mkdir -p ${pkgdir}/opt/NX/lib 

  cd ${srcdir}/nxcomp
  if [ "$CARCH" = "x86_64" ]; then
    patch -Np1 -i ../NXproto.h.64bit.diff  || return 1
  fi
  
  ./configure --prefix=/opt/NX
  make || return 1
  cp -a libXcomp.so.* ${pkgdir}/opt/NX/lib
  cd ${pkgdir}/opt/NX/lib
  ln -s libXcomp.so.1 libXcomp.so

  cd ${srcdir}/nxssh
  ./configure --prefix=/opt/NX
  make || return 1
  install -D -m755 nxssh ${pkgdir}/opt/NX/bin/nxssh
 
  cd ${srcdir}/nxesd
  ./configure --prefix=/opt/NX
  make || return 1
  make DESTDIR=${pkgdir} install || return 1
  # fix libxcomp linking
  cd ${pkgdir}/opt/NX/lib
  ln -s libXcomp.so.3 libXcomp.so.1
}
