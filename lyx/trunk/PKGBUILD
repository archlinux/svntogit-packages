# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Jason Chu <jason@archlinux.org>

pkgname=lyx
pkgver=1.6.7
pkgrel=2
pkgdesc="An advanced open-source document processor."
arch=('i686' 'x86_64')
url="http://www.lyx.org"
depends=('qt' 'texlive-core' 'python2' 'perl' 'imagemagick' 'aspell' 'aiksaurus' 'boost-libs')
makedepends=('boost')
optdepends=('texlive-latex3: pdf export')
license=('GPL')
source=(ftp://ftp.lyx.org/pub/lyx/stable/1.6.x/$pkgname-$pkgver.tar.bz2 \
	lyx.desktop)
md5sums=('078a3568f41ceb6dd5f090071aab93bc'
         'c11db315dc99254a4118827f98922623')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  find . -type f -exec sed -i 's|#!.*python|#!/usr/bin/env python2|' {} +
  sed -i 's|"python|"python2|' lib/configure.py src/support/os.cpp
  ./configure --prefix=/usr \
    --with-frontend=qt4 --without-included-boost
  make || return 1
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install || return 1

  # install desktop entry
  install -Dm644 ${srcdir}/lyx.desktop \
	${pkgdir}/usr/share/applications/lyx.desktop || return 1
}
