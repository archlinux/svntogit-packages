# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Jason Chu <jason@archlinux.org>

pkgname=lyx
pkgver=2.3.0
pkgrel=1
pkgdesc="An advanced WYSIWYM document processor & LaTeX front-end"
arch=('x86_64')
url="http://www.lyx.org"
depends=('qt5-svg' 'texlive-core' 'python2' 'imagemagick' 'enchant' 'boost-libs' 'libmythes' 'file'
         'hicolor-icon-theme')
makedepends=('boost' 'bc')
optdepends=('rcs: built-in version control system'
            'texlive-latexextra: float wrap support'
            'python: support for python scripts')
license=('GPL')
backup=('etc/lyx/lyxrc.dist')
options=('emptydirs')
source=(ftp://ftp.lyx.org/pub/lyx/stable/2.3.x/$pkgname-$pkgver.tar.xz{,.sig}
        lyxrc.dist lyx-enchant2.patch)
validpgpkeys=('FE66471B43559707AFDAD955DE7A44FAC7FB382D')  # LyX Release Manager
sha512sums=('77366d365f600069564b6d508ba2169744bbc7bc4dc9cb7e9d5f44941796dd7a027ca9ad08f5a67fd6863cbd1f6308ce5233b6e8685ebc6cf78ed19eef84ccb5'
            'SKIP'           
            'eef777cf6033a7b1e04700f33068b07309f8d5c6931c16927305dafb3a00fd46d4b536149349ab56b7455e7dea195c8889da2b6fbf9caa9e76bc0557f9358bc3'
            '4c3ac4d6b01cb8072179718d9401e61634ce3ac0f324e1d4955d7026889183734d04d9760cc850bce6308917060584eab41d389812e0ddafdf80386c21e04d49')


prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../lyx-enchant2.patch # Fix build with Enchant 2.x
  sed -e 's|\[enchant\]|\[enchant-2\]|g' -i config/spell.m4
  autoreconf -vi
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  export CXXFLAGS="$CXXFLAGS -fpermissive"
  ./configure --prefix=/usr \
    --enable-qt5 --without-included-boost \
    --without-included-mythes
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  # install default config file
  install -Dm644 "${srcdir}/lyxrc.dist" "${pkgdir}/etc/lyx/lyxrc.dist"
  ln -sf /etc/lyx/lyxrc.dist "${pkgdir}/usr/share/lyx/lyxrc.dist"
}
