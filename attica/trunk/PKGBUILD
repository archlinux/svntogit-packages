# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=attica
pkgver=0.4.0
pkgrel=1
pkgdesc='A Qt library that implements the Open Collaboration Services API'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kdesupport/attica'
license=('LGPL')
depends=('qt')
makedepends=('cmake')
source=("http://download.kde.org/stable/${pkgname}/${pkgname}-${pkgver}.tar.bz2")
md5sums=('2de3a49d79884ed3ce9df491bf35a86b')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}/build"
  make DESTDIR="${pkgdir}" install
}
