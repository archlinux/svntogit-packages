# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
pkgname=seamonkey
pkgver=2.7.2
pkgrel=1
pkgdesc="SeaMonkey internet suite"
arch=('i686' 'x86_64')
license=('MPL')
depends=('alsa-lib'  'dbus-glib'  'desktop-file-utils'  'gtk2' 'hunspell'  'libevent'  'libnotify'  'libvpx'  'libxt' 'mime-types'  'mozilla-common'  'nss'  'sqlite3'  'startup-notification')
makedepends=('unzip' 'zip' 'pkg-config'  'python2' 'yasm' 'wireless_tools' 'mesa' 'autoconf2.13')
install=$pkgname.install
url="http://www.mozilla.org/projects/seamonkey"
source=(ftp://releases.mozilla.org/pub/mozilla.org/$pkgname/releases/$pkgver/source/$pkgname-$pkgver.source.tar.bz2
        mozconfig
        seamonkey.desktop
        seamonkey-2.0-lang.patch
        libvpx.patch)
md5sums=('44f9bd3c68951f2f21ab0f1fea123383'
         'd02b5f002a7cdf8d15dccd2548d09256'
         '6119a2254716752c9d08e366f8d4c048'
         '25b6fe16ac24cd5c852213e5c1adb272'
         '5b2aaff7dfe7f1f94ad965905b20e782')

build() {
  cd "$srcdir/comm-release"
  patch -Np1 -i "$srcdir/seamonkey-2.0-lang.patch"
  patch -Np1 -i "$srcdir/libvpx.patch"

  cp "$srcdir/mozconfig" .mozconfig
  export LDFLAGS="$LDFLAGS -Wl,-rpath,/usr/lib/seamonkey-$pkgver"

  make -j1 -f client.mk build MOZ_MAKE_FLAGS="$MAKEFLAGS"
}

package() {
  cd "$srcdir/comm-release"

  make -j1 -f client.mk DESTDIR="$pkgdir" install

  rm -rf "$pkgdir"/usr/lib/seamonkey-$pkgver/{dictionaries,hyphenation}
  ln -sf /usr/share/hunspell "$pkgdir/usr/lib/seamonkey-$pkgver/dictionaries"
  ln -sf /usr/share/hyphen "$pkgdir/usr/lib/seamonkey-$pkgver/hyphenation"

  install -m755 -d "$pkgdir/usr/share/applications"
  install -m755 -d "$pkgdir/usr/share/pixmaps"
  install -m644 suite/branding/nightly/icons/gtk/seamonkey.png \
                "$pkgdir/usr/share/pixmaps/"
  install -m644 "$srcdir/seamonkey.desktop" "$pkgdir/usr/share/applications/"

  rm -f "$pkgdir/usr/lib/pkgconfig/"seamonkey-ns{s,pr}.pc
}
