# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
pkgname=seamonkey
pkgver=2.0.1
pkgrel=2
pkgdesc="SeaMonkey internet suite"
arch=('i686' 'x86_64')
license=('MPL')
depends=('gtk2>=2.18.3' 'libidl2>=0.8.13' 'mozilla-common>=1.3'
	 'nss>=3.12.4' 'libxt' 'gcc-libs>=4.4.2' 'alsa-lib' 'dbus-glib>=0.82')
makedepends=('zip' 'pkgconfig' 'diffutils' 'gcc' 'libgnomeui>=2.24.2' 'python')
replaces=('mozilla')
url="http://www.mozilla.org/projects/seamonkey"
source=(http://releases.mozilla.org/pub/mozilla.org/${pkgname}/releases/${pkgver}/source/${pkgname}-${pkgver}.source.tar.bz2
        mozconfig
        seamonkey.desktop
    	seamonkey-appversion.patch
	seamonkey-2.0-lang.patch)
md5sums=('100c67f5dc351af5b9efb02bb375db50' '6f25f3af4506a3dfdd9da2d79181c0fd'
         '448848e65efcbf1b2be2873b66b6cd48' '788091fa626317c7e226717df869aa40'
         '25b6fe16ac24cd5c852213e5c1adb272')

build() {
  cd "${srcdir}/comm-1.9.1"
  patch -Np1 -i "${srcdir}/seamonkey-appversion.patch" || return 1
  patch -Np1 -i "${srcdir}/seamonkey-2.0-lang.patch" || return 1

  cp "${srcdir}/mozconfig" .mozconfig
  unset CFLAGS
  unset CXXFLAGS

  export LDFLAGS="-Wl,-rpath,/usr/lib/seamonkey-2.0"

  make -j1 -f client.mk build MOZ_MAKE_FLAGS="${MAKEFLAGS}" || return 1
  make -j1 DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/applications"
  install -m755 -d "${pkgdir}/usr/share/pixmaps"
  install -m644 "${srcdir}/comm-1.9.1/suite/branding/icons/gtk/seamonkey.png" \
                "${pkgdir}/usr/share/pixmaps/" || return 1
  install -m644 "${srcdir}/seamonkey.desktop" "${pkgdir}/usr/share/applications/" || return 1

  rm -f "${pkgdir}/usr/lib/pkgconfig/"seamonkey-ns{s,pr}.pc
}
