# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=bovo
pkgver=15.08.3
pkgrel=1
pkgdesc="A Gomoku like game for two players"
url="http://kde.org/applications/games/bovo/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegames')
depends=('libkdegames')
makedepends=('extra-cmake-modules' 'python' 'kdoctools')
install=${pkgname}.install
conflicts=('kdegames-bovo')
replaces=('kdegames-bovo')
source=("http://download.kde.org/stable/applications/${pkgver}/src/bovo-${pkgver}.tar.xz")
sha1sums=('4e6b82b456cbb6749cd6c54a4c34028c18978e73')

build() {
  mkdir -p build
  cd build
  cmake ../bovo-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
