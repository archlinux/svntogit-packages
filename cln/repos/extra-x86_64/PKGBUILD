# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=cln
pkgver=1.3.3
pkgrel=2
pkgdesc="Class library for numbers"
arch=('i686' 'x86_64')
url="http://www.ginac.de/CLN/"
license=('GPL')
depends=('gmp')
makedepends=('texlive-core')
install=cln.install
source=(http://www.ginac.de/CLN/${pkgname}-${pkgver}.tar.bz2)
sha1sums=('11c56780eb83ed54f2ad1ecef7f0dc0f609c426d')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make all html pdf
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install install-html install-pdf
}
