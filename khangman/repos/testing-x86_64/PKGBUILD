# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=khangman
pkgver=14.12.2
pkgrel=1
pkgdesc="Hangman Game"
url="http://kde.org/applications/education/khangman/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
depends=('libkeduvocdocument' 'knewstuff')
makedepends=('extra-cmake-modules' 'kdoctools' 'python')
replaces=('kdeedu-khangman')
conflicts=('kdeedu-khangman')
install=${pkgname}.install
source=("http://download.kde.org/stable/applications/${pkgver}/src/khangman-${pkgver}.tar.xz")
sha1sums=('5cf3699efb2c46a9873fba8f590823ad6d68b287')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../khangman-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DSYSCONF_INSTALL_DIR=/etc
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
