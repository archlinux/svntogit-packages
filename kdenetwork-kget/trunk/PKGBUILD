# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdenetwork-kget
pkgver=4.14.0
pkgrel=1
pkgdesc='Download Manager'
url='http://kde.org/applications/internet/kget/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdenetwork')
depends=('kdebase-workspace' 'kdebase-lib' 'libktorrent' 'libmms')
makedepends=('cmake' 'automoc4' 'boost')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/kget-${pkgver}.tar.xz")
sha1sums=('cce8b37a786f0da6af84d64ea9ee923fa00c5e13')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../kget-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_NepomukCore=OFF \
    -DWITH_NepomukWidgets=OFF
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
