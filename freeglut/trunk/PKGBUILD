# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=freeglut
pkgver=3.2.2
pkgrel=1
pkgdesc="Provides functionality for small OpenGL programs"
url="http://freeglut.sourceforge.net/"
arch=(x86_64)
license=(MIT)
depends=(libxi libxrandr libgl)
makedepends=(mesa glu libxxf86vm cmake ninja)
replaces=(glut)
provides=(glut)
conflicts=(glut)
source=(https://downloads.sourceforge.net/freeglut/${pkgname}-${pkgver}.tar.gz)
sha512sums=('190231951d314f854c244bd27f7c20488403bd0eecffc342097a2e3d1621cec87f6c209a77b4c153aabcf44697b4070227930e295ab74fb88953cbbf94fc81d9')

prepare() {
  cd $pkgname-$pkgver
}

build() {
  cmake -S $pkgname-$pkgver -B build -G Ninja \
    -DFREEGLUT_BUILD_STATIC_LIBS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_BUILD_TYPE=None
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  ln -sr "$pkgdir"/usr/lib/pkgconfig/{glut,freeglut}.pc
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 $pkgname-$pkgver/COPYING
}

# vim:set sw=2 et:
