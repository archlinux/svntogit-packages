# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=hunspell-it
pkgver=2.4
_pkgver=2_4
_date=2007_09_01
pkgrel=8
pkgdesc="Italian dictionary for Hunspell"
arch=('any')
url="https://sourceforge.net/projects/linguistico/"
license=('GPL3')
makedepends=('hunspell' 'qt5-webengine')
optdepends=('hunspell: the spell checking libraries and apps')
source=("https://downloads.sourceforge.net/linguistico/italiano_${_pkgver}_${_date}.zip")
md5sums=('e7fbd9e2dfb25ea3288cdb918e1e1260')

build() {
  # Fix dic file empty lines (FS#22275)
  sed '/^\/$/d' -i it_IT.dic

  # fix file encoding - FS#68481
  # check for broken files using
  # file -i  $(pacman -Ql hunspell-it | sed "s/hunspell-it//") | grep iso
  for file in it_IT.aff it_IT.dic; do
    mv $file $file.ISO-8859
    iconv -f iso-8859-1 -t utf-8 $file.ISO-8859 -o $file
  done
}

package() {
  install -d "${pkgdir}"/usr/share/hunspell

  install -D -m644 it_IT.dic "${pkgdir}/usr/share/hunspell/it_IT.dic"
  install -D -m644 it_IT.aff "${pkgdir}/usr/share/hunspell/it_IT.aff"

  pushd "${pkgdir}"/usr/share/hunspell/
  it_IT_aliases="it_CH"
  for lang in $it_IT_aliases; do
       ln -s it_IT.aff $lang.aff
       ln -s it_IT.dic $lang.dic
  done
  popd

  # the symlinks
  install -d "${pkgdir}"/usr/share/myspell/dicts
  pushd "${pkgdir}"/usr/share/myspell/dicts
    for file in "${pkgdir}"/usr/share/hunspell/*; do
      ln -sv /usr/share/hunspell/$(basename $file) .
    done
  popd

# Install webengine dictionaries
  install -d "$pkgdir"/usr/share/qt/qtwebengine_dictionaries/
  for _file in "$pkgdir"/usr/share/hunspell/*.dic; do
  _filename=$(basename $_file)
    qwebengine_convert_dict $_file "$pkgdir"/usr/share/qt/qtwebengine_dictionaries/${_filename/\.dic/\.bdic}
  done
}
