# $Id$
# Maintainer: tobias <tobias@archlinux.org>
# Contributor: Dominik Ryba <domryba@post.pl>

pkgname=enblend-enfuse
pkgver=4.0
pkgrel=6
pkgdesc="Intelligent blend tool for overlapping picture"
arch=("i686" "x86_64")
license=('GPL')
url="http://enblend.sourceforge.net"
depends=('openexr' 'glew' 'libxmi' 'freeglut' 'lcms' 'libpng' 'boost-libs')
makedepends=('boost')
replaces=('enblend')
conflicts=('enblend')
provides=('enblend')
install=${pkgname}.install
source=(http://downloads.sourceforge.net/sourceforge/enblend/${pkgname}-${pkgver}.tar.gz{,.asc} \
        libpng-1.4.patch enblend-4.0-libpng15.patch)
sha1sums=('34c3a5ce11c6ef0ef520d8a15a3cb6a94a567033'
          'aac6364b29b0a9d6c61c3db0982f52ce1579e934'
          'eae6cf48ea082865130302d0b4d1ca0b884a563b'
          '71e961d90be842eafe9d473d8d70b04b29077036')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}-753b534c819d"
  sed -i 's/info.arith_code = TRUE/info.arith_code = FALSE/' src/vigra_impex/jpeg.cxx
  patch -p1 -i ../libpng-1.4.patch
  patch -p0 -i ../enblend-4.0-libpng15.patch
  LIBS="-lboost_system" ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}-753b534c819d"
  make DESTDIR="${pkgdir}" install
  install -D -m644 doc/enblend.info "${pkgdir}/usr/share/info/enblend.info"
  install -D -m644 doc/enfuse.info "${pkgdir}/usr/share/info/enfuse.info"
}
