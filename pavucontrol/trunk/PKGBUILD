# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=pavucontrol
pkgdesc="PulseAudio Volume Control"
pkgver=3.0+23+g335c26c
pkgrel=1
epoch=1
arch=(x86_64)
url="https://freedesktop.org/software/pulseaudio/pavucontrol/"
license=(GPL)
depends=(libcanberra-pulse gtkmm3 libsigc++)
makedepends=(intltool lynx git)
optdepends=("pulseaudio: Audio backend")
_commit=335c26c57c18d95cc7d4ca693a75ef94fe919e1d  # master~8
source=("git+https://gitlab.freedesktop.org/pulseaudio/pavucontrol.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --long | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./bootstrap.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --enable-gtk3
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
