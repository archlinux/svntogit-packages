# Maintainer: anex <assassin.anex[@]gmail.com

pkgname=signon-kwallet-extension
pkgver=16.11.90
pkgrel=1
pkgdesc="KWallet signon extension"
arch=('i686' 'x86_64')
url="http://kde.org"
license=('LGPL')
groups=('kde-applications' 'kdenetwork')
depends=('signond' 'kwallet')
makedepends=('extra-cmake-modules')
source=("http://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
md5sums=('a32e2225c1633bc9cb5659ccf4574c13'
         'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
