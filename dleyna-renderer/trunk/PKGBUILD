# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=dleyna-renderer
pkgver=0.7.2
pkgrel=1
pkgdesc="Library to discover and manipulate Digital Media Renderers"
url="https://github.com/phako/dleyna-renderer"
arch=(x86_64)
license=(LGPL2.1)
depends=(dleyna-connector-dbus gupnp-av gupnp-dlna libsoup)
makedepends=(git meson docbook-xsl)
_commit=a6fb8c077aba1465d978174f91d17b3dcfc87cd4  # tags/v0.7.2^0
source=("git+https://github.com/phako/dleyna-renderer#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
