# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: trya <tryagainprod@gmail.com>

pkgbase=quazip
pkgname=(quazip-qt4 quazip-qt5)
pkgver=0.7.1
pkgrel=5
pkgdesc="C++ wrapper for the Gilles Vollant's ZIP/UNZIP C package"
url="http://sourceforge.net/projects/quazip/"
license=('LGPL')
arch=('i686' 'x86_64')
makedepends=(qt4 qt5-base cmake automoc4)
source=("http://downloads.sourceforge.net/$pkgbase/$pkgbase-$pkgver.tar.gz" 'qt5.patch')
md5sums=('3b99effb2a9417707d463e6f19cf2629'
         'f79814b21f1fbbfa86a14240d71158fc')

prepare() {
# Make qt4 and qt5 versions coinstallable
  cd $pkgbase-$pkgver
  patch -p1 -i ../qt5.patch
}

build() {
  cd $pkgbase-$pkgver
  mkdir -p qt{4,5}

  pushd qt4
   cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DBUILD_WITH_QT4:BOOL=ON
   make
  popd

  pushd qt5
   cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DBUILD_WITH_QT4:BOOL=OFF
   make
  popd  
}

package_quazip-qt4() {
  depends=(qt4)
  replaces=(quazip)
  provides=(quazip)
  conflicts=(quazip)

  cd $pkgbase-$pkgver/qt4
  make DESTDIR="$pkgdir" install
}

package_quazip-qt5() {
  depends=(qt5-base)

  cd $pkgbase-$pkgver/qt5
  make DESTDIR="$pkgdir" install
}
