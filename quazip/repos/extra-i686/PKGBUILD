# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: trya <tryagainprod@gmail.com>

pkgname=quazip
pkgver=0.7
pkgrel=1
pkgdesc="C++ wrapper for the Gilles Vollant's ZIP/UNZIP C package"
url="http://sourceforge.net/projects/quazip/"
license=('LGPL')
arch=('i686' 'x86_64')
depends=('qt4')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
md5sums=('52a9d807934ce4e9c30ead677a3148d2')

build() {
  cd ${pkgname}-${pkgver}/${pkgname}
  qmake-qt4 PREFIX=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}/${pkgname}
  make INSTALL_ROOT="${pkgdir}" install

  install -d "${pkgdir}"/usr/share/cmake-3.0/Modules
  install -m644 ../FindQuaZip.cmake\
    "${pkgdir}"/usr/share/cmake-3.0/Modules
}
