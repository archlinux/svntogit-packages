# $Id$
# Maintainer: Daniel Isenmann <daniel @ archlinux.org>
# Contributor: Shadowhand <woody.gilk@gmail.com>

pkgname=beagle
pkgver=0.3.9
pkgrel=4
pkgdesc="Search tool that ransacks your personal information space to find whatever you are looking for."
arch=(i686 x86_64)
url="http://beagle-project.org/"
license=('custom')
depends=('gnome-sharp>=2.24.1' 'gmime>=2.4.10' 'evolution-sharp>=0.20.0' 'libexif' 'librsvg>=2.26.0' 'sqlite3>=3.6.13' 'libxss' 'wv>=1.2.4' 'chmlib' 'gsf-sharp>=0.8.1' 'libsm' 'xdg-utils' 'ndesk-dbus-glib' 'mono>=2.4' 'taglib-sharp')
makedepends=('intltool' 'zip' 'pkgconfig')
options=('!libtool')
backup=('etc/beagle/crawl-rules/crawl-manpages'
        'etc/beagle/crawl-rules/crawl-windows'
        'etc/beagle/crawl-rules/crawl-applications'
        'etc/beagle/crawl-rules/crawl-documentation'
        'etc/beagle/crawl-rules/crawl-monodoc'
        'etc/beagle/crawl-rules/crawl-executables')
install=beagle.install
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/0.3/${pkgname}-${pkgver}.tar.bz2
        beagle-0.3.9-spew.patch
        beagle-0.3.9-gmime24.patch
        beagle-0.3.9-mono-data-sqlite-2.patch
        mono-2.8.patch)
md5sums=('b73c12423d2d67133dbb05933f4c8fe1'
         '003ada7acc2724e94e0218e4a6705b4a'
         '73b5a5ea03dc3a7ca71ed41772e4dd82'
         'd99a93730485b7f9a5e2111a8b2bf440'
         '7ef884a29c17b73a20cb09fac0536a47')

build() {
  export "MONO_SHARED_DIR=${srcdir}/.wabi"
  mkdir -p "${MONO_SHARED_DIR}"

  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i -e 's/CRAWL_USER=beagleindex/CRAWL_USER=beaglidx/' tools/beagle-crawl-system.in
  sed -i -e 's/libchm.so.1/libchm.so.0/' Util/ChmFile.cs
  sed -i -e 's/libgnome-desktop-2.so.2/libgnome-desktop-2.so.17/' search/Beagle.Search.exe.config
  sed -i -e 's/python/python2/' tools/keygrabber.py

  patch -Np1 -i "${srcdir}/beagle-0.3.9-spew.patch"
  patch -Np1 -i "${srcdir}/beagle-0.3.9-mono-data-sqlite-2.patch"
  patch -Np1 -i "${srcdir}/beagle-0.3.9-gmime24.patch"
  patch -Np1 -i "${srcdir}/mono-2.8.patch"

  libtoolize --force
  autoreconf

  sed -i -e 's/gmime-sharp/gmime-sharp-2.4/' configure

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
	--enable-inotify \
	--disable-static \
	--disable-docs
  make
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/var/cache/beagle/indexes"
  chown -R 86:86 "${pkgdir}/var/cache/beagle"
  install -m755 -d "${pkgdir}/usr/share/licenses/beagle"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/beagle/"
}
