# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-parley
pkgver=4.10.4
pkgrel=1
pkgdesc="Vocabulary Trainer"
url="http://kde.org/applications/education/parley/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
depends=('kdebase-runtime' 'libkdeedu')
makedepends=('cmake' 'automoc4')
optdepends=('kdebindings-kross-python: scripting support')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/parley-${pkgver}.tar.xz")
sha1sums=('d13d686b77a36ca488ab6f545e0871ad37f9b490')

build() {
  mkdir build
  cd build
  cmake ../parley-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
