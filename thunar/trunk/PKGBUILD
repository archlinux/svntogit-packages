# $Id$
# Maintainer: andyrtr <andyrtr funnychar archlinux.org>
# Contributor: Andrew Simmons <andrew.simmons@gmail.com>

pkgname=thunar
pkgver=1.0.2
pkgrel=1
pkgdesc="new modern file manager for Xfce"
arch=(i686 x86_64)
license=('GPL2' 'LGPL2.1')
url="http://thunar.xfce.org"
groups=('xfce4')
depends=('desktop-file-utils' 'libexif>=0.6.19' 'hicolor-icon-theme' 'fam' \
         'gtk2' 'exo>=0.3.107' 'libxfce4util>=4.6.2' 'hal>=0.5.14' 'libpng' 'libjpeg>=8.0.1' 'startup-notification')
makedepends=('intltool' 'pkgconfig' 'xfce4-panel>=4.6.4')
optdepends=('gamin:   a fam replacement to make thunar monitor changes to files on the fly'
            'xfce4-panel: for trash applet')
options=('!libtool')
install=${pkgname}.install
source=(http://archive.xfce.org/src/xfce/${pkgname}/1.0/Thunar-${pkgver}.tar.bz2)
md5sums=('c91073202d373a1f9951a1240083c36d')

build() {
  cd ${srcdir}/Thunar-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib \
    --localstatedir=/var --disable-static \
    --disable-gnome-thumbnailers --enable-exif --enable-pcre
  make || return 1
}

package() {
  cd ${srcdir}/Thunar-${pkgver}
  make DESTDIR=${pkgdir} install || return 1
  sed -i 's:x-directory/gnome-default-handler;::' \
    ${pkgdir}/usr/share/applications/Thunar-folder-handler.desktop
}
