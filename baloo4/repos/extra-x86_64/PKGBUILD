# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgbase=baloo4
pkgname=('libbaloo4')
pkgver=4.14.3
pkgrel=4
arch=('i686' 'x86_64')
url='https://community.kde.org/Baloo'
license=('LGPL')
makedepends=('cmake' 'automoc4' 'boost' 'kdepimlibs4' 'xapian-core' 'kfilemetadata4')
source=("http://download.kde.org/stable/${pkgver}/src/baloo-${pkgver}.tar.xz")
sha1sums=('bf90846545c5b5a0b0badab25dc3e839dc0c6574')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../baloo-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package_libbaloo4() {
  pkgdesc='Libraries for baloo4'
  depends=('kdepimlibs4' 'xapian-core' 'kfilemetadata4')
  conflicts=('baloo<=4.14.0-1')

  cd build
  make DESTDIR="$pkgdir" install

# Only install library
  rm -r "$pkgdir"{/etc,/usr/bin,/usr/share/{akonadi,autostart,dbus-1,icons,polkit-1}}
}
