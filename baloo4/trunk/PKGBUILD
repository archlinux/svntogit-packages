# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgbase=baloo4
pkgname=('baloo4' 'baloo4-akonadi' 'libbaloo4')
pkgver=4.14.3
pkgrel=2
arch=('i686' 'x86_64')
url='https://community.kde.org/Baloo'
license=('LGPL')
makedepends=('cmake' 'automoc4' 'boost' 'kdepimlibs' 'xapian-core' 'kfilemetadata4')
source=("http://download.kde.org/stable/${pkgver}/src/baloo-${pkgver}.tar.xz")
sha1sums=('bf90846545c5b5a0b0badab25dc3e839dc0c6574')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../baloo-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package_baloo4() {
  pkgdesc='A KDE4 framework for searching and managing metadata'
  depends=('baloo4-akonadi')
  conflicts=('baloo')
  install=${pkgname}.install

  cd build
  make DESTDIR="${pkgdir}" install

  # Fix conflicts
  rm -r "${pkgdir}"{/usr/bin/akonadi_baloo_indexer,/usr/include,/usr/lib,/usr/share/akonadi,/usr/share/kde4}
}

package_baloo4-akonadi() {
  pkgdesc='Baloo resource for Akonadi'
  depends=('libbaloo4')
  conflicts=('baloo<=4.14.0-1')

  cd build
  make DESTDIR="${pkgdir}" install

  # Fix conflicts
  rm -r "${pkgdir}"{/etc,/usr/bin/baloo*,/usr/include,/usr/lib,/usr/share/{kde4,autostart,dbus-1,icons,polkit-1}}
}

package_libbaloo4() {
  pkgdesc='Libraries for baloo4'
  depends=('kdepimlibs' 'xapian-core' 'kfilemetadata4')
  conflicts=('baloo<=4.14.0-1')

  cd build
  make DESTDIR="${pkgdir}" install

  # Fix conflicts
  rm -r "${pkgdir}"{/etc,/usr/bin,/usr/share/{akonadi,autostart,dbus-1,icons,polkit-1}}
}

