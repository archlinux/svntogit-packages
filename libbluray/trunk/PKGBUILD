# $Id$
# Maintainer:  Ionut Biru <ibiru@archlinux.org>
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Andrew Cook <ariscop@gmail.com>

pkgname=libbluray
pkgver=0.8.1
pkgrel=1
pkgdesc='Library to access Blu-Ray disks for video playback'
arch=('i686' 'x86_64')
url='http://www.videolan.org/developers/libbluray.html'
license=('LGPL2.1')
depends=('libxml2' 'freetype2' 'fontconfig')
makedepends=('apache-ant' 'git')
optdepends=('java-environment: for BD-J library')
#source=(ftp://ftp.videolan.org/pub/videolan/$pkgname/$pkgver/$pkgname-$pkgver.tar.bz2)
source=("git://git.videolan.org/libbluray.git#tag=$pkgver")
md5sums=('SKIP')

prepare() {
  cd $pkgname
  ./bootstrap
}

build() {
  cd $pkgname
  ./configure --prefix=/usr
  make
}

package() {
  make DESTDIR="$pkgdir" -C $pkgname install
}
