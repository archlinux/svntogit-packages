# $Id$
# Maintainer:
# Contributor: Thomas BÃ¤chler <thomas@archlinux.org>

pkgname=wpa_supplicant_gui
pkgver=0.6.10
pkgrel=2
pkgdesc="A Qt frontend to wpa_supplicant"
url="http://hostap.epitest.fi/wpa_supplicant"
arch=('i686' 'x86_64')
depends=('qt' "wpa_supplicant=$pkgver")
license=('GPL')
source=("http://hostap.epitest.fi/releases/wpa_supplicant-$pkgver.tar.gz")
md5sums=('8985b1030d753e5f74f8acfc34e591bb')

build() {
  cd ${srcdir}/wpa_supplicant-${pkgver}/wpa_supplicant/wpa_gui-qt4
  qmake || return 1
  make || return 1
}

package() {
  cd ${srcdir}/wpa_supplicant-${pkgver}/wpa_supplicant/wpa_gui-qt4
  install -Dm755 wpa_gui \
    ${pkgdir}/usr/sbin/wpa_gui || return 1
  install -Dm644 icons/wpa_gui.svg \
    ${pkgdir}/usr/share/pixmaps/wpa_gui.svg || return 1
  install -Dm644 wpa_gui.desktop \
    ${pkgdir}/usr/share/applications/wpa_gui.desktop || return 1
  install -Dm644 ${srcdir}/wpa_supplicant-${pkgver}/wpa_supplicant/doc/docbook/wpa_gui.8 \
     ${pkgdir}/usr/share/man/man8/wpa_gui.8 || return 1
}
