# $Id$
# Maintainer: SÃ©bastien Luttringer <seblu at seblu dot net>
# Contributor: Bastien Dejean <nihilhill@gmail.com>

pkgname=xcb-util-cursor
pkgver=0.1.2
pkgrel=1
pkgdesc='XCB cursor library'
arch=('i686' 'x86_64')
url='http://cgit.freedesktop.org/xcb/util-cursor'
license=('custom:MIT')
depends=('libxcb' 'xcb-util-renderutil' 'xcb-util-image')
source=("http://xcb.freedesktop.org/dist/xcb-util-cursor-$pkgver.tar.bz2")
md5sums=('88e70d92ff5616d406ce5b1b62e0c58b')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

