# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Jochem Kossen <j.kossen@home.nl>

pkgname=fakeroot
pkgver=1.24
pkgrel=2
pkgdesc='Tool for simulating superuser privileges'
arch=(x86_64)
license=(GPL)
url='https://packages.debian.org/fakeroot'
groups=(base-devel)
install=fakeroot.install
depends=(glibc filesystem sed util-linux sh)
makedepends=(po4a)
checkdepends=(sharutils)
source=("https://deb.debian.org/debian/pool/main/f/$pkgname/${pkgname}_${pkgver}.orig.tar.gz"
        'silence-dlerror.patch')
sha256sums=('2e045b3160370b8ab4d44d1f8d267e5d1d555f1bb522d650e7167b09477266ed'
            '347f6496c93ed0d91dcb554db92602b50520a16308e3b8bc33822bd69966267b')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i "$srcdir"/silence-dlerror.patch

  ./bootstrap
}

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr \
    --libdir=/usr/lib/libfakeroot \
    --disable-static \
    --with-ipc=sysv

  make

  cd doc
  po4a -k 0 --rm-backups --variable 'srcdir=../doc/' po4a/po4a.cfg
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  install -dm755 "$pkgdir/etc/ld.so.conf.d/"
  echo '/usr/lib/libfakeroot' > "$pkgdir/etc/ld.so.conf.d/fakeroot.conf"

  # install README for sysv/tcp usage
  install -Dm644 README "$pkgdir/usr/share/doc/$pkgname/README"
}
