# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Jochem Kossen <j.kossen@home.nl>

pkgname=fakeroot
pkgver=1.23
pkgrel=1
pkgdesc='Tool for simulating superuser privileges'
arch=(x86_64)
license=(GPL)
url='http://packages.debian.org/fakeroot'
groups=(base-devel)
install=fakeroot.install
depends=(glibc filesystem sed util-linux sh)
makedepends=(po4a)
checkdepends=(sharutils)
source=(http://ftp.debian.org/debian/pool/main/f/$pkgname/${pkgname}_${pkgver}.orig.tar.xz
        silence-dlerror.patch)
sha256sums=('009cd6696a931562cf1c212bb57ca441a4a2d45cd32c3190a35c7ae98506f4f6'
            '347f6496c93ed0d91dcb554db92602b50520a16308e3b8bc33822bd69966267b')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i "$srcdir"/silence-dlerror.patch

  ./bootstrap
}

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr \
    --libdir=/usr/lib/libfakeroot \
    --disable-static \
    --with-ipc=sysv

  make

  cd doc
  po4a -k 0 --rm-backups --variable 'srcdir=../doc/' po4a/po4a.cfg
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  install -dm755 "$pkgdir/etc/ld.so.conf.d/"
  echo '/usr/lib/libfakeroot' > "$pkgdir/etc/ld.so.conf.d/fakeroot.conf"

  # install README for sysv/tcp usage
  install -Dm644 README "$pkgdir/usr/share/doc/$pkgname/README"
}
