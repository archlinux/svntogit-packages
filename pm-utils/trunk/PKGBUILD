#$Id$
#Maintainer: Thayer Williams <thayer@archlinux.org>

pkgname=pm-utils
pkgver=1.2.3
pkgrel=3
pkgdesc="Utilities and scripts for suspend and hibernate power management"
arch=('i686' 'x86_64')
url="http://pm-utils.freedesktop.org"
license=('GPL')
depends=('glibc' 'vbetool' 'procps')
makedepends=('xmlto')
source=(http://pm-utils.freedesktop.org/releases/${pkgname}-${pkgver}.tar.gz
	11netcfg
    daemons.patch)
md5sums=('2a9a9e3de8bcda43c88a9e346866fd55'
         '9839687c8787d99fd4ff36aa3238c27b'
         '67bafe375f385e66bae432b9db914aaf')

build() {
  cd $srcdir/$pkgname-$pkgver

  # make sure Arch Linux daemons are properly handled
  patch -Np0 -i $srcdir/daemons.patch || return 1

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make || return 1
  make DESTDIR=$pkgdir install || return 1

  # install additional hooks
  install -m755 $srcdir/11netcfg $pkgdir/usr/lib/pm-utils/sleep.d/11netcfg
}

