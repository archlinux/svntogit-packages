# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Thayer Williams <thayer@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=pm-utils
pkgver=1.3.0
pkgrel=2
pkgdesc="Utilities and scripts for suspend and hibernate power management"
arch=('i686' 'x86_64')
url="http://pm-utils.freedesktop.org"
license=('GPL')
depends=('bash' 'procps' 'vbetool' 'pm-quirks')
optdepends=('upower')
makedepends=('xmlto' 'docbook-xml' 'docbook-xsl')
source=(http://pm-utils.freedesktop.org/releases/${pkgname}-${pkgver}.tar.gz
        on_ac_power-upower.patch
        11netcfg
       	daemons.patch)
md5sums=('37d71f8adbb409442212a85a080d324d'
         '676855c54039a3b2c3171e69ccee2113'
         '9839687c8787d99fd4ff36aa3238c27b'
         '7be3f7460569d5a382474abd7f7b53fb')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # make sure Arch Linux daemons are properly handled
  patch -Np1 -i "${srcdir}/daemons.patch" || return 1

  # Support upower for on_ac_power
  patch -Np1 -i "${srcdir}/on_ac_power-upower.patch" || return 1

  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --localstatedir=/var || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1

  # install additional hooks
  install -m755 "${srcdir}/11netcfg" "${pkgdir}/usr/lib/pm-utils/sleep.d/" || return 1
}
