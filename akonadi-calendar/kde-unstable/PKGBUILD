# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=akonadi-calendar
pkgver=15.11.80
pkgrel=1
pkgdesc="Akonadi calendar integration"
arch=(i686 x86_64)
url="https://projects.kde.org/$pkgname"
license=(LGPL)
depends=(kmailtransport kcalutils akonadi-contact)
makedepends=(extra-cmake-modules kdoctools boost)
source=("http://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
md5sums=('c1c47b1d69627cdfcb145ca8d00667dd')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
