# Maintainer: Allan McRae <allan@archlinux.org>
# Contributer: Jan de Groot <jgc@archlinux.org>

pkgname=gmp
pkgver=6.1.2
pkgrel=3
pkgdesc="A free library for arbitrary precision arithmetic"
arch=('x86_64')
url="https://gmplib.org/"
depends=('gcc-libs' 'sh')
license=('LGPL3' 'GPL')
source=(https://gmplib.org/download/gmp/gmp-${pkgver}.tar.xz{,.sig})
md5sums=('f58fa8001d60c4c77595fbbb62b63c1d'
         'SKIP')
validpgpkeys=('343C2FF0FBEE5EC2EDBEF399F3599FF828C67298')   # Niels MÃ¶ller

build() {
  cd "${pkgname}-${pkgver}"

  ./configure --build=${CHOST} \
    --prefix=/usr \
    --enable-cxx \
    --enable-fat
  make
}

check() {
  cd "${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
