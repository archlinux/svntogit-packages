# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Thomas Baechler <thomas@archlinux.org>
pkgname=gmp
pkgver=4.3.2
pkgrel=1
pkgdesc="A free library for arbitrary precision arithmetic"
arch=('i686' 'x86_64')
url="http://gmplib.org/"
depends=('gcc-libs')
license=('LGPL3')
options=(!libtool)
install=gmp.install
source=(ftp://ftp.gnu.org/gnu/gmp/gmp-${pkgver}.tar.bz2)
md5sums=('dd60683d7057917e34630b4a787932e8')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  [ "${CARCH}" = "i686" ] && export ABI="32"
  ./configure --build=$CHOST \
    --prefix=/usr --infodir=/usr/share/info \
    --enable-cxx || return 1
  make || return 1
  make check || return 1
  make DESTDIR="${pkgdir}" install || return 1
  cd "${pkgdir}/usr/share/info"
  rm -f dir
  gzip * || return 1
}
