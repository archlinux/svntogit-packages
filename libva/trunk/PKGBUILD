# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

pkgname=libva
pkgver=1.3.1
pkgrel=2
pkgdesc='Video Acceleration (VA) API for Linux'
arch=('i686' 'x86_64')
url='http://freedesktop.org/wiki/Software/vaapi'
license=('MIT')
depends=('libgl' 'libdrm' 'libxfixes')
makedepends=('mesa')
optdepends=('libva-vdpau-driver: vdpau back-end for nvidia'
            'libva-intel-driver: back-end for intel cards')
source=(http://freedesktop.org/software/vaapi/releases/$pkgname/$pkgname-$pkgver.tar.bz2
        0001-va-release-VADisplayContext-at-the-end-of-vaTerminat.patch)
md5sums=('eb4db967f068854444b597071c66b480'
         'e564ae7c20962c821ac2b5cb1693e1a2')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../0001-va-release-VADisplayContext-at-the-end-of-vaTerminat.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
