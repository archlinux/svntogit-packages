# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Tobias Kieslich <tobias (at) archlinux.org>

pkgname=thunar-volman
pkgver=0.6.0
pkgrel=2
pkgdesc="automatic management for removeable devices in thunar"
arch=('i686' 'x86_64')
license=('GPL2')
url="http://foo-projects.org/~benny/projects/thunar-volman"
groups=('xfce4-goodies')
depends=('thunar>=1.2.0' 'libxfce4ui' 'hicolor-icon-theme')
makedepends=('intltool')
options=('!libtool')
install=${pkgname}.install
source=(http://archive.xfce.org/src/apps/${pkgname}/0.6/${pkgname}-${pkgver}.tar.bz2)
md5sums=('2f166662dd100d5195da238af417f305')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--libexecdir=/usr/lib/xfce4 \
	--localstatedir=/var \
	--disable-static \
	--disable-debug
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
