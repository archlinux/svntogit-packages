# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegraphics-svgpart
pkgver=15.12.0
pkgrel=1
pkgdesc="A KPart for viewing SVGs"
url="https://projects.kde.org/projects/kde/kdegraphics/svgpart"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdelibs')
makedepends=('cmake' 'automoc4')
replaces=('kdegraphics-libs')
conflicts=('kdegraphics-libs')
source=("http://download.kde.org/stable/applications/${pkgver}/src/svgpart-${pkgver}.tar.xz")
sha1sums=('a963d61e5bc6164f0af1a3687df0d4f42d62a7be')

build() {
  cd "${srcdir}"
  mkdir -p build
  cd build
  cmake ../svgpart-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
