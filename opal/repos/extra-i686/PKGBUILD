# $Id$
# Maintainer: Jan de Groot <jan@archlinux.org>
# Contributor: Tom K <tomk@runbox.com>
pkgname=opal
pkgver=3.10.7
pkgrel=2
pkgdesc="Open Phone Abstraction Library"
arch=(i686 x86_64)
url="http://www.opalvoip.org"
license=('GPL')
depends=('ptlib' 'libtheora' 'x264' 'speex' 'gsm')
makedepends=('ffmpeg')
options=(!makeflags)
source=(http://downloads.sourceforge.net/sourceforge/opalvoip/$pkgname-$pkgver.tar.bz2
        opal-ffmpeg.patch)
md5sums=('8686bb930668067eefadab115d2ee1c2'
         '0f05c01dada644b37805667ba9e81aa5')

build() {
  cd "${srcdir}/opal-${pkgver}"
  patch -Np1 -i "${srcdir}/opal-ffmpeg.patch"
  CXXFLAGS="$CXXFLAGS -fpermissive" \
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd "${srcdir}/opal-${pkgver}"
  make DESTDIR="${pkgdir}" install
  chmod 644 "${pkgdir}/usr/lib/libopal_s.a"
}
