# $Id$
# Maintainer: Jan de Groot <jan@archlinux.org>
# Contributor: Tom K <tomk@runbox.com>
pkgname=opal
pkgver=3.10.11
pkgrel=3
pkgdesc="Open Phone Abstraction Library"
arch=(i686 x86_64)
url="http://www.opalvoip.org"
license=(GPL)
depends=(ptlib libtheora x264 speex gsm celt)
makedepends=(ffmpeg x264-dev)
optdepends=('ffmpeg: h263 and mpeg4 plugins')
options=(!makeflags)
source=(http://downloads.sourceforge.net/opalvoip/$pkgname-$pkgver.tar.bz2
        opal-ffmpeg.patch)
sha256sums=('f38e7969e88a28dd9a10a6051315aceecde79d0bbd1a35676868dc5787aed706'
            '83f60ecf692e004293ce9dbaae7d7eb64d9713d3bfb71a0e7b1a0d1f936794cd')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../opal-ffmpeg.patch
}

build() {
  cd $pkgname-$pkgver
  CXXFLAGS="$CXXFLAGS -fpermissive" \
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="${pkgdir}" install
}
