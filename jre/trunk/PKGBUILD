# $Id$
# Maintainer: Jason Chu <jason@archlinux.org>

pkgname=jre
pkgver=6u6
pkgrel=1
pkgdesc="Sun's java runtime environment"
arch=(i686 x86_64)
  [ "$CARCH" = "i686" ]   && _arch=i586
  [ "$CARCH" = "x86_64" ] && _arch=amd64
url="http://java.sun.com"
depends=('glibc' 'libstdc++5')
install="jre.install"
source=(http://download.java.net/dlj/binaries/jdk-$pkgver-dlj-linux-$_arch.bin https://jdk-distros.dev.java.net/source/browse/*checkout*/jdk-distros/trunk/utils/construct.sh jre.profile)
md5sums=('0efe7120ffb9a3379bc66d993056d80d'
         '94065b612df0046d9ae758943f9f6a75'
         'c91ef2812b6554264f587f6b3035dbcc')
replaces=('j2re')
conflicts=('j2re' 'java-runtime')
provides=('j2re' 'java-runtime')
license=('custom')

build() {
  cd $startdir/src

  mkdir unbundle-jdk
  cd unbundle-jdk

  sh ../jdk-$pkgver-dlj-linux-$_arch.bin --accept-license

  cd ..

  sh construct.sh unbundle-jdk linux-jdk linux-jre

  mkdir -p $startdir/pkg/opt/java
  mv linux-jdk/jre $startdir/pkg/opt/java

  install -D -m755 $startdir/src/${pkgname}.profile $startdir/pkg/etc/profile.d/${pkgname}.sh

  # no plugin for x86_64
  if [ "$CARCH" = "i686" ]; then
    mkdir -p $startdir/pkg/usr/lib/mozilla/plugins
    ln -s /opt/java/jre/plugin/i386/ns7/libjavaplugin_oji.so $startdir/pkg/usr/lib/mozilla/plugins
    else echo "no plugin for x86_64"
  fi

  mkdir -p $startdir/pkg/usr/share/licenses/jre
  cp $startdir/pkg/opt/java/jre/COPYRIGHT $startdir/pkg/usr/share/licenses/jre
  cp $startdir/pkg/opt/java/jre/LICENSE $startdir/pkg/usr/share/licenses/jre
  cp $startdir/pkg/opt/java/jre/THIRDPARTYLICENSEREADME.txt $startdir/pkg/usr/share/licenses/jre
}
