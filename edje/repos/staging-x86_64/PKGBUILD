# $Id$
# Maintainer: Ronald van Haren <ronald@archlinux.org>
# Contributor: Gustavo Sverzut Barbieri <barbieri@gmail.com>

pkgname=edje
pkgver=1.7.1
pkgrel=2
pkgdesc="A graphical design and layout library based on Evas"
arch=('i686' 'x86_64')
url="http://www.enlightenment.org"
license=('BSD')
depends=('eina' 'ecore' 'evas' 'eio' 'lua51' 'shared-mime-info' 'embryo')
optdepends=('python2: inkscape2edc')
install=edje.install
options=('!libtool' '!emptydirs')
source=(http://download.enlightenment.org/releases/$pkgname-$pkgver.tar.gz)
sha1sums=('65e983041fa4405f14c23239d35dcce65ac1bbe4')

build() {
  cd $srcdir/$pkgname-$pkgver
  
  export CFLAGS="-fvisibility=hidden -ffunction-sections -fdata-sections ${CFLAGS}"
  export CXXFLAGS="-fvisibility=hidden -ffunction-sections -fdata-sections ${CXXFLAGS}"
  export LDFLAGS="-fvisibility=hidden -ffunction-sections -fdata-sections -Wl,--gc-sections -Wl,--as-needed  ${LDFLAGS}"

  # python2 fix
  sed -i 's_#!/usr/bin/env python_#!/usr/bin/env python2_' utils/inkscape2edc

  ./configure \
    --prefix=/usr \
    --disable-static \
    --enable-ecore-imf \
    --enable-eio \
    --disable-sndfile \
    --disable-remix \
    --disable-vorbisenc \
    --disable-alsa \
    --disable-flac \
    --disable-tests \
    --disable-coverage \
    --enable-amalgamation \
    --disable-doc
  make
}

package(){
  cd $srcdir/$pkgname-$pkgver

  make DESTDIR=$pkgdir install

  # install license files
  install -Dm644 $srcdir/$pkgname-$pkgver/COPYING \
        $pkgdir/usr/share/licenses/$pkgname/COPYING
}
