# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=texinfo
pkgver=7.0.3
pkgrel=1
pkgdesc='GNU documentation system for on-line information and printed output'
arch=(x86_64)
url='https://www.gnu.org/software/texinfo/'
license=(GPL3)
depends=(ncurses gzip perl sh)
optdepends=('perl-archive-zip: EPUB file output via texi2any')
source=(https://ftp.gnu.org/pub/gnu/$pkgname/$pkgname-$pkgver.tar.xz{,.sig}
        texinfo-install.hook
        texinfo-remove.hook)
sha256sums=('74b420d09d7f528e84f97aa330f0dd69a98a6053e7a4e01767eed115038807bf'
            'SKIP'
            '5409fc3aa8a35e8f3d901e96bc50c986f204d90ce6bface0a7d79d99b487f801'
            '913ca8aac84386399b0a83ed0f6b04b5e6322da62f5c1d7ed31e1050ed37c1a9')
validpgpkeys=('EAF669B31E31E1DECBD11513DDBC579DAB37FBA9') # Gavin Smith

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

check() {
  make -C $pkgname-$pkgver check
}

package() {
  make -C $pkgname-$pkgver DESTDIR="$pkgdir" install

  install -dm755 "$pkgdir"/usr/share/libalpm/hooks/
  install -m644 texinfo-{install,remove}.hook "$pkgdir"/usr/share/libalpm/hooks/
}
