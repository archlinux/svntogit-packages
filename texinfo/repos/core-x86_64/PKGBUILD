# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=texinfo
pkgver=6.3
pkgrel=2
pkgdesc='GNU documentation system for on-line information and printed output'
arch=('i686' 'x86_64')
url='http://www.gnu.org/software/texinfo/'
license=('GPL3')
groups=('base' 'base-devel')
depends=('ncurses' 'gzip' 'perl' 'sh')
source=(https://ftp.gnu.org/pub/gnu/$pkgname/$pkgname-$pkgver.tar.xz{,.sig}
        texinfo-6.0-disable-failing-info-test.patch
        texinfo-install.hook
        texinfo-remove.hook)
md5sums=('32baefe5c7080dfb512a4eac5ce67b2a'
         'SKIP'
         '8bfaa21315a8a6184811de8478005d22'
         '9a98fb162650ff3065f9a4ebec22db36'
         '5590fb3ef9ec309c98f5a4d85eca49da')
validpgpkeys=('EAF669B31E31E1DECBD11513DDBC579DAB37FBA9') # Gavin Smith

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../texinfo-6.0-disable-failing-info-test.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

check() {
  make -C $pkgname-$pkgver check
}

package() {
  make -C $pkgname-$pkgver DESTDIR="$pkgdir" install

  install -dm755 "$pkgdir"/usr/share/libalpm/hooks/
  install -m644 texinfo-{install,remove}.hook "$pkgdir"/usr/share/libalpm/hooks/
}
