# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=texinfo
pkgver=6.6
pkgrel=1
pkgdesc='GNU documentation system for on-line information and printed output'
arch=(x86_64)
url='http://www.gnu.org/software/texinfo/'
license=(GPL3)
groups=(base base-devel)
depends=(ncurses gzip perl sh)
source=(https://ftp.gnu.org/pub/gnu/$pkgname/$pkgname-$pkgver.tar.xz{,.sig}
        texinfo-install.hook
        texinfo-remove.hook)
md5sums=('5231da3e6aa106cd0532b8609e5b3702'
         'SKIP'
         '9a98fb162650ff3065f9a4ebec22db36'
         '5590fb3ef9ec309c98f5a4d85eca49da')
validpgpkeys=('EAF669B31E31E1DECBD11513DDBC579DAB37FBA9') # Gavin Smith

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

check() {
  make -C $pkgname-$pkgver check
}

package() {
  make -C $pkgname-$pkgver DESTDIR="$pkgdir" install

  install -dm755 "$pkgdir"/usr/share/libalpm/hooks/
  install -m644 texinfo-{install,remove}.hook "$pkgdir"/usr/share/libalpm/hooks/
}
