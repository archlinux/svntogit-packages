# Maintainer: AndyRTR <andyrtr@archlinux.org>

pkgname=mythes-de
_date=20180514
pkgver=${_date}
pkgrel=1
pkgdesc="German thesaurus"
arch=(any)
url="http://www.openthesaurus.de/"
license=('LGPL')
optdepends=('libmythes: offers thesaurus library functions')
source=(#http://www.openthesaurus.de/export/Deutscher-Thesaurus.oxt - changes every day
        https://sources.archlinux.org/other/$pkgname/Deutscher-Thesaurus-${_date}.oxt
        # alternative source with most times older snapshot
        #http://extensions.libreoffice.org/extension-center/german-de-de-frami-dictionaries/releases/2011.06.03/dict-de_de-frami_2011-06-03.oxt
)
sha256sums=('c3ae10f88148d8967f4b28e6df43675ec0385291a636f5d96cec4eec9d1116da')

# source PKGBUILD && mksource
mksource() {
	mkdir /tmp/$pkgname-${_date}
	pushd /tmp/$pkgname-${_date}
	wget http://www.openthesaurus.de/export/Deutscher-Thesaurus.oxt
	mv Deutscher-Thesaurus.oxt Deutscher-Thesaurus-${_date}.oxt
	popd
}

package() {
  cd "$srcdir"
  install -dm755 ${pkgdir}/usr/share/mythes
  cp -p th_de_DE_v2.* $pkgdir/usr/share/mythes

  pushd $pkgdir/usr/share/mythes/
  de_DE_aliases="de_AT de_BE de_CH de_LI de_LU"
  for lang in $de_DE_aliases; do
        ln -s th_de_DE_v2.idx "th_"$lang"_v2.idx"
        ln -s th_de_DE_v2.dat "th_"$lang"_v2.dat"
  done
  popd

  # the symlinks
  install -dm755 ${pkgdir}/usr/share/myspell/dicts
  pushd $pkgdir/usr/share/myspell/dicts
    for file in $pkgdir/usr/share/mythes/*; do
      ln -sv /usr/share/mythes/$(basename $file) .
    done
  popd
}
