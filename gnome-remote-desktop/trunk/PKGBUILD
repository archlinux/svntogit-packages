# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=gnome-remote-desktop
pkgver=42.1
pkgrel=1
pkgdesc="GNOME Remote Desktop server"
url="https://wiki.gnome.org/Projects/Mutter/RemoteDesktop"
arch=(x86_64)
license=(GPL2)
depends=(cairo freerdp glib2 pipewire systemd libvncserver libsecret libnotify
         dconf fuse3 libxkbcommon libepoxy)
makedepends=(git meson ffnvcodec-headers)
checkdepends=(xorg-server-xvfb python-dbus python-gobject mutter wireplumber)
groups=(gnome)
options=(debug)
_commit=7b18a7fcf58d170a70788c8126e3c2e7b4c19509  # tags/42.1^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-remote-desktop.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

_check() (
  mkdir -p -m 700 "${XDG_CONFIG_HOME:=$PWD/config-dir}"
  mkdir -p -m 700 "${XDG_RUNTIME_DIR:=$PWD/runtime-dir}"
  glib-compile-schemas "${GSETTINGS_SCHEMA_DIR:=$PWD/build/src}"
  export XDG_CONFIG_HOME XDG_RUNTIME_DIR GSETTINGS_SCHEMA_DIR

  export GSETTINGS_BACKEND=keyfile
  gsettings set org.gnome.desktop.remote-desktop.vnc enable true

  pipewire &
  _p1=$!

  wireplumber &
  _p2=$!

  trap "kill $_p1 $_p2; wait" EXIT

  meson test -C build --print-errorlogs -t 3
)

check() {
  dbus-run-session bash -c "$(declare -f _check); _check"
}

package() {
  meson install -C build --destdir "$pkgdir"
}
