# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-pairs
pkgver=14.12.1
pkgrel=1
pkgdesc="A memory and pairs game for KDE"
url="https://projects.kde.org/projects/kde/kdeedu/pairs"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/applications/${pkgver}/src/pairs-${pkgver}.tar.xz")
sha1sums=('16e737b8a168420b3f6782b093f610d4f3bae2b8')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../pairs-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
