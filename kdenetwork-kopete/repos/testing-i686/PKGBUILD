# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdenetwork-kopete
pkgver=14.12.3
pkgrel=2
pkgdesc='Instant Messenger'
url='http://kde.org/applications/internet/kopete/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdenetwork')
depends=('kdebase-runtime' 'kdepimlibs' 'qca-ossl' 'libotr' 'libidn'
         'qimageblitz' 'libgadu' 'jsoncpp')
makedepends=('cmake' 'automoc4' 'boost' 'kdepim-libkdepim' 'mediastreamer')
optdepends=('mediastreamer: jingle support')
install=${pkgname}.install
source=("http://download.kde.org/stable/applications/${pkgver}/src/kopete-${pkgver}.tar.xz" 'mediastreamer-2.11.patch')
sha1sums=('7582f0ee8198fa62a5f02c6e532b993258df6fd8'
          '7999bb62c2c228ef1ee9056e4f1b910b8cb02fcf')

prepare() {
  mkdir build

# fix compilation with mediastreamer 2.11
  cd kopete-$pkgver
  patch -p1 -i "$srcdir"/mediastreamer-2.11.patch
}

build() {
  cd build
  cmake ../kopete-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON \
    -DWITH_Libmsn=OFF
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
