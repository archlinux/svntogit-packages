# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdenetwork-kopete
pkgver=16.04.2
pkgrel=1
pkgdesc='Instant Messenger'
url='http://kde.org/applications/internet/kopete/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdenetwork')
depends=('kdebase-runtime' 'libotr' 'qimageblitz' 'jsoncpp')
makedepends=('cmake' 'automoc4' 'boost' 'mediastreamer' 'libgadu')
optdepends=('mediastreamer: jingle support' 'libgadu: Gadu-Gadu protocol')
source=("http://download.kde.org/stable/applications/${pkgver}/src/kopete-${pkgver}.tar.xz")
sha1sums=('6666e0c2b2798719232433734a0b6872d326cc25')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kopete-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON \
    -DWITH_Libmsn=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
