# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdenetwork-kopete
pkgver=4.11.2
pkgrel=1
pkgdesc='Instant Messenger'
url='http://kde.org/applications/internet/kopete/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdenetwork')
depends=('kdebase-runtime' 'kdepimlibs' 'qca-ossl' 'libotr3' 'libmsn' 'libidn'
         'qimageblitz' 'libgadu' 'mediastreamer' 'jsoncpp')
makedepends=('cmake' 'automoc4' 'boost')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/kopete-${pkgver}.tar.xz"
        'libotr3.patch'
        'mediastreamer29.patch')
sha1sums=('d7616376d7488050ce2c6ec6b7a1f4010fe91084'
          '9c3b0ee15538fbfa36aa0a4748b1f6b5a7905384'
          '317683a5c2acd0a0058c5ab42f6006e9db511ef3')

prepare() {
  cd kopete-${pkgver}
  patch -p2 -i "${srcdir}"/libotr3.patch
  patch -p1 -i "${srcdir}"/mediastreamer29.patch
}

build() {
  mkdir build
  cd build
  cmake ../kopete-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
