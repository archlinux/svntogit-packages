# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdenetwork-kopete
pkgver=15.04.1
pkgrel=1
pkgdesc='Instant Messenger'
url='http://kde.org/applications/internet/kopete/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdenetwork')
depends=('kdebase-runtime' 'kdepimlibs' 'qca-ossl' 'libotr' 'qimageblitz' 'jsoncpp')
makedepends=('cmake' 'automoc4' 'boost' 'kdepim-libkdepim' 'mediastreamer' 'libgadu' 'kdepim-kleopatra')
optdepends=('mediastreamer: jingle support' 'libgadu: Gadu-Gadu protocol')
install=${pkgname}.install
source=("http://download.kde.org/stable/applications/${pkgver}/src/kopete-${pkgver}.tar.xz")
sha1sums=('18d3061ecfc7e287408d3842ed690e128520fd40')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kopete-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON \
    -DWITH_Libmsn=OFF
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
