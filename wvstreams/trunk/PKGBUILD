# $Id$
# Maintainer:
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=wvstreams
pkgver=4.6.1
pkgrel=2
pkgdesc="A network programming library written in C++"
arch=('i686' 'x86_64')
url="http://alumnit.ca/wiki/?page=WvStreams"
license=('LGPL')
depends=('zlib' 'pam' 'xplc' "openssl" 'readline')
source=(http://wvstreams.googlecode.com/files/${pkgname}-${pkgver}.tar.gz)
md5sums=('2760dac31a43d452a19a3147bfde571c')
options=('!makeflags')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr \
     --sysconfdir=/etc \
     --localstatedir=/var \
     --without-dbus \
     --with-pam \
     --with-openssl \
     --without-tcl \
     --without-qt || return 1
  make COPTS="$CFLAGS -fPIC" CXXOPTS="$CXXFLAGS -fPIC -fpermissive" VERBOSE=1 || return 1
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install || return 1
  
  # FS#15974
  # --without-dbus still installs the pkg-config file
  rm ${pkgdir}/usr/lib/pkgconfig/libwvdbus.pc
  # --without-qt still installs the pkg-config file
  rm ${pkgdir}/usr/lib/pkgconfig/libwvqt.pc
}
