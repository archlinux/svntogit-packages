# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gstreamer0.10
pkgver=0.10.28
pkgrel=1
pkgdesc="GStreamer Multimedia Framework"
arch=('i686' 'x86_64')
license=('LGPL')
url="http://gstreamer.freedesktop.org/"
depends=('libxml2>=2.7.6' 'glib2>=2.22.4')
makedepends=('intltool' 'pkgconfig' 'gtk-doc' 'gobject-introspection')
options=('!libtool')
source=(${url}/src/gstreamer/gstreamer-${pkgver}.tar.bz2)
sha256sums=('61613ec7574d22230e8cc893f91b66faec6b21288a22d2f21c9d9eeb7667eb2c')

build() {
  cd "${srcdir}/gstreamer-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib \
	--with-package-name="GStreamer (Archlinux)" \
	--with-package-origin="http://www.archlinux.org/" || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
  cd "${pkgdir}/usr/bin"

  #Remove unversioned gst-* binaries to get rid of conflicts
  for bins in `ls *-0.10`; do
    rm -f ${bins/-0.10/}
  done
}
