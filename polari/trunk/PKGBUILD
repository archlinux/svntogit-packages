# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=polari
pkgver=43.0
pkgrel=1
pkgdesc="An IRC Client for GNOME"
url="https://wiki.gnome.org/Apps/Polari"
arch=(x86_64)
license=(GPL)
depends=(gjs gtk4 telepathy-glib telepathy-idle telepathy-mission-control
         telepathy-logger gspell libsoup3 libadwaita)
makedepends=(gobject-introspection yelp-tools appstream-glib git meson)
groups=(gnome-extra)
_commit=0cf049c1c5704cf5fa0a4b80194860e2c4e433a9  # tags/43.0^0
source=("git+https://gitlab.gnome.org/GNOME/polari.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd polari
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd polari
}

build() {
  arch-meson polari build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 sts=-1 et:
