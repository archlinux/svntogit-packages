# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-resolvelib
pkgname=(python-resolvelib python2-resolvelib)
pkgver=0.5.2
pkgrel=1
pkgdesc="Resolve abstract dependencies into concrete ones"
url="https://github.com/sarugaku/resolvelib"
license=('ISC')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-commentjson' 'python-pytest-runner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/sarugaku/resolvelib/archive/$pkgver.tar.gz")
sha512sums=('b37194e19484e469ca84f813a9318a29b35871dbe32d56cdc6260819f78cfa42d12f9c10b1e7faff7a88945ff50254d2a804cadb89d905ced20fb7755d4c9500')

build() {
  cd resolvelib-$pkgver
  python setup.py build
  python2 setup.py build
}

check() {
  cd resolvelib-$pkgver
  python setup.py pytest
}

package_python-resolvelib() {
  depends=('python')

  cd resolvelib-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

package_python2-resolvelib() {
  depends=('python2')

  cd resolvelib-$pkgver
  python2 setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
