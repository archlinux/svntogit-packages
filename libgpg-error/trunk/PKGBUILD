# $Id$
# Maintainer: judd <jvinet@zeroflux.org>

pkgname=libgpg-error
pkgver=1.9
pkgrel=3
pkgdesc="Support library for libgcrypt"
arch=(i686 x86_64)
url="http://www.gnupg.org"
license=('LGPL')
depends=('glibc' 'sh')
options=(!libtool)
source=(ftp://ftp.gnupg.org/gcrypt/libgpg-error/${pkgname}-${pkgver}.tar.bz2)
	#ftp://ftp.franken.de/pub/crypt/mirror/ftp.gnupg.org/gcrypt/libgpg-error/${pkgname}-${pkgver}.tar.bz2)
md5sums=('521b98aa9395e7eaf0ef2236233a0796')


build() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}/" install

  # Move dynamic libraries to /lib
  install -d -m755 "${pkgdir}"/lib/
  mv "${pkgdir}"/usr/lib/libgpg-error.so* "${pkgdir}"/lib/
  ln -sf /lib/libgpg-error.so "${pkgdir}"/usr/lib/libgpg-error.so
}
