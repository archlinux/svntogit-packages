# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgbase=pipewire-media-session
pkgname=(pipewire-media-session pipewire-media-session-docs)
pkgver=0.4.1
pkgrel=2
epoch=1
pkgdesc="Example session manager for PipeWire"
url="https://gitlab.freedesktop.org/pipewire/media-session"
license=(MIT)
arch=(x86_64)
makedepends=(git meson doxygen graphviz 'pipewire>=0.3.39' systemd dbus
             alsa-lib)
_commit=e5d5cf2404786af8bcc40bdb8a2962bef4ec18b6  # tags/0.4.1
source=("$pkgbase::git+https://gitlab.freedesktop.org/pipewire/media-session.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgbase
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgbase
}

build() {
  local meson_options=(
    -D docs=enabled
    -D with-module-sets=[]
  )

  arch-meson $pkgbase build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

package_pipewire-media-session() {
  depends=('pipewire>=0.3.39' libpipewire-0.3.so libsystemd.so libdbus-1.so
           libasound.so)
  optdepends=('pipewire-media-session-docs: Documentation')
  provides=(pipewire-session-manager)
  conflicts=(wireplumber)
  install=pipewire-media-session.install

  meson install -C build --destdir "$pkgdir"

  _pick docs "$pkgdir"/usr/share/doc

  install -Dt "$pkgdir/usr/share/doc/$pkgname" -m644 $pkgbase/{NEWS,README}*
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 $pkgbase/COPYING
}

package_pipewire-media-session-docs() {
  pkgdesc+=" - documentation"

  mv docs/* "$pkgdir"

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 $pkgbase/COPYING
}

# vim:set sw=2 sts=-1 et:
