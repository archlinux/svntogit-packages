# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kcompletion
pkgver=5.55.0
pkgrel=1
pkgdesc='Text completion helpers and widgets'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(kwidgetsaddons kconfig)
makedepends=(extra-cmake-modules qt5-tools clang python-pyqt5 doxygen sip)
optdepends=('python-pyqt5: for the Python bindings')
groups=(kf5)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('224761954421b2e25c2b9651c7e730da9fb7a569674c399c867a9f895d3ad523'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
