# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Manolis Tzanidakis

pkgname=perl-net-ssleay
_cpanname=Net-SSLeay
pkgver=1.81
pkgrel=2
pkgdesc="Perl extension for using OpenSSL"
arch=('i686' 'x86_64')
license=('custom:BSD')
url="http://search.cpan.org/dist/${_cpanname}/"
depends=('openssl')
options=(!emptydirs)
replaces=('net-ssleay')
provides=('net-ssleay')
source=("http://www.cpan.org/authors/id/M/MI/MIKEM/${_cpanname}-${pkgver}.tar.gz")
md5sums=('71932ce34d4db44de8d00399c3405792')

prepare() {
  cd ${srcdir}/${_cpanname}-${pkgver}

  sed -i \
      -e "/\$opts->{optimize} = '-O2 -g';/d" \
      inc/Module/Install/PRIVATE/Net/SSLeay.pm
}

build() {
  cd ${srcdir}/${_cpanname}-${pkgver}

  export OPTIMIZE="$CFLAGS"
  export OPENSSL_PREFIX=/usr

  # install module in vendor directories.
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL -- INSTALLDIRS=vendor 
  make
}

package() {
  cd ${srcdir}/${_cpanname}-${pkgver}

  make install DESTDIR=${pkgdir} 
  install -D -m644 README "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

# template start; name=perl-binary-module-dependency; version=1;
if [[ $(find "$pkgdir/usr/lib/perl5/" -name "*.so") ]]; then
	_perlver_min=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]);')
	_perlver_max=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]+1);')
	depends+=("perl>=$_perlver_min" "perl<$_perlver_max")
fi
# template end;
}
