# $Id$
# Maintainer : Daniel Isenmann <daniel@archlinux.org>
# Contributor: György Balló <ballogy@freestart.hu>
pkgname=gio-sharp
pkgver=0.3
_pkgver=0.3-0-31b4926
_git=31b4926
pkgrel=2
pkgdesc="a branch of the official gtk-sharp/gio to get gio-sharp building on gtk-sharp 2.12"
arch=('any')
url="http://github.com/mono/gio-sharp"
license=('MIT')
depends=('gtk-sharp-2>=2.12')
source=(mono-$pkgname-$_pkgver.zip::https://github.com/mono/gio-sharp/zipball/$pkgver)
options=('!makeflags')
md5sums=('5df902082999bb33273517ceed790144')

build() {
  cd "$srcdir/mono-$pkgname-${_git}"

  ./autogen-2.22.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd "$srcdir/mono-$pkgname-${_git}"

  make DESTDIR="$pkgdir" install

  install -m755 -d "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname"
}
