# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-xbiff
pkgver=1.0.4
pkgrel=3
pkgdesc="Watch mailboxes for new message delivery"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libxaw' 'libxext')
makedepends=('xorg-util-macros' 'xbitmaps')
source=(https://xorg.freedesktop.org/archive/individual/app/xbiff-${pkgver}.tar.bz2{,.sig})
sha512sums=('0fb920a08640ec59f23d8a8a47bd244565dfbaff7a21c38a8a2abe1edb5eacabc970b5dbdc409163fc444fde5b75a2dc4e0114312938efec5b77228f3bf785ac'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # Alan Coopersmith <alan.coopersmith@oracle.com>

build() {
  cd "xbiff-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "xbiff-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
