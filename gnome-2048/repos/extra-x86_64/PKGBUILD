# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=gnome-2048
pkgver=3.34.5
pkgrel=1
pkgdesc="Obtain the 2048 tile"
url="https://wiki.gnome.org/Apps/2048"
arch=(x86_64)
license=(GPL3)
depends=(clutter-gtk libgee libgnome-games-support)
makedepends=(meson vala yelp-tools appstream-glib git)
_commit=0a06a44f89b8f63398417bef6d5961793cad62a5  # tags/3.34.5^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-2048.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
