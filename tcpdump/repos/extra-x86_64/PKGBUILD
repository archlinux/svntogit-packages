# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=tcpdump
pkgver=4.99.3
pkgrel=1
pkgdesc='Powerful command-line packet analyzer'
url="https://www.tcpdump.org/"
arch=(x86_64)
license=(BSD)
depends=(glibc)
makedepends=(cmake libpcap libcap-ng openssl)
options=(debug)
source=(https://www.tcpdump.org/release/$pkgname-$pkgver.tar.gz{,.sig})
sha512sums=('e1442e923d89f367cfe403dc460d0d46e19a241470d56b30b7a411bedd8d21a78c428f20ffe725cbb4fa5068f7dcc5c93d206e1a8d53feb9bccc6f0cda8bf0f9'
            'SKIP')
b2sums=('8e8adda6df40f3a79cbeb80bd01d43834e0b3ec44c8227bda2260b43ea0852f76beb02199120cca16adae0d793841cf684e7e276e520f49479a1b04ab1dc4821'
        'SKIP')
validpgpkeys=('1F166A5742ABB9E0249A8D30E089DEF1D9C15D0D') # The Tcpdump Group (Package signing key) <release@tcpdump.org>

build() {
  local cmake_options=(
    -DCMAKE_INSTALL_PREFIX=/usr
    -DCMAKE_BUILD_TYPE=None
    -Wno-dev
    -B build
    -S $pkgname-$pkgver
  )

  cmake "${cmake_options[@]}"
  make VERBOSE=1 -C build
}

check() {
  make VERBOSE=1 check -C build
}

package() {
  depends+=(
    libpcap libpcap.so
    libcap-ng libcap-ng.so
    openssl libcrypto.so
  )

  make DESTDIR="$pkgdir" install -C build
  install -vDm 644 $pkgname-$pkgver/LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
