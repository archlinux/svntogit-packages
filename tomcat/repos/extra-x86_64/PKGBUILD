# $Id$
# Maintainer: simo <simo@archlinux.org>
pkgname=tomcat
pkgver=5.5.23
pkgrel=2
pkgdesc="Servlet container for Java Servlet and JavaServer Pages"
arch=('i686' 'x86_64')
url="http://tomcat.apache.org/"
license=("APACHE")
depends=('java-runtime')
makedepends=('jdk')
backup=(opt/tomcat/conf/server.xml opt/tomcat/conf/tomcat-users.xml \
        etc/conf.d/tomcat)
install=tomcat.install
source=(http://archive.apache.org/dist/tomcat/tomcat-5/v$pkgver/bin/apache-tomcat-$pkgver.tar.gz \
        tomcat tomcat.conf.d)
md5sums=('7a4cc2e00c7d2c9d9c4437ede337f832' 'c93bad359c8d2ffd6b31947a81d29fff'\
         '4da263803378b208144183c9f7df66a7')

build() {
  # build jsvc
  cd $startdir/src/apache-tomcat-$pkgver/bin
  tar xzf jsvc.tar.gz
  cd jsvc-src
  sh configure --with-java=/opt/java
  make || return 1
  cp jsvc ..
  
  # get rid of some cruft
  cd $startdir/src/apache-tomcat-$pkgver
  rm -f LICENSE NOTICE RELEASE-NOTES RUNNING.txt
  rm -fr bin/*.exe bin/*.bat bin/*.tar.gz bin/jsvc-src
  
  # install everything
  mkdir -p $startdir/pkg/opt/tomcat
  cp -R * $startdir/pkg/opt/tomcat
  install -D -m755 $startdir/src/tomcat $startdir/pkg/etc/rc.d/tomcat
  install -D -m755 $startdir/src/tomcat.conf.d $startdir/pkg/etc/conf.d/tomcat
  chgrp -R 66 $startdir/pkg/opt/tomcat/conf $startdir/pkg/opt/tomcat/work
  chmod 0660 $startdir/pkg/opt/tomcat/conf/{*.xml,*.policy,*.properties,/Catalina/localhost/*.xml}
  chmod 775 $startdir/pkg/opt/tomcat/conf $startdir/pkg/opt/tomcat/work
}
