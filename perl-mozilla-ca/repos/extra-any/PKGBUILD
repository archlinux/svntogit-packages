# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Justin Davis (juster) <jrcd83@gmail.com>

pkgname=perl-mozilla-ca
pkgver=20160104
pkgrel=2
pkgdesc="Mozilla's CA cert bundle in PEM format"
arch=('any')
license=('GPL2' 'LGPL2.1')
options=('!emptydirs')
depends=('perl')
url='https://metacpan.org/release/Mozilla-CA'
source=("http://search.cpan.org/CPAN/authors/id/A/AB/ABH/Mozilla-CA-$pkgver.tar.gz")
md5sums=('1b91edb15953a8188f011ab5ff433300')

build() (
  cd Mozilla-CA-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
)

check() (
  cd Mozilla-CA-$pkgver
  make test
)

package() (
  cd Mozilla-CA-$pkgver
  make install DESTDIR="$pkgdir"
)

# vim:set ts=2 sw=2 et:
