# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kpkpass
pkgver=22.04.0
pkgrel=1
pkgdesc='Apple Wallet Pass reader'
arch=(x86_64)
url='https://kontact.kde.org'
license=(LGPL)
depends=(karchive)
makedepends=(extra-cmake-modules qt5-tools doxygen)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('03036288aa35f93f77da3fad3a8a06021d6e85e1689de6584a7aa6fe7842673a'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
options=(debug)

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
