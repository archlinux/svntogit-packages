# $Id$
# Maintainer: Angel Velasquez <angvp@archlinux.org> 
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>

pkgname=serf
pkgver=1.3.1
pkgrel=2
pkgdesc="High-performance asynchronous HTTP client library"
arch=('i686' 'x86_64')
url="http://code.google.com/p/serf/"
license=('Apache')
depends=('apr-util')
makedepends=('scons')
options=('!libtool' '!staticlibs')
source=(http://serf.googlecode.com/files/${pkgname}-${pkgver}.tar.bz2 'serf-ssl-allocator-v2.patch')
md5sums=('da5aca0cad19fd9c19129c3f8f7393dd'
         'a0ff486ee26f86cb2825decfd1dc8355')

prepare() {
  mv serf-ssl-allocator-v2.patch ${pkgname}-${pkgver} 
  cd ${pkgname}-${pkgver}
  patch -Np0 -i serf-ssl-allocator-v2.patch
}

build() {
  cd ${pkgname}-${pkgver}
  scons PREFIX=/usr
}

check() {
  cd ${pkgname}-${pkgver}
  scons check
}

package() {
  cd ${pkgname}-${pkgver}
  install -d "${pkgdir}/usr"
  scons PREFIX="${pkgdir}/usr" install
}
