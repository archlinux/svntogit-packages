# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=telepathy-kde-integration-module
_pkgname=ktp-kded-module
pkgver=17.07.80
pkgrel=1
pkgdesc='Sits in KDED and takes care of various bits of system integration like setting user to auto-away or handling connection errors'
arch=('i686' 'x86_64')
url='https://community.kde.org/Real-Time_Communication_and_Collaboration'
license=('GPL')
groups=('kde-applications' 'kdenetwork' 'telepathy-kde')
depends=('telepathy-kde-common-internals' 'kidletime')
makedepends=('extra-cmake-modules' 'kdoctools' 'python')
source=("https://download.kde.org/unstable/applications/${pkgver}/src/$_pkgname-${pkgver}.tar.xz"{,.sig})
sha256sums=('6e5039a2197a906c7749d57cfb1dc6ca32345205289a747e801015d05b28a640'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$_pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
