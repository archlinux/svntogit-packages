# $Id$
# Maintainer: Alexander Baldeck <alexander@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
pkgname=xf86-video-savage
pkgver=2.3.0
pkgrel=1
pkgdesc="X.org savage video driver"
_mesaver="7.4.4"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=("libgl>=${_mesaver}" 'expat')
makedepends=('pkgconfig' 'xorg-server>=1.6.0' "mesa>=${_mesaver}" 'xf86driproto' 'glproto>=1.4.9' 'dri2proto>=1.99.3')
conflicts=('xorg-server<1.6.0')
options=(!libtool)
groups=('xorg-video-drivers')
source=(${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2
	http://downloads.sourceforge.net/sourceforge/mesa3d/MesaLib-${_mesaver}.tar.bz2
	mesa-7.1-link-shared.patch
        LICENSE)
md5sums=('fc21c0b76c9403fc2cdc7d924abaa461' 'b66528d314c574dccbe0ed963cac5e93'\
         'f0baa948d9810f268413111ee439d24b' '0f485840d3cee0a8d9952c0cd34f64c3')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --enable-dri || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1

  cd "${srcdir}/Mesa-${_mesaver}"
  patch -Np1 -i "${srcdir}/mesa-7.1-link-shared.patch" || return 1
  ./configure --prefix=/usr \
    --with-dri-driverdir=/usr/lib/xorg/modules/dri \
    --with-dri-drivers=savage \
    --enable-glx-tls \
    --with-driver=dri \
    --enable-xcb \
    --disable-glu \
    --disable-glut \
    --disable-glw || return 1
  make || return 1
  cd src/mesa/drivers/dri || return 1
  make DESTDIR="${pkgdir}" install || return 1
  rm -rf "${pkgdir}/usr/lib/pkgconfig"
  rm -rf "${pkgdir}/usr/include"
  rm -f "${pkgdir}/usr/lib/xorg/modules/dri/libdricore.so"
  install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
