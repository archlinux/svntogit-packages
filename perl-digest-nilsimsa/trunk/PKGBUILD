# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: kevin <kevin@archlinux.org>
# Contributor: Manolis Tzanidakis

pkgname=perl-digest-nilsimsa
_realname=Digest-Nilsimsa
pkgver=0.06
pkgrel=13
pkgdesc="Perl version of Nilsimsa code"
arch=('i686' 'x86_64')
license=('LGPL')
url="http://search.cpan.org/dist/${_realname}/"
depends=('perl')
replaces=('digest-nilsimsa')
provides=('digest-nilsimsa')
options=('!emptydirs')
source=(http://www.cpan.org/authors/id/V/VI/VIPUL/${_realname}-${pkgver}.tar.gz)
md5sums=('08e940bd7f5d1167ef3fd1aa7ce234d7')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make test
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make install DESTDIR="${pkgdir}"
# template start; name=perl-binary-module-dependency; version=1;
if [[ $(find "$pkgdir/usr/lib/perl5/" -name "*.so") ]]; then
	_perlver_min=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]);')
	_perlver_max=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]+1);')
	depends+=("perl>=$_perlver_min" "perl<$_perlver_max")
fi
# template end;
}
# vim: ts=2 sw=2 et ft=sh
