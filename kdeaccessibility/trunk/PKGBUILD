# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kdeaccessibility
pkgname=('kdeaccessibility-jovie'
         'kdeaccessibility-kaccessible'
         'kdeaccessibility-kmag'
         'kdeaccessibility-kmousetool'
         'kdeaccessibility-kmouth')
pkgver=4.7.3
pkgrel=1
arch=('i686' 'x86_64')
url='http://accessibility.kde.org'
license=('GPL' 'FDL')
groups=('kde' 'kdeaccessibility')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'kdelibs' 'speech-dispatcher')
source=("http://download.kde.org/stable/${pkgver}/src/${pkgbase}-${pkgver}.tar.bz2")
sha1sums=('9695d2b8b6f55266e3f76ebf05543f23857b9520')

build() {
	cd ${srcdir}
	mkdir build
	cd build
	cmake ../${pkgbase}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package_kdeaccessibility-jovie() {
	pkgdesc='A text to speech application'
	depends=('kdebase-runtime' 'speech-dispatcher')
    replaces=('kdeaccessibility-kttsd')
	install='kdeaccessibility.install'
	cd $srcdir/build/jovie
	make DESTDIR=$pkgdir install
	cd $srcdir/build/jovie/doc
	make DESTDIR=$pkgdir install
}

package_kdeaccessibility-kaccessible() {
    pkgdesc='Provides accessibility services like focus tracking and a screenreader'
    depends=('kdebase-runtime' 'speech-dispatcher')
	install='kdeaccessibility.install'
    cd $srcdir/build/kaccessible
    make DESTDIR=$pkgdir install
}

package_kdeaccessibility-kmag() {
	pkgdesc='Screen Magnifier'
	depends=('kdebase-runtime')
	url="http://kde.org/applications/utilities/kmag/"
	install='kdeaccessibility.install'
	cd $srcdir/build/kmag
	make DESTDIR=$pkgdir install
	cd $srcdir/build/kmag/doc
	make DESTDIR=$pkgdir install
}

package_kdeaccessibility-kmousetool() {
	pkgdesc='Clicks the mouse for you, reducing the effects of RSI'
	depends=('kdebase-runtime')
	url="http://kde.org/applications/utilities/kmousetool/"
	install='kdeaccessibility.install'
	cd $srcdir/build/kmousetool
	make DESTDIR=$pkgdir install
	cd $srcdir/build/kmousetool/doc
	make DESTDIR=$pkgdir install
}

package_kdeaccessibility-kmouth() {
	pkgdesc='Speech Synthesizer Frontend'
	depends=('kdebase-runtime')
	url="http://kde.org/applications/utilities/kmouth/"
	install='kdeaccessibility.install'
	cd $srcdir/build/kmouth
	make DESTDIR=$pkgdir install
	cd $srcdir/build/kmouth/doc
	make DESTDIR=$pkgdir install
}
