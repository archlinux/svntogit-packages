# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=qt5-svg
_qtver=5.14.0-beta3
pkgver=${_qtver/-/}
pkgrel=3
arch=('x86_64')
url='https://www.qt.io'
license=('GPL3' 'LGPL3' 'FDL' 'custom')
pkgdesc='Classes for displaying the contents of SVG files'
depends=('qt5-base')
groups=('qt' 'qt5')
_pkgfqn="${pkgname/5-/}-everywhere-src-${_qtver}"
source=("https://download.qt.io/development_releases/qt/${pkgver%.*}/${_qtver}/submodules/${_pkgfqn}.tar.xz"
         qtbug-78200.patch::"https://code.qt.io/cgit/qt/qtsvg.git/patch/?id=c3ba5762"
         qtbug-79933a.patch::"https://code.qt.io/cgit/qt/qtsvg.git/patch/?id=beb0d741"
         qtbug-79933b.patch::"https://code.qt.io/cgit/qt/qtsvg.git/patch/?id=e489a325")
sha256sums=('1344c124f38a0b19ba8efd275c7e2cb9f16061826fcc0408ff9fa1e0a0e128c8'
            '2b8de41e215df01cda57562a8dcf1ae3fc116fb3327da26a57911f7666203207'
            'aee3a61dbe01d70aa877feb4b821cafad30dd269fcef5b8378e4816a63bdccd6'
            '0b9acbcb9729526233fd50e3ffe69acb3d482f58838b7fd0b2eb70f38ad9891f')

prepare() {
  mkdir -p build

  cd $_pkgfqn
  patch -p1 -i ../qtbug-78200.patch # Fix svg rendering in Plasma https://bugreports.qt.io/browse/QTBUG-78200
  patch -p1 -i ../qtbug-79933a.patch
  patch -p1 -i ../qtbug-79933b.patch
}

build() {
  cd build

  qmake ../${_pkgfqn}
  make
}

package() {
  cd build
  make INSTALL_ROOT="$pkgdir" install

  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  find "$pkgdir/usr/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt5-base "$pkgdir"/usr/share/licenses/${pkgname}
}
