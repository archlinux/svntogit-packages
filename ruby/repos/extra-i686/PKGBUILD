# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>
# Contributor: Jeramy Rutley <jrutley@gmail.com>

pkgname=('ruby' 'ruby-docs')
pkgver=1.9.1_p429
_pkgver=1.9.1-p429
pkgrel=1
pkgdesc="An object-oriented language for quick and easy programming"
arch=('i686' 'x86_64')
url="http://www.ruby-lang.org/en/"
license=('custom')
makedepends=('gdbm' 'db' 'openssl' 'zlib' 'readline' 'tk')
options=('!emptydirs')
source=("ftp://ftp.ruby-lang.org/pub/ruby/1.9/${pkgname}-${_pkgver}.tar.bz2"
        'openssl-1.0.patch')
md5sums=('09df32ae51b6337f7a2e3b1909b26213' 'e22ebf0dbe836f5344682bc5da83dcb5')
sha1sums=('66091777a43939b888fabafddeb659fe5ba0de0d' '17e2e2ebc3a610a1ad71df6de7a9d12f79de5d37')

build() {
  cd "${srcdir}/${pkgname}-${_pkgver}"
  # see http://cvs.fedoraproject.org/viewvc/rpms/ruby/devel/
  patch -p1 -i "${srcdir}/openssl-1.0.patch"
  ./configure --prefix=/usr --enable-shared --enable-pthread
  make
}

package_ruby()
{
  depends=('gdbm' 'db' 'openssl' 'zlib' 'readline')
  provides=('rubygems' 'rake')
  conflicts=('rubygems' 'rake')
  optdepends=('tk: for Ruby/TK')

  cd "${srcdir}/${pkgname}-${_pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

package_ruby-docs()
{
  pkgdesc="Documentation files for ruby"
  #arch=('any')
  depends=('ruby')

  cd "${srcdir}/${pkgname}-${_pkgver}"
  make -j1 DESTDIR="${pkgdir}" install-doc
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/ruby-docs/COPYING"
}
