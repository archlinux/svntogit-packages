# $Id$
# Maintainer:
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>
# Contributor: Jeramy Rutley <jrutley@gmail.com>

pkgbase=ruby
pkgname=('ruby' 'ruby-docs')
pkgver=1.9.2_p180
pkgrel=3
pkgdesc="An object-oriented language for quick and easy programming"
arch=('i686' 'x86_64')
url="http://www.ruby-lang.org/en/"
license=('custom')
makedepends=('openssl' 'tk' 'libffi' 'doxygen' 'graphviz')
options=('!emptydirs' '!makeflags')
source=("ftp://ftp.ruby-lang.org/pub/${pkgbase}/1.9/${pkgbase}-${pkgver//_/-}.tar.bz2")
md5sums=('68510eeb7511c403b91fe5476f250538')
sha1sums=('10824b44c8060c7b9b5afc0b3519a1e9f02f7fe5')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver//_/-}"
  ./configure --prefix=/usr \
      --sysconfdir=/etc \
      --enable-shared \
      --enable-pthread \
      --disable-rpath
  make
}

package_ruby() {
  depends=('openssl' 'libffi')
  provides=('rubygems' 'rake')
  conflicts=('rake')
  optdepends=('tk: for Ruby/TK')

  cd "${srcdir}/${pkgbase}-${pkgver//_/-}"
  make DESTDIR="${pkgdir}" install-nodoc
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_ruby-docs() {
  pkgdesc="Documentation files for ruby"
  depends=('ruby')

  cd "${srcdir}/${pkgbase}-${pkgver//_/-}"
  make DESTDIR="${pkgdir}" install-doc install-capi
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
