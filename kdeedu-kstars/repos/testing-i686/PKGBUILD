# $Id$
# Maintainer: Felix Yan <felixonamrs@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-kstars
pkgver=14.12.3
pkgrel=1
pkgdesc="Desktop Planetarium"
url="http://kde.org/applications/education/kstars/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
depends=('kdebase-runtime' 'wcslib' 'libindi' 'qjson')
makedepends=('cmake' 'automoc4' 'eigen')
install=${pkgname}.install
source=("http://download.kde.org/stable/applications/${pkgver}/src/kstars-${pkgver}.tar.xz")
sha1sums=('b372f26e5202b5c4369b974d126b5fa32d493c24')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../kstars-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_Xplanet=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install

  sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|g' \
    "${pkgdir}"/usr/share/apps/kstars/scripts/supernova_updates_parser.py
}
