# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Christian Hesse <mail@eworm.de>

pkgname=f2fs-tools
pkgver=1.8.0
pkgrel=1
pkgdesc="Tools for Flash-Friendly File System (F2FS)"
arch=('i686' 'x86_64')
url='http://git.kernel.org/cgit/linux/kernel/git/jaegeuk/f2fs-tools.git/about/'
depends=('util-linux')
license=('GPL')
source=("https://git.kernel.org/cgit/linux/kernel/git/jaegeuk/f2fs-tools.git/snapshot/$pkgname-$pkgver.tar.gz")
sha256sums=('d4dbecf55560c548bf0758c9f641d1beec1e960b38cbbc19951195d5144d39ae')

build() {
	cd "${pkgname}-${pkgver}"

	autoreconf -fi
	./configure --prefix=/usr --sbindir=/usr/bin
	make
}

package() {
	cd "${pkgname}-${pkgver}"

	make DESTDIR="${pkgdir}/" sbindir=/usr/bin install
}
