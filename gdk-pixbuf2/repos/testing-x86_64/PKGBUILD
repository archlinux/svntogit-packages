# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
pkgname=gdk-pixbuf2
pkgver=2.22.0
pkgrel=2
pkgdesc="An image loading library for gtk2"
arch=('i686' 'x86_64')
url="http://www.gtk.org/"
license=('GPL2')
depends=('glib2>=2.25.15' 'libpng>=1.4.3' 'libtiff>=3.9.4' 'libjpeg>=8.0.2' 'libx11')
makedepends=('gtk-doc' 'gobject-introspection')
options=('!libtool')
install=gdk-pixbuf2.install
source=(http://download.gnome.org/sources/gdk-pixbuf/2.22/gdk-pixbuf-${pkgver}.tar.bz2)
sha256sums=('b8ff6a308f037af6b501246ad8779a951ca92af975efc2e97c354ea6702667cf')

build() {
  cd "${srcdir}/gdk-pixbuf-${pkgver}"

  ./configure --prefix=/usr \
    --without-libjasper \
    --with-included-loaders=png
  make
}

package() {
  cd "${srcdir}/gdk-pixbuf-${pkgver}"

  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
