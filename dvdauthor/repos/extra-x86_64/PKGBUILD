# $Id$
# Maintainer:
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Travis Willard <travisw@wmpub.ca>
# Contributor: Jaroslaw Swierczynski <swiergot@juvepoland.com>
# Contributor: Nicolai Lissner <nlissne@linux01.gwdg.de>

pkgname=dvdauthor
pkgver=0.7.2
pkgrel=2
pkgdesc="DVD authoring tools"
arch=('x86_64')
url="http://dvdauthor.sourceforge.net/"
license=('GPL')
depends=('libdvdread' 'libmagick6' 'libxml2' 'libpng' 'fribidi')
makedepends=('imagemagick') # for the *-config wrappers
options=('!makeflags')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('3020a92de9f78eb36f48b6f22d5a001c47107826634a785a62dfcd080f612eb7')

build() {
  cd ${pkgname}
  PKG_CONFIG_PATH="/usr/lib/imagemagick6/pkgconfig" \
  ./configure --prefix=/usr \
              --sysconfdir=/etc
  make
}

package() {
  cd ${pkgname}

  make DESTDIR="${pkgdir}" install
}
