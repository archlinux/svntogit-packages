# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Travis Willard <travisw@wmpub.ca>
# Contributor: Jaroslaw Swierczynski <swiergot@juvepoland.com>
# Contributor: Nicolai Lissner <nlissne@linux01.gwdg.de>

pkgname=dvdauthor
pkgver=0.7.1
pkgrel=9
pkgdesc="DVD authoring tools"
arch=('i686' 'x86_64')
url="http://dvdauthor.sourceforge.net/"
license=('GPL')
depends=('libdvdread' 'imagemagick' 'libxml2' 'libpng' 'fribidi')
options=('!makeflags')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz" dvdauthor-fix-build.patch)
md5sums=('2694a5a3ef460106ea3caf0f7f60ff80'
         '3ac58688085e7a1a503d1f895dfce718')

prepare() {
  cd $pkgname
  # Fix build (Debian)
  patch -p1 -i ../dvdauthor-fix-build.patch
}

build() {
  cd "${srcdir}/${pkgname}"

  ./configure --prefix=/usr \
              --sysconfdir=/etc
  make
}

package() {
  cd "${srcdir}/${pkgname}"

  make DESTDIR="${pkgdir}" install
}
