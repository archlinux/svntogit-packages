# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgbase=libsigc++2.0
pkgname=('libsigc++2.0' 'libsigc++2.0-docs')
pkgver=2.2.7
pkgrel=1
arch=(i686 x86_64)
license=('LGPL')
url="http://libsigc.sourceforge.net/"
source=(http://ftp.gnome.org/pub/GNOME/sources/libsigc++/2.2/libsigc++-${pkgver}.tar.bz2)
makedepends=('gcc-libs>=4.5.0')
options=(!libtool !emptydirs)
sha256sums=('2b8cc4670714458cd4fecefb00e7d5d2c717ae9b1963a057e7ebca1cd93baafa')

build() {
  cd "${srcdir}/libsigc++-${pkgver}"
  ./configure --prefix=/usr || return 1
  make || return 1
}

package_libsigc++2.0() {
  pkgdesc="Libsigc++ implements a full callback system for use in widget libraries - V2"
  depends=('gcc-libs>=4.5.0')
  cd "${srcdir}/libsigc++-${pkgver}"
  sed -i -e 's/^doc_subdirs/#doc_subdirs/' Makefile || return 1
  make DESTDIR="${pkgdir}" install || return 1
}

package_libsigc++2.0-docs() {
  pkgdesc="Developer documentation for libsigc++"
  cd "${srcdir}/libsigc++-${pkgver}/docs"
  make DESTDIR="${pkgdir}" install || return 1
}
