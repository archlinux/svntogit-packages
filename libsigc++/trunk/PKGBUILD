# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgbase=libsigc++
pkgname=('libsigc++' 'libsigc++-docs')
pkgver=2.2.11
pkgrel=1
arch=('i686' 'x86_64')
license=('LGPL')
url="http://libsigc.sourceforge.net/"
makedepends=('gcc-libs')
options=('!libtool' '!emptydirs')
source=("http://ftp.gnome.org/pub/GNOME/sources/${pkgbase}/2.2/${pkgbase}-${pkgver}.tar.xz")
sha256sums=('9834045f74f56752c2c6b3cdc195c30ab8314ad22dc8e626d6f67f940f1e4957')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package_libsigc++ () {
  pkgdesc="Libsigc++ implements a full callback system for use in widget libraries - V2"
  depends=('gcc-libs')
  provides=("libsigc++2.0=${pkgver}")
  replaces=('libsigc++2.0')
  conflicts=('libsigc++2.0')

  cd "${srcdir}/libsigc++-${pkgver}"
  sed -i -e 's/^doc_subdirs/#doc_subdirs/' Makefile
  make DESTDIR="${pkgdir}" install
}

package_libsigc++-docs() {
  pkgdesc="Developer documentation for libsigc++"
  provides=("libsigc++2.0-docs=${pkgver}")
  replaces=('libsigc++2.0-docs')
  conflicts=('libsigc++2.0-docs')

  cd "${srcdir}/libsigc++-${pkgver}/docs"
  make DESTDIR="${pkgdir}" install
}
