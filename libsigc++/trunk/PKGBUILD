# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgbase=libsigc++
pkgname=(libsigc++ libsigc++-docs)
pkgver=2.10.3
pkgrel=1
pkgdesc="Callback system for use in widget libraries"
url="https://libsigcplusplus.github.io/libsigcplusplus/"
arch=('x86_64')
license=('LGPL')
depends=(gcc-libs)
makedepends=(git mm-common meson)
options=(!emptydirs)
_commit=88fdb3a14ec67de233fed22646fc9b14c24367f5  # tags/2.10.3^0
source=("git+https://github.com/libsigcplusplus/libsigcplusplus#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd libsigcplusplus
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd libsigcplusplus
}

build() {
  arch-meson libsigcplusplus build -D maintainer-mode=true
  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}

package_libsigc++ () {
  provides=("libsigc++2.0=${pkgver}")
  replaces=('libsigc++2.0')
  conflicts=('libsigc++2.0')

  DESTDIR="$pkgdir" meson install -C build

  mkdir -p docs/usr/share
  mv "$pkgdir"/usr/share/{devhelp,doc} docs/usr/share
}

package_libsigc++-docs() {
  pkgdesc+=" (documentation)"
  depends=()
  provides=("libsigc++2.0-docs=${pkgver}")
  replaces=('libsigc++2.0-docs')
  conflicts=('libsigc++2.0-docs')
  options=(!strip)

  mv docs/* "$pkgdir"
}
