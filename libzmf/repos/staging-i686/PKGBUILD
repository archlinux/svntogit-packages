# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>

pkgname=libzmf
pkgver=0.0.1
pkgrel=2
pkgdesc="a library for import of Zoner drawing and bitmap files"
arch=('i686' 'x86_64')
url="https://wiki.documentfoundation.org/DLP/Libraries/libzmf"
license=('MPL')
source=(http://dev-www.libreoffice.org/src/${pkgname}/${pkgname}-${pkgver}.tar.xz)
depends=('librevenge' 'libpng')
makedepends=('boost' 'cppunit' 'doxygen')
sha256sums=('eaf533ea29f9b66e75c0dcb631f1616b4f3cec9474e4fa0d2835769056a0e57a')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --disable-werror
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

