# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kdepim-addons
pkgver=18.04.1
pkgrel=1
pkgdesc='Addons for KDE PIM applications'
arch=(x86_64)
url='https://community.kde.org/KDE_PIM'
license=(GPL LGPL FDL)
depends=(incidenceeditor ktnef libksieve akonadi-notes akonadi-import-wizard kparts poppler-qt5)
makedepends=(extra-cmake-modules kdoctools boost)
groups=(kde-applications kdepim)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('eb4c1330a0fe65f88637e69a5abe3da8e6797ae05c56dfbb357c560ea9b5d1e2'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
