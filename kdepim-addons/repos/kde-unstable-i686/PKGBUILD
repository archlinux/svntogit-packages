# $Id: PKGBUILD 262594 2016-03-26 20:49:29Z arojas $
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kdepim-addons
pkgver=16.07.90
pkgrel=1
pkgdesc='Addons for KDE PIM applications'
arch=(i686 x86_64)
url='https://community.kde.org/KDE_PIM'
license=(GPL LGPL FDL)
depends=(mailcommon incidenceeditor libktnef libkgapi)
makedepends=(extra-cmake-modules kdoctools boost python kdesignerplugin)
groups=(kde-applications kdepim)
source=("http://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
sha1sums=('81cb22dd9acd2944c9fbab94b7eb3ab6c6e81bf4')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
