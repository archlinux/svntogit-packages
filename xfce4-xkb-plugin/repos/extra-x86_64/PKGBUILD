# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Tobias Kieslich <tobias (at) archlinux.org>

pkgname=xfce4-xkb-plugin
pkgver=0.5.4.3
pkgrel=1
pkgdesc="Plugin to switch keyboard layouts for the Xfce4 panel"
arch=('i686' 'x86_64')
license=('custom')
url="http://goodies.xfce.org/projects/panel-plugins/xfce4-xkb-plugin"
groups=('xfce4-goodies')
depends=('xfce4-panel' 'libxklavier>=5.0' 'librsvg')
makedepends=('intltool')
options=('!libtool')
source=(http://archive.xfce.org/src/panel-plugins/${pkgname}/${pkgver%.*.*}/${pkgname}-${pkgver}.tar.bz2)
sha1sums=('4dc42f96dc16a3bd78f86454ea3a931464e18497')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
	--sysconfdir=/etc \
	--libexecdir=/usr/lib \
	--localstatedir=/var \
	--disable-static \
	--disable-debug
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
