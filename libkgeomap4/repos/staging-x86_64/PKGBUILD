# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=libkgeomap4
_pkgname=libkgeomap
pkgver=15.08.3
pkgrel=1
pkgdesc="A wrapper around world map components for browsing and arranging photos on a map"
url="https://projects.kde.org/projects/kde/kdegraphics/libs/libkgeomap"
arch=(i686 x86_64)
license=(GPL LGPL FDL)
depends=(kdelibs libmarble4)
makedepends=(cmake automoc4)
replaces=('libkgeomap<15.11')
conflicts=('libkgeomap<15.11')
install=$pkgname.install
source=("http://download.kde.org/stable/applications/$pkgver/src/$_pkgname-$pkgver.tar.xz")
sha1sums=('d6cf746741b9e344890db7dac36a7900b8f9ad62')

prepare() {
  mkdir -p build

# Fix build with libmarble4
  sed -e 's|#include <marble|#include <marble4|' -i $_pkgname-$pkgver/libkgeomap/*.cpp -i $_pkgname-$pkgver/libkgeomap/*.h
}

build() {
  cd build
  cmake ../$_pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
