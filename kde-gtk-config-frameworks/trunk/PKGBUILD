# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Antonio Rojas <nqn1976 @ gmail.com>

pkgname=kde-gtk-config-frameworks
pkgver=5.2.1
pkgrel=1
pkgdesc='GTK2 and GTK3 Configurator for KDE'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/workspace/kde-gtk-config'
license=('LGPL')
depends=('knewstuff' 'kcmutils')
makedepends=('extra-cmake-modules' 'gtk2' 'gtk3')
optdepends=('gtk2: GTK2 apps support'
            'gtk3: GTK3 apps support')
conflicts=('kde-gtk-config')
groups=('plasma')
install=$pkgname.install
source=("http://download.kde.org/stable/plasma/$pkgver/kde-gtk-config-$pkgver.tar.xz")
md5sums=('28dc3451d64e04534012113594b09d78')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../kde-gtk-config-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DLIBEXEC_INSTALL_DIR=lib \
    -DSYSCONF_INSTALL_DIR=/etc \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
