# $Id$
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=paprefs
pkgver=0.9.9
pkgrel=6
pkgdesc="A simple GTK-based configuration dialog for PulseAudio"
arch=(i686 x86_64)
url="http://0pointer.de/lennart/projects/paprefs"
license=(GPL)
depends=(gconfmm gnome-icon-theme libglademm 'pulseaudio=0.9.22' libsigc++)
makedepends=(intltool)
source=(http://0pointer.de/lennart/projects/paprefs/$pkgname-$pkgver.tar.gz
        paprefs.desktop)
md5sums=('0592ccb28c8f601a8fe3607b64e9f5bc' '20dc978f0bd4a0bf925a4eaf80a2b8f8')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --disable-lynx
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir/paprefs.desktop" \
    "$pkgdir/usr/share/applications/paprefs.desktop"
}
