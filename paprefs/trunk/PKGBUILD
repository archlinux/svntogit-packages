# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=paprefs
pkgver=0.9.10
pkgrel=4
pkgdesc="A simple GTK-based configuration dialog for PulseAudio"
arch=(i686 x86_64)
url="http://0pointer.de/lennart/projects/paprefs"
license=(GPL)
depends=(gconfmm gnome-icon-theme libglademm libsigc++ pulseaudio-gconf)
makedepends=(intltool)
source=(http://freedesktop.org/software/pulseaudio/$pkgname/$pkgname-$pkgver.tar.xz
        paprefs.desktop do-not-require-recompile.patch)
sha256sums=('261c5a67543f2d0c019894fb933949f6ee7c5e181808e1e9565f0784961eabb0'
            '5cdd6cffb08d611451f01b34b08b316fb14cbd14b283bf5d180b8dcde11fa46b'
            '00fd4b14e81d90ac6d74e6eb689fa4e20e1a2fbc2eefbb440a11b1373e115a9d')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i "../do-not-require-recompile.patch"
}

build() {
  cd $pkgname-$pkgver
  CXXFLAGS+=' -std=c++11'
  ./configure --prefix=/usr --disable-lynx
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 ../paprefs.desktop "$pkgdir/usr/share/applications/paprefs.desktop"
}
