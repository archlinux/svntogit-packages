# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=paprefs
pkgver=1.2
pkgrel=1
pkgdesc="Configuration dialog for PulseAudio"
url="https://freedesktop.org/software/pulseaudio/paprefs/"
license=(GPL2)
arch=(x86_64)
depends=(gtkmm3 libsigc++ 'pulseaudio>=12.0')
makedepends=(meson lynx tidy git)
conflicts=(pipewire-pulse)
_commit=d70f061e1703c53aa327d3e836222e47dde86204  # tags/v1.2^0
source=("git+https://gitlab.freedesktop.org/pulseaudio/paprefs.git#commit=$_commit")
sha512sums=('SKIP')

pkgver() {
  cd paprefs
  git describe --tags | sed 's/^v//;s/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd paprefs
}

build() {
  arch-meson paprefs build -D lynx=true
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -Dt "$pkgdir/usr/share/doc/$pkgname" -m644 build/doc/README
}
