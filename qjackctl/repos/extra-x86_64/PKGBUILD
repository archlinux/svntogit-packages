# $Id$
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: Tobias Kieslich <tobias@archlinux.org>

pkgname=qjackctl
pkgver=0.4.1
pkgrel=1
pkgdesc="A Qt front-end for the JACK low-latency audio server"
url="http://qjackctl.sourceforge.net/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('jack' 'qt5-x11extras' 'hicolor-icon-theme')
makedepends=('qt5-tools')
install=$pkgname.install
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz")
md5sums=('6a0a4245e2b9e470e04009e1d0f29f08')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  export ac_qmake=/usr/bin/qmake-qt5 \
         ac_moc=/usr/bin/moc-qt5 \
         ac_uic=/usr/bin/uic-qt5 \
         ac_lupdate=/usr/bin/lupdate-qt5 \
         ac_lrelease=/usr/bin/lrelease-qt5 \
         QMAKE=qmake-qt5

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
