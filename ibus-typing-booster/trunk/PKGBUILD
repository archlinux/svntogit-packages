# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=ibus-typing-booster
pkgver=1.5.35
pkgrel=1
pkgdesc="Predictive input method for the IBus platform"
url="https://mike-fabian.github.io/ibus-typing-booster/"
arch=(any)
license=(GPL3)
depends=(ibus python-gobject python-dbus python-pyenchant m17n-lib sqlite)
makedepends=(git m17n-db hunspell hunspell-{de,el,en,es,fr,he,hu,it,nl,pl,ro})
_commit=e4cc4c7a0970523c949fe0abdb1458e22be006e6  # tags/1.5.35
source=("git+https://github.com/mike-fabian/ibus-typing-booster#commit=$_commit")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  autoreconf -fvi
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --libexec=/usr/lib/ibus
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install

  cd "$pkgdir/usr/share/$pkgname/data"
  gzip -nf --best *.{txt,json} annotations{,Derived}/*.xml
}
