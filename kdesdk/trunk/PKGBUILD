# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kdesdk
pkgname=('kdesdk-cervisia'
         'kdesdk-dolphin-plugins'
         'kdesdk-kapptemplate'
         'kdesdk-kcachegrind'
         'kdesdk-kioslave'
         'kdesdk-kmtrace'
         'kdesdk-kompare'
         'kdesdk-kpartloader'
         'kdesdk-kprofilemethod'
         'kdesdk-kstartperf'
         'kdesdk-kuiviewer'
         'kdesdk-lokalize'
         'kdesdk-okteta'
         'kdesdk-poxml'
         'kdesdk-scripts'
         'kdesdk-strigi-analyzer'
         'kdesdk-umbrello')
pkgver=4.9.2
pkgrel=2
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdesdk')
makedepends=('cmake' 'automoc4' 'boost' 'subversion' 'antlr2' 'kdepimlibs'
             'kdebase-lib')
source=("http://download.kde.org/stable/${pkgver}/src/${pkgbase}-${pkgver}.tar.xz"
        'fix-python2-path.patch')
sha1sums=('8acefc3701f12834b1e5606b635aef4b500e0aee'
          '923cabd7a877cf9a68efeb24fbf3d5827e1d949e')

build() {
	cd ${srcdir}/${pkgbase}-${pkgver}

	# Fix hardcoded python2 cmd
	patch -Np1 -i ${srcdir}/fix-python2-path.patch

	cd ${srcdir}
	mkdir build
	cd build
	cmake ../${pkgbase}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package_kdesdk-cervisia() {
	pkgdesc='CVS Frontend'
	depends=('kdebase-runtime')
	url="http://kde.org/applications/development/cervisia/"
	install='kdesdk-cervisia.install'
	cd $srcdir/build/cervisia
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/cervisia
	make DESTDIR=$pkgdir install
}

package_kdesdk-dolphin-plugins() {
	pkgdesc='Extra Dolphin plugins'
	depends=('kdebase-dolphin')
    optdepends=('bzr: bazaar support'
                'git: git support'
                'mercurial: hg support'
                'subversion: svn support')
	install='kdesdk.install'
    for i in bazaar git hg svn; do
        cd $srcdir/build/dolphin-plugins/$i
    	make DESTDIR=$pkgdir install
    done
}

package_kdesdk-kapptemplate() {
	pkgdesc='KDE Template Generator'
	depends=('kdebase-runtime')
	url="http://kde.org/applications/development/kapptemplate/"
	install='kdesdk.install'
	cd $srcdir/build/kapptemplate
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kapptemplate
	make DESTDIR=$pkgdir install
}

package_kdesdk-kcachegrind() {
	pkgdesc='Visualization of Performance Profiling Data'
	depends=('kdebase-runtime' 'python2')
	optdepends=('php: PHP support')
	url="http://kde.org/applications/development/kcachegrind/"
	install='kdesdk-kcachegrind.install'
	cd $srcdir/build/kcachegrind
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kcachegrind
	make DESTDIR=$pkgdir install

        # Fix python 2 path
        sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|' \
                "${pkgdir}"/usr/bin/hotshot2calltree
}

package_kdesdk-kioslave() {
	pkgdesc='KDED Subversion Module'
	depends=('kdebase-runtime' 'subversion')
        install='kdesdk.install'
	cd $srcdir/build/kioslave
	make DESTDIR=$pkgdir install
}

package_kdesdk-kmtrace() {
	pkgdesc='A KDE tool to assist with malloc debugging using glibcÂ´s "mtrace" functionality'
	depends=('kdebase-runtime')
	cd $srcdir/build/kmtrace
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kmtrace
	make DESTDIR=$pkgdir install
}

package_kdesdk-kompare() {
	pkgdesc='Diff/Patch Frontend'
	depends=('kdebase-runtime')
	url="http://kde.org/applications/development/kompare/"
	install='kdesdk-kompare.install'
	cd $srcdir/build/kompare
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kompare
	make DESTDIR=$pkgdir install
}

package_kdesdk-kpartloader() {
	pkgdesc='A test application for KParts'
	depends=('kdebase-runtime')
	install='kdesdk.install'
	cd $srcdir/build/kpartloader
	make DESTDIR=$pkgdir install
}

package_kdesdk-kprofilemethod() {
	pkgdesc='Macros helping to profile'
	cd $srcdir/build/kprofilemethod
	make DESTDIR=$pkgdir install
}

package_kdesdk-kspy() {
	pkgdesc='An Object Inspector for Qt/KDE applications'
	depends=('kdebase-runtime')
	cd $srcdir/build/kspy
	make DESTDIR=$pkgdir install
}

package_kdesdk-kstartperf() {
	pkgdesc='Startup time measurement tool for KDE applications'
	depends=('kdebase-runtime')
	cd $srcdir/build/kstartperf
	make DESTDIR=$pkgdir install
}

package_kdesdk-kuiviewer() {
	pkgdesc='Qt Designer UI File Viewer'
	depends=('kdebase-runtime')
	url="http://kde.org/applications/development/kuiviewer/"
	install='kdesdk-kuiviewer.install'
	cd $srcdir/build/kuiviewer
	make DESTDIR=$pkgdir install
}

package_kdesdk-lokalize() {
	pkgdesc='Computer-Aided Translation System'
	depends=('kdebase-runtime' 'kdebindings-python2' 'kdesdk-strigi-analyzer')
	url="http://kde.org/applications/development/lokalize/"
	optdepends=('translate-toolkit: enable extra python script')
	install='kdesdk-lokalize.install'
	cd $srcdir/build/lokalize
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/lokalize
	make DESTDIR=$pkgdir install

  # Fix python 2 path
  sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|' \
    "${pkgdir}"/usr/share/apps/lokalize/scripts/odf/xliffmerge.py
}

package_kdesdk-okteta() {
	pkgdesc='Hex Editor'
	depends=('kdebase-runtime')
	replaces=('kdeutils-okteta')
	conflicts=('kdeutils-okteta')
	url="http://kde.org/applications/utilities/okteta/"
	install='kdesdk-okteta.install'
	cd $srcdir/build/okteta
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/okteta
	make DESTDIR=$pkgdir install
}

package_kdesdk-poxml() {
	pkgdesc='Translates DocBook XML files using gettext po files'
	depends=('qt' 'antlr2')
	cd $srcdir/build/poxml
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/poxml
	make DESTDIR=$pkgdir install
}

package_kdesdk-scheck() {
	pkgdesc='An interface style to highlight accel and style guide conflicts'
	depends=('kdebase-runtime')
	cd $srcdir/build/scheck
	make DESTDIR=$pkgdir install
}

package_kdesdk-scripts() {
	pkgdesc='KDE SDK scripts'
	depends=('python2')
	cd $srcdir/build/scripts
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/scripts
	make DESTDIR=$pkgdir install

  # Fix python 2 path
  sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|' \
    "${pkgdir}"/usr/bin/kde-systemsettings-tree.py
  sed -i 's|#! /usr/bin/env python|#!/usr/bin/env python2|' \
    "${pkgdir}"/usr/bin/{{kdelnk2desktop,zonetab2pot}.py,kde_generate_export_header}
}

package_kdesdk-strigi-analyzer() {
	pkgdesc='Strigi-Analyzer for KDE SDK'
	depends=('kdelibs')
	cd $srcdir/build/strigi-analyzer
	make DESTDIR=$pkgdir install
}

package_kdesdk-umbrello() {
	pkgdesc='UML Modeller'
	depends=('kdebase-runtime')
	url="http://kde.org/applications/development/umbrello/"
	install='kdesdk-umbrello.install'
	cd $srcdir/build/umbrello
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/umbrello
	make DESTDIR=$pkgdir install

  # Fix python 2 path
  sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|' \
    "${pkgdir}"/usr/share/apps/umbrello/headings/heading.py
}
