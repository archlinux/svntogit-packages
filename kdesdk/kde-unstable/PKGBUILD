# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kdesdk
pkgname=('kdesdk-cervisia'
         'kdesdk-dolphin-plugins'
         'kdesdk-kapptemplate'
         'kdesdk-kate'
         'kdesdk-kbugbuster'
         'kdesdk-kcachegrind'
         'kdesdk-kdeaccounts-plugin'
         'kdesdk-kdepalettes'
         'kdesdk-kioslave'
         'kdesdk-kmtrace'
         'kdesdk-kompare'
         'kdesdk-kpartloader'
         'kdesdk-kprofilemethod'
         'kdesdk-kstartperf'
         'kdesdk-kuiviewer'
         'kdesdk-lokalize'
         'kdesdk-poxml'
         'kdesdk-scripts'
         'kdesdk-strigi-analyzer'
         'kdesdk-umbrello')
pkgver=4.4.90
pkgrel=1
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdesdk')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'boost' 'kdepim-runtime' 'subversion'
	'kdebindings-python' 'docbook-xsl')
source=("http://download.kde.org/unstable/${pkgver}/src/${pkgbase}-${pkgver}.tar.bz2")
sha1sums=('60ecfeefb0c544e92de00fb9b2caf343d46bb32a')

build() {
	cd $srcdir
	mkdir build
	cd build
	cmake ../${pkgbase}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package_kdesdk-cervisia() {
	pkgdesc='CVS Frontend'
	depends=('kdebase-runtime')
	url="http://kde.org/applications/development/cervisia/"
	install='kdesdk.install'
	cd $srcdir/build/cervisia
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/cervisia
	make DESTDIR=$pkgdir install
}

package_kdesdk-dolphin-plugins() {
	pkgdesc='Extra Dolphin plugins'
	depends=('kdebase-dolphin' 'subversion' 'git' 'kdesdk-kompare')
	install='kdesdk.install'
	cd $srcdir/build/dolphin-plugins
	make DESTDIR=$pkgdir install
}

package_kdesdk-kapptemplate() {
	pkgdesc='KDE Template Generator'
	depends=('kdebase-runtime')
	url="http://kde.org/applications/development/kapptemplate/"
	install='kdesdk.install'
	cd $srcdir/build/kapptemplate
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kapptemplate
	make DESTDIR=$pkgdir install
}

package_kdesdk-kate() {
	pkgdesc='Advanced Text Editor'
	depends=('kdebase-runtime')
	url="http://kde.org/applications/utilities/kate/"
	install='kdesdk-kate.install'
	cd $srcdir/build/kate
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kate
	make DESTDIR=$pkgdir install
}

package_kdesdk-kbugbuster() {
	pkgdesc='KDE Bug Management'
	depends=('kdepim-runtime')
	url="http://kde.org/applications/development/kbugbuster/"
	install='kdesdk.install'
	cd $srcdir/build/kbugbuster
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kbugbuster
	make DESTDIR=$pkgdir install
}

package_kdesdk-kcachegrind() {
	pkgdesc='Visualization of Performance Profiling Data'
	depends=('kdebase-runtime' 'python')
	url="http://kde.org/applications/development/kcachegrind/"
	install='kdesdk.install'
	cd $srcdir/build/kcachegrind
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kcachegrind
	make DESTDIR=$pkgdir install
}

package_kdesdk-kdeaccounts-plugin() {
	pkgdesc='KDE Repository Accounts'
	depends=('kdepim-runtime')
	cd $srcdir/build/kdeaccounts-plugin
	make DESTDIR=$pkgdir install
}

package_kdesdk-kdepalettes() {
	pkgdesc='Palettes for the Gimp that match the KDE standard color palette'
	depends=('gimp')
	install -D -m644 $srcdir/${pkgbase}-${pkgver}/kdepalettes/KDE_Gimp \
		$pkgdir/usr/share/gimp/2.0/palettes/KDE.gpl
}

package_kdesdk-kioslave() {
	pkgdesc='KDED Subversion Module'
	depends=('kdebase-runtime' 'subversion')
	cd $srcdir/build/kioslave
	make DESTDIR=$pkgdir install
}

package_kdesdk-kmtrace() {
	pkgdesc='A KDE tool to assist with malloc debugging using glibcÂ´s "mtrace" functionality'
	depends=('kdebase-runtime')
	cd $srcdir/build/kmtrace
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kmtrace
	make DESTDIR=$pkgdir install
}

package_kdesdk-kompare() {
	pkgdesc='Diff/Patch Frontend'
	depends=('kdebase-runtime')
	url="http://kde.org/applications/development/kompare/"
	install='kdesdk.install'
	cd $srcdir/build/kompare
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kompare
	make DESTDIR=$pkgdir install
}

package_kdesdk-kpartloader() {
	pkgdesc='A test application for KParts'
	depends=('kdebase-runtime')
	install='kdesdk.install'
	cd $srcdir/build/kpartloader
	make DESTDIR=$pkgdir install
}

package_kdesdk-kprofilemethod() {
	pkgdesc='Macros helping to profile'
	cd $srcdir/build/kprofilemethod
	make DESTDIR=$pkgdir install
}

package_kdesdk-kstartperf() {
	pkgdesc='Startup time measurement tool for KDE applications'
	depends=('kdebase-runtime')
	cd $srcdir/build/kstartperf
	make DESTDIR=$pkgdir install
}

package_kdesdk-kuiviewer() {
	pkgdesc='Qt Designer UI File Viewer'
	depends=('kdebase-runtime')
	url="http://kde.org/applications/development/kuiviewer/"
	install='kdesdk.install'
	cd $srcdir/build/kuiviewer
	make DESTDIR=$pkgdir install
}

package_kdesdk-lokalize() {
	pkgdesc='Computer-Aided Translation System'
	depends=('kdebase-runtime' 'kdebindings-python')
	url="http://kde.org/applications/development/lokalize/"
	optdepends=('translate-toolkit: enable extra python script')
	install='kdesdk.install'
	cd $srcdir/build/lokalize
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/lokalize
	make DESTDIR=$pkgdir install
}

package_kdesdk-poxml() {
	pkgdesc='Translates DocBook XML files using gettext po files'
	depends=('qt')
	cd $srcdir/build/poxml
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/poxml
	make DESTDIR=$pkgdir install
}

package_kdesdk-scripts() {
	pkgdesc='KDE SDK scripts'
	depends=('python')
	cd $srcdir/build/scripts
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/scripts
	make DESTDIR=$pkgdir install
}

package_kdesdk-strigi-analyzer() {
	pkgdesc='Strigi-Analyzer for KDE SDK'
	depends=('kdelibs')
	cd $srcdir/build/strigi-analyzer
	make DESTDIR=$pkgdir install
}

package_kdesdk-umbrello() {
	pkgdesc='UML Modeller'
	depends=('kdebase-runtime')
	url="http://kde.org/applications/development/umbrello/"
	install='kdesdk.install'
	cd $srcdir/build/umbrello
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/umbrello
	make DESTDIR=$pkgdir install
}
