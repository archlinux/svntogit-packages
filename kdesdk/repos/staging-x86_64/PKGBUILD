# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kdesdk
pkgname=('kdesdk-cervisia'
         'kdesdk-dolphin-plugins'
         'kdesdk-dev-utils'
         'kdesdk-dev-scripts'
         'kdesdk-kapptemplate'
         'kdesdk-kcachegrind'
         'kdesdk-kioslaves'
         'kdesdk-kompare'
         'kdesdk-lokalize'
         'kdesdk-okteta'
         'kdesdk-poxml'
         'kdesdk-strigi-analyzers'
         'kdesdk-umbrello')
pkgver=4.10.4
pkgrel=1
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdesdk')
makedepends=('cmake' 'automoc4' 'boost' 'subversion' 'antlr2' 'kdepimlibs'
             'kdebase-lib')
source=("http://download.kde.org/stable/${pkgver}/src/${pkgbase}-${pkgver}.tar.xz"
        'fix-python2-path.patch')
sha1sums=('da886df4ddebb12e760db7469de1350feed2b338'
          '07db42bbcae402c292eebf002e549b04162621c5')

prepare() {
	cd ${pkgbase}-${pkgver}

	# Fix hardcoded python2 cmd
	patch -Np1 -i ${srcdir}/fix-python2-path.patch
}

build() {
	mkdir build
	cd build
	cmake ../${pkgbase}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DKDE4_BUILD_TESTS=OFF \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package_kdesdk-cervisia() {
	pkgdesc='CVS Frontend'
	depends=('kdebase-runtime')
	url="http://kde.org/applications/development/cervisia/"
	install='kdesdk-cervisia.install'
	cd $srcdir/build/cervisia
	make DESTDIR=$pkgdir install
}

package_kdesdk-dev-utils() {
    pkgdesc='Small utilities for developers using KDE/Qt libs/frameworks'
    url="https://projects.kde.org/projects/kde/kdesdk/kde-dev-utils"
    depends=('kdebase-runtime')
    conflicts=('kdesdk-kmtrace' 'kdesdk-kpartloader' 'kdesdk-kprofilemethod'
              'kdesdk-kstartperf' 'kdesdk-kuiviewer')
    replaces=('kdesdk-kmtrace' 'kdesdk-kpartloader' 'kdesdk-kprofilemethod'
              'kdesdk-kstartperf' 'kdesdk-kuiviewer')
    install='kdesdk-dev-utils.install'
    cd $srcdir/build/kde-dev-utils
    make DESTDIR=$pkgdir install
}

package_kdesdk-dolphin-plugins() {
	pkgdesc='Extra Dolphin plugins'
    url="https://projects.kde.org/projects/kde/kdesdk/dolphin-plugins"
	depends=('kdebase-dolphin')
    optdepends=('bzr: bazaar support'
                'git: git support'
                'mercurial: hg support'
                'subversion: svn support')
	install='kdesdk.install'
    cd $srcdir/build/dolphin-plugins
  	make DESTDIR=$pkgdir install
}

package_kdesdk-kapptemplate() {
	pkgdesc='KDE Template Generator'
	depends=('kdebase-runtime')
	url="http://kde.org/applications/development/kapptemplate/"
	install='kdesdk.install'
	cd $srcdir/build/kapptemplate
	make DESTDIR=$pkgdir install
}

package_kdesdk-kcachegrind() {
	pkgdesc='Visualization of Performance Profiling Data'
	depends=('kdebase-runtime' 'python2')
	optdepends=('php: PHP support')
	url="http://kde.org/applications/development/kcachegrind/"
	install='kdesdk-kcachegrind.install'
	cd $srcdir/build/kcachegrind
	make DESTDIR=$pkgdir install

    # Fix python 2 path
    sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|' \
      "${pkgdir}"/usr/bin/hotshot2calltree
}

package_kdesdk-kioslaves() {
	pkgdesc='KIO-Slaves'
    url="https://projects.kde.org/projects/kde/kdesdk/kdesdk-kioslaves"
	depends=('kdebase-runtime' 'subversion')
    install='kdesdk.install'
    replaces=('kdesdk-kioslave')
    conflicts=('kdesdk-kioslave')
	cd $srcdir/build/kdesdk-kioslaves
	make DESTDIR=$pkgdir install
}

package_kdesdk-kompare() {
	pkgdesc='Diff/Patch Frontend'
	depends=('kdebase-runtime')
	url="http://kde.org/applications/development/kompare/"
	install='kdesdk-kompare.install'
	cd $srcdir/build/kompare
	make DESTDIR=$pkgdir install
}

package_kdesdk-lokalize() {
	pkgdesc='Computer-Aided Translation System'
	depends=('kdebase-runtime' 'kdebindings-python2' 'kdesdk-strigi-analyzers')
	url="http://kde.org/applications/development/lokalize/"
	optdepends=('translate-toolkit: enable extra python script')
	install='kdesdk-lokalize.install'
	cd $srcdir/build/lokalize
	make DESTDIR=$pkgdir install

    # Fix python 2 path
    sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|' \
      "${pkgdir}"/usr/share/apps/lokalize/scripts/odf/xliffmerge.py
}

package_kdesdk-okteta() {
	pkgdesc='Hex Editor'
	depends=('kdebase-runtime')
	replaces=('kdeutils-okteta')
	conflicts=('kdeutils-okteta')
	url="http://kde.org/applications/utilities/okteta/"
	install='kdesdk-okteta.install'
	cd $srcdir/build/okteta
	make DESTDIR=$pkgdir install
}

package_kdesdk-poxml() {
	pkgdesc='Translates DocBook XML files using gettext po files'
    url="https://projects.kde.org/projects/kde/kdesdk/poxml"
	depends=('qt4')
    optdepends=('antlr2: required for po2xml and swappo tool')
	cd $srcdir/build/poxml
	make DESTDIR=$pkgdir install
}

package_kdesdk-dev-scripts() {
	pkgdesc='Scripts and setting files useful during development of KDE software'
    url="https://projects.kde.org/projects/kde/kdesdk/kde-dev-scripts"
	depends=('python2')
    replaces=('kdesdk-scripts')
    conflicts=('kdesdk-scripts')
	cd $srcdir/build/kde-dev-scripts
	make DESTDIR=$pkgdir install

    # Fix python 2 path
    sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|' \
      "${pkgdir}"/usr/bin/{zonetab2pot,kde-systemsettings-tree}.py
    sed -i 's|#! /usr/bin/env python|#!/usr/bin/env python2|' \
      "${pkgdir}"/usr/bin/{kdelnk2desktop.py,kde_generate_export_header}
}

package_kdesdk-strigi-analyzers() {
	pkgdesc='Analyzer plugins for strigi'
    url="https://projects.kde.org/projects/kde/kdesdk/kdesdk-strigi-analyzers"
	depends=('kdelibs')
    conflicts=('kdesdk-strigi-analyzer')
    replaces=('kdesdk-strigi-analyzer')
	cd $srcdir/build/kdesdk-strigi-analyzers
	make DESTDIR=$pkgdir install
}

package_kdesdk-umbrello() {
	pkgdesc='UML Modeller'
	depends=('kdebase-runtime')
    optdepends=('ruby')
	url="http://kde.org/applications/development/umbrello/"
	install='kdesdk-umbrello.install'
	cd $srcdir/build/umbrello
	make DESTDIR=$pkgdir install
    
    # Fix python 2 path
    sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|' \
      "${pkgdir}"/usr/share/apps/umbrello/headings/heading.py
}
