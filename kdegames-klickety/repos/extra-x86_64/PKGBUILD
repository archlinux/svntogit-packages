# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-klickety
pkgver=15.08.3
pkgrel=1
pkgdesc="An adaptation of the Clickomania game"
url="http://kde.org/applications/games/klickety/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegames')
depends=('kdebase-runtime' 'libkdegames4')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/applications/${pkgver}/src/klickety-${pkgver}.tar.xz")
sha1sums=('b38d8e1ff56346b10b39bc2abcb71e7a59bfac69')

build() {
  mkdir -p build
  cd build
  cmake ../klickety-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
