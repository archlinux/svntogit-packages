# $Id$
# Maintainer: Aaron Griffin <aaron@archlinux.org>
# Contributor: juergen <juergen@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>

pkgname=ratpoison
pkgver=1.4.5
pkgrel=3
pkgdesc="A simple keystroke-driven window manager"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libxinerama' 'readline' 'bash' 'perl' 'libxtst' 'libxft')
url="http://www.nongnu.org/ratpoison/"
source=(http://savannah.nongnu.org/download/${pkgname}/${pkgname}-${pkgver}.tar.gz
        ${pkgname}.desktop)
md5sums=('330a08dbed6be88cab54f6947e9f0b60'
         '29c3cb9be59758e39d8471391231a74a')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # fix timestamp issue
  find . -exec touch \{\} \;
  
  ./configure --prefix=/usr
  make CFLAGS="$CFLAGS -DHAVE_GETLINE"|| return 1
  make DESTDIR=${pkgdir} install

  # fix permissions
  chmod a+x ${pkgdir}/usr/share/ratpoison/{allwindows.sh,clickframe.pl,rpshowall.sh,rpws,split.sh}

  # Not useful outside the source tree. Kill it
  rm ${pkgdir}/usr/share/ratpoison/genrpbindings

  cd contrib
  ./genrpbindings || return 1
  install -dm755 ${pkgdir}/usr/share/ratpoison/bindings
  install -m644 {Ratpoison.pm,ratpoison-cmd.el,ratpoison.rb,ratpoison.lisp,ratpoison.py} \
			${pkgdir}/usr/share/ratpoison/bindings/ || return 1
        
  install -Dm644 ${srcdir}/${pkgname}.desktop ${pkgdir}/etc/X11/sessions/${pkgname}.desktop
  rm ${pkgdir}/usr/share/info/dir

  install -Dm644 ${pkgdir}/usr/share/ratpoison/ratpoison.el \
       ${pkgdir}/usr/share/emacs/site-lisp/ratpoison.el || return 1
}

# vim: ts=2 sw=2 et ft=sh
