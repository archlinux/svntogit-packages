# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=link-grammar
pkgver=5.10.5
pkgrel=1
pkgdesc="A Grammar Checking library"
arch=('x86_64')
url="https://www.abisource.com/projects/link-grammar/"
license=('LGPL')
depends=('hunspell' 'sqlite' 'libedit')
makedepends=('python' 'swig' 'apache-ant' 'java-environment=11')
options=('!makeflags')
source=(https://www.abisource.com/downloads/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz{,.asc})
validpgpkeys=('6407453C98BECC19ADB03D82EB6AA534E0C0651C'
              '8305252160000B5E89843F5464A99A8201045933') # Linas Vepstas <linasvepstas@gmail.com>
sha256sums=('324710cd8132975ff9ccb53509732f7558473b1c19f17892000a3dedb8618aed'
            'SKIP')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Fix build with Python 3.10
  sed -i '/test -n "$lg_python_unusable" && PythonFound=no/d' configure
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
}
