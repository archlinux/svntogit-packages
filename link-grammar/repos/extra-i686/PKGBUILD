# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=link-grammar
pkgver=4.7.12
pkgrel=1
pkgdesc="A Grammar Checking library"
arch=('i686' 'x86_64')
url="http://www.abisource.com/downloads/link-grammar/"
license=('BSD')
depends=('aspell')
options=('!libtool' '!makeflags')
source=("http://www.abisource.com/downloads/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
md5sums=('f4f0b731dc0bbd1c7adfe9a8ef7ab116')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/link-grammar/LICENSE"
}
