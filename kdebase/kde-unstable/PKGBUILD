# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kdebase
pkgname=('kdebase-kdepasswd'
         'kdebase-kdialog'
         'kdebase-keditbookmarks'
         'kdebase-kfind'
         'kdebase-konq-plugins'
         'kdebase-konqueror'
         'kdebase-lib'
         'kdebase-plasma'
	 'dolphinpart4')
pkgver=15.11.80
pkgrel=2
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdebase')
makedepends=('libkactivities4' 'cmake' 'automoc4' 'tidyhtml')
source=("http://download.kde.org/unstable/applications/${pkgver}/src/kde-baseapps-${pkgver}.tar.xz")
sha1sums=('8e9b65943624ff3996f62add84e1d712537bfc37')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kde-baseapps-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package_kdebase-kdepasswd() {
  pkgdesc='Change Password'
  depends=('kdebase-runtime' 'kdebase-lib')

  cd "$srcdir"/build/kdepasswd
  make DESTDIR="$pkgdir" install
  cd "$srcdir"/build/doc/kdepasswd
  make DESTDIR="$pkgdir" install
}

package_kdebase-kdialog() {
  pkgdesc='A utility for displaying dialog boxes from shell scripts'
  depends=('kdebase-runtime')

  cd "$srcdir"/build/kdialog
  make DESTDIR="$pkgdir" install
}

package_kdebase-keditbookmarks() {
  pkgdesc='Bookmark Organizer and Editor'
  depends=('kdebase-runtime')

  cd "$srcdir"/build/keditbookmarks
  make DESTDIR="$pkgdir" install
}

package_kdebase-kfind() {
  pkgdesc='Find Files/Folders'
  depends=('kdebase-runtime' 'kdebase-lib')
  url="http://kde.org/applications/utilities/kfind/"
  install='kdebase.install'

  cd "$srcdir"/build/kfind
  make DESTDIR="$pkgdir" install
  cd "$srcdir"/build/doc/kfind
  make DESTDIR="$pkgdir" install
}

package_kdebase-konq-plugins() {
  pkgdesc='Extra plugins for Konqueror'
  depends=('kdebase-konqueror' 'tidyhtml')
  replaces=('konq-plugins')
  install='kdebase.install'

  cd "$srcdir"/build/konq-plugins
  make DESTDIR="$pkgdir" install
}

package_kdebase-konqueror() {
  pkgdesc='KDE File Manager & Web Browser'
  depends=('dolphinpart4' 'kdebase-keditbookmarks')
  optdepends=('kwebkitpart: to enable webkit engine')
  url="http://kde.org/applications/internet/konqueror/"
  install='kdebase-konqueror.install'
  conflicts=('kdebase-nsplugins')
  replaces=('kdebase-nsplugins')

  for i in konqueror doc/konqueror nsplugins; do
    cd "$srcdir"/build/${i}
    make DESTDIR="$pkgdir" install
  done
}

package_kdebase-lib() {
  pkgdesc='KDE libraries for the basic desktop applications'
  groups=()
  depends=('kdelibs')
  conflicts=('kde-templates')

  cd "$srcdir"/build/lib
  make DESTDIR="$pkgdir" install
}

package_kdebase-plasma() {
  pkgdesc='Display the contents of folders (UserÂ´s home folder as default)'
  depends=('kdebase-workspace' 'kdebase-lib')
  groups=()

  cd "$srcdir"/build/plasma
  make DESTDIR="$pkgdir" install
}

package_dolphinpart4() {
  pkgdesc='KDE4 Dolphin KPart'
  depends=('libkactivities4' 'kdebase-lib')
  conflicts=('kdebase-dolphin')
  provides=('kdebase-dolphin')
  groups=()

  cd "$srcdir"/build/dolphin
  make DESTDIR="$pkgdir" install
}
