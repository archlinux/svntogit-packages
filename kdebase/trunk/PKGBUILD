# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kdebase
pkgname=('kdebase-dolphin'
         'kdebase-kdepasswd'
         'kdebase-kdialog'
         'kdebase-keditbookmarks'
         'kdebase-kfind'
         'kdebase-konqueror'
         'kdebase-konsole'
         'kdebase-kwrite'
         'kdebase-lib'
         'kdebase-plasma')
pkgver=4.6.0
pkgrel=1
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdebase')
makedepends=('kdebase-workspace' 'pkgconfig' 'cmake' 'automoc4' 'docbook-xsl')
source=("http://download.kde.org/stable/${pkgver}/src/${pkgbase}-${pkgver}.tar.bz2")
sha1sums=('887ddea760aaabb277082f9e8c0b2f8905e318b8')

build() {
	cd $srcdir
	mkdir build
	cd build
	cmake ../${pkgbase}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package_kdebase-dolphin() {
	pkgdesc='File Manager'
	depends=('kdebase-runtime' 'kdebase-lib')
	optdepends=('ruby: servicemenu installation')
	url="http://kde.org/applications/system/dolphin/"
	cd $srcdir/build/apps/dolphin
	make DESTDIR=$pkgdir install
	cd $srcdir/build/apps/doc/dolphin
	make DESTDIR=$pkgdir install
}

package_kdebase-kdepasswd() {
	pkgdesc='Change Password'
	depends=('kdebase-runtime' 'kdebase-lib')
	cd $srcdir/build/apps/kdepasswd
	make DESTDIR=$pkgdir install
	cd $srcdir/build/apps/doc/kdepasswd
	make DESTDIR=$pkgdir install
}

package_kdebase-kdialog() {
	pkgdesc='A utility for displaying dialog boxes from shell scripts'
	depends=('kdebase-runtime')
	cd $srcdir/build/apps/kdialog
	make DESTDIR=$pkgdir install
}

package_kdebase-keditbookmarks() {
	pkgdesc='Bookmark Organizer and Editor'
	depends=('kdebase-runtime')
	cd $srcdir/build/apps/keditbookmarks
	make DESTDIR=$pkgdir install
}

package_kdebase-kfind() {
	pkgdesc='Find Files/Folders'
	depends=('kdebase-runtime' 'kdebase-lib')
	url="http://kde.org/applications/utilities/kfind/"
	install='kdebase.install'
	cd $srcdir/build/apps/kfind
	make DESTDIR=$pkgdir install
	cd $srcdir/build/apps/doc/kfind
	make DESTDIR=$pkgdir install
}

package_kdebase-konqueror() {
	pkgdesc='KDE File Manager & Web Browser'
	depends=('kdebase-dolphin' 'kdebase-keditbookmarks')
	optdepends=('kwebkitpart: to enable webkit engine')
	url="http://kde.org/applications/internet/konqueror/"
	install='kdebase.install'
    conflicts=('kdebase-nsplugins')
    replaces=('kdebase-nsplugins')
    for i in konqueror doc/konqueror nsplugins; do
        cd $srcdir/build/apps/${i}
        make DESTDIR=$pkgdir install
    done
}

package_kdebase-konsole() {
	pkgdesc='Terminal'
	depends=('kdebase-runtime')
	url="http://kde.org/applications/system/konsole/"
	cd $srcdir/build/apps/konsole
	make DESTDIR=$pkgdir install
	cd $srcdir/build/apps/doc/konsole
	make DESTDIR=$pkgdir install
}

package_kdebase-kwrite() {
	pkgdesc='Text Editor'
	depends=('kdebase-runtime')
	url="http://kde.org/applications/utilities/kwrite/"
	cd $srcdir/build/apps/kwrite
	make DESTDIR=$pkgdir install
	cd $srcdir/build/apps/doc/kwrite
	make DESTDIR=$pkgdir install
}

package_kdebase-lib() {
	pkgdesc='KDE libraries for the basic desktop applications'
	groups=()
	depends=('kdelibs')
	cd $srcdir/build/apps/lib
	make DESTDIR=$pkgdir install
}

package_kdebase-plasma() {
	pkgdesc='Display the contents of folders (UserÂ´s home folder as default)'
	depends=('kdebase-workspace' 'kdebase-lib')
	cd $srcdir/build/apps/plasma
	make DESTDIR=$pkgdir install
}
