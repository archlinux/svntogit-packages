# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kdebase
pkgname=('kdebase-dolphin'
         'kdebase-kdepasswd'
         'kdebase-kdialog'
         'kdebase-keditbookmarks'
         'kdebase-kfind'
         'kdebase-konq-plugins'
         'kdebase-konqueror'
         'kdebase-lib'
         'kdebase-plasma')
pkgver=4.9.4
pkgrel=1
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdebase')
makedepends=('kdelibs' 'cmake' 'automoc4' 'tidyhtml')
source=("http://download.kde.org/stable/${pkgver}/src/kde-baseapps-${pkgver}.tar.xz")
sha1sums=('b68e76766331272a45b1d897adf4c1928a11e747')

build() {
	cd ${srcdir}
	mkdir build
	cd build
	cmake ../kde-baseapps-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package_kdebase-dolphin() {
	pkgdesc='File Manager'
	depends=('kdebase-runtime' 'kdebase-lib')
	optdepends=('kdegraphics-svgpart: thumbailers for SVG files'
                'kdegraphics-thumbnailers: thumbnailers for graphics file'
                'ruby: servicemenu installation')
	url="http://kde.org/applications/system/dolphin/"
        install=kdebase-dolphin.install
	cd $srcdir/build/dolphin
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/dolphin
	make DESTDIR=$pkgdir install
}

package_kdebase-kdepasswd() {
	pkgdesc='Change Password'
	depends=('kdebase-runtime' 'kdebase-lib')
	cd $srcdir/build/kdepasswd
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kdepasswd
	make DESTDIR=$pkgdir install
}

package_kdebase-kdialog() {
	pkgdesc='A utility for displaying dialog boxes from shell scripts'
	depends=('kdebase-runtime')
	cd $srcdir/build/kdialog
	make DESTDIR=$pkgdir install
}

package_kdebase-keditbookmarks() {
	pkgdesc='Bookmark Organizer and Editor'
	depends=('kdebase-runtime')
	cd $srcdir/build/keditbookmarks
	make DESTDIR=$pkgdir install
}

package_kdebase-kfind() {
	pkgdesc='Find Files/Folders'
	depends=('kdebase-runtime' 'kdebase-lib')
	url="http://kde.org/applications/utilities/kfind/"
	install='kdebase.install'
	cd $srcdir/build/kfind
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kfind
	make DESTDIR=$pkgdir install
}

package_kdebase-konq-plugins() {
	pkgdesc='Extra plugins for Konqueror'
	depends=('kdebase-konqueror' 'tidyhtml')
	replaces=('konq-plugins')
	install='kdebase.install'
	cd $srcdir/build/konq-plugins
	make DESTDIR=$pkgdir install
}

package_kdebase-konqueror() {
	pkgdesc='KDE File Manager & Web Browser'
	depends=('kdebase-dolphin' 'kdebase-keditbookmarks')
	optdepends=('kwebkitpart: to enable webkit engine')
	url="http://kde.org/applications/internet/konqueror/"
	install='kdebase-konqueror.install'
        conflicts=('kdebase-nsplugins')
        replaces=('kdebase-nsplugins')
        for i in konqueror doc/konqueror nsplugins; do
              cd $srcdir/build/${i}
              make DESTDIR=$pkgdir install
        done
}

package_kdebase-lib() {
	pkgdesc='KDE libraries for the basic desktop applications'
	groups=()
	depends=('kdelibs')
	cd $srcdir/build/lib
	make DESTDIR=$pkgdir install
}

package_kdebase-plasma() {
	pkgdesc='Display the contents of folders (UserÂ´s home folder as default)'
	depends=('kdebase-workspace' 'kdebase-lib')
	cd $srcdir/build/plasma
	make DESTDIR=$pkgdir install
}
