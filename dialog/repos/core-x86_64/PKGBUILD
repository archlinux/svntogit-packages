# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>

pkgname=dialog
pkgver=1.2_20150513
pkgrel=1
epoch=1
pkgdesc="A tool to display dialog boxes from shell scripts"
arch=('i686' 'x86_64')
url="http://invisible-island.net/dialog/"
license=('LGPL2.1')
depends=('sh' 'ncurses')
source=(ftp://invisible-island.net/$pkgname/$pkgname-${pkgver/_/-}.tgz)
sha256sums=('a70f957e983235ccbe63f8c5a45cd5ebde547bd57cedd07e35527a236d3cab4c')

build() {
  cd "$srcdir/$pkgname-${pkgver/_/-}"

  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man \
    --with-ncursesw \
    --with-shared \
    --with-versioned-syms \
    --enable-nls
  make
}

package() {
  cd "$srcdir/$pkgname-${pkgver/_/-}"
  make DESTDIR="$pkgdir" install-full
}

# vim:set ts=2 sw=2 et:
