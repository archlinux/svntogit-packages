# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: Thomas Arnhold <thomas at arnhold dot org>

pkgname=liborcus
pkgver=0.13.4
pkgrel=4
pkgdesc="File import filter library for spreadsheet documents."
arch=('x86_64')
url="https://gitlab.com/orcus/orcus/blob/master/README.md"
license=('MPL')
depends=('libixion')
makedepends=('boost' 'mdds')
source=(https://kohei.us/files/orcus/src/${pkgname}-${pkgver}.tar.xz
        boost-1.67.patch)
sha256sums=('b71c4c15febe7dae63406e8023898e3a5cf7fe4ec43b2028dfbbf24e9fe282e4'
            'ea6041d217e775bb5e5bb2bce2c6bc5b12de4397bc80b1a5d1d1a0c37144b420')

prepare() {
    cd ${pkgname}-${pkgver}
    patch -Np1 -i ../boost-1.67.patch
}

build() {
    cd ${pkgname}-${pkgver}
    ./configure --prefix=/usr
    make
}

check() {
    cd ${pkgname}-${pkgver}
    make check
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR=$pkgdir install
}
