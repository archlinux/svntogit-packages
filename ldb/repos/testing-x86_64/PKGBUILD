# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Christian Hesse <mail@eworm.de>
# Contributor: Marco A Rojas <marquicus at gmail dot com>

pkgname=ldb
pkgver=1.5.6
pkgrel=1
epoch=1
pkgdesc="A schema-less, ldap like, API and database"
arch=('x86_64')
url="https://ldb.samba.org/"
source=(https://samba.org/ftp/${pkgname}/${pkgname}-${pkgver}.tar.gz
        https://samba.org/ftp/${pkgname}/${pkgname}-${pkgver}.tar.asc)
license=('GPL3')
depends=('talloc' 'tevent' 'tdb' 'popt' 'lmdb')
makedepends=('python' 'cmocka' 'docbook-xsl')
optdepends=('python: for python bindings')
validpgpkeys=('9147A339719518EE9011BCB54793916113084025') # Samba Library Distribution Key <samba-bugs@samba.org> 

build() {
	cd ${pkgname}-${pkgver}

	./configure --prefix=/usr \
		--disable-rpath \
		--disable-rpath-install \
		--bundled-libraries=NONE \
		--builtin-libraries=replace \
                --with-modulesdir=/usr/lib/ldb/modules \
                --with-privatelibdir=/usr/lib/ldb
	make
}

package() {
	cd ${pkgname}-${pkgver}

	make DESTDIR="${pkgdir}/" install
}
md5sums=('fc58ef432c1fcb03fc3bb6cccce08977'
         'SKIP')
