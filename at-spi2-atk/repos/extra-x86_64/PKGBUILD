# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=at-spi2-atk
pkgver=2.34.0
pkgrel=1
pkgdesc="A GTK+ module that bridges ATK to D-Bus at-spi"
url="https://wiki.gnome.org/Accessibility"
arch=(x86_64)
license=(LGPL)
depends=(at-spi2-core libx11 libxml2 atk)
makedepends=(git meson)
_commit=15ad5b3aab208c97352187e2408d7b3752504230  # tags/AT_SPI2_ATK_2_34_0^0
source=("git+https://gitlab.gnome.org/GNOME/at-spi2-atk.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^AT_SPI2_ATK_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  dbus-run-session meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}

# vim:set ts=2 sw=2 et:
