# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=at-spi2-atk
pkgver=2.8.0
pkgrel=1
pkgdesc="A GTK+ module that bridges ATK to D-Bus at-spi"
arch=('i686' 'x86_64')
url="http://www.gnome.org"
license=('GPL2')
depends=('at-spi2-core' 'libx11' 'atk')
makedepends=('intltool')
options=('!libtool')
source=(http://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('4688acbc1474cda0aa49341f109ad0726603ce3e872cc6521c74931338c7ba20')

build() {
  cd $pkgname-$pkgver

  sed -i -e '/AC_PATH_XTRA/d' configure.ac
  autoreconf --force --install

  ./configure --prefix=/usr --sysconfdir=/etc \
    --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
