# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-lskat
pkgver=4.11.3
pkgrel=1
pkgdesc="Lieutenant Skat is a fun and engaging card game for two players"
url="http://kde.org/applications/games/lskat/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'libkdegames')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/lskat-${pkgver}.tar.xz")
sha1sums=('86046bcb33bbe85415fe0275964366236228ac3f')

build() {
  mkdir build
  cd build
  cmake ../lskat-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
