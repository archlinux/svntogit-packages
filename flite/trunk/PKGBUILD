# $Id$
# Maintainer:
# Contributor: Simo Leone <simo@archlinux.org>

pkgname=flite
pkgver=1.4
pkgrel=1
pkgdesc="A lighweight version of festival speech synthesis"
arch=('i686' 'x86_64')
url="http://www.speech.cs.cmu.edu/flite/"
license=('custom')
depends=('glibc')
source=(http://www.speech.cs.cmu.edu/flite/packed/${pkgname}-${pkgver}/${pkgname}-${pkgver}-release.tar.bz2)
md5sums=('b7c3523b3bbc6f29ce61e6650cd9a428')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}-release"
  ./configure --prefix=/usr || return 1

  #we want the 16k, not 8k voice
  sed -i 's/cmu_us_kal/cmu_us_kal16/' config/config || return 1
  
  make || return 1
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}-release"
  make prefix="${pkgdir}/usr" install || return 1
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE" || return 1
}
