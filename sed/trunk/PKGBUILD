# $Id$
# Maintainer: SÃ©bastien "Seblu" Luttringer <seblu@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=sed
pkgver=4.2.2
pkgrel=4
pkgdesc='GNU stream editor'
arch=('i686' 'x86_64')
url='http://www.gnu.org/software/sed'
license=('GPL3')
groups=('base' 'base-devel')
depends=('glibc' 'acl' 'attr')
makedepends=('gettext')
# gpg key of Paolo Bonzini has been revoked in 2013, not using it
# check if a new one is issued in the next release
source=("ftp://ftp.gnu.org/pub/gnu/sed/$pkgname-$pkgver.tar.gz")
md5sums=('4111de4faa3b9848a0686b2f260c5056')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
