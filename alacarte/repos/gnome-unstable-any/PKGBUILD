# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: pressh <pressh@gmail.com>

pkgname=alacarte
pkgver=3.10.0
pkgrel=1
pkgdesc="Menu editor for gnome"
arch=(any)
license=('LGPL')
url="http://www.gnome.org"
depends=('gnome-menus' 'hicolor-icon-theme' 'python-gobject' 'gtk3')
makedepends=('intltool')
install=alacarte.install
options=('!libtool')
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:4}/${pkgname}-${pkgver}.tar.xz
        py3.patch)
sha256sums=('bdc91981859a5076ae38a213892074e1263629835702920d47f6ee2bb76179f1'
            '19dd62126e4eb6eee2f1175a5e2120ee360e06461d2e275e1b494dadbdf4dfae')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../py3.patch
}

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var PYTHON=python
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
