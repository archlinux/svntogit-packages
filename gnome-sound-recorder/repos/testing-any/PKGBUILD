# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-sound-recorder
pkgver=3.24.0.1+8+g0b8e221
pkgrel=1
pkgdesc="A utility to make simple audio recording from your GNOME desktop"
url="https://wiki.gnome.org/Apps/SoundRecorder"
arch=(any)
license=(GPL2)
depends=(gtk3 gjs gst-plugins-good gst-plugins-base)
makedepends=(intltool gobject-introspection git gnome-common)
groups=('gnome-extra')
_commit=0b8e221b73add3c57cc9b24d0eb1fe11c67a7d16  # master
source=("git+https://git.gnome.org/browse/gnome-sound-recorder#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

check() {
  cd $pkgname
  make check
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
