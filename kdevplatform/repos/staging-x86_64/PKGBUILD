# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=kdevplatform
pkgver=5.0.3
pkgrel=1
pkgdesc='A C/C++ development platform for KDE'
arch=(i686 x86_64)
url='http://www.kdevelop.org/'
license=(GPL)
depends=(grantlee hicolor-icon-theme kcmutils threadweaver kitemmodels ktexteditor knotifyconfig knewstuff libkomparediff2 qt5-webkit)
makedepends=(extra-cmake-modules boost subversion python)
optdepends=('subversion: Subversion plugin')
source=("http://download.kde.org/stable/kdevelop/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha1sums=('9c8900deb39d2627f6d861e238935ad543de045b'
          'SKIP')
validpgpkeys=('329FD02C5AA48FCC77A4BBF0AC44AC6DB29779E6') # Sven Brauch <mail@svenbrauch.de>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
