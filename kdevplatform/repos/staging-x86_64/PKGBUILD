# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=kdevplatform
pkgver=1.7.2
_pkgver=4.7.2
pkgrel=2
pkgdesc='A C/C++ development platform for KDE'
arch=('i686' 'x86_64')
url='http://www.kdevelop.org/'
license=('GPL')
depends=('kdelibs' 'subversion' 'qjson' 'grantlee-qt4')
makedepends=('cmake' 'automoc4' 'boost')
optdepends=('kdesdk-kompare: difference checking')
install="${pkgname}.install"
source=("http://download.kde.org/stable/kdevelop/${_pkgver}/src/${pkgname}-${pkgver}.tar.xz")
sha1sums=('eed01990b91e81bffeb24104da604ee7b8f45cad')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
