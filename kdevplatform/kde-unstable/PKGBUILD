# $Id: PKGBUILD 248913 2015-10-12 10:00:30Z arojas $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=kdevplatform
pkgver=4.90.90
pkgrel=1
pkgdesc='A C/C++ development platform for KDE'
arch=(i686 x86_64)
url='http://www.kdevelop.org/'
license=(GPL)
depends=(grantlee-qt5 hicolor-icon-theme kcmutils threadweaver kitemmodels ktexteditor knotifyconfig knewstuff libkomparediff2 qt5-quick1)
makedepends=(extra-cmake-modules boost subversion)
optdepends=('subversion: Subversion plugin')
install="$pkgname.install"
source=("http://download.kde.org/unstable/kdevelop/$pkgver/src/$pkgname-$pkgver.tar.xz")
sha1sums=('0231764f5c8f41bb69dae7aa780a531e584103e2')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
