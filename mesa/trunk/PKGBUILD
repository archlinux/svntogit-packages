# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase="mesa"
pkgname=('mesa' 'libgl' 'ati-dri' 'intel-dri' 'unichrome-dri' 'mach64-dri' 'mga-dri' 'r128-dri' 'savage-dri' 'sis-dri' 'tdfx-dri' 'trident-dri')
pkgver=7.5
pkgrel=2
arch=(i686 x86_64)
makedepends=('glproto' 'pkgconfig' 'libdrm>=2.4.12' 'libxxf86vm' 'libxdamage' 'expat>=2.0.1' 'libx11>=1.2.2' 'libxt' 'gcc-libs' 'dri2proto=2.1')
url="http://mesa3d.sourceforge.net"
license=('custom')
options=(!makeflags)
source=(http://downloads.sourceforge.net/mesa3d/MesaLib-${pkgver}.tar.bz2
        http://downloads.sourceforge.net/mesa3d/MesaDemos-${pkgver}.tar.bz2
        ftp://ftp.archlinux.org/other/mesa/gl-manpages-1.0.1.tar.bz2
        mesa-7.1-link-shared.patch
        intel-revert-vbl.patch
        LICENSE
        git-fixes.patch)
md5sums=('459f332551f6ebb86f384d21dd15e1f0'
         '398ee8801814a00e47f6c2314e3dfddc'
         '6ae05158e678f4594343f32c2ca50515'
         '9eddc02e23ec08295822911e726ae89a'
         'a111f4dc82e894f8801bc3fa129af7af'
         '5c65a0fe315dd347e09b1f2826a1df5a'
         'dcfc0a49080ea6ad8b848b5ad92e1a2d')

build() {
  cd "${srcdir}/Mesa-${pkgver}"
  patch -Np1 -i "${srcdir}/mesa-7.1-link-shared.patch" || return 1
  patch -Np1 -i "${srcdir}/git-fixes.patch" || return 1
  patch -Np1 -i "${srcdir}/intel-revert-vbl.patch" || return 1
  ./configure --prefix=/usr \
    --with-dri-driverdir=/usr/lib/xorg/modules/dri \
    --with-dri-drivers=swrast,radeon,r200,r300,i810,i915,i965,unichrome,mach64,mga,r128,savage,sis,tdfx,trident \
    --enable-glx-tls \
    --with-driver=dri \
    --enable-xcb \
    --disable-glut || return 1
  make || return 1

  cd "${srcdir}/gl-manpages-1.0.1"
  ./configure --prefix=/usr || return 1
  make || return 1
}

package_libgl() {
  depends=('libdrm>=2.4.12' 'libxxf86vm' 'libxdamage' 'expat>=2.0.1')
  pkgdesc="Mesa 3-D graphics library and DRI software rasterizer"

  cd "${srcdir}/Mesa-${pkgver}" || return 1
  install -m755 -d "${pkgdir}/usr/lib" || return 1
  install -m755 -d "${pkgdir}/usr/lib/xorg/modules/extensions"

  bin/minstall lib/libGL.so* "${pkgdir}/usr/lib/" || return 1

  cd src/mesa/drivers/dri
  make -C swrast DESTDIR="${pkgdir}" install || return 1
  install -m755 libdricore.so "${pkgdir}/usr/lib/xorg/modules/dri/" || return 1
  ln -s libglx.xorg "${pkgdir}/usr/lib/xorg/modules/extensions/libglx.so" || return 1

  install -m755 -d "${pkgdir}/usr/share/licenses/libgl"
  install -m755 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/libgl/" || return 1
}

package_mesa() {
  depends=('libgl' 'libx11>=1.2.2' 'libxt' 'gcc-libs>=4.4.1' 'dri2proto=2.1' 'libdrm>=2.4.12')
  pkgdesc="Mesa 3-D graphics libraries and include files"

  cd "${srcdir}/Mesa-${pkgver}" || return 1
  make DESTDIR="${pkgdir}" install || return 1
  install -m755 -d "${pkgdir}/usr/bin"
  install -m755 progs/xdemos/glx{gears,info} "${pkgdir}/usr/bin/" || return 1

  rm -f "${pkgdir}/usr/lib/libGL.so"*
  rm -rf "${pkgdir}/usr/lib/xorg"

  cd "${srcdir}/gl-manpages-1.0.1" || return 1
  make DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/licenses/mesa"
  install -m755 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/mesa/" || return 1
}

package_ati-dri() {
  depends=("libgl=${pkgver}")
  pkgdesc="Mesa DRI drivers for AMD/ATI Radeon"
  conflicts=('xf86-video-ati<6.9.0-6')

  cd "${srcdir}/Mesa-${pkgver}/src/mesa/drivers/dri" || return 1
  make -C radeon DESTDIR="${pkgdir}" install || return 1
  make -C r200 DESTDIR="${pkgdir}" install || return 1
  make -C r300 DESTDIR="${pkgdir}" install || return 1
}

package_intel-dri() {
  depends=("libgl=${pkgver}")
  pkgdesc="Mesa DRI drivers for Intel"

  cd "${srcdir}/Mesa-${pkgver}/src/mesa/drivers/dri" || return 1
  make -C i810 DESTDIR="${pkgdir}" install || return 1
  make -C i915 DESTDIR="${pkgdir}" install || return 1
  make -C i965 DESTDIR="${pkgdir}" install || return 1

  cd "${srcdir}/Mesa-${pkgver}/src/gallium/winsys/drm/intel/egl"
  make DESTDIR="${pkgdir}" install || return 1
}

package_unichrome-dri() {
  depends=("libgl=${pkgver}")
  pkgdesc="Mesa DRI drivers for S3 Graphics/VIA Unichrome"

  cd "${srcdir}/Mesa-${pkgver}/src/mesa/drivers/dri" || return 1
  make -C unichrome DESTDIR="${pkgdir}" install || return 1
}

package_mach64-dri() {
  depends=("libgl=${pkgver}")
  pkgdesc="Mesa DRI drivers for ATI Mach64"
  conflicts=('xf86-video-mach64<6.8.2')

  cd "${srcdir}/Mesa-${pkgver}/src/mesa/drivers/dri" || return 1
  make -C mach64 DESTDIR="${pkgdir}" install || return 1
}

package_mga-dri() {
  depends=("libgl=${pkgver}")
  pkgdesc="Mesa DRI drivers for Matrox"
  conflicts=('xf86-video-mga<1.4.11')

  cd "${srcdir}/Mesa-${pkgver}/src/mesa/drivers/dri" || return 1
  make -C mga DESTDIR="${pkgdir}" install || return 1
}

package_r128-dri() {
  depends=("libgl=${pkgver}")
  pkgdesc="Mesa DRI drivers for ATI Rage128"
  conflicts=('xf86-video-r128<6.8.1')

  cd "${srcdir}/Mesa-${pkgver}/src/mesa/drivers/dri" || return 1
  make -C r128 DESTDIR="${pkgdir}" install || return 1
}

package_savage-dri() {
  depends=("libgl=${pkgver}")
  pkgdesc="Mesa DRI drivers for S3 Sraphics/VIA Savage"
  conflicts=('xf86-video-savage<2.3.1')

  cd "${srcdir}/Mesa-${pkgver}/src/mesa/drivers/dri" || return 1
  make -C savage DESTDIR="${pkgdir}" install || return 1
}

package_sis-dri() {
  depends=("libgl=${pkgver}")
  pkgdesc="Mesa DRI drivers for SiS"
  conflicts=('xf86-video-sis<0.10.2')

  cd "${srcdir}/Mesa-${pkgver}/src/mesa/drivers/dri" || return 1
  make -C sis DESTDIR="${pkgdir}" install || return 1
}

package_tdfx-dri() {
  depends=("libgl=${pkgver}")
  pkgdesc="Mesa DRI drivers for 3dfx"
  conflicts=('xf86-video-tdfx<1.4.3')

  cd "${srcdir}/Mesa-${pkgver}/src/mesa/drivers/dri" || return 1
  make -C tdfx DESTDIR="${pkgdir}" install || return 1
}

package_trident-dri() {
  depends=("libgl=${pkgver}")
  pkgdesc="Mesa DRI drivers for Trident"
  conflicts=('xf86-video-trident<1.3.2-3')

  cd "${srcdir}/Mesa-${pkgver}/src/mesa/drivers/dri" || return 1
  make -C trident DESTDIR="${pkgdir}" install || return 1
}
