# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
pkgname=xf86-video-r128
pkgver=6.8.0
pkgrel=6
_mesaver="7.5"
pkgdesc="X.org ati Rage128 video driver"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libdrm>=2.4.12' "libgl>=${_mesaver}" 'expat')
makedepends=('pkgconfig' 'xorg-server>=1.6.2' "mesa>=${_mesaver}" 'glproto>=1.4.9' 'xf86driproto' 'diffutils' 'dri2proto' 'xineramaproto')
conflicts=('xorg-server<1.6.2')
groups=('xorg-video-drivers')
options=('!libtool')
source=(${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2
       http://downloads.sourceforge.net/sourceforge/mesa3d/MesaLib-${_mesaver}.tar.bz2
       mesa-7.1-link-shared.patch
       LICENSE)
md5sums=('007bec0c6b07d9d3aeca4da18e561efb'
         '459f332551f6ebb86f384d21dd15e1f0'
         '9eddc02e23ec08295822911e726ae89a'
         '97ad034bea2a153f4418a6bb0c77acf4')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --enable-dri || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1

  cd "${srcdir}/Mesa-${_mesaver}"
  patch -Np1 -i "${srcdir}/mesa-7.1-link-shared.patch" || return 1
  ./configure --prefix=/usr \
    --with-dri-driverdir=/usr/lib/xorg/modules/dri \
    --with-dri-drivers=r128 \
    --enable-glx-tls \
    --with-driver=dri \
    --enable-xcb \
    --disable-glu \
    --disable-glut \
    --disable-glw || return 1
  make || return 1
  cd src/mesa/drivers/dri || return 1
  make DESTDIR="${pkgdir}" install || return 1
  rm -f "${pkgdir}/usr/lib/xorg/modules/dri/libdricore.so"
  rm -rf "${pkgdir}/usr/include"
  rm -rf "${pkgdir}/usr/lib/pkgconfig"
  install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE" || return 1
}
