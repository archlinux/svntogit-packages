# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xf86-video-r128
pkgver=6.9.2
pkgrel=5
pkgdesc="X.org ati Rage128 video driver"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('glibc')
makedepends=('xorg-server-devel' 'X-ABI-VIDEODRV_VERSION=19')
conflicts=('xorg-server<1.16' 'X-ABI-VIDEODRV_VERSION<19' 'X-ABI-VIDEODRV_VERSION>=20')
optdepends=('r128-dri: DRI1 support from community repo')
groups=('xorg-drivers' 'xorg')
source=(${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2 
        git-fixes.patch
        exa.patch
        0001-Fix-allocation-of-private-entity.patch
        0001-Look-for-VGA-ports-in-the-VBIOS.patch
        0001-Update-bitmasks-for-DDC.patch)
md5sums=('2e906d856a1c477bde000254b142445c'
         '4f1b508b64dd0435f1ab85c043bd531d'
         'de1c99f8697c25d0d07cc31754d31a4e'
         '1fe6389478461b71704f66161a1d3a91'
         '086aba37a33d624bd83686fb190639b2'
         '6d8bbf5c7f77596d7df040b14b542806')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -Np1 -i ../git-fixes.patch
  # fixes for r128 with VGA port, pushed upstream by Connor Behan
  # tested by Tobias Powalowski
  patch -Np1 -i ../exa.patch
  patch -Np1 -i ../0001-Fix-allocation-of-private-entity.patch
  patch -Np1 -i ../0001-Look-for-VGA-ports-in-the-VBIOS.patch
  patch -Np1 -i ../0001-Update-bitmasks-for-DDC.patch
  autoreconf -fi
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
