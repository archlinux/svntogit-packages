# $Id: PKGBUILD 226395 2014-11-19 15:22:21Z fyan $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kfilemetadata4
_pkgname=kfilemetadata
pkgver=4.14.3
pkgrel=2
pkgdesc="A library for extracting file metadata"
arch=('i686' 'x86_64')
url='https://projects.kde.org/kfilemetadata'
license=('LGPL')
depends=('poppler-qt' 'taglib' 'ffmpeg' 'ebook-tools' 'kdegraphics-mobipocket')
makedepends=('cmake' 'automoc4')
conflicts=('kfilemetadata<5')
replaces=('kfilemetadata<5')
source=("http://download.kde.org/stable/${pkgver}/src/${_pkgname}-${pkgver}.tar.xz")
sha1sums=('6b106223b3d4ab9ba0516b8301e564af2416eaa9')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../${_pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
