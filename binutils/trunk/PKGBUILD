# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>

# toolchain build order: linux-api-headers->glibc->binutils->gcc->binutils->glibc

pkgname=binutils
pkgver=2.23
pkgrel=1
pkgdesc="A set of programs to assemble and manipulate binary and object files"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/binutils/"
license=('GPL')
groups=('base-devel')
depends=('glibc>=2.16' 'zlib')
checkdepends=('dejagnu' 'bc')
options=('!libtool' '!distcc' '!ccache')
install=binutils.install
source=(ftp://ftp.gnu.org/gnu/binutils/binutils-${pkgver}.tar.bz2{,.sig})
md5sums=('6e6c85eb4468501051ae209c9e2a7ecc'
         '46ca898c6c7ab8f35c676a965c7ce804')

build() {
  cd ${srcdir}
  mkdir binutils-build && cd binutils-build

  ${srcdir}/binutils-${pkgver}/configure --prefix=/usr \
    --with-lib-path=/usr/lib:/usr/local/lib \
    --with-bugurl=https://bugs.archlinux.org/ \
    --enable-ld=default --enable-gold \
    --enable-plugins --enable-threads --enable-shared \
    --disable-werror --disable-multilib

  # check the host environment and makes sure all the necessary tools are available
  make configure-host

  make tooldir=${pkgdir}/usr

  # Rebuild libiberty.a with -fPIC
  cp -a libiberty libiberty-pic
  make -C libiberty-pic clean
  make CFLAGS="$CFLAGS -fPIC" -C libiberty-pic

  # Rebuild libbfd.a with -fPIC
  # hidden visability prevent 3rd party shared libraries exporting bfd non-stable API
  cp -a bfd bfd-pic
  make -C bfd-pic clean
  make CFLAGS="$CFLAGS -fPIC -fvisibility=hidden" -C bfd-pic

  # Rebuild libopcodes.a with -fPIC
  cp -a opcodes opcodes-pic
  make -C opcodes-pic clean 
  make CFLAGS="$CFLAGS -fPIC" -C opcodes-pic
}

check() {
  cd ${srcdir}/binutils-build
  
  # unset LDFLAGS as testsuite makes assumptions about which ones are active
  # do not abort on errors - manually check log files
  make LDFLAGS="" -k check || true
}

package() {
  cd ${srcdir}/binutils-build
  make prefix=${pkgdir}/usr tooldir=${pkgdir}/usr install
  
  # Add some useful headers
  install -m644 ${srcdir}/binutils-${pkgver}/include/libiberty.h ${pkgdir}/usr/include
  install -m644 ${srcdir}/binutils-${pkgver}/include/demangle.h ${pkgdir}/usr/include

  # install libraries rebuilt with -fPIC
  install -m644 libiberty-pic/libiberty.a ${pkgdir}/usr/lib
  install -m644 bfd-pic/libbfd.a ${pkgdir}/usr/lib
  install -m644 opcodes/libopcodes.a ${pkgdir}/usr/lib

  # Remove Windows/Novell specific man pages
  rm ${pkgdir}/usr/share/man/man1/{dlltool,nlmconv,windres,windmc}*

  # Remove these symlinks, they are not ABI stable.
  # Programs should compile static to the .a file.
  rm ${pkgdir}/usr/lib/lib{bfd,opcodes}.so
  echo "INPUT ( /usr/lib/libbfd.a -liberty -lz )" >${pkgdir}/usr/lib/libbfd.so
  echo "INPUT ( /usr/lib/libopcodes.a -lbfd )" >${pkgdir}/usr/lib/libopcodes.so
}
