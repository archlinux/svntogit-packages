# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=libwpe
pkgver=1.3.91
pkgrel=1
pkgdesc="General-purpose library for WPE WebKit"
url="https://wpewebkit.org/"
arch=(x86_64)
license=(custom)
depends=(libxkbcommon gcc-libs)
makedepends=(cmake mesa)
source=($url/releases/$pkgname-$pkgver.tar.xz{,.asc})
sha256sums=('34d2e7057700d5cfaead4e554fe253d30613279f2e7b9e3ea4a307f06799f93f'
            'SKIP')
validpgpkeys=('5AA3BC334FD7E3369E7C77B291C559DBE4C9123B') # Adrián Pérez de Castro <aperez@igalia.com>

prepare() {
  cd $pkgname-$pkgver
}

build() {
  cmake -H$pkgname-$pkgver -Bbuild \
    -DCMAKE_BUILD_TYPE=Plain \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --build build --target install
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" $pkgname-$pkgver/COPYING
}

# vim:set sw=2 et:
