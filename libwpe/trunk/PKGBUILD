# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=libwpe
pkgver=1.12.0
pkgrel=2
pkgdesc="General-purpose library for WPE WebKit"
url="https://wpewebkit.org"
arch=(x86_64)
license=(custom)
depends=(libxkbcommon gcc-libs)
makedepends=(mesa git meson)
provides=(libwpe-1.0.so)
options=(debug)
_commit=c785ad5fad51c20bd47f648a05279ec34b08848d  # tags/1.12.0^0
source=("git+https://github.com/WebPlatformForEmbedded/libwpe?signed#commit=$_commit")
sha256sums=('SKIP')
validpgpkeys=('5AA3BC334FD7E3369E7C77B291C559DBE4C9123B') # Adrián Pérez de Castro <aperez@igalia.com>

pkgver() {
  cd libwpe
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd libwpe
}

build() {
  arch-meson libwpe build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 libwpe/COPYING
}

# vim:set sw=2 et:
