# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdesignerplugin
pkgver=5.69.0
pkgrel=1
pkgdesc='Integration of Frameworks widgets in Qt Designer/Creator'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(kconfig kcoreaddons)
makedepends=(extra-cmake-modules qt5-tools kdoctools)
groups=(kf5-aids)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/portingAids/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('7af5b4647cd9a912422c705894a7c13eddc914e5b464f3ab81aab5b9fa0f2dfd'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
