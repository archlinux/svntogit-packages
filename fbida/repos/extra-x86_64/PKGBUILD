# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=fbida
pkgver=2.10
pkgrel=1
pkgdesc="Few applications to display and elementary edit images: fbi, fbgs, ida, exiftran"
arch=('i686' 'x86_64')
url="http://www.kraxel.org/blog/linux/fbida/"
license=('GPL2')
depends=('giflib' 'libtiff' 'libexif' 'lesstif' 'libpng' 'fontconfig' 'curl' 'desktop-file-utils')
makedepends=('libxpm' 'imagemagick')
optdepends=('ghostscript: to use fbgs')
install=fbida.install
source=(http://www.kraxel.org/releases/${pkgname}/${pkgname}-${pkgver}.tar.gz{,.asc}
        ida-desktop.patch)
sha1sums=('127d80f258d429ff8f2474949b1cf0ab78682ff7'
          'SKIP'
          'd06b70f2d581a1a3e9e0909b5af7f3f71ed54717')
validpgpkeys=('A0328CFFB93A17A79901FE7D4CB6D8EED3E87138')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -i 's/ungif/gif/' GNUmakefile
  patch -p0 -i "${srcdir}/ida-desktop.patch"
  echo 2.10 > VERSION
}

build() {
  cd ${pkgname}-${pkgver}
  make verbose=yes
  convert -resize 25% logo.jpg ida.png
}

package() {
  cd ${pkgname}-${pkgver}
  make STRIP="" DESTDIR="${pkgdir}" prefix=/usr install
  install -D -m644 desktop/ida.desktop "${pkgdir}/usr/share/applications/ida.desktop"
  install -D -m644 ida.png "${pkgdir}/usr/share/pixmaps/ida.png"
}
