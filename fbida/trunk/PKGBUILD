# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=fbida
pkgver=2.07
pkgrel=5
pkgdesc="Few applications to display and elementary edit images: fbi, fbgs, ida, exiftran"
arch=('i686' 'x86_64')
url="http://linux.bytesex.org/fbida/"
license=('GPL2')
depends=('giflib' 'libtiff' 'libjpeg' 'libexif' 'lesstif' 'libxpm' 'libpng' 'libx11' 'libxext' 'fontconfig')
optdepends=('ghostscript: to use fbgs')
replaces=('fbi')
provides=('fbi')
source=(http://dl.bytesex.org/releases/${pkgname}/${pkgname}-${pkgver}.tar.gz libpng-1.4.patch \
        transupp.h transupp.c jinclude.h)
md5sums=('3e05910fb7c1d9b2bd3e272d96db069c' '31b5a5318c16808d55403e06110ff5a5'\
         '785d928f4ff04e06bd2c8acc6b6c5e7b' 'f2d6f905c4ce6098f6c1af77a0a4eef8'\
         'dbde79bc104a2caa9316cc2a9df7fd25')
sha1sums=('4758178299e09d5251b9cf20337a81cc20553d45' 'dcb39c82e7e6f4d7d6e8d0e95c661f83a2655c7f'\
         '5028318704fbc8f476f858d24b39cf454a16fe85' '0d9f58703d2eb4ce2d11f42a1ad01d958efe3312'\
         '31ab682733b096b3a98c0a35f9b54a7936e480d5')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 < ../libpng-1.4.patch || return 1

# Using source code from libjpeg 8a
  rm jpeg/*
  cp ${srcdir}/transupp.h jpeg/
  cp ${srcdir}/transupp.c jpeg/
  cp ${srcdir}/jinclude.h jpeg/

  make Make.config || return 1
  for config in HAVE_LIB{SANE,CURL,LIRC}; do
     sed -i "s/$config.*/$config := no/" Make.config || return 1
  done
  make CC=gcc || return 1
  make DESTDIR="${pkgdir}" prefix=/usr install || return 1
}

