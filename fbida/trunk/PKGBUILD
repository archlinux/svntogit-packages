# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=fbida
pkgver=2.09
pkgrel=4
pkgdesc="Few applications to display and elementary edit images: fbi, fbgs, ida, exiftran"
arch=('i686' 'x86_64')
url="http://www.kraxel.org/blog/linux/fbida/"
license=('GPL2')
depends=('giflib' 'libtiff' 'libexif' 'lesstif' 'libpng' 'fontconfig' 'curl' 'desktop-file-utils')
makedepends=('libxpm' 'imagemagick')
optdepends=('ghostscript: to use fbgs')
install=fbida.install
source=(http://www.kraxel.org/releases/${pkgname}/${pkgname}-${pkgver}.tar.gz{,.asc}
        ida-desktop.patch ida-PrintGifError.patch)
sha1sums=('5838b2cb7188481fece6159fccb3b121acd906e7'
          'SKIP'
          'd06b70f2d581a1a3e9e0909b5af7f3f71ed54717'
          'a5644d75e868bfc23bc462c3b4bc84c1a168146c')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -i 's/ungif/gif/' GNUmakefile
  patch -p0 -i "${srcdir}/ida-desktop.patch"
  patch -p1 -i "${srcdir}/ida-PrintGifError.patch"
}

build() {
  cd ${pkgname}-${pkgver}
  make verbose=yes
  convert -resize 25% logo.jpg ida.png
}

package() {
  cd ${pkgname}-${pkgver}
  make STRIP="" DESTDIR="${pkgdir}" prefix=/usr install
  install -D -m644 desktop/ida.desktop "${pkgdir}/usr/share/applications/ida.desktop"
  install -D -m644 ida.png "${pkgdir}/usr/share/pixmaps/ida.png"
}
