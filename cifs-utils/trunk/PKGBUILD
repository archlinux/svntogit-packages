# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
pkgname=cifs-utils
pkgver=6.13
pkgrel=3
pkgdesc="CIFS filesystem user-space tools"
arch=(x86_64)
url="https://wiki.samba.org/index.php/LinuxCIFS_utils"
license=('GPL')
depends=('libcap' 'keyutils' 'krb5' 'talloc' 'libwbclient' 'pam')
makedepends=('python-docutils')
source=("https://download.samba.org/pub/linux-cifs/$pkgname/$pkgname-$pkgver.tar.bz2"{,.asc}
        "cifs-utils-6.13_fix-regression-in-kerberos-mount.patch")

validpgpkeys=('C699981A31F338706C817650DF5BA9D30642D5A0') #cifs-utils Distribution Verification Key <cifs-utils@samba.org>
sha256sums=('43d8786c8613caccfa84913081c1d62bc2409575854cf895b05b48af0863d056'
            'SKIP'
            'ae3c2dca120d39995b4a24c7c4d3024c6ba16e342010d1811680a1236520731a')

prepare() {
  # Fix install to honor DESTDIR
  sed -e 's|cd \$(ROOTSBINDIR)|cd $(DESTDIR)$(ROOTSBINDIR)|g' -i $pkgname-$pkgver/Makefile.am
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -i "$srcdir/cifs-utils-6.13_fix-regression-in-kerberos-mount.patch"
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  # systemd support is broken in mount.cifs
  # https://bugs.archlinux.org/task/30958
  autoreconf -i
  # https://www.spinics.net/lists/linux-cifs/msg21550.html
  # change back to libcap-ng depend when upstream is fixed
  ./configure --prefix=/usr --with-libcap=yes --sbindir=/usr/bin --disable-systemd 
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" ROOTSBINDIR=/usr/bin install
  mkdir -p "$pkgdir"/etc/request-key.d
  install -m 644 contrib/request-key.d/cifs.idmap.conf "$pkgdir"/etc/request-key.d
  install -m 644 contrib/request-key.d/cifs.spnego.conf "$pkgdir"/etc/request-key.d
  # set mount.cifs uid, to enable none root mounting form fstab
  chmod +s "$pkgdir"/usr/bin/mount.cifs
  # fix idmap-plugin #42052
  mkdir -p "$pkgdir"/etc/cifs-utils
  ln -s /usr/lib/cifs-utils/idmapwb.so "${pkgdir}"/etc/cifs-utils/idmap-plugin
}
