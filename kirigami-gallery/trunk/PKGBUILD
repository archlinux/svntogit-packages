# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kirigami-gallery
pkgver=20.08.3
pkgrel=1
pkgdesc="Gallery application built using Kirigami"
url="https://www.kde.org/products/kirigami/"
arch=(x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kdesdk)
depends=(kirigami2 qt5-svg)
makedepends=(extra-cmake-modules qt5-tools)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('e93a3f3d39799d5b05d371b530b5615678ed37c1775a4437d0a544859e052050'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
