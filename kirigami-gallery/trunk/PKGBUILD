# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kirigami-gallery
pkgver=19.08.1
pkgrel=1
pkgdesc="Gallery application built using Kirigami"
url="https://www.kde.org/products/kirigami/"
arch=(x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kdesdk)
depends=(kirigami2 qt5-svg)
makedepends=(extra-cmake-modules qt5-tools)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('b981b26fb268448f20077f3e69b3e12f45de91289f5b2026c618cdbff9ec5241'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
