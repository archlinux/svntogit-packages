# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Hussam Al-Tayeb <ht990332@gmail.com>

pkgname=hunspell
pkgver=1.3.2
pkgrel=2
pkgdesc="Spell checker and morphological analyzer library and program"
arch=('i686' 'x86_64')
url="http://hunspell.sourceforge.net/"
license=('GPL' 'LGPL' 'MPL')
depends=('gcc-libs' 'readline')
optdepends=('perl: for ispellaff2myspell')
options=('!libtool')
source=(http://downloads.sourceforge.net/hunspell/hunspell-$pkgver.tar.gz)
md5sums=('3121aaf3e13e5d88dfff13fb4a5f1ab8')

build() {
  cd "$srcdir/hunspell-$pkgver"
  ./configure --prefix=/usr --disable-static \
	--with-ui --with-readline --with-experimental
  make
}

package() {
  cd "$srcdir/hunspell-$pkgver"
  make DESTDIR="$pkgdir" install
  
  # add generic hunspell.so for development and projects not using pkgconfig flags - FS#30592
  pushd $pkgdir/usr/lib
  ln -s libhunspell-?.?.so libhunspell.so
  popd
}
