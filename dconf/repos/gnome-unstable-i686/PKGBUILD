# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=dconf
pkgver=0.24.0
pkgrel=1
pkgdesc="A low-level configuration system."
arch=(i686 x86_64)
url="http://live.gnome.org/dconf"
license=('LGPL2.1')
depends=('glib2' 'libdbus')
makedepends=('vala' 'intltool' 'docbook-xsl' 'python2')
install=dconf.install
source=(http://download.gnome.org/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz)
sha256sums=('4373e0ced1f4d7d68d518038796c073696280e22957babb29feb0267c630fec2')

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc \
    --libexecdir=/usr/lib/dconf
  make
}

package() {
  cd "$pkgname-$pkgver"
  make completiondir=/usr/share/bash-completion/completions DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
