# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
pkgname=dconf
pkgver=0.8.0
pkgrel=1
pkgdesc="A low-level configuration system."
arch=(i686 x86_64)
url="http://live.gnome.org/dconf"
license=('LGPL2.1')
depends=('glib2')
makedepends=('vala' 'gobject-introspection' 'gtk3')
optdepends=('gtk3: for dconf-editor')
install=dconf.install
source=(http://download.gnome.org/sources/${pkgname}/0.8/${pkgname}-${pkgver}.tar.xz)
sha256sums=('05111e973c365696759dd1b37e3f5acc877eff24dd2e4036d742aac5da5dda3b')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr --sysconfdir=/etc \
    --libexecdir=/usr/lib/dconf
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
