# $Id$
# Maintainer:
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: grimi@poczta.fm

pkgname=sdparm
pkgver=1.07
pkgrel=1
pkgdesc="An utility similar to hdparm but for SCSI devices"
arch=('i686' 'x86_64')
url="http://sg.danny.cz/sg/sdparm.html"
license=('BSD')
depends=('glibc')
source=("http://sg.danny.cz/sg/p/${pkgname}-${pkgver}.tgz")
md5sums=('c807f9db3dd7af175214be0d7fece494')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -Dm644 COPYING \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
