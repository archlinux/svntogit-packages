# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Zuf <kontakt.zuf@gmail.com>
# Contributor: Darwin Bautista <djclue917@gmail.com>
# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgname=kdenlive
pkgver=16.04.2
pkgrel=1
pkgdesc="A non-linear video editor for Linux using the MLT video framework"
arch=(i686 x86_64)
url="http://www.kdenlive.org/"
license=(GPL)
groups=(kde-applications kdemultimedia)
depends=(knewstuff kplotting knotifyconfig kded kfilemetadata qt5-webkit mlt glu sdl_image hicolor-icon-theme qt5-quickcontrols)
makedepends=(extra-cmake-modules kdoctools v4l-utils python)
optdepends=('ffmpeg: for FFmpeg plugin'
            'cdrkit: for creation of DVD ISO images'
            'dvdauthor: for creation of DVD'
            'dvgrab: for firewire capture'
            'libdv: for webcam capture (if FFmpeg is not installed)'
            'recordmydesktop: for screen capture'
            'xine-ui: for DVD preview'
            'plasma-desktop: theme configuration')
source=("http://download.kde.org/stable/applications/${pkgver}/src/$pkgname-${pkgver}.tar.xz")
md5sums=('e23a43e619b1a958cbb10742866c9bcb')

prepare(){
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  make -C build DESTDIR="${pkgdir}" install
}

