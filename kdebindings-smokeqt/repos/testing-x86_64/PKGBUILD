# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdebindings-smokeqt
pkgver=4.13.2
pkgrel=1
pkgdesc="Language independent library for Qt bindings"
url="https://projects.kde.org/projects/kde/kdebindings/smoke/smokeqt"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kdebindings')
depends=('kdebindings-smokegen' 'qimageblitz' 'qscintilla')
makedepends=('cmake' 'automoc4' 'mesa')
conflicts=('kdebindings-smoke')
source=("http://download.kde.org/stable/${pkgver}/src/smokeqt-${pkgver}.tar.xz")
sha1sums=('b3c091a3cd59b1b8ce86a15a7bc9cce3972a7ddb')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../smokeqt-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_Qwt5=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
