# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdebindings-smokeqt
pkgver=4.9.0
pkgrel=1
pkgdesc="Language independent library for Qt bindings"
url="https://projects.kde.org/projects/kde/kdebindings/smoke/smokeqt"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kdebindings')
depends=('kdebindings-smokegen' 'qimageblitz' 'qscintilla')
makedepends=('cmake' 'automoc4')
conflicts=('kdebindings-smoke')
source=("http://download.kde.org/stable/${pkgver}/src/smokeqt-${pkgver}.tar.xz")
sha1sums=('d9c0b21882263e038cdb3de9d0e99ecbf2df7986')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../smokeqt-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_Qwt5=OFF
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
