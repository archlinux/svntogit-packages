# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Andrew Krawchyk <krawch_a@denison.edu>, Marco Lima <cipparello gmail com>

pkgname=librpcsecgss
pkgver=0.19
pkgrel=3
pkgdesc="Library for RPCSECGSS support"
arch=('i686' 'x86_64')
url="http://www.citi.umich.edu/projects/nfsv4/linux/"
license=('GPL')
depends=('glibc' 'heimdal')
makedepends=('pkgconfig' 'autoconf')
options=('!libtool')
source=("http://www.citi.umich.edu/projects/nfsv4/linux/$pkgname/$pkgname-$pkgver.tar.gz"
	librpcsecgss-0.18-heimdal.patch)
md5sums=('b45ed565bdc3099023aa35830ec92997'
         '0cfe088551d5776f5bc08c1741a34346')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Patch from gentoo for heimdal compatibility Bug #231395
  # http://bugs.gentoo.org/show_bug.cgi?id=231395
  patch -Np1 -i $srcdir/librpcsecgss-0.18-heimdal.patch || return 1
  
  rm -f config.guess config.sub ltmain.sh
  autoreconf -i
  
  GSSAPI_CFLAGS='-I/usr/include/gssapi' \
  ./configure --prefix=/usr
  make || return 1
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install || return 1
}
