# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Mark Rosenstand <mark@borkware.net>

pkgname=notification-daemon
pkgver=0.7.3
pkgrel=1
pkgdesc="Notification daemon for the desktop notifications framework"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.galago-project.org/specs/notification/"
depends=('gtk3' 'libcanberra')
groups=('gnome')
makedepends=('pkgconfig' 'intltool')
options=('!libtool' '!emptydirs')
source=(http://download.gnome.org/sources/${pkgname}/0.7/${pkgname}-${pkgver}.tar.xz)
sha256sums=('c40931557c42cd12fdb04ab531fd7d5c5766641e76babdbff6079a6d88342704')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure --prefix=/usr --sysconfdir=/etc \
        --libexecdir=/usr/lib/notification-daemon-1.0 \
        --localstatedir=/var --disable-static
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR=${pkgdir} install
}
