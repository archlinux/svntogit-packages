# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=xfce4-netload-plugin
pkgver=1.2.3
pkgrel=2
pkgdesc="A netload plugin for the Xfce panel"
arch=('i686' 'x86_64')
url="http://goodies.xfce.org/projects/panel-plugins/xfce4-netload-plugin"
license=('GPL')
groups=('xfce4-goodies')
depends=('xfce4-panel')
makedepends=('intltool')
install=$pkgname.install
source=(http://archive.xfce.org/src/panel-plugins/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2
        0001-Add-argument-as_bits-to-format_byte_humanreadable-fu.patch)
sha256sums=('43bd1f291c83143a8a6acbe81f63080da772918c1c9a694793a75b29a2d3ba2c'
            '0ac37c6091544a2807090d07c6ca7775ef598ab20292891b80f769650ec2cd6e')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # https://bugs.archlinux.org/task/42834
  patch -Np1 -i ../0001-Add-argument-as_bits-to-format_byte_humanreadable-fu.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
