# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-xcalc
pkgver=1.0.7
pkgrel=1
pkgdesc="Scientific calculator for X"
arch=('x86_64')
url="https://cgit.freedesktop.org/xorg/app/xcalc/"
license=('custom')
depends=('libxaw')
optdepends=('xorg-fonts-100dpi: for some symbols or'
           'xorg-fonts-75dpi: for some symbols')
makedepends=('xorg-util-macros')
source=(https://xorg.freedesktop.org/archive/individual/app/xcalc-${pkgver}.tar.bz2{,.sig})
sha512sums=('890cb9399e3c55816318ab5f0152ddba05ee141cbae6032759a3b762742ab54df8fcf0394dacc539299f93f27cbbb17aee5cbda5dde8b169a7099f20a1a38388'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # Alan Coopersmith <alan.coopersmith@oracle.com>

build() {
  cd xcalc-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd xcalc-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
