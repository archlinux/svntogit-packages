# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-xcalc
pkgver=1.0.6
pkgrel=1
pkgdesc="Scientific calculator for X"
arch=('i686' 'x86_64')
url="https://cgit.freedesktop.org/xorg/app/xcalc/"
license=('custom')
depends=('libxaw')
optdepends=('xorg-fonts-100dpi: for some symbols or'
           'xorg-fonts-75dpi: for some symbols')
makedepends=('xorg-util-macros')
source=(https://xorg.freedesktop.org/archive/individual/app/xcalc-${pkgver}.tar.bz2{,.sig})
sha256sums=('2f73c7160c09dc32586ea07daa408ac897c0a16eaa98cad9f9e4ee98cd9057d8'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # Alan Coopersmith <alan.coopersmith@oracle.com>

build() {
  cd "${srcdir}/xcalc-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/xcalc-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
