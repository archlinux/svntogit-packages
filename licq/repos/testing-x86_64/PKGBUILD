# $Id$
# Maintainer: Juergen Hoetzel <juergen@archlinux.org>

pkgname=licq
pkgver=1.8.2
pkgrel=1
pkgdesc="Advanced graphical ICQ clone and more for Unix"
arch=('i686' 'x86_64')
url="http://www.licq.org"
license=('GPL')
depends=('boost-libs' 'gpgme' 'libxss' 'qt4')
makedepends=('cmake' 'boost' 'optipng')
source=(http://downloads.sourceforge.net/sourceforge/licq/licq-${pkgver}.tar.bz2)
md5sums=('46af2bc95865ee61a103d27812fe6e6a')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  mkdir {,plugins/qt4-gui/}build

  # Fix invalid PNG images to work with libpng 1.6
  # https://github.com/licq-im/licq/pull/32
  optipng -quiet -force -fix plugins/qt4-gui/share/skins/Mirabilis/*.png
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # licq
  cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr  ..
  make

  # qt4-gui
  cd ../plugins/qt4-gui/build
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_MODULE_PATH="${srcdir}/${pkgname}-${pkgver}/cmake"  ..
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/build"
  make DESTDIR="${pkgdir}" install

  cd ../plugins/qt4-gui/build
  make DESTDIR="${pkgdir}" install
}
