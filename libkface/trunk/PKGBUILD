# $Id: PKGBUILD 226446 2014-11-19 17:09:45Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=libkface
pkgver=16.04.1
pkgrel=1
pkgdesc="A Qt/C++ wrapper around LibFace library to perform face recognition and detection over pictures"
url="https://www.kde.org/applications/graphics/"
arch=(i686 x86_64)
license=(GPL LGPL FDL)
depends=(qt5-base opencv)
makedepends=(extra-cmake-modules)
source=("http://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"
	libkface-opencv3.1.patch::"https://git.reviewboard.kde.org/r/126833/diff/raw/")
sha1sums=('770fe6427bde0c12408233aaa2a8820e2598ca8b'
          '35f1b3aa533b384a7a8da460209d843b7c49fa30')

prepare() {
  mkdir -p build

# Fix build with opencv 3.1 https://bugs.kde.org/show_bug.cgi?id=358910
  cd $pkgname-$pkgver
  patch -p1 -i ../libkface-opencv3.1.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DENABLE_OPENCV3=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
