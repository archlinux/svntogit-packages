# $Id$
# Maintainer:
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=brltty
pkgver=4.4
pkgrel=1
pkgdesc="Braille display driver for Linux/Unix"
arch=('i686' 'x86_64')
url="http://mielke.cc/brltty"
license=('GPL' 'LGPL')
depends=('libxaw' 'at-spi2-core' 'gpm' 'icu' 'tcl' 'atk' 'pyrex')
makedepends=('bluez')
optdepends=('bluez: bluetooth support')
backup=(etc/brltty.conf etc/conf.d/brltty.conf)
options=('!makeflags' '!emptydirs')
install=brltty.install
source=(http://mielke.cc/$pkgname/releases/$pkgname-$pkgver.tar.gz
        'brltty'
        'brltty.conf'
	     'brltty.service')
md5sums=('8ebe96efe679f5f6ccff08928fec5b26'
         '831ebaf0c56091702929c68805d20c4f'
         'a8ab8b3dd059e96e1734bc9cdcf844fc'
         '7acecd700b9f94d76fe4a6ad56cb0448')
build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --mandir=/usr/share/man \
    --localstatedir=/var \
    --with-screen-driver=a2 \
    --enable-gpm \
    --disable-java-bindings \
    PYTHON=/usr/bin/python2

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make INSTALL_ROOT="$pkgdir" install
  install -D -m755 "$srcdir/brltty" "$pkgdir/etc/rc.d/brltty"
  install -D -m644 "$srcdir/brltty.conf" "$pkgdir/etc/conf.d/brltty.conf"
  install -D -m644 Documents/brltty.conf "$pkgdir/etc/brltty.conf"
  install -D -m644 "$srcdir/brltty.service" "$pkgdir/usr/lib/systemd/system/brltty.service"
}
