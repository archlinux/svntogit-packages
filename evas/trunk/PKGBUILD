# $Id$
# Maintainer: Ronald van Haren <ronald@archlinux.org>
# Contributor: Gustavo Sverzut Barbieri <barbieri@gmail.com>

pkgname=evas
pkgver=1.7.2
pkgrel=1
pkgdesc="A hardware-accelerated canvas API for X-Windows"
arch=('i686' 'x86_64')
url="http://www.enlightenment.org"
license=('BSD')
depends=('eet' 'giflib' 'libpng' 'libjpeg>=7' 'libxrender' 'mesa'
  'fribidi' 'fontconfig' 'freetype2' 'harfbuzz')
optdepends=("evas_generic_loaders")
options=('!libtool' '!emptydirs')
source=(http://download.enlightenment.org/releases/$pkgname-$pkgver.tar.gz)
sha1sums=('47e43fb21dae959959e9e3e2b1e0868e4c92b63f')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  export CFLAGS="-fvisibility=hidden -ffunction-sections -fdata-sections ${CFLAGS}"
  export CXXFLAGS="-fvisibility=hidden -ffunction-sections -fdata-sections ${CXXFLAGS}"
  export LDFLAGS="-fvisibility=hidden -ffunction-sections -fdata-sections -Wl,--gc-sections -Wl,--as-needed  ${LDFLAGS}"

  ./configure \
    --prefix=/usr \
    --libexecdir=/usr/lib/evas \
    --enable-strict \
    --disable-static \
    --enable-fontconfig \
    --enable-fribidi \
    --enable-harfbuzz \
    --disable-pixman \
    --enable-buffer=static \
    --disable-gl-sdl \
    --enable-fb \
    --disable-directfb \
    --disable-wayland-shm \
    --disable-wayland-egl \
    --disable-sdl-primitive \
    --disable-software-xcb \
    --enable-software-xlib=static \
    --disable-gl-xcb \
    --enable-gl-xlib \
    --disable-image-loader-edb \
    --enable-image-loader-gif=static \
    --enable-image-loader-tiff=static \
    --enable-image-loader-svg=static \
    --enable-image-loader-eet=static \
    --enable-image-loader-generic=static \
    --disable-install-examples \
    --disable-tests \
    --disable-coverage \
    --disable-doc
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  # install license files
  install -Dm644 "$srcdir/$pkgname-$pkgver/COPYING" \
        "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
