# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdenetwork-krdc
pkgver=15.04.2
pkgrel=1
pkgdesc='Remote Desktop Client'
url='http://kde.org/applications/internet/krdc/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdenetwork')
depends=('kdebase-runtime' 'telepathy-qt')
makedepends=('cmake' 'automoc4' 'libvncserver' 'freerdp')
optdepends=('libvncserver: VNC support'
            'freerdp: RDP support'
            'kdebase-keditbookmarks: to edit bookmarks')
source=("http://download.kde.org/stable/applications/${pkgver}/src/krdc-${pkgver}.tar.xz")
sha1sums=('cd25dd80ed3c614f036786468d2e4ab3b37bed27')

prepare() {
  mkdir build

  cd krdc-${pkgver}
}

build() {
  cd build
  cmake ../krdc-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
