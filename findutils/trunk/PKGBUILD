# $Id$
# Maintainer: judd <jvinet@zeroflux.org>
pkgname=findutils
pkgver=4.4.1
pkgrel=2
pkgdesc="GNU utilities to locate files"
arch=(i686 x86_64)
license=('GPL3')
groups=('base')
depends=('glibc>=2.8')
url="http://www.gnu.org/software/findutils"
source=(ftp://ftp.gnu.org/pub/gnu/findutils/findutils-$pkgver.tar.gz)
install=findutils.install
md5sums=('5883f569dc021eee765f330bb7a3782d')

build() {
  cd $startdir/src/$pkgname-$pkgver
  ./configure --prefix=/usr
  make || return 1
  # believe it or not, the Makefile really wants to remove /usr/var, so
  # we let it...
  mkdir -p $startdir/pkg/usr/var
  make DESTDIR=$startdir/pkg install
  rm -rf $startdir/pkg/usr/libexec

  # remove locate utils (we use slocate instead)
  rm -f $startdir/pkg/usr/bin/{locate,updatedb}
  rm -f $startdir/pkg/usr/share/man/man1/{updatedb.1,locate.1}
  rm -f $startdir/pkg/usr/share/man/man5/locatedb.5
  rmdir $startdir/pkg/usr/var
  rm -f $pkgdir/usr/share/info/dir
}

