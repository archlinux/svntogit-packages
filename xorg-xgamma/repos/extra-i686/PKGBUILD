# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-xgamma
pkgver=1.0.6
pkgrel=1
pkgdesc="Alter a monitor's gamma correction"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('libx11' 'libxxf86vm')
makedepends=('xorg-util-macros')
groups=('xorg-apps' 'xorg')
source=(http://xorg.freedesktop.org/archive/individual/app/xgamma-${pkgver}.tar.bz2{,.sig})
sha256sums=('0ef1c35b5c18b1b22317f455c8df13c0a471a8efad63c89c98ae3ce8c2b222d3'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # Alan Coopersmith <alan.coopersmith@oracle.com>

build() {
  cd xgamma-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd xgamma-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
