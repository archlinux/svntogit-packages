# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-xgamma
pkgver=1.0.6
pkgrel=2
pkgdesc="Alter a monitor's gamma correction"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libx11' 'libxxf86vm')
makedepends=('xorg-util-macros')
groups=('xorg-apps' 'xorg')
source=(https://xorg.freedesktop.org/archive/individual/app/xgamma-${pkgver}.tar.bz2{,.sig})
sha512sums=('2d41798e15ac8c6f6731a2da29589207d936eaee1223ce6f3ca948e67d63c5c8b955d11c7db092dcd8c66a6209bd15c1de3dc1446dad4b4277134f010c6fc47a'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # Alan Coopersmith <alan.coopersmith@oracle.com>

build() {
  cd xgamma-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd xgamma-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
