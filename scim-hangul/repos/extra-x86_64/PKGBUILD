# $Id$
# Contributor: damir <damir@archlinux.org>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=scim-hangul
pkgver=0.3.2
pkgrel=4
pkgdesc='Korean input method module for SCIM'
arch=('i686' 'x86_64')
url='http://www.scim-im.org/'
license=('GPL')
depends=('libhangul' 'scim')
options=('!libtool')
source=("http://downloads.sourceforge.net/sourceforge/scim/$pkgname-$pkgver.tar.gz"
        'gcc43.patch')
sha1sums=('d1f4a2114d284c926d64fbfe8ea83307b8d8e2ec'
          '956bb2b6bd75b3d2756895f8ade429f95bacc0ff')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	patch -p1 -i ../gcc43.patch
	./bootstrap # skim complains; it's alright
	./configure --prefix=/usr --enable-skim-support
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
