# $Id$
# Contributor: Sarah Hay <sarahhay@mb.sympatico.ca>
# Maintainer: Vesa Kaihlavirta <vesa@archlinux.org>

pkgname=erlang
pkgver=R13B03
pkgrel=1
pkgdesc="A small concurrent functional programming language developed by Ericsson."
arch=(i686 x86_64)
url="http://www.erlang.org/index.html"
license=('custom')
depends=('ncurses' 'glibc')
makedepends=('java-environment' 'perl' 'openssl' 'wxgtk')
optdepends=('wxgtk: for wx support')
options=('!makeflags')
source=(http://www.erlang.org/download/otp_src_${pkgver/./-}.tar.gz
	http://www.erlang.org/download/otp_doc_man_${pkgver/./-}.tar.gz)

build() {
  cd "$srcdir/otp_src_${pkgver/./-}"
  ERTSVERSION=$(sed -n 's/^VSN[ ]*=[ ]*\([0-9.]\)/\1/p' < erts/vsn.mk)
  ERLINTERFACEVERSION=$(sed -n 's/^EI_VSN[ ]*=[ ]*\([0-9.]\)/\1/p' < lib/erl_interface/vsn.mk)

  sed -i '/SSL_DYNAMIC_ONLY=/s:no:yes:' erts/configure || return 1

  CFLAGS="${CFLAGS} -fno-strict-aliasing" ./configure --prefix=/usr --enable-smp-support --enable-threads || return 1
  make || return 1
  make INSTALL_PREFIX="$pkgdir" install || return 1

# fix prefix
  cd "$pkgdir"
  for i in usr/lib/erlang/bin/erl \
      usr/lib/erlang/bin/start \
      usr/lib/erlang/erts-${ERTSVERSION}/bin/erl \
      usr/lib/erlang/erts-${ERTSVERSION}/bin/start \
      usr/lib/erlang/releases/RELEASES ;do 
    sed -i "s#$pkgdir##" $i || return 1
  done

# fix symlinks
  cd "$pkgdir/usr/lib/erlang/bin"
  ln -sf ../erts-${ERTSVERSION}/bin/epmd 

  cd "$pkgdir/usr/bin"
  for file in *; do
    ln -sfv ../lib/erlang/bin/$file || return 1
  done

  ln -s ../lib/erlang/lib/erl_interface-${ERLINTERFACEVERSION}/bin/erl_call "$pkgdir/usr/bin/erl_call" || return 1

# install documentation 
  install -d "$pkgdir/usr/share/doc/erlang"
  install -D -m644 "$srcdir"/{COPYRIGHT,PR.template,README} "$pkgdir/usr/share/doc/erlang"
  cp -r "$srcdir/man" "$pkgdir/usr/share"

  install -Dm644 "$srcdir/otp_src_${pkgver/./-}/EPLICENCE" "$pkgdir/usr/share/licenses/$pkgname/EPLICENCE"

# fixed man pages conflict
  for i in array crypto inet io queue random rpc ssl string zlib; do
    mv "$pkgdir/usr/share/man/man3/$i.3" "$pkgdir/usr/share/man/man3/erlang_$i.3" 
  done
}
md5sums=('411fcb29f0819973f71e28f6b56d9948'
         '1fe80b110061ef73614824fb06d4d6eb')
