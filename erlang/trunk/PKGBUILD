# $Id$
# Contributor: Sarah Hay <sarahhay@mb.sympatico.ca>
# Maintainer: Vesa Kaihlavirta <vesa@archlinux.org>

pkgname=erlang
pkgver=R13B04
pkgrel=3
pkgdesc="A small concurrent functional programming language developed by Ericsson."
arch=(i686 x86_64)
url="http://www.erlang.org/index.html"
license=('custom')
depends=('ncurses' 'glibc')
makedepends=('java-environment' 'perl' 'openssl' 'wxgtk' 'unixodbc')
optdepends=('wxgtk: for wx support' 'unixodbc: database')

options=('!makeflags')
source=(http://www.erlang.org/download/otp_src_${pkgver/./-}.tar.gz
	http://www.erlang.org/download/otp_doc_man_${pkgver/./-}.tar.gz)

build() {
  cd "$srcdir/otp_src_${pkgver/./-}"
  ERTSVERSION=$(sed -n 's/^VSN[ ]*=[ ]*\([0-9.]\)/\1/p' < erts/vsn.mk)
  ERLINTERFACEVERSION=$(sed -n 's/^EI_VSN[ ]*=[ ]*\([0-9.]\)/\1/p' < lib/erl_interface/vsn.mk)

  # fix for #17001
  sed -i 's|WX_LIBS=`$WX_CONFIG_WITH_ARGS --libs`|WX_LIBS="`$WX_CONFIG_WITH_ARGS --libs` -lGLU"|' lib/wx/configure || return 1

  sed -i '/SSL_DYNAMIC_ONLY=/s:no:yes:' erts/configure || return 1

  CFLAGS="${CFLAGS} -fno-strict-aliasing" ./configure --prefix=/usr --enable-smp-support --enable-threads || return 1
  make || return 1
  make INSTALL_PREFIX="$pkgdir" install || return 1

# fix prefix
  cd "$pkgdir"
  for i in usr/lib/erlang/bin/erl \
      usr/lib/erlang/bin/start \
      usr/lib/erlang/erts-${ERTSVERSION}/bin/erl \
      usr/lib/erlang/erts-${ERTSVERSION}/bin/start \
      usr/lib/erlang/releases/RELEASES ;do 
    sed -i "s#$pkgdir##" $i || return 1
  done

# fix symlinks
  cd "$pkgdir/usr/lib/erlang/bin"
  ln -sf ../erts-${ERTSVERSION}/bin/epmd 

  cd "$pkgdir/usr/bin"
  for file in *; do
    ln -sfv ../lib/erlang/bin/$file || return 1
  done

  ln -s ../lib/erlang/lib/erl_interface-${ERLINTERFACEVERSION}/bin/erl_call "$pkgdir/usr/bin/erl_call" || return 1

# install documentation 
  install -d "$pkgdir/usr/share/doc/erlang"
  install -D -m644 "$srcdir"/{COPYRIGHT,PR.template,README} "$pkgdir/usr/share/doc/erlang"
  cp -r "$srcdir/man" "$pkgdir/usr/lib/erlang"

  install -Dm644 "$srcdir/otp_src_${pkgver/./-}/EPLICENCE" "$pkgdir/usr/share/licenses/$pkgname/EPLICENCE"

}
md5sums=('ca6da4921e438891967900aa6a084341'
         '681aaef70affc64743f4e8c0675034af')
