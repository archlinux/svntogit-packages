# $Id$
# Maintainer : Thomas Baechler <thomas@archlinux.org>

pkgname=nvidia-304xx
pkgver=304.117
_extramodules=extramodules-3.13-ARCH
pkgrel=5
pkgdesc="NVIDIA drivers for linux, 304xx legacy branch"
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=('linux>=3.13' 'linux<3.14' "nvidia-304xx-utils=${pkgver}")
makedepends=('linux-headers>=3.13' 'linux-headers<3.14')
conflicts=('nvidia' 'nvidia-96xx' 'nvidia-173xx')
license=('custom')
install=nvidia.install
options=(!strip)
source=('nvidia_3.13_kernel.patch')
md5sums=('f1569a1ce7f9293b876c2b72f3fd7c9e')

if [ "$CARCH" = "i686" ]; then
    _arch='x86'
    _pkg="NVIDIA-Linux-${_arch}-${pkgver}"
    source+=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
    md5sums+=('023eb03da304a1842fc503c81e3c5694')
elif [ "$CARCH" = "x86_64" ]; then
    _arch='x86_64'
   _pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
    source+=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
    md5sums+=('8ee9f672028971c6d36118894d0e2c59')
fi


prepare() {
    cd "${srcdir}"
    sh "${_pkg}.run" --extract-only
    cd "${_pkg}"
    patch -p1 -i "${srcdir}"/nvidia_3.13_kernel.patch
}

build() {
    _kernver="$(cat /usr/lib/modules/${_extramodules}/version)"
    cd "${_pkg}/kernel"
    make SYSSRC=/usr/lib/modules/"${_kernver}/build" module
}

package() {
    install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" \
        "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
    install -d -m755 "${pkgdir}/usr/lib/modprobe.d"
    echo "blacklist nouveau" >> "${pkgdir}/usr/lib/modprobe.d/nvidia.conf"
    sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia.install"
    gzip "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"

    # the license file is part of nvidia-304xx-utils - the module depends on it, so we don't ship it another time.
}
