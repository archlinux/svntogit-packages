# Maintainer: SÃ©bastien "Seblu" Luttringer <seblu@archlinux.org>
# Contributor: Alessandro Sagratini <ale_sagra@hotmail.com>

pkgname=nfacct
pkgver=1.0.2
pkgrel=3
pkgdesc='Command line tool to create/retrieve/delete accounting objects'
arch=('x86_64')
url='https://www.netfilter.org/projects/nfacct/'
license=('GPL2')
depends=('libnetfilter_acct')
validpgpkeys=('C09DB2063F1D7034BA6152ADAB4655A126D292E4') # Netfilter Core Team
source=("https://www.netfilter.org/projects/$pkgname/files/$pkgname-$pkgver.tar.bz2"{,.sig})
sha256sums=('ecff2218754be318bce3c3a5d1775bab93bf4168b2c4aac465785de5655fbd69'
            'SKIP')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sbindir=/usr/bin
  make
}

check() {
  cd $pkgname-$pkgver
  make -k check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
