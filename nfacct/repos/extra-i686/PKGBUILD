# $Id$
# Maintainer: SÃ©bastien Luttringer
# Contributor: Alessandro Sagratini <ale_sagra@hotmail.com>

pkgname=nfacct
pkgver=1.0.1
pkgrel=2
pkgdesc='Command line tool to create/retrieve/delete accounting objects'
arch=('i686' 'x86_64')
url='http://www.netfilter.org/projects/nfacct/'
license=('GPL2')
depends=('libnetfilter_acct')
source=("http://www.netfilter.org/projects/$pkgname/files/$pkgname-$pkgver.tar.bz2")
md5sums=('992e863409d144350dbc8f0554a0f478')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sbindir=/usr/bin
  make
}

check() {
  cd $pkgname-$pkgver
  make -k check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
