# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: David Keogh <davekeogh@shaw.ca>

pkgname=squeeze
pkgver=0.2.3
pkgrel=3
pkgdesc="Squeeze is a modern and advanced archive manager for the Xfce Desktop Environment."
arch=('i686' 'x86_64')
license=('GPL2')
url="http://squeeze.xfce.org/"
groups=('xfce4-goodies')
depends=('thunar' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('pkgconfig' 'tar' 'desktop-file-utils')
optdepends=('tar: TAR support'
            'gzip: GZIP support'
            'bzip2: BZIP2 support'
            'zip: ZIP support'
            'unzip: ZIP support'
            'unrar: RAR support'
            'p7zip: 7z support'
            'arj: ARJ support'
            'lha: LHA support'
            'lzma-utils: LZMA support'
            'lzop: LZOP support')
options=('!libtool')
conflicts=('squeeze-svn')
install=${pkgname}.install
source=(http://squeeze.xfce.org/downloads/${pkgname}-${pkgver}.tar.bz2)
md5sums=('bd3cb0648b49be1f146fc4f675606176')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib/xfce4 \
    --localstatedir=/var --disable-static || return 1
  make || return 1
}
package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install || return 1
}
