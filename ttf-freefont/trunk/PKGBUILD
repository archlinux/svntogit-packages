# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=ttf-freefont
pkgver=20120503
pkgrel=4
pkgdesc='Set of free outline fonts covering the Unicode character set'
url='https://www.nongnu.org/freefont/'
license=('GPL')
arch=('any')
depends=('fontconfig' 'xorg-font-utils')
provides=('ttf-font')
validpgpkeys=('A0156C139D2DAA3B352E42CD506361DBA36FDD52')
source=("https://ftp.gnu.org/gnu/freefont/freefont-ttf-${pkgver}.zip"{,.sig}
        'conf.avail')
sha1sums=('46179a6f3469b386f6569fc61913314ba79aadac' 'SKIP'
          '11bbd7e1be9ce310917642cf7ee589d4433383e0')

install=install

package() {
	cd "${srcdir}/freefont-${pkgver}"
	install -d "${pkgdir}"/usr/share/fonts/TTF
	install -m644 *.ttf "${pkgdir}"/usr/share/fonts/TTF
	install -Dm644 ../conf.avail "${pkgdir}"/etc/fonts/conf.avail/69-freefont.conf
}
