# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=flashplugin
_licensefile='PlatformClients_PC_WWEULA_Combined_20100108_1657.pdf'
pkgver=11.2.202.238
pkgrel=1
pkgdesc='Adobe Flash Player'
url='http://get.adobe.com/flashplayer'
arch=('i686' 'x86_64')
depends=('mozilla-common' 'libxt' 'libxpm' 'gtk2' 'nss' 'curl' 'hicolor-icon-theme')
optdepends=('libvdpau: GPU acceleration on Nvidia card')
provides=('flashplayer')
license=('custom')
options=(!strip)
install=flashplugin.install
backup=(etc/adobe/mms.cfg)

if [ "$CARCH" = "i686" ]; then
    source=("http://fpdownload.macromedia.com/get/flashplayer/pdc/$pkgver/install_flash_player_11_linux.i386.tar.gz"
            "http://www.adobe.com/products/eulas/pdfs/${_licensefile}"
            mms.cfg)
    md5sums=('0bb68113fc1ccdfa3fbb212bc9a13d1f'
             '94ca2aecb409abfe36494d1a7ec7591d'
             'f34aae6279b40e0bd2abfb0d9963d7b8')
elif [ "$CARCH" = "x86_64" ]; then
    source=("http://fpdownload.macromedia.com/get/flashplayer/pdc/$pkgver/install_flash_player_11_linux.x86_64.tar.gz"
            "http://www.adobe.com/products/eulas/pdfs/${_licensefile}"
            mms.cfg)
    md5sums=('1fc642ea19f5b851b434ac2b2e685257'
             '94ca2aecb409abfe36494d1a7ec7591d'
             'f34aae6279b40e0bd2abfb0d9963d7b8')
fi

package () {
    install -Dm755 libflashplayer.so "$pkgdir/usr/lib/mozilla/plugins/libflashplayer.so"
    if [ "$CARCH" = x86_64 ]; then
        install -Dm755 usr/lib64/kde4/kcm_adobe_flash_player.so "$pkgdir/usr/lib/kde4/kcm_adobe_flash_player.so"
    else
        install -Dm755 usr/lib/kde4/kcm_adobe_flash_player.so "$pkgdir/usr/lib/kde4/kcm_adobe_flash_player.so"
    fi
    install -Dm755 usr/bin/flash-player-properties "$pkgdir/usr/bin/flash-player-properties"
    for i in 16x16 22x22 24x24 32x32 48x48; do
        install -Dm644 usr/share/icons/hicolor/$i/apps/flash-player-properties.png \
            "$pkgdir/usr/share/icons/hicolor/$i/apps/flash-player-properties.png"
    done
    install -Dm644 usr/share/applications/flash-player-properties.desktop "$pkgdir/usr/share/applications/flash-player-properties.desktop"
    install -Dm644 usr/share/kde4/services/kcm_adobe_flash_player.desktop "$pkgdir/usr/share/kde4/services/kcm_adobe_flash_player.desktop"
    install -Dm644 "${_licensefile}" "$pkgdir/usr/share/licenses/$pkgname/LICENSE.pdf"
    install -Dm644 "$srcdir/mms.cfg" "$pkgdir/etc/adobe/mms.cfg"
}
