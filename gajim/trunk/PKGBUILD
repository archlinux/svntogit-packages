# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=gajim
pkgver=0.16.4
pkgrel=1
pkgdesc="A full featured and easy to use Jabber client"
arch=('any')
url="http://www.gajim.org/"
license=('GPL3')
depends=('pygtk' 'python2-pyopenssl' 'python2-pyasn1' 'python2-nbxmpp' 
         'ca-certificates' 'ldns')
makedepends=('intltool')
optdepends=('python2-dbus: dbus support'
            'farstream-0.1: for video/voice support'
            'gstreamer0.10-bad: for video/voice support'
	    'gstreamer0.10-python: for video/voice support'
            'gtkspell: for spelling support'
            'libxss: for idle module'
            'notification-daemon: for desktop notification'
            'python2-gnomekeyring: for GnomeKeyring support'
            'python2-crypto: support for E2E encryption'
            'python2-docutils: for RST generator support'
            'gupnp-igd: for UPnP-IGD support')
install=gajim.install
source=(http://www.gajim.org/downloads/${pkgver%.*}/gajim-${pkgver}.tar.bz2 gajim-drill.patch)
sha1sums=('a99bd5d75cf37c59f80651a77eec9566872d537b'
          'a8b6586613a4b9a2f7f44471567b7d427cfb19b2')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i "${srcdir}/gajim-drill.patch"
}

build() {
  cd ${pkgname}-${pkgver}
  PYTHON=python2 ./configure --prefix=/usr --enable-site-packages
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
