# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>

pkgname=geeqie
pkgver=1.2.2
pkgrel=2
pkgdesc="A lightweight image browser and viewer"
arch=('i686' 'x86_64')
url="http://www.geeqie.org/"
license=('GPL2')
depends=('exiv2' 'gtk2' 'lcms' 'desktop-file-utils')
makedepends=('intltool' 'doxygen' 'gnome-doc-utils' 'git')
optdepends=('librsvg: SVG rendering'
	    'fbida: for jpeg rotation'
	    'exiv2: for tiff and png rotation')
install=geeqie.install
source=(http://www.geeqie.org/${pkgname}-${pkgver}.tar.xz
        fix-fullscreen.patch)
sha1sums=('9dd094707d7bf7389eb62a218ff7bc86339fc995'
          'dab15bbda9b329f663276b25bf1989b410596644')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i ../fix-fullscreen.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package(){
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
