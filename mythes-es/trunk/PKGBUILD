# $Id$
# Maintainer: Angel Velasquez <angvp@archlinux.org> 
# Contributor: AndyRTR <andyrtr@archlinux.org>

pkgname=mythes-es
pkgver=0.7
pkgrel=2
epoch=1
pkgdesc="Spanish thesaurus"
arch=(any)
url="https://forja.rediris.es/projects/rla-es/"
license=('LGPL3')
optdepends=('libmythes: offers thesaurus library functions')
noextract=('es_ANY.oxt')
source=('http://forja.rediris.es/frs/download.php/2933/es_ANY.oxt')
md5sums=('e3d4b38f280e7376178529db2ece982b')

package() {
  cd "$srcdir"
  LANG=en_US.utf8 bsdtar -xf es_ANY.oxt
  install -dm755 "${pkgdir}/usr/share/mythes"
  install -m644 th_es_ES_v2.* "$pkgdir/usr/share/mythes"

  pushd "$pkgdir/usr/share/mythes/"
  es_ES_aliases="es_AR es_BO es_CL es_CO es_CR es_CU es_DO es_EC es_GT es_HN es_MX es_NI es_PA es_PE es_PR es_SV es_UY es_VE"
  for lang in $es_ES_aliases; do
        ln -s th_es_ES_v2.idx "th_"$lang"_v2.idx"
        ln -s th_es_ES_v2.dat "th_"$lang"_v2.dat"
  done
  popd

  # the symlinks
  install -dm755 "${pkgdir}/usr/share/myspell/dicts"
  pushd "$pkgdir/usr/share/myspell/dicts"
  for file in "$pkgdir"/usr/share/mythes/*; do
    ln -sv /usr/share/mythes/$(basename $file) .
  done
  popd
  
  # docs
  install -Dm644 README_th_es_ES.txt "${pkgdir}/usr/share/doc/$pkgname/README_th_es_ES.txt"
}
