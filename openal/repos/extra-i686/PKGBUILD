# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Jason Chu <jchu@xentac.net>

pkgname=openal
pkgver=1.16.0
pkgrel=1
pkgdesc="A cross-platform 3D audio library"
arch=(i686 x86_64)
url="http://www.openal.org/"
license=(LGPL)
depends=(glibc)
makedepends=(alsa-lib pkgconfig cmake libpulse qt4 fluidsynth portaudio)
optdepends=('qt4: alsoft-config GUI Configurator'
            'fluidsynth: MIDI rendering')
source=(http://kcat.strangesoft.net/openal-releases/openal-soft-$pkgver.tar.bz2)
md5sums=('14db4b0d12f07252070b4fe5eb5911f6')

build() {
  cd $pkgname-soft-$pkgver/build
  cmake -D CMAKE_INSTALL_PREFIX=/usr -D CMAKE_BUILD_TYPE=Release ..
  make
}

package() {
  cd $pkgname-soft-$pkgver
  make -C build DESTDIR="$pkgdir/" install
  install -m644 -t "$pkgdir/usr/share/openal" env-vars.txt hrtf.txt
}
