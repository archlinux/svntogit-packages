# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Jason Chu <jchu@xentac.net>

pkgbase=openal
pkgname=(openal openal-examples)
pkgver=1.17.0
pkgrel=1
pkgdesc="A cross-platform 3D audio library"
arch=(i686 x86_64)
url="http://www.openal.org/"
license=(LGPL)
depends=(glibc)
makedepends=(alsa-lib pkgconfig cmake libpulse qt4 fluidsynth portaudio jack sdl2 sdl_sound ffmpeg)
source=(http://kcat.strangesoft.net/openal-releases/openal-soft-$pkgver.tar.bz2)
md5sums=('0b7d1e79fd57e6c9827a3bbdc97f19b6')

prepare() {
  mkdir build examples
}

build() {
  cd build
  cmake -D CMAKE_INSTALL_PREFIX=/usr -D CMAKE_BUILD_TYPE=Release ../$pkgbase-soft-$pkgver
  make
}

package_openal() {
  optdepends=('qt4: alsoft-config GUI Configurator'
              'fluidsynth: MIDI rendering')

  make -C build DESTDIR="$pkgdir/" install
  install -m644 $pkgbase-soft-$pkgver/{env-vars,hrtf}.txt "$pkgdir/usr/share/openal/"

### Split openal-examples
  mv "$pkgdir"/usr/bin/al{ffplay,latency,loopback,reverb,stream} examples/
}

package_openal-examples() {
  pkgdesc+=" (example programs)"
  depends=("openal=$pkgver-$pkgrel" sdl2 sdl_sound ffmpeg)

  mkdir -p "$pkgdir/usr/bin"
  mv examples/* "$pkgdir/usr/bin/"
}
