# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=libkleo
pkgver=22.03.90
pkgrel=1
pkgdesc='KDE PIM cryptographic library'
arch=(x86_64)
url='https://kontact.kde.org'
license=(LGPL)
depends=(qgpgme kitemmodels kpimtextedit)
makedepends=(extra-cmake-modules qt5-tools boost doxygen)
source=(https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('4ef84044faa802e3d0a9d3b66d18fcaf3d9d77db6e7f4cf7b9044a016af025a7'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
options=(debug)

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
