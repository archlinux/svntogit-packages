# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=libkleo
pkgver=20.12.0
pkgrel=1
pkgdesc="KDE PIM cryptographic library"
arch=(x86_64)
url="https://kontact.kde.org"
license=(LGPL)
depends=(qgpgme kitemmodels kpimtextedit)
makedepends=(extra-cmake-modules boost)
source=("https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('9da666931763520582047cf472dac436b6efac076f0245befc0f884658c6f91e'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
