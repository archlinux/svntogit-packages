# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdepim-runtime
pkgver=4.6.0
pkgrel=2
pkgdesc='KDE PIM Runtime Environment'
arch=('i686' 'x86_64')
url='http://pim.kde.org'
license=('GPL' 'LGPL' 'FDL')
depends=('kdepimlibs' 'kdebase-runtime')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'boost')
install=${pkgname}.install
source=("http://download.kde.org/stable/kdepim-${pkgver}/src/${pkgname}-${pkgver}.tar.bz2"
        'disable-l10n.patch')
sha1sums=('2a2e29ba805ae2c94fa4da6371693e59575d3ed8'
          'd4f8c39911cf288973a30d4b71087906ebb26359')

build() {
	cd "${srcdir}"/${pkgname}-${pkgver}
    patch -p1 -i "${srcdir}"/disable-l10n.patch

    cd "${srcdir}"
	mkdir build
	cd build
	cmake ../${pkgname}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package() {
	cd "${srcdir}"/build
	make DESTDIR="${pkgdir}" install
}
