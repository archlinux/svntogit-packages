# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=kdepim-runtime
pkgver=15.08.1
pkgrel=4
pkgdesc='Extends the functionality of kdepim'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kdepim-runtime'
license=('GPL' 'LGPL' 'FDL')
depends=(hicolor-icon-theme qt5-quick1 libkgapi libkolab akonadi-socialutils akonadi-calendar
         knotifyconfig kross kalarmcal kmbox kimap kde-syndication)
makedepends=(extra-cmake-modules kdoctools boost)
install=$pkgname.install
source=("http://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz" kdepim-runtime-bug338658.patch
	ical-resource.patch::"https://quickgit.kde.org/?p=kdepim-runtime.git&a=commitdiff&h=1612e91&o=plain"
	outlook365.patch::"https://quickgit.kde.org/?p=kdepim-runtime.git&a=commitdiff&h=813e4df&o=plain")
sha1sums=('fcab275175a82152ee2345a8657e0ab238f7033a'
          '4d0bcdb3887f1146d289471d7e2494e2a76bb8bd'
          '5c6f5128ca2a56124e4c0b09a911c7979d682466'
          '999d17c24e7cebef2900afce68d6fea774348bb7')

prepare() {
  mkdir -p build

# Fix https://bugs.kde.org/show_bug.cgi?id=338658
  cd $pkgname-$pkgver
  patch -p1 -i ../kdepim-runtime-bug338658.patch
# Fix saving ical resources https://bugs.kde.org/show_bug.cgi?id=352693
  patch -p1 -i ../ical-resource.patch
# Fix syncing Outlook365 accounts https://bugs.kde.org/show_bug.cgi?id=351814
  patch -p1 -i ../outlook365.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
