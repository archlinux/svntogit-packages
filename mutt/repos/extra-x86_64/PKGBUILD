# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: tobias [tobias [at] archlinux.org]

pkgname=mutt
pkgver=2.2.3
pkgrel=1
pkgdesc="Small but very powerful text-based mail client"
arch=(x86_64)
url="http://www.mutt.org/"
license=(GPL2)
depends=(glibc libgpg-error openssl libsasl mime-types sqlite zlib)
makedepends=(docbook-xml docbook-xsl elinks gdbm git gpgme krb5 libidn2 libxslt
lynx ncurses)
optdepends=(
  'perl: for smime_keys'
  'python: for experimental mutt_oath2.py'
  'smtp-forwarder: to send mail'
)
backup=(etc/Muttrc)
options=(debug)
source=(git+https://gitlab.com/muttmua/$pkgname.git#tag=$pkgname-${pkgver//./-}-rel?signed)
sha512sums=('SKIP')
b2sums=('SKIP')
validpgpkeys=('8975A9B33AA37910385C5308ADEF768480316BDA') # Kevin J. McCarthy <kevin@8t8.us>

prepare() {
  cd $pkgname
  autoreconf -fiv
}

build() {
  cd $pkgname
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --enable-debug \
              --enable-gpgme \
              --enable-pop \
              --enable-imap \
              --enable-smtp \
              --enable-hcache \
              --enable-sidebar \
              --enable-autocrypt \
              --with-curses=/usr \
              --with-gss=/usr \
              --with-ssl=/usr \
              --with-sqlite3 \
              --with-sasl \
              --with-idn2
  make
}

package() {
  depends+=(libgdbm.so libgpgme.so libgssapi_krb5.so libidn2.so libncursesw.so)

  cd $pkgname
  make DESTDIR="$pkgdir" install
  install -vDm 644 contrib/gpg.rc "$pkgdir/etc/Muttrc.gpg.dist"

  # /etc/mime.types is provided by mailcap
  rm "$pkgdir/etc/mime.types"{,.dist}
}
