# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kf5umbrella
pkgver=r5.e457a0a
pkgrel=1
pkgdesc='KF5 Umbrella'
arch=('any')
url='https://projects.kde.org/projects/frameworks/kf5umbrella'
license=('LGPL')
depends=('qt5-base')
makedepends=('extra-cmake-modules' 'git')
groups=('kf5')
source=('git://anongit.kde.org/kf5umbrella.git')
md5sums=('SKIP')

pkgver() {
  cd kf5umbrella
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kf5umbrella \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
