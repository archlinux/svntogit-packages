# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=cyrus-sasl-plugins
pkgver=2.1.23
pkgrel=5
pkgdesc="Cyrus Simple Authentication Service Layer (SASL) library"
arch=('i686' 'x86_64')
url="http://cyrusimap.web.cmu.edu/"
license=('custom')
depends=('postgresql-libs>=9.0.3' 'krb5' 'libldap>2.4'
        'libmysqlclient>=5.5.10')
source=(ftp://ftp.andrew.cmu.edu/pub/cyrus-mail/cyrus-sasl-${pkgver}.tar.gz
        cyrus-sasl-2.1.22-gcc44.patch
    	cyrus-sasl-2.1.23-db5-fix.patch)
md5sums=('2eb0e48106f0e9cd8001e654f267ecbc'
         '5deb4d67b53ecba20c7887fc8fdebee1'
         '3ae4347705141145f31cf786c38ea9ef')
options=('!libtool')

build() {
  cd ${srcdir}/cyrus-sasl-${pkgver}
  patch -Np1 -i $srcdir/cyrus-sasl-2.1.22-gcc44.patch
  # from http://bugs.gentoo.org/show_bug.cgi?id=319935
  patch -Np0 -i ${srcdir}/cyrus-sasl-2.1.23-db5-fix.patch
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-login \
    --disable-plain \
    --enable-sql \
    --disable-sqlite \
    --enable-gssapi=/usr/include/gssapi \
    --with-mysql=/usr \
    --with-pgsql=/usr \
    --enable-postgresql \
    --enable-ldapdb \
    --with-ldap=/usr
  cd sasldb
  make
  cd ../plugins
  make
}

package () {
  cd ${srcdir}/cyrus-sasl-${pkgver}/plugins
  make DESTDIR=${pkgdir} install
  
  install -Dm644 ../COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING

  rm -f ${pkgdir}/usr/lib/sasl2/libsasldb.*
}
