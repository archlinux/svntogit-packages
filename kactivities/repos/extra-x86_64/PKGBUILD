# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kactivities
pkgver=5.41.0
pkgrel=1
arch=('x86_64')
pkgdesc="Core components for the KDE's Activities"
url='https://community.kde.org/Frameworks'
license=('LGPL')
depends=('kcoreaddons' 'kconfig' 'kwindowsystem' 'qt5-declarative')
makedepends=('extra-cmake-modules' 'boost' 'python' 'doxygen' 'qt5-tools')
groups=('kf5')
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('8cf600304dbccea07a251f458b97e40745fcf57fa00c9fda357322827facbcff'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DQML_INSTALL_DIR=lib/qt/qml \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
