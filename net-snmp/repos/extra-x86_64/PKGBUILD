# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Dale Blount <dale@archlinux.org>

pkgname=net-snmp
pkgver=5.5
pkgrel=5
pkgdesc="A suite of applications used to implement SNMP v1, SNMP v2c and SNMP v3 using both IPv4 and IPv6"
arch=('i686' 'x86_64')
url="http://www.net-snmp.org/"
license=('custom')
depends=('openssl' 'tcp_wrappers' 'libnl')
makedepends=('setuptools')
optdepends=('perl-term-readkey: for snmpcheck application' 'perl-tk: for snmpcheck application')
provides=('ucd-snmp')
options=('!libtool' '!makeflags' '!emptydirs')
source=("http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.gz"
	'snmpd.rc')
md5sums=('5b2551e7bd024fbbee84dca22a5f13a1'
         '9f0d13676ba1fae1eb7eb178edd85b43')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  for _manpages in snmp_api_errstring.3 snmp_close.3 snmp_error.3 snmp_free_pdu.3 \
      snmp_open.3 snmp_perror.3 snmp_read.3 snmp_select_info.3 snmp_send.3 \
      snmp_sess_perror.3 snmp_timeout.3 ; do
    sed -i 's/snmp_api/session_api/' man/${_manpages} || return 1
  done
  sed -i 's/snmp_api/pdu_api /' man/snmp_free_pdu.3 || return 1
  sed -i -e "s:\(install --basedir=\$\$dir\):\1 --root='${pkgdir}':" Makefile.in || return 1
   ./configure --prefix=/usr --sysconfdir=/etc --mandir=/usr/share/man \
    --enable-ucd-snmp-compatibility --with-libwrap --with-python-modules \
    --with-default-snmp-version="3" --with-sys-contact="root@localhost" \
    --with-sys-location="Unknown" --with-logfile="/var/log/snmpd.log" \
    --with-mib-modules="host misc/ipfwacc ucd-snmp/diskio tunnel ucd-snmp/dlmod" \
    --with-persistent-directory="/var/net-snmp" || return 1
  make NETSNMP_DONT_CHECK_VERSION=1 LDFLAGS+="-Wl,-rpath -Wl,/usr/lib/perl5/core_perl/CORE" || return 1
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" INSTALL_PREFIX="${pkgdir}" INSTALLDIRS=vendor install || return 1
  install -D -m755 "${srcdir}/snmpd.rc" "${pkgdir}/etc/rc.d/snmpd" || return 1
  install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING" || return 1
}
