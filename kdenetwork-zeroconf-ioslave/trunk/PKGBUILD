# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdenetwork-zeroconf-ioslave
pkgver=15.08.3
pkgrel=1
pkgdesc='Network Monitor for DNS-SD services (Zeroconf)'
url='https://projects.kde.org/projects/kde/kdenetwork/zeroconf-ioslave'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdenetwork')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
conflicts=('kdenetwork-kdnssd')
replaces=('kdenetwork-kdnssd')
source=("http://download.kde.org/stable/applications/${pkgver}/src/zeroconf-ioslave-${pkgver}.tar.xz")
sha1sums=('842375521ee9bca113a15da61f058e63dd42911e')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../zeroconf-ioslave-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
