# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdenetwork-zeroconf-ioslave
pkgver=4.13.2
pkgrel=1
pkgdesc='Network Monitor for DNS-SD services (Zeroconf)'
url='https://projects.kde.org/projects/kde/kdenetwork/zeroconf-ioslave'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdenetwork')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
conflicts=('kdenetwork-kdnssd')
replaces=('kdenetwork-kdnssd')
source=("http://download.kde.org/stable/${pkgver}/src/zeroconf-ioslave-${pkgver}.tar.xz")
sha1sums=('1e18b93775a5dd34c38f8f00cdb55328e247795e')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../zeroconf-ioslave-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
