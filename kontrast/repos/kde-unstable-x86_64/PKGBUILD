# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kontrast
pkgver=22.11.90
pkgrel=1
pkgdesc='Tool to check contrast for colors that allows verifying that your colors are correctly accessible'
arch=(x86_64)
url='https://apps.kde.org/kontrast/'
license=(GPL)
depends=(kirigami2 ki18n kcoreaddons qt5-svg hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools)
groups=(kde-applications kde-accessibility)
source=(https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('b250f974b940519ce7cc1c556d0a2ed8b9b90a073ef397e9722be87762716cdb'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
options=(debug)

build() {
  cmake -B build -S $pkgname-$pkgver \
   -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}

