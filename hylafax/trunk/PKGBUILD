# $Id$
# Maintainer: Paul Mattal <paul@archlinux.org>
pkgname=hylafax
pkgver=6.0.5
pkgrel=6
pkgdesc="Fax Server"
arch=('i686' 'x86_64')
install='hylafax.install'
license=('custom')
depends=('libtiff' 'pam' 'ghostscript')
makedepends=('smtp-server')
url="http://www.hylafax.org/"
backup=(var/spool/hylafax/bin/{faxrcvd,notify}
	usr/lib/fax/{faxcover.ps,hfaxd.conf,pagesizes,typerules}
	var/spool/hylafax/etc/hosts.hfaxd) 
source=(ftp://ftp.hylafax.org/source/${pkgname}-${pkgver}.tar.gz 
        hylafax 
        hylafax.cron.daily 
        config.local
        configure-6.0.5.patch 
        hylafax-libtiff4.patch
        faxq.service
        hfaxd.service)
md5sums=('eb9ac942354ad708e20e4583cec6615f'
         '6602288a405324d8c8e3c5eac2bf19fd'
         '52beffe7dc296b4f9ce9fd0387f7804e'
         '60f2a7e62c256ead4e9238cb31b35b90'
         '51d9f639bb76e5e39cdc8e2ac07e208d'
         '3d239c186f24720e945508d349b069b1'
         '832c8e8098e2dfa56040c975b554590c'
         '38a00db6d9d765d130b2d8d3a98e5c24')

build() {
  cd "$srcdir/${pkgname}-${pkgver}"
  cp ../config.local .
  # fix compiling
  patch -Np0 -i ../configure-6.0.5.patch
  patch -Np1 -i ../hylafax-libtiff4.patch
  ./configure --nointeractive --with-OPTIMIZER="${CFLAGS}"
  make
}

package () {
  cd "$srcdir/${pkgname}-${pkgver}"
  mkdir -p "$pkgdir"/usr/{bin,lib/fax,sbin,share/ghostscript/fonts}
  mkdir -p "$pkgdir"/var/{spool/hylafax,lock}
  make ROOT="$pkgdir" install
  install -D -m 755 "$srcdir"/hylafax "$pkgdir"/etc/rc.d/hylafax
  install -D -m 744 "$srcdir"/hylafax.cron.daily "$pkgdir"/etc/cron.daily/hylafax
  # add missing awk file for notify-4.1 script
  install -D -m 755 "$srcdir"/${pkgname}-${pkgver}/util/notify.awk \
  	"$pkgdir"/var/spool/hylafax/bin/notify.awk
  # remove /var/lock
  rm -r "$pkgdir"/var/lock
  # add systemd files
  install -D -m644 $srcdir/hfaxd.service $pkgdir/usr/lib/systemd/system/hfaxd.service
  install -D -m644 $srcdir/faxq.service $pkgdir/usr/lib/systemd/system/faxq.service

  install -D -m644 COPYRIGHT "$pkgdir"/usr/share/licenses/$pkgname/COPYRIGHT
}
