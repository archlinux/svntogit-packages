# $Id$
# Maintainer: Paul Mattal <paul@archlinux.org>
pkgname=hylafax
pkgver=6.0.6
pkgrel=4
pkgdesc="Fax Server"
arch=('i686' 'x86_64')
install='hylafax.install'
license=('custom')
depends=('libtiff' 'pam' 'ghostscript')
makedepends=('smtp-server')
url="http://www.hylafax.org/"
backup=(var/spool/hylafax/bin/{faxrcvd,notify}
	usr/lib/fax/{faxcover.ps,hfaxd.conf,pagesizes,typerules}
	var/spool/hylafax/etc/hosts.hfaxd) 
source=(ftp://ftp.hylafax.org/source/${pkgname}-${pkgver}.tar.gz 
        hylafax.cron.daily 
        config.local
        faxq.service
        hfaxd.service
        faxgetty@.service)

build () {
  cd "$srcdir/${pkgname}-${pkgver}"
  cp ../config.local .
  ./configure --nointeractive --with-OPTIMIZER="${CFLAGS}"
  make
}

package () {
  cd "$srcdir/${pkgname}-${pkgver}"
  mkdir -p "$pkgdir"/usr/{bin,lib/fax,share/ghostscript/fonts}
  mkdir -p "$pkgdir"/var/{spool/hylafax,lock}
  make ROOT="$pkgdir" install
  install -D -m 744 "$srcdir"/hylafax.cron.daily "$pkgdir"/etc/cron.daily/hylafax
  # add missing awk file for notify-4.1 script
  install -D -m 755 "$srcdir"/${pkgname}-${pkgver}/util/notify.awk \
  	"$pkgdir"/var/spool/hylafax/bin/notify.awk
  # remove /var/lock
  rm -r "$pkgdir"/var/lock
  # add systemd files
  install -D -m644 $srcdir/hfaxd.service $pkgdir/usr/lib/systemd/system/hfaxd.service
  install -D -m644 $srcdir/faxq.service $pkgdir/usr/lib/systemd/system/faxq.service
  install -D -m644 $srcdir/faxgetty@.service $pkgdir/usr/lib/systemd/system/faxgetty@.service
  install -D -m644 COPYRIGHT "$pkgdir"/usr/share/licenses/$pkgname/COPYRIGHT
}
md5sums=('d063d45049c8fcbabefe09d662313067'
         'b0b8d19a10c2145476d9fbb18c0478fe'
         '5f3a89fbfb10c47755d3ca19183b8a59'
         'a10aa2ef613551a88d750f6602b5f2f7'
         '38a00db6d9d765d130b2d8d3a98e5c24'
         'fe10ea597e786fe8c09e4104fbdf16de')
