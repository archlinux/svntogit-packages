# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Jan Hambrecht <jaham at gmx dot net>

pkgname=libspnav
pkgver=0.2.2
pkgrel=5
pkgdesc="Alternative to the proprietary 3Dconnexion device driver and SDK for their 3D input devices"
arch=('i686' 'x86_64')
url="http://spacenav.sourceforge.net/"
license=('GPL')
depends=('glibc')
source=("http://downloads.sourceforge.net/spacenav/$pkgname-$pkgver.tar.gz"
        'Makefile.patch')
md5sums=('b85a0f4ab711e2d4f73a40e2e371f5ae'
         'cc008ea9f5e1abb870c0e2dbc86c3b86')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i "${srcdir}"/Makefile.patch
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr \
    --disable-debug
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
