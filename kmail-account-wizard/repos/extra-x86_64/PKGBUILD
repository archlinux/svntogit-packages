# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=kmail-account-wizard
pkgver=22.12.0
pkgrel=1
pkgdesc='KMail account wizard'
arch=(x86_64)
url='https://kontact.kde.org'
license=(GPL2)
depends=(kidentitymanagement pimcommon kmailtransport kross libkleo)
makedepends=(extra-cmake-modules kdoctools qt5-tools knotifyconfig ktexteditor)
groups=(kde-applications kde-pim)
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('85ede3aa5b43cacca8f6c7b0443a66c0de1cf0f4fe3b0096fbef5c08333d431d'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
options=(debug)

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
