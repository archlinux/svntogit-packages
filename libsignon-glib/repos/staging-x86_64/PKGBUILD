# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Balló György <ballogyor+arch@gmail.com>

pkgname=libsignon-glib
pkgver=2.0
pkgrel=1
pkgdesc='GLib-based client library for applications handling account authentication through the Online Accounts Single Sign-On service'
arch=(x86_64)
url='https://gitlab.com/accounts-sso/libsignon-glib'
license=(LGPL2.1)
depends=(signond)
makedepends=(python-gobject gtk-doc gobject-introspection vala meson git)
source=("git+https://gitlab.com/accounts-sso/${pkgname}#tag=VERSION_$pkgver")
sha256sums=('SKIP')

prepare() {
  mkdir -p build

  cd $pkgname
  git submodule init
  git submodule update
}

build() {
  cd build
  arch-meson ../$pkgname -Dtests=false
  ninja -j1
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}
