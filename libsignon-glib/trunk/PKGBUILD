# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Balló György <ballogyor+arch@gmail.com>

pkgname=libsignon-glib
pkgver=1.12
pkgrel=2
pkgdesc='GLib-based client library for applications handling account authentication through the Online Accounts Single Sign-On service'
arch=(i686 x86_64)
url='https://gitlab.com/accounts-sso/libsignon-glib'
license=(LGPL2.1)
depends=(signon)
makedepends=(python2-gobject gtk-doc gobject-introspection)
source=("https://gitlab.com/accounts-sso/$pkgname/repository/archive.tar.gz?ref=$pkgver")
md5sums=('e12a123d9093a62700b46fa2c6ed8e56')

build() {
  cd $pkgname.git

  export PYTHON='/usr/bin/python2'

  ./autogen.sh
  ./configure \
    --prefix='/usr' \
    --localstatedir='/var' \
    --sysconfdir='/etc'
  make
}

package() {
  cd $pkgname.git

  make DESTDIR="$pkgdir" install
}

