# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>

pkgname=intel-ucode
pkgver=20191115
pkgrel=2
pkgdesc='Microcode update files for Intel CPUs'
arch=('any')
url='https://github.com/intel/Intel-Linux-Processor-Microcode-Data-Files'
replaces=('microcode_ctl')
makedepends=('iucode-tool')
license=('custom')
source=("${pkgname}-${pkgver/./}.tar.gz::https://github.com/intel/Intel-Linux-Processor-Microcode-Data-Files/archive/microcode-${pkgver/./}.tar.gz"
        'intel-ucode-06-55-04::https://github.com/intel/Intel-Linux-Processor-Microcode-Data-Files/blob/5ada2f32ed7d216e38823f1320358e4679941990/intel-ucode/06-55-04?raw=true')
sha256sums=('14b26d27be70774948b8cb582e298f5317263b8b8bb1fe8e41260eae54f531dc'
            '3f25e42edc1cd43c1f0a08899c6b5a92cc1a9450a013d73fc56294e7081f7307')

prepare() {
  # https://bugs.archlinux.org/task/64498
  # https://github.com/intel/Intel-Linux-Processor-Microcode-Data-Files/issues/21
  install -m0644 intel-ucode-06-55-04 Intel-Linux-Processor-Microcode-Data-Files-microcode-${pkgver/./}/intel-ucode/06-55-04
}

build() {
  cd Intel-Linux-Processor-Microcode-Data-Files-microcode-${pkgver/./}

  rm -f intel-ucode{,-with-caveats}/list
  mkdir -p kernel/x86/microcode
  iucode_tool -w kernel/x86/microcode/GenuineIntel.bin intel-ucode{,-with-caveats}/
  # Make intel-ucode.img reproducible
  [ ! -z $SOURCE_DATE_EPOCH ] && touch -d @$SOURCE_DATE_EPOCH kernel/x86/microcode/GenuineIntel.bin
  echo kernel/x86/microcode/GenuineIntel.bin | bsdcpio -o -H newc -R 0:0 > intel-ucode.img
}

package() {
  cd Intel-Linux-Processor-Microcode-Data-Files-microcode-${pkgver/./}

  install -D -m0644 intel-ucode.img "${pkgdir}"/boot/intel-ucode.img
  install -D -m0644 license "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
