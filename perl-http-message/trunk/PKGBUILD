# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Justin "juster" Davis <jrcd83@gmail.com>
# Generator : pbjam 0.01

pkgname=perl-http-message
pkgver=6.33
pkgrel=1
pkgdesc="HTTP style messages"
arch=('any')
url='https://search.cpan.org/dist/HTTP-Message'
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl' 'perl-clone' 'perl-encode-locale' 'perl-http-date' 'perl-io-html'
         'perl-lwp-mediatypes' 'perl-uri')
makedepends=('perl-try-tiny')
conflicts=('perl-libwww<6')
source=("https://search.cpan.org/CPAN/authors/id/O/OA/OALDERS/HTTP-Message-$pkgver.tar.gz")
sha512sums=('8c27560796cc28f7db8e5c54a8fec0d21e5bb4d775d1d0d9fa791a7f5b51e523ce4e854facee82211d989f1cacc6f69af53dba23f4558ef0c947add6b35f8314')

build() {
  cd HTTP-Message-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd HTTP-Message-$pkgver
  make test
}

package() {
  cd HTTP-Message-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
