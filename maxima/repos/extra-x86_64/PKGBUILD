# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Damir Perisa <damir@archlinux.org>

pkgname=maxima
pkgver=5.38.0
pkgrel=2
pkgdesc="A sophisticated computer algebra system"
arch=('i686' 'x86_64')
license=('GPL')
url="http://maxima.sourceforge.net"
depends=('sbcl=1.3.1' 'texinfo' 'sh' 'shared-mime-info')
makedepends=('python2' 'emacs')
optdepends=('gnuplot: plotting capabilities' 'rlwrap: readline support via /usr/bin/rmaxima' 'tk: graphical xmaxima interface')
# needs rebuild when bash changes version
# needs a rebuild when sbcl changes version
options=('!makeflags' '!zipman') # don't zip info pages or they won't work inside maxima
source=("http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz" "$pkgname.desktop")
sha1sums=('1cc9ba840f115ab18813dfffeab89fb68ea735ce'
          '4398ebb1ec85ccfa12f37516a56d60c26f74b18b')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # set correct python executable to create docs
  sed -i "s|${PYTHONBIN:-python}|python2|" doc/info/extract_categories.sh
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr --mandir=/usr/share/man --infodir=/usr/share/info \
	--libexecdir=/usr/lib --enable-sbcl --with-default-lisp=sbcl

  # help avoid (re)running makeinfo/tex
  touch doc/info/maxima.info
  make
}

#check() {  # test 23&129 fail from rtest_taylor
#  cd "${srcdir}/${pkgname}-${pkgver}"
#  make check
#}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" emacsdir=/usr/share/emacs/site-lisp/maxima install

  # install some freedesktop.org compatibility
  install -Dm644 "${srcdir}/${pkgname}.desktop" \
  	"${pkgdir}/usr/share/applications/${pkgname}.desktop"

  # make sure, we have a nice icon for the desktop file at the right place ;)
  install -d "${pkgdir}/usr/share/pixmaps/"
  ln -s /usr/share/maxima/${pkgver}/xmaxima/maxima-new.png \
	"${pkgdir}/usr/share/pixmaps/${pkgname}.png"
}
