# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Damir Perisa <damir@archlinux.org>

pkgname=maxima
pkgver=5.21.1
pkgrel=1
pkgdesc="Maxima - a sophisticated computer algebra system"
arch=('i686' 'x86_64')
license=('GPL')
url="http://maxima.sourceforge.net"
depends=('sbcl=1.0.37' 'texinfo' 'sh')
optdepends=('gnuplot: plotting capabilities' \
	'rlwrap: readline support via /usr/bin/rmaxima' \
	'tk: graphical xmaxima interface')
# needs rebuild when bash changes version
# needs a rebuild when sbcl changes version
options=('!makeflags' '!zipman') # don't zip info pages or they won't work inside maxima
install=maxima.install
source=("http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz" "$pkgname.desktop")
md5sums=('e08ed533f3059cec52788ef35198bdcc'
         '24aa81126fbb8b726854e5a80d4c2415')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr --mandir=/usr/share/man --infodir=/usr/share/info \
	--libexecdir=/usr/lib --enable-sbcl --with-default-lisp=sbcl || return 1
  make || return 1
  make DESTDIR=${pkgdir} install || return 1
  
  # install some freedesktop.org compatibility
  install -Dm644 ${srcdir}/${pkgname}.desktop \
  	${pkgdir}/usr/share/applications/${pkgname}.desktop || return 1
  
  # make sure, we have a nice icon for the desktop file at the right place ;)
  install -d ${pkgdir}/usr/share/pixmaps/
  ln -s /usr/share/maxima/${pkgver}/xmaxima/maxima-new.png \
	${pkgdir}/usr/share/pixmaps/${pkgname}.png || return 1
}
