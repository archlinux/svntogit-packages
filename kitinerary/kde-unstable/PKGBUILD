# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kitinerary
pkgver=22.03.90
pkgrel=1
pkgdesc='Data model and extraction system for travel reservation information'
arch=(x86_64)
url='https://kontact.kde.org'
license=(LGPL)
depends=(kmime kpkpass kcalendarcore kcontacts poppler libphonenumber zxing-cpp)
makedepends=(extra-cmake-modules qt5-tools doxygen)
source=(https://download.kde.org/unstable/release-service/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('e2d4721f40a3acadab18cc861de97c4edbaf5fd39d935a4fa7a2568d641c353c'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
options=(debug)

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
