# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gst-plugins-ugly
pkgver=1.14.2
pkgrel=3
pkgdesc="GStreamer Multimedia Framework Ugly Plugins"
url="https://gstreamer.freedesktop.org/"
arch=(x86_64)
license=(LGPL)
depends=(gst-plugins-base-libs libdvdread libmpeg2 a52dec libsidplay libcdio
         x264 opencore-amr)
makedepends=(python gtk-doc autoconf-archive git)
_commit=df1bcfa0ba57859eb39610ef53f1eb744ca57465  # tags/1.14.2^0
source=("git+https://anongit.freedesktop.org/git/gstreamer/gst-plugins-ugly#commit=$_commit"
        "gst-common::git+https://anongit.freedesktop.org/git/gstreamer/common"
        0001-x264enc-Set-bit-depth-for-x264-153.patch)
sha256sums=('SKIP'
            'SKIP'
            'bff7a355de976a3d19b8e6e2f8f15ccb1dacda95679ec1eee7bd98246f5e3a71')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname

  # x264 multi-bit-depth fix
  patch -Np1 -i ../0001-x264enc-Set-bit-depth-for-x264-153.patch

  git submodule init
  git config --local submodule.common.url "$srcdir/gst-common"
  git submodule update

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib \
    --with-package-name="GStreamer Ugly Plugins (Arch Linux)" \
    --with-package-origin="https://www.archlinux.org/" \
    --enable-experimental \
    --enable-gtk-doc \
    --disable-static
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
