# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kio-extras
pkgver=15.08.1
pkgrel=2
pkgdesc="Additional components to increase the functionality of KIO"
arch=(i686 x86_64)
url='https://projects.kde.org/projects/kde/workspace/kio-extras'
license=(LGPL)
depends=(kdelibs4support khtml kdnssd libssh smbclient exiv2 openexr openslp libmtp kpty)
makedepends=(extra-cmake-modules kdoctools)
groups=(kde-applications kdenetwork)
install=$pkgname.install
source=("http://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"
	fish-delete.patch::"https://quickgit.kde.org/?p=kio-extras.git&a=commitdiff&h=311f7e1&o=plain")
md5sums=('b2f48f8a2545b87273fa1caa50e4cf00'
         'd0d808ed2e7d8162cdc4c694c6e2f37f')

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
# Fix delete in fish protocol https://bugs.kde.org/show_bug.cgi?id=351309
  patch -p1 -i ../fish-delete.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}

