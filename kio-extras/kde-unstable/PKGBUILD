# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kio-extras
pkgver=19.03.80
pkgrel=2
pkgdesc="Additional components to increase the functionality of KIO"
arch=(x86_64)
url='https://www.kde.org/applications/internet/'
license=(LGPL)
depends=(kio kdnssd libssh smbclient exiv2 openexr libmtp kpty kactivities syntax-highlighting)
makedepends=(extra-cmake-modules kdoctools gperf taglib khtml libappimage)
optdepends=('qt5-imageformats: thumbnails for additional image formats'
            'kimageformats: thumbnails for additional image formats' 'taglib: audio file thumbnails'
            'khtml: man kioslave' 'phonon-qt5: audio file previews' 'libappimage: AppImage thumbnails')
groups=(kde-applications kdenetwork)
source=("https://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
        kio-extras-rpc.patch)
sha256sums=('4d5b6b85280218764550e02c855f2f25d3ba17a07e80122c94ce11da7f4192ef'
            'SKIP'
            '94d9fd44daddb07fcd18d4c49c379838bdc5e4f0bdd9e03519814295eb6b7895')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../kio-extras-rpc.patch # fix build of kio-nfs with glibc 2.27
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DLIBAPPIMAGE_LIBRARIES=libappimage.so \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
