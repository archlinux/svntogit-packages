# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Antonio Rojas

pkgname=kio-extras
pkgver=5.1.1
pkgrel=2
pkgdesc="Additional components to increase the functionality of KIO"
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/workspace/kio-extras'
license=('LGPL')
depends=('kdelibs4support' 'khtml' 'kdnssd' 'libssh' 'smbclient' 'exiv2' 'openexr' 'openslp')
makedepends=('extra-cmake-modules' 'kdoctools' 'python')
options=('!buildflags')
install="${pkgname}.install"
source=("http://download.kde.org/stable/plasma/${pkgver}/$pkgname-$pkgver.tar.xz"
        'sanitize-input.patch')
md5sums=('831fb9bc8960932233cf6042f6097751'
         '6462465f886decd5c7706d09bc71b2b8')

prepare() {
  mkdir -p build

  cd ${pkgname}-${pkgver}
  patch -p1 -i "${srcdir}"/sanitize-input.patch
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install

  # conflicts with kdebase-runtime
  rm "$pkgdir"/usr/lib/libmolletnetwork.so
  mv "$pkgdir"/usr/share/config.kcfg/jpegcreatorsettings{,5}.kcfg
}

