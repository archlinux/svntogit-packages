# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kio-extras
pkgver=15.11.80
pkgrel=1
pkgdesc="Additional components to increase the functionality of KIO"
arch=(i686 x86_64)
url='https://projects.kde.org/projects/kde/workspace/kio-extras'
license=(LGPL)
depends=(kdelibs4support khtml kdnssd libssh smbclient exiv2 openexr openslp libmtp kpty)
makedepends=(extra-cmake-modules kdoctools)
groups=(kde-applications kdenetwork)
install=$pkgname.install
source=("http://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
md5sums=('88f3eab53430961dbeaf3f8bb7eb8416')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}

