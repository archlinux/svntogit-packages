# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdelibs4support
pkgver=5.3.0
pkgrel=1
pkgdesc='KDE 4 Support'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kdelibs4support'
license=('LGPL')
depends=('kunitconversion' 'kdesignerplugin' 'kinit' 'kitemmodels' 'kemoticons')
makedepends=('extra-cmake-modules' 'kdoctools' 'qt5-tools') # 'networkmanager'
groups=('kf5-aids')
replaces=('kde4support')
conflicts=('kde4support')
source=("http://download.kde.org/stable/frameworks/${pkgver}/portingAids/${pkgname}-${pkgver}.tar.xz")
md5sums=('20f4c4939e6db7c3a1a545356fb95884')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DSYSCONF_INSTALL_DIR=/etc \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
