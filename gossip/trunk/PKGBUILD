# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: Brice Carpentier <brice@dlfp.org>

pkgname=gossip
pkgver=0.31
pkgrel=2
pkgdesc="A GNOME Jabber client"
arch=('i686' 'x86_64')
url="http://live.gnome.org/Gossip"
license=('GPL')
depends=('libxss>=1.2.0' 'libnotify>=0.4.5' 'libgnomeui>=2.24.4' 'loudmouth>=1.4.3' 'aspell' 'iso-codes' 'hicolor-icon-theme' 'libgnome-keyring>=2.32.0')
makedepends=('intltool' 'gnome-doc-utils>=0.20.1' 'gnome-panel>=2.30.2')
optdepends=('gnome-panel: GNOME panel applet')
install=gossip.install
source=(http://ftp.gnome.org/pub/GNOME/sources/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.bz2
        org.gnome.Gossip.service)
sha256sums=('ad2de7ab1700fe2368cc558137e4c17fd983a204894f2a911769709073e38510'
            '9e1244715c0c50210f7afa758cc50a143af1b3e6125fa70ff097bb90be9e7ae0')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/gossip \
      --localstatedir=/var \
      --disable-scrollkeeper \
      --enable-libnotify=yes \
      --enable-dbus=yes \
      --enable-gnome-keyring=yes \
      --enable-aspell=yes
  make
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install

  install -dm755 "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" \
      --domain gossip ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas

  install -Dm644 "${srcdir}/org.gnome.Gossip.service" \
      "${pkgdir}/usr/share/dbus-1/services/org.gnome.Gossip.service"
}
