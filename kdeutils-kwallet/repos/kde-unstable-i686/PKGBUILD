# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeutils-kwallet
pkgver=4.8.97
pkgrel=1
pkgdesc='Wallet Management Tool'
url='http://kde.org/applications/system/kwalletmanager/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdebase-runtime')
makedepends=('cmake' 'automoc4')
groups=('kde' 'kdeutils')
install=${pkgname}.install
source=("http://download.kde.org/unstable/${pkgver}/src/kwallet-${pkgver}.tar.xz")
sha1sums=('601d3fee7551767329aa65a833c22253b49c57f1')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../kwallet-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $srcdir/build
  make DESTDIR=$pkgdir install
  cd $srcdir/build/doc
  make DESTDIR=$pkgdir install
}
