# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>

pkgname=pcre
pkgver=8.01
pkgrel=1
pkgdesc="A library that implements Perl 5-style regular expressions"
arch=('i686' 'x86_64') 
url="http://www.pcre.org/"
license=('BSD')
groups=('base')
depends=('gcc-libs')
options=('!libtool' '!makeflags')
source=(ftp://ftp.csx.cam.ac.uk/pub/software/programming/pcre/${pkgname}-${pkgver}.tar.bz2)
md5sums=('413be1c23dabe91f637fb3770f640006')
sha1sums=('c1addc8201f428be88c54892cfc9a30021e5f091')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  [ "${CARCH}" = "x86_64" ] && export CFLAGS="${CFLAGS} -fPIC"
  ./configure --prefix=/usr --enable-utf8 --enable-unicode-properties || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
  # grep uses pcre, so we need the libs in /lib
  mkdir -p "${pkgdir}/lib"
  mv "${pkgdir}"/usr/lib/libpcre.so.* "${pkgdir}/lib/" || return 1
  ln -sf ../../lib/libpcre.so.0 "${pkgdir}/usr/lib/libpcre.so" || return 1
  install -D -m644 LICENCE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE" || return 1
}
