# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-pyasn1-modules
pkgname=('python-pyasn1-modules' 'python2-pyasn1-modules')
pkgver=0.2.5
pkgrel=1
pkgdesc="A collection of ASN.1-based protocols modules"
arch=('any')
license=('BSD')
url="https://pypi.python.org/pypi/pyasn1-modules"
makedepends=('python-pyasn1' 'python2-pyasn1' 'python-setuptools' 'python2-setuptools')
source=("https://pypi.io/packages/source/p/pyasn1-modules/pyasn1-modules-$pkgver.tar.gz"
        https://github.com/etingof/pyasn1-modules/commit/89e36e97ee1e6fe588a18e47043f447a3061fb18.patch)
sha512sums=('16b49e95b570e1e4c4185cfe596bc4614930030101ddb02080067ca1d94aa388a7932f807fe7b76eaad50ef135f6a0cdbe2a0b355fd7778364eb9d8df7610434'
            'f3a82fc47bc7f580b051208e3639ad4359917f982509e3f1d752b81ae55f965fcfb76bb156b9b0b226ae364b26fa80ecc1583caca07cf77d07732676a0c11a4b')

prepare() {
  # Fix tests
  patch -d pyasn1-modules-$pkgver -p1 -i ../89e36e97ee1e6fe588a18e47043f447a3061fb18.patch
  cp -a pyasn1-modules-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/pyasn1-modules-$pkgver
  python setup.py build

  cd "$srcdir"/pyasn1-modules-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/pyasn1-modules-$pkgver
  PYTHONPATH=. python tests/__main__.py

  cd "$srcdir"/pyasn1-modules-$pkgver-py2
  PYTHONPATH=. python2 tests/__main__.py
}

package_python-pyasn1-modules() {
  depends=('python-pyasn1')

  cd pyasn1-modules-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}

package_python2-pyasn1-modules() {
  depends=('python2-pyasn1')

  cd pyasn1-modules-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
