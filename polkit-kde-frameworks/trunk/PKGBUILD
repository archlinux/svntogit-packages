# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Antonio Rojas <nqn1976 @ gmail.com>

pkgname=polkit-kde-frameworks
pkgver=0.99.1.20.cecfca6
pkgrel=1
pkgdesc='Daemon providing a polkit authentication UI for KDE'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/extragear/base/polkit-kde-agent-1/'
license=('LGPL')
depends=('kdelibs4support')
makedepends=('extra-cmake-modules' 'git' 'kdoctools' 'python')
source=('git://anongit.kde.org/polkit-kde-agent-1.git#commit=cecfca6'
        'libexecpath.patch')
md5sums=('SKIP'
         '166213b7c620fe604f3ecbd9353f294a')

prepare() {
  mkdir -p build

  cd polkit-kde-agent-1
  patch -Rp1 -i "${srcdir}"/libexecpath.patch
}

build() {
  cd build
  cmake ../polkit-kde-agent-1 \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DSYSCONF_INSTALL_DIR=/etc \
    -DLIBEXEC_INSTALL_DIR=lib/polkit-kde \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
