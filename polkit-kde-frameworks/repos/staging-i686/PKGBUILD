# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Antonio Rojas <nqn1976 @ gmail.com>

pkgname=polkit-kde-frameworks
pkgver=5.2.1
pkgrel=1
pkgdesc='Daemon providing a polkit authentication UI for KDE'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/workspace/polkit-kde-agent-1'
license=('LGPL')
depends=('knotifications')
makedepends=('extra-cmake-modules' 'git' 'kdoctools' 'python')
source=("http://download.kde.org/stable/plasma/${pkgver}/polkit-kde-agent-1-$pkgver.tar.xz")
md5sums=('837eec2b8885b6a25dfb649eb9481f3f')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../polkit-kde-agent-1-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DLIBEXEC_INSTALL_DIR=lib/polkit-kde \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
