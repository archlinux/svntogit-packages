# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>

pkgname=qwt
pkgver=6.0.1
pkgrel=3
pkgdesc="Qt Widgets for Technical Applications"
arch=('i686' 'x86_64')
url="http://qwt.sourceforge.net/"
depends=('qt')
options=('!makeflags')
license=("custom:$pkgname")
source=("http://downloads.sourceforge.net/sourceforge/qwt/$pkgname-$pkgver.tar.bz2" \
        "qwtconfig-archlinux.pri")
sha1sums=('301cca0c49c7efc14363b42e082b09056178973e'
          'aba0fc75f6d450237028b7b143bdcf05a79cabb3')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # copy our config file to the right place
  install -Dm644 ${srcdir}/qwtconfig-archlinux.pri \
	${srcdir}/${pkgname}-${pkgver}/qwtconfig.pri

  # build qwt:
  qmake qwt.pro
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}

  make INSTALL_ROOT=${pkgdir} QTDIR=/usr install

  # install web designer plugin
  install -d ${pkgdir}/usr/lib/qt/plugins/designer/
  install -Dm755 designer/plugins/designer/libqwt_designer_plugin.so \
    ${pkgdir}/usr/lib/qt/plugins/designer/

  # Move man files to proper directory.
#  install -d ${pkgdir}/usr/share/man/man3
#  mv -f ${pkgdir}/usr/share/doc/qwt/man/man3/* ${pkgdir}/usr/share/man/man3

  # install licence
  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/COPYING \
	${pkgdir}/usr/share/licenses/${pkgname}/license.txt
}
