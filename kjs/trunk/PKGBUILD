# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kjs
pkgver=r4.b4bbeca
pkgrel=1
pkgdesc='KJS'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/kjs'
license=('LGPL')
depends=('qt5-base')
makedepends=('extra-cmake-modules' 'git')
conflicts=('kjs')
provides=('kjs')
source=('git://anongit.kde.org/kjs.git')
md5sums=('SKIP')

pkgver() {
  cd kjs
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kjs \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
