# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgname=dirmngr
pkgver=1.1.1
pkgrel=2
pkgdesc="A daemon to handle CRL and certificate requests"
arch=('i686' 'x86_64')
license=('GPL')
url="ftp://ftp.gnupg.org/gcrypt/dirmngr"
depends=('libgcrypt' 'libldap' 'libksba' 'libgpg-error' 'libassuan' 'pth')
source=(ftp://ftp.gnupg.org/gcrypt/$pkgname/$pkgname-$pkgver.tar.bz2)
install=dirmngr.install
md5sums=('f5a40e93bcf07a94522579bfd58a2c96')

build() {
  cd $srcdir/$pkgname-$pkgver
  # FIX for https://bugzilla.redhat.com/565131
  # not sure how best to turn this into something more upstreamable
  # patch configure to try this combo first? -- Rex
  # https://bugs.archlinux.org/task/28606
  export LDAPLIBS="-lldap -llber"
  ./configure --prefix=/usr  --libexecdir=/usr/lib
  make 
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
}
