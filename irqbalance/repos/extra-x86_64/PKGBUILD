# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Dan McGee <dan@archlinux.org>
# Contributor: Martin Striz <ms@poruba.net>

pkgname=irqbalance
pkgver=1.9.0
pkgrel=1
pkgdesc="IRQ balancing daemon for SMP systems"
arch=(x86_64)
url="https://github.com/irqbalance/irqbalance"
license=(GPL2)
backup=(etc/$pkgname.env)
depends=(glibc)
makedepends=(glib2 libcap-ng ncurses numactl systemd systemd-libs)
options=(debug)
source=($pkgname-$pkgver.tar.gz::https://github.com/$pkgname/$pkgname/archive/v$pkgver.tar.gz)
sha512sums=('878977da5eff18e53bdceeaa4aca952f73ba8b03eb028cf176816af971ffc65f0b1f1bb3a68e3a2502491895cc2b9438652dc97d5696232bb2f64860109e9a24')
b2sums=('11df51b03a473da10340c0c9ca9a28b3d9f9ed81e202e434b135a4be1cf54bc5558b33a4a922eba89bdd8cebbd6d3448527507fa72f03c4ff494ae83d513854e')

prepare() {
  # fix location of configuration and binary in service
  sed -e 's|/path/to/|/etc/|g;s|/usr/sbin|/usr/bin|g' -i $pkgname-$pkgver/misc/$pkgname.service

  cd $pkgname-$pkgver
  autoreconf -fiv
}

build() {
  cd $pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --with-systemd
  make
}

check() {
  make -k check -C $pkgname-$pkgver
}

package() {
  depends+=(libglib-2.0.so libcap-ng.so libncursesw.so libnuma.so libsystemd.so)

  cd $pkgname-$pkgver
  make install DESTDIR="$pkgdir"
  install -vDm 644 misc/irqbalance.service -t "$pkgdir/usr/lib/systemd/system/"
  install -vDm 644 misc/irqbalance.env -t "$pkgdir/etc/"
  install -vDm 644 {AUTHORS,README.md} -t "$pkgdir/usr/share/doc/$pkgname/"
}
