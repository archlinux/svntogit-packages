# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=telepathy-kde-contact-runner
_pkgname=ktp-contact-runner
pkgver=17.08.0
pkgrel=1
pkgdesc='KRunner plugin for KDE Telepathy'
arch=('i686' 'x86_64')
url='https://community.kde.org/Real-Time_Communication_and_Collaboration'
license=('GPL')
groups=('kde-applications' 'kdenetwork' 'telepathy-kde')
depends=('telepathy-kde-common-internals' 'krunner')
makedepends=('extra-cmake-modules' 'kdoctools' 'python')
source=("https://download.kde.org/stable/applications/${pkgver}/src/$_pkgname-${pkgver}.tar.xz"{,.sig})
sha256sums=('d2823505798ef2fb39b824827f5f1ffc002d80322353ba9ad4b593a598a4499a'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$_pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
