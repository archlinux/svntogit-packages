# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdevelop-php
pkgver=22.04.3
pkgrel=1
pkgdesc='PHP language and documentation plugin for KDevelop'
arch=(x86_64)
url='http://www.kdevelop.org/'
license=(GPL)
depends=(kdevelop)
makedepends=(extra-cmake-modules kdoctools kdevelop-pg-qt)
groups=(kde-applications kdevelop)
source=(https://download.kde.org/stable/release-service/$pkgver/src/kdev-php-$pkgver.tar.xz{,.sig})
sha256sums=('1a267c2eaed3658ac0b2dacc28956867eab7608bacf57b3041952a868f27c915'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>
options=(debug)


build() {
  cmake -B build -S kdev-php-$pkgver \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
