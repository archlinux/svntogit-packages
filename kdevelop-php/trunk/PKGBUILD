# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdevelop-php
pkgver=5.0.1
pkgrel=2
pkgdesc="PHP language and documentation plugin for KDevelop"
arch=(i686 x86_64)
url="http://www.kdevelop.org/"
license=(GPL)
depends=(kdevelop)
makedepends=(extra-cmake-modules kdoctools kdevelop-pg-qt python)
source=("http://download.kde.org/stable/kdevelop/$pkgver/src/kdev-php-$pkgver.tar.xz"{,.sig})
sha1sums=('e6593ee234825d660b8cf289446e447ec006b23c'
          'SKIP')
validpgpkeys=('329FD02C5AA48FCC77A4BBF0AC44AC6DB29779E6') # Sven Brauch <mail@svenbrauch.de>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kdev-php-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
