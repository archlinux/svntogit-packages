# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegraphics-gwenview
pkgver=4.9.90
pkgrel=1
pkgdesc="A fast and easy to use image viewer for KDE"
url="http://kde.org/applications/graphics/gwenview/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegraphics')
depends=('kdebase-lib' 'kdebase-runtime' 'libkipi' 'lcms2')
makedepends=('cmake' 'automoc4' 'mesa')
optdepends=('kipi-plugins: extra plugins to share photos')
install=$pkgname.install
source=("http://download.kde.org/unstable/${pkgver}/src/gwenview-${pkgver}.tar.xz")
sha1sums=('98da738eb8c16377d72f3e8e6b5b3bdb2238e071')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../gwenview-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
