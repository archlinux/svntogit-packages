# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=libmm-qt
pkgver=1.0.1
pkgrel=1
pkgdesc='Qt-only wrapper for ModemManager DBus API'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/extragear/libs/libmm-qt'
license=('LGPL')
depends=('qt4' 'modemmanager')
makedepends=('cmake' 'doxygen')
source=("http://download.kde.org/unstable/modemmanager-qt/${pkgver}/src/${pkgname}-${pkgver}.tar.xz"
        'fix-build.patch')
md5sums=('73cf04d0e71d75ce343d791968a7c460'
         '265fd2e32a5e350c1703612d29268748')

prepare() {
  mkdir -p build

  cd ${pkgname}-${pkgver}
  patch -p1 -i "${srcdir}"/fix-build.patch
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
