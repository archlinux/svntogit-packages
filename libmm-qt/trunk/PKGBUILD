# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=libmm-qt
pkgver=1.0.1
pkgrel=3
pkgdesc='Qt-only wrapper for ModemManager DBus API'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/extragear/libs/libmm-qt'
license=('LGPL')
depends=('qt4' 'modemmanager')
makedepends=('cmake' 'doxygen')
source=("http://download.kde.org/unstable/modemmanager-qt/${pkgver}/src/${pkgname}-${pkgver}-1.tar.xz")
md5sums=('1ee1c7754cbb4fdf484ac2648d7263e0')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
