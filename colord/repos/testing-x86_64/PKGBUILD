# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=colord
pkgver=0.1.23
pkgrel=1
pkgdesc="Color daemon"
arch=('i686' 'x86_64')
url="http://www.freedesktop.org/software/colord"
license=('GPL2')
depends=('lcms2' 'libgusb' 'polkit' 'sane' 'shared-color-profiles' 'sqlite' 'udev')
makedepends=('intltool' 'gobject-introspection' 'vala' 'docbook2x')
source=($url/releases/$pkgname-$pkgver.tar.xz)
options=('!libtool')
sha1sums=('08019c86f89b1675e4e155772cc555ed748f319e')

build() {
  cd "$pkgname-$pkgver"
  
  # put udev files in /usr/lib
  sed -i "/slashlibdir=/s#/lib#/usr/lib#" configure
  
  ./configure --prefix=/usr \
    --sysconfdir=/etc --libexecdir=/usr/lib/colord \
    --localstatedir=/var --disable-static \
    --with-systemdsystemunitdir=/usr/lib/systemd/system
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install \
    bashcompletiondir=/usr/share/bash-completion/completions
}

# vim:set ts=2 sw=2 et:
