# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgbase=colord
pkgname=(colord colord-sane)
pkgver=1.4.4
pkgrel=2
pkgdesc="System daemon for managing color devices"
url="https://www.freedesktop.org/software/colord"
arch=(x86_64)
license=(GPL2)
depends=(lcms2 libgusb polkit sqlite dconf dbus libgudev shared-mime-info systemd-libs udev)
makedepends=(gobject-introspection vala sane bash-completion argyllcms git meson gtk-doc systemd
             docbook-xsl)
options=(!emptydirs)
_commit=233e642f730e46e026c1fe45d36ea298de1e00fe  # tags/1.4.4^0
source=("git+https://github.com/hughsie/colord#commit=$_commit"
        0001-trivial-Fix-error-checking-for-sd_pid_get_session-an.patch)
sha256sums=('SKIP'
            '90df20f7962a24a97b92fcaa7178c263f4b00ba1e90ff2dc5efdb7e97873b3a1')
validpgpkeys=('163EB50119225DB3DF8F49EA17ACBA8DFA970E17')  # Richard Hughes

pkgver() {
  cd colord
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd colord

  # https://bugs.archlinux.org/task/37014
  patch -Np1 -i ../0001-trivial-Fix-error-checking-for-sd_pid_get_session-an.patch
}

build() {
  arch-meson colord build \
    -D libcolordcompat=true \
    -D sane=true \
    -D vapi=true \
    -D print_profiles=true \
    -D daemon_user=colord
  ninja -C build
}

check() {
  meson test -C build
}

package_colord() {
  optdepends=('argyllcms: color profiling'
              'colord-sane: SANE support')
  replaces=(shared-color-profiles)

  DESTDIR="$pkgdir" meson install -C build

  echo 'u colord - "Color management daemon" /var/lib/colord' |
    install -Dm644 /dev/stdin "$pkgdir/usr/lib/sysusers.d/colord.conf"

### Split colord-sane
  mkdir -p colord-sane/usr/lib/colord-plugins
  mv {"$pkgdir",colord-sane}/usr/lib/colord-sane
  mv {"$pkgdir",colord-sane}/usr/lib/colord-plugins/libcolord_sensor_sane.so
}

package_colord-sane() {
  pkgdesc+=" (SANE support)"
  depends=("colord=$pkgver-$pkgrel" sane)
  mv colord-sane/* "$pkgdir"
}

# vim:set sw=2 et:
