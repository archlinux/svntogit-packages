# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libstdc++5
pkgver=3.3.6
pkgrel=6
pkgdesc="GNU Standard C++ library version 3"
arch=('x86_64')
url="http://gcc.gnu.org"
license=('GPL' 'LGPL')
depends=('gcc-libs')
makedepends=('binutils' 'gcc')
options=('!makeflags')
source=(ftp://gcc.gnu.org/pub/gcc/releases/gcc-${pkgver}/gcc-{core,g++}-${pkgver}.tar.bz2
        gcc-3.4.3-no_multilib_amd64.patch
        gcc-3.4.6-ucontext.patch
        siginfo.patch)
md5sums=('18c52e6fb8966b7700665dca289d077f'
         '6b3d00b8d079805be1b895f7f6ce47a0'
         '8504bb22dae9d49e8b6d70d7e6910f1b'
         '982ec78eed2887657189aefbe0c642d0'
         'bf36b281a9fc81f624f31f2623a3ad0e')

prepare() {
  cd gcc-$pkgver

  patch -Np1 -i $srcdir/gcc-3.4.3-no_multilib_amd64.patch
  patch -Np1 -i $srcdir/gcc-3.4.6-ucontext.patch
  
  # fix build issue with recent gcc
  sed -i "s#O_CREAT#O_CREAT, 0666#" gcc/collect2.c

  # No fixincludes
  sed -i -e 's@\./fixinc\.sh@-c true@' gcc/Makefile.in

  patch -Np0 -i ../siginfo.patch

  mkdir ../gcc-build
}

build(){
  export CFLAGS="-march=${CARCH/_/-} -O2"
  export CXXFLAGS="-march=${CARCH/_/-} -O2"
  unset CPPFLAGS

  cd gcc-build
  CPP=/usr/bin/cpp ../gcc-${pkgver}/configure --prefix=/usr --enable-shared \
      --enable-languages=c++ --enable-threads=posix --enable-__cxa_atexit \
      --disable-multilib --libdir=/usr/lib
  make all-target-libstdc++-v3 BOOT_CFLAGS="${CFLAGS}" STAGE1_CFLAGS="-O"
}

package() {
  cd ${srcdir}/gcc-build
  make DESTDIR=${pkgdir} install-target-libstdc++-v3

  # Remove includefiles and libs provided by gcc
  rm -rf ${pkgdir}/usr/{include,share/locale}
  rm -f ${pkgdir}/usr/lib/*.a
  rm -f ${pkgdir}/usr/lib/libstdc++.so
}
