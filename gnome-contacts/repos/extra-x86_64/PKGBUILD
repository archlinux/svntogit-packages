# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=gnome-contacts
pkgver=3.22.1+4+gb235b6d
pkgrel=1
pkgdesc="Contacts Manager for GNOME"
url="https://wiki.gnome.org/Apps/Contacts"
arch=(i686 x86_64)
license=(GPL2)
depends=(gtk3 folks gnome-desktop dconf gnome-online-accounts libgee telepathy-glib cheese libchamplain)
makedepends=(vala gobject-introspection git)
groups=(gnome)
_commit=b235b6d63eba6e528f3d428f34c49ed790a8f91f
source=("git://git.gnome.org/gnome-contacts#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --libexecdir=/usr/lib/$pkgname
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
