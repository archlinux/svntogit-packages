# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=loki
pkgver=1.3.0
pkgrel=1
pkgdesc='like Prometheus, but for logs'
url='https://github.com/grafana/loki'
arch=('x86_64')
license=('Apache')
depends=('glibc')
makedepends=('go-pie')
backup=('etc/loki/loki.yaml' 'etc/loki/promtail.yaml')
source=($pkgname-$pkgver.tar.gz::https://github.com/grafana/loki/archive/v$pkgver.tar.gz
        promtail.sysusers promtail.service promtail.tmpfiles
        loki.sysusers loki.service loki.tmpfiles)
sha512sums=('db2c5e81b2b24d884f2c56531e577beae693cc06e30fe74b4d89b6b1c3857992396aeb46877ab5b787b268741cc9de75fd5ed53c548de6abac701afe97477df2'
            '2b6c44b18ea3c9f955a7450222180d0b20b5fc551d0b7e5d0d8949e40adc847c4166829146260f87a75732cc5473eab0347dd56fc2125517698bac0652738c74'
            'a3427ddecada33b90658635962c4ea36ced6b9d0e1686ce898884980c2ce1a82be2ddcfa6b42736392653f48fa561408633d6016b77e2b513029ba0cec977727'
            '598042c40673a7914c5a1eeccfb78f832379a61f4360212c5d86f667343cf2fc78e98d9025f9717ea64f3e16e0a28f08cd7709706d811656722019f6167dd788'
            'f00b3cb64b71d3ca5a422a2bdff1f81a3e2707c4f73bcb27e24338eabff039b96125c847aaae43c9b22ccc47f89585118c1d0e0b29c4eb7b6f9260c68f8a8324'
            '3c40d70366ee0ac3d11c87a818d82dfb4e64143e905187b287f7716ee009640bab6676b8a348e526458a00e4cff35fbd8e9d81bdaa6db38a5d673294f09e6638'
            'de40d1e6752edbf8c21317ce1ee10f98dfc869e569c07092c613f7144e261e9438683a145a2e0e37e2a9fc758c5c2f02e1d0ac1c60347e98b147e4a550ec6040')

build() {
  cd loki-$pkgver

  LDFLAGS="-extldflags $LDFLAGS"

  go build \
    -mod=vendor \
    -trimpath \
    -ldflags "$LDFLAGS" \
    ./cmd/loki

  go build \
    -mod=vendor \
    -trimpath \
    -ldflags "$LDFLAGS" \
    ./cmd/promtail

  go build \
    -mod=vendor \
    -trimpath \
    -ldflags "$LDFLAGS" \
    ./cmd/logcli
}

check() {
  cd loki-$pkgver

  go test -v $(go list  ./... | grep -v "distributor")
}

package() {
  cd loki-$pkgver

  install -Dm755 -t "$pkgdir"/usr/bin loki promtail logcli

  install -Dm644 cmd/promtail/promtail-local-config.yaml $pkgdir/etc/loki/promtail.yaml
  install -Dm644 cmd/loki/loki-local-config.yaml $pkgdir/etc/loki/loki.yaml

  # Promtail
  install -Dm644 ${srcdir}/promtail.sysusers "$pkgdir/usr/lib/sysusers.d/promtail.conf"
  install -Dm644 ${srcdir}/promtail.tmpfiles "$pkgdir/usr/lib/tmpfiles.d/promtail.conf"
  install -Dm644 ${srcdir}/promtail.service "$pkgdir/usr/lib/systemd/system/promtail.service"

  # Loki
  install -Dm644 ${srcdir}/loki.sysusers "$pkgdir/usr/lib/sysusers.d/loki.conf"
  install -Dm644 ${srcdir}/loki.tmpfiles "$pkgdir/usr/lib/tmpfiles.d/loki.conf"
  install -Dm644 ${srcdir}/loki.service "$pkgdir/usr/lib/systemd/system/loki.service"
}
