# $Id$
# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Aaron Griffin <aaron@archlinux.org>

pkgname=iputils
pkgver=20121106
pkgrel=1
pkgdesc="IP Configuration Utilities (and Ping)"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.linuxfoundation.org/en/Net:Iputils"
groups=('base')
depends=('openssl' 'sysfsutils' 'libcap')
optdepends=('xinetd: for tftpd')
makedepends=('docbook2x' 'opensp')
conflicts=('netkit-base' 'arping' 'netkit-tftpd')
replaces=('netkit-base')
backup=(etc/xinetd.d/tftp)
install=${pkgname}.install
source=(http://www.skbuff.net/${pkgname}/${pkgname}-s${pkgver}.tar.bz2
        tftp.xinetd)
sha1sums=('9570832f3945bb5f0e89f5837facc8fdca3b7769'
          'fc2ae26f5609725e3f4aeaf4ab82dfa6d2e378fd')

build() {
  cd "${srcdir}/${pkgname}-s${pkgver}"

  # Use our CFLAGS
  sed -i -e "/^CCOPT=/s|-O2|${CFLAGS}|" Makefile

  make

  cd doc
  for file in *.sgml; do
    xf=${file/.sgml/.xml}
    osx -xlower -xno-nl-in-tag $file > $xf || true
    sed -i "s|<refname>\(.*\), \(.*\)</refname>|<refname>\1</refname>, <refname>\2</refname>|g" $xf
    docbook2man $xf
  done
}

package() {
  cd "${srcdir}/${pkgname}-s${pkgver}"

  install -dm755 "${pkgdir}"/usr/{bin,sbin} "${pkgdir}"/bin

  install -m755 arping clockdiff rarpd rdisc tftpd tracepath tracepath6 \
    "${pkgdir}"/usr/sbin/

  install -m755 ping{,6} "${pkgdir}"/usr/bin/
   ln -sf /usr/bin/ping{,6}  "${pkgdir}"/bin/

   install -dm755 "${pkgdir}"/usr/share/man/man8
   install -m644 doc/{arping,clockdiff,ping,rarpd,rdisc,tftpd,tracepath}.8 \
     "${pkgdir}"/usr/share/man/man8/

  cd "${pkgdir}"/usr/share/man/man8
  ln -sf ping.8.gz  ping6.8.gz
  ln -sf tracepath.8.gz tracepath6.8.gz

  # FS#24768
  install -dm755 "${pkgdir}"/etc/xinetd.d/
  install -m644 "${srcdir}"/tftp.xinetd "${pkgdir}"/etc/xinetd.d/tftp
}
