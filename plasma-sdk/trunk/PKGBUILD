# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=plasma-sdk
pkgver=5.6.4
pkgrel=1
pkgdesc="Applications useful for Plasma development"
arch=(i686 x86_64)
url='https://www.kde.org/workspaces/plasmadesktop/'
license=(LGPL)
depends=(plasma-framework ktexteditor)
makedepends=(extra-cmake-modules qt5-webkit kitemmodels knewstuff python2)
groups=(plasma)
source=("http://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz")
md5sums=('26dd46468ffc31a09bfa988c8290c7a0')

prepare() {
  mkdir -p build
}    

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
