# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Isenmann <daniel@archlinux.org>
# Contributor: orelien <aurelien.foret@wanadoo.fr>

pkgname=rp-pppoe
pkgver=3.15
pkgrel=2
pkgdesc="Roaring Penguin's Point-to-Point Protocol over Ethernet client"
arch=('x86_64')
url="https://dianne.skoll.ca/projects/rp-pppoe"
license=('GPL')
depends=('ppp=2.4.9' 'sh' 'iproute2')
backup=(etc/ppp/pppoe.conf etc/ppp/firewall-standalone etc/ppp/firewall-masq 
        etc/ppp/pppoe-server-options)
options=('!makeflags')
install=rp-pppoe.install
source=(https://dianne.skoll.ca/projects/rp-pppoe/download/rp-pppoe-$pkgver.tar.gz{,.sig} 
       adsl.service)
validpgpkeys=('FC2E9B645468698FD7B21655C1842E2A126F42E0'  # Dianne Skoll <dfs@roaringpenguin.com>
              '738E4D954052902C147D07B2685A5A5E511D30E2') # Dianne Skoll <dianne@skoll.ca>
sha512sums=('a156c084e57361ab6a464c3205ffb85cf86d02f71f17f92c9567f1ab0ed300f10030832fd232084699dc842ac4891efc8c54c8165587bfc7b4c92724318a60d9'
            'SKIP'
            '75fdb55b872e6388053aa0c1ba0b98ab9eda6b6f59a7452843cff8de5a68276be3f48dbd9a6324917254d4fe0d1b2d7442f7a9bbfa1355b6f500db13c9409089')

build() {
  cd $pkgname-$pkgver/src
  ./configure --prefix=/usr --sbindir=/usr/bin --enable-plugin
  make PLUGIN_DIR="/usr/lib/rp-pppoe" all rp-pppoe.so
}

package() {
  cd $pkgname-$pkgver/src
  make PLUGIN_DIR="/usr/lib/rp-pppoe" DESTDIR="$pkgdir" install

  #install -Dm755 "$srcdir/adsl" "$pkgdir/etc/rc.d/adsl"
  install -Dm644 "$srcdir/adsl.service" "$pkgdir/usr/lib/systemd/system/adsl.service"
}
