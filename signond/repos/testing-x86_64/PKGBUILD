# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>

pkgname=signond
pkgver=8.60
pkgrel=1
pkgdesc="A D-Bus service which performs user authentication on behalf of its clients"
arch=(x86_64)
url="https://gitlab.com/accounts-sso/signond/"
license=(LGPL)
depends=(qt5-base libproxy)
makedepends=(doxygen graphviz ttf-font qt5-tools)
conflicts=(signon signon-qt5)
provides=(signon signon-qt5)
replaces=(signon signon-qt5)
source=("https://gitlab.com/accounts-sso/signond/repository/archive.tar.bz2?ref=VERSION_$pkgver")
sha1sums=('55f745a6379338e99bd2b41107a569c96c3d5db8')

build() {
  cd $pkgname-*
  qmake PREFIX=/usr LIBDIR=/usr/lib
  make
}

package() {
  cd $pkgname-*
  make INSTALL_ROOT="$pkgdir" install

  # Do not ship docs
  rm -rf "$pkgdir"/usr/share/doc
}
