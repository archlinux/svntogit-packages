# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>

pkgname=xchat
pkgver=2.8.6
pkgrel=3
pkgdesc="A GTK+ based IRC client"
arch=('i686' 'x86_64')
url="http://www.xchat.org/"
license=('GPL')
depends=('gtk2>=2.10.9' 'openssl>=0.9.8b' 'dbus-glib>=0.7.2')
makedepends=('tcl' 'perl' 'python')
optdepends=('enchant: for spell checking support' 'tcl: for tcl plugin')
options=('!libtool')
source=(http://www.xchat.org/files/source/${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2 \
        http://www.xchat.org/files/source/2.8/patches/xc286-smallfixes.diff
        xchat-gtk2.patch)
md5sums=('1f2670865d43a23a9abc596dde999aca' 
         'eb68b6261bf6740cb5f223627eb8f384'
         'f3fe00982c0bec93152ed0b32438ef87')
sha1sums=('7a7463e65cb26c680ed70dd96cec98bcc05435a4'
          'b13eb421f6d72fca195b398c606d9413aec57fc4'
          'cf9c5d93af262474e0855f2965210a4a3600acc7')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  patch -p1 < ../xc286-smallfixes.diff || return 1
  patch -p1 < ../xchat-gtk2.patch || return 1
  ./configure --prefix=/usr --sysconfdir=/etc \
              --enable-openssl --enable-dbus \
              --disable-gnome --disable-textfe \
	      --enable-ipv6 --enable-shm \
	      --enable-spell=static || return 1
  make || return 1
  make DESTDIR=${pkgdir} install || return 1
}
