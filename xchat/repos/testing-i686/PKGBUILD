# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=xchat
pkgver=2.8.8
pkgrel=7
pkgdesc="A GTK+ based IRC client"
arch=('i686' 'x86_64')
url="http://www.xchat.org/"
license=('GPL')
depends=('gtk2' 'openssl' 'dbus-glib' 'libnotify')
makedepends=('tcl' 'perl' 'python2')
optdepends=('enchant: for spell checking support' 'tcl: for tcl plugin'
            'python2: for python plugin')
options=('!libtool')
source=(http://www.xchat.org/files/source/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz
        xchat-2.8.8-libnotify07.patch
        xchat-2.8.8-link-against-libnotify.patch)
md5sums=('9a11f13121ff0af787bba3b014378e58'
         '83359a807fa4f7faee90297576fb17a7'
         'dc30fd1203a93b19d47734ba55713b7a')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i ${srcdir}/xchat-2.8.8-libnotify07.patch
  patch -Np1 -i ${srcdir}/xchat-2.8.8-link-against-libnotify.patch
  autoconf
  autoheader

  ./configure --prefix=/usr --sysconfdir=/etc --enable-openssl --enable-dbus \
    --disable-textfe --enable-ipv6 --enable-shm --enable-spell=static
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
