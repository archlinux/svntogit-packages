# Maintainer: tobias <tobias@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>

pkgname=perl-gtk2-ex-formfactory
_realname=Gtk2-Ex-FormFactory
pkgver=0.67
pkgrel=4
pkgdesc="Framework for perl-gtk2 development"
arch=('any')
license=('LGPL')
url="http://www.exit1.org/${_realname}/"
depends=('gtk2-perl' 'perl')
options=('!emptydirs')
DLAGENTS=('http::/usr/bin/wget -c -t 3 --waitretry=3 -O %o %u') # curl doesn't work
source=(http://www.exit1.org/packages/${_realname}/dist/${_realname}-${pkgver}.tar.gz)
md5sums=('17f0cfd4957196f9928d023a4801ed5c')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  # install module in vendor directories.
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make test
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make install DESTDIR="${pkgdir}"

  # remove perllocal.pod and .packlist
  find "${pkgdir}" -name perllocal.pod -delete
  find "${pkgdir}" -name .packlist -delete
}
