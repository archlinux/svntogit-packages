# Maintainer: Antonio Rojas <arojas@archlinux,org>

pkgname=kseexpr
pkgver=4.0.2.0
pkgrel=1
pkgdesc='An embeddable expression evaluation engine (Krita fork)'
arch=(x86_64)
url='https://krita.org'
license=(Apache)
depends=(qt5-base)
makedepends=(extra-cmake-modules qt5-tools)
replaces=(seexpr-krita)
source=(https://download.kde.org/stable/kseexpr/${pkgver%.*}/$pkgname-$pkgver.tar.gz
        https://invent.kde.org/graphics/kseexpr/-/commit/e399f852.patch)
sha256sums=('6a9857522883691a4184e7567645e7103a662d27f0452b7602b78c6f8b8bc1c2'
            '4c65daa1c32d39b9605b03691675e2611b2863fd73cc7cef9498845ef2b9bb1a')

prepare() {
  patch -d $pkgname-$pkgver -p1 < e399f852.patch # Fix soversion
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_DEMOS=OFF \
    -DBUILD_TESTS=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
