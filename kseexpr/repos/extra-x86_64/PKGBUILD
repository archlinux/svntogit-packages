# Maintainer: Antonio Rojas <arojas@archlinux,org>

pkgname=kseexpr
pkgver=4.0.4.0
pkgrel=1
pkgdesc='An embeddable expression evaluation engine (Krita fork)'
arch=(x86_64)
url='https://krita.org'
license=(Apache)
depends=(qt5-base)
makedepends=(extra-cmake-modules qt5-tools)
replaces=(seexpr-krita)
source=(https://download.kde.org/stable/kseexpr/${pkgver%.*}/$pkgname-$pkgver.tar.gz)
sha256sums=('ea4f6a8e820e6cc12d2a1e6977e96ea5457f37729fe0bc4ee8cc62d4c26c36ae')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_DEMOS=OFF \
    -DBUILD_TESTS=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
