# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=accountsservice
pkgver=0.6.35
pkgrel=2
pkgdesc="D-Bus interface for user account query and manipulation"
arch=(i686 x86_64)
url="http://www.freedesktop.org/software/accountsservice/"
license=('GPL3')
depends=('glib2' 'polkit' 'systemd')
makedepends=('intltool' 'gobject-introspection')
source=($url/$pkgname-$pkgver.tar.xz
        avoid-deleting-the-root-user.patch)
md5sums=('3a81133e95faafb603de4475802cb06a'
         '4970e77c3c0d56e513f9a5f29fdacd2c')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../avoid-deleting-the-root-user.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
    --libexecdir=/usr/lib/accountsservice \
    --with-systemdsystemunitdir=/usr/lib/systemd/system \
    --localstatedir=/var --disable-static --enable-systemd
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
