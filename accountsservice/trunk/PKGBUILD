# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

pkgname=accountsservice
pkgver=0.6.29
pkgrel=1
pkgdesc="D-Bus interface for user account query and manipulation"
arch=(i686 x86_64)
url="http://www.freedesktop.org/software/accountsservice/"
license=('GPL3')
depends=('glib2' 'polkit' 'systemd')
makedepends=('intltool' 'gobject-introspection' 'vala')
options=('!libtool')
source=($url/$pkgname-$pkgver.tar.xz more-exclusions.patch)
md5sums=('396c16f39711e24bb7cc685a170befa3'
         '1d5cf127e5aac407fe7c37b2f4fd503c')

build() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../more-exclusions.patch
  ./configure --prefix=/usr --sysconfdir=/etc \
    --libexecdir=/usr/lib/accountsservice \
    --with-systemdsystemunitdir=/usr/lib/systemd/system \
    --localstatedir=/var --disable-static --enable-systemd
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
