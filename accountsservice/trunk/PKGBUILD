# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
pkgname=accountsservice
pkgver=0.6.4
pkgrel=2
pkgdesc="D-Bus interface for user account query and manipulation"
arch=(i686 x86_64)
url="http://cgit.freedesktop.org/accountsservice/"
license=('GPL3')
depends=('dbus-glib' 'polkit')
makedepends=('intltool' 'gobject-introspection')
options=('!libtool')
source=(http://cgit.freedesktop.org/accountsservice/snapshot/${pkgname}-${pkgver}.tar.bz2)
md5sums=('274d9167bca65b3f3abfdd5702f6e4fc')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc \
    --libexecdir=/usr/lib/accountsservice \
    --localstatedir=/var --disable-static
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
