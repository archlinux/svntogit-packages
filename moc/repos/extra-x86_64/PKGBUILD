# $Id$
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: dorphell <dorphell@gmx.net>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=moc
pkgver=2.4.4
pkgrel=2
pkgdesc="An ncurses console audio player with support for the mp3, ogg, and wave formats"
arch=('i686' 'x86_64')
url="http://moc.daper.net/"
license=('GPL')
depends=('libmad' 'libid3tag' 'jack' 'curl' 'libsamplerate' 'libtool')
makedepends=('speex' 'ffmpeg' 'taglib' 'libmpcdec' 'wavpack' 'libmodplug')
optdepends=('speex: for using the speex plugin' 
	    'ffmpeg: for using the ffmpeg plugin'
	    'taglib: for using the musepack plugin' 
	    'libmpcdec: for using the musepack plugin' 
            'wavpack: for using the wavpack plugin' 
	    'libmodplug: for using the modplug plugin')
options=('!libtool' 'force')
source=(ftp://ftp.daper.net/pub/soft/moc/stable/${pkgname}-${pkgver}.tar.bz2)
md5sums=('647c770a5542a4ae5437386807a89796')
sha1sums=('e56ee13aa17c177f0afc42efe3804ebbbf46d4db')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}

	sed -i 's|ffmpeg/avformat.h|libavformat/avformat.h|' decoder_plugins/ffmpeg/ffmpeg.c || return 1
	# Disabling aac to use the external ffmpeg to play them (FS#13164)
	./configure --prefix=/usr --without-rcc --without-aac \
	    --with-oss  --with-alsa  --with-jack --with-mp3 \
	    --with-musepack --with-vorbis --with-flac --with-wavpack \
	    --with-sndfile --with-modplug --with-ffmpeg --with-speex \
	    --with-samplerate --with-curl || return 1
	make || return 1
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}

	make DESTDIR=${pkgdir} install || return 1
}
