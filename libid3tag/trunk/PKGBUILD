# $Id$
# Maintainer: dorphell <dorphell@archlinux.org>

pkgname=libid3tag
pkgver=0.15.1b
pkgrel=5
pkgdesc="library for id3 tagging"
arch=('i686' 'x86_64')
url="http://www.underbit.com/products/mad/"
license=('GPL')
depends=('zlib')
makedepends=('gperf')
options=(!libtool)
source=(ftp://ftp.mars.org/pub/mpeg/${pkgname}-${pkgver}.tar.gz \
        id3tag.pc 10_utf16.diff 11_unknown_encoding.diff)
md5sums=('e5808ad997ba32c498803822078748c3' '8bb41fd814fafcc37ec8bc88f5545a4a'\
         '4f9df4011e6a8c23240fff5de2d05f6e' '3ca856b97924d48a0fdfeff0bd83ce7d')
sha1sums=('4d867e8a8436e73cd7762fe0e85958e35f1e4306' '27753135177029bd443be188307c2934dd3caa13'\
         '40fbb2e790adfa98703d1a33bbd73f4e1da3d566' 'ae4dc32185ee2257e4269ca482d94b0cd5440b7e')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 < ../10_utf16.diff
  patch -p1 < ../11_unknown_encoding.diff
  ./configure --prefix=/usr
  make
}

package() {
 cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -D -m644 "${srcdir}/id3tag.pc" "${pkgdir}/usr/lib/pkgconfig/id3tag.pc"
}
