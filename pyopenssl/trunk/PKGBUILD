# $Id$
# Maintainer : Ionut Biru<ibiru@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgbase=pyopenssl
pkgname=('pyopenssl' 'python2-pyopenssl')
pkgver=0.13
pkgrel=1
arch=('i686' 'x86_64')
url="http://pypi.python.org/pypi/pyOpenSSL"
license=('LGPL2.1')
makedepends=('python' 'python2' 'openssl')
source=(http://pypi.python.org/packages/source/p/pyOpenSSL/pyOpenSSL-${pkgver}.tar.gz)
md5sums=('767bca18a71178ca353dff9e10941929')

build() {
    cd "${srcdir}"
    cp -a "pyOpenSSL-${pkgver}"{,-python2}

    #build python 3 module
    cd "pyOpenSSL-${pkgver}"
    python setup.py build

    #build python 2 module
    cd ../"pyOpenSSL-${pkgver}-python2"
    python2 setup.py build
}

package_pyopenssl(){
    pkgdesc="Python3 wrapper module around the OpenSSL library"
    depends=('python>=3.2' 'openssl')

    cd "${srcdir}/pyOpenSSL-${pkgver}"
    python setup.py install --root "${pkgdir}" --optimize=1
}

package_python2-pyopenssl(){
    pkgdesc="Python2 wrapper module around the OpenSSL library"
    depends=('python2>=2.7' 'openssl')

    cd "${srcdir}/pyOpenSSL-${pkgver}-python2"
    python2 setup.py install --root "${pkgdir}" --optimize=1
}
