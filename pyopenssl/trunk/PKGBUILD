# $Id$
# Maintainer : Felix Yan <felixonmars@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgbase=pyopenssl
pkgname=('python-pyopenssl' 'python2-pyopenssl')
pkgver=16.2.0
pkgrel=2
arch=('any')
url="http://pypi.python.org/pypi/pyOpenSSL"
license=('LGPL2.1')
makedepends=('openssl' 'python-setuptools' 'python2-setuptools' 'python-cryptography' 'python2-cryptography')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
source=("https://pypi.io/packages/source/p/pyOpenSSL/pyOpenSSL-$pkgver.tar.gz")
md5sums=('6635503758c65ea6f70d18d1b18e46d5')

prepare() {
  cp -a pyOpenSSL-$pkgver{,-py2}

  export LC_CTYPE=en_US.UTF-8
}

build () {
  cd "$srcdir"/pyOpenSSL-$pkgver
  python setup.py build

  cd "$srcdir"/pyOpenSSL-$pkgver-py2
  python2 setup.py build
}

check () {
  (
    cd "$srcdir"/pyOpenSSL-$pkgver
    python setup.py ptr

    cd "$srcdir"/pyOpenSSL-$pkgver-py2
    python2 setup.py ptr
  ) || warning "Tests failed due to missing SSLv3_METHOD"
}

package_python-pyopenssl(){
  pkgdesc="Python3 wrapper module around the OpenSSL library"
  depends=('openssl' 'python-six' 'python-cryptography')
  replaces=('pyopenssl')
  provides=('pyopenssl')
  conflicts=('pyopenssl')

  cd "$srcdir"/pyOpenSSL-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-pyopenssl(){
  pkgdesc="Python2 wrapper module around the OpenSSL library"
  depends=('openssl' 'python2-six' 'python2-cryptography')

  cd "$srcdir"/pyOpenSSL-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
