# $Id$
# Maintainer : Ionut Biru<ibiru@archlinux.org>
# Maintainer : Felix Yan <felixonmars@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgbase=pyopenssl
pkgname=('python-pyopenssl' 'python2-pyopenssl')
pkgver=0.15.1
pkgrel=3
arch=('any')
url="http://pypi.python.org/pypi/pyOpenSSL"
license=('LGPL2.1')
makedepends=('python-setuptools' 'python2-setuptools' 'openssl' 'python-six' 'python2-six' 'python-cryptography' 'python2-cryptography')
checkdepends=('python-pytest' 'python2-pytest')
source=(http://pypi.python.org/packages/source/p/pyOpenSSL/pyOpenSSL-$pkgver.tar.gz
        new-cryptography.patch)
md5sums=('f447644afcbd5f0a1f47350fec63a4c6'
         '100c9ebada71e6d88d7da3941f498bd2')

prepare() {
  (cd pyOpenSSL-$pkgver; patch -p2 -i ../new-cryptography.patch)
  cp -a pyOpenSSL-$pkgver{,-py2}
}

build () {
  cd "$srcdir"/pyOpenSSL-$pkgver
  python setup.py build

  cd "$srcdir"/pyOpenSSL-$pkgver-py2
  python2 setup.py build
}

check () {
  cd "$srcdir"/pyOpenSSL-$pkgver
  LC_CTYPE=en_US.utf8 python setup.py test || warning "Python 3 tests failed"

  cd "$srcdir"/pyOpenSSL-$pkgver-py2
  LC_CTYPE=en_US.utf8 python2 setup.py test || warning "Python 2 tests failed"
}

package_python-pyopenssl(){
  pkgdesc="Python3 wrapper module around the OpenSSL library"
  depends=('python>=3.4' 'openssl' 'python-six' 'python-cryptography')
  replaces=('pyopenssl')
  provides=('pyopenssl')
  conflicts=('pyopenssl')

  cd "$srcdir"/pyOpenSSL-$pkgver
  python setup.py install --root "$pkgdir" --optimize=1
}

package_python2-pyopenssl(){
  pkgdesc="Python2 wrapper module around the OpenSSL library"
  depends=('python2>=2.7' 'openssl' 'python2-six' 'python2-cryptography')

  cd "$srcdir"/pyOpenSSL-$pkgver-py2
  python2 setup.py install --root "$pkgdir" --optimize=1
}
