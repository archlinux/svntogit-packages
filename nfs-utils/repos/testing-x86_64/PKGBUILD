# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>
# Contributor: dibblethewrecker <dibblethewrecker.at.jiwe.org>
# Contributor: abelstr <abel@pinklf.eu>
# Contributor: Marco Lima <cipparello gmail com>

pkgname=nfs-utils
pkgver=1.3.1
pkgrel=1
pkgdesc="Support programs for Network File Systems"
arch=('i686' 'x86_64')
url='http://nfs.sourceforge.net'
license=('GPL')
backup=(etc/{exports,idmapd.conf,nfsmount.conf} etc/conf.d/{nfs-common.conf,nfs-server.conf})
depends=('glibc' 'e2fsprogs' 'rpcbind' 'libtirpc>=0.2.1' 'librpcsecgss>=0.19-2' 'nfsidmap' 'libevent>=2.0.10' 'device-mapper')
makedepends=('pkgconfig' 'autoconf' 'automake' 'sqlite')
source=(http://downloads.sourceforge.net/project/nfs/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.bz2
	nfs-common.conf
	nfs-server.conf
	exports
	idmapd.conf
	nfs-utils-1.1.4-mtab-sym.patch
	nfs-utils.conf
        id_resolver.conf
        nfs-utils_env.sh)
install=nfs-utils.install
optdepends=('sqlite: for nfsdcltrack usage')
md5sums=('97f157f954edb6d4d4385a0c5986f36f'
         '21510b0b30e71c8d6679c7c23f3b78eb'
         'c1fe8dd9581c1df788d8965f4f1479f3'
         'e6ad3c7a59c7e4c24965a0e7da35026c'
         'eb4f4027fab6fc1201f1ca04f5954c76'
         '7674106eaaa4c149bccd4f05fe3604e9'
         '8ac484023d786766d287ccbe878ae4ba'
         'a43aabf0b8d02406b1babc3a206d952a'
         'c6c9c32f153dfa6c0477ea6b99354288')

prepare() {
  cd $srcdir/${pkgname}-${pkgver}
  patch -Np1 -i ../nfs-utils-1.1.4-mtab-sym.patch
  ./autogen.sh
}

build() {
  cd $srcdir/${pkgname}-${pkgver}
  ./configure --prefix=/usr --sbindir=/usr/bin --enable-nfsv4 --enable-nfsv41 --enable-gss \
              --without-tcp-wrappers --with-statedir=/var/lib/nfs \
              --enable-ipv6 --sysconfdir=/etc --enable-libmount-mount \
              --enable-mountconfig --with-start-statd=/usr/bin/start-statd
  # move mount helpers to /usr/bin
  sed -i -e 's#sbindir = /sbin#sbindir = /usr/bin#g' utils/mount/Makefile
  # move osd_login to /usr/bin
  sed -i -e 's#sbindir = /sbin#sbindir = /usr/bin#g' utils/osd_login/Makefile
  # move nfsdcltrack to /usr/bin
  sed -i -e 's#sbindir = /sbin#sbindir = /usr/bin#g' utils/nfsdcltrack/Makefile
  make 
}

package() {
  cd $srcdir/${pkgname}-${pkgver}

  make DESTDIR="$pkgdir" install
  sed -i '1s/python$/python2/' "$pkgdir"/usr/bin/{nfsiostat,mountstats}
  install -D -m 644 utils/mount/nfsmount.conf "$pkgdir"/etc/nfsmount.conf
  
  for i in systemd/{*.service,*.mount,*.target}; do
    install -D -m 644 $i "$pkgdir"/usr/lib/systemd/system/$(basename $i)
  done

  cd ..
  install -D -m 644 nfs-common.conf  "$pkgdir"/etc/conf.d/nfs-common.conf
  install -D -m 644 nfs-server.conf  "$pkgdir"/etc/conf.d/nfs-server.conf
  install -D -m 755 nfs-utils_env.sh "$pkgdir"/usr/lib/systemd/scripts/nfs-utils_env.sh
  install -D -m 644 exports          "$pkgdir"/etc/exports
  install -D -m 644 idmapd.conf      "$pkgdir"/etc/idmapd.conf
  install -D -m 644 id_resolver.conf "$pkgdir"/etc/request-key.d/id_resolver.conf
  install -D -m 644 nfs-utils.conf   "$pkgdir"/usr/lib/modules-load.d/nfs-utils.conf
  mkdir "$pkgdir"/etc/exports.d
  mkdir -m 555 "$pkgdir"/var/lib/nfs/rpc_pipefs
  mkdir "$pkgdir"/var/lib/nfs/v4recovery
}
