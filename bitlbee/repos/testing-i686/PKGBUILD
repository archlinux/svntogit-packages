# $Id$

# Contributor: FUBAR <mrfubar@gmail.com>
# Contributor: simo <simo@archlinux.org>
# Contributor: Jeff 'codemac' Mickey <jeff@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Maintainer: Dave Reisner <dreisner@archlinux.org>

pkgname=bitlbee
pkgver=3.0.5
pkgrel=4
pkgdesc='Brings instant messaging (XMPP, MSN, Yahoo!, AIM, ICQ, Twitter) to IRC'
url='http://www.bitlbee.org/'
license=('GPL')
arch=('i686' 'x86_64')
depends=('openssl' 'glib2')
makedepends=('asciidoc' 'libotr')
optdepends=('skype4py: to use skyped'
            'libotr: for OTR encryption support'
            'xinetd: to run bitlbee through xinetd')
source=("http://get.bitlbee.org/src/${pkgname}-${pkgver}.tar.gz"
        'sbindir.patch'
        'xinetd'
        'rc.d'
        'bitlbee.tmpfiles')
sha1sums=('74afdff87be49ce060771a6ae10d7643cd57b9b6'
          '9b774e6d85471a4e43e25b8b941cd0e1032a7a6c'
          '5e0af27ba9cc4fe455e3381c75fc49a9326e2f17'
          '02423c500ac64f673ba26a3395c7c7036c17343e'
          '3695ed2fe22436c4d0fc3ead829f7d1f89bc491c')
backup=('etc/bitlbee/bitlbee.conf'
        'etc/bitlbee/motd.txt'
        'etc/xinetd.d/bitlbee')
install=bitlbee.install

build() { 
	cd "$pkgname-$pkgver"

	patch -p1 -i ../sbindir.patch

	./configure \
		--prefix=/usr \
		--etcdir=/etc/bitlbee \
		--pidfile=/run/bitlbee/bitlbee.pid \
		--ipcsocket=/run/bitlbee/bitlbee.sock \
		--systemdsystemunitdir=/usr/lib/systemd/system \
		--ssl=openssl \
		--strip=0 \
		--otr=plugin \
		--skype=plugin

	make
}

package() {
	make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install{,-etc,-dev}

	install -o65 -g65 -dm770 "$pkgdir/var/lib/bitlbee"
	install -Dm644 "$srcdir/xinetd" "$pkgdir/etc/xinetd.d/bitlbee"
	install -Dm755 "$srcdir/rc.d" "$pkgdir/etc/rc.d/bitlbee"
	install -Dm644 "$srcdir/bitlbee.tmpfiles" "$pkgdir/usr/lib/tmpfiles.d/bitlbee.conf"
}
