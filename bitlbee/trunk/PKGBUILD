# $Id$
# Contributor: FUBAR <mrfubar@gmail.com>
# Contributor: simo <simo@archlinux.org>
# Contributor: Jeff 'codemac' Mickey <jeff@archlinux.org>

pkgname=bitlbee
pkgver=1.2.4
pkgrel=1
pkgdesc="An IRC to IM gateway. Supports MSN, ICQ, Jabber, Yahoo!, and AIM"
arch=('i686' 'x86_64')
url="http://www.bitlbee.org/" 
license=('GPL')
depends=('gnutls' 'glib2')
optdepends=('xinetd: to run bitlbee through xinetd')
install=bitlbee.install
backup=(etc/bitlbee/bitlbee.conf etc/bitlbee/motd.txt etc/xinetd.d/bitlbee)
source=(http://get.bitlbee.org/src/$pkgname-$pkgver.tar.gz
	'bitlbee.xinetd'
	'bitlbee.rcd')
md5sums=('26b9bfc5446bdf0e932e8074d117be67'
         '81e576369e31377f5217e636b00874b1'
         '90893b706e0e50d37cfe6b283bc7d46c')

build() { 
  cd $srcdir/$pkgname-$pkgver
  ./configure --prefix=/usr \
    --ssl=gnutls \
    --etcdir=/etc/bitlbee \
    --pidfile=/var/run/bitlbee/bitlbee.pid \
    --ipcsocket=/var/run/bitlbee/bitlbee.sock || return 1
  make || return 1

  install -m755 -d $pkgdir/var/lib
  install -o65 -g65 -d -m0770 $pkgdir/var/lib/bitlbee
  install -m755 -d $pkgdir/var/run
  install -o65 -g65 -d -m755 $pkgdir/var/run/bitlbee
  install -D -m644 $srcdir/bitlbee.xinetd \
    $pkgdir/etc/xinetd.d/bitlbee
  install -D -m755 $srcdir/bitlbee.rcd \
    $pkgdir/etc/rc.d/bitlbee || return 1
  make DESTDIR=$pkgdir install || return 1
  make DESTDIR=$pkgdir install-etc || return 1
  make DESTDIR=$pkgdir install-dev || return 1
}
