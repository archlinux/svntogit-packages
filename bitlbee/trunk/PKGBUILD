# Maintainer: Dave Reisner <dreisner@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Jeff 'codemac' Mickey <jeff@archlinux.org>
# Contributor: simo <simo@archlinux.org>
# Contributor: FUBAR <mrfubar@gmail.com>

pkgname=bitlbee
pkgver=3.5.1
pkgrel=2
pkgdesc='Brings instant messaging (XMPP, MSN, Yahoo!, AIM, ICQ, Twitter) to IRC'
url='http://www.bitlbee.org/'
license=('GPL')
arch=('x86_64')
depends=('gnutls' 'glib2')
makedepends=('python' 'libotr')
optdepends=('libotr: for OTR encryption support')
source=("https://get.bitlbee.org/src/$pkgname-$pkgver.tar.gz"
        'bitlbee.tmpfiles')
sha1sums=('de0767facdb7729253ae4d6ef6e3637ebd54939a'
          '3695ed2fe22436c4d0fc3ead829f7d1f89bc491c')
backup=('etc/bitlbee/bitlbee.conf'
        'etc/bitlbee/motd.txt')
install=bitlbee.install

build() {
  cd "$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --etcdir=/etc/bitlbee \
    --sbindir=/usr/bin \
    --pidfile=/run/bitlbee/bitlbee.pid \
    --ipcsocket=/run/bitlbee/bitlbee.sock \
    --systemdsystemunitdir=/usr/lib/systemd/system \
    --ssl=gnutls \
    --strip=0 \
    --otr=plugin

  make
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install{,-etc,-dev,-systemd}

  install -o65 -g65 -dm770 "$pkgdir/var/lib/bitlbee"
  install -Dm644 "$srcdir/bitlbee.tmpfiles" "$pkgdir/usr/lib/tmpfiles.d/bitlbee.conf"
}
