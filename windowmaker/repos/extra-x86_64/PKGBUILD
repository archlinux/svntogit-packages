# $Id$
# Maintainer: Daniel Isenmann <daniel@archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=windowmaker
pkgver=0.95.5
pkgrel=2
pkgdesc="An X11 window manager with a NEXTSTEP look and feel"
arch=('i686' 'x86_64')
url="http://www.windowmaker.org/"
license=('GPL' 'custom')
depends=('libxinerama' 'libxrandr' 'libxmu' 'libpng' 'libxpm' 'libxft' 'libtiff' 'giflib')
source=(http://windowmaker.org/pub/source/release/WindowMaker-$pkgver.tar.gz
        wmaker.desktop
        wm-giflib.patch)
md5sums=('19e9cc256ccb7075ff82f4b809df3a0a'
         '2fba97bebfd691836b92b8f0db79ff13'
         '05d8e98fb579f2fa5a097b629374e6b8')

prepare() {
  cd WindowMaker-$pkgver
  patch -Np0 -i ../wm-giflib.patch
  autoreconf -fi
}

build() {
  cd WindowMaker-$pkgver
  [ -z "$LINGUAS" ] && export LINGUAS="`ls po/*.po | sed 's:po/\(.*\)\.po$:\1:'`"
  ./configure --prefix=/usr --sysconfdir=/etc --enable-xinerama \
    --with-nlsdir=/usr/share/locale --with-gnustepdir=/usr/lib/GNUstep \
    --enable-usermenu --enable-modelock --enable-xrandr
  make
}

package() {
  cd WindowMaker-$pkgver
  
  make DESTDIR="$pkgdir" install
  install -D -m644 COPYING.WTFPL "$pkgdir/usr/share/licenses/$pkgname/COPYING.WTFPL"
  install -D -m644 ../wmaker.desktop "$pkgdir/usr/share/xsessions/wmaker.desktop"
}

