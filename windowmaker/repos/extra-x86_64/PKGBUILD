# $Id$
# Maintainer: Daniel Isenmann <daniel@archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=windowmaker
pkgver=0.95.5
pkgrel=4
pkgdesc="An X11 window manager with a NEXTSTEP look and feel"
arch=('i686' 'x86_64')
url="http://www.windowmaker.org/"
license=('GPL' 'custom')
depends=('libxinerama' 'libxrandr' 'libxmu' 'libpng' 'libxpm' 'libxft' 'libtiff' 'giflib')
source=(http://windowmaker.org/pub/source/release/WindowMaker-$pkgver.tar.gz
        wmaker.desktop
        wm-giflib.patch
        giflib_510.diff)
md5sums=('19e9cc256ccb7075ff82f4b809df3a0a'
         '2fba97bebfd691836b92b8f0db79ff13'
         '05d8e98fb579f2fa5a097b629374e6b8'
         '9eee694ecec55a5185e421bac9170344')

prepare() {
  cd WindowMaker-$pkgver
  #patch -Np0 -i ../wm-giflib.patch
  
  # patch taken from NetBSD
  patch -Np0 -i ../giflib_510.diff
  
  autoreconf -fi
  
  # fix some paths FS#3080 - ckeck also Gentoo ebuild
  for file in WindowMaker/*menu* util/wmgenmenu.c; do
 	if [[ -r $file ]] ; then
 	sed -i -e "s:/usr/local/GNUstep/Applications/WPrefs.app:/usr/lib/GNUstep/Applications/WPrefs.app:g;" "$file"
 	sed -i -e "s:/usr/local/share/WindowMaker:/usr/share/WindowMaker:g;" "$file"
 	sed -i -e "s:/opt/share/WindowMaker:/usr/share/WindowMaker:g;" "$file"
 	fi;
  done;
}

build() {
  cd WindowMaker-$pkgver
  [ -z "$LINGUAS" ] && export LINGUAS="`ls po/*.po | sed 's:po/\(.*\)\.po$:\1:'`"
  ./configure --prefix=/usr --sysconfdir=/etc --enable-xinerama \
    --with-nlsdir=/usr/share/locale --with-gnustepdir=/usr/lib/GNUstep \
    --enable-usermenu --enable-modelock --enable-xrandr
  make
}

package() {
  cd WindowMaker-$pkgver
  
  make DESTDIR="$pkgdir" install
  install -D -m644 COPYING.WTFPL "$pkgdir/usr/share/licenses/$pkgname/COPYING.WTFPL"
  install -D -m644 ../wmaker.desktop "$pkgdir/usr/share/xsessions/wmaker.desktop"
}

