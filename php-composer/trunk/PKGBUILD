# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=php-composer
_tag=1.0.0-alpha11
pkgver=${_tag/-/.}
pkgrel=1
pkgdesc="Dependency Manager for PHP"
arch=('any')
license=('MIT')
url='https://getcomposer.org/'
depends=('php')
makedepends=('git')
source=("${pkgname}::git+https://github.com/composer/composer.git#tag=${_tag}"
        "composer-${pkgver}.phar::https://getcomposer.org/download/${_tag}/composer.phar"
        'php' 'php.ini')
md5sums=('SKIP'
         '488ca7972e447e0e1b2988f66d8e01e6'
         '407acec5e2be64bc18c95fadb2ad86d4'
         '5cc154807e3a7a990657c1c4bcc80149')

prepare() {
	cd ${srcdir}/${pkgname}

	# Use a php wrapper which is configured with the required modules
	export PATH=${srcdir}:$PATH

	php ${srcdir}/composer-${pkgver}.phar -n install --no-dev
}

build() {
	cd ${srcdir}/${pkgname}

	bin/compile
}

check() {
	cd ${srcdir}/${pkgname}

	# Install the dev dependencies to run the unit tests
	bin/composer -n install
	vendor/bin/phpunit
}

package() {
	cd ${srcdir}/${pkgname}

	install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
	install -Dm755 composer.phar ${pkgdir}/usr/bin/composer
}
