# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=php-composer
pkgver=1.0.0.alpha9.179
pkgrel=1
pkgdesc="Dependency Manager for PHP"
arch=('any')
license=('MIT')
url='http://getcomposer.org/'
depends=('php' 'php-intl')
makedepends=('git')
checkdepends=('mercurial')
source=("${pkgname}::git+https://github.com/composer/composer.git"
        "composer-${pkgver}.phar::https://getcomposer.org/composer.phar"
        'php.ini')
md5sums=('SKIP'
         '8fb86af4e9d4cb0f7f38e7ba232b6acb'
         'b31326c352763328dca0a567a67609ab')

pkgver() {
	cd ${srcdir}/${pkgname}

	local _tag=$(git tag -l --sort -version:refname | head -1)
	echo "${_tag}.$(git rev-list ${_tag}.. --count)" | sed 's/-/./g'
}

prepare() {
	cd ${srcdir}/${pkgname}

	sed "s~^#!/usr/bin/env php~#!/usr/bin/php -nc/usr/share/${pkgname}/php.ini~g" -i src/Composer/Compiler.php
	/usr/bin/php -nc ${srcdir}/php.ini ${srcdir}/composer-${pkgver}.phar -on install --prefer-dist
}

check() {
	cd ${srcdir}/${pkgname}

	git config --global user.email "${USER}@${HOST}"
	git config --global user.name "${USER}"
	/usr/bin/php -nc ${srcdir}/php.ini vendor/bin/phpunit
}

build() {
	cd ${srcdir}/${pkgname}

	/usr/bin/php -nc ${srcdir}/php.ini bin/compile
}

package() {
	cd ${srcdir}/${pkgname}

	install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
	install -Dm644 ${srcdir}/php.ini ${pkgdir}/usr/share/php-composer/php.ini
	install -Dm755 composer.phar ${pkgdir}/usr/bin/composer
}
