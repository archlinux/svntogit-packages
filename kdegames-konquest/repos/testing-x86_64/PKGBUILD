# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-konquest
pkgver=15.12.2
pkgrel=1
pkgdesc="The KDE version of Gnu-Lactic"
url="http://kde.org/applications/games/konquest/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegames')
depends=('kdebase-runtime' 'libkdegames4')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/applications/${pkgver}/src/konquest-${pkgver}.tar.xz")
sha1sums=('9b2038f2797826d9d2845536595e886c9ecdb445')

build() {
  mkdir -p build
  cd build
  cmake ../konquest-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
