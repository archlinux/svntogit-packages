# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-konquest
pkgver=15.03.97
pkgrel=1
pkgdesc="The KDE version of Gnu-Lactic"
url="http://kde.org/applications/games/konquest/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegames')
depends=('kdebase-runtime' 'libkdegames4')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/unstable/applications/${pkgver}/src/konquest-${pkgver}.tar.xz")
sha1sums=('63a3160d074bd2956d9cf2dcc88bfcf957afa4e4')

build() {
  mkdir build
  cd build
  cmake ../konquest-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
