# $Id$
# Maintainer: 

pkgname=i8kutils
pkgver=1.41
pkgrel=1
pkgdesc="Dell Inspiron/Latitude kernel driver and utilities"
arch=('i686' 'x86_64')
url="https://launchpad.net/i8kutils"
license=('GPL')
depends=('bash' 'acpi')
optdepends=('tk: for i8kmon deamon')
backup=('etc/i8kutils/i8kmon.conf')
DLAGENTS=('https::/usr/bin/curl -fLC - --retry 3 --retry-delay 3 --insecure -o %o %u')
source=(https://launchpad.net/i8kutils/trunk/${pkgver}/+download/i8kutils_${pkgver}.tar.gz{,.asc}
        i8kmon.service)
sha1sums=('7baf9ed1b27f4bcab7a9a5c7c037d36c769945cc'
          'SKIP'
          '5c20f5241de7e60b46117269f4e867b08734cdc8')

prepare() {
  cd $pkgname
  # conform to Arch Linux guidelines
  sed -i 's|/etc/i8kmon|/etc/i8kutils/i8kmon.conf|g' i8kmon.1
  sed -i 's|/etc/i8kmon.conf|/etc/i8kutils/i8kmon.conf|g' i8kmon
  make clean
}

build() {
  cd $pkgname
  make
}

package() {
  cd $pkgname
  install -d "$pkgdir"/usr/{bin,share/man/man1}
  install -D -m644 i8kctl.1 i8kmon.1 "$pkgdir/usr/share/man/man1"
  install -D -m755 i8kctl i8kfan i8kmon "$pkgdir/usr/bin"

  install -D -m644 i8kmon.conf "$pkgdir/etc/i8kutils/i8kmon.conf"
  install -D -m644 ../i8kmon.service "$pkgdir/usr/lib/systemd/system/i8kmon.service"
}
