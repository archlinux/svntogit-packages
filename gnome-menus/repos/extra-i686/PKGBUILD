# $Id$
# Maintainer: Jan de Groot <jan@archlinux.org>

pkgname=gnome-menus
pkgver=3.10.1
pkgrel=2
pkgdesc="GNOME menu specifications"
arch=('i686' 'x86_64')
depends=('glib2')
makedepends=('intltool' 'gobject-introspection')
license=('GPL' 'LGPL')
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:4}/$pkgname-$pkgver.tar.xz
        git-fixes.patch)
sha256sums=('46950aba274c1ad58234374fa9b235258650737307f3bc396af48eb983668a71'
            'b738c2ea37784e531a2f4517d6bb5f2d1a00e488c89a5bf0dc73fdbca70374c1')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../git-fixes.patch
}

build() {
  cd "$pkgname-$pkgver"
  ./configure \
      --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var  --disable-static
  make
}

package(){
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
