# $Id$
# Maintainer: Jan de Groot <jan@archlinux.org>

pkgname=gnome-menus
pkgver=2.28.0.1
pkgrel=1
pkgdesc="GNOME menu specifications"
arch=(i686 x86_64)
depends=('pygtk>=2.16.0')
makedepends=('pkgconfig' 'intltool')
options=('!libtool' '!makeflags')
license=('GPL' 'LGPL')
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.28/${pkgname}-${pkgver}.tar.bz2
        menus.patch)
sha256sums=('75e73554cd0b0a8356956e76ccb517def5382eafa7bdb14336499cd271570dbc'
            'df2779777906b038fa911dc745534564ede9524a64ab368e2f6f8e38d54e7acc')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np0 -i "${srcdir}/menus.patch" || return 1
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var \
      --disable-static || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
}
