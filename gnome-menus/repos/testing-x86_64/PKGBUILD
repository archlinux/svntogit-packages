# $Id$
# Maintainer: Jan de Groot <jan@archlinux.org>

pkgname=gnome-menus
pkgver=3.8.0
pkgrel=2
pkgdesc="GNOME menu specifications"
arch=('i686' 'x86_64')
depends=('glib2')
makedepends=('intltool' 'gobject-introspection')
options=('!libtool')
license=('GPL' 'LGPL')
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz
        calculator.patch)
sha256sums=('33c215ae8d815f4a94d2dbfc8c6679fddbbf8026d30da55a350e67ea940ce14f'
            'e2b17562cd48794075b6bff98cec1fc8ff817728bcea40834144c699cd8680c8')

build() {
  cd "$pkgname-$pkgver"
  patch -Np1 -i ../calculator.patch
  ./configure \
      --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var  --disable-static
  make
}

package(){
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
