# $Id
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kactivities-frameworks
pkgver=r585.91b627a
pkgrel=1
pkgdesc="Core components for the KDE's Activities"
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kde/kdelibs/kactivities'
license=('LGPL')
depends=('qt5-declarative' 'boost-libs' 'kf5umbrella' 'kdbusaddons' 'kwindowsystem' 'kservice')
makedepends=('extra-cmake-modules' 'git' 'boost')
source=('git://anongit.kde.org/kactivities.git#branch=frameworks')
md5sums=('SKIP')

pkgver() {
  cd kactivities
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kactivities \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/kf5 \
    -DLIB_INSTALL_DIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
