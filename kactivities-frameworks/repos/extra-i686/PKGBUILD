# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kactivities-frameworks
pkgver=4.100.0
pkgrel=2
arch=('i686' 'x86_64')
pkgdesc="The KActivity Manager Daemon"
url='https://projects.kde.org/projects/kde/kdelibs/kactivities'
license=('LGPL')
depends=('kio')
makedepends=('extra-cmake-modules' 'boost')
provides=('kactivities')
conflicts=('kactivities4' 'libkactivities-frameworks')
replaces=('libkactivities-frameworks')
groups=('kf5')
source=("http://download.kde.org/unstable/frameworks/${pkgver}/kactivities-${pkgver}.tar.xz")
md5sums=('45e20c18096f272ce665a120aa32ac83')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../kactivities-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DQT_PLUGIN_INSTALL_DIR=lib/qt/plugins \
    -DQML_INSTALL_DIR=lib/qt/qml \
    -DECM_MKSPECS_INSTALL_DIR=/usr/share/qt/mkspecs/modules \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
