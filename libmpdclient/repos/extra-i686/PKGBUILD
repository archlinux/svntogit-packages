# $Id$
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=libmpdclient
pkgver=2.6
pkgrel=1
pkgdesc='Asynchronous API library for interfacing MPD in the C, C++ & Objective C languages'
url='http://mpd.wikia.com/wiki/ClientLib:libmpdclient'
license=('custom:BSD')
arch=('i686' 'x86_64')
options=('!libtool')
source=("http://downloads.sourceforge.net/project/musicpd/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha1sums=('39bdcf5399fb98e34b8b6110057b64f4d1945333')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr --disable-static
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	cd "${pkgdir}"
	install -d usr/share/licenses/libmpdclient
	ln -s ../../doc/libmpdclient/COPYING usr/share/licenses/libmpdclient/COPYING
}
