# Maintainer: Andreas Radke <andyrtr@archlinux.org>
#Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libdmx
pkgver=1.1.4
pkgrel=1
pkgdesc="X11 Distributed Multihead extension library"
arch=('x86_64')
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('dmxproto' 'libxext')
makedepends=('xorg-util-macros')
source=("${url}/releases/individual/lib/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('77aba6cadfd17db0cc7e1bc26f14d908223a0fdb135d46f86c3be34e3bc7e2f843f08d4d0ef773357e83b17122bffe56a0442d12908378c05a22177b10ae10e9')

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
