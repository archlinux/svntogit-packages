# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>

pkgname=libexttextcat
pkgver=3.4.5
pkgrel=1
pkgdesc="N-Gram-Based Text Categorization library primarily intended for language guessing"
arch=('x86_64')
url="http://www.freedesktop.org/wiki/Software/libexttextcat/"
license=('custom')
depends=('glibc')
provides=('libtextcat')
changelog=$pkgname.changelog
source=(https://dev-www.libreoffice.org/src/$pkgname/$pkgname-$pkgver.tar.xz)
sha256sums=('13fdbc9d4c489a4d0519e51933a1aa21fe3fb9eb7da191b87f7a63e82797dac8')

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make DESTDIR="${pkgdir}" install
  
  install -D -m644 LICENSE "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
}
