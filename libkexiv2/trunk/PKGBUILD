# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=libkexiv2
pkgver=16.08.2
pkgrel=1
pkgdesc="A library to manipulate pictures metadata"
url="https://www.kde.org/applications/graphics/"
arch=(i686 x86_64)
license=(GPL LGPL FDL)
depends=(qt5-base exiv2)
makedepends=(extra-cmake-modules)
source=("http://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
sha1sums=('4c1ca0a7f2c91e83588e34471009aaa42d3e4d18')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
