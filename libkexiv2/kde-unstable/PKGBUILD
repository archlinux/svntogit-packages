# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=libkexiv2
pkgver=4.6.80
pkgrel=1
pkgdesc="A library to manipulate pictures metadata"
url="http://kde.org/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdelibs')
makedepends=('cmake' 'automoc4')
conflicts=('kdegraphics-libs')
source=("http://download.kde.org/unstable/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2")
sha1sums=('cff979e2bcc604ec422feff0bc1fee3483955e5d')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
