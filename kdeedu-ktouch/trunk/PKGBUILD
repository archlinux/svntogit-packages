# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeedu-ktouch
pkgver=15.08.1
pkgrel=1
pkgdesc="Touch Typing Tutor"
url="http://kde.org/applications/education/ktouch/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdeedu')
depends=('kdebase-runtime' 'kqtquickcharts' 'libxkbfile')
makedepends=('cmake' 'automoc4' 'mesa')
install=${pkgname}.install
source=("http://download.kde.org/stable/applications/${pkgver}/src/ktouch-${pkgver}.tar.xz")
sha1sums=('028df3bf5079885241f69bcd3af42e6091197861')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../ktouch-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
