# Maintainer: Firmicus <firmicus ατ gmx δοτ net>

pkgname=texlive-core
pkgver=2008.13013
pkgrel=1
pkgdesc="TeX Live core distribution"
license=('GPL')
arch=('i686' 'x86_64')
depends=('texlive-bin')
optdepends=('perl' 'ruby' 'ghostscript' 'psutils')
makedepends=('lzma-utils')
groups=('texlive-most')
conflicts=('tetex')
provides=('tetex')
replaces=('tetex')
url='http://tug.org/texlive/'
source=("http://dev.archlinux.org/~francois/texlive/src/$pkgname-$pkgver-src.zip")
install=texlive.install
backup=(usr/share/texmf-config/tex/context/config/cont-usr.tex)
md5sums=('efc4277cf81471c868a0dd17a4b1d82f')

build() {
  cd $srcdir
   install -m755 -d $pkgdir/usr/share/texmf-var/arch/installedpkgs || return 1
   sed -i '/^#/d' CONTENTS || return 1
   _revnr=`echo $pkgver | sed 's/2008\.//'`
   install -m644 CONTENTS $pkgdir/usr/share/texmf-var/arch/installedpkgs/${pkgname}_${_revnr}.pkgs || return 1
   for p in *.tar.lzma; do 
	   lzma --force -dc $p | tar xf - || return 1
   done
   install -m755 -d $pkgdir/usr/share || return 1
   find texmf-dist -type d -exec install -d -m755 $pkgdir/usr/share/'{}' \; || return 1
   find texmf-dist -type f -exec install -m644 '{}' $pkgdir/usr/share/'{}' \; || return 1
   find $pkgdir/usr/share/texmf-dist/scripts -type f -exec chmod a+x '{}' \; || return 1
   # copy config file to texmf-config
   mkdir -p $pkgdir/usr/share/texmf-config/tex/context/config
   cp -a $pkgdir/usr/share/texmf-dist/tex/context/config/cont-usr.tex \
   	$pkgdir/usr/share/texmf-config/tex/context/config/cont-usr.tex
  
  # remove obsolete stuff (we don't provide the omega and aleph binaries anyway)
  rm -rf $pkgdir/usr/share/texmf-dist/omega
  rm -f $pkgdir/usr/share/texmf-dist/tex/plain/config/{omega,aleph}.ini
  # more cleanup
  rm -rf $pkgdir/usr/share/texmf-dist/scripts/context/stubs/mswin/
  # remove doc and sources for koma-script
  rm -rf $pkgdir/usr/share/texmf-dist/doc/
  rm -rf $pkgdir/usr/share/texmf-dist/source/
  # TEMPORARY FIX: symlinks for context
  for s in mtxrun luatools; do
  	ln -s /usr/share/texmf-dist/scripts/context/lua/$s.lua $pkgdir/usr/share/texmf-dist/scripts/context/stubs/unix/$s 
  done
}
