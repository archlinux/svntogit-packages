# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=totem
pkgver=43.0
pkgrel=2
pkgdesc="Movie player for the GNOME desktop based on GStreamer"
url="https://wiki.gnome.org/Apps/Videos"
arch=(x86_64)
license=(GPL2 custom)
depends=(totem-plparser iso-codes libpeas grilo gsettings-desktop-schemas
         python-gobject gnome-desktop gst-plugins-base gst-plugins-good
         gst-plugins-bad gst-plugin-gtk libhandy libportal-gtk3)
makedepends=(docbook-xsl gobject-introspection git appstream-glib gtk-doc meson
             yelp-tools)
checkdepends=(xorg-server-xvfb)
optdepends=('gst-plugins-ugly: Extra media codecs'
            'gst-libav: Extra media codecs'
            'grilo-plugins: Media discovery')
provides=(libtotem.so)
conflicts=(totem-plugin)
replaces=(totem-plugin)
groups=(gnome)
_commit=7bb879e1f6746ca19f99629794e918cdbbd06008  # tags/43.0^0
source=("git+https://gitlab.gnome.org/GNOME/totem.git#commit=$_commit"
        "git+https://gitlab.gnome.org/GNOME/libgd.git")
sha256sums=('SKIP'
            'SKIP')

pkgver() {
  cd totem
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd totem

  git submodule init
  git submodule set-url subprojects/libgd "$srcdir/libgd"
  git -c protocol.file.allow=always submodule update
}

build() {
  local meson_options=(
    -D enable-gtk-doc=true
  )

  arch-meson totem build "${meson_options[@]}"
  meson compile -C build
}

check() {
  dbus-run-session xvfb-run -s '-nolisten local' \
    meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 totem/COPYING
}

# vim:set sw=2 sts=-1 et:
