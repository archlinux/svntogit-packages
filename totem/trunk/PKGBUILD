# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=totem
pkgname=('totem' 'totem-plugin')
pkgver=3.1.4
pkgrel=1
pkgdesc="A GNOME3 integrated movie player based on Gstreamer."
url="http://www.gnome.org"
arch=('i686' 'x86_64')
license=('GPL2' 'custom')
depends=('gstreamer0.10-base-plugins' 'gstreamer0.10-good-plugins' 'totem-plparser' 'libxxf86vm'
         'libxtst' 'desktop-file-utils' 'iso-codes' 'python2' 'libpeas' 'hicolor-icon-theme'
         'gnome-icon-theme-symbolic' 'gsettings-desktop-schemas' 'dconf' 'clutter-gst'
         'clutter-gtk' 'mx')
makedepends=('intltool' 'gtk-doc' 'python2-gobject2' 'nautilus' 'libgdata' 'xulrunner' 'lirc-utils'
             'libepc' 'bluez' 'vala' 'grilo')
options=('!libtool' '!emptydirs')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgbase}/${pkgver%.*}/${pkgbase}-${pkgver}.tar.xz)
sha256sums=('46b9c1cf136de90ed649ca6ff636a63aa452c9724e137e983ba4e3250fa35749')

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/totem \
    --localstatedir=/var \
    --disable-static \
    --enable-python \
    --enable-nautilus
  make
}

package_totem() {
  groups=('gnome-extra')
  install=totem.install
  optdepends=('gstreamer0.10-ugly-plugins: Extra media codecs'
              'gstreamer0.10-bad-plugins: Extra media codecs'
              'gstreamer0.10-ffmpeg: Extra media codecs'
              'lirc-utils: Infrared Remote Control plugin'
              'libepc: Publish Playlist plugin'
              'libgdata: YouTube Browser plugin'
              'bluez: Bemused plugin'
              'grilo: Grilo plugin')

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  rm -r "${pkgdir}/usr/lib/mozilla"
  rm "${pkgdir}/usr/lib/totem/totem-plugin-viewer"

  sed -i "s|#!/usr/bin/python$|#!/usr/bin/python2|" \
    $pkgdir/usr/lib/totem/{totem/totem-bugreport.py,plugins/iplayer/iplayer2.py}

  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/totem/COPYING"
}

package_totem-plugin() {
  pkgdesc="Totem mozilla/firefox plugin"
  depends=("totem=${pkgver}")

  cd "${srcdir}/${pkgbase}-${pkgver}"
  make -C browser-plugin \
    plugindir=/usr/lib/mozilla/plugins \
    xptdir=/usr/lib/mozilla/plugins \
    DESTDIR="${pkgdir}" install

  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/totem-plugin/COPYING"
}
