# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=breeze-gtk
pkgver=5.6.5
pkgrel=1
pkgdesc="Breeze widget theme for GTK 2 and 3"
arch=(i686 x86_64)
url='https://quickgit.kde.org/?p=breeze-gtk.git'
license=(LGPL)
depends=(qt5-base)
makedepends=(extra-cmake-modules git)
groups=(plasma)
source=("http://download.kde.org/stable/plasma/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig} breeze-gtk-3.20-{1,2,3}.patch)
sha256sums=('7b313a31b1aac9dcbb6a0d73c76dd7eaea5c410a6eb267d6d52126c42508854c'
            'SKIP'
            '35be7e9ee64e06a2d6529b4d470a043350c0ec058426e043e75ceea88a935c25'
            '4449354b9b595f797d0883ab6166aaca57945160cc78d069edc3104e5b94c1e1'
            '3b436a84521f27ed934679bd1ce59e78f6481130a8f0bd948612ea3c9c35d3f2')
validpgpkeys=('13C16D03EDE728514473AA73A506E6D4DD4D5088') # Jonathan Riddell

prepare(){
  mkdir -p build

# Port to GTK 3.20
  cd $pkgname-$pkgver
  for _n in 1 2 3; do
   git apply -p1 < ../breeze-gtk-3.20-$_n.patch
  done
}

build(){
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DWITH_GTK3_VERSION=3.20
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
