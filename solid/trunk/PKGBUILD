# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=solid
pkgver=5.2.0
pkgrel=1
pkgdesc='Solid'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/solid'
license=('LGPL')
depends=('qt5-declarative' 'media-player-info' 'upower' 'udisks2')
makedepends=('extra-cmake-modules' 'qt5-tools')
groups=('kf5')
source=("http://download.kde.org/stable/frameworks/${pkgver}/${pkgname}-${pkgver}.tar.xz")
md5sums=('957f6ef644ef4bbe90daecf7cf1f1e72')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DQML_INSTALL_DIR=lib/qt/qml \
    -DECM_MKSPECS_INSTALL_DIR=/usr/share/qt/mkspecs/modules \
    -DLOCALE_INSTALL_DIR=/usr/share/locale/kf5 \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
