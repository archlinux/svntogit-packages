# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=breeze-icons
pkgver=5.79.0
pkgrel=1
pkgdesc='Breeze icon themes'
arch=(any)
url='https://community.kde.org/Frameworks'
license=(LGPL)
groups=(kf5)
makedepends=(extra-cmake-modules qt5-base python-lxml)
source=("https://download.kde.org/stable/frameworks/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('c6320406a8e93df3a5918e24ef94b76b3c0ddd3e17567451b7dce403f33b4d1b'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBINARY_ICONS_RESOURCE=ON \
    -DBUILD_TESTING=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
