# $Id$
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgname=run-parts
pkgver=4.2.1
pkgrel=1
pkgdesc='run scripts or programs in a directory'
arch=('i686' 'x86_64')
url='http://packages.qa.debian.org/d/debianutils.html'
license=('GPL')
depends=('glibc')
source=("ftp://ftp.archlinux.org/other/run-parts/debianutils_${pkgver}.tar.gz")
sha256sums=('6359b273bee9b959d243d1490caa8f0f5ff8dc294690b1bdb5df755d6364be0f')

build() {
	cd $srcdir/debianutils-$pkgver
	./configure --prefix=/usr
	make run-parts
}

package() {
	cd $srcdir/debianutils-$pkgver
	install -D -m755 run-parts $pkgdir/usr/bin/run-parts
	install -D -m644 run-parts.8 $pkgdir/usr/share/man/man8/run-parts.8
	for l in po4a/*/run-parts.8; do
		install -D -m644  ${l} $pkgdir/usr/share/man/${l:5:2}/man8/run-parts.8
	done
}
