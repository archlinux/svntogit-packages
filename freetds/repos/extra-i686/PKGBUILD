# $Id: PKGBUILD 11362 2010-02-09 23:57:26Z dgriffiths $
# Contributor: Aaron Malone <aaron@munge.net>

pkgname=freetds
pkgver=0.91
pkgrel=2
pkgdesc='Library for accessing Sybase and MS SQL Server databases'
url='http://www.freetds.org'
arch=('i686' 'x86_64')
license=('LGPL')
depends=('unixodbc')
options=('!libtool')
backup=('etc/freetds/freetds.conf'
        'etc/freetds/locales.conf'
        'etc/freetds/pool.conf')
source=("ftp://ftp.ibiblio.org/pub/Linux/ALPHA/freetds/stable/${pkgname}-${pkgver}.tar.gz")
md5sums=('b14db5823980a32f0643d1a84d3ec3ad')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr --sysconfdir=/etc/freetds --mandir=/usr/share/man --enable-msdblib --with-tdsver=7.0
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
