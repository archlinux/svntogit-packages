# Maintainer:

pkgname=freetds
pkgver=1.3
pkgrel=1
pkgdesc='Library for accessing Sybase and MS SQL Server databases'
url='https://www.freetds.org'
arch=('x86_64')
license=('LGPL')
depends=('openssl' 'unixodbc')
backup=('etc/freetds/freetds.conf'
        'etc/freetds/locales.conf'
        'etc/freetds/pool.conf')
source=("ftp://ftp.freetds.org/pub/freetds/stable/$pkgname-$pkgver.tar.bz2")
sha256sums=('8f979cad4d8350906e6c3645f70471af478d08759ed93cd3a31427f0792b5f97')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc/freetds --enable-msdblib \
    --with-unixodbc=/usr --with-openssl
  make 
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
