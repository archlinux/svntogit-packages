# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Dumitru Ursu <dima@ceata.org>

pkgname=opencolorio
pkgver=2.1.2
pkgrel=1
pkgdesc="A color management framework for visual effects and animation"
arch=('x86_64')
url="https://opencolorio.org"
license=('BSD')
depends=('freeglut' 'glew' 'lcms2' 'tinyxml' 'yaml-cpp' 'imath' 'pystring')
makedepends=('cmake' 'python' 'pybind11' 'ninja')
optdepends=('python: python bindings')
source=($pkgname-$pkgver.tar.gz::https://github.com/AcademySoftwareFoundation/OpenColorIO/archive/v$pkgver.tar.gz
        https://github.com/AcademySoftwareFoundation/OpenColorIO/commit/4fa94918c2cf572dcaf61ca07016f3b5c231c14c.patch)
sha512sums=('594e808fb1c175d5b14eb540be0dfb6f41cd37b5bf7df8c2d24d44dfe4986643ea68e52d0282eb3b25283489789001a57a201de1eecc1560fc9461780c7da353'
            'baffedf7d81c67c9a14f5977379a86eeed3d9891069ad810e4303994369b500e78c43e4c0fcbe5bd7f7ba16163c16b6796884f4250ff6200a162ed29d60780fb')

prepare() {
  cd OpenColorIO-$pkgver

  # We don't care about the failures of this patch in this current release
  patch -Np1 -i "$srcdir"/4fa94918c2cf572dcaf61ca07016f3b5c231c14c.patch || true
}

build() {
  cd OpenColorIO-$pkgver

  CXXFLAGS+=" -ffat-lto-objects"
  cmake \
    -GNinja \
    -Bbuild \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  ninja -C build
}

package() {
  cd OpenColorIO-$pkgver

  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
