# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: jepaan <jepaan at hotmail dot com>

pkgname=assimp
pkgver=5.2.4
pkgrel=1
pkgdesc="Library to import various well-known 3D model formats in an uniform manner"
arch=('x86_64')
license=('BSD')
depends=('zlib' 'gcc-libs')
makedepends=('cmake' 'ninja')
url='https://github.com/assimp/assimp'
source=("$pkgname-$pkgver.tar.gz::https://github.com/assimp/assimp/archive/v${pkgver}.tar.gz"
        https://github.com/gentoo/gentoo/raw/master/media-libs/assimp/files/assimp-5.2.3-drop-Werror-gcc-option.patch)
sha512sums=('ee988b1806b46c78f97bb5b25237a3f56a7028ed37898fb9b243e379e29e8bfd29e4dffc616566941ed9bdcf502bd30568904ad2ef2ef7d0f63b40daefdc66bf'
            '774abd8d0334605a1d48889036376d16fed683dd0f0f86fcfc39792e97d088383ca7eb49f8df842feedba1d2790b8a86c7d752aab38ca6fcf0f7819cc1a96d73')

build() {
  cd ${pkgname}-${pkgver}

  cmake \
    -Bbuild \
    -GNinja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DASSIMP_BUILD_SAMPLES=OFF \
    -DASSIMP_WARNINGS_AS_ERRORS=OFF
  ninja -C build
}

package()  {
  cd ${pkgname}-${pkgver}

  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
