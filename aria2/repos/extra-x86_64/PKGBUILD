# Contributor: Alexander Fehr <pizzapunk gmail com>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=aria2
pkgver=1.9.5
pkgrel=1
pkgdesc="Download utility that supports HTTP(S), FTP, BitTorrent, and Metalink"
arch=('i686' 'x86_64')
url="http://aria2.sourceforge.net/"
license=('GPL')
depends=('gnutls' 'libxml2' 'sqlite3' 'c-ares' 'ca-certificates')
source=(http://downloads.sourceforge.net/aria2/aria2-${pkgver}.tar.bz2)
md5sums=('03b98365c94711177bd2c579896eacc6')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}

	./configure --prefix=/usr \
		--with-ca-bundle=/etc/ssl/certs/ca-certificates.crt || return 1
	make || return 1
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}
	make DESTDIR=${pkgdir} install || return 1
}
