# $Id$
# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=aria2
pkgver=1.11.1
pkgrel=1
pkgdesc="Download utility that supports HTTP(S), FTP, BitTorrent, and Metalink"
arch=('i686' 'x86_64')
url="http://aria2.sourceforge.net/"
license=('GPL')
depends=('gnutls' 'libxml2' 'sqlite3' 'c-ares' 'ca-certificates')
source=(http://downloads.sourceforge.net/aria2/aria2-${pkgver}.tar.bz2)
md5sums=('da785645a6d92450b0a54f384202ba6b')
sha1sums=('b3b37cc7363305d55e86dcd74a73dc493ecfa530')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --with-ca-bundle=/etc/ssl/certs/ca-certificates.crt
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -D -m644 doc/bash_completion/aria2c "${pkgdir}/etc/bash_completion.d/aria2c"
  rm -r "${pkgdir}/usr/share/doc/aria2/bash_completion"
}
