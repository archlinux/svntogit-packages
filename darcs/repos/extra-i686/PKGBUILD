# $Id$
# Maintainer: Vesa Kaihlavirta <vesa@archlinux.org>
# Contributor: Simo Leone <neotuli@gmail.com>

pkgname=darcs
pkgver=2.5.2
pkgrel=1
pkgdesc="Decentralized replacement for CVS with roots in quantum mechanics"
arch=(i686 x86_64)
url="http://darcs.net/"
license=('GPL')
depends=('curl' 'gmp' 'ncurses>=5.6-7')
makedepends=('ghc' 'haskell-tar<0.4' 'haskell-text<0.12'
    'haskell-hashed-storage<0.6' 'haskell-haskeline<0.7'
    'haskell-html<1.1' 'haskell-parsec<3.2' 'haskell-regex-compat<0.94')
source=("http://darcs.net/releases/$pkgname-$pkgver.tar.gz")
md5sums=('491b6ca01dec245a16112ad2c7e07dc1')

build() {
    cd $srcdir/$pkgname-$pkgver
    runhaskell Setup.lhs configure --ghc -O --prefix=/usr \
         --disable-library-for-ghci --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup.lhs build
}

check() {
    cd ${srcdir}/$pkgname-$pkgver
    runhaskell Setup.lhs test
}

package() {
    cd $srcdir/$pkgname-$pkgver
    runhaskell Setup.lhs copy --destdir=$pkgdir
    rm -r $pkgdir/usr/lib/ 
    chmod 755 $pkgdir/usr/share/man/man1/$pkgname.1   
}

