# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=gpgmepp
pkgver=15.11.80
pkgrel=1
pkgdesc="C++ bindings/wrapper for gpgme"
arch=(i686 x86_64)
url="https://projects.kde.org/gpgmepp"
license=(LGPL)
depends=(gpgme qt5-base)
makedepends=(extra-cmake-modules python boost)
source=("http://download.kde.org/unstable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
md5sums=('715ef6c54fe0d7b6ff5cfa36e07f85bf')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
