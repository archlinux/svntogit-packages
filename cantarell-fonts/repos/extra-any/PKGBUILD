# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=cantarell-fonts
pkgver=0.303
pkgrel=1
epoch=1
pkgdesc="Humanist sans serif font"
url="https://gitlab.gnome.org/GNOME/cantarell-fonts"
arch=(any)
license=(custom:SIL)
makedepends=(meson appstream-glib git)
source=("https://gitlab.gnome.org/GNOME/$pkgname/-/jobs/1515398/artifacts/raw/build/meson-dist/$pkgname-$pkgver.tar.xz")
sha256sums=('8f65f4bdd7aabf16076d9b514b7d731b5db0d860dded0a68642cbc4ab658f8c0')

build() {
  arch-meson $pkgname-$pkgver build -D useprebuilt=true
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 $pkgname-$pkgver/COPYING
}

# vim:set sw=2 et:
