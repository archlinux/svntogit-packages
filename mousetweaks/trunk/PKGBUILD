# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com> 
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=mousetweaks
pkgver=3.32.0
pkgrel=1
pkgdesc="Mouse accessibility enhancements"
url="https://wiki.gnome.org/Projects/Mousetweaks"
arch=(x86_64)
license=(GPL3 FDL)
depends=(libxtst gtk3 gsettings-desktop-schemas)
makedepends=(python git)
groups=(gnome)
_commit=db65986e791c8dd001f8e295d19e374dd5e3efd5  # tags/3.32.0^0
source=("git+https://gitlab.gnome.org/GNOME/mousetweaks.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

check() {
  make -C $pkgname check
}

package() {
  DESTDIR="$pkgdir" make install -C $pkgname
}
