# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=libkdegames
pkgver=4.10.80
pkgrel=1
pkgdesc="Common code and data for many KDE games"
url="https://projects.kde.org/projects/kde/kdegames/libkdegames"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kdelibs' 'openal')
makedepends=('cmake' 'automoc4')
replaces=('kdegames-libkdegames')
conflicts=('kdegames-libkdegames')
source=("http://download.kde.org/unstable/${pkgver}/src/${pkgname}-${pkgver}.tar.xz")
sha1sums=('07add21684b9875fe17f0c99e8512dc8dbb6636e')

build() {
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
