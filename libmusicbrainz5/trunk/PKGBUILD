# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=libmusicbrainz5
pkgver=5.1.0
pkgrel=2
pkgdesc="Library That Provides Access to the MusicBrainz Server"
arch=('i686' 'x86_64')
url='http://musicbrainz.org/doc/libmusicbrainz'
license=('LGPL2.1')
depends=('neon' 'libxml2')
makedepends=('cmake')
source=("https://github.com/metabrainz/libmusicbrainz/releases/download/release-${pkgver}/libmusicbrainz-$pkgver.tar.gz")
md5sums=('4cc5556aa40ff7ab8f8cb83965535bc3')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../libmusicbrainz-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
