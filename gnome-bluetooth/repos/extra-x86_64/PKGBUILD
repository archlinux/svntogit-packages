# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
# Contributor: Roman Kyrylych <roman@archlinux.org>

pkgname=gnome-bluetooth
pkgver=3.4.0
pkgrel=2
pkgdesc="The GNOME Bluetooth Subsystem"
arch=('i686' 'x86_64')
url="http://live.gnome.org/GnomeBluetooth"
license=('GPL' 'LGPL')
depends=('gtk3' 'hicolor-icon-theme' 'gvfs-obexftp' 'obexd-client' 'dconf' 'libnotify')
makedepends=('intltool' 'gnome-doc-utils' 'nautilus-sendto' 'gobject-introspection')
options=('!libtool' '!emptydirs')
install=gnome-bluetooth.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz
        61-gnome-bluetooth-rfkill.rules)
sha256sums=('97a229e81997c14064899bdec2a62f94738d00d2f88cc18bcbeb2bdf0610e272'
            'b8acb8ea2e7f3588575cffd8ea14ec50c8641f518f2ea899771a508b299ea474')

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-desktop-update \
    --disable-icon-update \
    --disable-schemas-compile
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  install -m644 -D "$srcdir/61-gnome-bluetooth-rfkill.rules" \
                     "$pkgdir/usr/lib/udev/rules.d//61-gnome-bluetooth-rfkill.rules"
}
