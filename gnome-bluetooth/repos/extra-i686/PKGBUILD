# $Id$
# Maintainer: Roman Kyrylych <roman@archlinux.org>

pkgname=gnome-bluetooth
pkgver=2.30.0
pkgrel=2
pkgdesc="The GNOME Bluetooth Subsystem."
arch=('i686' 'x86_64')
url="http://live.gnome.org/GnomeBluetooth"
license=('GPL' 'LGPL')
depends=('bluez>=4.63' 'libunique>=1.1.6' 'libnotify>=0.4.5' 'gconf>=2.28.1' 'hicolor-icon-theme' 'gvfs-obexftp>=1.6.1' 'obexd>=0.22')
makedepends=('intltool' 'pygtk>=2.16.0' 'gnome-doc-utils>=0.20.0' 'nautilus-sendto' 'gobject-introspection' 'gir-repository')
replaces=('bluez-gnome')
conflicts=('bluez-gnome')
options=('!libtool' '!emptydirs' '!makeflags')
install=gnome-bluetooth.install
source=(http://ftp.gnome.org/pub/GNOME/sources/gnome-bluetooth/2.30/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('4caafa8c28195c81d8c51414f3a64489f81b9b46cac56979aeaaff8c9ebb78d4')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --disable-desktop-update --disable-icon-update || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gnome-bluetooth2 ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
