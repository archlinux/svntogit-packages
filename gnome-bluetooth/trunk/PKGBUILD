# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>
# Contributor: Roman Kyrylych <roman@archlinux.org>

pkgname=gnome-bluetooth
pkgver=3.5.92
pkgrel=1
pkgdesc="The GNOME Bluetooth Subsystem"
arch=('i686' 'x86_64')
url="http://live.gnome.org/GnomeBluetooth"
license=('GPL' 'LGPL')
depends=('gtk3' 'hicolor-icon-theme' 'gvfs-obexftp' 'obexd-client' 'dconf' 'libnotify')
makedepends=('intltool' 'nautilus-sendto' 'gobject-introspection' 'itstool' 'docbook-xsl')
options=('!libtool' '!emptydirs')
install=gnome-bluetooth.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz
        61-gnome-bluetooth-rfkill.rules)
sha256sums=('bb832118364be36510d32e6e33e90788c0f651c82c146f9abd800403f0a235f8'
            'b8acb8ea2e7f3588575cffd8ea14ec50c8641f518f2ea899771a508b299ea474')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-desktop-update \
    --disable-icon-update \
    --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  install -m644 -D ../61-gnome-bluetooth-rfkill.rules \
      "$pkgdir/usr/lib/udev/rules.d//61-gnome-bluetooth-rfkill.rules"
}
