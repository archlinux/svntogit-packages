# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Charles Mauch <cmauch@gmail.com>

pkgname=perl-yaml
pkgver=1.26
pkgrel=2
pkgdesc="Perl/CPAN Module YAML : YAML Aint Markup Language"
arch=('any')
url="http://search.cpan.org/dist/YAML/"
license=('GPL' 'PerlArtistic')
depends=('perl')
checkdepends=('perl-test-deep' 'perl-test-yaml')
options=('!emptydirs')
source=(http://www.cpan.org/authors/id/T/TI/TINITA/YAML-${pkgver}.tar.gz)
sha512sums=('17c75ed61d2961c5169c700caeb3403cc0233eec90ae8f4e55cee76ac827c48582d3e0c9c2146835132914c1e4276e0cf9d51cc8a82db308e7e930c97d05bb5f')

build() {
  cd YAML-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd YAML-${pkgver}
  make test
}

package() {
  cd YAML-${pkgver}
  make DESTDIR="${pkgdir}" install
}
