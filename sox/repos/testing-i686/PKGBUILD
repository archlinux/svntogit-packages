# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: dale <dale@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=sox
pkgver=14.3.0
pkgrel=4
pkgdesc="The Swiss Army knife of sound processing tools"
arch=('i686' 'x86_64')
url="http://sox.sourceforge.net/"
license=('GPL' 'LGPL')
depends=('ffmpeg' 'libao' 'libvorbis' 'lame' 'libmad' 'bash' 'alsa-lib' 'libsndfile' 'libsamplerate' 'libtool' 'file')
options=('!libtool')
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz)
md5sums=('8e3509804e6227273ef84092e1a2fea7')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --with-dyn-default \
    --with-distro="Arch Linux" || return 1
  make || return 1
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install || return 1
  ln -sf play "${pkgdir}/usr/bin/rec" || return 1
  ln -sf ../man1/sox.1.gz "${pkgdir}/usr/share/man/man7/soxeffect.7"
}
