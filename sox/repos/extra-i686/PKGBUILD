# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=sox
pkgver=14.4.1
pkgrel=1
pkgdesc="The Swiss Army knife of sound processing tools"
arch=('i686' 'x86_64')
url="http://sox.sourceforge.net/"
license=('GPL' 'LGPL')
depends=('libltdl' 'file' 'libsndfile' 'libpng' 'lame' 'opencore-amr' 'gsm')
makedepends=('ffmpeg' 'libao' 'libmad' 'libid3tag' 'wavpack' 'libpulse')
checkdepends=('time')
optdepends=('libao: for ao plugin'
            'ffmpeg: for ffmpeg plugin'
            'libmad: for mp3 plugin'
            'libid3tag: for mp3 plugin'
            'wavpack: for wavpack plugin'
            'libpulse: for pulse plugin')
options=('!libtool')
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.bz2
        sox-ffmpeg0.11.patch)
sha1sums=('4775f9e9eebb5a2fe79df548908a7a24088118d9'
          '2f92bcaebcaf300782396f106cdd27c72048a851')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i "${srcdir}/sox-ffmpeg0.11.patch"
  sed -i 's|man1/sox.1 soxeffect.7|man1/sox.1.gz soxeffect.7.gz|' Makefile.in
  ./configure --prefix=/usr --sysconfdir=/etc \
    --with-dyn-default --with-distro="Arch Linux"
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make bindir=. installcheck
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
