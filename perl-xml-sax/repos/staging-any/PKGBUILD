# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: François Charette <firmicus ατ gmx δοτ net>

pkgname=perl-xml-sax
pkgver=0.99
pkgrel=5
pkgdesc="Simple API for XML"
arch=('any')
url="http://search.cpan.org/dist/XML-SAX"
license=('GPL' 'PerlArtistic')
depends=('perl' 'perl-xml-sax-base' 'perl-xml-namespacesupport')
options=('!emptydirs')
install=perl-xml-sax.install
source=("http://www.cpan.org/authors/id/G/GR/GRANTM/XML-SAX-$pkgver.tar.gz"
        'perl-xml-sax.patch') 
md5sums=('290f5375ae87fdebfdb5bc3854019f24'
         'f114c4aad3221d3e7d8314d13b84a869')

build() {
  cd  $srcdir/XML-SAX-$pkgver
  patch -Np1 -i "${srcdir}/perl-xml-sax.patch"
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd  $srcdir/XML-SAX-$pkgver
  make test
}

package() {
  cd  $srcdir/XML-SAX-$pkgver
  make install DESTDIR=$pkgdir

  find $pkgdir -name '.packlist' -delete
  find $pkgdir -name '*.pod' -delete
}
