# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: François Charette <firmicus ατ gmx δοτ net>

pkgname=perl-xml-sax
pkgver=0.96
pkgrel=3
pkgdesc="Simple API for XML"
arch=('any')
url="http://search.cpan.org/dist/XML-SAX"
license=('GPL' 'PerlArtistic')
depends=('perl>=5.10.0' 'perl-xml-namespacesupport>=0.03')
options=('!emptydirs')
install=perl-xml-sax.install
source=("http://www.cpan.org/authors/id/G/GR/GRANTM/XML-SAX-$pkgver.tar.gz" 'perl-xml-sax.patch') 
md5sums=('bdcd4119a62505184e211e9dfaef0ab1'
         'b8ad2da4195ac0eede28dfaed4d874c3')

build() {
  cd  $srcdir
  patch -p0 < perl-xml-sax.patch

  cd  $srcdir/XML-SAX-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
  make test
}

package() {
  cd  $srcdir/XML-SAX-$pkgver
  make install DESTDIR=$pkgdir

  find $pkgdir -name '.packlist' -delete
  find $pkgdir -name '*.pod' -delete
}
