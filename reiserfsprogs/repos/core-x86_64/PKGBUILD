# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=reiserfsprogs
pkgver=3.6.22
pkgrel=2
pkgdesc="Reiserfs utilities"
arch=('i686' 'x86_64')
url="http://www.kernel.org/"
license=('GPL')
groups=('base')
depends=('util-linux')
source=("http://ftp.kernel.org/pub/linux/kernel/people/jeffm/reiserfsprogs/v3.6.22/${pkgname}-${pkgver}.tar.xz")
md5sums=('91d2fdb5eeaa15c8afcc9e815179690d')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sbindir=/usr/bin
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
