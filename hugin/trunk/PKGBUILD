# $Id$
# Maintainer: Tobias Kieslich <tobias@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Dominik Ryba <domryba@post.pl>

pkgname=hugin
pkgver=2011.0.0
pkgrel=1
pkgdesc="A frontend to the panorama-tools"
arch=('i686' 'x86_64')
url="http://hugin.sourceforge.net/"
license=('LGPL')
depends=('wxgtk' 'libpano13' 'boost-libs' 'enblend-enfuse' 'exiv2' 'openexr'
         'autopano-sift-c' 'lapack' 'desktop-file-utils' 'make' 'perl-exiftool')
makedepends=('zip' 'cmake' 'boost')
install=${pkgname}.install
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.bz2)
md5sums=('ac8a129b4c3021233df6d9368c8164cf')
sha1sums=('22e1ad3846291a9bbe58570683e213921a6f0116')

build() {
  cd "${srcdir}"
  mkdir build && cd build
  cmake "${srcdir}/${pkgname}-${pkgver}" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_EXE_LINKER_FLAGS="-lpthread" \
    -DENABLE_LAPACK=yes
  make
}

package(){
  cd "${srcdir}/build"
  make DESTDIR="${pkgdir}" install
}
