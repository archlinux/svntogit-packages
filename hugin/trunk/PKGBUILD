# $Id$
# Maintainer: Tobias Kieslich <tobias@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Dominik Ryba <domryba@post.pl>

pkgname=hugin
pkgver=2010.0.0
pkgrel=3
pkgdesc="A frontend to the panorama-tools"
arch=('i686' 'x86_64')
url="http://hugin.sourceforge.net/"
license=('LGPL')
depends=('wxgtk' 'libpano13' 'boost-libs' 'enblend-enfuse' 'exiv2' 'openexr' 
         'autopano-sift-c' 'lapack' 'desktop-file-utils')
makedepends=('zip' 'cmake' 'boost')
optdepends=('perl-exiftool: write metadata on output images')
install=${pkgname}.install
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz
	'libpng14.patch')
md5sums=('0cd0b78e0d866023e3b6645e5010876f'
         'e806c170e69f9d87cc2542338c86eab6')

build() {
  cd ${srcdir}
  patch -Np0 -i libpng14.patch

  mkdir build && cd build

  cmake "${srcdir}/${pkgname}-${pkgver}" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_EXE_LINKER_FLAGS="-lpthread" \
    -DENABLE_LAPACK=yes

  make
}

package(){
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install
}
