# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Tobias Kieslich <tobias@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Dominik Ryba <domryba@post.pl>

pkgname=hugin
pkgver=2016.0.0
pkgrel=1
pkgdesc='Panorama photo stitcher'
url='http://hugin.sourceforge.net/'
license=('GPL')
arch=('i686' 'x86_64')
makedepends=('cmake' 'boost' 'tclap' 'mesa' 'swig')
depends=('wxgtk' 'boost-libs' 'enblend-enfuse' 'exiv2' 'libpano13'
         'python' 'lensfun' 'lapack' 'make' 'sqlite' 'perl-image-exiftool'
         'desktop-file-utils')
source=("http://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgname}-${pkgver%.*}/${pkgname}-${pkgver/r/_r}.tar.bz2")
sha1sums=('6811e19f1c10da163e1c4228779beb4c12448db9')

install=install

build() {
	cd "${srcdir}/${pkgname}-${pkgver/rc?}"
	install -d build
	cd build
	cmake .. \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DENABLE_LAPACK=yes \

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver/rc?}"
	cd build
	make DESTDIR="${pkgdir}" install
}
