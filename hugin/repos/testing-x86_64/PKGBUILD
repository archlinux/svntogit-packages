# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Tobias Kieslich <tobias@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Dominik Ryba <domryba@post.pl>

pkgname=hugin
pkgver=2013.0.0
pkgrel=7
pkgdesc='Panorama photo stitcher'
url='http://hugin.sourceforge.net/'
license=('GPL')
arch=('i686' 'x86_64')
optdepends=('python2: bindings and plugins')
makedepends=('cmake' 'boost' 'tclap' 'mesa' 'python2' 'swig')
depends=('wxgtk' 'boost-libs' 'enblend-enfuse' 'exiv2' 'libpano13'
         'lensfun' 'lapack' 'make' 'perl-image-exiftool' 'desktop-file-utils')
source=("http://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgname}-${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2" hugin-build.patch)
sha1sums=('0e0689cf2ca0c4a2730cffe06380eda976b6772d'
          '45a8d8f5ee9b3c013cbc1ae3cfad14a2705c0dd4')

install=install

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	patch -p1 -i ../hugin-build.patch
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	cmake . \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DENABLE_LAPACK=yes
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
