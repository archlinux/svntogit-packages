# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Tobias Kieslich <tobias@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Dominik Ryba <domryba@post.pl>

pkgname=hugin
pkgver=2012.0.0
pkgrel=1
pkgdesc='Panorama photo stitcher'
url='http://hugin.sourceforge.net/'
license=('GPL')
arch=('i686' 'x86_64')
depends=('wxgtk' 'boost-libs' 'enblend-enfuse' 'exiv2' 'autopano-sift-c'
         'lensfun' 'lapack' 'make' 'perl-image-exiftool' 'desktop-file-utils')
makedepends=('cmake' 'boost' 'tclap' 'mesa' 'python' 'swig')
source=("http://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgname}-${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2")
sha1sums=('53f97d030716e66dc5a49c7e786686a9d5262b8d')

install=install

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	export CXXFLAGS+=" -fpermissive" 
	cmake . \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_EXE_LINKER_FLAGS="-lpthread" \
		-DENABLE_LAPACK=yes
	make
}

package(){
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
