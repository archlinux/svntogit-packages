# $Id$
# Maintainer: Tobias Kieslich <tobias@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Dominik Ryba <domryba@post.pl>

pkgname=hugin
pkgver=2011.4.0
pkgrel=3
pkgdesc="A frontend to the panorama-tools"
arch=('i686' 'x86_64')
url="http://hugin.sourceforge.net/"
license=('GPL')
depends=('wxgtk' 'boost-libs' 'enblend-enfuse' 'exiv2' 'autopano-sift-c'
         'lapack' 'desktop-file-utils' 'make' 'perl-image-exiftool')
makedepends=('zip' 'cmake' 'boost' 'tclap')
install=hugin.install
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.bz2"
        gcc47.patch)
sha1sums=('21ec7331d5e196aca2a8771d3c9101dd181ad93b'
          'aa0fc9425bd7a4b5e270412f4ca72258fac77298')

build() {
  cd "${srcdir}"
  patch -Np0 -i gcc47.patch
  mkdir build
  cd build
  export CXXFLAGS+=" -fpermissive" 
  cmake "${srcdir}/${pkgname}-${pkgver}" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_EXE_LINKER_FLAGS="-lpthread" \
    -DENABLE_LAPACK=yes
  make
}

package(){
  cd "${srcdir}/build"
  make DESTDIR="${pkgdir}" install
}
