# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Dale Blount <dale@archlinux.org>
# Contributor: Manolis Tzanidakis

pkgname=autofs
pkgver=5.0.5
pkgrel=3
pkgdesc="A kernel-based automounter for Linux"
arch=('i686' 'x86_64')
url="http://wiki.autofs.net/FrontPage"
license=('GPL2')
depends=('libldap' 'libxml2' 'heimdal')
backup=(etc/autofs/auto.master etc/autofs/auto.misc etc/conf.d/autofs)
install=${pkgname}.install
source=("http://www.kernel.org/pub/linux/daemons/${pkgname}/v5/${pkgname}-${pkgver}.tar.bz2"
        'autofs' 'autofs.conf.d' 'auto.master' 'auto.misc'
        'heimdal.patch')
md5sums=('a1d262cb6ebef0c2dd0fe22232fb3d5a'
         'e307bf6d2638e46eeb916cf42fe029b2'
         '47f597c870410055e0fdb66103daf928'
         'a6cefb591e77b31b79dbb7243646c96b'
         'd8a15ec9186c5c0b36e5cea1e2739e8a'
         'efd46b6406b940cb48f7c6197790fb8e')

build() {
  export MAKEFLAGS="-j1"
  cd ${srcdir}/${pkgname}-${pkgver}

  sed -i "s:SUBDIRS = lib daemon modules man samples:SUBDIRS = lib daemon modules man:" Makefile.rules
  patch -Np1 -i ${srcdir}/heimdal.patch

  ./configure --prefix=/usr \
    --sysconfdir=/etc/autofs \
    --with-mapdir=/etc/autofs \
    --without-hesiod
  make || return 1

  sed -i "s:/etc:/etc/autofs:g" man/auto.master.5
}

package(){
  cd ${srcdir}/${pkgname}-${pkgver}
  make INSTALLROOT=${pkgdir} install
  
  install -Dm644 ${srcdir}/auto.master ${pkgdir}/etc/autofs/auto.master || return 1
  install -Dm644 ${srcdir}/auto.misc ${pkgdir}/etc/autofs/auto.misc || return 1
  install -Dm755 ${srcdir}/autofs ${pkgdir}/etc/rc.d/autofs || return 1
  install -Dm644 ${srcdir}/autofs.conf.d ${pkgdir}/etc/conf.d/autofs || return 1
}
