# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Dale Blount <dale@archlinux.org>
# Contributor: Manolis Tzanidakis

pkgname=autofs
pkgver=5.0.4
pkgrel=4
pkgdesc="A kernel-based automounter for Linux."
arch=('i686' 'x86_64')
url="http://wiki.autofs.net/FrontPage"
license=('GPL2')
depends=('libldap' 'libxml2' 'heimdal')
backup=(etc/autofs/auto.master etc/autofs/auto.misc etc/conf.d/autofs)
install=autofs.install
source=("http://www.kernel.org/pub/linux/daemons/${pkgname}/v5/${pkgname}-${pkgver}.tar.bz2"
  'autofs' 'autofs.conf.d' 'auto.master' 'auto.misc'
  'heimdal.patch')
md5sums=('2646dde61edd08dd952255558c733c08'
         'e307bf6d2638e46eeb916cf42fe029b2'
         '47f597c870410055e0fdb66103daf928'
         'a6cefb591e77b31b79dbb7243646c96b'
         'd8a15ec9186c5c0b36e5cea1e2739e8a'
         '1ee4825e26ad72bfe86acec3d3da5d2a')

build() {
  export MAKEFLAGS="-j1"
  cd ${srcdir}/${pkgname}-${pkgver}

  sed -i "s:SUBDIRS = lib daemon modules man samples:SUBDIRS = lib daemon modules man:" Makefile.rules
  patch -Np1 -i ${srcdir}/heimdal.patch

  ./configure --prefix=/usr \
    --sysconfdir=/etc/autofs \
    --with-mapdir=/etc/autofs \
    --without-hesiod
  make || return 1
  make INSTALLROOT=${pkgdir} install
  
  local conf_file
  for conf_file in auto.{master,misc}; do
    install -D -m 644 ${srcdir}/$conf_file ${pkgdir}/etc/autofs/$conf_file
  done

  sed -i "s:/etc:/etc/autofs:g" ${pkgdir}/usr/share/man/man5/auto.master.5
  install -D -m 755 ${srcdir}/autofs ${pkgdir}/etc/rc.d/autofs
  install -D -m 644 ${srcdir}/autofs.conf.d ${pkgdir}/etc/conf.d/autofs
}
# vim: ts=2 sw=2 et ft=sh
