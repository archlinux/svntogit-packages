# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=prison-frameworks
pkgver=1.2.1.20150831
_commit=84724fa132b983266d8cd2b036d5a0f75a78ca46
pkgrel=1
pkgdesc="A barcode API to produce QRCode barcodes and DataMatrix barcodes"
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kdesupport/prison'
license=('GPL')
depends=('qt5-base' 'libdmtx' 'qrencode')
makedepends=('extra-cmake-modules' 'git')
source=("git://anongit.kde.org/prison.git#commit=$_commit")
md5sums=('SKIP')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../prison \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
