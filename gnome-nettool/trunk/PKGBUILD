# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Kritoke <kritoke@nospam.gmail.com>

pkgname=gnome-nettool
pkgver=3.1.1
pkgrel=1
pkgdesc="A Collection of GNOME3 Networking Tools."
arch=(i686 x86_64)
license=('GPL')
depends=('gtk3' 'libgtop' 'dnsutils' 'iputils' 'nmap' 'netkit-bsd-finger' 'whois' 'hicolor-icon-theme')
makedepends=('gnome-doc-utils' 'intltool')
groups=('gnome-extra')
url="http://www.gnome.org"
install=gnome-nettool.install
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('1e0430c94084484f234864c160e6252552d54bb72c163d3e451641045429901c')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
