# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegames-kfourinline
pkgver=4.11.90
pkgrel=1
pkgdesc="A four-in-a-row game"
url="http://kde.org/applications/games/kfourinline/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegames')
depends=('kdebase-runtime' 'libkdegames')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/unstable/${pkgver}/src/kfourinline-${pkgver}.tar.xz")
sha1sums=('fc4283e7f78da84cd41b311d7546e11c8fb57323')

build() {
  mkdir build
  cd build
  cmake ../kfourinline-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
