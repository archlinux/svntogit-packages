# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=libvncserver
pkgver=0.9.14
pkgrel=2
pkgdesc="Cross-platform C libraries that allow you to easily implement VNC server or client functionality"
arch=('x86_64')
url="https://libvnc.github.io/"
license=('GPL')
depends=('gnutls' 'libgcrypt' 'libjpeg' 'libpng' 'lzo' 'openssl')
makedepends=('sdl2' 'cmake')
source=("https://github.com/LibVNC/libvncserver/archive/LibVNCServer-${pkgver}.tar.gz")
sha256sums=('83104e4f7e28b02f8bf6b010d69b626fae591f887e949816305daebae527c9a5')

build() {
  cd $pkgname-LibVNCServer-$pkgver
  cmake -DCMAKE_INSTALL_PREFIX=/usr .
  make
}

package() {
  cd $pkgname-LibVNCServer-$pkgver
  make DESTDIR="$pkgdir" install
}
