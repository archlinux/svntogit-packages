# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Jan Jezek <honzin.jezek@gmail.com>

pkgname=tumbler
pkgver=4.16.1
pkgrel=3
pkgdesc="D-Bus service for applications to request thumbnails"
arch=('x86_64')
url="https://docs.xfce.org/xfce/tumbler/start"
license=('GPL2' 'LGPL')
groups=('xfce4')
depends=('gdk-pixbuf2')
makedepends=('intltool' 'python' 'ffmpegthumbnailer' 'poppler-glib' 'libgsf'
             'libgepub' 'libopenraw' 'freetype2' 'xfce4-dev-tools')
optdepends=('ffmpegthumbnailer: video thumbnails'
            'poppler-glib: PDF thumbnails'
            'libgsf: ODF thumbnails'
            'libgepub: EPUB thumbnails'
            'libopenraw: RAW thumbnails'
            'freetype2: font thumbnails')
backup=('etc/xdg/tumbler/tumbler.rc')
source=(https://archive.xfce.org/src/xfce/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2
        $pkgname-libopenraw-gnome-0.3.patch::https://gitlab.xfce.org/xfce/tumbler/-/commit/ad680ef6c79a.patch
        $pkgname-libgepub-0.7.patch::https://gitlab.xfce.org/xfce/tumbler/-/commit/3ddd3e940bf8.patch)
sha256sums=('852bbffa13f35b48ce92e243a03a118e32037c03e68297335c2bc4578e39273e'
            '9a3356b243fffa4dc34e966f2f80aff1cb947a90bec3fed3008aff1973a93efc'
            '07356891920ed4a5f9e991e3b87d1a66028c37e00325f614059ca61ad4750897')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -Np1 -i ../$pkgname-libopenraw-gnome-0.3.patch
  patch -Np1 -i ../$pkgname-libgepub-0.7.patch
  NOCONFIGURE=1 xdt-autogen
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/xfce4 \
    --disable-static \
    --disable-debug \
    --disable-gstreamer-thumbnailer
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
