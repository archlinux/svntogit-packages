# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Jan Jezek <honzin.jezek@gmail.com>

pkgname=tumbler
pkgver=4.16.1
pkgrel=1
pkgdesc="D-Bus service for applications to request thumbnails"
arch=('x86_64')
url="https://docs.xfce.org/xfce/tumbler/start"
license=('GPL2' 'LGPL')
groups=('xfce4')
depends=('gdk-pixbuf2')
makedepends=('intltool' 'python' 'ffmpegthumbnailer' 'poppler-glib' 'libgsf'
             'libgepub' 'libopenraw' 'freetype2')
optdepends=('ffmpegthumbnailer: video thumbnails'
            'poppler-glib: PDF thumbnails'
            'libgsf: ODF thumbnails'
            'libgepub: EPUB thumbnails'
            'libopenraw: RAW thumbnails'
            'freetype2: font thumbnails')
backup=('etc/xdg/tumbler/tumbler.rc')
source=(https://archive.xfce.org/src/xfce/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=('852bbffa13f35b48ce92e243a03a118e32037c03e68297335c2bc4578e39273e')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # Fix build with libopenraw 0.3
  sed -i 's|libopenraw-gnome-0.1|libopenraw-gnome-0.3|' configure
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/xfce4 \
    --disable-static \
    --disable-debug \
    --disable-gstreamer-thumbnailer
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
