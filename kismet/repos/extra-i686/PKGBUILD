# $Id$
# Contributer: Jason Chu <jason@archlinux.org>
# Maintainer: Juergen Hoetzel <jason@archlinux.org>

pkgname=kismet
pkgver=2010_01_R1
_realver="${pkgver//_/-}"
pkgrel=1
pkgdesc="802.11b sniffing software"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gmp' 'imagemagick' 'libpcap>=1.0.0' 'ncurses' 'dbus' 'libjpeg>=7')
optdepends=(perl-libwww)
url="http://www.kismetwireless.net/"
backup=('etc/ap_manuf' 'etc/client_manuf' 'etc/kismet.conf' 'etc/kismet_drone.conf' 'etc/kismet_ui.conf')
options=('!makeflags')
source=(http://www.kismetwireless.net/code/kismet-2010-01-R1.tar.gz)
md5sums=('a6d6edcf65d5bb2cb5de6472bcc16f19')

build() {
   cd "$srcdir/kismet-$_realver"

   ./configure --prefix=/usr \
               --sysconfdir=/etc \
               --localstatedir=/var || return 1

   make dep || return 1
   make || return 1
   sed -i 's/prism2/wlanng/g' conf/kismet.conf || return 1
   make DESTDIR="$pkgdir" install || return 1

   chown root:root "$pkgdir"/usr/share/man/man{1,5}/*

   install -D -m 644 "$srcdir/kismet-$_realver/README" "$pkgdir/usr/share/kismet/README"

   # Fix the *.conf unexpanded ${prefix} problems
   sed -i 's%\${prefix}%/usr%' "$pkgdir"/etc/*.conf || return 1
}
