# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Juergen Hoetzel <juergen@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Jason Chu <jason@archlinux.org>

pkgname=kismet
pkgver=2020_04_R2
_realver="${pkgver//_/-}"
pkgrel=2
pkgdesc="802.11 layer2 wireless network detector, sniffer, and intrusion detection system"
url="https://www.kismetwireless.net/"
arch=('x86_64')
license=('GPL')
depends=('libpcap' 'pcre' 'bluez-libs' 'libcap' 'libmicrohttpd' 'protobuf-c' 'libprotobuf-c.so'
         'sqlite' 'libnm' 'gcc-libs' 'libusb' 'libusb-1.0.so' 'libnl' 'glibc' 'zlib' 'libelf'
         'lm_sensors' 'systemd' 'python' 'python-setuptools' 'python-protobuf' 'python-pyserial'
         'python-numpy')
makedepends=('python-setuptools')
optdepends=(
  'gpsd: log coordinates of detected networks'
  'wireshark-cli: provide OUI files used to determine device manufacturer'
  'wireshark-cli: mergecap, to merge multiple capture files'
  'sox: provide the default kismet sound playback binary'
  'festival: text-to-speech support'
  'flite:  alternative/lightweight text-to-speech support')
backup=(
  etc/kismet/kismet.conf
  etc/kismet/kismet_80211.conf
  etc/kismet/kismet_alerts.conf
  etc/kismet/kismet_filter.conf
  etc/kismet/kismet_httpd.conf
  etc/kismet/kismet_logging.conf
  etc/kismet/kismet_memory.conf
  etc/kismet/kismet_storage.conf
  etc/kismet/kismet_uav.conf)
install=kismet.install
source=(https://www.kismetwireless.net/code/${pkgname}-${_realver}.tar.xz
        kismet.sysusers
        python-install-flags.patch)
sha512sums=('4eaa01f848eb51bafbd11d9ae47d7878a3891b99dd858f12f357f34b375b214b0fc37b916d040092091f19da4cb702bf22b2482bdb54600f43b6e6cfb3dfa462'
            '452b728c30c9932bdb91741af425267da87a66962ea9b736751948881f5477dee0bff8e2eeca2befdea2920da6d7d9e198bde69cef8c48bb49a0093a7f0ef897'
            '5effff1c25026436adccada5086f122e3c542a8edeec2f2f1018937ff43860b1b26a814876d72746bca4f7d1bcb9d4dcb1805c35c1786e6552ff45bc14fa6e4c')
validpgpkeys=('354689DF3C9DED803381A661D7B28822738BBDB1') # Michael Kershaw

prepare() {
  cd ${pkgname}-${_realver}
  patch -Np0 < ../python-install-flags.patch
  autoreconf -fiv
}

build() {
  cd ${pkgname}-${_realver}
  ./configure \
    --prefix=/usr \
    --localstatedir=/var \
    --sysconfdir=/etc/kismet
  make all plugins
}

package() {
  cd ${pkgname}-${_realver}
  make DESTDIR="${pkgdir}" install

  # Makepkg strip bug #43600
  chmod u+w "${pkgdir}"/usr/bin/kismet*
  chmod o-x "${pkgdir}"/usr/bin/kismet_cap*
  chown 0:315 "${pkgdir}"/usr/bin/kismet_cap*

  install -Dm 644 ../kismet.sysusers "${pkgdir}/usr/lib/sysusers.d/kismet.conf"
}

# vim: ts=2 sw=2 et:
