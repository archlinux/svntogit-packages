# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libgovirt
pkgver=0.3.4+45+g46288e8
pkgrel=1
pkgdesc="GObject-based library to access oVirt REST API"
arch=(x86_64)
url="https://git.gnome.org/browse/libgovirt"
license=(GPL)
depends=(glib2 rest)
makedepends=(gobject-introspection git)
_commit=46288e859023c6a1b3450d29456e59c999a69799  # master
source=("git+https://git.gnome.org/browse/libgovirt#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed -e 's/^v//' -e 's/-/+/g'
}

prepare() {
  cd $pkgname
  autoreconf -fvi
}
  
build(){
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check(){
  cd $pkgname
  make check
}

package(){
  cd $pkgname
  make DESTDIR="${pkgdir}" install
}
