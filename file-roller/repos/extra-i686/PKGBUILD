# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=file-roller
pkgver=2.22.3
pkgrel=1
pkgdesc="Archive manipulator for GNOME2"
arch=(i686 x86_64)
license=('GPL')
depends=('libgnomeui>=2.22.1' 'desktop-file-utils') 
makedepends=('perlxml' 'gnome-doc-utils>=0.12.2' 'pkgconfig' 'nautilus>=2.22.3')
groups=('gnome-extra')
options=('!libtool' '!emptydirs')
install=file-roller.install
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.22/${pkgname}-${pkgver}.tar.bz2)
md5sums=('8b731d60167670dbe3b5b976c96815e5')

build() {
  cd ${startdir}/src/${pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc \
              --libexecdir=/usr/lib/file-roller \
              --localstatedir=/var --disable-static \
	      --disable-scrollkeeper || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR=${startdir}/pkg install || return 1

  install -m755 -d ${startdir}/pkg/usr/share/gconf/schemas
  gconf-merge-schema ${startdir}/pkg/usr/share/gconf/schemas/${pkgname}.schemas ${startdir}/pkg/etc/gconf/schemas/*.schemas || return 1
  rm -f ${startdir}/pkg/etc/gconf/schemas/*.schemas
}
