# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=file-roller
pkgver=2.26.2
pkgrel=1
pkgdesc="Archive manipulator for GNOME2"
arch=(i686 x86_64)
license=('GPL')
depends=('desktop-file-utils' 'gconf>=2.26.0-3') 
makedepends=('intltool' 'gnome-doc-utils>=0.16.0' 'pkgconfig' 'nautilus>=2.26.3')
optdepends=('nautilus: nautilus shell extension')
groups=('gnome-extra')
options=('!libtool' '!emptydirs')
install=file-roller.install
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.26/${pkgname}-${pkgver}.tar.bz2
	fr-rpm-bsdtar.patch
	fr-fix-source.patch)
md5sums=('9d4575665dcbf063a062aa5a776eca68'
	 '918057080ef487c625f326ed5637f490'
	 'd8669520863718319d80666e424ffafb')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/fr-fix-source.patch" || return 1
  patch -Np0 -i "${srcdir}/fr-rpm-bsdtar.patch" || return 1
  ./configure --prefix=/usr --sysconfdir=/etc \
              --libexecdir=/usr/lib/file-roller \
              --localstatedir=/var --disable-static \
	      --disable-scrollkeeper || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain file-roller ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
