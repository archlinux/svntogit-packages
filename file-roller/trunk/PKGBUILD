# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=file-roller
pkgver=3.26.2
pkgrel=1
pkgdesc="Create and modify archives"
url="https://wiki.gnome.org/Apps/FileRoller"
arch=(x86_64)
license=(GPL)
depends=(gtk3 dconf libarchive file json-glib libnotify zip unzip)
makedepends=(intltool itstool docbook-xsl gnome-common git libnautilus-extension)
optdepends=('p7zip: 7z, arj, exe and encrypted zip files support'
            'unrar: better RAR archive support'
            'unace: ACE archive support'
            'lrzip: lrzip archive support')
groups=(gnome)
_commit=723d45e7c437f87303fd0203132b5770d2775d87  # tags/3.26.2^0
source=("git+https://git.gnome.org/browse/file-roller#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib/$pkgname --disable-static --disable-schemas-compile
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
