# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=file-roller
pkgver=2.30.0
pkgrel=1
pkgdesc="Archive manipulator for GNOME2"
arch=('i686' 'x86_64')
license=('GPL')
depends=('desktop-file-utils' 'gconf>=2.28.0')
makedepends=('intltool' 'gnome-doc-utils>=0.20.0' 'pkgconfig' 'nautilus>=2.30.0')
optdepends=('nautilus: nautilus shell extension'
	    'unrar: the RAR uncompression program'
	    'zip: creates PKZIP-compatible .zip files'
	    'unzip: unpacks .zip archives'
	    'p7zip: 7zip compression utility'
	    'xz-utils: managing LZMA and XZ compressed files'
	    'arj: ARJ archiver'
	    'unace: extraction tool for the proprietary ace archive format')
groups=('gnome-extra')
options=('!libtool' '!emptydirs')
install=file-roller.install
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.30/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('1fa54fb3a2616506dc2908050adbb16128b70d60dad789b20817cd4b60102fa5')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
      --libexecdir=/usr/lib/file-roller \
      --localstatedir=/var --disable-static \
      --disable-scrollkeeper || return 1
  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="${pkgdir}" install || return 1

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain file-roller ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
