# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeaccessibility-jovie
pkgver=16.04.2
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc='A text to speech application'
url='http://kde.org/applications/utilities/jovie/'
license=('GPL' 'FDL')
groups=('kde-applications' 'kdeaccessibility')
depends=('kdebase-runtime' 'speech-dispatcher')
makedepends=('cmake' 'automoc4')
replaces=('kdeaccessibility-kttsd')
source=("http://download.kde.org/stable/applications/${pkgver}/src/jovie-${pkgver}.tar.xz")
sha1sums=('869a3c50cd893e96dc10f5f8dc0d3ca3d955b32f')

build() {
  mkdir -p build
  cd build
  cmake ../jovie-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
