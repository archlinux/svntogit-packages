# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeaccessibility-jovie
pkgver=17.08.0
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc='A text to speech application'
url='https://kde.org/applications/utilities/jovie/'
license=('GPL' 'FDL')
groups=('kde-applications' 'kdeaccessibility')
depends=('kdebase-runtime' 'speech-dispatcher')
makedepends=('cmake' 'automoc4')
replaces=('kdeaccessibility-kttsd')
source=("https://download.kde.org/stable/applications/${pkgver}/src/jovie-${pkgver}.tar.xz"{,.sig})
sha256sums=('0a07e4dff91a51b43ebeae53e485c84460f0a4515978853e5353f4916a2f14e1'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  mkdir -p build
  cd build
  cmake ../jovie-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}
