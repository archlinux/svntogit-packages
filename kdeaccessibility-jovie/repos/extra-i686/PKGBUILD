# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdeaccessibility-jovie
pkgver=4.9.0
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc='A text to speech application'
url='http://kde.org/applications/utilities/jovie/'
license=('GPL' 'FDL')
groups=('kde' 'kdeaccessibility')
depends=('kdebase-runtime' 'speech-dispatcher')
makedepends=('cmake' 'automoc4')
replaces=('kdeaccessibility-kttsd')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/jovie-${pkgver}.tar.xz")
sha1sums=('2e5218951425e32d725001f4836283a2d2ac412f')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../jovie-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $srcdir/build
  make DESTDIR=$pkgdir install
  cd $srcdir/build/doc
  make DESTDIR=$pkgdir install
}
