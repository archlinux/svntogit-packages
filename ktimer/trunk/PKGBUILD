# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=ktimer
pkgver=17.08.0
pkgrel=1
pkgdesc='Countdown Launcher'
url='https://kde.org/applications/utilities/ktimer/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kio' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'kdoctools' 'python')
conflicts=('kdeutils-ktimer')
replaces=('kdeutils-ktimer')
groups=('kde-applications' 'kdeutils')
source=("https://download.kde.org/stable/applications/${pkgver}/src/ktimer-${pkgver}.tar.xz"{,.sig})
sha256sums=('07a7d5f2be591de4e6d8e1bebb245c69964fda1d0af735743fe9f8f1c3065289'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

build() {
  cd "${srcdir}"
  mkdir -p build
  cd build
  cmake ../ktimer-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $srcdir/build
  make DESTDIR=$pkgdir install
}
