# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=ktimer
pkgver=16.12.0
pkgrel=1
pkgdesc='Countdown Launcher'
url='http://kde.org/applications/utilities/ktimer/'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('kio' 'hicolor-icon-theme')
makedepends=('extra-cmake-modules' 'kdoctools' 'python')
conflicts=('kdeutils-ktimer')
replaces=('kdeutils-ktimer')
groups=('kde-applications' 'kdeutils')
source=("http://download.kde.org/stable/applications/${pkgver}/src/ktimer-${pkgver}.tar.xz"{,.sig})
sha1sums=('003587487563ae24ccf6ca94a93db7cfa7de0fcb'
          'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

build() {
  cd "${srcdir}"
  mkdir -p build
  cd build
  cmake ../ktimer-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $srcdir/build
  make DESTDIR=$pkgdir install
}
