# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdebindings-korundum
pkgver=4.9.0
pkgrel=1
pkgdesc="Ruby bindings for libraries created by the KDE community"
url="https://projects.kde.org/projects/kde/kdebindings/ruby/korundum"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kdebindings')
depends=('kdebindings-qtruby' 'kdebindings-smokekde')
makedepends=('cmake' 'automoc4' 'kdebindings-smokegen' 'boost'
             'kdepimlibs' 'kdegraphics-okular' 'kdesdk-kate')
conflicts=('kdebindings-ruby')
options=('!makeflags')
source=("http://download.kde.org/stable/${pkgver}/src/korundum-${pkgver}.tar.xz"
        'ruby19.patch')
sha1sums=('7c97b549bb6958223fc0b28f657fec09bb95b7e5'
          '73299f56a891d8a5475ac07dd485619859312f70')

build() {
  cd "${srcdir}/korundum-${pkgver}"
  patch -p1 -i "${srcdir}/ruby19.patch"

  cd "${srcdir}"
  mkdir build
  cd build

  cmake ../korundum-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCUSTOM_RUBY_SITE_LIB_DIR=$(ruby -e 'puts RbConfig::CONFIG["vendorlibdir"]') \
    -DCUSTOM_RUBY_SITE_ARCH_DIR=$(ruby -e 'puts RbConfig::CONFIG["vendorarchdir"]')

  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
