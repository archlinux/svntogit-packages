# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdebindings-korundum
pkgver=4.9.90
pkgrel=1
pkgdesc="Ruby bindings for libraries created by the KDE community"
url="https://projects.kde.org/projects/kde/kdebindings/ruby/korundum"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kdebindings')
depends=('kdebindings-qtruby' 'kdebindings-smokekde')
makedepends=('cmake' 'automoc4' 'kdebindings-smokegen' 'boost'
             'kdepimlibs' 'kdegraphics-okular' 'kdesdk-kate')
optdepends=('kdegraphics-okular: okular bindings'
            'kdepimlibs: akonadi bindings')
conflicts=('kdebindings-ruby')
source=("http://download.kde.org/unstable/${pkgver}/src/korundum-${pkgver}.tar.xz"
        'ruby19.patch')
sha1sums=('c257940ee5e89a3fc1ae45f0715b882cb03cff32'
          'c074f027bac3b9bffe70a2c2304a6a7be12be034')

build() {
  cd "${srcdir}/korundum-${pkgver}"
  patch -p1 -i "${srcdir}/ruby19.patch"

  cd "${srcdir}"
  mkdir build
  cd build

  cmake ../korundum-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCUSTOM_RUBY_SITE_LIB_DIR=$(ruby -e 'puts RbConfig::CONFIG["vendorlibdir"]') \
    -DCUSTOM_RUBY_SITE_ARCH_DIR=$(ruby -e 'puts RbConfig::CONFIG["vendorarchdir"]')

  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
