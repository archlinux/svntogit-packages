# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdebindings-korundum
pkgver=4.12.0
pkgrel=1
pkgdesc="Ruby bindings for libraries created by the KDE community"
url="https://projects.kde.org/projects/kde/kdebindings/ruby/korundum"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kdebindings')
depends=('kdebindings-qtruby' 'kdebindings-smokekde')
makedepends=('cmake' 'automoc4' 'kdebindings-smokegen' 'boost'
             'kdepimlibs' 'kdegraphics-okular' 'kdesdk-kate')
optdepends=('kdegraphics-okular: okular bindings'
            'kdepimlibs: akonadi bindings')
conflicts=('kdebindings-ruby')
source=("http://download.kde.org/stable/${pkgver}/src/korundum-${pkgver}.tar.xz")
sha1sums=('0b2a5f4453116a773ae446268efe697accc0d4c4')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../korundum-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCUSTOM_RUBY_SITE_LIB_DIR=$(ruby -e 'puts RbConfig::CONFIG["vendorlibdir"]') \
    -DCUSTOM_RUBY_SITE_ARCH_DIR=$(ruby -e 'puts RbConfig::CONFIG["vendorarchdir"]')

  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
