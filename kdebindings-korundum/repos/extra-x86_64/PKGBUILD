# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdebindings-korundum
pkgver=4.7.3
pkgrel=1
pkgdesc="KDE bindings for ruby"
url="http://kde.org/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kdebindings')
depends=('kdebindings-qtruby' 'kdebindings-smokekde')
makedepends=('cmake' 'automoc4' 'kdebindings-smokegen' 'boost'
             'kdepimlibs' 'kdegraphics-okular' 'kdesdk-kate')
conflicts=('kdebindings-ruby')
source=("http://download.kde.org/stable/${pkgver}/src/korundum-${pkgver}.tar.bz2")
sha1sums=('bf41817298a8b18ba3542e45256c1c6ddb08c900')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../korundum-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
