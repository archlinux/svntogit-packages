# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Committer: Judd Vinet <jvinet@zeroflux.org>

pkgname=pkg-config
pkgver=0.27.1
pkgrel=1
pkgdesc="A system for managing library compile/link flags"
arch=('i686' 'x86_64')
url="http://pkgconfig.freedesktop.org/wiki/"
license=('GPL')
groups=('base-devel')
depends=('glib2')
provides=("pkgconfig=${pkgver}")
conflicts=('pkgconfig')
replaces=('pkgconfig')
source=(http://pkgconfig.freedesktop.org/releases/${pkgname}-${pkgver}.tar.gz)
md5sums=('5392b4e3372879c5bf856173b418d6a2')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

check() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make check
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
