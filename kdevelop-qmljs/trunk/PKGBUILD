# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdevelop-qmljs
pkgver=1.7.0
pkgrel=2
pkgdesc="QML/JavaScript language plugin for KDevelop"
arch=('i686' 'x86_64')
url="http://www.kdevelop.org/"
license=('GPL')
depends=('kdevelop')
makedepends=('cmake' 'automoc4')
source=("http://download.kde.org/stable/kdevelop/kdev-qmljs/${pkgver}/src/kdev-qmljs-${pkgver}.tar.xz")
sha1sums=('78ad0f72ffa091f6aff5ea633eb5f135e9d625f3')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../kdev-qmljs-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
