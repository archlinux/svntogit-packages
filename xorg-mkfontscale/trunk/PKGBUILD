# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-mkfontscale
pkgver=1.1.2
pkgrel=1
pkgdesc="Create an index of scalable font files for X"
arch=(i686 x86_64)
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('freetype2' 'libfontenc')
makedepends=('xorg-util-macros' 'xproto')
groups=('xorg-apps' 'xorg')
source=(https://xorg.freedesktop.org/archive/individual/app/mkfontscale-${pkgver}.tar.bz2{,.sig})
sha256sums=('8c6d5228af885477b9aec60ca6f172578e7d2de42234357af62fb00439453f20'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # Alan Coopersmith <alan.coopersmith@oracle.com>

build() {
  cd "${srcdir}/mkfontscale-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/mkfontscale-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
