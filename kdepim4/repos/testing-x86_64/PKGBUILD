# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kdepim4
pkgname=('kdepim-akonadiconsole'
         'kdepim-akregator'
         'kdepim-blogilo'
         'kdepim-console'
         'kdepim-kaddressbook'
         'kdepim-kalarm'
         'kdepim-kjots'
         'kdepim-kleopatra'
         'kdepim-kmail'
         'kdepim-knode'
         'kdepim-knotes'
         'kdepim-kontact'
         'kdepim-korganizer'
         'kdepim-kresources'
         'kdepim-ktimetracker'
         'kdepim-ktnef'
         'kdepim-libkdepim')
pkgver=4.14.10
_kdeappver=15.04.3
pkgrel=3
arch=('i686' 'x86_64')
url='http://pim.kde.org'
license=('GPL' 'LGPL' 'FDL')
makedepends=('cmake' 'automoc4' 'boost' 'kdepim4-runtime' 'libxss' 'pilot-link'
             'libbaloo4' 'grantlee-qt4')
groups=('kdepim4')
source=("http://download.kde.org/stable/applications/${_kdeappver}/src/kdepim-${pkgver}.tar.xz")
sha1sums=('6f74763dda68dbb44394e807eab9204777bddf0a')

build() {
	mkdir build
	cd build
	cmake ../kdepim-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DKDE4_BUILD_TESTS=OFF \
		-DCMAKE_SKIP_RPATH=ON \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DKDEPIM_BUILD_MOBILE=OFF
	make
}

package_kdepim-akonadiconsole() {
	pkgdesc='Akonadi Management and Debugging Console'
	depends=('kdepim-libkdepim')
	provides=('akonadiconsole')
	url='http://pim.kde.org'
	install='kdepim4.install'
	cd "${srcdir}"/build/akonadiconsole
	make DESTDIR="${pkgdir}" install
}

package_kdepim-akregator() {
	pkgdesc='A Feed Reader for KDE'
	depends=('kdepim-libkdepim')
	provides=('akregator')
	url="http://kde.org/applications/internet/akregator/"
	install='kdepim-akregator.install'
	cd "${srcdir}"/build/akregator
	make DESTDIR="${pkgdir}" install
	cd "${srcdir}"/build/kontact/plugins/akregator
	make DESTDIR="${pkgdir}" install
	cd "${srcdir}"/build/doc/akregator
	make DESTDIR="${pkgdir}" install
}

package_kdepim-blogilo() {
	pkgdesc='A KDE Blogging Client'
	depends=('kdepim-libkdepim')
	url="http://kde.org/applications/internet/blogilo/"
	provides=('blogilo')
	install='kdepim4.install'
	cd "${srcdir}"/build/blogilo
	make DESTDIR="${pkgdir}" install
    cd "${srcdir}"/build/composereditor-ng
	make DESTDIR="${pkgdir}" install
	cd "${srcdir}"/build/doc/blogilo
	make DESTDIR="${pkgdir}" install
}

package_kdepim-console() {
	pkgdesc='Command line tool for accessing calendar files'
	depends=('kdepim-libkdepim')
	provides=('konsolekalendar')
	url='http://pim.kde.org'
	install='kdepim4.install'
	cd "${srcdir}"/build/console
	make DESTDIR="${pkgdir}" install
}

package_kdepim-kaddressbook() {
	pkgdesc='Contact Manager'
	depends=('kdepim-libkdepim')
	provides=('kaddressbook')
	url="http://kde.org/applications/office/kaddressbook/"
  install='kdepim-kaddressbook.install'
  for i in kaddressbook kontact/plugins/kaddressbook \
    plugins/kaddressbook; do
    cd "${srcdir}"/build/${i}
    make DESTDIR="${pkgdir}" install
  done
}

package_kdepim-kalarm() {
	pkgdesc='Personal Alarm Scheduler'
	depends=('kdepim-libkdepim')
	provides=('kalarm')
	url="http://kde.org/applications/utilities/kalarm/"
	install='kdepim4.install'
	cd "${srcdir}"/build/kalarm
	make DESTDIR="${pkgdir}" install
	cd "${srcdir}"/build/doc/kalarm
	make DESTDIR="${pkgdir}" install
}

package_kdepim-kjots() {
	pkgdesc='Note Taker'
	depends=('kdepim-libkdepim')
	url="http://kde.org/applications/utilities/kjots/"
	install='kdepim4.install'
	cd "${srcdir}"/build/kjots
	make DESTDIR="${pkgdir}" install
	cd "${srcdir}"/build/kontact/plugins/kjots
	make DESTDIR="${pkgdir}" install
	cd "${srcdir}"/build/doc/kjots
	make DESTDIR="${pkgdir}" install
}

package_kdepim-kleopatra() {
	pkgdesc='Certificate Manager and Unified Crypto GUI'
	depends=('kdepim-libkdepim')
	provides=('kleopatra')
    install=kdepim-kleopatra.install
	url="http://kde.org/applications/utilities/kleopatra/"
	cd "${srcdir}"/build/kleopatra
	make DESTDIR="${pkgdir}" install
	cd "${srcdir}"/build/doc/kleopatra
	make DESTDIR="${pkgdir}" install
	cd "${srcdir}"/build/doc/kwatchgnupg
	make DESTDIR="${pkgdir}" install
}

package_kdepim-kmail() {
	pkgdesc='Mail Client'
    depends=('kdepim-libkdepim')
	provides=('kmail')
	url="http://kde.org/applications/internet/kmail/"
	install='kdepim-kmail.install'
	conflicts=('kdepim-kmailcvt' 'kdepim-ksendemail' 'kdepim-libksieve'
	           'kdepim-mimelib' 'kdepim-plugins')
	replaces=('kdepim-kmailcvt' 'kdepim-ksendemail' 'kdepim-libksieve'
	          'kdepim-mimelib' 'kdepim-plugins')
    for i in kmail kmailcvt agents/archivemailagent storageservicemanager\
      agents/mailfilteragent agents/followupreminderagent importwizard ksendemail\
      libksieve messagelist kontact/plugins/kmail grantleeeditor mboximporter; do
      cd "${srcdir}"/build/${i}
      make DESTDIR="${pkgdir}" install
    done
	cd "${srcdir}"/build/doc/kmail
	make DESTDIR="${pkgdir}" install
	cd "${srcdir}"/build/doc/sieveeditor
	make DESTDIR="${pkgdir}" install
	cd "${srcdir}"/build/doc/importwizard
	make DESTDIR="${pkgdir}" install
}

package_kdepim-knode() {
	pkgdesc='News Reader'
	depends=('kdepim-libkdepim')
	url="http://kde.org/applications/internet/knode/"
	install='kdepim-knode.install'
	cd "${srcdir}"/build/knode
	make DESTDIR="${pkgdir}" install
	cd "${srcdir}"/build/kontact/plugins/knode
	make DESTDIR="${pkgdir}" install
	cd "${srcdir}"/build/doc/knode
	make DESTDIR="${pkgdir}" install
}

package_kdepim-knotes() {
	pkgdesc='Popup Notes'
	depends=('kdepim-libkdepim')
	provides=('knotes')
	url="http://kde.org/applications/utilities/knotes/"
	install='kdepim4.install'
	cd "${srcdir}"/build/knotes
	make DESTDIR="${pkgdir}" install
    cd "${srcdir}"/build/agents/notesagent
	make DESTDIR="${pkgdir}" install
	cd "${srcdir}"/build/kontact/plugins/knotes
	make DESTDIR="${pkgdir}" install
	cd "${srcdir}"/build/doc/knotes
	make DESTDIR="${pkgdir}" install
}

package_kdepim-kontact() {
	pkgdesc='Personal Information Manager'
	depends=('kdepim-libkdepim')
	provides=('kontact')
	url="http://kde.org/applications/office/kontact/"
	install='kdepim4.install'
	conflcits=('kdepim-kontactinterfaces')
	replaces=('kdepim-kontactinterfaces')
	for i in kontact/src kontact/plugins/summary \
		kontact/plugins/specialdates; do
		cd "${srcdir}"/build/${i}
		make DESTDIR="${pkgdir}" install
	done
	cd "${srcdir}"/build/doc/kontact
	make DESTDIR="${pkgdir}" install
}

package_kdepim-korganizer() {
	pkgdesc='Calendar and Scheduling Program'
	depends=('kdepim-libkdepim')
	provides=('korganizer')
	url="http://kde.org/applications/office/korganizer"
	install='kdepim-korganizer.install'
	for i in korganizer korgac kontact/plugins/korganizer calendarviews; do
		cd "${srcdir}"/build/${i}
		make DESTDIR="${pkgdir}" install
	done
	cd "${srcdir}"/build/doc/korganizer
	make DESTDIR="${pkgdir}" install
}

package_kdepim-kresources() {
	pkgdesc='KDE PIM resources'
	depends=('kdepim-libkdepim')
	url='http://pim.kde.org'
	cd "${srcdir}"/build/kresources
	make DESTDIR="${pkgdir}" install
}

package_kdepim-ktimetracker() {
	pkgdesc='Personal Time Tracker'
	depends=('kdepim-kresources')
	url="http://kde.org/applications/utilities/ktimetracker/"
	install='kdepim4.install'
	cd "${srcdir}"/build/ktimetracker
	make DESTDIR="${pkgdir}" install
	cd "${srcdir}"/build/doc/ktimetracker
	make DESTDIR="${pkgdir}" install
}

package_kdepim-ktnef() {
	pkgdesc='A viewer/extractor for TNEF files'
	depends=('kdebase-runtime' 'kdepimlibs4')
	provides=('ktnef')
	url="https://projects.kde.org/projects/kde/kdepim/ktnef"
    install=kdepim-ktnef.install
	cd "${srcdir}"/build/ktnef
	make DESTDIR="${pkgdir}" install
	cd "${srcdir}"/build/doc/ktnef
	make DESTDIR="${pkgdir}" install
}

package_kdepim-libkdepim() {
	pkgdesc='Library for KDE PIM'
	groups=()
	depends=('kdepim4-runtime' 'baloo4-akonadi' 'grantlee-qt4')
	url='http://pim.kde.org'
	conflicts=('kdepim-icons' 'kdepim-libkleo' 'kdepim-libkpgp'
	           'kdepim-akonadi')
	replaces=('kdepim-icons' 'kdepim-libkleo' 'kdepim-libkpgp'
	          'kdepim-akonadi')
	install='kdepim4.install'
	for i in agents/sendlateragent akonadi_next calendarsupport \
  incidenceeditor-ng kaddressbookgrantlee grantleetheme kdgantt2 libkdepim \
  libkdepimdbusinterfaces libkleo libkpgp mailcommon mailimporter \
  messagecomposer messagecore messageviewer noteshared icons templateparser \
  plugins/messageviewer plugins/ktexteditor pimcommon pimsettingexporter; do
		cd "${srcdir}"/build/${i}
		make DESTDIR="${pkgdir}" install
	done
	cd "${srcdir}"/build/doc/pimsettingexporter
	make DESTDIR="${pkgdir}" install
	cd "${srcdir}"/build/doc/headerthemeeditor
	make DESTDIR="${pkgdir}" install
	cd "${srcdir}"/build/doc/contactthemeeditor
	make DESTDIR="${pkgdir}" install
}
