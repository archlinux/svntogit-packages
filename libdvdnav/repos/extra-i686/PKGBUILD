# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Arjan <arjan@archlinux.org>
# contributor Sarah Hay <sarahhay@mb.sympatico.ca>

pkgname=libdvdnav
pkgver=5.0.0
pkgrel=1
pkgdesc="The library for xine-dvdnav plugin."
arch=('i686' 'x86_64')
license=('GPL')
url="http://dvdnav.mplayerhq.hu/"
depends=('libdvdread')
makedepends=('git')
source=(git://git.videolan.org/libdvdnav.git#tag=$pkgver)
sha256sums=('SKIP')

prepare() {
  cd $pkgname
  autoreconf -fi
}

build() {
  cd $pkgname
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname
  make DESTDIR="${pkgdir}" install
}
