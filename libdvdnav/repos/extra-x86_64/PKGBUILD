# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Arjan <arjan@archlinux.org>
# contributor Sarah Hay <sarahhay@mb.sympatico.ca>

pkgname=libdvdnav
pkgver=6.0.1
pkgrel=1
pkgdesc="The library for xine-dvdnav plugin."
arch=('x86_64')
license=('GPL')
url="https://www.videolan.org/developers/libdvdnav.html"
depends=('libdvdread')
makedepends=('git')
_commit=ee4f6486d73ab54e7704b4fed2615a1bad76427f  # tags/6.0.1^0
source=("git+https://code.videolan.org/videolan/libdvdnav.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed -e 's/-/+/g'
}

prepare() {
  cd $pkgname
  autoreconf -fi
}

build() {
  cd $pkgname
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname
  make DESTDIR="${pkgdir}" install
}
