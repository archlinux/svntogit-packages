# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=freecell-solver
pkgver=5.0.0
pkgrel=1
pkgdesc="A program that automatically solves layouts of Freecell and similar variants of Card Solitaire"
arch=(x86_64)
url="http://fc-solve.shlomifish.org"
license=(MIT)
depends=(bash)
makedepends=(cmake gperf perl-path-tiny perl-template-toolkit python-random2 python-six)
optdepends=('python-random2: for the Python scripts' 'python-six: for the Python scripts')
source=(http://fc-solve.shlomifish.org/downloads/fc-solve/$pkgname-$pkgver.tar.xz)
sha256sums=('696717df7e4fab2d5a038b26cc23e2259f7b68a101bc8a4855dffa2841699dfd')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DFCS_WITH_TEST_SUITE=OFF \
    -DBUILD_STATIC_LIBRARY=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir"/$pkgname-$pkgver/COPYING.html "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.html
}

