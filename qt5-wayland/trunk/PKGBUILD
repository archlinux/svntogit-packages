# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=qt5-wayland
_qtver=5.13.0
pkgver=${_qtver/-/}
pkgrel=3
arch=('x86_64')
url='https://www.qt.io'
license=('GPL3' 'LGPL3' 'FDL' 'custom')
pkgdesc='Provides APIs for Wayland'
depends=('qt5-declarative' 'libxcomposite')
groups=('qt' 'qt5')
_pkgfqn="${pkgname/5-/}-everywhere-src-${_qtver}"
source=("https://download.qt.io/official_releases/qt/${pkgver%.*}/${_qtver}/submodules/${_pkgfqn}.tar.xz"
        kdebug-407651.patch::"https://code.qt.io/cgit/qt/qtwayland.git/patch/?id=21a5038f"
        qtbug-76397a.patch::"https://code.qt.io/cgit/qt/qtwayland.git/patch/?id=ec905708"
        qtbug-76397b.patch::"https://code.qt.io/cgit/qt/qtwayland.git/patch/?id=a4e6f88f")
sha256sums=('b67a6d8119628bca3301bd03992880db07d61d405534067c9cd2a598695a7cf3'
            'f21ff86a4ffb4ba7648cba8d6434d7b0a8caa8abf650ba382d3cfa63c934e01b'
            'ab3c4c1f91c3ebfb4da463fc52959cc4a9b531988b6253fb78e64be05581905d'
            'b61b9872c83a27aad97d21f4e217b6c21f8d94deb164358ce09e3fdbd09332b4')

prepare() {
  mkdir -p build

  cd $_pkgfqn
  patch -p1 -i ../kdebug-407651.patch # Prevent windows from getting larger after closing and reopening https://bugs.kde.org/show_bug.cgi?id=407651
  patch -p1 -i ../qtbug-76397a.patch
  patch -p1 -i ../qtbug-76397b.patch # https://bugreports.qt.io/browse/QTBUG-76657
}

build() {
  cd build

  qmake ../${_pkgfqn}
  make
}

package() {
  cd build

  make INSTALL_ROOT="$pkgdir" install

  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  find "$pkgdir/usr/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt5-base "$pkgdir"/usr/share/licenses/${pkgname}
}
