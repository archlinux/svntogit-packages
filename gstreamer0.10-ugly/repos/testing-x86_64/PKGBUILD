# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=gstreamer0.10-ugly
pkgname=('gstreamer0.10-ugly' 'gstreamer0.10-ugly-plugins')
pkgver=0.10.19
pkgrel=3
arch=('i686' 'x86_64')
license=('LGPL')
makedepends=('pkgconfig' 'gstreamer0.10-base>=0.10.34' 'libdvdread' 'lame' 'libmpeg2' 'a52dec' 'libmad' 'libsidplay' 'libcdio' 'x264' 'opencore-amr')
url="http://gstreamer.freedesktop.org/"
options=(!libtool)
source=(${url}/src/gst-plugins-ugly/gst-plugins-ugly-${pkgver}.tar.xz
        opencore-amr.patch)
md5sums=('ba26045c8c8c91f0d48d327ccf53ac0c'
         'd8feb6c99bfaff8be6b2c48ea4e98e47')

build() {
  cd "${srcdir}/gst-plugins-ugly-${pkgver}"
  patch -Np0 -i "${srcdir}/opencore-amr.patch"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --enable-experimental \
    --with-package-name="GStreamer Ugly Plugins (Archlinux)" \
    --with-package-origin="http://www.archlinux.org/"
  make
  sed -e 's/gst-libs gst ext/gst-libs gst/' -i Makefile
}

check() {
  cd "${srcdir}/gst-plugins-ugly-${pkgver}"
  make check
}

package_gstreamer0.10-ugly() {
  pkgdesc="GStreamer Multimedia Framework Ugly plugin libraries"
  depends=('gstreamer0.10-base>=0.10.34')
  
  cd "${srcdir}/gst-plugins-ugly-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

package_gstreamer0.10-ugly-plugins() {
  pkgdesc="GStreamer Multimedia Framework Ugly Plugins (gst-plugins-ugly)"
  depends=("gstreamer0.10-ugly=${pkgver}" 'libdvdread' 'lame' 'libmpeg2' 'a52dec' 'libmad' 'libsidplay' 'libcdio' 'x264' 'opencore-amr')
  groups=('gstreamer0.10-plugins')
  replaces=('gstreamer0.10-dvdread' 'gstreamer0.10-mpeg2dec' 'gstreamer0.10-mad' 'gstreamer0.10-lame' 'gstreamer0.10-sidplay' 'gstreamer0.10-a52dec')
  conflicts=('gstreamer0.10-dvdread' 'gstreamer0.10-mpeg2dec' 'gstreamer0.10-mad' 'gstreamer0.10-lame' 'gstreamer0.10-sidplay' 'gstreamer0.10-a52dec')

  cd "${srcdir}/gst-plugins-ugly-${pkgver}"
  make -C ext DESTDIR="${pkgdir}" install
}
