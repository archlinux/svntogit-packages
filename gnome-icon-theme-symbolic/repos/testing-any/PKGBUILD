# $Id$
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=gnome-icon-theme-symbolic
pkgver=3.8.0.1
pkgrel=1
pkgdesc="GNOME icon theme, symbolic icons"
arch=(any)
depends=('gtk-update-icon-cache' 'icon-naming-utils' 'hicolor-icon-theme')
makedepends=('intltool')
url="http://www.gnome.org"
license=('GPL')
install=gnome-icon-theme-symbolic.install
groups=('gnome')
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver:0:3}/$pkgname-$pkgver.tar.xz
        gnome-icon-theme-symbolic.pc.in)
sha256sums=('b2fc2a319b1b3b5d74899c2001616b1317d2ec73b462918b240da1beada38342'
            'eff5b212c3e67c2ee2ee4d038bbec57a12ecc274fb5ffeddf06e332a7036af7c')

build() {
  cd "$pkgname-$pkgver"
  # HACK: Upstream maintainer generated the tarball with outdated Makefiles, check if this is still needed on updates
  cp ../gnome-icon-theme-symbolic.pc.in .

  GTK_UPDATE_ICON_CACHE=/bin/true ./configure --prefix=/usr
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
