# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=startup-notification
pkgver=0.12
pkgrel=6
pkgdesc="Monitor and display application startup"
arch=(x86_64)
license=('LGPL')
depends=('libx11' 'xcb-util')
url="https://www.freedesktop.org"
source=(https://www.freedesktop.org/software/startup-notification/releases/${pkgname}-${pkgver}.tar.gz)
sha256sums=('3c391f7e930c583095045cd2d10eb73a64f085c7fde9d260f2652c7cb3cfbe4a')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i -e '/AC_PATH_XTRA/d' configure.in
  autoreconf --force --install
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --localstatedir=/var --sysconfdir=/etc
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
