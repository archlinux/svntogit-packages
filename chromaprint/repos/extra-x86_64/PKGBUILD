# $Id$
# Contributor: Wieland Hoffmann <the_mineo@web.de>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=chromaprint
pkgver=0.7
pkgrel=5
pkgdesc='Library that implements a custom algorithm for extracting fingerprints from any audio source'
url='http://acoustid.org/chromaprint/'
arch=('i686' 'x86_64')
license=('LGPL')
depends=('ffmpeg')
makedepends=('cmake' 'git')
source=('git+https://bitbucket.org/acoustid/chromaprint.git#commit=dd51f8e')
sha1sums=('SKIP')

build() {
	cd "${srcdir}/${pkgname}"

	cmake \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DBUILD_EXAMPLES=ON \
		.
	make
}

package() {
	cd "${srcdir}/${pkgname}"
	make DESTDIR="${pkgdir}" install
}
