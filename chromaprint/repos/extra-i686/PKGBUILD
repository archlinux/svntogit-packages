# $Id$
# Contributor: Wieland Hoffmann <the_mineo@web.de>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=chromaprint
pkgver=1.4.2
pkgrel=1
pkgdesc='Library that implements a custom algorithm for extracting fingerprints from any audio source'
url='https://acoustid.org/chromaprint'
arch=('i686' 'x86_64')
license=('LGPL')
depends=('ffmpeg')
makedepends=('cmake' 'git')
_commit=f2ce2e7ff05cf2b0b5e473fff55f31acf07c6a92 #tag=v1.4.2
source=("git+https://github.com/acoustid/chromaprint.git#commit=$_commit")
sha256sums=('SKIP')

build() {
	cd "${srcdir}/${pkgname}"
	cmake \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DBUILD_TOOLS=ON \
		.
	make
}

package() {
	cd "${srcdir}/${pkgname}"
	make DESTDIR="${pkgdir}" install
}
