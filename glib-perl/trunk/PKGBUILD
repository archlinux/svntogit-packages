# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Sarah Hay <sarah@archlinux.org>

pkgname=glib-perl
pkgver=1.242
pkgrel=2
pkgdesc="Perl wrappers for glib 2.x, including GObject"
arch=(i686 x86_64)
license=('LGPL')
url="http://gtk2-perl.sourceforge.net/"
options=('!emptydirs')
depends=('glib2>=2.24.1' 'perl>=5.14')
makedepends=('perl-extutils-depends>=0.302' 'perl-extutils-pkgconfig>=1.12')
source=(http://downloads.sourceforge.net/sourceforge/gtk2-perl/Glib-${pkgver}.tar.gz)
md5sums=('59c176888e8ef8139c4e1b8c2d45f1d0')

build() {
  cd "${srcdir}/Glib-${pkgver}"
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "${srcdir}/Glib-${pkgver}"
  make test
}

package() {
  cd "${srcdir}/Glib-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
