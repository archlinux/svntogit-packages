# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-xpr
pkgver=1.0.5
pkgrel=1
pkgdesc="Print an X window dump from xwd"
arch=(x86_64)
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libx11' 'libxmu' 'sh')
makedepends=('xorg-util-macros')
groups=('xorg-apps' 'xorg')
source=(https://xorg.freedesktop.org/archive/individual/app/xpr-${pkgver}.tar.bz2{,.sig})
sha512sums=('d5dfb936ec4ad0067d02654dec8e862fe337ff6a2c2f974b2b3a612a829c2e728a0d51e7aba00ed2a94dc96dc50cc69f2c59f522e0635013f5f4f5bb3f4ab967'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # "Alan Coopersmith <alan.coopersmith@oracle.com>"

build() {
  cd xpr-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd xpr-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
