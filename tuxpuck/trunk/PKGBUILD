# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgname=tuxpuck
pkgver=0.8.2
pkgrel=6
pkgdesc="Airhockey with tux"
arch=('i686' 'x86_64')
url="http://home.no.net/munsuun/tuxpuck/"
license=('GPL2')
depends=('libjpeg' 'sdl' 'libpng' 'libvorbis')
makedepends=('freetype2')
options=('!makeflags')
source=("http://ftp.de.debian.org/debian/pool/main/t/${pkgname}/${pkgname}_$pkgver.orig.tar.gz"
        'tuxpuck-0.8.2-ldflags.patch'
        'tuxpuck-0.8.2-libpng15.patch')
md5sums=('fc839abc2b1f3eafae397e1ed6487079'
         'cb914d211bf3f519b7700fda180612b0'
         'a4da530d670a0c30ac0840aab1db0dca')

build() {
  cd $srcdir/$pkgname-$pkgver

  sed -i 's|usr/man|usr/share/man|' Makefile
  sed -i -e 's/-Werror//' \
         -e '/^CC/d' \
         Makefile \
         utils/Makefile \
         data/Makefile

  patch -Np0 -i "${srcdir}/tuxpuck-0.8.2-ldflags.patch"
  patch -Np0 -i "${srcdir}/tuxpuck-0.8.2-libpng15.patch"

  make
}

package() {
  cd $srcdir/$pkgname-$pkgver

  make DESTDIR=$pkgdir install
}
