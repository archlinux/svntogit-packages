# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: ezzetabi <ezzetabi at gawab dot com>

pkgname=ppl
pkgver=0.11.2
pkgrel=2
pkgdesc="A modern library for convex polyhedra and other numerical abstractions."
arch=('i686' 'x86_64')
url="http://www.cs.unipr.it/ppl"
license=('GPL3')
depends=('gmp>=5.0')
options=('!docs' '!libtool')
source=(http://www.cs.unipr.it/ppl/Download/ftp/releases/$pkgver/ppl-$pkgver.tar.gz)
md5sums=('ce014f153a28006009db207ca953a984')

build() {
  cd $srcdir/ppl-$pkgver
  ./configure --prefix=/usr --enable-interfaces="c,cxx"
  make
}

check() {
  cd $srcdir/ppl-$pkgver
  make check
}

package() {
  cd $srcdir/ppl-$pkgver
  make DESTDIR=$pkgdir install
}

