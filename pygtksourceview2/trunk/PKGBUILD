# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=pygtksourceview2
pkgver=2.10.1
pkgrel=6
pkgdesc="Python bindings for gtksourceview2"
arch=(x86_64)
license=('GPL')
depends=('gtksourceview2>=2.10.0' 'pygtk>=2.17.0')
makedepends=('intltool' 'pkg-config')
url="http://www.gnome.org"
source=(https://download.gnome.org/sources/pygtksourceview/2.10/pygtksourceview-${pkgver}.tar.bz2)
sha512sums=('6f8f35ba36cb26f256861ff7f14c3236cf566d6d02f3dea69f63844081caee969ad6288a2aa48d35d4eb542cda73da9d4bfe2cc57548ca15cd372011cab0276e')

build() {
  cd "${srcdir}/pygtksourceview-${pkgver}"
  PYTHON=python2 ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static --disable-docs
  make
}

package() {
  cd "${srcdir}/pygtksourceview-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
