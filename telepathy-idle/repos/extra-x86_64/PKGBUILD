# Maintainer:
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Bjorn Lindeijer <bjorn lindeijer nl>
# Contributor: Samuel Mendes <heka.lok@gmail.com>

pkgname=telepathy-idle
pkgver=0.2.0
pkgrel=3
pkgdesc="An IRC connection manager for Telepathy"
arch=('x86_64')
url="https://telepathy.freedesktop.org"
groups=('telepathy')
license=('LGPL')
depends=('telepathy-glib')
makedepends=('libxslt' 'python2')
source=(https://telepathy.freedesktop.org/releases/$pkgname/$pkgname-$pkgver.tar.gz{,.asc})
sha256sums=('3013ad4b38d14ee630b8cc8ada5e95ccaa849b9a6fe15d2eaf6d0717d76f2fab'
            'SKIP')
validpgpkeys=('1B5A1BA811AAF0F12169E28AAC558671711E31B1') # Guillaume Desmottes <gdesmott@gnome.org>

build() {
    cd $pkgname-$pkgver
    ./configure --prefix=/usr \
    	--libexecdir=/usr/lib/telepathy
    make
}

package() {
    cd $pkgname-$pkgver
    make DESTDIR="$pkgdir" install
}
