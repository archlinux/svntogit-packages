# $Id$
#Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgname=lablgtk2
pkgver=2.18.3
pkgrel=2
pkgdesc="An Objective Caml interface to gtk2"
arch=(i686 x86_64)
license=('LGPL')
url="http://lablgtk.forge.ocamlcore.org/"
depends=('gtk2' 'gtkspell' 'libgnomecanvas' 'librsvg' 'libgnomeui' 'gtksourceview2')
makedepends=('ocaml' 'camlp4')
optdepends=('ocaml: for using the tools')
DLAGENTS=('https::/usr/bin/curl -fLC - --insecure --retry 3 --retry-delay 3 -o %o %u')
source=(https://forge.ocamlcore.org/frs/download.php/1479/lablgtk-${pkgver}.tar.gz)
options=(!makeflags staticlibs)

build() {
  cd "${srcdir}/lablgtk-${pkgver}"
  ./configure --prefix=/usr
  make world
  make opt
}

package() {
  cd "${srcdir}/lablgtk-${pkgver}"
  make DESTDIR=${pkgdir} old-install
  install -m644 META "${pkgdir}/usr/lib/ocaml/lablgtk2/"
}
md5sums=('bcbad64a28c3dc40f24cc7a4d2f1d0dd')
