# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=soundtouch
pkgver=2.3.0
pkgrel=1
pkgdesc='An audio processing library'
arch=(x86_64)
url=https://www.surina.net/soundtouch/
license=(LGPL)
depends=(gcc-libs)
makedepends=(git)
_tag=c65afe49f697fcea87d9a134870c8d115d7700cc
source=(git+https://gitlab.com/soundtouch/soundtouch.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd soundtouch

  git describe --tags
}

prepare() {
  cd soundtouch

  ./bootstrap
}

build() {
  cd soundtouch

  ./configure \
    --prefix=/usr \
    --enable-shared
  make
}

package() {
  make DESTDIR="${pkgdir}" pkgdocdir=/usr/share/doc/soundtouch -C soundtouch install
}

# vim: ts=2 sw=2 et:
