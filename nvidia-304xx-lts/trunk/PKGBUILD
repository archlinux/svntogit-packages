# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=nvidia-304xx-lts
pkgver=304.117
_extramodules=extramodules-3.10-lts
pkgrel=3
pkgdesc="NVIDIA drivers for linux-lts, 304xx legacy branch"
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=('linux-lts>=3.10.26' "nvidia-304xx-utils=${pkgver}")
makedepends=('linux-lts-headers>=3.10.26')
conflicts=('nvidia-lts' 'nvidia-96xx-lts' 'nvidia-173xx-lts')
provides=('nvidia-304xx')
license=('custom')
install=nvidia-304xx-lts.install
options=(!strip)

if [ "$CARCH" = "i686" ]; then
    _arch='x86'
    _pkg="NVIDIA-Linux-${_arch}-${pkgver}"
    source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
    md5sums=('023eb03da304a1842fc503c81e3c5694')
elif [ "$CARCH" = "x86_64" ]; then
    _arch='x86_64'
   _pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
    source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")
    md5sums=('8ee9f672028971c6d36118894d0e2c59')
fi

build() {
    _kernver="$(cat /usr/lib/modules/${_extramodules}/version)"
    cd "${srcdir}"
    sh "${_pkg}.run" --extract-only
    cd "${_pkg}/kernel"
    make SYSSRC=/usr/lib/modules/"${_kernver}/build" module
}

package() {
    install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" \
        "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
    install -d -m755 "${pkgdir}/usr/lib/modprobe.d"
    echo "blacklist nouveau" >> "${pkgdir}/usr/lib/modprobe.d/nvidia-304xx-lts.conf"
    echo "blacklist nvidiafb" >> "${pkgdir}/usr/lib/modprobe.d/nvidia-304xx-lts.conf"
    gzip "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
    install -D -m644 ${_pkg}/LICENSE "${pkgdir}/usr/share/licenses/nvidia-304xx-lts/LICENSE"
}
