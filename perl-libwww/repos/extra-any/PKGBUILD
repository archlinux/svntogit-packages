# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=perl-libwww
pkgver=6.06
pkgrel=1
pkgdesc="The World-Wide Web library for Perl"
arch=('any')
url="https://metacpan.org/release/libwww-perl"
license=('PerlArtistic' 'GPL')
depends=('perl' 'perl-encode-locale' 'perl-file-listing'
         'perl-html-parser' 'perl-http-cookies' 'perl-http-daemon'
         'perl-http-date' 'perl-http-negotiate' 'perl-lwp-mediatypes'
         'perl-net-http' 'perl-uri' 'perl-www-robotrules'
         'perl-http-message')
optdepends=('perl-lwp-protocol-https: for https:// url schemes')
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/M/MS/MSCHILLI/libwww-perl-${pkgver}.tar.gz)
sha1sums=('9640b88fb8bd3c88813525cad8e613f26a4e002b')

build() {
  cd libwww-perl-${pkgver}
  perl Makefile.PL --aliases INSTALLDIRS=vendor
  make
}

check()  {
  cd libwww-perl-${pkgver}
  make test
}

package() {
  cd libwww-perl-${pkgver}
  make DESTDIR="$pkgdir" install
}
