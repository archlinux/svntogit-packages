# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=kdeedu
pkgname=('kdeedu-blinken'
         'kdeedu-cantor'
         'kdeedu-data'
         'kdeedu-kalgebra'
         'kdeedu-kalzium'
         'kdeedu-kanagram'
         'kdeedu-kbruch'
         'kdeedu-kgeography'
         'kdeedu-khangman'
         'kdeedu-kig'
         'kdeedu-kiten'
         'kdeedu-klettres'
         'kdeedu-kmplot'
         'kdeedu-kstars'
         'kdeedu-ktouch'
         'kdeedu-kturtle'
         'kdeedu-kwordquiz'
         'kdeedu-libkdeedu'
         'kdeedu-marble'
         'kdeedu-parley'
         'kdeedu-rocs'
         'kdeedu-step')
pkgver=4.4.90
pkgrel=1
arch=('i686' 'x86_64')
url='http://www.kde.org'
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdeedu')
makedepends=('pkgconfig' 'cmake' 'automoc4' 'ocaml' 'facile' 'eigen' 'cfitsio'
	'kdebase-runtime' 'openbabel' 'libqalculate' 'gsl' 'boost' 'libindi' 'r'
	'libspectre' 'python' 'docbook-xsl')
source=("http://download.kde.org/unstable/${pkgver}/src/${pkgbase}-${pkgver}.tar.bz2")
sha1sums=('c1a9777e24fae64db2c373c0bd3e63d5fe0f0f7d')

build() {
	cd $srcdir
	mkdir build
	cd build
	# rpath is needed for cantor to find R
	cmake ../${pkgbase}-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_SKIP_RPATH=OFF \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package_kdeedu-blinken() {
	pkgdesc='A memory enhancement game'
	depends=('kdebase-runtime' 'kdeedu-libkdeedu')
	install='kdeedu.install'
	cd $srcdir/build/blinken
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/blinken
	make DESTDIR=$pkgdir install
}

package_kdeedu-cantor() {
	pkgdesc='KDE Frontend to Mathematical Software'
	depends=('kdeedu-libkdeedu' 'libspectre')
	optdepends=('kdeedu-kalgebra: backend'
		'maxima: backend'
		'r: backend')
	install='kdeedu.install'
	cd $srcdir/build/cantor
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/cantor
	make DESTDIR=$pkgdir install
}

package_kdeedu-data() {
	pkgdesc='Data for KDE Educational Software'
	groups=()
	cd $srcdir/build/data
	make DESTDIR=$pkgdir install
}

package_kdeedu-kalgebra() {
	pkgdesc='Math Expression Solver and Plotter'
	depends=('kdebase-runtime')
	install='kdeedu.install'
	cd $srcdir/build/kalgebra
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kalgebra
	make DESTDIR=$pkgdir install
}

package_kdeedu-kalzium() {
	pkgdesc='KDE Periodic Table of Elements'
	depends=('kdebase-runtime'  'kdeedu-libkdeedu' 'openbabel')
	install='kdeedu.install'
	cd $srcdir/build/kalzium
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kalzium
	make DESTDIR=$pkgdir install
}

package_kdeedu-kanagram() {
	pkgdesc='KDE Letter Order Game'
	depends=('kdebase-runtime' 'kdeedu-libkdeedu' 'kdeedu-data')
	install='kdeedu.install'
	cd $srcdir/build/kanagram
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kanagram
	make DESTDIR=$pkgdir install
}

package_kdeedu-kbruch() {
	pkgdesc='Practice exercises with fractions'
	depends=('kdelibs' 'kdebase-runtime')
	install='kdeedu.install'
	cd $srcdir/build/kbruch
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kbruch
	make DESTDIR=$pkgdir install
}

package_kdeedu-kgeography() {
	pkgdesc='A Geography Learning Program'
	depends=('kdebase-runtime')
	install='kdeedu.install'
	cd $srcdir/build/kgeography
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kgeography
	make DESTDIR=$pkgdir install
}

package_kdeedu-khangman() {
	pkgdesc='KDE Hangman Game'
	depends=('kdebase-runtime' 'kdeedu-libkdeedu' 'kdeedu-data')
	install='kdeedu.install'
	cd $srcdir/build/khangman
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/khangman
	make DESTDIR=$pkgdir install
}

package_kdeedu-kig() {
	pkgdesc='Explore Geometric Constructions'
	depends=('kdebase-runtime' 'boost-libs' 'python')
	install='kdeedu.install'
	cd $srcdir/build/kig
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kig
	make DESTDIR=$pkgdir install
}

package_kdeedu-kiten() {
	pkgdesc='Japanese Reference and Study Tool'
	depends=('kdebase-runtime')
	install='kdeedu.install'
	cd $srcdir/build/kiten
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kiten
	make DESTDIR=$pkgdir install
}

package_kdeedu-klettres() {
	pkgdesc='a KDE program to learn the alphabet'
	depends=('kdebase-runtime')
	install='kdeedu.install'
	cd $srcdir/build/klettres
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/klettres
	make DESTDIR=$pkgdir install
}

package_kdeedu-kmplot() {
	pkgdesc='Function Plotter'
	depends=('kdebase-runtime')
	install='kdeedu.install'
	cd $srcdir/build/kmplot
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kmplot
	make DESTDIR=$pkgdir install
}

package_kdeedu-kstars() {
	pkgdesc='Desktop Planetarium'
	depends=('kdebase-runtime' 'libindi')
	install='kdeedu.install'
	cd $srcdir/build/kstars
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kstars
	make DESTDIR=$pkgdir install
}

package_kdeedu-ktouch() {
	pkgdesc='Touch Typing Tutor'
	depends=('kdebase-runtime')
	install='kdeedu.install'
	cd $srcdir/build/ktouch
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/ktouch
	make DESTDIR=$pkgdir install
}

package_kdeedu-kturtle() {
	pkgdesc='Educational Programming Environment'
	depends=('kdebase-runtime')
	install='kdeedu.install'
	cd $srcdir/build/kturtle
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kturtle
	make DESTDIR=$pkgdir install
}

package_kdeedu-kwordquiz() {
	pkgdesc='A flashcard and vocabulary learning program'
	depends=('kdebase-runtime' 'kdeedu-libkdeedu' 'kdeedu-data')
	install='kdeedu.install'
	cd $srcdir/build/kwordquiz
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/kwordquiz
	make DESTDIR=$pkgdir install
}

package_kdeedu-libkdeedu() {
	pkgdesc='Support library for KDE Educational Software'
	groups=()
	depends=('kdelibs')
	cd $srcdir/build/libkdeedu
	make DESTDIR=$pkgdir install
	install -D -m644 $srcdir/${pkgbase}-${pkgver}/cmake/modules/FindKDEEdu.cmake \
		$pkgdir/usr/share/apps/cmake/modules/FindKDEEdu.cmake
}

package_kdeedu-marble() {
	pkgdesc='Desktop Globe'
	depends=('kdebase-runtime')
	install='kdeedu.install'
	cd $srcdir/build/marble
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/marble
	make DESTDIR=$pkgdir install
	install -D -m644 $srcdir/${pkgbase}-${pkgver}/cmake/modules/FindMarbleWidget.cmake \
		$pkgdir/usr/share/apps/cmake/modules/FindMarbleWidget.cmake
}

package_kdeedu-parley() {
	pkgdesc='Vocabulary Trainer'
	depends=('kdebase-runtime' 'kdeedu-libkdeedu' 'kdeedu-data' 'kdebindings-python')
	install='kdeedu.install'
	cd $srcdir/build/parley
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/parley
	make DESTDIR=$pkgdir install
}

package_kdeedu-rocs() {
	pkgdesc='Graph Theory Tool for Professors and Students.'
	depends=('kdebase-runtime' 'kdeedu-libkdeedu')
	install='kdeedu.install'
	cd $srcdir/build/rocs
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/rocs
	make DESTDIR=$pkgdir install
}

package_kdeedu-step() {
	pkgdesc='Simulate physics experiments'
	depends=('kdebase-runtime' 'gsl' 'libqalculate')
	install='kdeedu.install'
	cd $srcdir/build/step
	make DESTDIR=$pkgdir install
	cd $srcdir/build/doc/step
	make DESTDIR=$pkgdir install
}
