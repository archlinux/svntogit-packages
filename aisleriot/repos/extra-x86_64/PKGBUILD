# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

pkgname=aisleriot
pkgver=3.22.20
pkgrel=1
pkgdesc="A collection of patience games written in guile scheme"
url="https://wiki.gnome.org/Apps/Aisleriot"
arch=(x86_64)
license=(GPL)
depends=(guile gtk3 librsvg libcanberra dconf)
makedepends=(git meson yelp-tools appstream-glib)
optdepends=('pysolfc: PySol card sets'
            'pysolfc-cardsets: PySol card sets')
_commit=54eb2fe24ad6f78009f008072da8b369a8d512b8  # tags/3.22.20^0
source=("git+https://gitlab.gnome.org/GNOME/aisleriot.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build \
    -D theme_kde=false \
    -D theme_pysol=true \
    -D theme_pysol_path=/usr/share/PySolFC
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set sw=2 et:
