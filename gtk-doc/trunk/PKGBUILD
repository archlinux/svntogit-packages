# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gtk-doc
pkgver=1.23
pkgrel=1
pkgdesc="Documentation tool for public library API"
arch=('any')
license=('GPL' 'FDL')
depends=('docbook-xsl' 'yelp-tools' 'perl' 'source-highlight')
makedepends=('pkgconfig' 'jade' 'itstool' 'dblatex')
optdepends=('jade: SGML support'
            'dblatex: PDF support')
url="http://www.gtk.org/gtk-doc/"
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('0b5c23711166c10ff5c74603db80ac26c2b9a382ce778b0e795db821d50718c4')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  export PYTHON=/usr/bin/python2
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
