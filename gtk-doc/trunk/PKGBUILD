# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gtk-doc
pkgver=1.31
pkgrel=1
pkgdesc="Documentation tool for public library API"
url="https://www.gtk.org/gtk-doc/"
arch=(any)
license=(GPL FDL)
depends=(docbook-xsl docbook-xml glib2-docs python-lxml python-pygments
         python-anytree)
makedepends=(dblatex git yelp-tools meson)
checkdepends=(bc gtk3 python-six python-parameterized)
optdepends=('dblatex: PDF support')
_commit=c268fa535424d193953487c694e2a8f770d3255f  # tags/GTK_DOC_1_31^0
source=("git+https://gitlab.gnome.org/GNOME/gtk-doc.git#commit=$_commit"
        fixperms.diff)
sha256sums=('SKIP'
            '6264dc71d0c21f46c139691bf4ea98dd404549f8e4f93a45e93a3ee47753e2d4')

pkgver() {
  cd $pkgname
  git describe --tags | sed -e 's/GTK_DOC_//' -e 's/_/\./g' -e 's/-/+/g'
}

prepare() {
  cd $pkgname
  patch -Np1 -i ../fixperms.diff
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  # Tests don't work yet
  meson test -C build --print-errorlogs || :
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
