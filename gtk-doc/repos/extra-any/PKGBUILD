# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gtk-doc
pkgver=1.26
pkgrel=1
pkgdesc="Documentation tool for public library API"
url="https://www.gtk.org/gtk-doc/"
arch=(any)
license=(GPL FDL)
depends=(docbook-xsl docbook-xml source-highlight glib2-docs python-six)
makedepends=(dblatex git yelp-tools)
checkdepends=(bc gtk3)
optdepends=('dblatex: PDF support')
_commit=4c7bf464748963b275e0bf656beb6c12d48924df  # tags/GTK_DOC_1_26^0
source=("git+https://git.gnome.org/browse/gtk-doc#commit=$_commit"
        gtkdoc.patch)
sha256sums=('SKIP'
            '0ccf34e1a523b7a9dce3d66eca845847bcd985335c1d61168bdd7cde93b07552')

pkgver() {
  cd $pkgname
  git describe --tags | sed -e 's/GTK_DOC_//' -e 's/_/\./g' -e 's/-/+/g'
}

prepare() {
  cd $pkgname
  patch -Np1 -i ../gtkdoc.patch
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --disable-static
  make
}

check() {
  cd $pkgname
  make check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
