# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=gtk-doc
pkgver=1.19
pkgrel=1
pkgdesc="Documentation tool for public library API"
arch=('any')
license=('GPL' 'FDL')
depends=('docbook-xsl' 'gnome-doc-utils' 'perl' 'source-highlight')
makedepends=('pkgconfig' 'jade' 'itstool' 'dblatex')
optdepends=('jade: SGML support'
            'dblatex: PDF support')
url="http://www.gtk.org/gtk-doc/"
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('27df247fa828433a9390368e2088cc22f954f2ce1f255ddbd12ab6e027b12e68')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  export PYTHON=/usr/bin/python2
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
