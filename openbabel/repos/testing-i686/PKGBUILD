# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>

pkgname=openbabel
pkgver=2.3.2
pkgrel=5
pkgdesc="A library designed to interconvert between many file formats used in molecular modeling and computational chemistry"
arch=('i686' 'x86_64')
url="http://openbabel.org/wiki/Main_Page"
license=('GPL')
depends=('gcc-libs' 'libxml2' 'libsm')
makedepends=('cmake' 'eigen2' 'wxgtk2.8' 'boost')
optdepends=('eigen2: to use bindings'
            'wxgtk2.8: GUI interface')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz" 'gcc-5.patch')
md5sums=('9b0007560d9d838b40ab4ad06daf5610'
         '0821ec915b2ab52d54658dc83f5a93d8')

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
# Fix build with GCC 5 (Fedora)
  patch -p0 -i "$srcdir"/gcc-5.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr -DwxWidgets_CONFIG_EXECUTABLE=/usr/bin/wx-config-2.8
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
