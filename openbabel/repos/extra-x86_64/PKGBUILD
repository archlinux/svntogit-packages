# Maintainer:
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>

pkgbase=openbabel
pkgname=(openbabel python-openbabel)
pkgver=3.0.0
pkgrel=3
pkgdesc="A library designed to interconvert between many file formats used in molecular modeling and computational chemistry"
arch=('x86_64')
url="https://openbabel.org/wiki/Main_Page"
license=('GPL')
makedepends=('cmake' 'eigen' 'wxgtk3' 'boost' 'python' 'swig' 'maeparser' 'rapidjson')
source=("https://github.com/openbabel/openbabel/releases/download/openbabel-${pkgver//./-}/openbabel-$pkgver-source.tar.bz2"
        system-maeparser.patch::"https://github.com/openbabel/openbabel/commit/8500f8a9.patch")
sha256sums=('aad58ef8deaea9f58faeecb333f87bb18e0bdf4854e3a3b188a814a8c4517259'
            '14d2b019dfb3e4d6d1b8a529e5da8c2ec84b1e599b9ae74913f8548542d0ab4b')

prepare() {
  mkdir -p build

  cd $pkgbase-$pkgver
  patch -p1 -i ../system-maeparser.patch # Unbundle maeparser
}

build() {
  cd build
  cmake ../openbabel-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DwxWidgets_CONFIG_EXECUTABLE=/usr/bin/wx-config-gtk3 \
    -DRUN_SWIG=ON \
    -DPYTHON_BINDINGS=ON
  make

  # To split python bindings
  sed -i '/scripts.cmake_install.cmake/d' cmake_install.cmake
}

package_openbabel() {
  depends=('libxml2' 'libsm')
  optdepends=('wxgtk3: GUI interface' 'maeparser: maestro format support')

  cd build
  make DESTDIR="$pkgdir" install
}

package_python-openbabel() {
  depends=('python' 'openbabel')

  cd build/scripts
  make DESTDIR="$pkgdir" install
}
