# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=ksquares
pkgver=17.04.1
pkgrel=1
pkgdesc="A modeled after the well known pen and paper based game of Dots and Boxes"
url="https://kde.org/applications/games/ksquares/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegames')
depends=('libkdegames' 'hicolor-icon-theme') 
makedepends=('extra-cmake-modules' 'python' 'kdoctools')
conflicts=('kdegames-ksquares')
replaces=('kdegames-ksquares')
source=("https://download.kde.org/stable/applications/${pkgver}/src/ksquares-${pkgver}.tar.xz"{,.sig})
sha256sums=('01c99dfb82a45a803f980494f91f5ca415996af221b195261fb3ffd051aac787'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../ksquares-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
