# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegraphics-okular
pkgver=14.12.3
pkgrel=1
pkgdesc='Document Viewer'
arch=('i686' 'x86_64')
url="http://kde.org/applications/graphics/okular/"
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegraphics')
depends=('kdebase-runtime' 'qimageblitz' 'chmlib' 'djvulibre' 'libspectre'
         'libkexiv2' 'poppler-qt4' 'libkscreen')
makedepends=('cmake' 'automoc4' 'ebook-tools')
optdepends=('ebook-tools: mobi and epub support'
            'kdegraphics-mobipocket: mobi support')
install=${pkgname}.install
source=("http://download.kde.org/stable/applications/${pkgver}/src/okular-${pkgver}.tar.xz")
sha1sums=('402238357773d421fb7404c3a09bbaba73722592')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../okular-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_ActiveApp=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
