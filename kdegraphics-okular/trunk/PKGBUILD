# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegraphics-okular
pkgver=4.7.3
pkgrel=1
pkgdesc='Document Viewer'
arch=('i686' 'x86_64')
url="http://kde.org/applications/graphics/okular/"
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegraphics')
depends=('kdebase-runtime' 'qimageblitz' 'poppler-qt' 'chmlib' 'djvulibre'
         'ebook-tools' 'libspectre')
makedepends=('pkgconfig' 'cmake' 'automoc4')
optdepends=('kdegraphics-mobipocket: mobipocket support')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgver}/src/okular-${pkgver}.tar.bz2")
sha1sums=('a14700386f386cc50f7dd6785eeb93dae10a0362')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../okular-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
