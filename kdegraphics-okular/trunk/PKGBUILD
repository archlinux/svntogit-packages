# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegraphics-okular
pkgver=16.04.0
pkgrel=2
pkgdesc='Document Viewer'
arch=('i686' 'x86_64')
url="http://kde.org/applications/graphics/okular/"
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegraphics')
depends=('kdebase-runtime' 'qimageblitz' 'chmlib' 'djvulibre' 'libspectre'
         'libkexiv2_4' 'poppler-qt4' 'libkscreen4')
makedepends=('cmake' 'automoc4' 'ebook-tools' 'kdegraphics-mobipocket')
optdepends=('ebook-tools: mobi and epub support'
            'kdegraphics-mobipocket: mobi support')
source=("http://download.kde.org/stable/applications/${pkgver}/src/okular-${pkgver}.tar.xz")
sha1sums=('edf0cd99f9754d11a6bf50154c62fd3fb5186bc2')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../okular-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_ActiveApp=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
