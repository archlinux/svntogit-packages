# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegraphics-okular
pkgver=4.13.95
pkgrel=1
pkgdesc='Document Viewer'
arch=('i686' 'x86_64')
url="http://kde.org/applications/graphics/okular/"
license=('GPL' 'LGPL' 'FDL')
groups=('kde' 'kdegraphics')
depends=('kdebase-runtime' 'qimageblitz' 'chmlib' 'djvulibre' 'libspectre'
         'libkexiv2' 'poppler-qt4')
makedepends=('cmake' 'automoc4' 'ebook-tools')
optdepends=('ebook-tools: mobi and epub support')
install=${pkgname}.install
source=("http://download.kde.org/unstable/${pkgver}/src/okular-${pkgver}.tar.xz")
sha1sums=('3a5a6221013dd6cc0cbc098d2e8d5b932ab9eb3b')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../okular-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_ActiveApp=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
