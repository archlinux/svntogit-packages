# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributed: eliott <eliott@solarblue.net>, Andre Naumann <anaumann@SPARCed.org>

pkgname=nxserver
pkgver=3.5.0
pkgrel=4
pkgdesc="NoMachine NX is the next-generation X compression and roundtrip suppression scheme."
arch=(i686 x86_64)
url="http://nomachine.com/"
license=('GPL')
depends=("nx-common" 'libxaw' 'libxrender' 'libxp' 'gcc-libs' 'libjpeg>=8' #>=$pkgver
	 'libxpm' 'libpng>=1.4.0' 'libxdamage' 'libxrandr' 'libxcomposite' 'libxtst' 'freetype2'
         'xorg-sessreg')
makedepends=('imake')
source=(
#X11 support programs and libraries
http://64.34.161.181/download/$pkgver/sources/nx-X11-$pkgver-2.tar.gz
http://64.34.161.181/download/$pkgver/sources/nxwin-$pkgver-2.tar.gz
http://64.34.161.181/download/$pkgver/sources/nxauth-$pkgver-1.tar.gz
http://64.34.161.181/download/$pkgver/sources/nxcomp-$pkgver-2.tar.gz # needed to provide NX.h and -LXcomp - part of nx-common
#X11 Agent sources
http://64.34.161.181/download/$pkgver/sources/nxagent-$pkgver-7.tar.gz
http://64.34.161.181/download/$pkgver/sources/nxcompsh-$pkgver-1.tar.gz # needed to get X11 built - part of nx-common
#Compression libs and proxy sources
http://64.34.161.181/download/$pkgver/sources/nxproxy-$pkgver-1.tar.gz
http://64.34.161.181/download/$pkgver/sources/nxcompext-$pkgver-1.tar.gz
http://64.34.161.181/download/$pkgver/sources/nxcompshad-$pkgver-2.tar.gz
# gcc 43 fix
nxcompsh-gcc43.patch)
options=(!libtool) 
md5sums=('12060433a74ac61a1c776d1d6d136117'
         '84c7f1575d9a1506370125ed050514ab'
         'cf38ec1e5a5f6453946cd387c14f2684'
         'ad8c0f133122c6d07732ca69c8759410'
         '0a36c7e6a86c6c741179464b8f79c487'
         '84ade443b79ea079380b754aba9d392e'
         '488bb4d9b8e9f82dc272b4e6e9c57d30'
         'abde2ccc33e31fc695031c2cfb60f3dd'
         '90a762dd9eb19c8c97876ad837923857'
         'b6c279654dac421fc3dd1a27d66ff53c')

build() {
  cd ${srcdir}

  # nxcomp   
  cd ${srcdir}/nxcomp
  ./configure --prefix=/opt/NX
  make
  # nxcompshad
  cd ${srcdir}/nxcompshad
  ./configure --prefix=/opt/NX
  make
  # nxcompsh
  cd ${srcdir}/nxcompsh
  patch -Np1 -i ${srcdir}/nxcompsh-gcc43.patch
  ./configure --prefix=/opt/NX
  make
  # nxproxy
  cd ${srcdir}/nxproxy
  ./configure --prefix=/opt/NX
  make
  # nx-X11
  cd ${srcdir}/nx-X11
  make World
  # nxcompext
  cd ${srcdir}/nxcompext 
  ./configure --prefix=/opt/NX
  make
}

package() {
  mkdir -p ${pkgdir}/opt/NX/bin
  mkdir -p ${pkgdir}/opt/NX/lib

  # nxcompshad
  cd ${srcdir}/nxcompshad
  cp -a libXcompshad.so* ${pkgdir}/opt/NX/lib
  # nxproxy
  cd ${srcdir}/nxproxy
  make prefix=${pkgdir}/opt/NX install
  # nx-X11
  cd ${srcdir}/nx-X11
  cp -a lib/X11/libX11.so* ${pkgdir}/opt/NX/lib
  cp -a lib/Xext/libXext.so* ${pkgdir}/opt/NX/lib
  cp -a lib/Xrender/libXrender.so* ${pkgdir}/opt/NX/lib
  install -D -m755 programs/Xserver/nxagent ${pkgdir}/opt/NX/bin/nxagent
  install -D -m755 programs/nxauth/nxauth ${pkgdir}/opt/NX/bin/nxauth
  # nxcompext
  cd ${srcdir}/nxcompext 
  cp -a libXcompext.so* ${pkgdir}/opt/NX/lib
}