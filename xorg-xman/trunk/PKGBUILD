# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xorg-xman
pkgver=1.1.4
pkgrel=2
pkgdesc="Unix manual page viewer"
arch=(x86_64)
url="https://xorg.freedesktop.org/"
license=('custom')
depends=('libxaw' 'libxt')
makedepends=('xorg-util-macros' 'xproto' 'libxmu')
source=(https://xorg.freedesktop.org/archive/individual/app/xman-${pkgver}.tar.bz2{,.sig})
sha512sums=('785e973eb15b0300de08aace24f3604dfee61421c7f64ea4cde2e280480bf5d3ad797ab8208668dc023a375494f8db7534fbcaa1c1ef16e79464fdd017b5da68'
            'SKIP')
validpgpkeys=('4A193C06D35E7C670FA4EF0BA2FB9E081F2D130E') # Alan Coopersmith <alan.coopersmith@oracle.com>

build() {
  cd xman-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd xman-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
