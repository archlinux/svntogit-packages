# $Id$
# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: kusakata <shohei atmark kusakata period com>

pkgname=libixion
pkgver=0.9.1
pkgrel=2
pkgdesc="A general purpose formula parser & interpreter"
arch=('i686' 'x86_64')
url="https://gitlab.com/ixion/ixion/blob/master/README.md"
license=('custom')
depends=('boost-libs' 'python2')
makedepends=('boost' 'mdds')
source=("http://kohei.us/files/ixion/src/libixion-${pkgver}.tar.xz"
        'mdds-1.0.patch')
sha1sums=('8ae071e7e89784933caadeffc16ed7b0764350a9'
          '06bb9d2eb331dbc2d1f06acdb9b006eb366553ec')

prepare() {
	cd ${pkgname}-${pkgver}

	patch -Np1 -i ../mdds-1.0.patch

	sed -i "s/python >= 0.27.1/python2 >= 0.27.1/" configure.ac
	autoreconf -vfi
	sed -i "s/env python/env python2/" test/ixion-python-test.py
}

build() {
	cd ${pkgname}-${pkgver}

	# mdds 1.0.0 requires C++11
	CXXFLAGS+=' -std=c++11'

	./configure --prefix=/usr \
	  --disable-static
	make
}

check() {
    cd ${pkgname}-${pkgver}
    make -k check
}

package() {
	cd ${pkgname}-${pkgver}
	make DESTDIR="${pkgdir}" install
	
	install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

