# Maintainer: AndyRTR <andyrtr@archlinux.org>
# Contributor: kusakata <shohei atmark kusakata period com>

pkgname=libixion
pkgver=0.18.1
pkgrel=1
pkgdesc="A general purpose formula parser & interpreter"
arch=('x86_64')
url="https://gitlab.com/ixion/ixion/blob/master/README.md"
license=('custom')
depends=('boost-libs' 'python')
makedepends=('boost' 'mdds' 'spdlog')
source=("https://kohei.us/files/ixion/src/libixion-${pkgver}.tar.xz")
# https://gitlab.com/ixion/ixion/-/releases
sha256sums=('ec2327854601d41bd38f151bc94b47ef43f0b8258a551e41e9f5e19b9ad729c1')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr \
    --disable-static
#  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool # doesn't fix it
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make -k check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  # LICENSE file is missing in tarball
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
