# $Id: $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgbase=phonon
pkgname=('phonon' 'phonon-xine' 'phonon-gstreamer')
pkgver=4.4.0
pkgrel=1
arch=('i686' 'x86_64')
url="http://phonon.kde.org"
license=('LGPL')
makedepends=('cmake' 'automoc4' 'qt' 'xine-lib' 'gstreamer0.10-base-plugins' 'mesa')
source=("http://download.kde.org/stable/${pkgname}/${pkgver}/${pkgbase}-${pkgver}.tgz")
md5sums=('80544b876cf0e0af05f2303b3f534351')

build() {
  cd ${srcdir}
  mkdir build
  cd build
  cmake ../${pkgbase}-4.4 \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_SKIP_RPATH=ON \
    -DWITH_PulseAudio=OFF
  make || return 1
}

package_phonon(){
  pkgdesc="The multimedia API for KDE4"
  depends=('qt' 'phonon-backend')
  cd ${srcdir}/build/phonon
  make DESTDIR=${pkgdir} install || return 1
  cd ${srcdir}/build/includes
  make DESTDIR=${pkgdir} install || return 1
}

package_phonon-gstreamer(){
  pkgdesc="Phonon Gstreamer backend"
  depends=('qt' 'gstreamer0.10-base-plugins')
  provides=('phonon-backend')
  cd ${srcdir}/build/gstreamer
  make DESTDIR=${pkgdir} install || return 1
}

package_phonon-xine(){
  pkgdesc="Phonon Xine backend"
  depends=('qt' 'xine-lib')
  provides=('phonon-backend')
  cd ${srcdir}/build/xine
  make DESTDIR=${pkgdir} install || return 1
}
