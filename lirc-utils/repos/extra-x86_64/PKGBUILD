# $Id$
# Maintainer:
# Contributor: Paul Mattal <paul@archlinux.org>

pkgname=lirc-utils
pkgver=0.9.0.pre1
pkgrel=2
pkgdesc="Linux Infrared Remote Control utils"
arch=('i686' 'x86_64')
url="http://www.lirc.org/"
license=('GPL')
_kernver=2.6.36-ARCH
depends=('alsa-lib' 'libx11' 'libsm' 'python2' 'libftdi')
makedepends=('help2man' 'kernel26-headers')
replaces=('lirc+pctv')
backup=('etc/conf.d/lircd.conf' 'etc/conf.d/irexec.conf')
options=('!libtool' '!makeflags')
source=(http://www.lirc.org/software/snapshots/lirc-0.9.0-pre1.tar.bz2
        #"http://downloads.sourceforge.net/lirc/lirc-${pkgver}.tar.bz2"
	lircd lircmd lirc.logrotate lircd.conf irexec.conf irexecd)

build() {
	cd "${srcdir}/lirc-0.9.0-pre1"

	autoreconf
	libtoolize

	./configure --enable-sandboxed \
	  --prefix=/usr \
	  --with-driver=all \
	  --with-kerneldir=/usr/src/linux-${_kernver} \
	  --with-moduledir=/lib/modules/${_kernver}/kernel/drivers/misc \
	  --with-transmitter
	# disable parallel and bt829
        # because of incompatibility with smp systems
        sed -i -e "s:lirc_parallel::" -e "s:lirc_bt829::" \
		Makefile drivers/Makefile drivers/*/Makefile tools/Makefile
	# lirc_i2c  lirc_igorplugusb  lirc_imon  lirc_it87  lirc_ite8709
        # lirc_sasem  lirc_serial  lirc_sir lirc_ttusbir lirc_zilog
        # because part of kernel 2.6.36 staging tree
        sed -i -e "s:lirc_i2c::" -e "s:lirc_igorplugusb::" -e "s:lirc_imon::" \
		-e "s:lirc_it87::" -e "s:lirc_ite8709::" -e "s:lirc_sasem::" \
		-e "s:lirc_serial::" -e "s:lirc_sir::" -e "s:lirc_ttusbir::" \
		-e "s:lirc_zilog::" Makefile drivers/Makefile drivers/*/Makefile tools/Makefile
	# Disabling lirc_gpio driver as it does no longer work Kernel 2.6.22+
	sed -i -e "s:lirc_gpio\.o::" drivers/lirc_gpio/Makefile.am

	make
}

package() {
	cd "${srcdir}/lirc-0.9.0-pre1"
	make DESTDIR="${pkgdir}" install
	install -d "${pkgdir}/usr/share/lirc" "${pkgdir}/etc/rc.d"
	cp "${srcdir}"/{lircd,lircmd,irexecd} "${pkgdir}/etc/rc.d"
	cp -rp remotes "${pkgdir}/usr/share/lirc"
	chmod -R go-w "${pkgdir}/usr/share/lirc/"

	# install the logrotate config
    	install -Dm644 "${srcdir}/lirc.logrotate" "${pkgdir}/etc/logrotate.d/lirc"
    
	# install conf.d file
	install -Dm644 "${srcdir}/lircd.conf" "${pkgdir}/etc/conf.d/lircd.conf"

	# install conf.d file
        install -Dm644 "${srcdir}/irexec.conf" "${pkgdir}/etc/conf.d/irexec.conf"

	install -d "${pkgdir}/etc/lirc"

	# remove built modules
	rm -r "${pkgdir}/lib/"
}
md5sums=('13ba59178adee4e6be8a9a1966ab3133'
         '8d0e238dc0eda95e340fe570605da492'
         '85f7fdac55e5256967241864049bf5e9'
         '3deb02604b37811d41816e9b4385fcc3'
         '5b1f8c9cd788a39a6283f93302ce5c6e'
         'f0c0ac930326168035f0c8e24357ae55'
         '69d099e6deedfa3c1ee2b6e82d9b8bfb')
