# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdemultimedia-audiocd-kio
pkgver=14.12.3
pkgrel=1
pkgdesc="Kioslave for accessing audio CDs"
url='https://projects.kde.org/projects/kde/kdemultimedia/audiocd-kio'
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
depends=('libkcddb' 'libkcompactdisc' 'lame' 'cdparanoia')
makedepends=('cmake' 'automoc4')
groups=('kde' 'kdemultimedia')
replaces=('kdemultimedia-kioslave')
conflicts=('kdemultimedia-kioslave')
source=("http://download.kde.org/stable/applications/${pkgver}/src/audiocd-kio-${pkgver}.tar.xz")
sha1sums=('88bb06679ec83873e55557c1f15c460ade132ee7')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake ../audiocd-kio-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
