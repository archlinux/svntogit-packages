# $Id: PKGBUILD 261475 2016-03-15 10:26:40Z arojas $
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=minuet
pkgver=16.08.0
pkgrel=1
pkgdesc='A KDE Software for Music Education'
url='https://minuet.kde.org'
arch=(i686 x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kdeedu)
depends=(plasma-framework fluidsynth timidity++ qt5-quickcontrols2 hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools python)
source=("http://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz")
sha1sums=('1f9785a234ea20c32eb081a8f4ca4db9598be5ae')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
