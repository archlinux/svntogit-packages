# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=minuet
pkgver=19.04.0
pkgrel=2
pkgdesc='A KDE Software for Music Education'
url='https://minuet.kde.org'
arch=(x86_64)
license=(GPL LGPL FDL)
groups=(kde-applications kdeedu)
depends=(plasma-framework fluidsynth hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools)
source=("https://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig}
        minuet-quick-compiler.patch::"https://github.com/KDE/minuet/commit/9bf2f1ef.patch")
sha256sums=('3bdf5470a154b2be4bfcd97db7374ef81ca8eefba3bb12030dd6d39f1466cc78'
            'SKIP'
            'af62390e1a656a77a3886e22e423e1da4bddbaf366c642a57d0ccc52695b612d')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87) # Christoph Feck <cfeck@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../minuet-quick-compiler.patch # Don't compile QML
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
