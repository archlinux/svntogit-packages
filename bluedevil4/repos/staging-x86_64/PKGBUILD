# $Id$
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=bluedevil4
_pkgname=bluedevil
pkgver=2.1.1
pkgrel=2
pkgdesc='KDE bluetooth framework'
arch=('i686' 'x86_64')
url="https://projects.kde.org/projects/extragear/base/bluedevil"
license=('GPL')
depends=('kdebase-runtime' 'libbluedevil')
makedepends=('cmake' 'automoc4')
conflicts=('bluedevil')
replaces=('bluedevil<1:5')
install="${pkgname}.install"
source=("http://download.kde.org/stable/${_pkgname}/${pkgver}/src/${_pkgname}-${pkgver}.tar.xz")
md5sums=('9a6b011331678ea08f303049bcdbb69f')

prepare() {
  mkdir build
}

build(){
  cd build
  cmake ../${_pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
