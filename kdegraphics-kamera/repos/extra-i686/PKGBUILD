# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kdegraphics-kamera
pkgver=15.08.3
pkgrel=1
pkgdesc="Configure Kamera"
url="http://kde.org/applications/graphics/kamera/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'FDL')
groups=('kde-applications' 'kdegraphics')
depends=('kdebase-runtime' 'libgphoto2')
makedepends=('cmake' 'automoc4')
source=("http://download.kde.org/stable/applications/${pkgver}/src/kamera-${pkgver}.tar.xz")
sha1sums=('571842c3028639ea8ad62140d479c49dc82c1165')

build() {
  cd "${srcdir}"
  mkdir -p build
  cd build
  cmake ../kamera-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DKDE4_BUILD_TESTS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
