# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=compsize
pkgver=1.3
pkgrel=1
pkgdesc="Calculate compression ratio of a set of files on Btrfs"
arch=(x86_64)
url="https://github.com/kilobyte/compsize"
license=(GPL)
depends=(glibc)
makedepends=(btrfs-progs git)
_commit=a7bf82296193f785757f0cb30627dce30462f085  # tags/v1.3^0
source=("git+$url#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

build() {
  cd $pkgname
  make
}

package() {
  cd $pkgname
  install -d "$pkgdir/usr/share/man/man8"
  make install PREFIX="$pkgdir"
}
